*          DATA SET MPFQVEC    AT LEVEL 003 AS OF 05/01/02                      
*CATALP MPFQVEC                                                                 
         TITLE 'MPFQVEC  - CREATE DATA VECTORS FOR FREQ REPORT'                 
         PRINT NOGEN                                                            
MPFQVEC  CSECT                                                                  
         SPACE 2                                                                
**********************************************************************          
*                                                                    *          
* MPFQVEC :- CREATES FIXED INFORMATION PART OF THE VECTOR,           *          
*            READING THE SCHEDULE RECORD TO OBTAIN THE TOTAL         *          
*            INSERTION COUNT FOR THE SCHEDULE I.E. MAX EXPOSURES     *          
*            RETURNS THE NUMBER OF INSERTIONS AS PARAM 1             *          
*                                                                    *          
*        PARAM 1 - DATA VECTOR TABLE ADDRESS                         *          
*        PARAM 2 - SCHEDULE SCREEN HEADER ADDRESS                    *          
*        PARAM 3 - AGENCY CODE ADDRESS                               *          
*        PARAM 4 - ADDRESS OF LVLGRP - LEVEL GROUPING TABLE          *          
*        PARAM 5 - LENGTH OF ONE DATA VECTOR                         *          
*                                                                    *          
**********************************************************************          
         SPACE 3                                                                
         NMOD1 WORKX-WORKD,MPFQVEC                                              
         SPACE 3                                                                
         LR    R7,RC                                                            
         USING WORKD,R7                                                         
         SH    RC,=H'4'            RESTORE REGISTER C ...                       
         L     RC,0(,RC)           ... THE GEND REG                             
         USING GEND,RC                                                          
         EJECT                                                                  
         LR    R8,R1               SAVE PARMS ADDRESS                           
         L     R3,0(R1)            VECTOR TABLE ADDRESS                         
         SH    R3,=H'4'            END ADDR OF VECTOR TAB STORED HERE           
         L     R3,0(R3)            GET END ADDRESS                              
         S     R3,16(R1)           LEAVE ROOM FOR A TOTALS VECTOR               
         STCM  R3,15,VTABEND       SAVE TO PREVENT TABLE OVERFLOW               
         L     R2,4(,R1)           SCHEDULE SCREEN HEADER                       
*                                                                               
*                                  GET SCHEDULE RECORD                          
         GOTO1 ANY                                                              
         XC    WKEY,WKEY                                                        
         MVC   WKEY(4),SKEYCON     SET UP SCHEDULE KEY STRUCTURE                
         L     R1,8(,R8)           GET TWAAGY ADDRESS                           
         MVC   WKEY+1(2),0(R1)     MOVE IN TWAAGY                               
         MVC   WKEY+4(8),WORK                                                   
         MVC   COMMAND,=C'DMREAD'                                               
         MVC   DMFILE,SYSDIR                                                    
         L     R6,AIO3                                                          
         GOTO1 DATAMGR,DMCB,(DMINBTS,COMMAND),DMFILE,WKEY,(R6),0                
         TM    DMCB+8,X'10'        CHECK SCHEDULE FOUND                         
         BZ    *+6                                                              
         DC    H'0'                DIE - CAN'T FIND SCHEDULE                    
         MVC   COMMAND,=C'GETREC'                                               
         MVC   DMFILE,SYSFIL       * READ ...FIL *                              
         LA    R9,36(R6)                                                        
         GOTO1 DATAMGR,DMCB,(DMINBTS,COMMAND),DMFILE,(R9),(R6),DMWK2            
         TM    DMCB+8,X'10'        CHECK ...FIL RECORD FOUND                    
         BZ    *+6                                                              
         DC    H'0'                DIE - ...FIL REC NOT FOUND                   
         MVI   ELCODE,X'20'        ELEMENT CODE FOR VEHICLES                    
         BAS   RE,GETEL            POINT TO VEHICLE ELEMENTS                    
         USING SCDVDEL,R6                                                       
         MVI   ERROR,NOVEH         ERROR CODE, IN CASE                          
         BNE   TRAPERR             NO VEHICLES IN SCHEDULE                      
         SR    R3,R3               INITIALISE INSERTION COUNT                   
DF010    DS    0H                                                               
         SR    R1,R1                                                            
         ICM   R1,3,SCDVDINS       INSERTIONS FOR THIS VEHICLE                  
         AR    R3,R1               ACCUMULATE                                   
         BAS   RE,NEXTEL                                                        
         BE    DF010               MORE VEHICLE ELEMETS SO ADD TO TOTAL         
*                                                                               
*        NOW GENERATE DATA VECTORS GROUPED AS DEFINED BY THE USER               
*                                                                               
         L     R2,0(,R8)           ADDRESS OF VECTOR TABLE                      
         USING VTABD,R2                                                         
         L     R5,12(,R8)          ADDRESS OF LEVEL GROUPING FIELD              
         USING LVLGRPD,R5                                                       
         L     R4,16(,R8)          LENGTH OF A DATA VECTOR                      
         SR    R6,R6               NEXT EXPOSURE COUNT TO DEFINE                
         SR    R9,R9               COUNTER FOR DATA VECTOR NUMBERS              
         MVI   EOTFLG,0            INIT ENED OF LVLGRP TABLE                    
*                                                                               
DF020    DS    0H                                                               
         LR    R1,R4                                                            
         BCTR  R1,0               GET EXECUTABLE LEN OF VECTOR                  
         EX    R1,*+8                                                           
         B     *+10                                                             
         XC    VTLEN(0),VTLEN     CLEAR ENTIRE VECTOR                           
         STH   R4,VTLEN           DEFINE ITS LENGTH                             
         MVI   VTTYPE,C'D'        'D' - DATA                                    
         LA    R9,1(,R9)          NEXT VECTOR NUMBER                            
         STH   R9,VTENTNO         ENTRY NUMBER IN VECTOR LIST                   
         CLI   EOTFLG,X'FF'       REACHED END OF USER DEFINED GROUPS ?          
         BE    DF025              YES - DEFINE SINGE ENTRIES FOR REST           
         ZIC   R1,LVLFROM         DEFINE NEXT USER SPECIFIED 'FROM' LVL         
         CR    R6,R1              HAVE WE DEFINED ALL LOWER LEVELS ?            
         BNL   DF030              YES -INSERT THIS USER DEFINED RANGE           
DF025    DS    0H                                                               
         STC   R6,VTFQLVLF        ELSE DEFINE VECTOR FOR N EXPOSURES            
         STC   R6,VTFQLVLT        N THRO M  ARE SAME                            
         B     DF040                                                            
DF030    DS    0H                 REACHED A USER DEFINED RANGE                  
         MVC   VTFQLVLF,LVLFROM   START OF RANGE                                
         MVC   VTFQLVLT,LVLTO     END OF RANGE                                  
         LA    R5,LVLLEN(,R5)     POINT TO NEXT PAIR OF LEVEL VALUES            
         CLI   LVLFROM,0          REACHED END OF TABLE ?                        
         BNE   DF040                                                            
         MVI   EOTFLG,X'FF'       FLAG AS END OF LEVEL GROUP TABLE              
DF040    DS    0H                                                               
         ZIC   R6,VTFQLVLT        GET HIGHEST EXPOSURE COUNT SO FAR             
         LA    R6,1(,R6)          INCR TO NEXT EXPOSURE COUNT                   
         LA    R2,0(R4,R2)        POINT TO NEXT DATA VECTOR SLOT                
         LA    R1,0(R4,R2)        POINT TO END OF NEXT VECTOR                   
         C     R1,VTABEND         CHECK NOT BEYOND END OF AREA                  
         BNH   DF050                                                            
         MVI   ERROR,CANTDO       VECTOR TABLE TOO SMALL                        
         L     R2,4(R8)           A(SCHEDULE SCREEN HEADER)                     
         CLI   OFFLINE,C'Y'                                                     
         BNE   TRAPERR            ONLINE - INFORM USER                          
         DC    H'0'               OFFLINE - DIE - VECTOR TAB TOO SMALL          
*                                                                               
DF050    XC    VTLEN,VTLEN        AND INIT AS EOT                               
         CLI   EOTFLG,0           REACHED END OF LVLGRP TABLE ???               
         BE    DF020              NO - PROCESS THIS LEVEL GRP                   
         CR    R6,R3              DONE ALL EXPOSURES DEFINED BY SCHED ?         
         BNH   DF020              NO - GENERATE SINGLE ENTRIES FOR REST         
*                                 OF EXPOSURE COUNTS DEFINED BY SCHED           
         ST    R3,0(,R8)          RETURN TOTAL EXPS IN SCHEDULE                 
         B     XIT                                                              
         EJECT                                                                  
*                                                                               
***********************************************************************         
XIT      DS    0H                                                               
         XMOD1 1                                                                
         SPACE 2                                                                
TRAPERR  DS    0H                                                               
         GOTO1 ERREX                                                            
         SPACE 2                                                                
         GETEL R6,DATADISP,ELCODE                                               
         SPACE 2                                                                
SKEYCON  DC    C'S',X'0000',C'P'                                                
         EJECT                                                                  
         LTORG                                                                  
         SPACE 3                                                                
       ++INCLUDE DDSPLWORKD                                                     
         SPACE 2                                                                
       ++INCLUDE MPVTABD                                                        
         SPACE 2                                                                
       ++INCLUDE MPSVHFIL                                                       
         SPACE 2                                                                
       ++INCLUDE MPEQUATES                                                      
         SPACE 2                                                                
WORKD    DSECT                                                                  
DMWK2    DS    12D                                                              
WKEY     DS    CL48                                                             
VTABEND  DS    AL4                 ADDR OF END OF VECTOR TABLE                  
EOTFLG   DS    X                                                                
WORKX    DS    0C                                                               
         SPACE 2                                                                
LVLGRPD  DSECT                                                                  
LVLFROM  DS    X                                                                
LVLTO    DS    X                                                                
LVLLEN   EQU   *-LVLGRPD                                                        
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003MPFQVEC   05/01/02'                                      
         END                                                                    
