*          DATA SET DDRECUP2   AT LEVEL 006 AS OF 07/23/96                      
*CATALP RECUP2                                                                  
         TITLE '*** ELEMENT DELETION/INSERTION RTN ***'                         
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
*                                                                     *         
*              WORD 1 BYTE  0   - SYSTEM CODE - SEE SYSTAB            *         
*                                    (X'FE' OR X'FD'  - SEE WORD 4)             
*                     BYTES 1-3 - A(RECORD)                           *         
*                                                                               
*              WORD 2 BYTES 1-3 - A(ELEMENT)                          *         
*                                                                               
*              WORD 3 BYTE  0   - C'R' = RETURN ERROR ON REC OVERFLOW *         
*                                        INSTEAD OF BLOWING UP        *         
*                                        (RECUP WILL LEAVE AS 'R' IF  *         
*                                        OK, ELSE WILL SET TO X'00')  *         
*                     BYTES 1-3 - A(INSERTION)                        *         
*                                                                               
*              WORD 4 BYTES 1-3 - IF WORD 1 BYTE 0 = X'FE' OR X'FD'   *         
*                                 A(SPECIAL SYSTEM DEFINITION)        *         
*                                 (DATE BYTES OF A SYSTAB ENTRY)                
*                                                                               
*                                 X'FD' MEANS 3-BYTE RECORD LENGTH              
*                                 FIELD IN TABLE AND IN RECORD - USED           
*                                 IN BUFFER MODE WITH HUGE BUFFERS              
*                                                                     *         
*        FUNCTION - IF ELEMENT ADDRESS IS OUTSIDE RECORD LIMITS,      *         
*                   THE ELEMENT IS INSERTED. IF THE ELEMENT ADDRESS   *         
*                   IS WITHIN LIMITS OF THE RECORD, THE ELEMENT       *         
*                   IS DELETED, RECORD LENGTH IS UPDATED TO           *         
*                   REFLECT THE APPROPRIATE CHANGE                    *         
*                                                                     *         
*      ** CHANGES MADE 5/23/91 TO ALLOW FOR FULL 16 BIT RECORD LENGTH *         
*      ** ARITHMETIC - GRANT                                          *         
*                                                                     *         
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
*                                                                               
         PRINT NOGEN                                                            
RECUP    CSECT                                                                  
         NMOD1 RECUPWKL,RECUP                                                   
         LM    R2,R4,0(R1)         LOAD PARAMS                                  
         LA    R2,0(R2)            CLEAR HOB                                    
         LA    R4,0(R4)            CLEAR HOB                                    
         USING RECUPWK,RC                                                       
         SPACE 1                                                                
         BAL   RE,SETSYS                                                        
         SPACE 2                                                                
CHECKEL  DS    0H                                                               
         XC    RLEN,RLEN                                                        
         MVC   RLEN+2(2),0(RA)     RA POINTS TO REC LENGTH FIELD                
         CLI   LMODE,2                                                          
         BE    *+10                                                             
         MVC   RLEN+1(3),0(RA)     3-BYTE MODE                                  
         L     R7,RLEN                                                          
         LA    R7,0(R7,R2)         POINT TO NEXT BYTE AT EOR                    
         LR    RE,R9                                                            
         SR    R0,R0                                                            
         BCTR  R7,0                                                             
CHECKEL1 CR    RE,R7               CALCULATE RECORD LENGTH                      
         BNL   *+14                                                             
         IC    R0,1(RE)                                                         
         AR    RE,R0                                                            
         B     CHECKEL1                                                         
         LR    R7,RE                                                            
         SR    RE,R2                                                            
         ST    RE,RLEN                                                          
         SR    R5,R5                                                            
*                                                                               
         LR    R8,R9                                                            
         CR    R7,R3                                                            
         BNH   CHECKEL3                                                         
         CR    R2,R3                                                            
         BH    CHECKEL3                                                         
*                                  ELEMENT WITHIN RECORD                        
CHECKEL2 CR    R8,R3                                                            
         BE    RECDEL              DELETE                                       
         BL    *+6                                                              
         DC    H'0'                NOT AN ELEMENT ADDR                          
         IC    R5,1(R8)                                                         
         AR    R8,R5                                                            
         B     CHECKEL2                                                         
*                                  ELEMENT OUTSIDE OF RECORD                    
CHECKEL3 CR    R7,R4               TEST LAST BYTE VS ELEM ADDR                  
         BNL   CHKEL3B                                                          
*                                  NB- HELLO SETS RECORD LENGTH                 
*                                      WITH EXTRA BYTE                          
         BCTR  R4,R0               THIS ADJUSTMENT ALLOWS FOR TRYING            
         CR    R7,R4               TO ADD AN ELEM THERE                         
         BNL   *+6                                                              
         DC    H'0'                INSERTION ADDRESS OUTSIDE RECORD             
CHKEL3B  CR    R2,R4                                                            
         BL    *+6                                                              
         DC    H'0'                INSERTION ADDRESS OUTSIDE RECORD             
CHECKEL4 CR    R8,R4                                                            
         BE    RECIN                                                            
         BL    *+6                                                              
         DC    H'0'                INSERTION ADDR NOT AT ELEMENT                
         IC    R5,1(R8)                                                         
         AR    R8,R5                                                            
         B     CHECKEL4                                                         
* ELEMENT TO BE DELETED                                                         
RECDEL   SR    R4,R4                                                            
         IC    R4,1(R3)            GET LENGTH OF ELEMENT TO BE DELETED          
         MVI   0(R3),0             CLEAR EOR                                    
         BCTR  R7,R0                                                            
         LR    R5,R7               SAVE FOR BXLE LIMIT                          
         L     R7,RLEN                                                          
         SR    R7,R4                                                            
         ST    R7,RLEN                                                          
         MVC   0(2,RA),RLEN+2      UPDATE RECORD LENGTH                         
         CLI   LMODE,2                                                          
         BE    *+10                                                             
         MVC   0(3,RA),RLEN+1      3-BYTE MODE                                  
         LA    R7,0(R4,R3)         POINT TO NXT ELEMENT IN RECORD               
         CLI   0(R7),0             TEST E-O-R                                   
         BE    RECUPX                                                           
RECDEL01 IC    R4,1(R7)            LENGTH OF NXT ELEM                           
         LTR   R4,R4                                                            
         BC    13,RECUPX                                                        
         LR    R6,R4                                                            
         BCTR  R6,R0               SET EX LENGTH                                
         EX    R6,MVCLFT           MVC  0(0,R3),0(R7)                           
         AR    R3,R4               BUMP PNTR TO END OF ELEMENT MOVED            
         BXLE  R7,R4,RECDEL01                                                   
         MVI   0(R3),0             CLEAR EOR                                    
         B     RECUPX              EXIT TO CALLER                               
*                                                                               
MVCLFT   MVC   0(0,R3),0(R7)                                                    
*                                                                               
* INSERT NEW ELEMENT                                                            
*                                                                               
RECIN    SR    R6,R6                                                            
         IC    R6,1(R3)            LNGTH OF NEW ELEM                            
         LTR   R6,R6               BLOW IF USE HAS ZERO LENGTH EL               
         BNZ   *+6                                                              
         DC    H'0'                                                             
         L     RF,RLEN                                                          
         AR    R6,RF                                                            
         C     R6,MAXLEN                                                        
         BNH   RECINB                                                           
*                                  RECORD WILL BE TOO BIG                       
         CLI   8(R1),C'R'          DOES CALLER WANT TO KNOW                     
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVI   8(R1),0             YES, TELL HIM                                
         B     RECUPX                                                           
*                                                                               
RECINB   ST    R6,RLEN                                                          
* PUT ZERO AT END OF NEW REC                                                    
         AR    R6,R2                                                            
         MVI   0(R6),0                                                          
*                                                                               
         MVC   0(2,RA),RLEN+2      UPDATE RECORD LENGTH                         
         CLI   LMODE,2                                                          
         BE    *+10                                                             
         MVC   0(3,RA),RLEN+1      3-BYTE MODE                                  
         LR    R5,R7               POINT TO NEXT BYTE AT EOR                    
         LR    R1,R5                                                            
         SR    R1,R4               NUMBER OF BYTES TO BE SHFTD                  
         SR    R0,R0                                                            
         SR    R6,R6                                                            
         IC    R6,1(R3)            NEW ELEM LGTH                                
         LR    R8,R6               ELEM LGTH                                    
         BCTR  R8,R0               EX LGTH                                      
         LR    R9,R8               SAVE                                         
         DR    R0,R6               BCT CNT IN R1/REMNDR IN R0                   
         LTR   R1,R1               TEST IF ANY CNT                              
         BC    8,RECIN02           NO-MOVE ONLY REMNG BYTES                     
*                                                                               
         LR    R2,R5                                                            
RECIN01  EQU   *                                                                
         SR    R2,R6               BUMP LFT THRU RECD                           
         EX    R8,MVCRHT           MOVING DATA RGHT FOR A DISTANCE              
         SR    R5,R6               EQUAL TO THE LGTH OF THE NEW ELEM            
         BCT   R1,RECIN01                                                       
*                                                                               
RECIN02  EQU   *                                                                
         LTR   R0,R0               TEST IF ANY REMNG BYTES                      
         BC    8,RECIN04           NO-INSERT NEW ELEM                           
         LR    R8,R0                                                            
         BCTR  R8,R0               SET EXECUTE FOR REMNG BYTES                  
         AR    R5,R6                                                            
         SR    R5,R0               SET DESTINATION ADDRESS                      
         LR    R2,R4               INSERTION ADDRESS                            
         EX    R8,MVCRHT                                                        
         LR    R8,R9               RESTORE NEW ELEM EX LGTH                     
         B     RECIN04                                                          
*                                                                               
MVCRHT   MVC   0(0,R5),0(R2)       SHIFT CURRENT DATA RIGHT                     
RECIN04  LR    R5,R4               INSERT ADDRESS                               
         LR    R2,R3               ELEMENT ADDRESS                              
         EX    R8,MVCRHT           INSERT NEW ELEM                              
*                                                                               
RECUPX   XIT                       RETURN                                       
         SPACE 3                                                                
SETSYS   DS    0H                  RETURN SYSTAB VALUES                         
         MVI   LMODE,2             2-BYTE LENGTH MODE                           
         CLI   0(R1),X'FD'         CHECK FOR FE OR FD MODE                      
         BL    SS1                                                              
         BH    *+8                                                              
         MVI   LMODE,3             3-BYTE LENGTH MODE                           
         L     RF,12(R1)           A(SPECIAL SYS DEFINITION)                    
         BCTR  RF,R0               BACK UP ONE                                  
         B     SS8                                                              
*                                                                               
SS1      DS    0H                                                               
         LA    RF,SYSTAB                                                        
         LA    R0,SYSTABN                                                       
*                                                                               
SS2      DS    0H                                                               
         CLC   0(1,R1),0(RF)       TEST VS GIVEN SYSTEM                         
         BE    SS8                                                              
         LA    RF,SYSTABL(RF)                                                   
         BCT   R0,SS2                                                           
         DC    H'0'                                                             
*                                                                               
SS8      DS    0H                                                               
         SR    R9,R9                                                            
         ICM   R9,3,1(RF)          ELEM START                                   
         AR    R9,R2                                                            
         SR    RA,RA                                                            
         ICM   RA,3,3(RF)          LENGTH POSITION                              
         AR    RA,R2                                                            
         SR    R0,R0                                                            
         ICM   R0,3,5(RF)          MAX LENGTH (2-BYTE)                          
         CLM   R0,3,=X'FFFF'       NO MAX                                       
         BNE   *+8                                                              
         L     R0,=X'7FFFFFFF'                                                  
         CLI   LMODE,2                                                          
         BE    *+8                                                              
         ICM   R0,7,5(RF)          MAX LENGTH (3-BYTE)                          
         ST    R0,MAXLEN                                                        
*                                                                               
         CLI   0(R1),1             TEST PRINTPAK                                
         BE    SS12                                                             
         CLI   0(R1),C'P'                                                       
         BNE   SS20                                                             
*                                                                               
SS12     DS    0H                  SPECIALS FOR PRINT                           
         CLI   1(R2),X'99'         PUB RECS                                     
         BH    *+14                                                             
         MVC   MAXLEN,=F'3976'     ARE BIG                                      
         B     SS20                                                             
         CLI   3(R2),X'20'         INSERTION RECS NOW 3000 5/18/87              
         BNE   *+14                                                             
         MVC   MAXLEN,=F'2976'                                                  
         B     SS20                                                             
         CLI   3(R2),X'10'         SO ARE CONTRACTS                             
         BE    SS12B                                                            
         CLI   3(R2),X'09'         AND BUCKET RECS                              
         BNE   SS20                                                             
*                                                                               
SS12B    DS    0H                                                               
         MVC   MAXLEN,=F'1976'                                                  
*                                                                               
SS20     DS    0H                                                               
         BR    RE                                                               
*                                                                               
         SPACE 3                                                                
*        SYSTEM TABLE                                                           
*              SYSTEM (1)                                                       
*              ELEM START (2)                                                   
*              LENGTH POSITION (2)                                              
*              MAXIMUM REC LENGTH (2)                                           
*                                                                               
SYSTAB   DS    0C                                                               
         DC    X'00',AL2(24,13,1976)    SPOT                                    
         DC    X'01',AL2(33,25,999)     PRINT                                   
         DC    X'02',AL2(34,27,999)     REP                                     
         DC    X'FF',AL2(2,0),X'FFFF'   ELEM BUFFER (NO LENGTH LIMIT)           
         DC     C'S',AL2(24,13,1976)    SPOT                                    
         DC     C'P',AL2(33,25,999)     PRINT                                   
         DC     C'R',AL2(34,27,999)     REP                                     
         DC     C'L',AL2(42,32,1976)    PLANNING                                
         DC     C'X',AL2(16,09,3976)    PLANNING - XVERT FILE                   
         DC     C'U',AL2(27,20,2976)    NETWORK UNITS                           
*                                                                               
SYSTABL  EQU   7                                                                
SYSTABN  EQU   (*-SYSTAB)/SYSTABL                                               
RECUPWK  DSECT                                                                  
RLEN     DS    F                                                                
MAXLEN   DS    F                                                                
LMODE    DS    X                                                                
RECUPWKL EQU   *-RECUPWK                                                        
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006DDRECUP2  07/23/96'                                      
         END                                                                    
