*          DATA SET DELDALLOC  AT LEVEL 009 AS OF 10/18/06                      
*CATALP DELDALOC                                                                
LDALLOC  TITLE ' - DEMOS - OUTPUT TAPE DYNAMIC ALLOCATION'                      
*        PARAMS VIA R1                                                          
*                                                                               
*        AL4   A(OUTPUT TAPE DCB)                                               
*                                                                               
*        AL4   A(DANAME)                                                        
*                                                                               
*        AL4   A(SELECTIVE PERIOD)                                              
*                                                                               
*        AL4   A(BTYPE INDICATOR)                                               
*                                                                               
*        AL4   A(MEDIA INDICATOR)                                               
*                                                                               
LDALLOC  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,DELDALOC                                             
         USING WORKD,RC                                                         
         LR    R2,R1               R2=A(PARAMETER LIST)                         
         LM    R3,R7,0(R2)                                                      
*                                                                               
         MVC   DDN,40(R3)          EXTRACT DDNAME FROM DCB                      
         MVI   DSN,C' '                                                         
         MVC   DSN+1(L'DSN-1),DSN                                               
         MVC   DSNHLM,=C'DEMTAPE'                                               
         MVI   DSNDOT,C'.'                                                      
*                                                                               
         EJECT                                                                  
         CLI   DDN+4,C'O'          TEST OUTPUT TAPE                             
         BNE   LD4                                                              
         MVC   ODSNFIL,=C'NSI'                                                  
         CLI   6(R4),C'N'          TEST NIELSEN                                 
         BE    LD2                                                              
         CLI   6(R4),C'O'          DEMDIRO WAS SPLIT FROM NSI DEMDIRN           
         BE    LD2                                                              
         MVC   ODSNFIL,=C'ARB'                                                  
         CLI   6(R4),C'A'          TEST ARBITRON                                
         BE    LD2                                                              
         MVC   ODSNFIL,=C'RAD'                                                  
         CLI   6(R4),C'R'          TEST RADIO                                   
         BE    LD2                                                              
         MVC   ODSNFIL,=C'TST'                                                  
         CLI   6(R4),C'X'          TEST FILE ...DEMDIRX ETC.                    
         BE    LD2                                                              
         DC    H'0'                                                             
LD2      MVC   ODSNTYP,=C'119'                                                  
         MVC   ODSNSEL,=C'ALL'                                                  
*                                                                               
         CLC   DDN(7),=C'DIRCOPY'                                               
         BNE   *+14                                                             
         MVC   ODSNSEL,=C'DIR'                                                  
         B     LD3                                                              
*                                                                               
         OC    0(2,R5),0(R5)                                                    
         BZ    LD3                                                              
         ZIC   R0,0(R5)                                                         
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  ODSNSEL+0(2),DUB                                                 
         MVC   ODSNSEL+2(1),1(R5)                                               
         OI    ODSNSEL+2,X'F0'                                                  
*                                                                               
LD3      CLI   0(R7),0             ALL MEDIA                                    
         BE    LDA                                                              
         CLI   0(R7),C'T'          TV OR RADIO                                  
         BE    *+14                                                             
         MVC   ODSNMED,=C'R  '     RADIO ONLY                                   
         B     LDA                                                              
*                                                                               
         CLI   0(R6),X'FF'         SPILL OR REGULAR                             
         BNE   *+14                                                             
         MVC   ODSNMED,=C'TX '     SPILL ONLY                                   
         B     LDA                                                              
         MVC   ODSNMED,=C'T-X'     REGULAR ONLY                                 
         B     LDA                                                              
*                                                                               
         EJECT                                                                  
LD4      CLI   DDN+4,C'C'          TEST COPY TAPE                               
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   CDSNFIL,=C'NSI'                                                  
         CLI   6(R4),C'N'          TEST NIELSEN                                 
         BE    LD6                                                              
         MVC   CDSNFIL,=C'ARB'                                                  
         CLI   6(R4),C'A'          TEST ARBITRON                                
         BE    LD6                                                              
         MVC   CDSNFIL,=C'RAD'                                                  
         CLI   6(R4),C'R'          TEST RADIO                                   
         BE    LD6                                                              
         DC    H'0'                                                             
LD6      MVC   CDSNSEL,=C'ALL'                                                  
         MVC   CDSNIM,=C'IM'                                                    
         OC    0(2,R5),0(R5)                                                    
         BZ    LD7                                                              
         ZIC   R0,0(R5)                                                         
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  CDSNSEL+0(2),DUB                                                 
         MVC   CDSNSEL+2(1),1(R5)                                               
         OI    CDSNSEL+2,X'F0'                                                  
*                                                                               
LD7      CLI   0(R7),0             ALL MEDIA                                    
         BE    LDA                                                              
         CLI   0(R7),C'T'          TV OR RADIO                                  
         BE    *+14                                                             
         MVC   CDSNMED,=C'R  '     RADIO ONLY                                   
         B     LDA                                                              
*                                                                               
         CLI   0(R6),X'FF'         SPILL OR REGULAR                             
         BNE   *+14                                                             
         MVC   CDSNMED,=C'TX '     SPILL ONLY                                   
         B     LDA                                                              
         MVC   CDSNMED,=C'T-X'     REGULAR ONLY                                 
         B     LDA                                                              
*                                                                               
LDA      GOTO1 =V(DYNALLOC),DUB,(0,DDN),(1,DSN)                                 
*                                                                               
LDX      XIT1                                                                   
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DDN      DS    CL8                                                              
*                                                                               
DSN      DS    0CL20                                                            
DSNHLM   DS    CL7           1-7                                                
DSNDOT   DS    C               8                                                
ODSNFIL  DS    CL2          9-10                                                
ODSNTYP  DS    CL3         11-13                                                
ODSNSEL  DS    CL3         14-16                                                
ODSNMED  DS    CL3         17-19                                                
*                                                                               
         ORG   ODSNFIL                                                          
CDSNFIL  DS    CL3          9-11                                                
CDSNSEL  DS    CL3         12-14                                                
CDSNIM   DS    CL2         15-16                                                
CDSNMED  DS    CL3         17-19                                                
*                                                                               
         ORG   DSN+L'DSN                                                        
*                                                                               
WORKX    EQU   *                                                                
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'009DELDALLOC 10/18/06'                                      
         END                                                                    
