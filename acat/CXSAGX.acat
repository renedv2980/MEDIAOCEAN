*          DATA SET CXSAGX     AT LEVEL 009 AS OF 01/20/04                      
*CATALP CXSAGX                                                                  
         TITLE 'CXSAGX - CONTROL SECURITY EXTRACT- SQL CONVERT AGENCY'          
**********************************************************************          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(SQL BUFFER)                                                  *          
* AL4 0                                                              *          
* AL4 A(EXTRACT CONTROL BLOCK)                                       *          
**********************************************************************          
         SPACE 1                                                                
         PRINT NOGEN                                                            
CXSAGX   CSECT                                                                  
         NMOD1 WORKX-WORKD,*CXSAGX*,RR=RA                                       
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         XC    0(4,R3),0(R3)                                                    
         LA    R3,4(R3)            BUMP PAST LENGTH IN BUFFER                   
         USING CXSAGD,R2           R2=A(EXTRACT RECORD)                         
         MVI   SEPARATE,DEFSEP     SET DEFAULT FIELD SEPARATOR CHAR             
         MVI   XFLAG,0             INITIALISE CONVERT INPUT FLAG                
         CLI   CXSAGACT,C'L'       TEST IF IN LOAD MODE                         
         BNE   *+12                                                             
         MVI   SEPARATE,LOADSEP    SET LOAD MODE FIELD SEPARATOR CHAR           
         OI    XFLAG,LOADMODE      SET CONVERT FLAG TO LOAD MODE                
*                                                                               
         L     R7,ADXBLOCK         R7=A(EXTRACT CONTROL BLOCK)                  
         USING DXBLOCKD,R7                                                      
         L     R6,DXSTPTR          R6=A(SYSTEM CONTROL TABLE)                   
         USING SXDTABD,R6                                                       
         MVC   VERSION,SXDTVER     VERSION NUMBER                               
*                                                                               
CXTYP    NI    XFLAG,REQUIRED      CONVERT RECORD TYPE - REQUIRED               
         GOTO1 XMXCNVX,DMCB,(L'CXSAGTYP,CXSAGTYP),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
CXACT    NI    XFLAG,REQUIRED      CONVERT RECORD ACTION - REQUIRED             
         GOTO1 XMXCNVX,DMCB,(L'CXSAGACT,CXSAGACT),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
CXDTM    NI    XFLAG,REQUIRED      CONVERT ACTION DATE TIME - REQUIRED          
         LA    R0,L'CXSAGDAT+L'CXSAGTIM+1                                       
         GOTO1 XMXCNVX,DMCB,((R0),CXSAGDAT),(XFLAG,(R3)),(1,DTIM)               
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
CXAID    NI    XFLAG,REQUIRED      CONVERT AGENCY ALPHA ID - REQUIRED           
         GOTO1 XMXCNVX,DMCB,(L'CXSAGAID,CXSAGAID),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
CXSEC    NI    XFLAG,REQUIRED      CONVERT SEC AGENCY ID - REQUIRED             
         GOTO1 XMXCNVX,DMCB,(L'CXSAGSEC,CXSAGSEC),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
         CLI   VERSION,3           TEST VERSION >= 3                            
         BL    DXBUFLEN            NO                                           
*                                                                               
CXPPS    NI    XFLAG,REQUIRED                                                   
         CLI   CXSAGIPP,C' '       TEST FOR VALUE                               
         BNE   *+8                                                              
         MVI   CXSAGIPP,C'N'       DEFAULT TO NO                                
         GOTO1 XMXCNVX,DMCB,(L'CXSAGIPP,CXSAGIPP),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP                                                        
*                                                                               
         OI    XFLAG,OPTIONAL      REST OF FIELDS ARE OPTIONAL                  
         GOTO1 XMXCNVX,DMCB,(L'CXSAGPWE,CXSAGPWE),(XFLAG,(R3)),(1,SMAL)         
         BAS   RE,PUTSEP                                                        
*                                                                               
         OI    XFLAG,OPTIONAL      PASSWORD WARNING                             
         GOTO1 XMXCNVX,DMCB,(L'CXSAGPWW,CXSAGPWW),(XFLAG,(R3)),(1,SMAL)         
         BAS   RE,PUTSEP                                                        
*                                                                               
         OI    XFLAG,OPTIONAL      PASSWORD REUSE                               
         GOTO1 XMXCNVX,DMCB,(L'CXSAGPWR,CXSAGPWR),(XFLAG,(R3)),(1,SMAL)         
         BAS   RE,PUTSEP                                                        
*                                                                               
         OI    XFLAG,OPTIONAL      PASSWORD MINIMUM LENGTH                      
         GOTO1 XMXCNVX,DMCB,(L'CXSAGPWM,CXSAGPWM),(XFLAG,(R3)),(1,SMAL)         
         BAS   RE,PUTSEP                                                        
*                                                                               
DXBUFLEN L     RF,ASQLBUFF         STORE BUFFER DATA LENGTH                     
         SR    R3,RF               IN TOP 2 BYTES                               
         SLL   R3,16                                                            
         STCM  R3,15,0(RF)                                                      
         B     EXIT                                                             
*                                                                               
EXIT     XMOD1 1                                                                
*                                                                               
PUTSEP   SR    RF,RF               PUT OUT SEPARATOR CHARACTER                  
         ICM   RF,1,4(R1)          GET RETURN LENGTH                            
         LA    R3,0(RF,R3)         BUMP DATA BUFFER POINTER                     
         MVC   0(1,R3),SEPARATE                                                 
         LA    R3,1(R3)                                                         
         BR    RE                                                               
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
XMXCNVX  DC    V(MXCNVX)                                                        
         SPACE 1                                                                
CHAR     DC    CL8'CHAR    '                                                    
DTIM     DC    CL8'DATETIME'                                                    
TINY     DC    CL8'TINYINT '                                                    
SMAL     DC    CL8'SMALLINT'                                                    
INT      DC    CL8'INT     '                                                    
REAL     DC    CL8'REAL    '                                                    
FLOT     DC    CL8'FLOAT   '                                                    
BIT      DC    CL8'BIT     '                                                    
MONY     DC    CL8'MONEY   '                                                    
         EJECT                                                                  
WORKD    DSECT                                                                  
PARM     DS    0XL16               PARAMETER LIST                               
AXREC    DS    A                   A(EXTRACT RECORD)                            
ASQLBUFF DS    A                   A(SQL BUFFER)                                
         DS    A                                                                
ADXBLOCK DS    A                   A(EXTRACT CONTROL BLOCK)                     
*                                                                               
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
RELO     DS    A                                                                
DMCB     DS    6F                                                               
SEPARATE DS    CL1                 FIELD SEPARATOR CHARACTER                    
DEFSEP   EQU   C';'                                                             
LOADSEP  EQU   C';'                                                             
XFLAG    DS    XL1                 CONVERT INPUT CONTROL FLAGS                  
LOADMODE EQU   X'80'                 LODE MODE                                  
OPTIONAL EQU   X'40'                 OPTIONAL FIELD                             
REQUIRED EQU   X'FF'-X'40'           REQUIRED FIELD                             
*                                                                               
VERSION  DS    XL1                 VERSION NUMBER                               
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE CXSAGD                                                         
         EJECT                                                                  
       ++INCLUDE DXDSECTS                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'009CXSAGX    01/20/04'                                      
         END                                                                    
