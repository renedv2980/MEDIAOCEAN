*          DATA SET SPMEDSEED  AT LEVEL 028 AS OF 05/01/02                      
*CATALP MEDSEED                                                                 
         TITLE 'MODULE TO COMPLETE MEDIA SUMMARY BLOCKS'                        
         PRINT NOGEN                                                            
VMDSEED  CSECT                                                                  
         NMOD1 10,**SEED**                                                      
         USING SEED,RC                                                          
         L     RA,0(R1)                                                         
         LA    R9,2048(RA)                                                      
         LA    R9,2048(R9)                                                      
         USING SPWORKD,RA,R9                                                    
         L     R2,MEDTABLE                                                      
         L     R7,MEDBUFF                                                       
         USING MEDBLOCK,R7                                                      
         CLC   MEDNUMWK,=F'56'                                                  
         BNE   *+10                                                             
         MVC   MEDNUMWK,=F'75'                                                  
         CLC   MEDNUMMO,=F'12'                                                  
         BNE   *+10                                                             
         MVC   MEDNUMMO,=F'13'                                                  
         CLC   MEDNUMQT,=F'4'                                                   
         BNE   *+10                                                             
         MVC   MEDNUMQT,=F'05'                                                  
         LR    R3,R7                                                            
         LA    R4,(MEDBY14Q+4)-MEDGLD                                           
         CLI   MEDEXCH,C'C'        TEST CANADIAN EXCHANGE REQUESTED             
         BE    SEED1                                                            
         CLI   MEDEXCH,C'U'                                                     
         BE    SEED1                                                            
         CLI   MEDEXCH,C'X'                                                     
         BNE   *+8                 NO                                           
SEED1    LA    R4,MEDXCHX-MEDGLD   YES-INCLUDE EXCHANGE FIELDS                  
         CLI   MEDEXTDM,4          USE BIG CHUNK IF MORE THAN 4 DEMOS           
         BH    SEED2                                                            
         CLI   MEDEXTAC,C'Y'       OR IF ACCOUNTING DATA IS NEEDED              
         BE    SEED2                                                            
         LA    R4,MEDBY5-MEDGLD    OTHERWISE CUT DOWN VERSION IS OK             
         SPACE 2                                                                
SEED2    ST    R4,MEDLCHNK                                                      
         ST    R4,PERDISP                                                       
         L     R4,BUFFBUFF                                                      
         USING BUFFALOD,R4                                                      
         L     R5,BUFFLKEY                                                      
         A     R5,BUFFLCOM                                                      
         ST    R5,KEYWIDTH                                                      
         EJECT                                                                  
*              NOW TOUR ROUND THE TABLES                                        
         SPACE 3                                                                
         L     R8,MEDTABLE         RELOCATION FACTOR INTO TABLE                 
*&&OS*&& SR    R8,R8               MVS USES ACTUAL ADDRESSES                    
         MVI   LOOP,1                                                           
         BAS   RE,REPDEF           DO THE WORK                                  
         SR    R8,R8                                                            
         MVI   LOOP,2                                                           
         BAS   RE,REPDEF           TURN OFF THE BITS                            
         B     XIT                                                              
         SPACE 2                                                                
REPDEF   NTR1                                                                   
         LR    R3,R2                                                            
         SPACE 2                                                                
REP2     L     R2,0(R3)            LOOP ON REPDEF TABLE                         
         AR    R2,R8                                                            
         ST    R2,0(R3)                                                         
         BAS   RE,ROW                                                           
         LA    R3,4(R3)                                                         
         CLI   0(R3),X'FF'                                                      
         BNE   REP2                                                             
         B     XIT                                                              
         SPACE 2                                                                
ROW      NTR1                                                                   
         LR    R3,R2               CONTROL A ROW                                
         L     R4,BUFFIO                                                        
         L     R2,0(R3)                                                         
         LTR   R2,R2                                                            
         BZ    *+6                                                              
         AR    R2,R8                                                            
         ST    R2,0(R3)                                                         
         L     R2,4(R3)            RELOCATE COLUMN                              
         AR    R2,R8                                                            
         BAS   RE,COL              GO AND FIX COLUMNS                           
         ST    R2,4(R3)                                                         
         LA    R3,16(R3)                                                        
         ST    R4,AOUT             SET TO START OF KEY                          
         SPACE 2                                                                
ROW2     LR    R2,R3               AND HANDLE THE KEY FIELDS                    
         CLI   0(R2),X'FF'                                                      
         BE    XIT                                                              
         BAS   RE,DATA                                                          
         LA    R3,8(R3)                                                         
         B     ROW2                                                             
         SPACE 2                                                                
COL      NTR1                                                                   
         A     R4,KEYWIDTH         HANDLE A TABLE OF COLUMNS                    
         ST    R4,AOUT                                                          
         LR    R3,R2                                                            
         SPACE 2                                                                
COL2     LR    R2,R3                                                            
         CLI   0(R2),X'FF'                                                      
         BE    XIT                                                              
         BAS   RE,DATA                                                          
         LA    R3,8(R3)                                                         
         B     COL2                                                             
         EJECT                                                                  
*              DEAL WITH THE 8-BYTE DATA FIELDS                                 
         SPACE 3                                                                
DATA     NTR1                                                                   
         CLI   LOOP,2                                                           
         BE    RESET                                                            
         TM    0(R2),X'80'         DONT SEED A CHUNK TWICE                      
         BO    XIT                                                              
         L     R3,0(R2)            R3=DISPLACEMENT                              
         SLL   R3,16                                                            
         SRL   R3,16                                                            
         CLI   1(R2),1                                                          
         BL    DATA0                                                            
         BE    DATA1                                                            
         CLI   1(R2),2                                                          
         BE    DATA2                                                            
         B     DATA3                                                            
         SPACE 2                                                                
DATA0    LA    R4,=F'0'            R4=BASE                                      
         B     DATALL                                                           
DATA1    L     R4,MEDBUFF          (MEDBLOCK)                                   
         B     DATALL                                                           
DATA2    LR    R4,RA               (DSECT)                                      
         B     DATALL                                                           
DATA3    L     R4,MEDTABLE         (TABLES)                                     
         SLL   R3,20                                                            
         SRL   R3,20                                                            
         TM    2(R2),X'B0'                                                      
         BO    *+8                                                              
         A     R3,=F'4096'         BASE NOT RB- ADD 4096                        
         SPACE 2                                                                
DATALL   AR    R3,R4               NOW HAVE A(INPUT DATA)                       
         SR    R5,R5               NOW WORK OUT DATE DISPLACEMENT               
         IC    R5,5(R2)                                                         
         CLI   4(R2),0                                                          
         BE    DATALL2                                                          
         BCTR  R5,0                                                             
         CLI   4(R2),C'W'          WEEK CHUNK                                   
         BNE   DATALL1                                                          
         CLI   5(R2),15            ADJUST WEEK NUMBERS FOR GAPS                 
         BL    DATALL2                                                          
         LA    R5,1(R5)                                                         
         CLI   5(R2),29                                                         
         BL    DATALL2                                                          
         LA    R5,1(R5)                                                         
         CLI   5(R2),43                                                         
         BL    DATALL2                                                          
         LA    R5,1(R5)                                                         
         CLI   5(R2),57                                                         
         BL    DATALL2                                                          
         LA    R5,1(R5)                                                         
         B     DATALL2                                                          
         SPACE 2                                                                
DATALL1  A     R5,MEDNUMWK                                                      
         CLI   4(R2),C'M'          MONTH CHUNK                                  
         BE    DATALL2                                                          
         A     R5,MEDNUMMO                                                      
         CLI   4(R2),C'Q'          QUARTER CHUNK                                
         BE    DATALL2                                                          
         L     R5,MEDNUMWK                                                      
         A     R5,MEDNUMMO                                                      
         A     R5,MEDNUMQT                                                      
         CLI   4(R2),C'B'          BEFORE                                       
         BE    DATALL2                                                          
         CLI   MEDNYBEF,C'Y'                                                    
         BNE   *+8                                                              
         LA    R5,1(R5)                                                         
         CLI   4(R2),C'P'          PERIOD                                       
         BE    DATALL2                                                          
         CLI   MEDNYAFT,C'Y'                                                    
         BNE   *+8                                                              
         LA    R5,1(R5)                                                         
         CLI   4(R2),C'A'          AFTER                                        
         BE    DATALL2                                                          
         LA    R5,1(R5)                                                         
*                                  S/B TOTAL                                    
         SPACE 2                                                                
DATALL2  M     R4,PERDISP                                                       
         AR    R3,R5               ADD IN ANY DATE DISPLACEMENT                 
         IC    R4,0(R2)            LENGTH OF THIS FIELD                         
         SLL   R4,25                                                            
         SRL   R4,25                                                            
         ST    R3,0(R2)            COMPLETE INPUT DETAILS                       
         STC   R4,0(R2)                                                         
         OI    0(R2),X'80'                                                      
         L     R3,AOUT             OUTPUT IS EASY                               
         ST    R3,4(R2)                                                         
         AR    R3,R4                                                            
         ST    R3,AOUT             ADJUST WITH LENGTH OF THIS FIELD             
         B     XIT                                                              
         SPACE 2                                                                
RESET    NI    0(R2),X'7F'         TURN OFF BIT ON LAP 2                        
         SPACE 2                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*              DSECT FOR MODULE                                                 
         SPACE 3                                                                
SEED     DSECT                                                                  
KEYWIDTH DS    F                                                                
PERDISP  DS    F                                                                
AOUT     DS    F                                                                
LOOP     DS    CL1                                                              
         PRINT OFF                                                              
       ++INCLUDE SPREPWORKD                                                     
       ++INCLUDE SPMEDBLOCK                                                     
       ++INCLUDE DDBUFFALOD                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'028SPMEDSEED 05/01/02'                                      
         END                                                                    
