*          DATA SET ACLDBALC   AT LEVEL 061 AS OF 04/11/18                      
*CATALP ACLDBALC                                                                
LDBALCK  TITLE '- ACCFILE DTB AND BALANCE CHECK/PRINT'                          
***********************************************************************         
* PARAMS VIA R1                                                       *         
* XL1   COUNT/PRINT  X'01'=COUNT  X'FF'=PRINT                         *         
* AL3   A(RECORD)                                                     *         
***********************************************************************         
LDBALCK  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,ACLDBALC,R9                                          
         USING WORKD,RC                                                         
         USING MASTD,R6                                                         
         USING BOXD,R7                                                          
         USING DPRINT,RA                                                        
                                                                                
         L     R6,VMASTC           DDMASTER                                     
         L     R7,VBOXAREA         BOXES                                        
         L     RA,VCPRINT          DDPRINT                                      
         MVI   BOXDDCTL,X'80'                                                   
*&&US*&& ZAP   MAXLINE,=P'57'                                                   
         CLI   0(R1),X'FF'         FINAL CALL                                   
         BNE   BALC1                                                            
         BAS   RE,LASTBOTH                                                      
         B     EXIT                                                             
*                                                                               
BALC1    SR    R2,R2                                                            
         ICM   R2,7,1(R1)                                                       
         ST    R2,ADAREC                                                        
         GOTO1 VRECTYP,DMCB,(C'D',(R2))                                         
         MVC   RECTYPE,0(R1)                                                    
         CLI   RECTYPE,ACRTUNKN    UNKNOWN RECORD TYPE                          
         BE    EXIT                SKIP                                         
         CLI   RECTYPE,ACRTTIM     TMS RECORD ?                                 
         BE    BALC1B              Include this type                            
         CLI   RECTYPE,ACRTGLBP    New GL bucket ?                              
         BNE   BALC1A              Continue                                     
         GOTOR GLBAL,0             General ledger balancing                     
BALC1A   CLI   RECTYPE,ACRTOTHR    Special records skip                         
         BH    EXIT                                                             
*                                                                               
         USING NAMELD,RE                                                        
BALC1B   MVC   RECNAME,SPACES      SET NAME TO SPACES                           
         MVC   ANAMEL,4(R1)                                                     
         MVC   ABALEL,8(R1)                                                     
         ICM   RE,15,ANAMEL                                                     
         BZ    BALC2                                                            
         SR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         CHI   RF,L'NAMEREC+2                                                   
         BNH   *+8                                                              
         LA    RF,L'NAMEREC+2                                                   
         SHI   RF,NAMLN1Q+1                                                     
         BM    BALC2                                                            
         EXMVC RF,RECNAME,NAMEREC                                               
         DROP  RE                                                               
*                                                                               
BALC2    CLI   PROCSW,PROCFRST                                                  
         BNE   *+8                                                              
         BAS   RE,INITBOTH                                                      
         BAS   RE,DTBMAIN          Daily trail balance                          
         GOTOR GLBAL,0             General ledger balancing                     
         TM    BALSW,BALCHK        NEED BALANCE CHECK REPORT ?                  
         BNO   EXIT                                                             
         CLI   RECTYPE,ACRTTRNA    FINANCIAL RECORD ?                           
         BH    EXIT                                                             
BALC2A   BAS   RE,BLCMAIN                                                       
         B     EXIT                                                             
         EJECT                                                                  
*********************************************************************           
* DAILY TRIAL BALANCE & BALANCE CHECK - INITIALISE                  *           
*********************************************************************           
INITBOTH NTR1  ,                                                                
         BAS   RE,SETPRINT                                                      
         MVC   BOXROWS,SPACES                                                   
         MVI   BOXROWS+6,C'T'                                                   
         MVI   BOXROWS+8,C'M'                                                   
         MVI   BOXROWS+99,C'B'                                                  
         MVI   BOXYORN,C'Y'                                                     
         MVC   BOXCOLS,SPACES                                                   
         LA    RF,BOXCOLS                                                       
         USING DTBPLIND,RF                                                      
         MVI   DTBPBXL,C'L'                                                     
         MVI   DTBPBXC1,C'C'                                                    
         MVI   DTBPBXC2,C'C'                                                    
         MVI   DTBPBXC3,C'C'                                                    
         MVI   DTBPBXC4,C'C'                                                    
         MVI   DTBPBXC5,C'C'                                                    
         MVI   DTBPBXR,C'R'                                                     
         MVI   MID2,X'00'                                                       
         MVI   SUB1,X'AC'          BOX CORNER FORCES PRINT                      
         DROP  RF                                                               
*                                                                               
         USING FHDATED,RF                                                       
         L     RF,VSVDAHDR         RF=A(A-FILE HEADER SAVE AREA)                
         L     RE,VOUTPUT                                                       
         MVC   ACTDATE,FHDUPD      SET ACTIVITY DATE                            
         TM    0(RE),DISK          TEST LOAD                                    
         BZ    INIT2                                                            
         MVC   ACTDATE,FHDLOD                                                   
         MVC   FHDLOD,FHDUPD       SET LOAD DATE IF LOADING                     
         SR    RE,RE                                                            
         ICM   RE,7,FHDCNT         BUMP AND STORE COUNT                         
         AHI   RE,1                                                             
         STCM  RE,7,FHDCNT                                                      
         DROP  RF                                                               
*                                                                               
INIT2    GOTO1 VDATCON,DMCB,(2,ACTDATE),(1,TODAY)                               
         MVI   PROCSW,PROCTBAL     TRIAL BALANCE ONLY                           
         L     RE,VMODE                                                         
         TM    0(RE),AFILE+BFILE   NEED BOTH FILES FOR BALANCE CHECK            
         BNO   EXIT                                                             
         GOTO1 VBUFFRN,DMCB,('BUFFAINI',BUFF),(0,0),COMFACS                     
         CLI   4(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         OI    BALSW,BALCHK                                                     
         B     EXIT                                                             
         EJECT                                                                  
**********************************************************************          
* DAILY TRIAL BALANCE & BALANCE CHECK - FINALISE                     *          
**********************************************************************          
LASTBOTH NTR1  ,                                                                
         CLI   PROCSW,PROCFRST     EXIT IF FIRST CALLED WITH X'FF'              
         BE    EXIT                                                             
         BAS   RE,DTBLAST                                                       
         MVI   BOXREQ,C'C'                                                      
         GOTO1 VPRINTER                                                         
*&&US                                                                           
         L     RE,VOUTPUT          TEST IF LOADING TO DISK                      
         TM    0(RE),DISK                                                       
         BO    LASTB09                                                          
         ICM   RF,15,=V(TYPCHK)    TEST TRANSACTION TYPE SUMMARY                
         BZ    LASTB09                                                          
         GOTO1 (RF),DMCB,(C'A',0)                                               
         ICM   R3,7,1(R1)          R3=A(TYPE SUMMARY TABLE)                     
         USING TTYD,R3                                                          
         BAS   RE,SETPRINT                                                      
         MVC   BOXROWS,SPACES                                                   
         MVI   BOXROWS+5,C'T'                                                   
         MVI   BOXROWS+8,C'M'                                                   
         MVI   BOXROWS+99,C'B'                                                  
         MVI   BOXYORN,C'Y'                                                     
         MVI   BOXINIT,0                                                        
         MVC   BOXCOLS,SPACES                                                   
         LA    R4,BOXCOLS                                                       
         USING TYPLD,R4                                                         
         MVI   TYPBXL,C'L'                                                      
         MVI   TYPBX1,C'C'                                                      
         MVI   TYPBX2,C'C'                                                      
         MVI   TYPBX3,C'C'                                                      
         MVI   TYPBX4,C'C'                                                      
         MVI   TYPBXR,C'R'                                                      
         MVC   TITLE+10(30),=CL30'ANALYSIS OF TODAY''S POSTINGS'                
         MVI   MID1,X'00'                                                       
         MVI   SUB1,X'AC'                                                       
         MVC   SUB2+(TYPDES-TYPLD)(L'TYPH1),TYPH1  SET HEADLINE                 
         GOTO1 VPRINTER                                                         
         GOTO1 VPRINTER                                                         
*                                                                               
LASTB03  LA    R4,P                                                             
         MVC   TYPDES,TTYDES      CAPTION                                       
         CURED (P8,TTYDR),TYPDR,2,MINUS=YES                                     
         CURED (P8,TTYCR),TYPCR,2,MINUS=YES                                     
         EDIT  (P8,TTYNUM),TYPNUM,ZERO=BLANK                                    
         ZAP   DUB,TTYDR                                                        
         SP    DUB,TTYCR                                                        
         CURED (P8,DUB),TYPDIF,2,MINUS=YES                                      
         GOTO1 VPRINTER                                                         
         LA    R3,TTYLNQ(R3)                                                    
         CLI   0(R3),X'FF'                                                      
         BNE   LASTB03                                                          
         MVI   BOXREQ,C'C'                                                      
         GOTO1 VPRINTER                                                         
         DROP  R3,R4                                                            
*&&                                                                             
*                                                                               
LASTB09  TM    BALSW,BALCHK                                                     
         BZ    *+8                                                              
         BAS   RE,BLCLST                                                        
                                                                                
         TM    GLSW,GLANY          Any thing to process ?                       
         BZ    LASTB12                                                          
         GOTOR GLBAL,1                                                          
                                                                                
LASTB12  MVI   BOXOFF,C'Y'                                                      
         BAS   RE,SETPRINT                                                      
         GOTO1 VPRINTER                                                         
         B     EXIT                                                             
         EJECT                                                                  
*********************************************************************           
* DAILY TRIAL BALANCE - MAIN & FINAL PROCESSES                      *           
*********************************************************************           
         USING TRNRECD,R2                                                       
DTBLAST  NTR1  ,                   FINAL                                        
         MVI   REPTHIS,4                                                        
         B     DTBM28                                                           
*                                                                               
DTBMAIN  NTR1  ,                   MAIN                                         
         CLI   RECTYPE,ACRTACTH                                                 
         BE    DTBM01                                                           
         CLI   RECTYPE,ACRTACTL                                                 
         BE    DTBM01                                                           
         CLI   RECTYPE,ACRTTRN                                                  
         BE    DTBM02                                                           
         CLI   RECTYPE,ACRTTRNA                                                 
         BE    DTBM02                                                           
         CLI   RECTYPE,ACRTCPY                                                  
         BE    DTBM18                                                           
         CLI   RECTYPE,ACRTUNT                                                  
         BE    DTBM20                                                           
         CLI   RECTYPE,ACRTLDG                                                  
         BE    DTBM26                                                           
         B     EXIT                                                             
*                                                                               
DTBM01   BAS   RE,LEVL             TEST ACCOUNT HIGHER LEVELS                   
         MVC   LASTACC,TRNKCPY     SAVE LAST ACCOUNT KEY                        
         ICM   R3,15,ABALEL        HANDLE LOW ACCOUNT                           
         BZ    EXIT                                                             
         USING ABLELD,R3                                                        
         TM    TRNRSTAT,TRNSDELT                                                
         BO    EXIT                                                             
         AP    ACFORWD,ABLFRWD                                                  
         AP    ACBLDEB,ABLDR                                                    
         AP    ACBLCRD,ABLCR                                                    
         B     EXIT                                                             
*                                                                               
DTBM02   TM    TRNRSTAT,TRNSDRFT   IGNORE DRAFT TRANSACTIONS                    
         BNZ   EXIT                                                             
         TM    TRNRSTAT,TRNSDELT                                                
         BO    EXIT                                                             
         LA    R3,TRNRFST          HANDLE TRANSACTION                           
         USING TRNELD,R3                                                        
         GOTO1 VDATCON,DMCB,(1,TRNDATE),(2,WORK)  TRANSACTION DATE              
         LA    RE,TRNELD                            IN RESERVE                  
         SR    R0,R0                                                            
*                                                                               
         USING TRSELD,RE                                                        
DTBM03   IC    R0,TRSLN                                                         
         AR    RE,R0                                                            
         CLI   TRSEL,0                                                          
         BE    DTBM04                                                           
         CLI   TRSEL,TRSELQ                                                     
         BNE   DTBM03                                                           
         MVC   WORK(L'TRSDATE),TRSDATE                                          
         TM    TRNRSTA2,TRNSPEEL   TEST IF CLOSED                               
         BNO   DTBM04                                                           
         CLC   TRSPDAT,ACTDATE     SINCE LAST LOAD/UPDATE                       
         BL    DTBM04                                                           
*                                                                               
         LA    R1,ACCRDEL          ADD PEELED ITEMS TO DELETED TOTAL            
         TM    TRNSTAT,TRNSDR                                                   
         BZ    *+8                                                              
         LA    R1,L'ACCRDEL(R1)                                                 
         AP    0(L'ACCRDEL,R1),TRNAMNT                                          
         DROP  RE                                                               
*                                                                               
DTBM04   L     RE,VOUTPUT          TEST IF LOADING TO DISK                      
         TM    0(RE),DISK                                                       
         BZ    DTBM05              No                                           
         CLC   WORK(L'ACTDATE),ACTDATE                                          
         BNH   DTBM10                                                           
         B     DTBM08                                                           
                                                                                
DTBM05   CLC   WORK(L'ACTDATE),ACTDATE                                          
         BL    EXIT                Exit, before today                           
*                                                                               
DTBM08   BRAS  RE,GLTYPE           Totals GL by A25 & not A25 (Unit S)          
         LA    R1,ACCRPOS                                                       
         TM    TRNSTAT,TRNSDR      TEST FOR DEBIT                               
         BZ    *+8                                                              
         LA    R1,L'ACCRPOS(R1)                                                 
         AP    0(L'ACCRPOS,R1),TRNAMNT                                          
         ICM   RF,15,=V(TYPCHK)    SUMMARY BY TYPE                              
         BZ    DTBM10                                                           
         GOTO1 (RF),DMCB,(C'N',TRNRECD)                                         
*                                                                               
DTBM10   B     EXIT                                                             
*                                                                               
DTBM18   LA    R3,REPCOMPC                                                      
         MVI   REPTHIS,3                                                        
         B     DTBM28                                                           
*                                                                               
DTBM20   LA    R3,REPUNITC                                                      
         MVI   REPTHIS,2                                                        
         B     DTBM28                                                           
*                                                                               
DTBM26   BAS   RE,LDGR            GET LEDGER LENGTHS                            
         LA    R3,REPLEDGC                                                      
         MVI   REPTHIS,1                                                        
*                                                                               
DTBM28   LA    RF,DTBREPAS         Compare prior level and current              
DTBM30   CLI   0(RF),X'FF'         End of table                                 
         BE    EXIT                No match, do nothing                         
         CLC   REPPREV(2),0(RF)    Find match and go to routine                 
         BE    DTBM40                                                           
         LA    RF,L'DTBREPAS(RF)                                                
         B     DTBM30                                                           
                                                                                
DTBM40   SR    RE,RE                                                            
         ICM   RE,7,2(RF)                                                       
         BR    RE                                                               
         EJECT ,                                                                
***********************************************************************         
* Daily trial balance reporting routine                                         
***********************************************************************         
DTBREP2  BAS   RE,DRPTOTC          COMPANY/COMPANY                              
         B     DTBREP18                                                         
*                                                                               
DTBREP4  BAS   RE,DRPTOTU          UNIT/COMPANY                                 
         BAS   RE,DRPTOTC                                                       
         B     DTBREP18                                                         
*                                                                               
DTBREP6  BAS   RE,DRPTOTU          UNIT/UNIT                                    
         B     DTBREP18                                                         
*                                                                               
DTBREP8  BAS   RE,DRPTOTL          LEDGER/COMPANY                               
         BAS   RE,DRPTOTU                                                       
         BAS   RE,DRPTOTC                                                       
         ZAP   LINE,=P'99'                                                      
         B     DTBREP18                                                         
*                                                                               
DTBREP10 BAS   RE,DRPTOTL          LEDGER/UNIT                                  
         BAS   RE,DRPTOTU                                                       
         B     DTBREP18                                                         
*                                                                               
DTBREP12 BAS   RE,DRPTOTL          LEDGER/LEDGER                                
         B     DTBREP18                                                         
*                                                                               
DTBREP14 BAS   RE,DRPTOTL          ANY/LAST TIME                                
         BAS   RE,DRPTOTU                                                       
         BAS   RE,DRPTOTC                                                       
         GOTO1 DIVIDE,1            END LOGO FOR PREVIOUS COMPANY                
*&&UK*&& CLI   SAVLANG,LANGEUK     TEST LAST LANGUAGE WAS ENGLISH               
*&&US*&& CLI   SAVLANG,LANGEUS                                                  
         BE    DTBREP16                                                         
         MVI   DMCB+0,C'L'         LIST                                         
         MVI   DMCB+1,C'L'         LOWER CASE                                   
         MVI   DMCB+2,X'06'        ACCOUNTING SYSTEM                            
         MVI   DMCB+3,LANGEUK      LANGUAGE                                     
         BAS   RE,DTBDIC                                                        
         MVI   BOXLANG,0                                                        
         MVI   MCLANG,0                                                         
         MVI   MCCTRY,0                                                         
DTBREP16 ZAP   LINE,=P'99'                                                      
         ZAP   PAGE,=P'1'                                                       
         BAS   RE,DRPTOTF                                                       
         BAS   RE,DRPTOTG                                                       
         B     EXIT                                                             
*                                                                               
DTBREP18 MVC   2(L'RECNAME,R3),RECNAME                                          
         CLI   REPTHIS,1                                                        
         BNE   *+10                                                             
         MVC   0(1,R3),LDGKLDG-LDGRECD(R2)                                      
         CLI   REPTHIS,2                                                        
         BNE   *+10                                                             
         MVC   0(1,R3),UNTKUNT-UNTRECD(R2)                                      
         CLI   REPTHIS,3                                                        
         BNE   DTBREP22                                                         
         MVC   0(1,R3),CPYKCPY-CPYRECD(R2)                                      
         CLI   REPSWIT,0           TEST FIRST TIME                              
         BE    DTBREP20                                                         
         GOTO1 DIVIDE,1            END LOGO FOR PREVIOUS COMPANY                
         BRAS  RE,GOSMF            Put out company totals to SMF                
*                                                                               
DTBREP20 BAS   RE,DTBDIV           SET NEW COMPANY VALUES                       
         MVI   REPSWIT,1           TEST FIRST TIME                              
         GOTO1 DIVIDE,0                                                         
         ZAP   LINE,=P'99'                                                      
         ZAP   PAGE,=P'1'                                                       
         GOTO1 VHEXOUT,DMCB,REPCOMPC,MID1+22,1,=C'TOG'                          
         MVC   MID1+25(L'REPCOMPN),REPCOMPN                                     
*                                                                               
DTBREP22 MVC   REPPREV,REPTHIS     Last report level                            
         L     RE,AREPACCS                                                      
         SR    R0,R0                                                            
         IC    R0,REPTHIS                                                       
*                                                                               
DTBREP24 ZAP   0(REPTABW,RE),PZERO                                              
         MVC   REPTABW(REPTABL-REPTABW,RE),0(RE)                                
         LA    RE,REPTABL(RE)                                                   
         BCT   R0,DTBREP24                                                      
         B     EXIT                                                             
         EJECT                                                                  
**********************************************************************          
* DAILY TRIAL BALANCE - REPORT TOTAL ROUTINES                        *          
**********************************************************************          
         USING REPTABD,RE                                                       
DRPTOTL  NTR1  ,                                                                
         L     RE,AREPACCS                                                      
         ZAP   REPTABD(REPTABW),PZERO                                           
         MVC   REPTABD+REPTABW(REPTABL-REPTABW),REPTABD                         
*                                                                               
         AP    REPTIFBF,ACFORWD                                                 
         AP    REPTIFBF,ACCRDEL                                                 
         SP    REPTIFBF,ACDRDEL                                                 
*                                                                               
         AP    REPTIFDR,ACBLDEB                                                 
         SP    REPTIFDR,ACDRPOS                                                 
         AP    REPTIFDR,ACDRDEL                                                 
*                                                                               
         SP    REPTIFCR,ACCRDEL                                                 
         SP    REPTIFCR,ACBLCRD                                                 
         AP    REPTIFCR,ACCRPOS                                                 
*                                                                               
         AP    REPTIFCF,ACFORWD                                                 
         AP    REPTIFCF,ACBLDEB                                                 
         SP    REPTIFCF,ACDRPOS                                                 
         SP    REPTIFCF,ACBLCRD                                                 
         AP    REPTIFCF,ACCRPOS                                                 
*                                                                               
         AP    REPTTPDR,ACDRPOS                                                 
*                                                                               
         SP    REPTTPCR,ACCRPOS                                                 
*                                                                               
         AP    REPTTPCF,ACDRPOS                                                 
         SP    REPTTPCF,ACCRPOS                                                 
*                                                                               
         SP    REPTTDBF,ACCRDEL                                                 
         AP    REPTTDBF,ACDRDEL                                                 
*                                                                               
         SP    REPTTDDR,ACDRDEL                                                 
*                                                                               
         AP    REPTTDCR,ACCRDEL                                                 
*                                                                               
         AP    REPTOFBF,ACFORWD                                                 
*                                                                               
         AP    REPTOFDR,ACBLDEB                                                 
*                                                                               
         SP    REPTOFCR,ACBLCRD                                                 
*                                                                               
         AP    REPTOFCF,ACFORWD                                                 
         AP    REPTOFCF,ACBLDEB                                                 
         SP    REPTOFCF,ACBLCRD                                                 
*                                                                               
         LA    RF,REPTABD+REPTABL                                               
         LA    R1,3                                                             
DRPTOTL2 LA    R0,REPTABN          ADD TO LEVELS 2,3 AND 4                      
DRPTOTL4 AP    0(REPTABW,RF),0(REPTABW,RE)                                      
         LA    RE,REPTABW(RE)                                                   
         LA    RF,REPTABW(RF)                                                   
         BCT   R0,DRPTOTL4                                                      
         L     RE,AREPACCS                                                      
         BCT   R1,DRPTOTL2                                                      
         DROP  RE                                                               
         ZAP   ACFORWD,PZERO                                                    
         MVC   ACFORWD+8(ACCUMSL-8),ACFORWD                                     
*                                                                               
         USING DTBPLIND,R8                                                      
         LA    R8,REPRNT3                                                       
         L     RF,VCHOPPER                                                      
         LA    R1,DMCB                                                          
         GOTO1 (RF),(R1),(L'REPLEDGN,REPLEDGN),(L'DTBPULN,DTBPULN),    X        
               (C'P',2)                                                         
         MVC   DTBPUL,REPLEDGC                                                  
         LA    R8,REPRNT1                                                       
         GOTO1 (RF),(R1),(L'REPUNITN,REPUNITN),(L'DTBPULN,DTBPULN),    X        
               (C'P',2)                                                         
         MVC   DTBPUL,REPUNITC                                                  
         LA    R4,1                                                             
         B     DTBPRNT                                                          
***********************************************************************         
* Unit Total                                                                    
***********************************************************************         
DRPTOTU  NTR1  ,                                                                
         LA    R8,REPRNT1                                                       
         MVC   DTBPUL(L'AC@TUNT),AC@TUNT                                        
         LA    RF,DTBPUL+L'AC@TUNT-1                                            
         LHI   R0,L'AC@TUNT-1                                                   
         BRAS  RE,RTRIM            Return RF new location                       
         MVI   2(RF),C'-'                                                       
         MVC   4(1,RF),REPUNITC                                                 
         LA    R4,2                                                             
         B     DTBPRNT                                                          
***********************************************************************         
* Company total                                                                 
***********************************************************************         
DRPTOTC  NTR1  ,                                                                
         LA    R8,REPRNT1                                                       
         MVC   DTBPUL(L'AC@TCPY),AC@TCPY                                        
         LA    RF,DTBPUL+L'AC@TCPY-1                                            
         LHI   R0,L'AC@TCPY-1                                                   
         BRAS  RE,RTRIM                                                         
*                                                                               
         MVI   2(RF),C'-'                                                       
         GOTO1 VHEXOUT,DMCB,REPCOMPC,4(RF),1,=C'TOG'                            
         L     RE,AREPACCS                                                      
         LA    RE,REPTABL*2(RE)                                                 
         CP    REPTOFCF-REPTABD(L'REPTOFCF,RE),PZERO                            
         BE    DRPTOTD                                                          
                                                                                
         USING ERRTABD,RF                                                       
         L     RF,AERRNEXT         NO - ADD ENTRY TO ERROR LIST                 
         MVC   ERRTCPYC,REPCOMPC                                                
         MVC   ERRTCPYN,REPCOMPN                                                
         ZAP   ERRTBALF,REPTOFCF-REPTABD(L'REPTOFCF,RE)                         
         LA    RF,ERRTABL(RF)                                                   
         ST    RF,AERRNEXT                                                      
         DROP  RF                                                               
*                                                                               
DRPTOTD  LA    R4,3                Company total                                
         B     DTBPRNT                                                          
         EJECT ,                                                                
***********************************************************************         
* Right trim                                                                    
* R0 = length                                                                   
* RF = End of string to start to trim                                           
***********************************************************************         
RTRIM    CLI   1(RF),C' '                                                       
         JNE   *+12                                                             
         CLI   0(RF),C' '                                                       
         BNER  RE                                                               
         BCTR  RF,0                                                             
         BRCT  R0,RTRIM                                                         
***********************************************************************         
*                                                                               
***********************************************************************         
DRPTOTF  NTR1  ,                                                                
         LA    R8,REPRNT1                                                       
         MVC   DTBPUL(L'AC@TFIL),AC@TFIL                                        
         MVC   MID1+13(50),SPACES                                               
         MVI   MID1,X'00'                                                       
         LA    R4,4                File total                                   
         B     DTBPRNT                                                          
***********************************************************************         
*                                                                               
***********************************************************************         
DRPTOTG  NTR1  ,                                                                
         USING ERRTABD,R2                                                       
         L     R2,AERRACCS         R2=A(LIST OF CMPYS OUT OF BALANCE)           
         CLI   ERRTABD,ERRTEOTQ                                                 
         BE    DRPTOTGX                                                         
*                                  PRINT ERROR REPORT                           
         MVI   BOXREQ,C'C'         CLOSE PREVIOUS BOX                           
         GOTO1 VPRINTER                                                         
*                                                                               
         MVI   BOXINIT,0                                                        
         MVI   BOXYORN,C'Y'                                                     
         MVC   BOXROWS,SPACES                                                   
         MVI   BOXROWS+6,C'T'                                                   
         MVI   BOXROWS+8,C'M'                                                   
         MVI   BOXROWS+99,C'B'                                                  
         MVC   BOXCOLS,SPACES                                                   
         MVI   BOXCOLS+28,C'L'                                                  
         MVI   BOXCOLS+70,C'C'                                                  
         MVI   BOXCOLS+89,C'R'                                                  
*                                                                               
         MVC   TITLE,SPACES                                                     
         MVC   TITLE+14(L'AC@LCNB),AC@LCNB                                      
         MVC   MID1,SPACES                                                      
         MVC   MID2,SPACES                                                      
         MVC   SUB1,SPACES                                                      
         MVC   SUB2,SPACES                                                      
         ZAP   LINE,=P'99'                                                      
         MVI   MID1,X'00'                                                       
         MVI   MID2,X'00'                                                       
         MVI   SUB1,X'00'                                                       
         MVC   SUB2+30(L'AC@CPYNC),AC@CPYNC                                     
         MVC   SUB2+81(L'AC@AMT),AC@AMT                                         
*                                                                               
DRPTOTG2 CLI   ERRTABD,ERRTEOTQ    TEST E-O-T                                   
         BE    DRPTOTG4                                                         
         GOTO1 VHEXOUT,DMCB,ERRTCPYC,P+30,1                                     
         MVC   P+33(L'ERRTCPYN),ERRTCPYN                                        
         CURED ERRTBALF,(14,P+73),2,MINUS=YES                                   
         GOTO1 VPRINTER                                                         
         BASR  RE,RF                                                            
         LA    R2,ERRTABL(R2)                                                   
         B     DRPTOTG2                                                         
*                                                                               
DRPTOTG4 GOTOR SENDMAIL                                                         
*                                                                               
DRPTOTGX B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
**********************************************************************          
* DAILY TRIAL BALANCE - REPORT PRINTING ROUTINES                     *          
* R4 = level at which we want to print totals                                   
*    = 1, Ledger                                                                
*    = 2, Unit                                                                  
*    = 3, Company                                                               
*    = 4, File                                                                  
**********************************************************************          
DTBPRNT  LA    R8,REPRNT1                                                       
         MVC   DTBPNAR,AC@INPF     Input file                                   
         LA    R8,REPRNT2                                                       
         MVC   DTBPNAR,AC@TODP     Today postings                               
         LA    R8,REPRNT3                                                       
         MVC   DTBPNAR,AC@TODD     Today deletes                                
         LA    R8,REPRNT4                                                       
         MVC   DTBPNAR,AC@OTPF     Output file                                  
*                                                                               
         BCTR  R4,0                TOTALS LEVEL                                 
         MHI   R4,REPTABL                                                       
         A     R4,AREPACCS                                                      
         LA    R8,REPRNT1                                                       
         LA    R2,DTBPBBF          POINT TO BAL B/F COLUMN                      
         LA    R3,4                                                             
*                                                                               
DTBPRNT2 LA    R5,4                                                             
         LR    R0,R2                                                            
DTBPRNT4 CP    0(8,R4),PZERO                                                    
         BE    DTBPRNT6                                                         
         CURED (P8,0(R4)),(18,0(R2)),2,MINUS=YES                                
DTBPRNT6 LA    R2,DTBPDR-DTBPBBF(R2)                                            
         LA    R4,8(R4)                                                         
         BCT   R5,DTBPRNT4                                                      
*                                                                               
         LR    R2,R0                                                            
         LA    R2,L'P(R2)                                                       
         BCT   R3,DTBPRNT2         Next (Input/Todays/Deletes/Output)           
*                                                                               
         LA    R2,REPRNT1                                                       
         LA    R3,4                                                             
*&&UK*&& CP    LINE,=P'57'                                                      
*&&US*&& CP    LINE,=P'53'                                                      
         BL    *+10                                                             
         ZAP   LINE,=P'99'                                                      
         L     RF,VPRINTER                                                      
         MVC   P,0(R2)                                                          
         MVC   0(L'P,R2),SPACES                                                 
         BASR  RE,RF                                                            
         LA    R2,L'P(R2)                                                       
         BCT   R3,*-18                                                          
         BASR  RE,RF                                                            
         B     EXIT                                                             
         DROP  R8                                                               
         EJECT                                                                  
**********************************************************************          
* DAILY TRIAL BALANCE - ROUTINES FOR DIVIDER PAGES                   *          
**********************************************************************          
DTBDIV   NTR1  ,                                                                
         L     R3,VREMOTEC         DEFAULT IS NOT REMOTE                        
         USING REMOTED,R3                                                       
         XC    REMOTKEY,REMOTKEY                                                
         NI    PROCSW,X'FF'-PROCREMO                                            
         GOTO1 VLDLANG,DMCB,ADAREC                                              
         MVC   DIVID,2(R1)                                                      
*                                                                               
         CLI   SAVLANG,X'FF'       TEST FIRST TIME                              
         BE    *+14                                                             
         CLC   SAVLANG,BOXLANG     TEST SAME LANGUAGE AS LAST TIME              
         BE    DTBDIV08                                                         
         MVC   SAVLANG,BOXLANG                                                  
         MVI   DMCB+0,C'L'         LIST                                         
         MVI   DMCB+1,C'L'         LOWER CASE                                   
         MVI   DMCB+2,X'06'        ACCOUNTING SYSTEM                            
         MVC   DMCB+3(1),BOXLANG      LANGUAGE                                  
         BAS   RE,DTBDIC                                                        
*                                                                               
         USING CPYRECD,R2                                                       
DTBDIV08 L     R2,ADAREC           EXTRACT VALUES FROM COMPANY RECORD           
         MVC   COMPAGY,CPYKCPY                                                  
         LA    R2,CPYRFST          First element                                
                                                                                
         USING CPYELD,R2                                                        
         SR    R0,R0                                                            
DTBDIV10 CLI   CPYEL,0                                                          
         BE    DTBDIV12                                                         
         CLI   CPYEL,CPYELQ                                                     
         BE    DTBDIV11                                                         
         IC    R0,CPYLN                                                         
         AR    R2,R0                                                            
         B     DTBDIV10                                                         
                                                                                
DTBDIV11 MVI   CMPSW,0                                                          
         TM    CPYSTAT4,CPYSOFF2                                                
         BNO   *+8                                                              
         OI    CMPSW,CMPOFF2                                                    
         NI    CPYSTAT3,X'FF'-CPYSOPBM                                          
         NI    CPYSTAT4,X'FF'-CPYSOV12                                          
         MVC   COMPALPH,CPYALPHA                                                
         MVC   COMPUID,CPYUID                                                   
         MVC   COMPLOGO,CPYLOGO                                                 
*                                                                               
         USING CTPREC,R2           R2=A(PROFILE RECORD)                         
DTBDIV12 L     R2,AIO                                                           
         XC    CTPKEY,CTPKEY                                                    
         MVI   CTPKTYP,CTPKTYPQ                                                 
         MVC   CTPKSYS(L'CTPKSYS+L'CTPKPROG),DTSYSPRG                           
         MVC   CTPKORIG,DIVID                                                   
         GOTO1 VDATAMGR,DMCB,DMREAD,CTFILE,CTPREC,CTPREC                        
         BNE   DTBDIVX                                                          
*                                                                               
         LA    R1,CTPDATA                                                       
         SR    R0,R0                                                            
         USING CTOCOD,R1                                                        
DTBDIV14 CLI   CTOCOEL,0           TEST END OF RECORD                           
         BE    DTBDIVX                                                          
         CLI   CTOCOEL,X'43'       TEST OUTPUT MODE TYPE ELEMENT                
         BE    DTBDIV16                                                         
         CLI   CTOCOEL,CTOCOELQ    TEST OUTPUT TYPE CODE ELEMENT                
         BE    DTBDIV18                                                         
         IC    R0,CTOCOLEN                                                      
         AR    R1,R0                                                            
         B     DTBDIV14                                                         
*                                                                               
DTBDIV16 CLC   SR1LIT,CTOCODE      TEST SERIES 1 (SHUTTLE)                      
         BNE   DTBDIVX                                                          
         B     DTBDIV20                                                         
DTBDIV18 CLC   REMLIT,CTOCODE      TEST REMOTE                                  
         BNE   DTBDIVX                                                          
DTBDIV20 OI    PROCSW,PROCREMO     ENABLE REMOTE PRINTING                       
         L     R3,VREMOTEC                                                      
         USING REMOTED,R3                                                       
         MVC   REMOTJID,DTSYSPRG                                                
         MVC   REMOTKEY(L'REPLIT),REPLIT                                        
         MVC   REMOTDST,DIVID                                                   
         MVI   REMOTCLS,C'M'                                                    
         MVC   REMOTABF,VPQBUFF                                                 
         MVC   REMOTADM,VDATAMGR                                                
         MVC   REMOTAOP,VPQOPEN                                                 
*                                                                               
DTBDIVX  B     EXIT                                                             
         DROP  R1,R2,R3                                                         
         EJECT                                                                  
**********************************************************************          
* Daily trial balance                                                *          
**********************************************************************          
DTBDIC   NTR1  ,                                                                
         GOTO1 VDICTATE,DMCB,,DDIN,DDOUT                                        
         MVC   TITLE+15(L'AC@DTB),AC@DTB                                        
         MVC   MID1+13(L'AC@CPY),AC@CPY                                         
         LA    RF,SUB2                                                          
         USING DTBPLIND,RF                                                      
         MVC   DTBPUL(L'AC@UNTLD),AC@UNTLD                                      
         MVC   DTBPBBF(L'AC@BALBF),AC@BALBF                                     
         MVC   DTBPPBLT(L'AC@PRSB),AC@PRSB                                      
         MVC   DTBPDR(L'AC@DRS),AC@DRS                                          
         MVC   DTBPCR(L'AC@CRS),AC@CRS                                          
         B     EXIT                                                             
         DROP  RF                                                               
         EJECT                                                                  
**********************************************************************          
* Divide                                                                        
**********************************************************************          
DIVIDE   NTR1  ,                                                                
         LR    R0,R1                                                            
         TM    PROCSW,PROCREMO                                                  
         BZ    DIVIDE2                                                          
         LTR   R0,R0                                                            
         BZ    DIVIDEX                                                          
         GOTO1 VPRINT,DMCB,SPACES,BC01                                          
         GOTO1 (RF),(R1),(L'PRTCLOSE,PRTCLOSE)                                  
         L     RF,VREMOTEC         ENSURE REMOTE PRINTING IS OFF                
         USING REMOTED,RF                                                       
         XC    REMOTKEY,REMOTKEY                                                
         DROP  RF                                                               
         B     DIVIDEX                                                          
*                                                                               
DIVIDE2  GOTO1 VGETLOGO,DMCB,DIVID,VLOGOC,VDATAMGR                              
         L     R2,VLOGOC                                                        
         USING LOGOD,R2                                                         
         MVI   LOGOTYPE,C'E'                                                    
         LTR   R0,R0                                                            
         BNZ   *+8                                                              
         MVI   LOGOTYPE,C'S'                                                    
         GOTO1 VLOGO,DMCB,LOGOD                                                 
DIVIDEX  B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
**********************************************************************          
* BALANCE CHECK REPORT - MAIN PROCESS                                *          
**********************************************************************          
                                                                                
BLCMAIN  NTR1  ,                                                                
         CLI   RECTYPE,ACRTLDG     LEDGER RECORD ?                              
         BL    EXIT                                                             
         BE    BLCM03                                                           
         TM    BALSW,BALACC        TEST MINIMUM LEVEL OF BALANCE CHECK          
         BNO   EXIT                NO, SKIP BALANCE CHECK                       
         CLI   RECTYPE,ACRTACTL    ACCOUNT LOW ?                                
         BL    EXIT                                                             
         BNE   BLCM13                                                           
                                                                                
**********************************************************************          
* PROCESS ACCOUNT & LEDGER RECORDS                                   *          
**********************************************************************          
                                                                                
BLCM03   BAS   RE,BLCOFF           CHECK OFFICE BALANCE(LAST ACCOUNT)           
         MVC   BALOFF,SPACES                                                    
         CP    BALBDR,BALTDR       LAST ACCOUNT IN BALANCE ?                    
         BNE   *+14                                                             
         CP    BALBCR,BALTCR       DR/CR EQUAL ACCOUNT BALANCE ?                
         BE    BLCM05                                                           
         MVI   BALTYP,BALBALQ      NO, PUT 'BALANCE CHECK'                      
         GOTO1 PUTBUFF,BALBDR                                                   
*                                                                               
BLCM05   TM    BALSW,BALCON        CHECK BUCKET TOTALS ?                        
         BNO   *+14                                                             
         CP    ACCBBAL,ACCABAL     BUCKET BALANCE VS. ACCOUNT BALANCE           
         BNE   BLCM07                                                           
         TM    BALSW,BALOFA        CHECK OFFICE BALANCE ?                       
         BNO   *+14                                                             
         CP    OFFABAL,ACCABAL     OFFICE BALANCE VS. ACCOUNT BALANCE           
         BNE   BLCM07                                                           
         TM    BALSW,BALOCN        CHECK OFFICE CONTRAS ?                       
         BNO   BLCM09                                                           
         CP    OFFBBAL,ACCABAL     OFFICE BUCKETS VS. ACCOUNT BALANCE           
         BE    BLCM09                                                           
*                                                                               
BLCM07   MVI   BALTYP,BALBUKQ     BUCKETS NOT EQUAL - 'BALANCE CHECK'           
         GOTO1 PUTBUFF,ACCABAL                                                  
*                                                                               
*                                 SET-UP NEW ACCOUNT                            
BLCM09   LA    R0,(3*NBUKC)       CLEAR ACCUMS                                  
         LA    R1,BALBDR                                                        
         ZAP   0(L'BALBDR,R1),PZERO                                             
         LA    R1,L'BALBDR(R1)                                                  
         BCT   R0,*-10                                                          
*                                                                               
         MVC   ACCOFF,SPACES                                                    
         MVI   ACCSW,0                                                          
         CLI   RECTYPE,ACRTLDG     LEDGER RECORD ?                              
         BNE   BLCM11                                                           
         BAS   RE,SETBCK           SET BALANCE CHECK FLAGS                      
         B     EXIT                                                             
*                                                                               
         USING ACTRECD,R2                                                       
BLCM11   NI    ACCSW,X'FF'-(ACCDEL)                                             
         TM    ACTRSTAT,ACTSDELT   ACCOUNT DELETED ?                            
         BZ    *+12                                                             
         OI    ACCSW,ACCDEL        SET - ACCOUNT IS DELETED                     
         B     EXIT                                                             
*                                                                               
         ICM   R3,15,ABALEL        SET ACCOUNT TOTALS                           
         BNZ   *+6                                                              
         DC    H'0'                                                             
         USING ABLELD,R3                                                        
         ZAP   BALBDR,ABLDR        DEBITS                                       
         ZAP   BALBCR,ABLCR        CREDITS                                      
         AP    ACCABAL,ABLFRWD     BALANCE                                      
         AP    ACCABAL,ABLDR                                                    
         SP    ACCABAL,ABLCR                                                    
         MVC   BALKEY,ACTKCULA     ACCOUNT CODE                                 
         MVC   BALNAME,RECNAME     NAME                                         
         MVC   BALOFF,SPACES                                                    
         MVC   ACCNAME,RECNAME                                                  
         XC    BALTRN,BALTRN                                                    
         B     EXIT                                                             
         DROP  R2,R3                                                            
         EJECT                                                                  
**********************************************************************          
* PROCESS OFFICE ACCOUNT RECORD                                      *          
**********************************************************************          
BLCM13   TM    ACCSW,ACCDEL        IS ACCOUNT DELETED ?                         
         BNZ   EXIT                YES, IGNORE ALL OTHER RECORDS                
         USING ACTRECD,R2                                                       
         TM    ACTRSTAT,ACTSDELT   SKIP ALL DELETED RECORDS                     
         BNZ   EXIT                                                             
         CLI   RECTYPE,ACRTOFA     OFFICE ACCOUNT                               
         BNE   BLCM15                                                           
*                                                                               
         USING OFARECD,R2                                                       
         BAS   RE,BLCOFF           CHECK PREVIOUS OFFICE/ACCOUNT                
         ICM   R3,15,ABALEL        GET OFF/ACCT BALANCE ELEMENT                 
         BNZ   *+6                                                              
         DC    H'0'                                                             
         USING ABLELD,R3                                                        
         AP    OFFABAL,ABLFRWD     SET ACCOUNT BALANCE                          
         AP    OFFABAL,ABLDR                                                    
         SP    OFFABAL,ABLCR                                                    
         ZAP   BALOBDR,ABLDR                                                    
         ZAP   BALOBCR,ABLCR                                                    
         MVC   ACCOFF,OFAKOFF      SAVE OFFICE CODE                             
         B     EXIT                                                             
         DROP  R2,R3                                                            
         EJECT                                                                  
**********************************************************************          
* PROCESS CONTRA ACCOUNT & TRANSACTION RECORDS                       *          
**********************************************************************          
         USING CHDRECD,R2                                                       
BLCM15   CLC   CHDKWRK,=C'**'      IGNORE ORDERS                                
         BE    EXIT                                                             
         CLC   CHDKCUNT(2),=C'3P'  CONTRA OF COKE SX                            
         BNE   *+8                                                              
         NI    BALSW,X'FF'-(BALCON+BALOFC)  SKIP THE EXTRA CHECKS               
*                                                                               
         TM    BALSW,BALWCK        WORKCODE IN KEY ?                            
         BO    BLCM17                                                           
         CLC   CHDKOFF,SPACES      IS THERE AN OFFICE ?                         
         BNH   BLCM17              NO,                                          
         TM    CMPSW,CMPOFF2       SHOULD THERE BE ?                            
         BNO   BLCM25              NO, THAT'S AN ERROR                          
*                                                                               
BLCM17   CLC   BALKEY,CHDKCULA     FOR THIS ACCOUNT ?                           
         BNE   BLCM25                                                           
         CLI   RECTYPE,ACRTCHDH    CONTRA HEADER RECORD ?                       
         BNE   BLCM19              NO                                           
         CLC   CHDKSPAC,SPACES     MUST BE SPACES IN THIS FIELD                 
         BNE   EXIT                                                             
         MVC   SVCHDKEY,CHDKEY     SAVE CONTRA-HEADER KEY                       
         B     EXIT                                                             
*                                                                               
BLCM19   CLC   SVCHDKEY,CHDKEY     TEST CONTRA-HEADER FOUND                     
         BE    BLCM21                                                           
         MVC   BALNAME,=CL36'**Missing Contra-Header**'                         
         MVC   BALTRN,CHDKCULC                                                  
         MVC   BALOFF,CHDKOFF                                                   
         MVI   BALTYP,BALBALQ                                                   
         GOTO1 PUTBUFF,ZEROS                                                    
         MVC   SVCHDKEY,CHDKEY     AVOID REPETITION OF SAME ERROR               
*                                                                               
BLCM21   CLI   RECTYPE,ACRTCAC     CONTRA ACCOUNT BUCKETS                       
         BNE   BLCM23                                                           
         BAS   RE,BLCMBUK                                                       
         B     EXIT                                                             
*                                                                               
         USING TRNRECD,R2                                                       
BLCM23   TM    TRNRSTAT,TRNSDRFT   SKIP DRAFT                                   
         BNZ   EXIT                                                             
         TM    TRNRSTA2,TRNSPEEL   SKIP PEELED                                  
         BNZ   EXIT                                                             
         BAS   RE,BLCMTRN                                                       
         B     EXIT                                                             
*                                                                               
BLCM25   MVC   BALTRN,TRNKCULC                                                  
         MVC   BALOFF,TRNKOFF                                                   
         MVC   BALNAME,=CL36'**Transaction out of seq.**'                       
         CLI   RECTYPE,ACRTCHDH    CONTRA HEADER ?                              
         BE    *+12                                                             
         CLI   RECTYPE,ACRTCAC                                                  
         BNE   *+10                                                             
         MVC   BALNAME,=CL36'**ContraHeader out of seq.**'                      
         MVI   BALTYP,BALBALQ                                                   
         GOTO1 PUTBUFF,ZEROS                                                    
         B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
**********************************************************************          
* BALANCE CHECK AT OFFICE LEVEL                                      *          
**********************************************************************          
BLCOFF   NTR1  ,                                                                
         CLC   ACCOFF,SPACES      ANY OFFICE ACCOUNT ?                          
         BE    BLCOFF3            NO,                                           
         TM    BALSW,BALOTR       TEST TRANSACTIONS TO OFFICE ?                 
         BNO   BLCOFF3                                                          
         CP    BALOBDR,BALOTDR    LAST OFFICE IN BALANCE ?                      
         BNE   *+14                                                             
         CP    BALOBCR,BALOTCR                                                  
         BE    BLCOFF3                                                          
         MVC   BALOFF,ACCOFF       PUT OFFICE 'BALANCE CHECK'                   
         MVI   BALTYP,BALBALQ                                                   
         GOTO1 PUTBUFF,BALOBDR                                                  
*                                                                               
BLCOFF3  LA    R0,NBUKC                                                         
         LA    R1,BALOBDR                                                       
         ZAP   0(L'BALOBDR,R1),PZERO                                            
         LA    R1,L'BALOBDR(R1)                                                 
         BCT   R0,*-10                                                          
         B     EXIT                                                             
         EJECT                                                                  
**********************************************************************          
* ADD BUCKETS AND TRANSACTIONS                                       *          
**********************************************************************          
         USING CACRECD,R2                                                       
BLCMBUK  CLC   CACKSPAC(CACRLEN-CACKSPAC),SPACES                                
         BNER  RE                  IGNORE SPECIAL BUCKETS                       
         SR    R0,R0                                                            
         LA    R3,CACRFST                                                       
         CLC   CACKOFF,SPACES                                                   
         BH    BLCMBUK1                                                         
         TM    BALSW,BALCON        NEED NON-OFFICE BUCKETS ?                    
         BNOR  RE                                                               
         LA    RF,ACCBBAL          ADD TO NON-OFFICE                            
         B     BLCMBUK2                                                         
*                                                                               
BLCMBUK1 TM    BALSW,BALOCN        NEED OFFICE BUCKETS ?                        
         BNOR  RE                                                               
         LA    RF,OFFBBAL          ADD TO OFFICE BUCKETS                        
*                                                                               
BLCMBUK2 CLI   0(R3),BUKELQ                                                     
         BE    BLCMBUK3                                                         
         CLI   0(R3),PBKELQ                                                     
         BNER  RE                                                               
         USING PBKELD,R3                                                        
         AP    0(L'OFFBBAL,RF),PBKDR                                            
         SP    0(L'OFFBBAL,RF),PBKCR                                            
         B     BLCMBUK5                                                         
*                                                                               
         USING BUKELD,R3                                                        
BLCMBUK3 AP    0(L'OFFBBAL,RF),BUKDR                                            
         SP    0(L'OFFBBAL,RF),BUKCR                                            
BLCMBUK5 IC    R0,BUKLN                                                         
         AR    R3,R0                                                            
         B     BLCMBUK2                                                         
         DROP  R2,R3                                                            
*                                                                               
         USING TRNRECD,R2                                                       
BLCMTRN  CLI   TRNRFST,TRNELQ      TEST TRANSACTION                             
         BNER  RE                                                               
         LA    R3,TRNRFST                                                       
         USING TRNELD,R3                                                        
         LA    R1,BALTDR                                                        
         TM    TRNSTAT,TRNSDR      DR OR CR                                     
         BNZ   *+8                                                              
         LA    R1,L'BALTDR(R1)                                                  
         AP    0(L'BALTDR,R1),TRNAMNT                                           
*                                                                               
         LA    R1,BALOTDR          OFFICE                                       
         TM    TRNSTAT,TRNSDR      DR OR CR                                     
         BNZ   *+8                                                              
         LA    R1,L'BALOTDR(R1)                                                 
         AP    0(L'BALOTDR,R1),TRNAMNT                                          
         BR    RE                                                               
         DROP  R2,R3                                                            
         EJECT                                                                  
**********************************************************************          
* BALANCE CHECK REPORT - FINAL PROCESS                               *          
**********************************************************************          
BLCLST   NTR1  ,                                                                
         TM    PROCSW,PROCOACC     TEST ACCOUNT OUT-OF-BALANCE                  
         BNO   BLCLST13                                                         
         MVI   RTYPE,BALBALQ       SET FOR BALANCE CHECK                        
*                                                                               
BLCLST1  XC    BALREC(BALRLNQ),BALREC                                           
         MVC   BALTYP,RTYPE        SET REPORT TYPE                              
         GOTO1 VBUFFRN,DMCB,('BUFFARDH',BUFF),(0,BALREC),COMFACS                
         CLI   4(R1),0                                                          
         BNE   BLCLST9                                                          
         CLC   BALTYP,RTYPE         CORRECT REPORT TYPE ?                       
         BNE   BLCLST9                                                          
*                                                                               
         BAS   RE,SETPRINT                                                      
         MVC   BOXROWS,SPACES                                                   
         MVI   BOXROWS+5,C'T'                                                   
         MVI   BOXROWS+8,C'M'                                                   
         MVI   BOXROWS+99,C'B'                                                  
         MVI   BOXYORN,C'Y'                                                     
         MVI   BOXINIT,0                                                        
         MVC   BOXCOLS,SPACES                                                   
         LA    RF,BOXCOLS                                                       
         USING BALPLIND,RF                                                      
         MVI   BALPBXL,C'L'                                                     
         MVI   BALPBXC1,C'C'                                                    
         MVI   BALPBXC2,C'C'                                                    
         MVI   BALPBXC3,C'C'                                                    
         MVI   BALPBXC4,C'C'                                                    
         MVI   BALPBXR,C'R'                                                     
*                                                                               
         MVC   TITLE+20(20),=C'Balance check report'                            
         MVI   MID1,X'00'                                                       
         MVI   SUB1,X'AC'                                                       
         CLI   RTYPE,BALBUKQ                                                    
         BE    BLCLST2                                                          
         LA    RF,SUB2                                                          
         MVC   BALPBLD(15),=C'Balance element'                                  
         MVC   BALPBLC(15),=C'Balance element'                                  
         MVC   BALPTRD+2(11),=C'Transaction'                                    
         MVC   BALPTRC+2(11),=C'Transaction'                                    
         LA    RF,SUB3                                                          
         MVC   BALPACC(20),=C'Account key and name'                             
         MVC   BALPBLD+8(6),=C'Debits'                                          
         MVC   BALPBLC+7(7),=C'Credits'                                         
         MVC   BALPTRD+8(6),=C'Debits'                                          
         MVC   BALPTRC+7(7),=C'Credits'                                         
         B     BLCLST3                                                          
*                                                                               
BLCLST2  LA    RF,SUB2                                                          
         MVC   BALPBLD+3(7),=C'Account'                                         
         MVC   BALPBLC+4(6),=C'Bucket'                                          
         MVC   BALPTRD(14),=C'Office/Account'                                   
         MVC   BALPTRC(13),=C'Office/Bucket'                                    
         LA    RF,SUB3                                                          
         MVC   BALPACC(20),=C'Account key and name'                             
         MVC   BALPBLD+3(7),=C'Balance'                                         
         MVC   BALPBLC+3(7),=C'Balance'                                         
         MVC   BALPTRD+3(7),=C'Balance'                                         
         MVC   BALPTRC+3(7),=C'Balance'                                         
         DROP  RF                                                               
*                                                                               
BLCLST3  LA    R8,P                                                             
         USING BALPLIND,R8                                                      
         GOTO1 VHEXOUT,DMCB,BALKEY,BALPHEX,1,=C'TOG'                            
         MVC   BALPULA,BALKEY+1                                                 
         MVC   BALPOFF,BALOFF                                                   
         MVC   BALPNAME,BALNAME                                                 
         LA    R2,BALPBLD                                                       
         LA    R4,BALBUK                                                        
         LA    R3,4                                                             
*                                                                               
BLCLST4  DS    0H                                                               
         CP    0(L'BALBDR,R4),PZERO                                             
         BE    BLCLST6                                                          
         CURED (P8,0(R4)),(15,0(R2)),2,MINUS=YES                                
*                                                                               
BLCLST6  LA    R2,BALPBLC-BALPBLD(R2)                                           
         LA    R4,L'BALBDR(R4)                                                  
         BCT   R3,BLCLST4                                                       
*                                                                               
         GOTOR BALCMAIL,DMCB,1                                                  
         GOTOR BALCMAIL,DMCB,2                                                  
*                                                                               
         GOTO1 VPRINTER                                                         
         OC    BALTRN,BALTRN       TEST TRANSACTION KEY SET                     
         BZ    BCLAST8                                                          
         GOTO1 VHEXOUT,DMCB,BALTRN,BALPHEX,1,=C'TOG'                            
         MVC   BALPULA,BALTRN+1                                                 
         GOTO1 VPRINTER                                                         
*                                                                               
BCLAST8  GOTO1 VPRINTER            DOUBLE SPACE                                 
         GOTO1 VBUFFRN,DMCB,('BUFFASEQ',BUFF),(0,BALREC),COMFACS                
         CLI   4(R1),0                                                          
         BNE   BLCLST9                                                          
         CLC   BALTYP,RTYPE         SAME TYPE ?                                 
         BE    BLCLST3                                                          
*                                                                               
BLCLST9  MVI   BOXREQ,C'C'                                                      
         GOTO1 VPRINTER                                                         
         CLI   RTYPE,BALBUKQ        ALL DONE WITH BUCKETS ?                     
         BE    BLCLSTX              YES,                                        
*                                                                               
BLCLST13 TM    PROCSW,PROCOBUK      ANY BUCKET PROBLEMS ?                       
         BNO   BLCLSTX                                                          
         MVI   RTYPE,BALBUKQ        SET FOR BUCKET REPORT                       
         B     BLCLST1                                                          
*                                                                               
BLCLSTX  DS    0H                                                               
         GOTOR BALCMAIL,DMCB,3                                                  
         B     EXIT                                                             
         DROP  R8                                                               
         EJECT                                                                  
***********************************************************************         
* SET BALANCE CHECK FLAGS                                             *         
***********************************************************************         
         USING LDGRECD,R2                                                       
SETBCK   NTR1  ,                                                                
         NI    BALSW,X'FF'-(BALALL)  TURN OFF ALL CHECKS                        
         LA    R4,LDGRFST                                                       
         SR    R0,R0                                                            
*                                                                               
SETBCK1  CLI   0(R4),LDGELQ        X'14' ELEMENT                                
         BNE   SETBCK2                                                          
         USING LDGELD,R4                                                        
         TM    LDGSTAT,LDGSCOKE    TEST COKE EXPENDITURE                        
         BNO   SETBCK3                                                          
         OI    BALSW,BALACC        ONLY CHECK ACCOUNT LEVEL                     
         B     EXIT                                                             
*                                                                               
SETBCK2  CLI   0(R4),0                                                          
         BE    SETBCK3                                                          
         IC    R0,1(R4)                                                         
         AR    R4,R0                                                            
         B     SETBCK1                                                          
*                                                                               
SETBCK3  LA    RF,LDGTAB                                                        
SETBCK4  CLI   0(RF),C' '          EOT                                          
         BE    SETBCK5                                                          
         SR    R1,R1                                                            
         CLI   1(RF),C' '                                                       
         BE    *+8                                                              
         LA    R1,1                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   LDGKUNT(0),0(RF)                                                 
         BE    SETBCK5                                                          
         LA    RF,L'LDGTAB(RF)                                                  
         B     SETBCK4                                                          
*                                                                               
SETBCK5  OC    BALSW,2(RF)                                                      
         TM    CMPSW,CMPOFF2       TEST 2 CHARACTER COMPANY                     
         BO    *+8                                                              
         NI    BALSW,X'FF'-(BALOFC)                                             
         B     EXIT                                                             
         DROP  R2,R4                                                            
         EJECT                                                                  
***********************************************************************         
* Get ledger lengths                                                  *         
***********************************************************************         
         USING LDGRECD,R2                                                       
LDGR     NTR1  ,                                                                
         XC    LEVELA(4),LEVELA                                                 
         LA    R4,LDGRFST                                                       
         SR    R0,R0                                                            
                                                                                
LDGR3    CLI   0(R4),ACLELQ        X'16' ELEMENT                                
         BE    LDGR5                                                            
         CLI   0(R4),0                                                          
         BNE   *+6                                                              
         DC    H'0'                                                             
         IC    R0,1(R4)                                                         
         AR    R4,R0                                                            
         B     LDGR3                                                            
*                                                                               
         USING ACLELD,R4                                                        
LDGR5    MVC   LEVELA,ACLVLEN      LEVEL LENGTHS                                
         MVC   LEVELB,ACLVLEN+(L'ACLVALS)                                       
         MVC   LEVELC,ACLVLEN+(L'ACLVALS*2)                                     
         MVC   LEVELD,ACLVLEN+(L'ACLVALS*3)                                     
         B     EXIT                                                             
         DROP  R2,R4                                                            
         EJECT                                                                  
***********************************************************************         
* Test levels for this account                                        *         
***********************************************************************         
         USING ACTRECD,R2                                                       
LEVL     NTR1  ,                                                                
         TM    ACTRSTA,ACTSDELT    TEST DELETED                                 
         BO    EXIT                                                             
         TM    BALSW,BALCHK                                                     
         BNO   EXIT                                                             
         LA    R1,L'ACTKACT        GET LENGTH OF ACCOUNT                        
         LA    RF,ACTKACT+L'ACTKACT-1                                           
                                                                                
LEVL3    CLI   0(RF),C' '                                                       
         BH    LEVL5                                                            
         BCTR  RF,0                                                             
         BCT   R1,LEVL3                                                         
         B     LEVL9               PRINT THE JUNK                               
                                                                                
LEVL5    LA    RF,LEVELA           GET LEVEL FOR THIS ACCOUNT                   
         LA    R0,4                                                             
         CLM   R1,1,0(RF)          GET LEDGER LEVEL                             
         BNH   LEVL7                                                            
         LA    RF,1(RF)                                                         
         BCT   R0,*-12                                                          
         B     LEVL9               PRINT THE JUNK                               
                                                                                
LEVL7    CHI   R0,4                FIRST LEVEL                                  
         BE    EXIT                                                             
         BCTR  RF,0                GET LENGTH OF HIGHER LEVEL                   
         SR    R1,R1                                                            
         IC    R1,0(RF)            LENGTH OF PREVIOUS                           
         LA    R1,2(R1)            PLUS CUL                                     
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   LASTACC(0),ACTKEY   KEY FOR PREVIOUS                             
         BE    EXIT                                                             
*                                                                               
LEVL9    MVC   BALKEY,ACTKCULA                                                  
         MVC   BALNAME,=CL36'**Missing high level acct.**'                      
         MVI   BALTYP,BALBALQ                                                   
         GOTO1 PUTBUFF,ZEROS                                                    
         B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
**********************************************************************          
* ROUTINE TO CLEAR DOWN PRINT DSECT                                  *          
**********************************************************************          
SETPRINT MVC   TITLE,SPACES                                                     
         MVC   MID1,SPACES                                                      
         MVC   MID2,SPACES                                                      
         MVC   MID3,SPACES                                                      
         MVC   MID4,SPACES                                                      
         MVC   SUB1,SPACES                                                      
         MVC   SUB2,SPACES                                                      
         MVC   SUB3,SPACES                                                      
         MVC   P,SPACES                                                         
         ZAP   LINE,=P'99'                                                      
         ZAP   PAGE,=P'1'                                                       
         BR    RE                                                               
                                                                                
**********************************************************************          
* ROUTINE TO ADD RECORD TO BUFFERIN                                  *          
**********************************************************************          
PUTBUFF  LR    R0,RE                                                            
         MVC   BALBUK,0(R1)                                                     
         CLI   BALTYP,BALBALQ                                                   
         BNE   *+8                                                              
         OI    PROCSW,PROCOACC     SET ACCOUNT OUT OF BALANCE                   
         CLI   BALTYP,BALBUKQ                                                   
         BNE   *+8                                                              
         OI    PROCSW,PROCOBUK     SET BUCKET OUT OF BALANCE                    
         GOTO1 VBUFFRN,DMCB,('BUFFAPUT',BUFF),(0,BALREC),COMFACS                
         CLI   4(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   BALNAME,ACCNAME                                                  
         XC    BALTRN,BALTRN                                                    
         LR    RE,R0                                                            
         BR    RE                                                               
                                                                                
EXIT     XIT1  ,                                                                
         EJECT                                                                  
**********************************************************************          
* Literal pool                                                                  
**********************************************************************          
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* Constants Dictionary                                                          
**********************************************************************          
DDIN     DS    0C                  DICTIONARY INPUT LIST                        
         DCDDL AC#DTB,19                                                        
         DCDDL AC#CPY,7                                                         
         DCDDL AC#UNTLD,22,L                                                    
         DCDDL AC#BALBF,14,C                                                    
         DCDDL AC#PRSB,16,C                                                     
         DCDDL AC#CRS,14,R                                                      
         DCDDL AC#DRS,14,R                                                      
         DCDDL AC#TUNT,14                                                       
         DCDDL AC#TCPY,17                                                       
         DCDDL AC#TFIL,14                                                       
         DCDDL AC#LCNB,32                                                       
         DCDDL AC#CPYNC,21                                                      
         DCDDL AC#AMT,6                                                         
         DCDDL AC#INPF,L'DTBPNAR                                                
         DCDDL AC#TODP,L'DTBPNAR                                                
         DCDDL AC#TODD,L'DTBPNAR                                                
         DCDDL AC#OTPF,L'DTBPNAR                                                
DDINX    DC    X'00'                                                            
         SPACE 2                                                                
DDOUT    DS    0C                                                               
         DSDDL PRINT=YES                                                        
         EJECT                                                                  
**********************************************************************          
* Constants and values saved between calls                                      
**********************************************************************          
DTSYSPRG DC    C'A07'              SYSTEM/PROGRAM ID                            
SR1LIT   DC    C'S1'               SHUTTLE OUPUT TYPE                           
REMLIT   DC    C'REMOTE'           REMOTE OUTPUT TYPE                           
REPLIT   DC    C'TRIAL BAL.'       FOR PQ REPORT ID                             
CONSYS   DC    C'CONTROL '                                                      
CONSYSL  DC    C'NCTFILE X'                                                     
CTFILE   DC    C'CTFILE  '                                                      
DMREAD   DC    C'DMREAD  '                                                      
DMOPEN   DC    C'DMOPEN  '                                                      
BC01     DC    C'BC01'                                                          
PRTCLOSE DC    C'CLOSE'                                                         
*                                                                               
TYPH1    DC   C'Transaction Source           Debits            Credits X        
                       Difference      Transactions'                            
*                                                                               
PZERO    DC    P'0'                                                             
RTYPE    DS    XL1                                                              
*                                                                               
BALREC   DS    0X                                                               
BALTYP   DS    XL1                 RECORD TYPE                                  
BALBALQ  EQU   1                   BALANCE CHECK                                
BALBUKQ  EQU   2                   BUCKET CHECK                                 
BALKEY   DS    CL(L'ACTKCULA)      ACCOUNT KEY                                  
BALOFF   DC    CL2'  '                                                          
BALTRN   DS    CL(L'ACTKCULA)      TRANSACTION KEY                              
BALNAME  DS    CL(L'NAMEREC)       ACCOUNT NAME                                 
BALKLNQ  EQU   *-BALREC                                                         
BALBUK   DS    XL(NBUKC*L'BALBDR)                                               
BALRLNQ  EQU   *-BALREC                                                         
*                                                                               
BALBDR   DC    PL8'0'              ACCOUNT DR                                   
BALBCR   DC    PL8'0'              ACCOUNT CR                                   
BALTDR   DC    PL8'0'              TRANSACTION DR                               
BALTCR   DC    PL8'0'              TRANSACTION CR                               
*                                                                               
BALOBDR  DC    PL8'0'              OFFICE LEVEL BUCKETS                         
BALOBCR  DC    PL8'0'              ACCOUNT CR                                   
BALOTDR  DC    PL8'0'              TRANSACTION DR                               
BALOTCR  DC    PL8'0'              TRANSACTION CR                               
*                                                                               
ACCABAL  DC    PL8'0'              ACCOUNT BALANCE                              
ACCBBAL  DC    PL8'0'              NON-OFFICE BUCKET BALANCE                    
OFFABAL  DC    PL8'0'              OFFICE ACCOUNT BALANCE                       
OFFBBAL  DC    PL8'0'              OFFICE BUCKET BALANCE                        
NBUKC    EQU   (*-ACCABAL)/L'ACCABAL                                            
*                                                                               
ZEROS    DC    (NBUKC)PL8'0'                                                    
ACCOFF   DC    CL2'  '                                                          
ACCNAME  DC    CL(L'NAMEREC)' '   ACCOUNT NAME                                  
*                                                                               
ACFORWD  DC    PL8'0'                                                           
ACBLDEB  DC    PL8'0'                                                           
ACBLCRD  DC    PL8'0'                                                           
ACCRPOS  DC    PL8'0'                                                           
ACDRPOS  DC    PL8'0'                                                           
ACCRDEL  DC    PL8'0'                                                           
ACDRDEL  DC    PL8'0'                                                           
ACCUMSL  EQU   *-ACFORWD                                                        
*                                                                               
REPPREV  DC    X'00'                                                            
REPTHIS  DC    X'00'                                                            
REPSWIT  DC    X'00'                                                            
*                                                                               
AREPACCS DC    A(REPACCS)                                                       
AERRACCS DC    A(ERRACCS)                                                       
AERRNEXT DC    A(ERRACCS)                                                       
*                                                                               
REPCOMPC DC    CL2' '                                                           
REPCOMPN DC    CL(L'NAMEREC)' '                                                 
REPUNITC DC    CL2' '                                                           
REPUNITN DC    CL(L'NAMEREC)' '                                                 
REPLEDGC DC    CL2' '                                                           
REPLEDGN DC    CL(L'NAMEREC)' '                                                 
*                                                                               
REPRNT1  DC    CL(L'P)' '                                                       
REPRNT2  DC    CL(L'P)' '                                                       
REPRNT3  DC    CL(L'P)' '                                                       
REPRNT4  DC    CL(L'P)' '                                                       
*                                                                               
TODAY    DS    XL3                 Packed date PWOS                             
ACTDATE  DC    XL2'00'             ACTIVITY DATE                                
DIVID    DC    AL2(0)              USER ID NUMBER FOR DIVIDER                   
SAVLANG  DC    XL1'FF'             SAVED LANGUAGE CODE                          
*                                                                               
BUFGREC  DS    0X                                                               
BUFGCPY  DS    XL1                 Company code                                 
BUFGULA  DS    CL14                GL account  /Account if error                
BUFERRS  DS    AL1                 Error #                                      
BUFEMGL  EQU   1                   .  Missing GLDELD                            
BUFEMTX  EQU   2                   .  Missing TRNELD                            
BUFGKEYQ EQU   *-BUFGCPY                                                        
BUFGAMTS DS    (GL#MNTHS*2)PL8     # of DR/CR pairs                             
BUFGRECQ EQU   *-BUFGCPY                                                        
*                                                                               
COMPLOGO DS    CL7                                                              
COMPALPH DS    CL2                 Company alpha                                
COMPAGY  DS    XL1                 Company binary                               
COMPUID  DS    XL2                 Company principle User id                    
         EJECT ,                                                                
***********************************************************************         
* Last level processed, current level processing, routine to run                
***********************************************************************         
DTBREPAS DS    0XL5                                                             
         DC    X'0303',AL3(DTBREP2)       Was company, is company               
         DC    X'0203',AL3(DTBREP4)       Was unit   , is company               
         DC    X'0202',AL3(DTBREP6)       Was unit   , is unit                  
         DC    X'0103',AL3(DTBREP8)       Was ledger , is company               
         DC    X'0102',AL3(DTBREP10)      Was ledger , is unit                  
         DC    X'0101',AL3(DTBREP12)      Was ledger , is ledger                
         DC    X'0104',AL3(DTBREP14)      Was ledger , is file total            
         DC    X'0204',AL3(DTBREP14)      Was Unit   , is file total            
         DC    X'0304',AL3(DTBREP14)      Was company, is file total            
         DC    X'0302',AL3(DTBREP18)      Was company, is unit                  
         DC    X'0301',AL3(DTBREP18)      Was company, is ledger                
         DC    X'0201',AL3(DTBREP18)      Was unit   , is ledger                
         DC    X'0003',AL3(DTBREP18)      Was start  , is company               
DTBREPAX DC    X'FFFF',AL3(0)                                                   
*                                                                               
CUREDIT  DC    V(CUREDIT)                                                       
VLDLANG  DC    V(LDLANG)                                                        
VSVDAHDR DC    V(SVDAHDR)                                                       
VMODE    DC    V(MODE)                                                          
VOUTPUT  DC    V(OUTPUT)                                                        
VDATEVAL DC    V(DATEVAL)                                                       
VCHOPPER DC    V(CHOPPER)                                                       
VDATAMGR DC    V(DATAMGR)                                                       
VDATCON  DC    V(DATCON)                                                        
VGETLOGO DC    V(GETLOGO)                                                       
VHEXOUT  DC    V(HEXOUT)                                                        
VLOGO    DC    V(LOGO)                                                          
VLOGOC   DC    V(LOGOC)                                                         
VPRINTER DC    V(PRINTER)                                                       
VRECTYP  DC    V(ACRECTYP)                                                      
VBOXAREA DC    V(BOXAREA)                                                       
VMASTC   DC    V(MASTC)                                                         
VCPRINT  DC    V(CPRINT)                                                        
VREMOTEC DC    V(REMOTEC)                                                       
VPRINT   DC    V(PPGPRINT)                                                      
VPQOPEN  DC    V(PQOPEN)                                                        
VPQBUFF  DC    V(PQBUFF)                                                        
VDICTATE DC    V(DICTATE)                                                       
VBUFFRN  DC    V(BUFFERIN)                                                      
VSMTP    DC    V(SMTP)             SIMPLE MAIL TRANSFER PROTOCOL                
VPRNTBL  DC    V(PRNTBL)                                                        
*                                                                               
COMFACS  DS    0F                                                               
         DC    V(DATAMGR)                                                       
*                                                                               
AIO      DC    A(IO)                                                            
*                                                                               
CMPSW    DC    AL1(0)              COMPANY SWITCH                               
CMPOFF2  EQU   X'80'               2 CHARACTER OFFICE                           
*                                                                               
ACCSW    DS    AL1(0)              ACCOUNT SWITCH                               
ACCDEL   EQU   X'80'               ACCOUNT IS DELETED                           
*                                                                               
PROCSW   DC    AL1(PROCFRST)       PROCESS SWITCH                               
PROCFRST EQU   X'FF'               FIRST TIME                                   
PROCTBAL EQU   X'80'               TRIAL BALANCE                                
PROCREMO EQU   X'40'               PRINTING IS REMOTE                           
PROCOACC EQU   X'20'               PRINT OUT-OF-BALANCE ACCOUNTS                
PROCOBUK EQU   X'10'               PRINT OUT-OF-BALANCE BUCKETS                 
*                                                                               
BALSW    DC    AL1(0)              BALANCE CHECK INDICATORS                     
BALCHK   EQU   X'80'               BALANCE CHECK REPORT                         
BALACC   EQU   X'40'               ACCOUNT DR/CR EQUAL TRANSACTONS              
BALCON   EQU   X'20'               CONTRA BUCKETS EQUAL ACCOUNT BAL.            
BALWCK   EQU   X'10'               WORKCODE IS IN KEY                           
BALOFA   EQU   X'08'               OFF/ACC DR/CR EQUAL ACCOUNT                  
BALOCN   EQU   X'04'               OFF/ACC BUCKETS EQUAL OFF/ACC BAL.           
BALOTR   EQU   X'02'               OFF/ACC DR/CR EQUAL TRANSACTIONS             
BALOFC   EQU   BALOFA+BALOCN+BALOTR                                             
BALALL   EQU   BALACC+BALCON+BALWCK+BALOFC                                      
*                                                                               
LEVELA   DC    XL1'00'             LENGTH OF LEVEL A                            
LEVELB   DC    XL1'00'             LENGTH OF LEVEL B                            
LEVELC   DC    XL1'00'             LENGTH OF LEVEL C                            
LEVELD   DC    XL1'00'             LENGTH OF LEVEL D                            
LASTACC  DC    CL15' '                                                          
*                                                                               
SVCHDKEY DS    XL(CHDKSPCS-CHDRECD) SAVE CONTRA-HEADER KEY                      
*                                                                               
GLSW     DC    X'00'               GL switch                                    
GLINIT   EQU   X'80'               .  Initialized                               
GLANY    EQU   X'40'               .  Any GLBRECD ?                             
GLCPYON  EQU   X'20'               .  Company on GL                             
GLPINIT  EQU   X'08'               .  Print initialized                         
GLMOA    DC    XL2'00'             Company GL start MOA                         
GLDATE   DC    XL3'00'             Date updated to GL unsigned packed           
GL#MNTHS EQU   6                   Six months of DR/CR pairs                    
GLMOAST  DS    (GL#MNTHS)XL2       6 months to process                          
         EJECT                                                                  
LDGTAB   DS    0XL3                                                             
         DC    C'SJ',AL1(BALACC+BALWCK)                                         
         DC    C'SN',AL1(0)                                                     
         DC    C'SM',AL1(0)                                                     
         DC    C'1J',AL1(BALACC+BALWCK)                                         
         DC    C'1R',AL1(BALACC)                                                
         DC    C'11',AL1(BALACC+BALOTR)                                         
         DC    C'12',AL1(BALACC+BALOTR)                                         
         DC    C'1C',AL1(BALACC+BALOTR)                                         
         DC    C'G ',AL1(BALACC+BALCON+BALOTR+BALOCN+BALOFA)                    
         DC    C'S ',AL1(BALACC+BALCON+BALOTR+BALOCN+BALOFA)                    
         DC    C'1 ',AL1(BALACC)                                                
         DC    C'2 ',AL1(BALACC+BALOTR)                                         
         DC    C'3 ',AL1(0)                                                     
         DC    C'  ',AL1(BALACC)                                                
*                                                                               
BUFF     BUFFD TYPE=P,                                                 X        
               KEYLEN=BALKLNQ,                                         X        
               COLUMNS=4,                                              X        
               REPCOM=NO,                                              X        
               FILE=SORTIN1                                                     
*                                                                               
REPACCS  DC    64PL8'0'                                                         
ERRACCS  DC    64XL(ERRTABL)'00'                                                
*                                                                               
IO       DS    XL1024              IO AREA                                      
         EJECT                                                                  
***********************************************************************         
* Add up by company GL posting updated imediately (non A25)                     
* R2 = Transaction key                                                          
* R3 = Transaction element                                                      
***********************************************************************         
         USING TRNRECD,R2                                                       
         USING TRNELD,R3                                                        
GLTYPE   NTR1  BASE=*,LABEL=*                                                   
         CLI   TRNKUNT,C'S'        Unit S                                       
         JNE   EXIT                                                             
         CLI   TRNKLDG,C'9'        Skip this too                                
         JE    EXIT                                                             
         LLC   R1,TRNKCPY          Get company code                             
         SHI   R1,COMPBASE         Base 1 convert (us zero for total)           
         MHI   R1,GLTYPLNQ                                                      
         LARL  R5,GLTYPTAB                                                      
         AR    R5,R1               Point to table entry to update               
                                                                                
         USING GLTYPD,R5                                                        
         LR    R4,R3                                                            
GLTYP10  CLI   0(R4),0                                                          
         JE    EXIT                Didn't find it, yikes                        
                                                                                
         USING TRSELD,R4                                                        
         CLI   0(R4),TRSELQ        X'60' Status element                         
         BNE   GLTYP12                                                          
         TM    TRSSTAT,TRSSGLUP                                                 
         JZ    EXIT                                                             
                                                                                
         USING GLDELD,R4                                                        
GLTYP12  CLI   0(R4),GLDELQ        X'63' GL element                             
         JNE   GLTYP18                                                          
         TM    GLDIND,GLDREV       Reversed?                                    
         JO    GLTYP16             Try another one                              
         CLC   GLDDATE,TODAY       Make sure today                              
         JNE   GLTYP18                                                          
         J     GLTYP20                                                          
                                                                                
GLTYP16  CLC   GLDRDTE,TODAY       MAKE SURE TODAY                              
         JE    GLTYP20                                                          
                                                                                
GLTYP18  LLC   RF,1(,R4)                                                        
         AR    R4,RF                                                            
         J     GLTYP10                                                          
*                                                                               
GLTYP20  LA    R6,GLTYPIDR         Start with these                             
         CLI   GLDPRG#,GLDAC25     A25?                                         
         JNE   *+8                                                              
         LA    R6,GLTYPODR         Swith to these                               
         TM    TRNSTAT,TRNSDR      Debit or Credit?                             
         JO    *+8                                                              
         AHI   R6,L'GLTYPIDR       Put to credit                                
         TM    GLDIND,GLDREV       REVERSED?                                    
         JO    GLTYP22                                                          
         AP    0(L'GLTYPIDR,R6),TRNAMNT                                         
         J     *+10                                                             
GLTYP22  SP    0(L'GLTYPIDR,R6),TRNAMNT                                         
                                                                                
         LA    RE,GLTYPIDR                                                      
         SR    R6,RE               Displacement in                              
         LARL  RF,GLTYPTAB                                                      
         AR    R6,RF               A(Total line)                                
         TM    GLDIND,GLDREV       REVERSED?                                    
         JO    GLTYP24                                                          
         AP    0(L'GLTYPIDR,R6),TRNAMNT                                         
         J     EXIT                                                             
                                                                                
GLTYP24  SP    0(L'GLTYPIDR,R6),TRNAMNT                                         
         J     EXIT                                                             
         DROP  R2,R3,R4,R5                                                      
                                                                                
         LTORG                                                                  
*                                                                               
COMPBASE EQU   X'3F'                                                            
         DS    0L                                                               
         DC    2CL16'**GLTYPE TABLE**'                                          
GLTYPTAB DC    ((255-COMPBASE)*4)PL8'0'                                         
         EJECT                                                                  
**********************************************************************          
* Put out SMF OUTFILE Debits/Credits by company                      *          
**********************************************************************          
         USING SMFBRECD,R2                                                      
GOSMF    NTR1  BASE=*,LABEL=*                                                   
         L     RE,=V(SMF)                                                       
         TM    0(RE),X'02'         SMF=YES                                      
         BZ    GOSMFXIT                                                         
                                                                                
         LA    R2,SMFREC                                                        
         XC    SMFREC,SMFREC                                                    
         LHI   RF,SMFBRLNQ           Length of SMF data record                  
         STH   RF,SMFBLEN                                                       
         MVC   SMFBDRS(6*L'SMFBMNY),=6PL8'0'                                    
         MVC   SMFBUSER,SPACES                                                  
         MVC   SMFBTXT,SPACES                                                   
         MVC   SMFBTXT1,SPACES                                                  
         MVC   SMFBTXT2,SPACES                                                  
         MVC   SMFBINFO,=C'BAL2'                                                
         MVI   SMFBDSPC,C' '                                                    
         MVI   SMFBTYPE,C'M'                                                    
         MVC   SMFBSRCE,=CL4'DUMP'                                              
         L     RE,VOUTPUT          Test if loading to disk                      
         TM    0(RE),DISK                                                       
         BZ    *+10                Only on dump                                 
         MVC   SMFBSRCE,=CL4'LOAD'                                              
         MVC   SMFBAGY,COMPAGY     Company hex id                               
         MVC   SMFBUID,COMPUID     Company number                               
         MVC   SMFBAGYA,COMPALPH   Company alpha                                
         MVC   SMFBTXT(L'COMPLOGO),COMPLOGO                                     
                                                                                
         USING UTLD,RE                                                          
         ICM   RE,15,=V(UTL)                                                    
         BZ    *+10                                                             
         MVC   SMFBSENO,TSYS       System number                                
         ICM   RE,15,=V(LDSENA)    From DMLDMOD entry point                     
         BZ    *+10                                                             
         MVC   SMFBSENM,0(RE)                                                   
                                                                                
                                                                                
         USING SSBOFFD,RE                                                       
         ICM   RE,15,=V(SSB)                                                    
         BZ    *+10                                                             
         MVC   SMFBDSPC,SSODSPAC   Data space value                             
         DROP  RE                                                               
                                                                                
         USING REPTABD,R4                                                       
         LHI   R4,3                Company totals                               
         BCTR  R4,0                                                             
         MHI   R4,REPTABL                                                       
         A     R4,AREPACCS                                                      
         ZAP   SMFBDRS,REPTOFDR    Output file by company debits                
         ZAP   PKL10,REPTOFCR                                                   
         MP    PKL10,=P'-1'        Account for MP and size limit                
         ZAP   SMFBCRS,PKL10       Reversed sign - output file company          
*                                                                               
         ICM   RF,15,=V(SMFOUT)                                                 
         BZ    GOSMFXIT                                                         
         SR    R0,R0                                                            
         ICM   R0,3,SMFBLEN                                                     
*DEBUG   GOTO1 VPRNTBL,SMFPARM,=C'SMFBAL',SMFREC,C'DUMP',(R0),=C'1D',  +        
               (C'P',VPRINT)                                                    
         GOTO1 (RF),SMFPARM,12,SMFREC                                           
*                                                                               
         USING GLTYPD,R5                                                        
         MVC   SMFBSRCE,=CL4'A25 ' Cheat                                        
         LLC   R5,SMFBAGY          Get hex company value                        
         SHI   R5,COMPBASE                                                      
         MHI   R5,GLTYPLNQ                                                      
         LARL  RF,GLTYPTAB                                                      
         AR    R5,RF               Point to current entry                       
         ZAP   SMFBDRS,GLTYPIDR                                                 
         ZAP   SMFBCRS,GLTYPICR                                                 
         ZAP   SMFBMNY3,GLTYPOCR                                                
         ZAP   SMFBMNY4,GLTYPOCR                                                
         SR    R0,R0                                                            
         ICM   R0,3,SMFBLEN                                                     
*DEBUG   GOTO1 VPRNTBL,SMFPARM,=C'GLBAL',SMFREC,C'DUMP',(R0),=C'1D',   +        
               (C'P',VPRINT)                                                    
         GOTO1 =V(SMFOUT),SMFPARM,12,SMFREC                                     
         DROP  R5                                                               
                                                                                
GOSMFXIT B     EXIT                                                             
                                                                                
         DS    0D                                                               
         DC    4CL8'*SMFREC*'                                                   
SMFREC   DS    XL(SMFBRECX-SMFBRECD) SMF balance record                         
         DROP  R2                                                               
         LTORG                                                                  
         EJECT ,                                                                
*********************************************************************           
* General ledger balancing                                          *           
*   Verify unit S transaction match GLBRECD buckets                 *           
*********************************************************************           
GLBAL    NTR1  BASE=*,LABEL=*                                                   
         L     RE,VMODE            Need balacne check report ?                  
         TM    0(RE),AFILE+BFILE   This is the same as BALCHK                   
         JZ    EXIT                                                             
         LTR   R1,R1                                                            
         BNZ   GLRPT               Start report                                 
         L     R2,ADAREC           A(Record)                                    
         TM    ACTRSTAT-ACTRECD(R2),ACTSDELT Deleted ?                          
         JO    EXIT                Skip deleted records.                        
         CLI   RECTYPE,ACRTGLBP    New GL bucket ?                              
         BE    GLBAL010            Yes                                          
         TM    GLSW,GLANY          Any GL records ?                             
         JZ    EXIT                No, not yet                                  
         CLI   RECTYPE,ACRTCPY     Company record                               
         BE    GLBAL100            Yes                                          
         TM    GLSW,GLCPYON        Must be on New GL                            
         JZ    EXIT                No                                           
         CLI   RECTYPE,ACRTTRN     Transactions                                 
         BE    GLBAL200            Yes                                          
         CLI   RECTYPE,ACRTTRNA    Archived transactions                        
         BE    GLBAL200            Yes                                          
         J     EXIT                                                             
                                                                                
***********************************************************************         
* Initialize if we have a single GLBRECD and process to BUFFERIN                
***********************************************************************         
         USING GLBRECD,R2                                                       
GLBAL010 TM    GLSW,GLINIT         Initialized ?                                
         BO    GLBAL020            Yes                                          
         OI    GLSW,GLINIT+GLANY   Initialized and have a GLBREC                
         GOTO1 VBUFFRN,DMCB,('BUFFAINI',GLBUFF),(0,0),COMFACS                   
         CLI   4(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                Increase VIO?                                
                                                                                
***********************************************************************         
* Set # of MOAs (based on GL#MNTHS EQU) starting with ACTDATE and               
*   Go back in time each month                                                  
***********************************************************************         
         L     RF,VDATEVAL                                                      
         MVC   WORK+6(3),14(RF)    Binary value of date                         
         GOTOR VDATCON,DMCB,(3,WORK+6),(1,WORK)                                 
         LA    R4,GL#MNTHS-1                                                    
         LA    R3,GLMOAST+(L'GLMOAST*(GL#MNTHS-1))                              
         MVC   0(L'GLMOAST,R3),WORK                                             
         SHI   R3,L'GLMOAST                                                     
GLBAL012 GOTOR VDATCON,DMCB,(X'31',WORK),(1,WORK+6),(4,0)                       
         MVC   0(L'GLMOAST,R3),WORK+6                                           
         MVC   WORK(3),WORK+6                                                   
         SHI   R3,L'GLMOAST                                                     
         BCT   R4,GLBAL012                                                      
                                                                                
GLBAL020 SR    R0,R0               Set to no deal                               
         XC    BUFGREC(BUFGKEYQ),BUFGREC                                        
         MVC   BUFGCPY,GLBKCPY     Set key                                      
         MVI   BUFGULA,C'G'                                                     
         MVC   BUFGULA+1(L'GLBKGLA),GLBKGLA                                     
         LA    RF,GL#MNTHS*2       Clear accums                                 
         LA    RE,BUFGAMTS                                                      
GLBAL022 ZAP   0(L'BUFGAMTS,RE),=P'0'                                           
         AHI   RE,L'BUFGAMTS                                                    
         BCT   RF,GLBAL022                                                      
                                                                                
         USING BUKELD,R3                                                        
         LA    R3,GLBRFST          A(First element)                             
         SR    RF,RF                                                            
GLBAL032 CLI   0(R3),EOR           End of record                                
         JE    GLBAL050                                                         
         CLI   0(R3),BUKELQ        X'45' bucket element                         
         BNE   GLBAL038                                                         
         CLC   BUKMOS,GLMOAST      Find MOA we want                             
         BL    GLBAL038                                                         
         CLC   BUKMOS,GLMOAST+(L'GLMOAST*(GL#MNTHS-1))                          
         BNH   GLBAL040                                                         
GLBAL038 IC    RF,1(,R3)                                                        
         AR    R3,RF                                                            
         B     GLBAL032                                                         
                                                                                
GLBAL040 LA    R5,BUFGAMTS         Start of DR/CR pairs                         
         LA    RE,GLMOAST                                                       
         LA    RF,GL#MNTHS                                                      
GLBAL042 CLC   BUKMOS,0(RE)                                                     
         BE    GLBAL048            Found it                                     
         AHI   R5,L'BUFGAMTS*2     Next DR/CR pair                              
         AHI   RE,L'GLMOAST        Next month                                   
         BCT   RF,GLBAL042                                                      
         B     GLBAL038            Next element                                 
                                                                                
GLBAL048 AP    0(L'BUFGAMTS,R5),BUKDR                                           
         AP    L'BUFGAMTS(L'BUFGAMTS,R5),BUKCR                                  
         LHI   R0,1                                                             
         B     GLBAL038            Next element                                 
                                                                                
GLBAL050 LTR   R0,R0                                                            
         JZ    EXIT                No record to put                             
         GOTO1 VBUFFRN,DMCB,('BUFFAPUT',GLBUFF),(0,BUFGREC),COMFACS             
         CLI   4(R1),0                                                          
         JE    EXIT                                                             
         DC    H'0'                                                             
         DROP  R2,R3                                                            
***********************************************************************         
* Process Company record. See if company is on new GL                           
***********************************************************************         
         USING CPYRECD,R2                                                       
         USING CPYELD,R3                                                        
GLBAL100 LA    R3,CPYRFST          A(First element)                             
         SR    RF,RF                                                            
         NI    GLSW,X'FF'-GLCPYON  Re-initialize                                
         XC    GLMOA,GLMOA                                                      
GLBAL102 CLI   0(R3),EOR           End of record                                
         BNE   *+6                                                              
         DC    H'00'                                                            
         CLI   0(R3),CPYELQ        X'10' Company elemenet                       
         BE    GLBAL110                                                         
         IC    RF,1(,R3)                                                        
         AR    R3,RF                                                            
         B     GLBAL102                                                         
                                                                                
GLBAL110 CLI   CPYLN,CPYLN4Q                                                    
         BL    EXIT                Not set                                      
         OC    CPYGLMOA,CPYGLMOA                                                
         JZ    EXIT                                                             
         MVC   GLMOA,CPYGLMOA      Save off date for tranactions                
         OI    GLSW,GLCPYON        Yes on new GL                                
         J     EXIT                                                             
         DROP  R2,R3                                                            
                                                                                
***********************************************************************         
* Process a transactions by subtracting from GLBREC postings by acct.           
***********************************************************************         
         USING TRNRECD,R2                                                       
         USING TRNELD,R3                                                        
GLBAL200 LA    R3,TRNRFST          A(First element)                             
         XC    BUFGREC(BUFGKEYQ),BUFGREC                                        
         CLI   TRNKUNT,C'S'                                                     
         BNE   GLBAL210                                                         
         CLC   =C'**',TRNKOFF      Ignore Purchase orders                       
         JE    EXIT                                                             
         CLI   TRNKLDG,C'9'        Ignore ledger 9                              
         JNE   GLBAL220                                                         
         J     EXIT                                                             
                                                                                
GLBAL210 CLI   TRNKUNT,C'G'                                                     
         BNE   EXIT                                                             
         CLI   0(R3),TRNELQ        X'44' Tranaction element                     
         JE    GLBAL218                                                         
         MVI   BUFERRS,BUFEMTX     Missing transaction element                  
         MVC   BUFGCPY(L'TRNKCULA),TRNKCULA                                     
         J     GLBAL260                                                         
                                                                                
GLBAL218 CLI   TRNTYPE,25          Created via AC25 or $INP 25                  
         JNE   GLBAL220            Must be $INP, so count it                    
         CLC   TRNBREF,SPACES                                                   
         JNH   EXIT                From A25                                     
                                                                                
GLBAL220 LR    R5,R3                                                            
         SR    RF,RF                                                            
GLBAL222 CLI   0(R5),EOR           Get TRSELQ first                             
         JE    EXIT                                                             
         CLI   0(R5),TRSELQ        X'60' Transaction status                     
         BE    GLBAL230                                                         
         IC    RF,1(,R5)                                                        
         AR    R5,RF                                                            
         B     GLBAL222                                                         
                                                                                
         USING TRSELD,R5                                                        
GLBAL230 CLC   TRSPMOS,GLMOA       Do we count this one ?                       
         JL    EXIT                                                             
         CLC   TRSPMOS,GLMOAST     Only for last 6 months                       
         JL    EXIT                Not within range                             
         CLC   TRSPMOS,GLMOAST+(L'GLMOAST*(GL#MNTHS-1))                         
         JH    EXIT                Not within range                             
         CLI   TRNKUNT,C'G'        Unit G                                       
         JNE   GLBAL238                                                         
         MVC   BUFGCPY(L'TRNKCULA),TRNKCULA                                     
         J     GLBAL260                                                         
                                                                                
GLBAL238 OC    TRSUPDT,TRSUPDT     If no date then skip                         
         JZ    EXIT                Not updated to GL                            
         TM    TRSSTAT,TRSSGLUP    Marked updated ?                             
         JZ    EXIT                No, so skip                                  
         TM    TRSSTAT,TRSSGLIP    Update via input                             
         JO    EXIT                Skip these, account for from unit G          
         GOTO1 VDATCON,DMCB,(2,TRSUPDT),(1,GLDATE)                              
                                                                                
         USING GLDELD,R4                                                        
         LR    R4,R3                                                            
         SR    RF,RF                                                            
GLBAL240 CLI   0(R4),EOR           End of record                                
         JNE   GLBAL246                                                         
         MVI   BUFERRS,BUFEMGL     Missing GLDELD                               
         MVC   BUFGCPY(L'TRNKCULA),TRNKCULA                                     
         J     GLBAL260                                                         
                                                                                
GLBAL246 CLI   0(R4),GLDELQ        X'63' GL posting details                     
         BNE   GLBAL248                                                         
         CLC   GLDDATE,GLDATE      TRSUPDT = GLDDATE                            
         JNE   GLBAL248                                                         
         TM    GLDIND,GLDREV       Skip reversed items                          
         JZ    GLBAL250                                                         
GLBAL248 IC    RF,1(,R4)                                                        
         AR    R4,RF                                                            
         B     GLBAL240                                                         
                                                                                
GLBAL250 MVC   BUFGCPY,TRNKCPY                                                  
         MVC   BUFGULA,GLDULA                                                   
         DROP  R4                                                               
                                                                                
GLBAL260 LA    RF,GL#MNTHS*2       Clear accums                                 
         LA    RE,BUFGAMTS                                                      
GLBAL262 ZAP   0(L'BUFGAMTS,RE),=P'0'                                           
         AHI   RE,L'BUFGAMTS                                                    
         BCT   RF,GLBAL262                                                      
                                                                                
*        CLI   BUFERRS,0           Any errors                                   
*        BNE   GLBAL310                                                         
         LA    R1,BUFGAMTS         Start of DR/CR pairs                         
         LA    RE,GLMOAST                                                       
         LA    RF,GL#MNTHS                                                      
GLBAL264 CLC   TRSPMOS,0(RE)                                                    
         BE    GLBAL300            Found it                                     
         AHI   R1,L'BUFGAMTS*2     Next DR/CR pair                              
         AHI   RE,L'GLMOAST        Next month                                   
         BCT   RF,GLBAL264                                                      
         J     EXIT                                                             
         DROP  R5                                                               
                                                                                
GLBAL300 TM    TRNSTAT,TRNSDR      Debit ?                                      
         BO    *+8                                                              
         AHI   R1,L'BUFGAMTS                                                    
         SP    0(L'BUFGAMTS,R1),TRNAMNT                                         
                                                                                
GLBAL310 GOTO1 VBUFFRN,DMCB,('BUFFAPUT',GLBUFF),(0,BUFGREC),COMFACS             
         CLI   4(R1),0                                                          
         JE    EXIT                                                             
         DC    H'0'                                                             
         DROP  R2,R3                                                            
         EJECT ,                                                                
*********************************************************************           
* Display any errors                                                            
*********************************************************************           
GLRPT    XC    BUFGREC,BUFGREC                                                  
         MVI   BYTE,BUFFARDH       Read high                                    
GLRPT100 GOTO1 VBUFFRN,DMCB,(BYTE,GLBUFF),(0,BUFGREC),COMFACS                   
         CLI   4(R1),0                                                          
         JNE   GLRPT400            End of records                               
                                                                                
         MVI   BYTE,BUFFASEQ                                                    
*        CLI   BUFERRS,0           Error found if not zero                      
*        BNE   GLRPT120                                                         
         LA    R0,GL#MNTHS                                                      
         LA    R2,BUFGAMTS                                                      
         LA    R3,GLMOAST                                                       
GLRPT110 CP    0(L'BUFGAMTS,R2),=P'0'             Debit                         
         BNE   GLRPT120                           Not in balance                
         CP    L'BUFGAMTS(L'BUFGAMTS,R2),=P'0'    Credit                        
         BNE   GLRPT120                           Not in balance                
         AHI   R2,L'BUFGAMTS*2                                                  
         AHI   R3,L'GLMOAST        Next month date                              
         BCT   R0,GLRPT110                                                      
         B     GLRPT100            Next record                                  
                                                                                
GLRPT120 TM    GLSW,GLPINIT                                                     
         BO    GLRPT140                                                         
         OI    GLSW,GLPINIT        Initialize printing                          
         BAS   RE,SETPRINT                                                      
         MVC   BOXROWS,SPACES                                                   
         MVI   BOXROWS+5,C'T'                                                   
         MVI   BOXROWS+8,C'M'                                                   
         MVI   BOXROWS+99,C'B'                                                  
         MVI   BOXYORN,C'Y'                                                     
         MVI   BOXINIT,0                                                        
         MVC   BOXCOLS,SPACES                                                   
                                                                                
         USING GLPLINED,R8                                                      
         LA    R8,BOXCOLS                                                       
         MVI   GLBXLEFT,C'L'                                                    
         MVI   GLBXCNT1,C'C'                                                    
         MVI   GLBXCNT2,C'C'                                                    
         MVI   GLBXCNT3,C'C'                                                    
         MVI   GLBXCNT4,C'C'                                                    
         MVI   GLBXCNT5,C'C'                                                    
         MVI   GLBXCNT6,C'C'                                                    
         MVI   GLBXCNT7,C'C'                                                    
         MVI   GLBXRGHT,C'R'                                                    
         MVI   MID1,X'00'                                                       
         MVI   SUB1,X'AC'                                                       
         MVC   TITLE+21(17),=C'GL not in balance'                               
*                                                                               
         LA    R8,SUB2                                                          
         LA    R0,GL#MNTHS                                                      
         MVC   GLPULA(7),=C'Account'                                            
         LA    R2,GLMOAST                                                       
         LA    R3,GLPMON1                                                       
GLRPT130 MVC   WORK(2),0(R2)                                                    
         MVI   WORK+2,1            Set day                                      
         GOTOR VDATCON,DMCB,(1,WORK),(18,(R3))                                  
         AHI   R2,L'GLMOAST                                                     
         AHI   R3,GLPMON2-GLPMON1                                               
         BCT   R0,GLRPT130                                                      
         GOTO1 VPRINTER                                                         
*                                                                               
GLRPT140 LA    R8,P                                                             
         GOTO1 VHEXOUT,DMCB,BUFGCPY,GLPHEX,1,=C'TOG'                            
         SR    RF,RF                                                            
         ICM   RF,1,BUFERRS        Any errors?                                  
         BZ    GLRPT141            No                                           
         BCTR  RF,0                                                             
         MHI   RF,GLERRLNQ                                                      
         LA    RE,GLERRS(RF)                                                    
         MVC   GLPNOTE1+1(36),0(RE) Show error                                  
         MVC   GLPULA,BUFGULA                                                   
         GOTO1 VPRINTER                                                         
*        B     GLRPT100            Next record                                  
                                                                                
GLRPT141 MVC   GLPNOTE1+1(6),=C'Debits'                                         
         MVC   GLPULA,BUFGULA      Account code                                 
         LA    R2,GLPAMT1                                                       
         LA    R3,BUFGAMTS                                                      
         LA    R4,GL#MNTHS                                                      
GLRPT142 CURED (P8,0(R3)),(12,0(R2)),2,MINUS=YES                                
         AHI   R2,GLPAMT2-GLPAMT1                                               
         AHI   R3,L'BUFGAMTS*2                                                  
         BCT   R4,GLRPT142                                                      
         GOTO1 VPRINTER                                                         
*                                                                               
         MVC   GLPNOTE1+1(7),=C'Credits'                                        
         LA    R2,GLPAMT1                                                       
         LA    R3,BUFGAMTS+L'BUFGAMTS                                           
         LA    R4,GL#MNTHS                                                      
GLRPT144 CURED (P8,0(R3)),(12,0(R2)),2,MINUS=YES                                
         AHI   R2,GLPAMT2-GLPAMT1                                               
         AHI   R3,L'BUFGAMTS*2                                                  
         BCT   R4,GLRPT144                                                      
         GOTO1 VPRINTER                                                         
         B     GLRPT100            Get next record                              
                                                                                
GLRPT400 TM    GLSW,GLPINIT        Initialize printing                          
         BZ    EXIT                                                             
         MVI   BOXREQ,C'C'                                                      
         GOTO1 VPRINTER                                                         
         J     EXIT                                                             
         DROP  R8                                                               
                                                                                
GLERRLNQ EQU   36                                                               
GLERRS   DC    CL(GLERRLNQ)'** Missing GLDELD element **'    BUFEMGL            
         DC    CL(GLERRLNQ)'** Missing TRNELD element **'    BUFEMTX            
                                                                                
GLBUFF   BUFFD TYPE=P,                                                 +        
               KEYLEN=BUFGKEYQ,                                        +        
               COLUMNS=GL#MNTHS*2,                                     +        
               REPCOM=NO,                                              +        
               FILE=GLBUFFD                                                     
                                                                                
         LTORG                                                                  
         EJECT ,                                                                
**********************************************************************          
* EMAIL balance check report                                         *          
**********************************************************************          
BALCMAIL NTR1  BASE=*,LABEL=*                                                   
         CLI   3(R1),1             INIT EMAIL                                   
         BE    BALCINIT                                                         
         CLI   3(R1),2             PASS P TO EMAIL                              
         BE    BALCPASP                                                         
         CLI   3(R1),3             FINISH EMAIL                                 
         BE    BALCFINI                                                         
         B     BALCMX                                                           
*                                                                               
*======================================================================         
BALCINIT CLI   BALCEMLI,C'Y'       EMAIL INITIATED?                             
         BE    BALCMX              YES, DON'T DO IT AGAIN                       
*                                                                               
         L     RE,=V(EID)                                                       
         SHI   RE,16               Point to EMAIL=Y/N field                     
         TM    0(RE),NO                                                         
         BO    BALCMX                                                           
*                                                                               
         L     R1,=V(EMAILFLG)                                                  
         CLI   0(R1),0                                                          
         BE    BALCM00                                                          
         MVI   BMYSMTP,YES                                                      
         B     BALCM01                                                          
*                                                                               
BALCM00  L     RE,=V(EID)          We have people to email to?                  
         L     RE,12(RE)                                                        
         MVC   BTOWHO,SPACES                                                    
         MVC   BTOWHO(70),0(RE)                                                 
         CLC   BTOWHO,SPACES       No, leave                                    
         BNH   BALCMX                                                           
*                                                                               
*-------------------------------------------                                    
         LA    R3,BATIOT           GET JOB AND STEP NAMES                       
         EXTRACT (3),'S',FIELDS=TIOT                                            
         L     R3,BATIOT                                                        
         MVC   BSUBJOB,0(R3)       JOB                                          
         MVC   BSUBSTEP,8(R3)      STEP                                         
*-------------------------------------------                                    
*                                  PRINT ERROR REPORT                           
         GOTOR VSMTP,DMCB,('SMTPAINI',BJESMAIL)                                 
         GOTOR VSMTP,DMCB,('SMTPAPRS',BTOWHO),(L'BSUBDESC,BSUBDESC)             
*                                                                               
BALCM01  GOTOR VSMTP,DMCB,('SMTPAPTL',BHEAD)                                    
         GOTOR VSMTP,DMCB,('SMTPAPTL',BHEADU)                                   
         MVI   BALCEMLI,C'Y'       EMAIL INITIATED                              
         B     BALCMX                                                           
*                                                                               
*======================================================================         
BALCPASP CLI   BALCEMLI,C'Y'       EMAIL INITIATED?                             
         BNE   BALCMX              NO, LEAVE                                    
*                                  SPLIT INTO 2 LINES                           
*                                  SMTP LIMITED TO 80 CHARS PER LINE            
*                                                                               
         MVC   BDETAIL(48),P+3     CC / ACCOUNT / OF / NAME                     
         GOTOR VSMTP,DMCB,('SMTPAPTL',BDETAIL)                                  
         MVC   BDETAIL(75),P+52    AMOUNTS                                      
         GOTOR VSMTP,DMCB,('SMTPAPTL',BDETAIL)                                  
         MVC   BDETAIL,SPACES      SKIP LINE                                    
         GOTOR VSMTP,DMCB,('SMTPAPTL',BDETAIL)                                  
         B     BALCMX                                                           
*                                                                               
*======================================================================         
BALCFINI CLI   BALCEMLI,C'Y'       EMAIL INITIATED?                             
         BNE   BALCMX              NO, LEAVE                                    
BALCM04  CLI   BMYSMTP,YES                                                      
         BNE   BALCM05                                                          
         MVI   BMYSMTP,NO                                                       
         B     BALCMX                                                           
*                                                                               
BALCM05  GOTOR VSMTP,DMCB,('SMTPASND',0)                                        
         GOTOR VSMTP,DMCB,('SMTPAEND',0) DETACH SMTP                            
         MVI   BALCEMLI,C'N'       EMAIL NOT INITIATED                          
*                                                                               
BALCMX   B     EXIT                                                             
         LTORG                                                                  
*                                                                               
BATIOT   DC    A(0)                                                             
BJESMAIL DC    CL8'JESMAIL '                                                    
BMYSMTP  DC    AL1(NO)                                                          
*                                                                               
BTOWHO   DC    CL80' '                                                          
*                                                                               
BSUBDESC DC    0CL80                                                            
BSUBJOB  DS    CL8                                                              
         DC    C'.'                                                             
BSUBSTEP DS    CL8                                                              
         DC    C' '                                                             
         DC    C'A19 BALANCE CHECK REPORT'                                      
         DC    CL(L'BSUBDESC-(*-BSUBDESC))' '                                   
*                                                                               
BHEAD    DC    0CL80                                                            
         DC    CL2'CC'             HEADING                                      
         DC    C' '                                                             
         DC    CL14'ACCOUNT'                                                    
         DC    C' '                                                             
         DC    C'OF'                                                            
         DC    C' '                                                             
         DC    CL28'ACCOUNT NAME'                                               
         DC    CL(L'BHEAD-(*-BHEAD))' '                                         
*                                                                               
BHEADU   DC    0CL80                                                            
         DC    C'--'               UNDERLINE                                    
         DC    C' '                                                             
         DC    14C'-'                                                           
         DC    C' '                                                             
         DC    C'--'                                                            
         DC    C' '                                                             
         DC    28C'-'                                                           
         DC    CL(L'BHEADU-(*-BHEADU))' '                                       
*                                                                               
BDETAIL  DC    0CL80                                                            
BCPYC    DC    CL2' '              COMPANY CODE                                 
         DC    C' '                                                             
BCPYN    DC    CL36' '             COMPANY NAME                                 
         DC    C' '                                                             
BOOBAMT  DC    CL14' '             OOB AMOUNT                                   
         DC    CL(L'BDETAIL-(*-BDETAIL))' '                                     
*                                                                               
         EJECT                                                                  
**********************************************************************          
* EMAIL ERROR REPORT                                                 *          
**********************************************************************          
SENDMAIL NTR1  BASE=*,LABEL=*                                                   
         L     RE,=V(EID)                                                       
         SHI   RE,16              Point to EMAIL=Y/N field                      
         TM    0(RE),NO                                                         
         BO    SENDMX                                                           
*                                                                               
         L     R2,AERRACCS         R2=A(LIST OF CMPYS OUT OF BALANCE)           
         USING ERRTABD,R2                                                       
*                                                                               
         L     R1,=V(EMAILFLG)                                                  
         CLI   0(R1),0                                                          
         BE    SENDM00                                                          
         MVI   MYSMTP,YES                                                       
         B     SENDM01                                                          
*                                                                               
SENDM00  L     RE,=V(EID)                                                       
         L     RE,12(RE)                                                        
         MVC   TOWHO,SPACES                                                     
         MVC   TOWHO(70),0(RE)                                                  
         CLC   TOWHO,SPACES                                                     
         BNH   SENDMX                                                           
*-------------------------------------------                                    
         LA    R3,ATIOT            GET JOB AND STEP NAMES                       
         EXTRACT (3),'S',FIELDS=TIOT                                            
         L     R3,ATIOT                                                         
         MVC   SUBJOB,0(R3)        JOB                                          
         MVC   SUBSTEP,8(R3)       STEP                                         
*-------------------------------------------                                    
*                                  PRINT ERROR REPORT                           
         GOTOR VSMTP,DMCB,('SMTPAINI',JESMAIL)                                  
         GOTOR VSMTP,DMCB,('SMTPAPRS',TOWHO),(L'SUBDESC,SUBDESC)                
*                                                                               
SENDM01  GOTOR VSMTP,DMCB,('SMTPAPTL',EHEAD)                                    
         GOTOR VSMTP,DMCB,('SMTPAPTL',EHEADU)                                   
*                                                                               
SENDM02  CLI   ERRTABD,ERRTEOTQ    TEST E-O-T                                   
         BE    SENDM04                                                          
         GOTO1 VHEXOUT,DMCB,ERRTCPYC,ECPYC,1                                    
         MVC   ECPYN,ERRTCPYN                                                   
         CURED ERRTBALF,(14,EOOBAMT),2,MINUS=YES                                
         GOTOR VSMTP,DMCB,('SMTPAPTL',EDETAIL)                                  
         LA    R2,ERRTABL(R2)                                                   
         B     SENDM02                                                          
*                                                                               
SENDM04  CLI   MYSMTP,YES                                                       
         BNE   SENDM05                                                          
         MVI   MYSMTP,NO                                                        
         B     SENDMX                                                           
*                                                                               
SENDM05  GOTOR VSMTP,DMCB,('SMTPASND',0)                                        
         GOTOR VSMTP,DMCB,('SMTPAEND',0) DETACH SMTP                            
*                                                                               
SENDMX   B     EXIT                                                             
         DROP  R2                                                               
         LTORG                                                                  
*                                                                               
ATIOT    DC    A(0)                                                             
JESMAIL  DC    CL8'JESMAIL '                                                    
MYSMTP   DC    AL1(NO)                                                          
*                                                                               
TOWHO    DC    CL80' '                                                          
*                                                                               
SUBDESC  DC    0CL80                                                            
SUBJOB   DS    CL8                                                              
         DC    C'.'                                                             
SUBSTEP  DS    CL8                                                              
         DC    C' '                                                             
         DC    C'A19 OUT OF BALANCE REPORT'                                     
         DC    CL(L'SUBDESC-(*-SUBDESC))' '                                     
*                                                                               
EHEAD    DC    0CL80                                                            
         DC    CL2'CC'             HEADING                                      
         DC    C' '                                                             
         DC    CL36'COMPANY'                                                    
         DC    C' '                                                             
         DC    CL14'       AMOUNT'                                              
         DC    CL(L'EHEAD-(*-EHEAD))' '                                         
*                                                                               
EHEADU   DC    0CL80                                                            
         DC    C'--'               UNDERLINE                                    
         DC    C' '                                                             
         DC    36C'-'                                                           
         DC    C' '                                                             
         DC    14C'-'                                                           
         DC    CL(L'EHEADU-(*-EHEADU))' '                                       
*                                                                               
EDETAIL  DC    0CL80                                                            
ECPYC    DC    CL2' '              COMPANY CODE                                 
         DC    C' '                                                             
ECPYN    DC    CL36' '             COMPANY NAME                                 
         DC    C' '                                                             
EOOBAMT  DC    CL14' '             OOB AMOUNT                                   
         DC    CL(L'EDETAIL-(*-EDETAIL))' '                                     
*                                                                               
*        DS    0D                                                               
*        DC    CL8'**SSB **'                                                    
*SB      CSECT                                                                  
*        DC    X'0000FF',X'00',XL252'00'                                        
         EJECT                                                                  
***********************************************************************         
* Local working storage                                                         
***********************************************************************         
WORKD    DSECT                     ** LOCAL WORKING STORAGE **                  
EOR      EQU   0                                                                
DUB      DS    D                                                                
DMCB     DS    6F                                                               
ADAREC   DS    A                                                                
ANAMEL   DS    A                                                                
ABALEL   DS    A                                                                
RECNAME  DS    CL(L'NAMEREC)                                                    
RECTYPE  DS    XL1                                                              
BYTE     DS    XL1                                                              
WORK     DS    XL64                                                             
SMFPARM  DS    6F                                                               
PKL10    DS    PL10                                                             
BALCEMLI DS    C                                                                
WORKX    EQU   *                                                                
***********************************************************************         
DTBPLIND DSECT                     ** DAILY TRIAL BALANCE **                    
DTBPBXL  DS    CL1                                                              
         DS    CL1                                                              
DTBPUL   DS    CL1                                                              
         DS    CL1                                                              
DTBPULN  DS    CL21                                                             
DTBPBXC1 DS    CL1                                                              
         DS    CL2                                                              
DTBPNAR  DS    CL18                                                             
DTBPBXC2 DS    CL1                                                              
         DS    CL2                                                              
DTBPBBF  DS    CL18                                                             
DTBPBXC3 DS    CL1                                                              
         DS    CL2                                                              
DTBPDR   DS    CL18                                                             
DTBPBXC4 DS    CL1                                                              
         DS    CL2                                                              
DTBPCR   DS    CL18                                                             
DTBPBXC5 DS    CL1                                                              
         DS    CL2                                                              
DTBPPBLT DS    0CL15                                                            
DTBPPBL  DS    CL18                                                             
DTBPBXR  DS    CL1                                                              
***********************************************************************         
BALPLIND DSECT                     ** BALANCE CHECK REPORT **                   
BALPBXL  DS    CL1                                                              
         DS    CL2                                                              
BALPACC  DS    CL48                                                             
         ORG   BALPACC                                                          
BALPHEX  DS    CL2                                                              
         DS    CL1                                                              
BALPULA  DS    CL14                                                             
         DS    CL1                                                              
BALPOFF  DS    CL2                                                              
         DS    CL1                                                              
BALPNAME DS    CL(L'BALPACC-(*-BALPHEX))                                        
BALPBXC1 DS    CL1                                                              
         DS    CL2                                                              
BALPBLD  DS    CL16                                                             
BALPBXC2 DS    CL1                                                              
         DS    CL2                                                              
BALPBLC  DS    CL16                                                             
BALPBXC3 DS    CL1                                                              
         DS    CL2                                                              
BALPTRD  DS    CL16                                                             
BALPBXC4 DS    CL1                                                              
         DS    CL2                                                              
BALPTRC  DS    CL16                                                             
BALPBXR  DS    CL1                                                              
***********************************************************************         
GLPLINED DSECT                     ** General ledger report **                  
GLBXLEFT DS    CL1                                                              
         DS    CL2                                                              
GLPHEX   DS    CL2                                                              
         DS    CL1                                                              
GLPULA   DS    CL14                                                             
         DS    CL1                                                              
GLBXCNT1 DS    CL1                                                              
GLPNOTE1 DS    CL8                 Debit / Credit line                          
GLBXCNT2 DS    CL1                                                              
GLPAMT1  DS    CL12                Amount month 1                               
         ORG   GLPAMT1                                                          
         DS    CL3                                                              
GLPMON1  DS    CL6                                                              
         DS    CL3                                                              
GLBXCNT3 DS    CL1                                                              
GLPAMT2  DS    CL12                Amount month 2                               
         ORG   GLPAMT2                                                          
         DS    CL3                                                              
GLPMON2  DS    CL6                                                              
         DS    CL3                                                              
GLBXCNT4 DS    CL1                                                              
         DS    CL12                Amount month 3                               
GLBXCNT5 DS    CL1                                                              
         DS    CL12                Amount month 4                               
GLBXCNT6 DS    CL1                                                              
         DS    CL12                Amount month 5                               
GLBXCNT7 DS    CL1                                                              
         DS    CL12                Amount month 6                               
GLBXRGHT DS    CL1                                                              
***********************************************************************         
ERRTABD  DSECT                     ** COMPANIES NOT IN BALANCE TABLE **         
ERRTCPYC DS    CL1                 COMPANY CODE                                 
ERRTEOTQ EQU   0                   END OF TABLE INDICATOR                       
ERRTCPYN DS    CL(L'REPCOMPN)      COMPANY NAME                                 
ERRTBALF DS    PL8                 BALANCE FORWARD                              
ERRTABL  EQU   *-ERRTABD           LENGTH OF TABLE ENTRY                        
***********************************************************************         
TYPLD    DSECT                     ** TYPE SUMMARY LINE **                      
         DS    XL((132-TYPWLNQ)/2)                                              
TYPBXL   DS    CL1                 LEFT                                         
         DS    CL1                                                              
TYPDES   DS    CL20                DESCRIPTION                                  
         DS    CL3                                                              
TYPBX1   DS    CL1                                                              
TYPDR    DS    CL16                DEBIT                                        
         DS    CL1                                                              
TYPBX2   DS    CL1                                                              
TYPCR    DS    CL16                CREDIT                                       
         DS    CL1                                                              
TYPBX3   DS    CL1                                                              
TYPDIF   DS    CL16                DIFFERENCE                                   
         DS    CL1                                                              
TYPBX4   DS    CL1                                                              
TYPNUM   DS    CL14                NUMBER                                       
TYPBXR   DS    CL1                 RIGHT                                        
TYPWLNQ  EQU   *-TYPBXL                                                         
***********************************************************************         
TTYD     DSECT                     ** TRANSACTION TYPE SUMMARY ENTRY**          
TTYDES   DS    CL20                DESCRIPTION                                  
TTYDR    DS    PL8                 DEBITS                                       
TTYCR    DS    PL8                 CREDITS                                      
TTYNUM   DS    PL8                 NUMBER                                       
TTYLNQ   EQU   *-TTYD                                                           
***********************************************************************         
* Reporting total rows                                                          
*     Row 0 is ledger  totals                                                   
*     Row 1 is unit    totals                                                   
*     Row 2 is company totals                                                   
*     Row 3 is file    totals                                                   
***********************************************************************         
REPTABD  DSECT                     ** REPORT ACCUMS TABLE **                    
REPTABW  EQU   8                                                                
REPTIFBF DS    PL(REPTABW)         Input file                                   
REPTIFDR DS    PL(REPTABW)                                                      
REPTIFCR DS    PL(REPTABW)                                                      
REPTIFCF DS    PL(REPTABW)                                                      
*                                                                               
REPTTPBF DS    PL(REPTABW)         Today postings                               
REPTTPDR DS    PL(REPTABW)                                                      
REPTTPCR DS    PL(REPTABW)                                                      
REPTTPCF DS    PL(REPTABW)                                                      
*                                                                               
REPTTDBF DS    PL(REPTABW)         Todays deletes                               
REPTTDDR DS    PL(REPTABW)                                                      
REPTTDCR DS    PL(REPTABW)                                                      
REPTTDCF DS    PL(REPTABW)                                                      
*                                                                               
REPTOFBF DS    PL(REPTABW)         Todays output                                
REPTOFDR DS    PL(REPTABW)                                                      
REPTOFCR DS    PL(REPTABW)                                                      
REPTOFCF DS    PL(REPTABW)                                                      
REPTABL  EQU   *-REPTABD                                                        
REPTABN  EQU   REPTABL/REPTABW                                                  
***********************************************************************         
FHDATED  DSECT                     FILE HEADER DATES (IN SVDAHDR)               
FHDCNT   DS    XL3                 COUNT (KEEP NON-ZERO FOR DMLDMOD)            
FHDLOD   DS    XL2                 LAST FILE LOAD DATE                          
FHDUPD   DS    XL2                 LAST UPDATE RUN DATE                         
FHDGOD   DS    XL2                 LAST SUCCESSFUL UPDATE DATE                  
         EJECT                                                                  
GLTYPD   DSECT                                                                  
GLTYPIDR DS    PL8                 Immediate update                             
GLTYPICR DS    PL8                 Immediate update                             
GLTYPODR DS    PL8                 Overnight Update (A25)                       
GLTYPOCR DS    PL8                 Overnight Update (A25)                       
GLTYPLNQ EQU   *-GLTYPD                                                         
         EJECT                                                                  
* DDDPRINT                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDDPRINT                                                       
         PRINT ON                                                               
* DDBIGBOX                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDBIGBOX                                                       
         PRINT ON                                                               
* DDLOGOD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDLOGOD                                                        
         PRINT ON                                                               
* ACRECEQUS                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACRECEQUS                                                      
         PRINT ON                                                               
* DMLDEQUS                                                                      
         PRINT OFF                                                              
       ++INCLUDE DMLDEQUS                                                       
         PRINT ON                                                               
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
* CTGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
* DDREMOTED                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDREMOTED                                                      
         PRINT ON                                                               
* ACDDEQUS                                                                      
         PRINT OFF                                                              
       ++INCLUDE ACDDEQUS                                                       
         PRINT ON                                                               
* DDBUFFD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDBUFFD                                                        
         PRINT ON                                                               
* DDLANGEQUS                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDLANGEQUS                                                     
         PRINT ON                                                               
* DDMASTD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDMASTD                                                        
         PRINT ON                                                               
* DDSMTPD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDSMTPD                                                        
         PRINT ON                                                               
* DDSMFFBAL                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDSMFFBAL                                                      
         PRINT ON                                                               
         ORG   SMFBTXT1                                                         
SMFBSENM DS    CL5                                                              
         ORG   SMFBTXT2+8                                                       
SMFBINFO DS    CL4                                                              
         ORG                                                                    
* FAUTL                                                                         
         PRINT OFF                                                              
       ++INCLUDE FAUTL                                                          
         PRINT ON                                                               
* FASSBOFF                                                                      
         PRINT OFF                                                              
SSBOFFD  DSECT                                                                  
       ++INCLUDE FASSBOFF                                                       
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'061ACLDBALC  04/11/18'                                      
         END                                                                    
