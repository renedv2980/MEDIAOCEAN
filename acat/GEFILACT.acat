*          DATA SET GEFILACT   AT LEVEL 010 AS OF 09/19/02                      
*          DATA SET GEFILACT   AT LEVEL 001 AS OF 23/08/96                      
*CATALP GEFILACT                                                                
         TITLE 'ACTION HELP OVERLAY'                                            
FILACT   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,GEFILACT,R5,R6,R7,RR=RE                                        
         USING TWAD,RA                                                          
         USING WORKD,R9                                                         
         L     R8,AGWORK                                                        
         USING GWORKD,R8                                                        
         L     RC,AOVERWRK                                                      
         USING OVERWRKD,RC                                                      
         ST    RE,BORELO                                                        
         MVC   SVPARMS,0(R1)                                                    
         ST    R1,CALLR1                                                        
*                                                                               
         SRL   RF,24                                                            
         CLM   RF,1,=AL1(ROUTSN)                                                
         BNH   *+6                 UNKNOWN ROUTINE                              
         DC    H'0'                                                             
         SLL   RF,2                                                             
         B     ROUTS(RF)                                                        
         SPACE 1                                                                
ROUTS    DS    0XL4                                                             
         B     OBJECT              OBJECT INVOKER                               
         B     INIT                INITIALIZATION CALL                          
*                                                                               
ROUTSN   EQU   (*-ROUTS)/4                                                      
         SPACE 1                                                                
***********************************************************************         
* EXITS                                                               *         
***********************************************************************         
         SPACE 1                                                                
EXITH    CLI   *,0                 SET CC HIGH                                  
         B     EXIT                                                             
EXITL    CLI   *,FF                SET CC LOW                                   
         B     EXIT                                                             
EXITOK   CR    RB,RB               SET CC EQUAL                                 
         SPACE 1                                                                
EXIT     L     R1,CALLR1           RETURN PARAMETERS                            
         MVC   0(L'SVPARMS,R1),SVPARMS                                          
         XIT1  ,                   EXIT WITH CC SET                             
         EJECT                                                                  
***********************************************************************         
* TABLE ITERATION ROUTINE  - EXPECTS RF TO HOLD A(TABLE)              *         
*                          - EXPECTS R1 TO HOLD VERB                  *         
***********************************************************************         
         SPACE 1                                                                
         USING OBJTABD,RF                                                       
ITER     CLI   OBJVERB,EOT         E.O.T.                                       
         BE    EXITOK           ** NEED TO SET HIGH IF NOT OVERRIDE             
         CLM   R1,1,OBJVERB        R1 HOLDS EQUATED VERB                        
         BE    ITER02              MATCHED                                      
         LA    RF,OBJTABL(RF)                                                   
         B     ITER                ITERATE THIS TABLE                           
*                                                                               
ITER02   ICM   RF,15,OBJADR        ROUTINE TO HANDLE THE VERB                   
         LA    R1,SVPARMS                                                       
         A     RF,BORELO                                                        
         BR    RF                                                               
         DROP  RF                                                               
         SPACE 2                                                                
***********************************************************************         
* OBJECT CONTROLLER - INTERFACES TO ALL USER OBJECTS                  *         
*                                                                     *         
* P1 HOLDS EQUATED VERB                                               *         
***********************************************************************         
         SPACE 1                                                                
OBJECT   L     R1,SVPARMS                                                       
         LA    RF,TABLEOO          KNOWN OBJECTS                                
         B     ITER                                                             
*                                                                               
TABLEOO  DS    0X                                                               
         DC    AL1(ORECH),AL1(0,0,0),AL4(EXITH)                                 
         DC    AL1(ODATA),AL1(0,0,0),AL4(DATA)                                  
         DC    AL1(OSES),AL1(0,0,0),AL4(NTRSES)                                 
         DC    AL1(OLIST),AL1(0,0,0),AL4(LIST)                                  
         DC    AL1(OSUBACT),AL1(0,0,0),AL4(EXITH)                               
         DC    AL1(OACTH),AL1(0,0,0),AL4(EXITH)                                 
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* INITIALIZATION                                                      *         
***********************************************************************         
         SPACE 1                                                                
INIT     XR    RF,RF               PROTECT RECORD FIELD                         
         ICM   RF,3,GSDSPREC                                                    
         BZ    EXITOK              ALREADY DONE IT                              
         A     RF,ATWA                                                          
         OI    FHOI-FHD(RF),FHOITR                                              
         OI    FHAT-FHD(RF),FHATPR                                              
*                                                                               
         XC    GSDSPREC,GSDSPREC                                                
         XC    GSDSPACT,GSDSPACT                                                
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT                                                         *         
*                                                                     *         
* P1 HOLDS EQUATED OBJECT IDENTIFIER                                  *         
* P2 HOLDS EQUATED DATA IDENTIFIER OR 0 IF GLOBAL DATA ACTION         *         
* P3 BYTE  0   HOLDS EQUATED DATA VERB IF P2 IS ZERO                  *         
* P3 BYTES 1-3 HOLDS EQUATED ACTION VERB                              *         
* P4 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
* P5 HOLDS A(FIELD TABLE ENTRY) OR ZERO IF P2 IS ZERO                 *         
*                                                                     *         
* FIELD DATA IS EXTRACTED/OUTPUT INTO FVIFLD                          *         
***********************************************************************         
         SPACE 1                                                                
DATA     ICM   R1,15,SVPARMS2      R1=DATA IDENTIFIER                           
         BNZ   DATA02              ACTION IS ON A DATA OBJECT                   
*                                                                               
         L     R2,SVPARMS4                                                      
         USING FDRRECD,R2                                                       
         XR    R1,R1                                                            
         IC    R1,SVPARMS3         GET GLOBAL VERB                              
         LA    RF,DTATABL          TABLE OF GLOBAL VERBS                        
         B     ITER                ITERATE TABLE                                
*                                                                               
DATA02   LA    RF,KNOWTAB          TABLE OF KNOWN OBJECTS                       
         USING KNOWTABD,RF                                                      
*                                                                               
DATA04   CLC   KNOWID,=AL2(EOT)    REACH END - NOT A KNOWN DATA TYPE            
         BE    EXITH                                                            
         CLM   R1,3,KNOWID         IS THIS A KNOWN TYPE?                        
         BE    DATA06                                                           
         LA    RF,KNOWLQ(RF)                                                    
         B     DATA04                                                           
*                                                                               
DATA06   ICM   RF,15,KNOWADD       A(KNOWN OBJECT)                              
         A     RF,BORELO           RELOCATE IT                                  
*                                                                               
         LM    R1,R2,SVPARMS3      R1 HOLDS VERB                                
         USING FDRRECD,R2          R2 HOLDS A(RECORD)                           
         BR    RF                                                               
*                                                                               
DTATABL  DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* TABLE OF KNOWN RECORD OBJECTS                                       *         
***********************************************************************         
         SPACE 1                                                                
KNOWTAB  DC    AL2(00001),AL4(RNDTA)     ACTION NAME                            
         DC    AL2(00002),AL4(HLPDTA)    ACTION HELP                            
         DC    AL2(EOT)                                                         
         SPACE 1                                                                
KNOWTABD DSECT                                                                  
KNOWID   DS    XL2                 IDENTIFIER                                   
KNOWADD  DS    AL4                 A(OBJECT)                                    
KNOWLQ   EQU   *-KNOWTABD                                                       
*                                                                               
FILACT   CSECT                                                                  
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT FOR ACTION NAME                                         *         
*                                                                     *         
* R1 HOLDS EQUATED VERB                                               *         
* R2 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
***********************************************************************         
         SPACE 1                                                                
RNDTA    LA    RF,RNTBL            TABLE OF KNOWN VERBS                         
         B     ITER                ITERATE TABLE                                
*                                                                               
RNTBL    DC    AL1(DDIS),AL1(0,0,0),AL4(DISRN)                                  
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* DISPLAY ACTION NAME                                                 *         
***********************************************************************         
         SPACE 1                                                                
DISRN    L     RF,ATLST                                                         
         USING TLSTD,RF                                                         
         MVC   FVIFLD(L'TLKRNAML),TLKRNAML                                      
         B     EXITOK                                                           
         DROP  RF                                                               
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT FOR ACTION HELP                                         *         
*                                                                     *         
* R1 HOLDS EQUATED VERB                                               *         
* R2 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
***********************************************************************         
         SPACE 1                                                                
HLPDTA   LA    RF,HLPTBL           TABLE OF KNOWN VERBS                         
         B     ITER                ITERATE TABLE                                
*                                                                               
HLPTBL   DC    AL1(DDIS),AL1(0,0,0),AL4(DISHLP)                                 
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* DISPLAY ACTION HELP                                                 *         
***********************************************************************         
         SPACE 1                                                                
DISHLP   L     RF,ATLST                                                         
         USING TLSTD,RF                                                         
         OC    TLKRHELP,TLKRHELP                                                
         BZ    EXITOK                                                           
*                                                                               
         PUSH  USING                                                            
         USING GMSGD,IOKEY                                                      
         XC    GMKEY,GMKEY                                                      
         MVI   GMKREC,GMKRECQ                                                   
         MVI   GMKTYP,GMKTSCR      SET SCREEN RECORD                            
         MVC   GMKSYS,GCOVSYS      SET UP THE SYSTEM                            
         MVC   GMKMSG,TLKRHELP     SET MESSAGE NUMBER                           
         MVC   GMKLANG,CULANG                                                   
         XI    GMKLANG,X'FF'                                                    
*                                                                               
         L     R1,=AL4(XOHI+XOGENDIR+XIO1)                                      
         GOTOX ('XIO',AGROUTS)                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   GMKEY(GMKLANG-GMKEY),IOKEYSAV                                    
         BE    *+6                                                              
         DC    H'0'                                                             
         L     R1,=AL4(XOGET+XOGENFIL+XIO1)                                     
         GOTOX ('XIO',AGROUTS)                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R1,AIO1                                                          
         LA    R3,GMFIRST(R1)                                                   
         USING GMSGEL,R3                                                        
         XR    RF,RF                                                            
         CLI   GMSGEL,GMSGELC                                                   
         BE    *+12                                                             
         IC    RF,GMSGELL                                                       
         BXH   R3,RF,*-12                                                       
         IC    RF,GMSGELL                                                       
         SH    RF,=Y(GMSGTXT-GMSGEL+1)                                          
         EX    RF,*+4                                                           
         MVC   FVIFLD(0),GMSGTXT                                                
         B     EXITOK                                                           
         POP   USING                                                            
         DROP  RF                                                               
         SPACE 2                                                                
         EJECT                                                                  
***********************************************************************         
* NTRSES OBJECT                                                       *         
*                                                                     *         
* P1 HOLDS EQUATED OBJECT                                             *         
* P2 HOLDS EQUATED VERB                                               *         
* P3 HOLDS A(INITIAL LIST KEY)                                        *         
* P4 HOLDS A(MEMORY COVERED BY FESD)                                  *         
***********************************************************************         
         SPACE 1                                                                
NTRSES   LM    R0,R3,SVPARMS                                                    
         USING SSAVD,R2                                                         
         USING FESD,R3                                                          
         LA    RF,NSSTABL                                                       
         B     ITER                                                             
*                                                                               
NSSTABL  DS    0X                                                               
         DC    AL1(SNTRIN),AL1(0,0,0),AL4(NTRIN)                                
         DC    AL1(SXITOUT),AL1(0,0,0),AL4(XITOUT)                              
         DC    AL1(EOT)                                                         
         SPACE 1                                                                
***********************************************************************         
* PROCESS PARAMETER LIST FOR NTRSES TRANSFER                          *         
***********************************************************************         
         SPACE 1                                                                
NTRIN    DS    0H                                                               
         LA    RF,FESGSSV                                                       
PS       USING GSSAVE,RF                                                        
         MVC   SSELDSP,PS.GSDSPACT                                              
         MVI   PSHROW#1,3                                                       
         MVI   PSHROW#X,23                                                      
         MVI   PSHCOL#1,0                                                       
         MVI   PSHCOL#X,79                                                      
         B     EXITOK                                                           
         DROP  PS                                                               
         SPACE 1                                                                
***********************************************************************         
* PROCESS PARAMETER LIST FOR NTRSES TRANSFER                          *         
***********************************************************************         
         SPACE 1                                                                
XITOUT   TM    SXINDS1,SXISEL      TEST SELECTION MADE                          
         BZ    XOUT02                                                           
         LA    R4,FESGSSV                                                       
         LA    R4,GSDSPACT-GSSAVE(R4)                                           
         XR    RE,RE                                                            
         ICM   RE,3,0(R4)                                                       
         LA    RE,TWAD(RE)                                                      
         USING FHD,RE                                                           
         L     RF,ATLST                                                         
         MVC   FHDA(L'TLKRNAMU),TLKRNAMU-TLSTD(RF)                              
         NI    FHII,FF-FHIIVA                                                   
         OI    FHOI,FHOITR                                                      
         OI    SXINDS1,SXIXRA      SET DON'T RESTORE ACTION                     
         B     EXITOK                                                           
         DROP  RE                                                               
*                                                                               
XOUT02   TM    SXINDS1,SXISELIN    TEST SELECT INPUT TO                         
         BZ    XOUT04                                                           
         OI    SXINDS1,SXIXRA      SET DON'T RESTORE RECORD/ACTION              
         B     EXITOK                                                           
*                                                                               
XOUT04   XC    PSHOLE,PSHOLE       RESET HOLE VALUES                            
         B     EXITOK                                                           
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* LIST OBJECT                                                         *         
*                                                                     *         
* P1 HOLDS EQUATED OBJECT                                             *         
* P2 HOLDS EQUATED VERB                                               *         
* P3 HOLDS CURRENT KEY BUILD AREA                                     *         
* P4 HOLDS PREVIOUS KEY                                               *         
***********************************************************************         
         SPACE 1                                                                
LIST     LM    R0,R3,SVPARMS                                                    
THIS     USING FRARECD,R2                                                       
         LA    RF,LISTABL                                                       
         B     ITER                                                             
*                                                                               
LISTABL  DC    AL1(LGETFRST),AL1(0,0,0),AL4(BLST)                               
         DC    AL1(LGETNEXT),AL1(0,0,0),AL4(BLST)                               
         DC    AL1(LTSARDIR),AL1(0,0,0),AL4(TSARDIR)                            
         DC    AL1(LTSARFIL),AL1(0,0,0),AL4(TSARFIL)                            
         DC    AL1(LLSTFRST),AL1(0,0,0),AL4(FTFLST)                             
         DC    AL1(LINIT),AL1(0,0,0),AL4(ILST)                                  
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* INITIALISE LIST                                                     *         
***********************************************************************         
         SPACE 1                                                                
ILST     NI    LSSTAT1,FF-(LSSENTK)                                             
         OI    LSSTAT1,LSSBALL                                                  
         OI    LSSTAT2,LSSBDR+LSSSIZE                                           
         MVC   LSDSPROW,=AL2(3)                                                 
         B     EXITOK                                                           
         SPACE 2                                                                
***********************************************************************         
* FIRST TIME FOR LIST BUILD                                           *         
***********************************************************************         
         SPACE 1                                                                
FTFLST   XC    THIS.FRAKEY,THIS.FRAKEY                                          
         MVI   THIS.FRAKMIN,FRAKMINQ                                            
         MVI   THIS.FRAKTYP,FRAKTYPQ                                            
         MVC   THIS.FRAKSYS,GCOVSYS                                             
         MVC   THIS.FRAKPRG,GCPRGNO                                             
         MVC   THIS.FRAKREC,PSREC                                               
         B     EXITOK                                                           
         SPACE 2                                                                
***********************************************************************         
* BUILD LIST                                                          *         
***********************************************************************         
         SPACE 1                                                                
K        USING FRARECD,IOKEY                                                    
BLST     MVC   K.FRAKEY,THIS.FRARECD                                            
         XR    RF,RF                                                            
         IC    RF,K.FRAKACT        INCREMENT ACTION COUNT                       
         LA    RF,1(RF)                                                         
         STC   RF,K.FRAKACT                                                     
*                                                                               
         L     R1,=AL4(XIO11+XOHIGH+XOGENDIR)                                   
         GOTOX ('XIO',AGROUTS)                                                  
         BNE   EXITL                                                            
*                                                                               
         CLC   IOKEY(FRAKACT-FRARECD),THIS.FRAKEY                               
         BNE   EXITL               NO MORE RECORDS FOR THIS PROGRAM             
*                                                                               
         XR    R1,R1               GET BEST RECORD                              
         ICM   R1,8,K.FRAKSYS                                                   
         ICM   R1,4,K.FRAKPRG                                                   
         ICM   R1,2,K.FRAKREC                                                   
         ICM   R1,1,K.FRAKACT                                                   
         GOTOX ('GETFACTN',AGROUTS),(R1)                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R4,AIO1                                                          
         LA    R4,FRAFIRST(R4)                                                  
         USING FRAELD,R4                                                        
*                                                                               
         XR    RF,RF                                                            
BLST02   CLI   FRAEL,0                                                          
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   FRAEL,FRAELQ                                                     
         BE    BLST04                                                           
         IC    RF,FRALN                                                         
         AR    R4,RF                                                            
         B     BLST02                                                           
*                                                                               
BLST04   MVC   THIS.FRAKEY(FRAKLEN),K.FRAKEY                                    
         TM    FRAINDS1,FRA1NOH                                                 
         BNO   BLST06              NOT A SELECT RECORD                          
*                                                                               
         CLI   TWASESNL,1          NESTED?                                      
         BNH   BLST                NO                                           
         CLC   FRANUM,PSACT        CAME FROM THIS ACTION?                       
         BNE   BLST                DON'T WANT SELECT RECORDS                    
*                                                                               
BLST06   TM    FRAINDS1,FRA1DDS                                                 
         BZ    BLST08                                                           
         TM    CUSTAT,CUSDDS       DDS ONLY RECORD - DDS ONLY                   
         BZ    BLST                                                             
*        B     BLST10                                                           
*                                                                               
BLST08   XC    RTHALF1,RTHALF1     TEST SECURITY                                
         OC    RTHALF1(1),FRARSEC                                               
         BNZ   *+10                                                             
         MVC   RTHALF1(1),K.FRAKREC                                             
         OC    RTHALF1+1(1),FRAASEC                                             
         BNZ   *+10                                                             
         MVC   RTHALF1+1(1),FRANUM                                              
         GOTOX ('TSTACS',AGROUTS),RTHALF1                                       
         BNE   BLST                FAILED                                       
*                                                                               
BLST10   MVC   THIS.FRARECD(FRAKLEN),IOKEY                                      
         B     EXITOK              SAVE OFF DIRECTORY RECORD                    
         DROP  THIS,K,R4                                                        
         SPACE 2                                                                
***********************************************************************         
* GET TSAR RECORD INFORMATION FROM DIRECTORY RECORD                   *         
***********************************************************************         
         SPACE 1                                                                
         USING FRARECD,R2                                                       
         USING TLSTD,R3                                                         
TSARDIR  MVC   TLKRVLEN,FRAKSTAT   SET VALIDATE LENGTH FROM STATUS AREA         
         NI    TLKRVLEN,FRAKLVAL                                                
         OC    TLKRVLEN,TLKRVLEN                                                
         BNZ   EXITOK                                                           
         MVI   TLKRVLEN,1          DEFAULT VALIDATE LENGTH IS 1                 
         B     EXITOK                                                           
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* GET TSAR RECORD INFORMATION FROM FILE RECORD                        *         
***********************************************************************         
         SPACE 1                                                                
         USING FRARECD,R2                                                       
         USING TLSTD,R3                                                         
TSARFIL  LA    RF,FRAFIRST(R2)                                                  
         USING FRAELD,RF                                                        
*                                                                               
TSAF02   CLI   FRAEL,0                                                          
         BE    EXITOK                                                           
         CLI   FRAEL,FRAELQ                                                     
         BE    TSAF04                                                           
         IC    R1,FRALN                                                         
         LA    RF,0(R1,RF)                                                      
         B     TSAF02                                                           
*                                                                               
TSAF04   MVC   TLKRHELP,FRAHELP      SET HELP NUMBER                            
         XC    TLKRNAMU,TLKRNAMU                                                
         MVI   TLKRNAMU,DD#ESCL      LEFT ALIGNED, LENGTH 8                     
         MVC   TLKRNAMU+1(L'FRADICT),FRADICT                                    
         MVI   TLKRNAMU+1+L'FRADICT,8                                           
*                                                                               
         MVC   GCDUB1,TLKRNAMU       COPY FOR LOWERCASE NAME                    
*                                                                               
         ICM   R4,15,=C'SU  '                                                   
         ICM   R4,2,GCOVSYS                                                     
         GOTO1 VDICTAT,BOPARM,(R4),TLKRNAMU                                     
         ICM   R4,4,=C'L'                                                       
         GOTO1 (RF),(R1),(R4),GCDUB1                                            
*                                                                               
         MVI   TLKRNAML,C'('       NEED (XX)YYYYYY                              
         XR    RF,RF                                                            
         ICM   RF,1,TLKRVLEN       LENGTH OF XX PORTION                         
         BCTR  RF,0                                                             
         EX    RF,*+4                                                           
         MVC   TLKRNAML+1(0),GCDUB1 MOVE IN XX PORTION                          
*                                                                               
         LA    RF,1(RF)            REAL LENGTH OF XX PORTION                    
         LA    RE,TLKRNAML+1(RF)   END OF XX PORTION                            
         MVI   0(RE),C')'                                                       
         LA    RE,1(RE)            NEXT FREE ON LINE                            
*                                                                               
         LA    R1,8                LENGTH OF XXYYYYYY                           
         SR    R1,RF               AMOUNT LEFT (YYYYYY PORTION)                 
         BNP   EXITOK              NOTHING LEFT                                 
         BCTR  R1,0                                                             
         LA    RF,GCDUB1(RF)       POINT TO YYYYYY PORTION                      
         EX    R1,*+4                                                           
         MVC   0(0,RE),0(RF)       MOVE IN YYYYYY PORTION                       
         B     EXITOK                                                           
         DROP  R2,R3,RF                                                         
         EJECT                                                                  
***********************************************************************         
* LITERALS & CONSTANTS                                                *         
***********************************************************************         
         SPACE 1                                                                
         LTORG                                                                  
FF       EQU   X'FF'                                                            
         SPACE 1                                                                
***********************************************************************         
* OVERLAY WORKING STORAGE                                             *         
***********************************************************************         
         SPACE 1                                                                
OVERWRKD DSECT                                                                  
CALLR1   DS    A                                                                
SVPARMS  DS    0XL24                                                            
SVPARMS1 DS    A                                                                
SVPARMS2 DS    A                                                                
SVPARMS3 DS    A                                                                
SVPARMS4 DS    A                                                                
SVPARMS5 DS    A                                                                
SVPARMS6 DS    A                                                                
*                                                                               
RTHALF1  DS    H                                                                
*                                                                               
*        GEFILWORK                                                              
         PRINT OFF                                                              
       ++INCLUDE GEFILWORK                                                      
         PRINT ON                                                               
*        DDDDEQUS                                                               
         PRINT OFF                                                              
       ++INCLUDE DDDDEQUS                                                       
         PRINT ON                                                               
*                                                                               
TLSTD    DSECT                                                                  
         ORG   TLKSRT                                                           
TLKRNAMU DS    CL8                 ACTION NAME UPPERCASE                        
TLKRVLEN DS    XL1                 VALIDATE LENGTH                              
TLKRNAML DS    CL10                ACTION NAME LOWERCASE                        
TLKRHELP DS    CL2                 HELP NUMBER                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'010GEFILACT  09/19/02'                                      
         END                                                                    
