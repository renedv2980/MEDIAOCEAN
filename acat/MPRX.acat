*          DATA SET MPRX       AT LEVEL 142 AS OF 05/01/02                      
*CATALP READREX                                                                 
         TITLE 'READREX- READERSHIP EXTRACT MODULE'                             
         PRINT NOGEN                                                            
READREX  CSECT                                                                  
         NMOD1 WORKL,**RDRX**                                                   
         SPACE 2                                                                
         LA    R7,2048(RB)         **2ND BASE REG**                             
         LA    R7,2048(R7)                                                      
         USING READREX+4096,R7                                                  
         USING WORKD,RC                                                         
         L     R8,0(R1)            A(GEND)                                      
         USING GEND,R8                                                          
         L     RA,4(R1)            A(CONTROL DSECT)                             
         USING RXCTLD,RA                                                        
         LA    R9,IO               POINT R9 TO SYSTEM WORK AREA                 
         AH    R9,=Y(IOLEN)                                                     
         USING SYSD,R9                                                          
*                                                                               
         OC    RFMOD,RFMOD         TEST NEED TO LOAD RFMOD                      
         BNZ   RR12                                                             
*                                                                               
         LOAD  EP=RXRFMOD          LOAD RF MOD                                  
         ST    R0,RFMOD            SAVE ADDRESS                                 
*                                                                               
RR12     DS    0H                                                               
         OC    RXCORE,RXCORE       TEST NEED TO GET CORE                        
         BNZ   RR16                                                             
*                                                                               
         GOTO1 RXCOVAIL,DMCB,C'LOOK'     SEE HOW MUCH AVAILABLE                 
         L     RE,DMCB+8                                                        
         C     RE,=A(500*1024)     NEED AT LEAST 500K                           
         BNL   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         ST    RE,DMCB+4           MINIMUM                                      
         ST    RE,DMCB+8           MAXIMUM                                      
         GOTO1 (RF),(R1),C'GET'        ACTUALLY GET CORE                        
         L     R1,DMCB+4                                                        
         MVC   0(8,R1),=C'*RXCORE*'                                             
         LA    R1,8(R1)                                                         
         ST    R1,RXCORE               SET START OF READREX CORE                
         L     RF,DMCB+8               AMOUNT OBTAINED                          
         SH    RF,=H'16'                                                        
         ST    RF,RXCORLEN                                                      
         AR    RF,R1                                                            
         ST    RF,RXCOREX              SET END                                  
         MVC   0(8,RF),=C'*RXCORX*'                                             
*                                                                               
*                                  SET DUMP END TO INCLUDE NEW CORE             
         L     R2,ATWA                                                          
         L     R2,TWAMASTC-T510FFD(R2)   A(MASTC)                               
         USING MASTD,R2                                                         
         L     RF,RXCOREX                                                       
         LA    RF,8(RF)                                                         
         ST    RF,MCDMPEND         SET NEW DUMP END                             
         DROP  R2                                                               
*                                                                               
RR16     DS    0H                                                               
         CLC   RXSURV,SVSURV       TEST CHANGE OF SURVEY                        
         BE    RR17                                                             
         XC    SVCODES,SVCODES        YES- CLEAR OTHER SAVES                    
         XC    QVPARS+8(4),QVPARS+8   CLEAR QV LIST                             
         XC    WVPARS+8(4),WVPARS+8   CLEAR WAVE TAB                            
         XC    CNPARS+8(4),CNPARS+8   CLEAR COUNT TABLE                         
         MVI   CALLNO,0                                                         
         GOTO1 =A(CORMAP),DMCB,WORKD    AND SET CORE MAP                        
*                                                                               
*                                  FOR EACH NEW CALL                            
*                                  -----------------                            
RR17     DS    0H                                                               
         ZIC   RF,CALLNO           BUMP REQUEST NUMBER                          
         LA    RF,1(RF)                                                         
         STC   RF,CALLNO                                                        
*                                                                               
         L     R1,RXNDVS           SET END OF DATA VECTOR TABLE                 
         M     R0,RXDVL                                                         
         A     R1,RXDVTAB                                                       
         ST    R1,DVTABX                                                        
*                                                                               
         XC    RXGLOBCS,RXGLOBCS   CLEAR GLOBAL COUNTS                          
*                                  CLEAR COUNT ARRAYS                           
         L     RE,INSARR                                                        
         L     RF,=A(ARRAYTL)                                                   
         XCEF                                                                   
*                                                                               
         L     R4,WVPARS+4         CLEAR WAVE STATUS                            
         ICM   R5,15,WVPARS+8      FOR BCT                                      
         BZ    RR19                                                             
         USING WVTABD,R4                                                        
*                                                                               
RR18     DS    0H                                                               
         NI    WVSTAT,X'3F'        CLEAR THIS TIME ACTIVE AND DONE              
         A     R4,WVPARS+12        ENTRY LENGTH                                 
         BCT   R5,RR18                                                          
         DROP  R4                                                               
*                                                                               
RR19     DS    0H                                                               
         L     R2,RXTRGR           TARGET RECORD                                
         USING QTGREC,R2                                                        
         MVC   RXTARG,QTGKTRG      SET THIS TARGET CODE                         
*                                                                               
         XC    RXBASE,RXBASE       MAY BE NO BASE                               
         L     R2,RXBASR           BASE RECORD                                  
         USING QTGREC,R2                                                        
         LTR   R2,R2                                                            
         BZ    *+10                                                             
         MVC   RXBASE,QTGKTRG      SET THIS BASE CODE                           
         DROP  R2                                                               
*                                                                               
         L     R6,VMPQBLK          SET FILTER LIST                              
         USING MPQBLKD,R6                                                       
         LA    RF,FILTLST                                                       
         ST    RF,MPQBFLST                                                      
         MVC   FILTLST(8),BASBV    BASE AND TARG VECTORS                        
         OI    FILTLST+4,X'80'     SET EOL                                      
*                                                                               
         GOTO1 =A(CMDSCAN),DMCB,WORKD    SCAN COMMAND LIST                      
         GOTO1 =A(BVLIST),(R1),WORKD     BUILD VEHICLE LIST                     
         XC    NWAVES,NWAVES                                                    
         XC    BWVPOP,BWVPOP                                                    
*                                                                               
         CLI   SURWMRGE,C'Y'       FOR MERGED WAVE SURVEYS                      
         BNE   RR19F                                                            
         MVC   THISWAV,=2X'FF'     SET NO WAVE                                  
         BAS   RE,PROCWAV          AND PROCESS                                  
         B     RR30                DONE                                         
*                                                                               
RR19F    DS    0H                  LOOK TO SEE IF HAVE POP FOR                  
*                                  BASE WAVE (= 1ST IN WAVE LIST)-              
*                                  IF SO- CAN SET BWVPOP AND TRY                
*                                  TO DO MOST RECENTLY DONE WAVE                
*                                  TO SAVE A WEIGHT READ- ELSE                  
*                                  JUST TO WAVES IN GIVEN ORDER                 
*                                                                               
         OC    WVPARS+8(4),WVPARS+8                                             
         BZ    RR22                NO WAVE DATA SAVED                           
         L     R4,WVPARS+4                                                      
         L     R5,WVPARS+8                                                      
         USING WVTABD,R4                                                        
         L     RF,RXWAVLST                                                      
*                                                                               
RR19J    DS    0H                                                               
         CLC   WVDATE,0(RF)                                                     
         BE    RR19K                                                            
         A     R4,WVPARS+12        NEXT ENTRY                                   
         BCT   R5,RR19J                                                         
         B     RR22                BASE WAVE NOT SAVED                          
*                                                                               
RR19K    DS    0H                                                               
         MVC   BWVPOP,WVPOP        SET BASE WAVE POP                            
*                                                                               
RR20     DS    0H                  NON-MERGED WAVE SURVEYS                      
         OC    WGTWAV,WGTWAV       TEST HAVE WGTS FOR SOME WAVE                 
         BZ    RR22                                                             
*                                  YES-DO IT FIRST TO SAVE WGT RE-READ          
         L     R4,RXWAVLST         FIND IF IN REQ'D WAVE LIST                   
         B     *+8                                                              
*                                                                               
RR21     DS    0H                                                               
         LA    R4,2(R4)                                                         
         CLI   0(R4),0             EOL                                          
         BE    RR22                NOT REQ'D THIS TIME                          
         CLC   WGTWAV,0(R4)                                                     
         BNE   RR21                                                             
*                                                                               
         MVC   THISWAV,WGTWAV      NB- WAVE WILL NOT BE                         
         BAS   RE,PROCWAV              RE-DONE LATER                            
*                                                                               
RR22     DS    0H                                                               
         L     R4,RXWAVLST         NOW DO ALL REQ'D WAVES                       
*                                                                               
RR22D    DS    0H                                                               
         CLI   0(R4),0             EOL                                          
         BE    RR22F                                                            
         MVC   THISWAV,0(R4)                                                    
         BAS   RE,PROCWAV                                                       
         LA    R4,2(R4)                                                         
         B     RR22D                                                            
*                                                                               
RR22F    DS    0H                                                               
RR30     DS    0H                  AFTER ALL WAVES PROCESSED                    
         BAS   RE,ARSET            SET ARRAY DATA AND                           
*                                  DIVIDE BY NO. OF WAVES                       
         BAS   RE,RCHPROC          DO REACH EXTENSIONS                          
         BAS   RE,SCHDPROC         EVALUATE SCHEDULE                            
         MVC   RXINS,TNINS         SET INSERT COUNT                             
*                                                                               
         MVC   SVSURV,RXSURV       SAVE VARIOUS CODES                           
         MVC   SVWAVE,THISWAV                                                   
         L     R6,VMPQBLK                                                       
         USING MPQBLKD,R6                                                       
         CLI   MPQBNOWT,C'Y'       UNLESS WGTS SUPRESSED                        
         BE    *+10                                                             
         MVC   WGTWAV,THISWAV      SET WAVE FOR WGTS IN CORE                    
*                                                                               
         CLI   RXTRCOPT,C'Y'       TEST TO 'TRACE'                              
         BNE   RR32                                                             
         GOTO1 =A(RXTRCE),DMCB,WORKD                                            
*                                                                               
RR32     DS    0H                                                               
XIT      XIT1                                                                   
         EJECT                                                                  
*        PROCWAV- PROCESS A WAVE                                                
         SPACE 2                                                                
PROCWAV  NTR1                                                                   
         L     R6,VMPQBLK                                                       
         USING MPQBLKD,R6                                                       
         XC    MPQBWVDT,MPQBWVDT   SET WAVE DATE                                
         CLI   THISWAV,X'FF'       NONE IF MERGED WAVE                          
         BE    PW3M                                                             
*                                                                               
         LA    R4,SURWAVS          LOOK FOR WAVE INFO                           
         USING SURWVD,R4                                                        
*                                                                               
PW3      DS    0H                                                               
         CLI   0(R4),X'FF'                                                      
         BNE   *+12                                                             
         MVI   RXERROR,RXWAVERR    WAVE NOT DEFINED                             
         B     PWX                                                              
*                                                                               
         CLC   THISWAV,SURWVDT                                                  
         BE    PW3D                                                             
         LA    R4,SURWVDL(R4)      NEXT ENTRY                                   
         B     PW3                                                              
*                                                                               
PW3D     DS    0H                                                               
         MVC   MPQBWVDT,THISWAV    SET WAVE INFO                                
         MVC   MPQBQVD,SURWVDN     DIR NAME                                     
         MVC   MPQBQVF,SURWVFN     FILE NAME                                    
         CLI   SURWVSOV,C' '       TEST SURVEY CODE OVERRIDE                    
         BNH   *+10                                                             
         MVC   MPQBFCD,SURWVSOV                                                 
         MVC   MPQBNRES,SURWVRC    RESP COUNT                                   
*                                                                               
PW3M     DS    0H                  FIND OR ADD WAVE TAB ENTRY                   
         XC    X,X                                                              
         LA    R4,X                                                             
         USING WVTABD,R4                                                        
         MVC   WVDATE,THISWAV                                                   
         GOTO1 BINSRCH,WVPARS,(1,WVDATE)                                        
         OC    1(3,R1),1(R1)       TEST FULL                                    
         BNZ   *+6                                                              
         DC    H'0'                WAVE TABLE FULL                              
         L     R4,0(R1)            A(ENTRY)                                     
*                                                                               
         TM    WVSTAT,X'40'        TEST DONE THIS TIME ALREADY                  
         BNZ   PWX                                                              
         OI    WVSTAT,X'C0'        SET ACTIVE AND DONE                          
*                                                                               
         NI    FILTLST,X'BF'       DE-ACTIVATE FILTERS                          
         NI    FILTLST+4,X'BF'                                                  
*                                                                               
         CLC   THISWAV,WGTWAV      TEST NEED TO READ WEIGHTS                    
         BE    PW4                 ALREADY HAVE                                 
         CLI   MPQBNOWT,C'Y'       TEST WGTS SUPRESSED                          
         BE    PW4                                                              
         MVC   MPQBWSAV,WSVAREA                                                 
         GOTO1 MPQFAC,DMCB,=C'SAVW',MPQBLKD                                     
         CLI   MPQBERR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
PW4      DS    0H                                                               
         OC    WVSAM,WVSAM         TEST HAVE WAVE COUNTS                        
         BNZ   PW4D                                                             
*                                                                               
         CLI   RXOPTS+2,C'Y'       **OPTION TO ADJUST BASE (TEST)               
         BNE   PW4B                                                             
         OC    BWVPOP,BWVPOP       ONLY FOR FIRST WAVE                          
         BNZ   PW4B                                                             
         MVC   WVPOP,=F'50000'                                                  
         MVC   WVSAM,=F'7000'                                                   
         B     PW4D                                                             
*                                                                               
PW4B     DS    0H                                                               
         MVI   MPQBNOFL,C'Y'       SUPPRESS FILTERS                             
         MVI   MPQBNOTW,C'Y'       AND COMPONENT WGTING                         
         MVC   FULL(3),=C'ALL'     COUNT ALL RESPONDENTS                        
         MVI   FULL+3,X'FF'                                                     
         GOTO1 MPQFAC,DMCB,(X'90',=C'EXEC'),MPQBLKD,(4,FULL)                    
         CLI   MPQBERR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVI   MPQBNOTW,C'N'       RESET FOR COMP WGT                           
         MVI   MPQBNOFL,C'N'       RE-ACTIVATE FILTERS                          
         MVC   WVSAM(8),MPQBSAM    SET SAMPLE AND POP FOR WAVE                  
*                                                                               
PW4D     DS    0H                                                               
         MVC   TWVPOP,WVPOP        SAVE THIS WAVE POP                           
         OC    BWVPOP,BWVPOP       SAVE POP OF BASE WAVE                        
         BNZ   *+10                                                             
         MVC   BWVPOP,WVPOP                                                     
         L     RF,NWAVES           BUMP WAVE COUNT                              
         LA    RF,1(RF)                                                         
         CH    RF,=H'16'           MAX OF 16 WAVES PER CALL                     
         BNH   *+6                                                              
         DC    H'0'                                                             
         ST    RF,NWAVES                                                        
*                                                                               
         MVC   WVBEQU,=X'8000'     SET 'FIRST WAVE'                             
         CLI   THISWAV,X'FF'       IF MERGED WAVE SURVEY                        
         BE    PW5                                                              
*                                                                               
         L     RF,RXWAVLST         ELSE- FIND THIS WAVE IN WAVE LIST            
         LR    R0,RF                                                            
         CLC   THISWAV,0(RF)       AND USE POSITION IN LIST                     
         BE    *+12                TO SET BIT MASK                              
         LA    RF,2(RF)            (NB- WAVES MAY BE PROCESSED                  
         B     *-14                     OUT OF ORDER)                           
         SR    RF,R0                                                            
         LA    RF,WVBITLST(RF)     BIT MASK                                     
         MVC   WVBEQU,0(RF)                                                     
*                                                                               
PW5      DS    0H                                                               
         ICM   R1,15,WVSAM         ADD TO TOTAL SURVEY SAMPLE                   
         A     R1,RXSURSAM                                                      
         ST    R1,RXSURSAM                                                      
         ICM   R1,15,WVPOP         AND POP                                      
         BAS   RE,EQUIV            EQUIVALENCE TO BASE WAVE                     
         A     R1,RXSURPOP                                                      
         ST    R1,RXSURPOP                                                      
*                                                                               
         CLC   RXBASE,SVBASE       IF SAME BASE                                 
         BNE   PW6                                                              
         CLC   THISWAV,SVWAVE      AND WAVE                                     
         BNE   PW6B                                                             
         MVC   MPQBSAM(8),WVBASSAM  USE SAVED BASE SAM AND POP                  
         B     PW8                                                              
*                                                                               
PW6      DS    0H                   ELSE NEED TO PROCESS BASE RECORD            
         MVC   SVBASE,RXBASE                                                    
         XC    CNPARS+8(4),CNPARS+8 CLEAR COUNT TABLE                           
         XC    PRPARS+8(4),PRPARS+8 AND PAIR COUNT TABLE                        
*                                                                               
PW6B     DS    0H                                                               
         OC    RXBASR,RXBASR        TEST HAVE BASE                              
         BNZ   PW7                                                              
         MVC   MPQBSAM(8),WVSAM     NO- USE SURVEY SAM AND POP                  
         B     PW7D                                                             
*                                                                               
PW7      DS    0H                                                               
         MVC   MPQBTGRC,RXBASR     A(BASE RECORD)                               
         MVI   MPQBNOTW,C'Y'       SUPPRESS COMP WGT                            
         MVC   MPQBTGBV,BASBV      FOR BASE BIT VECTOR                          
*                                                                               
         GOTO1 MPQFAC,DMCB,=C'TARGV',MPQBLKD                                    
         CLI   MPQBERR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         OI    FILTLST,X'40'       ACTIVATE AS FILTER                           
*                                                                               
PW7D     DS    0H                                                               
         MVC   WVBASSAM(8),MPQBSAM    SAVE COUNTS                               
         XC    SVWAVE,SVWAVE       IF NEW BASE FORCE REDO                       
*                                                                               
PW8      DS    0H                                                               
         L     R1,MPQBSAM          ADD TO BASE SAMPLE                           
         A     R1,RXBSAM                                                        
         ST    R1,RXBSAM                                                        
         L     R1,MPQBPOP          AND POP                                      
         BAS   RE,EQUIV            EQUIV TO BASE WAVE                           
         A     R1,RXBPOP                                                        
         ST    R1,RXBPOP                                                        
*                                  OF TARGET FOR WAVE                           
         OC    RXBASR,RXBASR       IF HAVE BASE                                 
         BZ    PW9                                                              
         OI    FILTLST,X'40'       ACTIVATE AS FILTER                           
*                                                                               
PW9      DS    0H                                                               
         CLC   RXTARG,SVTARG       IF SAME TARGET                               
         BNE   PW10                                                             
         CLC   THISWAV,SVWAVE      AND WAVE                                     
         BNE   PW10B                                                            
         MVC   MPQBSAM(8),WVTRGSAM   USE SAVED SAMPLE AND POP                   
         B     PW12                                                             
*                                                                               
PW10     DS    0H                   ELSE NEED TO PROCESS TARG RECORD            
         MVC   SVTARG,RXTARG                                                    
         XC    CNPARS+8(4),CNPARS+8 CLEAR COUNT TABLE                           
         XC    PRPARS+8(4),PRPARS+8 AND PAIR COUNT TABLE                        
*                                                                               
PW10B    DS    0H                                                               
         MVC   MPQBTGRC,RXTRGR      A(TARG RECORD)                              
         MVC   MPQBTGBV,TRGBV       FOR TARGET BIT VECTOR                       
         MVC   MPQBTGWV,TRGWV       FOR TARGET WGT VECTOR                       
         MVI   MPQBNOTW,C'N'        ALLOW FOR COMP WGTS                         
         MVI   MPQBNOFL,C'N'        RE-ACTIVATE FILTERING                       
*                                                                               
         GOTO1 MPQFAC,DMCB,=C'TARGV',MPQBLKD                                    
         CLI   MPQBERR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   WVTRGSAM(8),MPQBSAM SAVE COUNTS                                  
*                                                                               
PW12     DS    0H                                                               
         L     R1,MPQBSAM          ADD TO TARGET SAMPLE                         
         A     R1,RXTRGSAM                                                      
         ST    R1,RXTRGSAM                                                      
         L     R1,MPQBPOP          AND POP                                      
         BAS   RE,EQUIV            EQUIV POP TO BASE WAVE                       
         A     R1,RXTRGPOP                                                      
         ST    R1,RXTRGPOP                                                      
*                                                                               
         OI    FILTLST+4,X'40'     ACTIVATE TARGET FILTER                       
*                                                                               
*                                  VEHICLE LIST PROCESSING LOOP                 
*                                  ----------------------------                 
         L     R5,VLIST                                                         
         USING VLISTD,R5                                                        
*                                                                               
PW21     DS    0H                                                               
         CLI   0(R5),X'FF'         EOL                                          
         BE    PW30                                                             
*                                                                               
         BAS   RE,C1PROC           GET C1 COUNTS                                
         BAS   RE,BC1PROC          GET BASE C1 COUNTS                           
         BAS   RE,C2PROC           GET C2 COUNTS                                
*                                                                               
         LA    R5,VLISTDL(R5)      NEXT ENTRY                                   
         B     PW21                                                             
*                                                                               
PW30     DS    0H                                                               
         CLI   SCHEDSW,C'Y'        IF DOING SCHEDULES                           
         BE    PW32                                                             
         CLI   PNSW,C'Y'           OR ANY PARI-WISE DATA                        
         BE    PW32                                                             
         CLI   PDSW,C'Y'                                                        
         BE    PW32                                                             
         CLI   PSSW,C'Y'                                                        
         BNE   PW34                                                             
*                                                                               
PW32     DS    0H                                                               
         MVC   PRSLOT,PNARR        START OF PAIR NET ARRAY                      
         BAS   RE,PAIRPROC                                                      
*                                                                               
PW34     DS    0H                                                               
PWX      DS    0H                                                               
         B     XIT                                                              
         SPACE 3                                                                
WVBITLST DC    X'80004000200010000800040002000100'   WAVES 1-8                  
         DC    X'00800040002000100008000400020001'   WAVES 9-16                 
         EJECT                                                                  
*        C1PROC- GET C1 COUNTS                                                  
         SPACE 2                                                                
C1PROC   DS    0H                                                               
         CLI   C1SW,C'Y'           TEST C1'S NEEDED                             
         BNER  RE                                                               
         MVI   C1PRSW,C'1'         SET COUNT TYPE                               
         B     C1PROCN                                                          
         SPACE 2                                                                
*        GET BASE C1 COUNTS                                                     
*                                                                               
BC1PROC  DS    0H                                                               
         CLI   BC1SW,C'Y'                                                       
         BNER  RE                                                               
         MVI   C1PRSW,C'B'         SET COUNT TYPE                               
         NI    FILTLST+4,X'BF'     DE-ACTIVATE TARGET FILTER                    
         MVI   MPQBNOTW,C'Y'       AND TARG COMP WGTS FOR BASE COUNT            
*                                                                               
C1PROCN  NTR1                                                                   
         LA    R4,CNWORK                                                        
         XC    CNWORK,CNWORK                                                    
         USING CNTABD,R4                                                        
         USING VLISTD,R5                                                        
         L     R6,VMPQBLK                                                       
         USING MPQBLKD,R6                                                       
*                                                                               
         MVC   CNQVEH,VLQCD        Q CODE                                       
         MVC   CNTYP,C1PRSW        SET TYPE OF COUNT                            
         MVC   CNWAVE,THISWAV      SET WAVE DATE                                
*                                                                               
         BAS   RE,CNLOOK           LOOK FOR COUNTS IN CNTAB                     
         BE    C1P60               FOUND                                        
*                                                                               
*                                  COUNT NOT FOUND- NEED TO COMPUTE             
         MVI   MPQBNOFL,C'Y'       NO FILTERING (GET FULL RDR VECTOR)           
         XC    QVWORK,QVWORK       SET QVTAB ENTRY                              
         LA    R3,QVWORK                                                        
         USING QVTABD,R3                                                        
         MVC   QVVEH,VLQCD                                                      
         MVC   QVWAVE,THISWAV                                                   
*                                                                               
         CLI   SURRMETH,C'1'       READERSHIP METHOD                            
         BNE   C1P20                                                            
*                                                                               
*                                  METHOD 1 - 1 ISSUE (NON-SMRB)                
*                                  -----------------------------                
*                                                                               
         MVC   QVQTYP(2),=C'C1'    LOOK FOR C1 QSPEC                            
         BAS   RE,QVLOOK           TRY IN QVTAB                                 
         BE    *+8                 FOUND- OK                                    
         BAS   RE,QGET             ELSE GET QSPEC                               
*                                                                               
         OC    QVVECT,QVVECT       TEST VEH DEFINED FOR WAVE                    
         BZ    C1PROCX             NO- SKIP                                     
*                                                                               
         MVI   MPQBNOFL,C'N'       RE-ACTIVATE FILTERING                        
         MVI   X,9                 ACTUAL BIT VECTOR CODE                       
         MVC   X+1(4),QVVECT                                                    
         MVI   X+5,0                                                            
*                                  NB- CANT JUST XMULT BECAUSE                  
*                                  NEED TO FILTER ON TARG AND BASE              
         GOTO1 MPQFAC,DMCB,(X'10',=C'EXEC'),MPQBLKD,X,0                         
         CLI   MPQBERR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   CNSAM(8),MPQBSAM    SAMPLE AND POP                               
         BAS   RE,CNSET            SET IN COUNT TABLE                           
         B     C1P60                                                            
*                                                                               
*                                                                               
*                                  METHOD 2 - 2 ISSUE (SMRB)                    
*                                  -------------------------                    
C1P20    DS    0H                                                               
         MVC   QVQTYP(2),=C'I1'    LOOK FOR ISSUE 1 COUNT                       
         BAS   RE,QVLOOK           LOOK IN QVTAB                                
         BE    *+8                 FOUND- OK                                    
         BAS   RE,QGET             ELSE READ FOR QSPEC                          
*                                                                               
         OC    QVVECT,QVVECT       TEST VEH DEFINED FOR WAVE                    
         BZ    C1PROCX             NO- SKIP                                     
         MVC   WVECT1,QVVECT       SET ISSUE 1 VECTOR                           
*                                                                               
         LA    R3,QVWORK                                                        
         MVI   QVQTYP+1,C'2'       ISSUE 2 COUNT                                
         BAS   RE,QVLOOK                                                        
         BE    *+8                                                              
         BAS   RE,QGET                                                          
         MVC   WVECT2,QVVECT       ISSUE 2 VECTOR                               
*                                                                               
         MVI   MPQBNOFL,C'N'       RE-ACTIVATE FILTERING                        
*                                  SET TO COUNT ISSUE 1 VECTOR                  
         MVI   X,9                 ACTUAL BIT VECTOR CODE                       
         MVC   X+1(4),WVECT1                                                    
         MVI   X+5,0                                                            
*                                  NB- CANT JUST DO XMULT BECAUSE               
*                                  NEED TO FILTER ON TARG AND BASE              
         GOTO1 MPQFAC,DMCB,(X'10',=C'EXEC'),MPQBLKD,X,0                         
         CLI   MPQBERR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   DUB,MPQBSAM                                                      
*                                                                               
         MVC   X+1(4),WVECT2       ISSUE 2 VECTOR                               
*                                                                               
         GOTO1 (RF),(R1)                                                        
         CLI   MPQBERR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LM    RE,RF,MPQBSAM       ISSUE 2 COUNTS                               
         A     RE,DUB              PLUS ISSUE 1 SAM                             
         LA    RE,1(RE)                                                         
         SRL   RE,1                /2                                           
         STCM  RE,15,CNSAM         = C1 SAM                                     
         A     RF,DUB+4            PLUS ISSUE 1 POP                             
         LA    RF,1(RF)                                                         
         SRL   RF,1                /2                                           
         STCM  RF,15,CNPOP         = C1 POP                                     
         BAS   RE,CNSET            SET IN COUNT TABLE                           
*                                                                               
C1P60    DS    0H                  HAVE C1 COUNTS FOR Q CODE                    
         CLI   C1PRSW,C'1'         WHEN DOING TARG C1'S                         
         BNE   C1P61                                                            
         OC    VLWVBLST,WVBEQU     SET VEH DONE FOR THIS WAVE                   
         ZIC   RF,VLNWVS           BUMP VEH WAVE COUNT                          
         LA    RF,1(RF)                                                         
         STC   RF,VLNWVS                                                        
*                                                                               
C1P61    DS    0H                                                               
         MVC   DUB,CNSAM           HOLD SAMPLE AND POP                          
         L     R1,DUB+4            POP                                          
*                                                                               
         ICM   RF,15,ADJDSP        APPLY ADJUSTMENT                             
         BZ    C1P64               NONE                                         
         ICM   RE,15,VLDVCT                                                     
         AR    RE,RF                                                            
         ICM   R2,15,0(RE)         ADJUSTMENT VALUE IN R2                       
         BZ    C1P64               ZERO = NO ADJUSTMENT                         
         MR    R0,R2                                                            
         LA    RF,1000             **ASSUME .000 ADJ PRECISION**                
         BAS   RE,DIV                                                           
*                                                                               
C1P64    DS    0H                                                               
         BAS   RE,EQUIV            EQUIV TO BASE WAVE (IN R1)                   
         CLI   C1PRSW,C'1'         IF DOING AUDIENCE C1'S                       
         BNE   C1P66                                                            
         ICM   RF,15,VLC1          ADD TO C1 POP IN VLIST                       
         AR    RF,R1                                                            
         STCM  RF,15,VLC1                                                       
         STCM  R1,15,VLC1W         AND SET CURRENT WAVE C1                      
         ICM   RF,15,VLC1SAM       ALSO CUME C1 SAMPLE                          
         A     RF,DUB                                                           
         STCM  RF,15,VLC1SAM                                                    
         B     C1P70                                                            
*                                                                               
C1P66    DS    0H                                                               
         ICM   RF,15,VLBC1         ADD TO BASE C1 POP IN VLIST                  
         AR    RF,R1                                                            
         STCM  RF,15,VLBC1                                                      
*                                                                               
C1P70    DS    0H                                                               
C1PROCX  DS    0H                                                               
         OI    FILTLST+4,X'40'     RE-ACTIVATE TARGET FILTER                    
         MVI   MPQBNOTW,C'N'       AND TARG COMP WGTS                           
         B     XIT                                                              
         EJECT                                                                  
*        C2PROC- GET C2 COUNTS                                                  
         SPACE 2                                                                
C2PROC   DS    0H                                                               
         CLI   C2SW,C'Y'           TEST C2'S NEEDED                             
         BNER  RE                                                               
         MVC   DUB(WVBN),WVBEQU    TEST DEFINED FOR WAVE                        
         NC    DUB(WVBN),VLWVBLST                                               
         BZR   RE                  NO - NO C2'S                                 
*                                                                               
         NTR1                                                                   
         LA    R4,CNWORK                                                        
         XC    CNWORK,CNWORK                                                    
         USING CNTABD,R4                                                        
         USING VLISTD,R5                                                        
         L     R6,VMPQBLK                                                       
         USING MPQBLKD,R6                                                       
*                                                                               
         MVC   CNQVEH,VLQCD        Q CODE                                       
         MVI   CNTYP,C'2'          CODE FOR C2 COUNTS                           
         MVC   CNWAVE,THISWAV      SET WAVE DATE                                
*                                                                               
         BAS   RE,CNLOOK           LOOK FOR COUNTS IN CNTAB                     
         BE    C2P60               FOUND                                        
*                                                                               
*                                  COUNT NOT FOUND- NEED TO COMPUTE             
         MVI   MPQBNOFL,C'Y'       SUPRESS FILTERING                            
         XC    QVWORK,QVWORK       SET QVTAB ENTRY                              
         LA    R3,QVWORK                                                        
         USING QVTABD,R3                                                        
         MVC   QVVEH,VLQCD                                                      
         MVC   QVWAVE,THISWAV                                                   
*                                                                               
         CLI   SURRMETH,C'1'       READERSHIP METHOD                            
         BNE   C2P20                                                            
*                                                                               
*                                  METHOD 1 - 1 ISSUE (NON-SMRB)                
*                                  -----------------------------                
*                                                                               
         MVC   QVQTYP(2),=C'C1'    LOOK FOR C1 QSPEC                            
         BAS   RE,QVLOOK           TRY IN QVTAB                                 
         BE    *+8                 FOUND- OK                                    
         BAS   RE,QGET             ELSE GET QSPEC                               
         MVC   WVECT1,QVVECT       SAVE C1 VECTOR                               
         MVI   MPQBNOFL,C'N'       RE-ACTIVATE FILTERING                        
         MVI   MPQBNSKP,C'Y'       DONT LET MPQFAC RE-READ QSPEC                
*                                                                               
         XC    WKSAM(16),WKSAM     USED TO CUME RESULTS                         
*                                                                               
         L     R2,VNODBLK                                                       
         USING NODBLKD,R2                                                       
*                                                                               
         XC    X,X                                                              
         MVC   X(8),QVVEH          SET VEH Q CODE                               
         LA    RF,X+7                                                           
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-8                                                           
         MVI   1(RF),C'.'                                                       
         MVC   2(2,RF),=C'PG'      PROBABILITY GROUP QSPECS                     
*                                                                               
         LA    RF,C2HOOK                                                        
         ST    RF,NDHOOK                                                        
         GOTO1 NODIO,DMCB,NODBLKD,=C'HIGH',X                                    
         CLI   NDERR,0                                                          
         BE    *+6                                                              
         DC    H'0'                TEMP ABEND                                   
*                                                                               
         MVI   NDSQBACK,1          BACK UP THRU TO LEVEL 1                      
         GOTO1 NODIO,DMCB,NODBLKD,=C'SEQ',X                                     
         CLI   NDERR,0                                                          
         BE    *+6                                                              
         DC    H'0'                TEMP ABEND                                   
*                                                                               
         L     RE,WKSAM            TOTAL COUNTS                                 
         S     RE,WKSAM2           LESS 'NON C2S'                               
         STCM  RE,15,CNSAM                                                      
         L     RE,WKPOP            TOTAL COUNTS                                 
         S     RE,WKPOP2           LESS 'NON C2S'                               
         STCM  RE,15,CNPOP                                                      
*                                                                               
         MVI   MPQBNSKP,C'N'       KILL MPQFAC/NODIO SUPPRESS                   
         BAS   RE,CNSET            SET IN COUNT TABLE                           
         B     C2P60                                                            
         SPACE 2                                                                
C2HOOK   NTR1                      PROC HOOK FOR NODIO                          
         CLI   NDMODE,NDPROC         ONLY WANT PROCS                            
         BNE   C2HX                                                             
*                                                                               
         MVI   X2,6               SET QSPEC IN X2 AS 'TRANSLATED' INPUT         
         MVC   X2+2(L'X2-2),X                                                   
         LA    RF,X2+L'X2-1        NEED LENGTH                                  
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-8                                                           
         MVI   1(RF),0                                                          
         LA    R0,X2-1                                                          
         SR    RF,R0                                                            
         STC   RF,X2+1                                                          
*                                                                               
         GOTO1 MPQFAC,DMCB,(X'10',=C'EXEC'),MPQBLKD,X2,0                        
         CLI   MPQBERR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     RF,WKSAM            BUMP TOTAL COUNT                             
         A     RF,MPQBSAM                                                       
         ST    RF,WKSAM                                                         
         L     RF,WKPOP                                                         
         A     RF,MPQBPOP                                                       
         ST    RF,WKPOP                                                         
         MVC   DUB,MPQBSAM         SAVE COUNT FOR PROB GRP                      
*                                                                               
         MVI   X2,10               CURRENT MPQSTK (PROB GROUP)                  
         MVI   X2+1,9              VECTOR ADDRESS TO FOLLOW                     
         MVC   X2+2(4),WVECT1      C1 VECTOR                                    
         MVI   X2+6,X'82'          'AND'                                        
         MVI   X2+7,0                                                           
*                                                                               
         GOTO1 MPQFAC,DMCB,(X'10',=C'EXEC'),MPQBLKD,X2,0                        
         CLI   MPQBERR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R1,DUB              COUNT FOR THIS PROB GRP (SAMPLE)             
         S     R1,MPQBSAM          LESS READERS                                 
         MR    R0,R1               SQUARED                                      
         L     RF,DUB              / COUNT                                      
         BAS   RE,DIV                                                           
         A     R1,WKSAM2           CUME RESULTS                                 
         ST    R1,WKSAM2                                                        
*                                                                               
         L     R1,DUB+4            COUNT FOR THIS PROB GRP (POP)                
         S     R1,MPQBPOP          LESS READERS                                 
         MR    R0,R1               SQUARED                                      
         L     RF,DUB+4            / COUNT                                      
         BAS   RE,DIV                                                           
         A     R1,WKPOP2           CUME RESULTS                                 
         ST    R1,WKPOP2                                                        
*                                                                               
C2HX     DS    0H                                                               
         B     XIT                                                              
         DROP  R2                                                               
*                                                                               
*                                  METHOD 2 - 2 ISSUE (SMRB)                    
*                                  -------------------------                    
C2P20    DS    0H                                                               
         MVC   QVQTYP(2),=C'I1'    LOOK FOR ISSUE 1 COUNT                       
         BAS   RE,QVLOOK           LOOK IN QVTAB                                
         BE    *+8                 FOUND- OK                                    
         BAS   RE,QGET             ELSE READ FOR QSPEC                          
         MVC   WVECT1,QVVECT       SET ISSUE 1 VECTOR                           
*                                                                               
         LA    R3,QVWORK                                                        
         MVI   QVQTYP+1,C'2'       ISSUE 2 COUNT                                
         BAS   RE,QVLOOK                                                        
         BE    *+8                                                              
         BAS   RE,QGET                                                          
         MVC   WVECT2,QVVECT       ISSUE 2 VECTOR                               
*                                                                               
         MVI   MPQBNOFL,C'N'       RE-ACTIVATE FILTERING                        
*                                  SET TO COUNT I1 OR I2                        
         MVI   X,9                 ACTUAL BIT VECTOR CODE                       
         MVC   X+1(4),WVECT1                                                    
         MVI   X+5,9                                                            
         MVC   X+6(4),WVECT2                                                    
         MVI   X+10,X'81'          'OR'                                         
         MVI   X+11,0                                                           
*                                                                               
         GOTO1 MPQFAC,DMCB,(X'10',=C'EXEC'),MPQBLKD,X,0                         
         CLI   MPQBERR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   CNSAM(8),MPQBSAM    SAMPLE AND POP                               
         BAS   RE,CNSET                                                         
*                                                                               
C2P60    DS    0H                  HAVE C2 COUNTS FOR Q CODE                    
         MVC   DUB,CNSAM           HOLD SAMPLE AND POP                          
         L     R1,DUB+4            POP                                          
*                              APPLY ADJUSTMENT                                 
*                              **NB- THE C2 ADJUSTMENT DONE HERE                
*                              ASSUMES A CONSTANT TURNOVER RATE                 
*                              -WE PROBABLY WANT TO DO SOMETHING ELSE           
         ICM   RF,15,ADJDSP        APPLY ADJUSTMENT                             
         BZ    C2P64               NONE                                         
         ICM   RE,15,VLDVCT                                                     
         AR    RE,RF                                                            
         ICM   R2,15,0(RE)         ADJUSTMENT VALUE IN R2                       
         BZ    C2P64               ZERO = NO ADJUSTMENT                         
         MR    R0,R2                                                            
         LA    RF,1000             **ASSUME .000 ADJ PRECISION**                
         BAS   RE,DIV                                                           
*                                                                               
C2P64    DS    0H                                                               
         BAS   RE,EQUIV            EQUIV TO BASE WAVE                           
         ICM   RF,15,VLC2          ADD TO C2 POP                                
         AR    RF,R1                                                            
         STCM  RF,15,VLC2                                                       
*                                                                               
C2PROCX  DS    0H                                                               
         B     XIT                                                              
         EJECT                                                                  
*              PAIRPROC- DO PAIR-WISE DATA                                      
         SPACE 2                                                                
PAIRPROC NTR1                                                                   
         L     R5,VLIST                                                         
*                                                                               
PRP2     DS    0H                                                               
         CLI   0(R5),X'FF'         EOL - OUTER LOOP                             
         BE    PRP90                                                            
         ST    R5,AFRSTV           SAVE 1ST OF PAIR                             
         CLI   SCHEDSW,C'Y'        IF SCHEDS                                    
         BNE   PRP2B                                                            
         TM    VLINS,X'80'         TEST LAST OCCUR OF VEHICLE                   
         BZ    PRP80               NO- IGNORE                                   
*                                                                               
PRP2B    DS    0H                                                               
         MVC   DUB(WVBN),WVBEQU    TEST DEFINED FOR WAVE                        
         NC    DUB(WVBN),VLWVBLST                                               
         BZ    PRP78                                                            
*                                                                               
         MVC   FRSTVCD,VLCOD       SAVE DATA FOR FIRST VEH                      
         MVC   FRSTVDV,VLDVCT                                                   
         MVC   FRSTVC1W,VLC1W                                                   
*                                                                               
         LA    R4,PRWORK                                                        
         USING PRTABD,R4                                                        
         MVC   PRQVEH1,VLQCD                                                    
         MVC   PRWAVE,THISWAV                                                   
*                                                                               
PRP3     DS    0H                                                               
         CLI   0(R5),X'FF'         EOL- INNER LOOP                              
         BE    PRP80                                                            
         MVC   DUB(WVBN),WVBEQU    TEST DEFINED FOR WAVE                        
         NC    DUB(WVBN),VLWVBLST                                               
         BZ    PRP68               NO                                           
         CLI   SCHEDSW,C'Y'        YES- IF SCHEDULE                             
         BNE   PRP3D                                                            
         C     R5,AFRSTV           SKIP IF SAME VEHICLE                         
         BE    PRP70                                                            
         TM    VLINS,X'80'         + DO ONLY LAST OCCUR OF VEHS                 
         BZ    PRP70                                                            
*                                                                               
PRP3D    DS    0H                                                               
         CLC   PRQVEH1,VLQCD       TEST SAME Q CODES                            
         BNE   PRP5                                                             
*                                 **NB- WHAT TO DO IF Q CODES AND/OR            
*                                       VEHICLE CODES THE SAME?                 
*                                       HOW TO HANDLE ADJUSTMENTS HERE?         
         MVC   WKPOP,VLC1W         FOR NOW JUST SET                             
         B     PRP40D              NET = CURRENT WAVE C1                        
*                                                                               
PRP5     DS    0H                                                               
         CLI   PNSW,C'Y'           TEST NEED NETS                               
         BE    PRP6                                                             
         CLI   PDSW,C'Y'           DUPES REQUIRE NETS ALSO                      
         BE    PRP6                                                             
         CLI   SCHEDSW,C'Y'        SO DO SCHEDS                                 
         BNE   PRP40                                                            
*                                                                               
PRP6     DS    0H                                                               
         LA    R4,PRWORK                                                        
         MVC   PRQVEH2,VLQCD                                                    
         BAS   RE,PRLOOK                                                        
         BE    PRP40                                                            
*                                                                               
         XC    PRQVEH1,PRQVEH2     IF PAIR NOT FOUND                            
         XC    PRQVEH2,PRQVEH1     TRY OTHER ORDER                              
         XC    PRQVEH1,PRQVEH2                                                  
         BAS   RE,PRLOOK                                                        
         BNE   *+12                                                             
         MVC   PRWORK,0(R4)        IF FOUND SET ENTRY IN PRWORK                 
         SR    RE,RE               AND SET HAVE FOUND                           
*                                                                               
         LA    R4,PRWORK                                                        
         XC    PRQVEH1,PRQVEH2     RESTORE ORIGINAL ORDER                       
         XC    PRQVEH2,PRQVEH1     IN PRWORK                                    
         XC    PRQVEH1,PRQVEH2                                                  
*                                                                               
         LTR   RE,RE               TEST FOUND                                   
         BZ    PRP40               YES                                          
*                                                                               
         LA    R3,QVWORK                                                        
         XC    QVWORK,QVWORK                                                    
         MVC   QVWAVE,PRWAVE                                                    
         MVI   MPQBNOFL,C'Y'       SUPPRESS FILTERING                           
*                                                                               
         CLI   SURRMETH,C'1'       READERSHIP METHOD                            
         BNE   PRP20                                                            
*                                                                               
*                                  METHOD 1 - 1 ISSUE (NON-SMRB)                
*                                  -----------------------------                
*                                                                               
         MVC   QVVEH,PRQVEH1                                                    
         MVC   QVQTYP(2),=C'C1'    LOOK FOR VEH A C1                            
         BAS   RE,QVLOOK           LOOK IN QVTAB                                
         BE    *+8                 FOUND- OK                                    
         BAS   RE,QGET             ELSE READ FOR QSPEC                          
         MVC   WVECT1,QVVECT       SET VEH A C1 VECTOR                          
*                                                                               
         LA    R3,QVWORK                                                        
         MVC   QVVEH,PRQVEH2       NOW VEH B                                    
         BAS   RE,QVLOOK                                                        
         BE    *+8                                                              
         BAS   RE,QGET                                                          
         MVC   WVECT2,QVVECT       VEH B C1 VECTOR                              
*                                                                               
         MVI   MPQBNOFL,C'N'       RE-ACTIVATE FILTERING                        
*                                  SET TO COUNT VEH A OR VEH B                  
         MVI   X,9                 ACTUAL BIT VECTOR CODE                       
         MVC   X+1(4),WVECT1                                                    
         MVI   X+5,9                                                            
         MVC   X+6(4),WVECT2                                                    
         MVI   X+10,X'81'          'OR'                                         
         MVI   X+11,0                                                           
*                                                                               
         GOTO1 MPQFAC,DMCB,(X'10',=C'EXEC'),MPQBLKD,X,0                         
         CLI   MPQBERR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   PRPOP,MPQBPOP       POPULATION ONLY                              
         BAS   RE,PRSET                                                         
         B     PRP40                                                            
*                                  METHOD 2 - 2 ISSUE (SMRB)                    
*                                  -------------------------                    
PRP20    DS    0H                                                               
         MVC   QVVEH,PRQVEH1                                                    
         MVC   QVQTYP(2),=C'I1'    LOOK FOR VEH A I1                            
         BAS   RE,QVLOOK           LOOK IN QVTAB                                
         BE    *+8                 FOUND- OK                                    
         BAS   RE,QGET             ELSE READ FOR QSPEC                          
         MVC   WVECT1,QVVECT       SET VEH A I1 VECTOR                          
*                                                                               
         LA    R3,QVWORK                                                        
         MVI   QVQTYP+1,C'2'       LOOK FOR VEH A I2                            
         BAS   RE,QVLOOK           LOOK IN QVTAB                                
         BE    *+8                 FOUND- OK                                    
         BAS   RE,QGET             ELSE READ FOR QSPEC                          
         MVC   WVECT2,QVVECT       SET VEH A I2 VECTOR                          
*                                                                               
         LA    R3,QVWORK                                                        
         MVC   QVVEH,PRQVEH2       NOW VEH B                                    
         MVC   QVQTYP(2),=C'I1'    LOOK FOR VEH B I1                            
         BAS   RE,QVLOOK           LOOK IN QVTAB                                
         BE    *+8                 FOUND- OK                                    
         BAS   RE,QGET             ELSE READ FOR QSPEC                          
         MVC   WVECT3,QVVECT       SET VEH B I1 VECTOR                          
*                                                                               
         LA    R3,QVWORK                                                        
         MVI   QVQTYP+1,C'2'       LOOK FOR VEH B I2                            
         BAS   RE,QVLOOK           LOOK IN QVTAB                                
         BE    *+8                 FOUND- OK                                    
         BAS   RE,QGET             ELSE READ FOR QSPEC                          
         MVC   WVECT4,QVVECT       SET VEH B I2 VECTOR                          
*                                                                               
         MVI   MPQBNOFL,C'N'       RE-ACTIVATE FILTERING                        
         XC    WKPOP,WKPOP                                                      
*                                  SET TO COUNT VEH A I1 OR VEH B I1            
         MVI   X,9                 ACTUAL BIT VECTOR CODE                       
         MVC   X+1(4),WVECT1                                                    
         MVI   X+5,9                                                            
         MVC   X+6(4),WVECT3                                                    
         MVI   X+10,X'81'          'OR'                                         
         MVI   X+11,0                                                           
         BAS   RE,PRP29            MPQFAC                                       
*                                  NOW VEH A I1 OR VEH B I2                     
         MVC   X+6(4),WVECT4                                                    
         BAS   RE,PRP29                                                         
*                                  NOW VEH A I2 OR VEH B I2                     
         MVC   X+1(4),WVECT2                                                    
         BAS   RE,PRP29                                                         
*                                  NOW VEH A I2 OR VEH B I1                     
         MVC   X+6(4),WVECT3                                                    
         BAS   RE,PRP29                                                         
*                                                                               
         L     RF,WKPOP            SUM / 4 PAIRINGS                             
         LA    RF,2(RF)                                                         
         SRL   RF,2                                                             
         STCM  RF,15,PRPOP         POPULATION ONLY                              
         BAS   RE,PRSET            SET IN PAIR COUNT TAB                        
         B     PRP40                                                            
*                                                                               
PRP29    DS    0H                                                               
         LR    R0,RE                                                            
         GOTO1 MPQFAC,DMCB,(X'10',=C'EXEC'),MPQBLKD,X,0                         
         CLI   MPQBERR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     RE,WKPOP                                                         
         A     RE,MPQBPOP                                                       
         ST    RE,WKPOP                                                         
*                                                                               
         LR    RE,R0                                                            
         BR    RE                                                               
*                                                                               
PRP40    DS    0H                                                               
         ICM   R1,15,PRPOP         SET WAVE BASE EQUIVALENCED                   
         BAS   RE,EQUIV            PAIR NET                                     
         ST    R1,WKPOP            IN WKPOP                                     
*                                                                               
PRP40D   DS    0H                                                               
         CLI   SCHEDSW,C'Y'        TEST DOING SCHEDULE                          
         BNE   PRP42                                                            
         L     R1,WKPOP            PAIR NET                                     
         L     RF,PRSLOT           SET IN PAIR NET ARRAY                        
         A     R1,0(RF)                                                         
         ST    R1,0(RF)                                                         
         B     PRP68               FOR SCHED, SKIP VXV REPORT LOGIC             
*                                                                               
PRP42    DS    0H                  PAIR-WISE DATA FOR VXV REPORT                
         MVC   THISDV,FRSTVDV      DATA VECTOR ADDR FOR 1ST                     
         ICM   R1,15,VLDVCT        2ND VEH DV ADDR                              
         S     R1,RXDVTAB          LESS START OF TABLE                          
         SR    R0,R0                                                            
         D     R0,RXDVL            / ENTRY LENGTH                               
         LA    R1,1(R1)                                                         
         STH   R1,SUFX             = RELATIVE NUMBER = SUFFIX                   
         BAS   RE,SETPAIR                                                       
*                                                                               
         C     R5,AFRSTV           IF SAME VEHICLE                              
         BE    PRP70               SET PAIR DATA ONLY ONCE                      
*                                                                               
         MVC   THISDV,VLDVCT       DATA VECTOR ADDR FOR 2ND                     
         L     R1,FRSTVDV          1ST VEH DV ADDR                              
         S     R1,RXDVTAB          LESS START OF TABLE                          
         SR    R0,R0                                                            
         D     R0,RXDVL            / ENTRY LENGTH                               
         LA    R1,1(R1)                                                         
         STH   R1,SUFX             = RELATIVE NUMBER = SUFFIX                   
         BAS   RE,SETPAIR                                                       
         B     PRP70                                                            
*                                                                               
PRP68    DS    0H                                                               
         L     RF,PRSLOT           BUMP TO NEXT PAIR SLOT                       
         LA    RF,4(RF)                                                         
         ST    RF,PRSLOT                                                        
*                                                                               
PRP70    DS    0H                                                               
         LA    R5,VLISTDL(R5)      NEXT VEHICLE (INNER LOOP)                    
         B     PRP3                                                             
*                                                                               
PRP78    DS    0H                                                               
         CLI   SCHEDSW,C'Y'        FOR SCHEDS                                   
         BNE   PRP80                                                            
*                                  MUST BUMP PRSLOT POSITION                    
         L     RF,NVEHS                                                         
         ZIC   RE,VLVEHNO                                                       
         SR    RF,RE               RF= NO. OF REMAINING VEHS                    
         SLL   RF,2                                                             
         A     RF,PRSLOT                                                        
         ST    RF,PRSLOT                                                        
*                                                                               
PRP80    DS    0H                                                               
         L     R5,AFRSTV           NEXT VEHICLE (OUTER LOOP)                    
         LA    R5,VLISTDL(R5)                                                   
         B     PRP2                                                             
*                                                                               
PRP90    DS    0H                                                               
PRPX     B     XIT                                                              
         EJECT                                                                  
*        SET PAIR - SET PAIR-WISE DATA FOR VXV REPORT IN DV TABLE               
         SPACE 2                                                                
SETPAIR  NTR1                                                                   
         L     R2,RXPCMTAB           COMMAND TABLE                              
         USING PCMDD,R2                                                         
*                                                                               
SPR4     DS    0H                                                               
         CLI   PCBASE,0            EOL                                          
         BE    SPR80                                                            
         CLI   PCQUAL,Q#           MUST BE RAW DATA                             
         BNE   SPR40                                                            
         CLC   PCSFX,SUFX          AND RIGHT SUFFIX                             
         BNE   SPR40                                                            
         ICM   RF,15,PCWHERE       SET DV DISPLACEMENT                          
         A     RF,THISDV                                                        
         CLI   PCBASE,BNET         NETS                                         
         BE    SPR10                                                            
         CLI   PCBASE,BDUP         DUPES                                        
         BE    SPR20                                                            
         CLI   PCBASE,BSUM         SUMS                                         
         BE    SPR30                                                            
         B     SPR40                                                            
*                                                                               
SPR10    DS    0H                  NETS                                         
         L     R1,WKPOP                                                         
         A     R1,0(RF)                                                         
         ST    R1,0(RF)                                                         
         B     SPR40                                                            
*                                                                               
SPR20    DS    0H                  DUPES                                        
         ICM   R1,15,VLC1W         2ND VEH C1 (CURRENT WAVE)                    
         A     R1,FRSTVC1W         + 1ST VEH C1                                 
         L     R0,WKPOP                                                         
         SR    R1,R0               LESS NET                                     
         A     R1,0(RF)            = DUPE                                       
         ST    R1,0(RF)                                                         
         B     SPR40                                                            
*                                                                               
SPR30    DS    0H                  SUMS                                         
         ICM   R1,15,VLC1W         2ND VEH C1 (CURRENT WAVE)                    
         A     R1,FRSTVC1W         + 1ST VEH C1                                 
         A     R1,0(RF)            = SUM                                        
         ST    R1,0(RF)                                                         
*                                                                               
SPR40    DS    0H                                                               
         LA    R2,PCMDL(R2)        NEXT COMMAND ENTRY                           
         B     SPR4                                                             
*                                                                               
SPR80    DS    0H                                                               
         B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*              RCHPROC- PROCESS REACH EXTENSIONS                                
         SPACE 2                                                                
RCHPROC  DS    0H                                                               
         CLI   RCHNSW,C'Y'         TEST DOING REACH EXTENSIONS                  
         BNER  RE                  NO - EXIT                                    
         CLI   SCHEDSW,C'Y'        AND SKIP FOR SCHEDULES                       
         BER   RE                  (RCHN MEANS CUME)                            
*                                                                               
         NTR1                                                                   
         L     R5,VLIST            LOOP THRU VEHCLE LIST                        
*                                                                               
RCHP6    DS    0H                                                               
         CLI   0(R5),X'FF'         EOL                                          
         BE    RCHP40                                                           
*                                                                               
         L     R4,RXPCMTAB         GO THRU PROCESSING COMMAND TABLE             
         USING PCMDD,R4                                                         
*                                                                               
RCHP26   DS    0H                                                               
         CLI   PCBASE,0            END                                          
         BE    RCHP34                                                           
         CLI   PCBASE,BRCH         LOOK FOR REACH N'S                           
         BNE   RCHP32                                                           
         CLI   PCQUAL,Q#           ONLY COUNTS                                  
         BNE   RCHP32                                                           
         OC    PCSFX,PCSFX         SKIP IF ZERO SUFFIX                          
         BZ    RCHP32                                                           
*                                                                               
         L     R3,NVEHS                                                         
         MVC   NVEHS,=F'1'         SET TO ONE VEHICLE                           
         SR    RF,RF                                                            
         ICM   RF,2,PCSFX          SUFFIX IS ISSUE COUNT                        
         L     RE,INSARR           SET IN INSERT ARRAY                          
         ST    RF,0(RE)                                                         
         L     RE,C1ARR                                                         
         MVC   0(4,RE),VLC1                                                     
         L     RE,C2ARR                                                         
         MVC   0(4,RE),VLC2                                                     
         L     RE,PNARR                                                         
         XC    0(4,RE),0(RE)                                                    
*                                                                               
         BAS   RE,CALLRF           CALL R/F MODULE                              
         BE    RCHP31              NO ERROR                                     
         ICM   RF,15,CTLDSP        SET ERROR IN VEH CONTROL AREA                
         BZ    RCHP31                                                           
         ICM   RE,15,VLDVCT                                                     
         AR    RF,RE                                                            
         USING RVCTLD,RF                                                        
         MVC   RVERR,RXERROR                                                    
         DROP  RF                                                               
*                                                                               
RCHP31   DS    0H                                                               
         ST    R3,NVEHS            RESTORE VEHICLE COUNT                        
         L     RF,RXTRGPOP         TARG POPULATION                              
         L     R1,RXFREQPA         LESS NOT-REACHED                             
         S     RF,0(R1)            = REACH                                      
         ICM   RE,15,PCWHERE       WHERE TO PUT IT                              
         ICM   R1,15,VLDVCT        DATA VECTOR ADDRESS                          
         AR    RE,R1                                                            
         ST    RF,0(RE)                                                         
*                                                                               
RCHP32   DS    0H                                                               
         LA    R4,PCMDL(R4)        NEXT 'COMMAND'                               
         B     RCHP26                                                           
*                                                                               
RCHP34   DS    0H                                                               
         LA    R5,VLISTDL(R5)      NEXT VEHICLE                                 
         B     RCHP6                                                            
*                                                                               
RCHP40   DS    0H                                                               
RCHPX    B     XIT                                                              
         EJECT                                                                  
*        SCHDPROC- SCHEDULE EVALUATION                                          
         SPACE 2                                                                
SCHDPROC DS    0H                                                               
         CLI   SCHEDSW,C'Y'        TEST DOING SCHED                             
         BNER  RE                                                               
*                                                                               
         NTR1                                                                   
         CLI   RCHNSW,C'Y'         IF DOING CUME REACHES                        
         BNE   SCHP3                                                            
*                                  RE-SORT VLIST TO ORIGINAL ORDER              
         L     R2,NLINS                                                         
         GOTO1 XSORT,DMCB,VLIST,(R2),VLISTDL,4,VLDVCT-VLISTD                    
*                                                                               
SCHP3    DS    0H                                                               
         XC    TNINS,TNINS         RE-COUNT INSERTS                             
         L     R5,VLIST            LOOP THRU VLIST                              
*                                                                               
SCHP4    DS    0H                                                               
         CLI   0(R5),X'FF'         EOL                                          
         BE    SCHP40                                                           
         MVC   HALF,VLINS          INSERT COUNT                                 
         NI    HALF,X'7F'          STRIP HOB                                    
         LH    R3,HALF                                                          
         LTR   R3,R3                                                            
         BZ    SCHP10                                                           
*                                                                               
         L     RF,TNINS            BUMP NUMBER OF INSERTS                       
         AR    RF,R3                                                            
         ST    RF,TNINS                                                         
*                                                                               
         ZIC   RF,VLVEHNO          VEH NUMBER                                   
         BCTR  RF,R0                                                            
         SLL   RF,2                X 4 = DISP INTO ARRAY                        
         LR    R1,RF               SAVE DISP                                    
         A     RF,INSARR           SET INSERT COUNT IN INSERT ARRAY             
         L     RE,0(RF)                                                         
         AR    RE,R3                                                            
         ST    RE,0(RF)                                                         
*                                                                               
*                                SET IMPACT FACTORS IN ARRAY                    
*                                **NB- WHAT TO DO IF DIFFERENT LINES            
*                                FOR SAVE VEH HAVE DIFFERENT FACTORS.           
         ICM   RE,15,IMPFDSP                                                    
         BZ    SCHP8               NO FACTORS                                   
         ICM   RF,15,VLDVCT                                                     
         AR    RE,RF                                                            
         ICM   RE,15,0(RE)         FACTOR IN RE                                 
         BNZ   *+8                                                              
SCHP8    DS    0H                                                               
         LA    RE,1000       DEFAULT 1.000 (**ASSUME .000 PREC)                 
         A     R1,IMPFARR                                                       
         ST    RE,0(R1)                                                         
*                                                                               
         CLI   RCHNSW,C'Y'         IF DOING CUMES                               
         BNE   SCHP20                                                           
*                                                                               
SCHP9    DS    0H                  GET PARTIAL SCHEDULE RESULTS                 
         BAS   RE,CALLRF           EVALUATE PARTIAL SCHED                       
         BAS   RE,RFNPROC          AND APPLY RESPONSE FUNCTION                  
*                                                                               
         CLI   RCHNSW,C'Y'         IF DOING CUMES                               
         BNE   SCHP20                                                           
*                                                                               
SCHP10   DS    0H                                                               
         L     R4,RXPCMTAB         GO THRU COMMAND TABLE                        
         USING PCMDD,R4            AND SET PARTIAL SCHED RESULTS                
*                                                                               
SCHP12   DS    0H                                                               
         CLI   PCBASE,0            EOL                                          
         BE    SCHP16                                                           
         CLI   PCQUAL,Q#           DO ONLY RAW DATA                             
         BNE   SCHP14                                                           
         CLI   PCBASE,BRCH         RCHN'S ONLY                                  
         BNE   SCHP14                                                           
*                                  SUFFIX IS N+ FREQUENCY                       
         LA    RF,1                                                             
         CLI   PCSFX+1,1           0 = 1+                                       
         BNH   *+8                                                              
         IC    RF,PCSFX+1                                                       
         SR    R1,R1               CLEAR COUNTER                                
         L     R2,TNINS            TOTAL INSERT COUNT                           
         CH    R2,=Y(MAXINS)       TEST VS MAX                                  
         BNH   *+8                                                              
         LA    R2,MAXINS                                                        
         LA    R2,1(R2)            PLUS ONE FOR ZERO LEVEL                      
         SR    R2,RF               LESS START = NUMBER TO COUNT                 
         BNP   SCHP13              SKIP IF DESIRED FREQ LEVEL                   
*                                  HIGHER THAN RESULT INSERT COUNT              
         SLL   RF,2                START X 4 = DISPLACEMENT                     
         L     RE,RXFREQPA         USE POP DATA                                 
         TM    PCSFX,X'80'                                                      
         BZ    *+8                                                              
         L     RE,RXFREQRA         OR RESP FUNCTION WGTD DATA                   
         AR    RE,RF                                                            
*                                                                               
         A     R1,0(RE)            CUME FREQ LEVELS                             
         LA    RE,4(RE)                                                         
         BCT   R2,*-8                                                           
*                                                                               
SCHP13   DS    0H                                                               
         ICM   RF,15,PCWHERE       DISP INTO DATA VECTOR                        
         ICM   RE,15,VLDVCT        START OF THIS VEH VECTOR                     
         ST    R1,0(RE,RF)         SET IN DATA VECTOR                           
*                                                                               
SCHP14   DS    0H                                                               
         LA    R4,PCMDL(R4)        NEXT COMMAND                                 
         B     SCHP12                                                           
*                                                                               
SCHP16   DS    0H                                                               
SCHP20   DS    0H                                                               
         LA    R5,VLISTDL(R5)      NEXT VEHICLE                                 
         B     SCHP4                                                            
*                                                                               
*                                                                               
SCHP40   DS    0H                                                               
         CLI   UNIQSW,C'Y'         TEST DOING UNIQUES                           
         BNE   SCHP90                                                           
*                                                                               
SCHP50   DS    0H                  LOOP THRU VEHICLE LIST                       
         L     R5,VLIST            SUBTRACTING EACH LINE TO                     
*                                  EVALUATE PARTIAL SCHEDULES                   
SCHP54   DS    0H                                                               
         CLI   0(R5),X'FF'         EOL                                          
         BE    SCHP80                                                           
*                                                                               
         ICM   R3,15,VLDVCT        GET INSERT COUNT FROM                        
         A     R3,INSDSP           DVTAB ENTRY BECAUSE VLIST                    
         ICM   R3,15,0(R3)         HAS CUMED INSERTS                            
         BZ    SCHP70              NO INSERTS- SKIP LINE                        
         ZIC   RF,VLVEHNO                                                       
         BCTR  RF,R0                                                            
         SLL   RF,2                VEH NO X 4                                   
         A     RF,INSARR           = DISP INTO INSARR                           
         L     RE,0(RF)                                                         
         LR    R2,RE               SAVE ORIGINAL INS COUNT                      
         SR    RE,R3               SUBTRACT OUT THIS LINE                       
         ST    RE,0(RF)                                                         
*                                                                               
         BAS   RE,CALLRF           EVALUATE REDUCED SCHEDULE                    
         BAS   RE,RFNPROC          AND RESPONSE FUNCTION                        
         ST    R2,0(RF)            RESTORE ORIGINAL INS COUNT                   
*                                                                               
*                                  NOW GO THRU COMMAND TABLE                    
         L     R4,RXPCMTAB         AND SET PARTIAL SCHED RESULTS                
         USING PCMDD,R4                                                         
*                                                                               
SCHP60   DS    0H                                                               
         CLI   PCBASE,0            EOL                                          
         BE    SCHP66                                                           
         CLI   PCQUAL,Q#           DO ONLY RAW DATA                             
         BNE   SCHP64                                                           
         CLI   PCBASE,BUNIQ        SET IN UNIQ'S                                
         BNE   SCHP64                                                           
*                                  SUFFIX IS N+ FREQUENCY                       
         LA    RF,1                                                             
         CLI   PCSFX+1,1           0 = 1+                                       
         BNH   *+8                                                              
         IC    RF,PCSFX+1                                                       
         SR    R1,R1               CLEAR COUNTER                                
         L     R2,TNINS            TOTAL INSERT COUNT                           
         SR    R2,R3               LESS NUMBER OUT FOR THIS LINE                
         CH    R2,=Y(MAXINS)       TEST VS MAX                                  
         BNH   *+8                                                              
         LA    R2,MAXINS                                                        
         LA    R2,1(R2)            PLUS ONE FOR ZERO LEVEL                      
         SR    R2,RF               LESS START = NUMBER TO COUNT                 
         BNP   SCHP63              SKIP IF DESIRED FREQ LEVEL                   
*                                  HIGHER THAN RESULT INSERT COUNT              
         SLL   RF,2                START X 4 = DISPLACEMENT                     
         L     RE,RXFREQPA         USE POP DATA                                 
         TM    PCSFX,X'80'                                                      
         BZ    *+8                                                              
         L     RE,RXFREQRA         OR RESP FUNCTION WGTD DATA                   
         AR    RE,RF                                                            
*                                                                               
         A     R1,0(RE)            CUME FREQ LEVELS                             
         LA    RE,4(RE)                                                         
         BCT   R2,*-8                                                           
*                                                                               
SCHP63   DS    0H                                                               
         ICM   RF,15,PCWHERE       DISP INTO DATA VECTOR                        
         ICM   RE,15,VLDVCT        START OF THIS VEH VECTOR                     
         ST    R1,0(RE,RF)         SET IN DATA VECTOR                           
*                                                                               
SCHP64   DS    0H                                                               
         LA    R4,PCMDL(R4)        NEXT COMMAND                                 
         B     SCHP60                                                           
*                                                                               
SCHP66   DS    0H                                                               
SCHP70   DS    0H                                                               
         LA    R5,VLISTDL(R5)      NEXT VEHICLE                                 
         B     SCHP54                                                           
*                                                                               
SCHP80   DS    0H                  NOW DO WHOLE SCHEDULE                        
         BAS   RE,CALLRF                                                        
         BAS   RE,RFNPROC                                                       
*                                                                               
*                                  DO PASS THRU COMMAND TABLE AND               
*                                  DATA VECTORS TO SUBTRACT PARTIAL             
*                                  SCHED RESULTS FROM ENTIRE SCHED              
*                                  TO GET UNIQUE CONTRIBUTIONS                  
*                                                                               
         L     R4,RXPCMTAB                                                      
         USING PCMDD,R4                                                         
*                                                                               
SCHP82   DS    0H                                                               
         CLI   PCBASE,0            EOL                                          
         BE    SCHP90                                                           
         CLI   PCBASE,BUNIQ                                                     
         BNE   SCHP88                                                           
         CLI   PCQUAL,Q#           DO ONLY RAW DATA                             
         BNE   SCHP88                                                           
*                                  SUFFIX IF CUME FREQUENCY LEVEL               
         LA    RF,1                                                             
         CLI   PCSFX+1,1           0 = 1+                                       
         BNH   *+8                                                              
         IC    RF,PCSFX+1                                                       
         SR    R1,R1               CLEAR COUNTER                                
         L     R2,TNINS            TOTAL INSERTS                                
         CH    R2,=Y(MAXINS)       TEST VS MAX                                  
         BNH   *+8                                                              
         LA    R2,MAXINS                                                        
         LA    R2,1(R2)                                                         
         SR    R2,RF               LESS START = NUMBER TO COUNT                 
         BNP   SCHP82D             SKIP IF DESIRED FREQ LEVEL                   
*                                  HIGHER THAN RESULT INSERT COUNT              
         SLL   RF,2                START X 4 = DISPLACEMENT                     
         L     RE,RXFREQPA         USE POP DATA                                 
         TM    PCSFX,X'80'                                                      
         BZ    *+8                                                              
         L     RE,RXFREQRA         OR RESP FUNCTION WGTD DATA                   
         AR    RE,RF                                                            
*                                                                               
         A     R1,0(RE)            CUME FREQ LEVELS IN R1                       
         LA    RE,4(RE)                                                         
         BCT   R2,*-8                                                           
*                                                                               
SCHP82D  DS    0H                                                               
         L     R5,VLIST            LOOP THRU VEHICLE LIST                       
*                                                                               
SCHP83   DS    0H                                                               
         CLI   0(R5),X'FF'         EOL                                          
         BE    SCHP85                                                           
         MVC   HALF,VLINS          SKIP IF NO INSERTS                           
         NC    HALF,=X'7FFF'                                                    
         BZ    SCHP84                                                           
         ICM   RF,15,PCWHERE       DISP INTO DATA VECTOR                        
         ICM   RE,15,VLDVCT        START OF THIS VEH VECTOR                     
         LR    R0,R1               TOTAL SCHEDULE                               
         S     R0,0(RF,RE)         LESS PARTIAL RESULT                          
         ST    R0,0(RF,RE)         = UNIQUE CONTRIBUTION                        
*                                                                               
SCHP84   DS    0H                                                               
         LA    R5,VLISTDL(R5)      NEXT VEHICLE                                 
         B     SCHP83                                                           
*                                                                               
SCHP85   DS    0H                                                               
SCHP88   DS    0H                                                               
         LA    R4,PCMDL(R4)        NEXT COMMAND                                 
         B     SCHP82                                                           
*                                                                               
SCHP90   DS    0H                                                               
         CLI   RCHNSW,C'Y'         IF HAVE DONE CUMES                           
         BE    SCHPX                                                            
         CLI   UNIQSW,C'Y'         OR UNIQUES                                   
         BE    SCHPX               DONE                                         
*                                                                               
         BAS   RE,CALLRF           ELSE DO WHOLE SCHEDULE NOW                   
         BAS   RE,RFNPROC                                                       
*                                                                               
SCHPX    DS    0H                                                               
         B     XIT                                                              
         EJECT                                                                  
*        CALLRF - CALL REACH/FREQ MODULE                                        
         SPACE 2                                                                
CALLRF   NTR1                                                                   
         LA    R1,RFARGLST         SET ARGUMENT LIST                            
         LA    RF,RXTRGPOP         TARGET POP                                   
         ST    RF,0(R1)                                                         
         LA    RF,NVEHS            VEHICLE COUNT                                
         ST    RF,4(R1)                                                         
         LA    RF,RFRTRN           RETURN CODE                                  
         ST    RF,8(R1)                                                         
         MVC   12(28,R1),INSARR    INSERT,C1,C2,IMPF,VERR,PERR AND              
*                                  PAIR NET ARRAYS                              
         MVC   40(4,R1),RXFREQPA   FREQ ARRAY                                   
         OI    40(R1),X'80'        SET EOL                                      
*                                                                               
         ST    RD,SAVRD            SAVE RD                                      
         LA    RD,RFRGSAV          REG SAVE AREA                                
         GOTO1 RFMOD                                                            
         L     RD,SAVRD            RESTORE RD                                   
*                                                                               
         OC    RFRTRN,RFRTRN       TEST ANY ERROR                               
         BZ    CRFXZ                                                            
*                                                                               
         MVC   RXERROR,RFRTRN+3                                                 
         CLI   SCHEDSW,C'Y'        IF DOING SCHED                               
         BNE   CRFXNZ                                                           
*                                  SET ERRORS IN VECTOR TABLE                   
         L     R5,VLIST            LOOP THRU VEHICLE LIST                       
         USING VLISTD,R5                                                        
*                                                                               
CRF4     DS    0H                                                               
         CLI   0(R5),X'FF'         EOL                                          
         BE    CRF8                                                             
         ICM   R3,15,VLDVCT                                                     
         A     R3,CTLDSP           R3 = CONTROL AREA                            
         ZIC   R2,VLVEHNO          VEH NO                                       
         BCTR  R2,R0                                                            
         SLL   R2,2                X 4 = DISPLACEMENT                           
         L     RF,VERRARR          VEHICLE ERRORS                               
         AR    RF,R2                                                            
         MVC   RVERR-RVCTLD(1,R3),3(RF)   ONE BYTE                              
         L     RF,PERRARR          PAIR ERRORS                                  
         AR    RF,R2                                                            
         MVC   RVERRP-RVCTLD(2,R3),2(RF)   2 BYTE VEH NUMBER                    
*                                                                               
         LA    R5,VLISTDL(R5)                                                   
         B     CRF4                                                             
*                                                                               
CRF8     DS    0H                                                               
*                                                                               
CRFXZ    DS    0H                  NB- CC= MEANS NO ERROR                       
         CR    R0,R0                                                            
         B     XIT                                                              
CRFXNZ   DS    0H                      CC NOT = MEANS ERROR                     
         LTR   RB,RB                                                            
         B     XIT                                                              
         EJECT                                                                  
*        ARSET - SET DATA IN ARRAYS AND DIVIDE BY NO. OF WAVES                  
         SPACE 2                                                                
ARSET    NTR1                                                                   
         MVC   PRSLOT,PNARR        START OF PAIR-NET ARRAY                      
         L     R5,VLIST            LOOP THRU VEHICLE LIST                       
*                                                                               
ARS4     DS    0H                                                               
         CLI   0(R5),X'FF'         EOL- OUTER LOOP                              
         BE    ARS20                                                            
*                                                                               
         MVC   WKC1(8),VLC1        SET C1 AND C2                                
         CLC   NWAVES,=F'1'        AS IS IF ONLY ONE WAVE                       
         BNH   ARS5                                                             
         SR    RF,RF               ELSE DIVIDE BY                               
         ICM   RF,1,VLNWVS         NO. OF WAVES FOR VEH                         
         ICM   R1,15,VLC1          C1                                           
         M     R0,=F'1'                                                         
         BAS   RE,DIV                                                           
         ST    R1,WKC1                                                          
         ICM   R1,15,VLC2          C2                                           
         M     R0,=F'1'                                                         
         BAS   RE,DIV                                                           
         ST    R1,WKC2                                                          
         ICM   R1,15,VLBC1         BASE C1                                      
         M     R0,=F'1'                                                         
         BAS   RE,DIV                                                           
         ST    R1,WKBC1                                                         
*                                                                               
ARS5     DS    0H                                                               
         CLI   SCHEDSW,C'Y'        IF DOING SCHEDULE SET ARRAYS                 
         BNE   ARS6                                                             
         TM    VLINS,X'80'         ONLY FOR LAST OCCUR OF VEH                   
         BZ    ARS6                                                             
         ZIC   R2,VLVEHNO          VEH NO                                       
         BCTR  R2,R0                                                            
         SLL   R2,2                X 4 = DISPLACEMENT                           
         LR    R3,R2                                                            
         A     R2,C1ARR                                                         
         MVC   0(4,R2),WKC1                                                     
         A     R3,C2ARR                                                         
         MVC   0(4,R3),WKC2                                                     
*                                                                               
ARS6     DS    0H                                                               
         ICM   RF,15,VLDVCT        A(DATA VECTOR)                               
         ICM   R2,15,C1DSP         C1                                           
         BZ    *+12                IF NOT NEEDED, SKIP                          
         AR    R2,RF                                                            
         MVC   0(4,R2),WKC1                                                     
*                                                                               
         ICM   R2,15,BC1DSP        BASE C1                                      
         BZ    *+12                IF NOT NEEDED, SKIP                          
         AR    R2,RF                                                            
         MVC   0(4,R2),WKBC1                                                    
*                                                                               
*                                  TEST SAMPLE SIZES                            
         A     RF,CTLDSP           POINT TO CONTROL AREA                        
         MVI   FULL,X'FF'                                                       
         LA    R2,SURSZMS          SAMPLE SIZE TABLE                            
         USING SURSZD,R2                                                        
*                                                                               
ARS7     DS    0H                                                               
         CLI   0(R2),0             EOL                                          
         BE    ARS8                                                             
         CLC   VLC1SAM,SURSZCNT    TEST SAMPLE VS TABLE                         
         BNL   ARS7D                                                            
         CLC   SURSZCNT,FULL       TEST SMALLEST TRHESHOLD                      
         BNL   ARS7D                                                            
         MVC   FULL,SURSZCNT                                                    
         MVC   RVSAMF-RVCTLD(4,RF),SURSZFL   SET FLAG                           
*                                                                               
ARS7D    DS    0H                                                               
         LA    R2,SURSZDL(R2)                                                   
         B     ARS7                                                             
         DROP  R2                                                               
*                                                                               
ARS8     DS    0H                                                               
         MVC   RVWVBL-RVCTLD(WVBN,RF),VLWVBLST  SET ACTIVE SAVE LIST            
*                                                                               
         CLC   NWAVES,=F'1'        IF MULTIPLE WAVES                            
         BNH   ARS18                                                            
         CLI   SCHEDSW,C'Y'        AND DOING SCHEDULES                          
         BNE   ARS18                                                            
*                                  NEED TO DIVIDE PARI NET ARRAY                
         ST    R5,AFRSTV           SAVE A(CURRENT VEH)                          
         MVC   DUB(WVBN),VLWVBLST  SAVE WAVE LIST                               
         LA    R5,VLISTDL(R5)                                                   
*                                                                               
ARS10    DS    0H                                                               
         CLI   0(R5),X'FF'         EOL- INNER LOOP                              
         BE    ARS16                                                            
         TM    VLINS,X'80'         LAST OCCUR ONLY                              
         BZ    ARS13                                                            
         MVC   FULL,DUB                                                         
         NC    FULL(WVBN),VLWVBLST                                              
         BZ    ARS14               ****- NB- NO COMMON WAVE FOR PAIR            
*                                                                               
         SR    RF,RF               COUNT WAVES                                  
         LA    R3,WVBN             NO. OF BYTES                                 
         LA    R2,FULL-1                                                        
         LA    R2,1(R2)                                                         
         BAS   RE,BITCOUNT                                                      
         BCT   R3,*-8                                                           
*                                                                               
         L     R3,PRSLOT           SLOT FOR PAIR                                
         L     R1,0(R3)                                                         
         M     R0,=F'1'                                                         
         BAS   RE,DIV                                                           
         ST    R1,0(R3)                                                         
*                                                                               
ARS13    DS    0H                                                               
         L     RF,PRSLOT           BUMP PRSLOT                                  
         LA    RF,4(RF)                                                         
         ST    RF,PRSLOT                                                        
*                                                                               
ARS14    DS    0H                                                               
         LA    R5,VLISTDL(R5)      NEXT VEH - INNER LOOP                        
         B     ARS10                                                            
*                                                                               
ARS16    DS    0H                                                               
         L     R5,AFRSTV           NEXT VEH - OUTER LOOP                        
ARS18    DS    0H                                                               
         LA    R5,VLISTDL(R5)                                                   
         B     ARS4                                                             
*                                                                               
ARS20    DS    0H                                                               
         CLC   NWAVES,=F'1'        IF MULTIPLE WAVES                            
         BNH   ARS30                                                            
         CLI   PNSW,C'Y'           AND VXV PAIR NETS                            
         BE    ARS22                                                            
         CLI   PDSW,C'Y'           OR DUPES                                     
         BE    ARS22                                                            
         CLI   PSSW,C'Y'           OR SUMS                                      
         BNE   ARS30                                                            
*                                                                               
ARS22    DS    0H                  ADJUST PAIR DATA IN DVTAB                    
         L     R3,RXDVTAB          A(DATA VECTOR TABLE)                         
         ICM   R4,15,RXNDVS        NUMBER OF VECTORS FOR BCT                    
         BZ    ARS30                                                            
*                                                                               
ARS22D   DS    0H                                                               
         LR    R6,R3                                                            
         A     R6,CTLDSP           CONTROL                                      
         MVC   DUB(WVBN),RVWVBL-RVCTLD(R6)  WAVE LIST                           
*                                                                               
         L     R5,RXPCMTAB         COMMAND TABLE                                
         USING PCMDD,R5                                                         
*                                                                               
ARS23    DS    0H                                                               
         CLI   PCBASE,0            EOL                                          
         BE    ARS28                                                            
         CLI   PCQUAL,Q#           DO ONLY ORIGINAL DATA                        
         BNE   ARS26                                                            
*                                                                               
         CLI   PCBASE,BNET         MUST BE NET                                  
         BE    ARS24                                                            
         CLI   PCBASE,BDUP         OR DUPE                                      
         BE    ARS24                                                            
         CLI   PCBASE,BSUM         OR SUM                                       
         BNE   ARS26                                                            
*                                                                               
ARS24    DS    0H                                                               
         SR    R6,R6                                                            
         ICM   R6,3,PCSFX          SUFFIX IS PAIR VECTOR NUMBER                 
         BCTR  R6,R0                                                            
         MH    R6,RXDVL+2          X LENGTH OF VECTOR                           
         A     R6,RXDVTAB          PLUS START OF TABLE= A(PAIR VECTOR)          
         A     R6,CTLDSP           CONTROL                                      
         MVC   FULL,DUB                                                         
         NC    FULL(WVBN),RVWVBL-RVCTLD(R6)  SET COMMON WAVES                   
         BZ    ARS26               ****NO COMMON WAVES                          
*                                                                               
         SR    RF,RF               COUNT COMMON WAVES                           
         LA    R2,FULL-1                                                        
         LA    R6,WVBN             NUMBER OF BYTES                              
         LA    R2,1(R2)                                                         
         BAS   RE,BITCOUNT                                                      
         BCT   R6,*-8                                                           
*                                                                               
         ICM   R6,15,PCWHERE       DISPL INTO DATA VECTOR                       
         AR    R6,R3                                                            
         L     R1,0(R6)                                                         
         BAS   RE,DIV              DIVIDE BY NO. OF WAVES                       
         ST    R1,0(R6)                                                         
*                                                                               
ARS26    DS    0H                                                               
         LA    R5,PCMDL(R5)        NEXT COMMAND ENTRY                           
         B     ARS23                                                            
*                                                                               
ARS28    DS    0H                                                               
         A     R3,RXDVL            NEXT DATA VECTOR                             
         BCT   R4,ARS22D                                                        
*                                                                               
ARS30    DS    0H                                                               
ARSX     DS    0H                                                               
         B     XIT                                                              
         SPACE 3                                                                
*        COUNT BITS IN BYTE AT 0(R2)                                            
*        AND CUME COUNT IN RF                                                   
         SPACE 2                                                                
BITCOUNT DS    0H                                                               
         SR    R0,R0                                                            
         ICM   R0,1,0(R2)                                                       
         BZR   RE                                                               
         SR    R1,R1                                                            
*                                                                               
BITC4    DS    0H                                                               
         SRDL  R0,1                                                             
         LTR   R1,R1               TEST HOB ON                                  
         BNM   *+8                                                              
         LA    RF,1(RF)            YES- BUMP COUNT                              
         LTR   R0,R0               TEST ANY BITS LEFT ON                        
         BZR   RE                                                               
         B     BITC4                                                            
*                                                                               
         DROP  R5                                                               
         EJECT                                                                  
*        RFNPROC- APPLY RESPONSE FUNCTION WGTS                                  
         SPACE 2                                                                
RFNPROC  DS    0H                                                               
         OC    RXRESPR,RXRESPR     TEST ANY FUNCTION REC                        
         BZR   RE                                                               
         OC    TNINS,TNINS         TEST ANY INSERTS                             
         BZR   RE                                                               
*                                                                               
         NTR1                                                                   
         L     RF,RXFREQRA         MOVE POP ARRAY TO WGT ARRAY                  
         L     R1,TNINS            INSERT COUNT                                 
         SLL   R1,2                                                             
         L     RE,RXFREQPA                                                      
         MOVE  ((RF),(R1)),(RE)                                                 
*                                                                               
         L     R2,RXRESPR          FUNCTION RECORD                              
         LA    R2,QELSTRT(R2)      FIRST ELEM                                   
         MVI   ELCODE,X'26'                                                     
         USING QRFELEM,R2                                                       
         MVI   FELSW,C'Y'          DO FIRST ELEM                                
*                                                                               
RFNP6    DS    0H                                                               
         BAS   RE,NEXTEL                                                        
         BNE   RFNP30                                                           
*                                                                               
         SR    R3,R3                                                            
         ICM   R3,3,QRFLO          LOW END FOR VALUE                            
         C     R3,TNINS            IF HIGHER THAT INSERT COUNT                  
         BH    RFNP6               SKIP ELEM                                    
         SLL   R3,2                                                             
         SR    R4,R4                                                            
         ICM   R4,3,QRFHI          HI END                                       
         C     R4,TNINS            INSERT COUNT IS LIMIT                        
         BNH   *+8                                                              
         L     R4,TNINS                                                         
         SLL   R4,2                                                             
         MVC   FULL,QRFWGT                                                      
*                                                                               
RFNP8    DS    0H                                                               
         L     R5,RXFREQPA         POP VALUE                                    
         L     R1,0(R5,R3)                                                      
         M     R0,FULL                                                          
         LA    RF,1000             ** .000 **                                   
         BAS   RE,DIV                                                           
         L     R5,RXFREQRA         WGTD ARRAY                                   
         ST    R1,0(R5,R3)                                                      
*                                                                               
         LA    R3,4(R3)            NEXT FREQ LEVEL                              
         CR    R3,R4               TEST PAST HI RANGE                           
         BNH   RFNP8                                                            
*                                                                               
         B     RFNP6               NEXT ELEM                                    
*                                                                               
RFNP30   DS    0H                                                               
         B     XIT                                                              
         EJECT                                                                  
*        CNLOOK- TRY FOR DATA FROM COUNT TABLE                                  
         SPACE 2                                                                
CNLOOK   DS    0H                                                               
         USING CNTABD,R4                                                        
         LR    R0,RE                                                            
         GOTO1 BINSRCH,CNPARS,CNWORK                                            
         LR    RE,R0                                                            
         CLI   0(R1),0             TEST FOUND                                   
         BNER  RE                  NO- EXIT CC NOT =                            
         L     R4,0(R1)            YES- SET ENTRY ADDRESS                       
         MVC   CNCALLNO,CALLNO     SET CURRENT CALL NUMBER                      
         BR    RE                  AND EXIT CC =                                
         SPACE 3                                                                
*        CNSET- ADD ENTRY TO COUNT TABLE                                        
         SPACE 2                                                                
CNSET    NTR1                                                                   
         MVC   CNCALLNO,CALLNO     SET CURRENT CALL NUMBER                      
         GOTO1 BINSRCH,CNPARS,(1,CNWORK)                                        
*                                                                               
         OC    1(3,R1),1(R1)       TEST WILL FIT                                
         BNZ   CNSETX              YES                                          
*                                  NO ROOM IN COUNT TABLE                       
*                                  LOOK FOR ENTRY TO REPLACE                    
         L     R4,CNPARS+4                                                      
         L     RF,CNPARS+8         COUNT FOR BCT                                
*                                                                               
CNS6     DS    0H                                                               
         CLC   CNCALLNO,CALLNO     NOT IF FOR THIS CALL                         
         BNE   CNS8                                                             
*                                                                               
         A     R4,CNPARS+12        NEXT QVTAB ENTRY                             
         BCT   RF,CNS6                                                          
         B     CNSETX              CANT SAVE THIS VECTOR                        
*                                                                               
CNS8     DS    0H                                                               
         BAS   RE,BINSDEL          REMOVE FROM QVTAB                            
         LA    R4,CNWORK                                                        
         GOTO1 BINSRCH,CNPARS,(1,CNWORK)                                        
         OC    1(3,R1),1(R1)                                                    
         BNZ   *+6                                                              
         DC    H'0'                MUST FIT                                     
*                                                                               
*                                                                               
CNSETX   DS    0H                                                               
         B     XIT                                                              
         SPACE 3                                                                
         DROP  R4                                                               
*        PRLOOK- TRY FOR DATA FROM PAIR COUNT TABLE                             
         SPACE 2                                                                
PRLOOK   DS    0H                                                               
         LR    R0,RE                                                            
         GOTO1 BINSRCH,PRPARS,PRWORK                                            
         LR    RE,R0                                                            
         CLI   0(R1),0             TEST FOUND                                   
         BNER  RE                  NO- EXIT CC NOT =                            
         L     R4,0(R1)            YES- SET ENTRY ADDRESS                       
         BR    RE                  AND EXIT CC =                                
         SPACE 3                                                                
*        PRSET- ADD ENTRY TO PAIR COUNT TABLE                                   
         SPACE 2                                                                
PRSET    DS    0H                                                               
         LR    R0,RE                                                            
         GOTO1 BINSRCH,PRPARS,(1,PRWORK)                                        
         LR    RE,R0              DONT TEST IF FIT BECAUSE NO SEARCH            
         BR    RE                 FOR REPLACEMENT IF DIDNT                      
         SPACE 3                                                                
*        QVLOOK- TRY FOR DATA FROM QVTAB                                        
         SPACE 2                                                                
QVLOOK   DS    0H                                                               
         LR    R0,RE                                                            
         GOTO1 BINSRCH,QVPARS,QVWORK                                            
         LR    RE,R0                                                            
         CLI   0(R1),0             TEST FOUND                                   
         BNER  RE                  NO- EXIT CC NOT =                            
         L     R3,0(R1)            YES- SET ENTRY ADDRESS                       
         MVC   QVCALLNO,CALLNO     SET USED THIS CALL                           
         BR    RE                  AND EXIT CC =                                
         EJECT                                                                  
*        QVSET- ADD ENTRY TO QVTAB                                              
         SPACE 2                                                                
QVSET    NTR1                                                                   
         MVC   QVCALLNO,CALLNO     SET CALL NUMBER                              
         OC    AVECT,AVECT         TEST NOT DEFINED FOR WAVE                    
         BZ    QVS40               YES- SKIP VECTOR SAVE                        
         CLC   NVECTS,MAXVECTS     TEST HAVE ROOM TO SAVE VECTOR                
         BL    QVS10                                                            
*                                  NO ROOM FOR VECTOR                           
*                                  LOOK FOR QVTAB ENTRY TO REPLACE              
         L     R3,QVPARS+4                                                      
         L     RF,QVPARS+8         COUNT FOR BCT                                
*                                                                               
QVS6     DS    0H                                                               
         CLC   QVCALLNO,CALLNO     NOT IF FOR THIS CALL                         
         BE    QVS7                                                             
         MVC   WORK,QVVEH                                                       
         BAS   RE,VSRCH            TEST VEH USED THIS CALL                      
         BNE   QVS8                NO- OK TO REMOVE                             
*                                                                               
QVS7     DS    0H                                                               
         A     R3,QVPARS+12        NEXT QVTAB ENTRY                             
         BCT   RF,QVS6                                                          
         LA    R3,QVWORK                                                        
         B     QVSETX              CANT SAVE THIS VECTOR                        
*                                                                               
QVS8     DS    0H                                                               
         MVC   FULL,QVVECT         SAVE SLOT FOR VECTOR                         
         BAS   RE,BINSDEL          REMOVE FROM QVTAB                            
         LA    R3,QVWORK                                                        
         MVC   QVVECT,FULL         SET VECTOR SLOT IN NEW ENTRY                 
         L     RF,FULL                                                          
         B     QVS30                                                            
*                                                                               
QVS10    DS    0H                  HAVE ROOM FOR VECTOR                         
         L     RF,NVECTS           BUMP VECTOR COUNT                            
         LA    RF,1(RF)                                                         
         ST    RF,NVECTS                                                        
*                                                                               
         L     RF,NXTVECT          A(VECTOR SLOT)                               
         STCM  RF,15,QVVECT                                                     
         LR    RE,RF               BUMP VECTOR SLOT                             
         A     RE,BVLEN                                                         
         ST    RE,NXTVECT                                                       
*                                                                               
QVS30    DS    0H                                                               
         L     RE,AVECT                                                         
         L     R1,BVLEN                                                         
         MOVE  ((RF),(R1)),(RE)    MOVE TO VECTOR SAVE AREA                     
*                                                                               
QVS40    DS    0H                                                               
         GOTO1 BINSRCH,QVPARS,(1,QVWORK)                                        
*                                                                               
         OC    1(3,R1),1(R1)       TEST WILL FIT                                
         BNZ   *+6                                                              
         DC    H'0'                NO- MEANS THERE WAS ENOUGH CORE TO           
*                                  SAVE VECTOR, BUT QVTAB IS TOO SMALL          
QVS50    DS    0H                                                               
QVSETX   DS    0H                                                               
         B     XIT                                                              
         EJECT                                                                  
*                                  QGET- READ FOR QSPEC                         
         SPACE 2                                                                
QGET     NTR1                                                                   
         XC    X,X                                                              
         MVC   X(8),QVVEH          SET VEH Q CODE                               
         LA    R2,X+7                                                           
         CLI   0(R2),C' '                                                       
         BH    *+8                                                              
         BCT   R2,*-8                                                           
         MVI   1(R2),C'.'                                                       
         MVC   2(L'QVQTYP,R2),QVQTYP                                            
         LA    R2,2+L'QVQTYP(R2)                                                
         CLI   0(R2),C' '                                                       
         BH    *+8                                                              
         BCT   R2,*-8                                                           
         LA    R0,X-1                                                           
         SR    R2,R0                                                            
*                                                                               
         GOTO1 MPQFAC,DMCB,(X'80',=C'EXEC'),MPQBLKD,((R2),X)                    
*                                                                               
         XC    AVECT,AVECT         CLEAR VECTOR ADDRESS                         
         CLI   MPQBERR,MPQEQSND    IF NOT DEFINED FOR WAVE                      
         BE    QGET4                                                            
         MVC   AVECT,MPQBSTK       SET VECTOR ADDRESS                           
         CLI   MPQBERR,0                                                        
         BE    *+6                                                              
         DC    H'0'                **NB- TEMP ABEND**                           
*                                                                               
QGET4    DS    0H                                                               
         BAS   RE,QVSET            ADD TO QVTAB                                 
*                                                                               
QGETX    DS    0H                                                               
         B     XIT                                                              
         DROP  R3                                                               
         SPACE 3                                                                
VSRCH    DS    0H                                                               
         L     R2,VLIST                                                         
         LA    R4,VLISTDL                                                       
         L     R5,VLISTX                                                        
         USING VLISTD,R2                                                        
*                                                                               
         CLC   VLQCD,WORK                                                       
         BER   RE                  EXIT CC = IF FOUND                           
         BXLE  R2,R4,*-8                                                        
         BR    RE                  EXIT CC NOT = IF NOT FOUND                   
         SPACE 3                                                                
BINSDEL  NTR1                      REMOVE ITEM FROM BINSRCH TABLE               
         L     R2,0(R1)            A(ITEM)                                      
         L     RF,8(R1)            LENGTH                                       
         L     R4,12(R1)           CURRENT COUNT                                
         LR    RE,R4                                                            
         BCTR  RE,R0                                                            
         ST    RE,12(R1)           SET NEW COUNT                                
*                                                                               
         MH    R4,10(R1)           COUNT X LENGTH                               
         A     R4,4(R1)            PLUS START = END OF TABLE                    
         BCTR  RF,R0                                                            
*                                                                               
BINSD4   DS    0H                                                               
         LA    RE,1(R2,RF)                                                      
         EX    RF,BDMVC                                                         
         LR    R2,RE                                                            
         CR    R2,R4                                                            
         BL    BINSD4                                                           
*                                                                               
         B     XIT                                                              
*                                                                               
BDMVC    MVC   0(0,R2),0(RE)                                                    
         SPACE 3                                                                
DIV      DIV   (R0),(RF)                                                        
         SPACE 3                                                                
EQUIV    DS    0H                  EQUIVALENCE TO BASE WAVE                     
         CLC   BWVPOP,TWVPOP                                                    
         BER   RE                                                               
         M     R0,BWVPOP                                                        
         L     RF,TWVPOP                                                        
         B     DIV                                                              
         SPACE 3                                                                
NEXTEL   DS    0H                                                               
         CLI   FELSW,C'Y'          IF FIRST TIME                                
         MVI   FELSW,C'N'          SET OFF SW                                   
         BE    NEXTEL2             AND USE FIRST ELEM                           
NEXTEL1  ZIC   R0,1(R2)                                                         
         AR    R2,R0                                                            
NEXTEL2  DS    0H                                                               
         CLI   0(R2),0                                                          
         BNE   NEXTEL4                                                          
         LTR   RE,RE                                                            
         BR    RE                                                               
NEXTEL4  DS    0H                                                               
         CLC   0(1,R2),ELCODE                                                   
         BNE   NEXTEL1                                                          
         BR    RE                                                               
         SPACE 3                                                                
         LTORG                                                                  
         EJECT                                                                  
*        CMDSCAN- SCAN PROCESSING COMMAND LIST                                  
         SPACE 2                                                                
CMDSCAN  NMOD1 0,CMDSCAN                                                        
         L     RC,0(R1)            A(WORK AREA)                                 
         L     R6,VMPQBLK                                                       
         USING MPQBLKD,R6                                                       
*                                                                               
         XC    HIRCH,HIRCH                                                      
         MVI   DATASWS,C'N'                                                     
         MVC   DATASWS+1(DATASWL-1),DATASWS                                     
         XC    DVDISPS(DVDISPL),DVDISPS                                         
*                                  SET VARIOUS DATA VECTOR DISPS                
         L     R4,RXPCMTAB                                                      
         USING PCMDD,R4                                                         
*                                                                               
CMS4     DS    0H                                                               
         CLI   PCBASE,0            EOL                                          
         BE    CMS5                                                             
*                                                                               
         CLI   PCQUAL,Q#           ONLY DEAL WITH COUNTS                        
         BNE   CMS4M               (ORIGINAL DATA)                              
*                                                                               
         CLI   PCBASE,BVCOD        VEHICLE CODE                                 
         BNE   CMS4D2                                                           
         LA    RF,VCODDSP                                                       
         B     CMS4K                                                            
*                                                                               
CMS4D2   DS    0H                                                               
         CLI   PCBASE,BVQCD        Q CODE                                       
         BNE   CMS4D3                                                           
         LA    RF,VQCDDSP                                                       
         B     CMS4K                                                            
*                                                                               
CMS4D3   DS    0H                                                               
         CLI   PCBASE,BVCTL        STATUS                                       
         BNE   CMS4D3B                                                          
         LA    RF,CTLDSP                                                        
         B     CMS4K                                                            
*                                                                               
CMS4D3B  DS    0H                                                               
         CLI   PCBASE,BVIMPF       IMPACT FACTOR                                
         BNE   CMS4D3D                                                          
         LA    RF,IMPFDSP                                                       
         B     CMS4K                                                            
*                                                                               
CMS4D3D  DS    0H                                                               
         CLI   PCBASE,BVDUPF       DUPLICATION FACTOR                           
         BNE   CMS4D3F                                                          
         LA    RF,DUPFDSP                                                       
         B     CMS4K                                                            
*                                                                               
CMS4D3F  DS    0H                                                               
         CLI   PCBASE,BVADJ        ADJUSTMENT                                   
         BNE   CMS4D4                                                           
         LA    RF,ADJDSP                                                        
         B     CMS4K                                                            
*                                                                               
CMS4D4   DS    0H                                                               
         CLI   PCBASE,BVINS        INSERT COUNT                                 
         BNE   CMS4D6                                                           
         MVI   SCHEDSW,C'Y'        SET DOING SCHEDULE                           
         MVI   C1SW,C'1'           AND C1'S                                     
         MVI   C2SW,C'Y'           AND C2'S                                     
         LA    RF,INSDSP                                                        
         B     CMS4K                                                            
*                                                                               
CMS4D6   DS    0H                                                               
         CLI   PCBASE,BAUD         AUDIENCE (C1)                                
         BNE   CMS4D8                                                           
         MVI   C1SW,C'Y'                                                        
         LA    RF,C1DSP                                                         
         B     CMS4K                                                            
*                                                                               
CMS4D8   DS    0H                                                               
         CLI   PCBASE,BBRD         BASE AUDIENCE                                
         BNE   CMS4E2                                                           
         MVI   BC1SW,C'Y'                                                       
         LA    RF,BC1DSP                                                        
         B     CMS4K                                                            
*                                                                               
CMS4E2   DS    0H                                                               
         CLI   PCBASE,BNET         PAIR NETS                                    
         BNE   CMS4E4                                                           
         MVI   PNSW,C'Y'                                                        
         MVI   C1SW,C'Y'           ALSO NEED C1'S                               
         B     CMS4M                                                            
*                                                                               
CMS4E4   DS    0H                                                               
         CLI   PCBASE,BDUP         PAIR DUPES                                   
         BNE   CMS4E6                                                           
         MVI   PDSW,C'Y'                                                        
         MVI   C1SW,C'Y'           ALSO NEED C1'S                               
         B     CMS4M                                                            
*                                                                               
CMS4E6   DS    0H                                                               
         CLI   PCBASE,BSUM         PAIR SUMS                                    
         BNE   CMS4E7                                                           
         MVI   PSSW,C'Y'                                                        
         MVI   C1SW,C'Y'           ALSO NEED C1'S                               
         B     CMS4M                                                            
*                                                                               
CMS4E7   DS    0H                                                               
         CLI   PCBASE,BUNIQ        UNIQUE CONTRIBUTION                          
         BNE   CMS4E8                                                           
         MVI   UNIQSW,C'Y'                                                      
         B     CMS4M                                                            
*                                                                               
CMS4E8   DS    0H                                                               
         CLI   PCBASE,BRCH         REACH                                        
         BNE   CMS4G                                                            
         MVI   C1SW,C'Y'           NEED C1'S                                    
         MVI   C2SW,C'Y'           ALS0 NEED C2'S                               
         MVI   RCHNSW,C'Y'         SET DOING REACHES                            
         SR    RF,RF                                                            
         ICM   RF,3,PCSFX                                                       
         C     RF,HIRCH            SAVE HIGHEST                                 
         BNH   *+8                                                              
         ST    RF,HIRCH                                                         
         B     CMS4M                                                            
*                                                                               
CMS4G    DS    0H                                                               
         B     CMS4M                                                            
*                                                                               
CMS4K    DS    0H                                                               
         MVC   0(4,RF),PCWHERE     SET DISPLACEMENT                             
*                                                                               
CMS4M    DS    0H                                                               
         LA    R4,PCMDL(R4)        NEXT COMMAND                                 
         B     CMS4                                                             
*                                                                               
CMS5     DS    0H                                                               
CMSX     XIT1                                                                   
         DROP  R4                                                               
         LTORG                                                                  
         EJECT                                                                  
*        CORMAP - SET CORE MAP                                                  
         SPACE 2                                                                
CORMAP   NMOD1 0,CORMAP                                                         
         L     RC,0(R1)            A(WORK AREA)                                 
         L     R6,VMPQBLK                                                       
         USING MPQBLKD,R6                                                       
*                                                                               
         XC    ADDRS(ADDRSL),ADDRS                                              
*                                                                               
*              RESP COUNT AND BIT VECTOR LENGTH                                 
*              --------------------------------                                 
*                                                                               
         MVC   NRES,MPQBNRES       SET RESP COUNT DIRECTLY                      
         CLI   SURWMRGE,C'Y'       IF WAVES MERGED                              
         BE    CORM5                                                            
*                                  ELSE SET FROM LARGEST WAVE                   
         LA    R5,SURWAVS                                                       
         USING SURWVD,R5                                                        
*                                                                               
CORM3    DS    0H                                                               
         CLI   0(R5),X'FF'         END OF SURVEY WAVE LIST                      
         BE    CORM5                                                            
*                                                                               
         CLC   NRES,SURWVRC        TEST ALREADY HAVE LARGER                     
         BNL   *+10                                                             
         MVC   NRES,SURWVRC                                                     
*                                                                               
         LA    R5,SURWVDL(R5)                                                   
         B     CORM3                                                            
*                                                                               
CORM5    DS    0H                                                               
         L     RF,NRES             SET BIT VECTOR LENGTH                        
         LA    RF,7(RF)                                                         
         SRL   RF,3                                                             
         ST    RF,BVLEN                                                         
*                                                                               
         DROP  R5                                                               
         EJECT                                                                  
*                                                                               
*        SET VARIOUS ADDRESSES                                                  
*        ---------------------                                                  
*                                                                               
         L     R3,RXCORE           R3 FOR CORE POINTER                          
*                                                                               
         ST    R3,WVTAB            WAVE TABLE                                   
         LA    R3,WVTABLN(R3)                                                   
*                                                                               
         ST    R3,VLIST            VEHICLE LIST                                 
         A     R3,=A((VLISTDL*MAXVEHS)+1)                                       
*                                                                               
         ST    R3,QVTAB            Q/VECTOR LIST                                
         A     R3,=A((QVTABDL*MAXQVS)+1)                                        
*                                                                               
         ST    R3,CNTAB            COUNT TABLE                                  
         A     R3,=A((CNTABDL*MAXCTS)+1)                                        
*                                                                               
         ST    R3,PRTAB            PAIR COUNT TABLE                             
         A     R3,=A((PRTABDL*MAXPRS)+1)                                        
*                                                                               
         LA    R3,7(R3)            ALIGN FOR WGT AREA                           
         SRL   R3,3                                                             
         SLL   R3,3                                                             
         ST    R3,WSVAREA          WGT SAVE AREA                                
         ZIC   R1,MPQBWDLN         WGT ITEM LENGTH                              
         M     R0,NRES             X NRES                                       
         AR    R3,R1                                                            
*                                                                               
         LA    R3,7(R3)            ALIGN FOR TARG WGT AREA                      
         SRL   R3,3                                                             
         SLL   R3,3                                                             
         ST    R3,TRGWV            TARG COMPONENT WGT AREA                      
         LA    R1,2                VECTOR ITEM LENGTH **(ASSUME 2)              
         M     R0,NRES                                                          
         AR    R3,R1                                                            
*                                                                               
         ST    R3,TRGBV            TARGET BIT VECTOR                            
         A     R3,BVLEN                                                         
*                                                                               
         ST    R3,BASBV            BASE BIT VECTOR                              
         A     R3,BVLEN                                                         
*                                                                               
         ST    R3,BVW1             TWO WORK BIT VECTORS                         
         A     R3,BVLEN                                                         
         ST    R3,BVW2                                                          
         A     R3,BVLEN                                                         
*                                                                               
         LA    R3,7(R3)            ALIGN FOR ARRAYS                             
         SRL   R3,3                                                             
         SLL   R3,3                                                             
*                                                                               
         LA    R4,MAXVEHS*4        MAXIMUM VEHICLES X 4                         
*                                                                               
         ST    R3,INSARR           INSERT COUNT ARRAY                           
         AR    R3,R4                                                            
         ST    R3,C1ARR            C1 POP ARRAY                                 
         AR    R3,R4                                                            
         ST    R3,C2ARR            C2 POP ARRAY                                 
         AR    R3,R4                                                            
         ST    R3,IMPFARR          IMPACT FACTOR ARRAY                          
         AR    R3,R4                                                            
         ST    R3,VERRARR          VEHICLE ERROR ARRAY                          
         AR    R3,R4                                                            
         ST    R3,PERRARR          PAIR ERROR ARRAY                             
         AR    R3,R4                                                            
*                                                                               
         LH    R4,=Y(MAXPRS*4)     MAXIMUM PAIRS X 4                            
*                                                                               
         ST    R3,PNARR            PAIR NET POP ARRAY                           
         AR    R3,R4                                                            
*                                                                               
         LA    R4,(MAXINS+1)*4     MAXIMUM INSERTS X 4                          
*                                                                               
         ST    R3,RXFREQPA         FREQ ARRAY -POPULATION                       
         AR    R3,R4                                                            
         ST    R3,RXFREQRA         AND RESPONSE WEIGHTED                        
         AR    R3,R4                                                            
*                                                                               
         ST    R3,RVSAV            START OF VECTOR SAVE AREA                    
         ST    R3,NXTVECT          VECTOR SLOT POINTER                          
*                                                                               
         L     R1,RXCOREX                                                       
         S     R1,RVSAV            REMAINING CORE                               
         SR    R0,R0                                                            
         D     R0,BVLEN            / VECTOR LENGTH                              
         ST    R1,MAXVECTS         = MAXIMUM VECTORS                            
         XC    NVECTS,NVECTS                                                    
*                                                                               
*                                  SET QVTAB BINSRCH PARS                       
         SR    R0,R0                                                            
         L     R1,QVTAB                                                         
         SR    R2,R2                                                            
         LA    R3,QVTABDL                                                       
         LA    R4,QVKEYL           KEY LENGTH                                   
         LA    R5,MAXQVS                                                        
         STM   R0,R5,QVPARS                                                     
*                                  SET WVTAB BINSRCH PARS                       
         SR    R0,R0                                                            
         L     R1,WVTAB                                                         
         SR    R2,R2                                                            
         LA    R3,WVTABDL                                                       
         LA    R4,2                KEY LENGTH                                   
         LA    R5,MAXWAVS                                                       
         STM   R0,R5,WVPARS                                                     
*                                  SET CNTAB BINSRCH PARS                       
         SR    R0,R0                                                            
         L     R1,CNTAB                                                         
         SR    R2,R2                                                            
         LA    R3,CNTABDL                                                       
         LA    R4,CNKEYL           KEY LENGTH                                   
         LA    R5,MAXCTS                                                        
         STM   R0,R5,CNPARS                                                     
*                                  SET PRTAB BINSRCH PARS                       
         SR    R0,R0                                                            
         L     R1,PRTAB                                                         
         SR    R2,R2                                                            
         LA    R3,PRTABDL                                                       
         LA    R4,PRKEYL           KEY LENGTH                                   
         LH    R5,=Y(MAXPRS)                                                    
         STM   R0,R5,PRPARS                                                     
*                                                                               
CORMX    XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*        BVLIST- BUILD VEHICLE LIST                                             
         SPACE 2                                                                
BVLIST   NMOD1 0,BVLIST                                                         
         L     RC,0(R1)            A(WORK AREA)                                 
         L     R6,VLIST                                                         
         USING VLISTD,R6                                                        
         L     R4,RXDVTAB                                                       
         ICM   R5,15,RXNDVS           FOR BCT                                   
         BZ    BVL7                                                             
         SR    R2,R2                  FOR LINE COUNT                            
*                                                                               
BVL4     DS    0H                                                               
         XC    0(VLISTDL,R6),0(R6)                                              
         LR    R3,R4                                                            
         A     R3,VCODDSP          VEH CODE                                     
         CLI   0(R3),0             SKIP ANY SPECIALS                            
         BE    BVL6                                                             
         CLI   0(R3),X'F9'         OR TOTALS                                    
         BH    BVL6                                                             
         MVC   VLCOD,0(R3)                                                      
         LR    R3,R4                                                            
         A     R3,VQCDDSP          Q CODE                                       
         MVC   VLQCD,0(R3)                                                      
         OC    INSDSP,INSDSP       TEST INSERTS PRESENT                         
         BZ    BVL4D                                                            
         LR    R3,R4                                                            
         A     R3,INSDSP           INSERTS                                      
         MVC   VLINS,2(R3)                                                      
*                                                                               
BVL4D    DS    0H                                                               
         LR    R3,R4                                                            
         A     R3,CTLDSP           CONTROL                                      
         MVC   VLCTL,0(R3)         SET SOME OF CONTROL INFO IN VLIST            
         STCM  R4,15,VLDVCT        A(DATA VECTOR)                               
*                                                                               
         LA    R2,1(R2)                                                         
         LA    R6,VLISTDL(R6)      NEXT VLIST ENTRY                             
*                                                                               
BVL6     DS    0H                                                               
         A     R4,RXDVL            NEXT VECTOR                                  
         BCT   R5,BVL4                                                          
*                                                                               
BVL7     DS    0H                                                               
         MVI   0(R6),X'FF'         SET EOL                                      
         ST    R6,VLISTX                                                        
         XC    TNINS,TNINS                                                      
         ST    R2,NVEHS            SET VEHICLE COUNT                            
         ST    R2,NLINS            AND LINE COUNT                               
         CLI   SCHEDSW,C'Y'        FOR NON-SCHED RUNS                           
         BNE   BVLX                                                             
*                                  ELSE SORT AND MASSAGE VEHICLE LIST           
         GOTO1 XSORT,DMCB,VLIST,(R2),VLISTDL,VLKEYL,0                           
*                                                                               
         L     R6,VLIST                                                         
         SR    R2,R2                                                            
         XC    LASTVEH,LASTVEH                                                  
*                                                                               
BVL21    DS    0H                                                               
         CLI   0(R6),X'FF'         EOL                                          
         BE    BVL21B                                                           
         CLC   VLCOD,LASTVEH       TEST VEHICLE BREAK                           
         BE    BVL22               NO                                           
*                                                                               
BVL21B   DS    0H                  YES                                          
         OC    LASTVEH,LASTVEH     TEST FIRST TIME                              
         BZ    BVL22                                                            
*                                                                               
         LR    RF,R6               MARK AS LAST OCCUR OF VEH                    
         SH    RF,=Y(VLISTDL-(VLINS-VLISTD))  POINT TO PREV VLINS               
         OI    0(RF),X'80'                                                      
*                                                                               
         LA    R2,1(R2)            BUMP ACTUAL VEHICLE COUNT                    
         CH    R2,=Y(MAXSCHD)      TEST VS MAX FOR SCHEDULE                     
         BNH   *+6                                                              
         DC    H'0'                                                             
*                                                                               
BVL22    DS    0H                                                               
         CLI   0(R6),X'FF'         EOL                                          
         BE    BVL60                                                            
         MVC   LASTVEH,VLCOD                                                    
         SR    RF,RF                                                            
         ICM   RF,3,VLINS          BUMP TOTAL INSERT COUNT                      
         A     RF,TNINS                                                         
         ST    RF,TNINS                                                         
         LA    RF,1(R2)                                                         
         STC   RF,VLVEHNO          SET SCHED VEHICLE NUMBER (MAX 255)           
*                                                                               
BVL59    DS    0H                                                               
         LA    R6,VLISTDL(R6)                                                   
         B     BVL21                                                            
*                                                                               
BVL60    DS    0H                                                               
         ST    R2,NVEHS                                                         
*                                                                               
BVLX     XIT1                                                                   
         DROP  R6                                                               
         LTORG                                                                  
         EJECT                                                                  
*        RXTRCE- 'TRACE' - PRINT TABLES AND ARRAYS                              
         SPACE 2                                                                
RXTRCE   NMOD1 0,RXTRCE                                                         
         L     RC,0(R1)            A(WORK AREA)                                 
         L     R6,ASPOOLD                                                       
         USING SPOOLD,R6                                                        
*                                                                               
         MVI   SPACING,2                                                        
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
         LA    R4,TOTWDS                                                        
         LA    R5,RXGLOBCS                                                      
         LA    R3,6                                                             
*                                                                               
RXTR08   DS    0H                                                               
         MVC   P(20),0(R4)                                                      
         EDIT  (B4,0(R5)),(10,P+21),COMMAS=YES,ZERO=NOBLANK                     
         GOTO1 SPOOL,DMCB,SPOOLD                                                
         LA    R4,20(R4)                                                        
         LA    R5,4(R5)                                                         
         BCT   R3,RXTR08                                                        
*                                                                               
         L     R4,WVPARS+4         WAVE TAB                                     
         USING WVTABD,R4                                                        
         ICM   R5,15,WVPARS+8                                                   
         BZ    RXTR2G                                                           
         GOTO1 SPOOL,DMCB,SPOOLD                                                
         MVC   P(9),=C'**WVTAB**'                                               
         MVC   P2+10(69),=C'STAT    SAMPLE        POP   BASE SAM   BASEX        
                POP   TARG SAM   TARG POP'                                      
         MVI   SPACING,2                                                        
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
RXTR2    DS    0H                                                               
         MVC   P(3),=C'ALL'                                                     
         CLI   WVDATE,X'FF'                                                     
         BE    RXTR2D                                                           
         GOTO1 DATCON,DMCB,(2,WVDATE),(5,P)                                     
RXTR2D   DS    0H                                                               
         GOTO1 HEXOUT,DMCB,WVSTAT,P+10,1,=C'N'                                  
         LA    R3,P+14                                                          
         LA    RE,WVSAM                                                         
         LA    R2,6                                                             
*                                                                               
RXTR2E   DS    0H                                                               
         EDIT  (B4,0(RE)),(10,0(R3)),ZERO=NOBLANK                               
         LA    R3,11(R3)                                                        
         LA    RE,4(RE)                                                         
         BCT   R2,RXTR2E                                                        
*                                                                               
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
         A     R4,WVPARS+12        NEXT ENTRY                                   
         BCT   R5,RXTR2                                                         
*                                                                               
RXTR2G   DS    0H                                                               
         L     R4,CNPARS+4         CNTAB                                        
         USING CNTABD,R4                                                        
         ICM   R5,15,CNPARS+8                                                   
         BZ    RXTR3G                                                           
         GOTO1 SPOOL,DMCB,SPOOLD                                                
         MVC   P(9),=C'**CNTAB**'                                               
         MVI   SPACING,2                                                        
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
RXTR3    DS    0H                                                               
         MVC   P(8),CNQVEH                                                      
         MVC   P+9(1),CNTYP                                                     
         CLI   CNWAVE,X'FF'                                                     
         BE    RXTR3D                                                           
         GOTO1 DATCON,DMCB,(2,CNWAVE),(5,P+11)                                  
RXTR3D   DS    0H                                                               
         EDIT  (B4,CNSAM),(10,P+22),COMMAS=YES,ZERO=NOBLANK                     
         EDIT  (B4,CNPOP),(10,P+33),COMMAS=YES,ZERO=NOBLANK                     
         EDIT  (B1,CNCALLNO),(3,P+44),ZERO=NOBLANK                              
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
         A     R4,CNPARS+12        NEXT ENTRY                                   
         BCT   R5,RXTR3                                                         
*                                                                               
RXTR3G   DS    0H                                                               
         L     R4,PRPARS+4         PRTAB                                        
         USING PRTABD,R4                                                        
         ICM   R5,15,PRPARS+8                                                   
         BZ    RXTR4                                                            
         GOTO1 SPOOL,DMCB,SPOOLD                                                
         MVC   P(9),=C'**PRTAB**'                                               
         MVI   SPACING,2                                                        
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
RXTR3J   DS    0H                                                               
         MVC   P(8),PRQVEH1                                                     
         MVC   P+9(8),PRQVEH2                                                   
         CLI   PRWAVE,X'FF'                                                     
         BE    RXTR3K                                                           
         GOTO1 DATCON,DMCB,(2,PRWAVE),(5,P+19)                                  
RXTR3K   DS    0H                                                               
         EDIT  (B4,PRPOP),(10,P+33),COMMAS=YES,ZERO=NOBLANK                     
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
         A     R4,PRPARS+12        NEXT ENTRY                                   
         BCT   R5,RXTR3J                                                        
*                                                                               
RXTR4    DS    0H                                                               
         L     R4,QVPARS+4         QVTAB                                        
         USING QVTABD,R4                                                        
         ICM   R5,15,QVPARS+8                                                   
         BZ    RXTR6                                                            
         GOTO1 SPOOL,DMCB,SPOOLD                                                
         MVC   P(9),=C'**QVTAB**'                                               
         MVI   SPACING,2                                                        
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
RXTR5    DS    0H                                                               
         MVC   P(8),QVVEH                                                       
         MVC   P+9(3),QVQTYP                                                    
         CLI   QVWAVE,X'FF'                                                     
         BE    RXTR5D                                                           
         GOTO1 DATCON,DMCB,(2,QVWAVE),(5,P+13)                                  
RXTR5D   DS    0H                                                               
         GOTO1 HEXOUT,DMCB,QVVECT,P+22,4,=C'N'                                  
         EDIT  (B1,QVCALLNO),(3,P+31),ZERO=NOBLANK                              
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
         A     R4,QVPARS+12        NEXT ENTRY                                   
         BCT   R5,RXTR5                                                         
*                                                                               
RXTR6    DS    0H                                                               
         GOTO1 SPOOL,DMCB,SPOOLD                                                
         MVC   P(9),=C'**VLIST**'                                               
         MVC   P2(78),=C'QCOD     PUB CODE    VEH#  CTL     INS  VECTORX        
                  WC WAVB         C1         C2'                                
         MVI   SPACING,2                                                        
         GOTO1 SPOOL,DMCB,SPOOLD                                                
         L     R5,VLIST                                                         
         USING VLISTD,R5                                                        
*                                                                               
RXTR8    DS    0H                                                               
         CLI   0(R5),X'FF'         EOL                                          
         BE    RXTR10                                                           
*                                                                               
         MVC   P(8),VLQCD                                                       
         MVC   P+9(12),VLCOD                                                    
         EDIT  (B1,VLVEHNO),(3,P+22),ZERO=NOBLANK                               
         GOTO1 HEXOUT,DMCB,VLCTL,P+27,2,=C'N'                                   
         MVC   HALF,VLINS                                                       
         NI    HALF,X'7F'                                                       
         EDIT  (B2,HALF),(3,P+35),ZERO=NOBLANK                                  
         TM    VLINS,X'80'                                                      
         BZ    *+8                                                              
         MVI   P+38,C'*'           LAST OCCURENCE OF VEH                        
         GOTO1 HEXOUT,DMCB,VLDVCT,P+40,4,=C'N'                                  
         EDIT  (B1,VLNWVS),(2,P+49),ZERO=NOBLANK                                
         GOTO1 (RF),(R1),VLWVBLST,P+52,2,=C'N'                                  
         EDIT  (B4,VLC1),(10,P+57),COMMAS=YES,ZERO=NOBLANK                      
         EDIT  (B4,VLC2),(10,P+68),COMMAS=YES,ZERO=NOBLANK                      
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
         LA    R5,VLISTDL(R5)      NEXT ENTRY                                   
         B     RXTR8                                                            
*                                                                               
RXTR10   DS    0H                                                               
         CLI   SCHEDSW,C'Y'                                                     
         BNE   RXTR15                                                           
*                                                                               
         GOTO1 SPOOL,DMCB,SPOOLD                                                
         MVC   P(18),=C'**VEHICLE ARRAYS**'                                     
         MVC   P2+8(57),=C'C1         C2        INS       IMPF      ERRX        
               OR   PAIR ERR'                                                   
         MVI   SPACING,2                                                        
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
         ICM   R2,15,NVEHS                                                      
         BZ    RXTR13                                                           
*                                                                               
         SR    R5,R5                                                            
*                                                                               
RXTR12   DS    0H                                                               
         LA    R3,P                                                             
         L     R4,C1ARR                                                         
         AR    R4,R5                                                            
         EDIT  (B4,0(R4)),(10,0(R3)),COMMAS=YES,ZERO=NOBLANK                    
*                                                                               
         LA    R3,11(R3)                                                        
         L     R4,C2ARR                                                         
         AR    R4,R5                                                            
         EDIT  (B4,0(R4)),(10,0(R3)),COMMAS=YES,ZERO=NOBLANK                    
*                                                                               
         LA    R3,11(R3)                                                        
         L     R4,INSARR                                                        
         AR    R4,R5                                                            
         EDIT  (B4,0(R4)),(10,0(R3)),COMMAS=YES,ZERO=NOBLANK                    
*                                                                               
         LA    R3,11(R3)                                                        
         L     R4,IMPFARR                                                       
         AR    R4,R5                                                            
         EDIT  (B4,0(R4)),(10,0(R3)),3                                          
*                                                                               
         LA    R3,11(R3)                                                        
         L     R4,VERRARR                                                       
         AR    R4,R5                                                            
         EDIT  (B4,0(R4)),(10,0(R3)),3                                          
*                                                                               
         LA    R3,11(R3)                                                        
         L     R4,PERRARR                                                       
         AR    R4,R5                                                            
         EDIT  (B4,0(R4)),(10,0(R3)),3                                          
*                                                                               
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
         LA    R5,4(R5)                                                         
         BCT   R2,RXTR12                                                        
*                                                                               
RXTR13   DS    0H                                                               
         GOTO1 SPOOL,DMCB,SPOOLD                                                
         MVC   P(14),=C'**PAIR ARRAY**'                                         
         MVI   SPACING,2                                                        
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
         L     R1,NVEHS            CALC NO. OF PAIRS                            
         CH    R1,=H'1'                                                         
         BNH   RXTR15                                                           
         LR    R0,R1                                                            
         BCTR  R1,R0               (NVEHS*(NVEHS-1))/2                          
         MR    R0,R0                                                            
         SRL   R1,1                                                             
         LR    R2,R1                                                            
*                                                                               
         SR    R5,R5                                                            
         LA    R3,P                                                             
*                                                                               
RXTR14   DS    0H                                                               
         L     R4,PNARR                                                         
         AR    R4,R5                                                            
         EDIT  (B4,0(R4)),(10,0(R3)),COMMAS=YES,ZERO=NOBLANK                    
*                                                                               
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
         LA    R5,4(R5)                                                         
         BCT   R2,RXTR14                                                        
*                                                                               
RXTR15   DS    0H                  FREQUENCY ARRAYS                             
         ICM   R5,15,RXINS         INSERT COUNT                                 
         BZ    RXTR26                                                           
         LA    R5,1(R5)            ONE MORE FOR ZERO LEVEL                      
         GOTO1 SPOOL,DMCB,SPOOLD                                                
         MVC   P(16),=C'FREQUENCY ARRAYS'                                       
         MVC   P2+10(17),=C'UNWGTD       WGTD'                                  
         MVI   SPACING,2                                                        
         GOTO1 SPOOL,DMCB,SPOOLD                                                
         L     R3,RXFREQPA         POPS                                         
         L     R4,RXFREQRA         WGTD DATA                                    
         SR    R2,R2                                                            
*                                                                               
RXTR25   DS    0H                                                               
         EDIT  (B4,0(R3)),(10,P+6),COMMAS=YES,ZERO=NOBLANK                      
         EDIT  (B4,0(R4)),(10,P+17),COMMAS=YES,ZERO=NOBLANK                     
         EDIT  (R2),(3,P+2),ZERO=NOBLANK                                        
         GOTO1 SPOOL,DMCB,SPOOLD                                                
         LA    R3,4(R3)                                                         
         LA    R4,4(R4)                                                         
         LA    R2,1(R2)                                                         
         BCT   R5,RXTR25                                                        
*                                                                               
RXTR26   DS    0H                                                               
         CLI   RXERROR,0                                                        
         BE    RXTR27                                                           
*                                                                               
         MVC   P(8),=C'**ERROR='                                                
         GOTO1 HEXOUT,DMCB,RXERROR,P+10,9,=C'N'                                 
         GOTO1 SPOOL,DMCB,SPOOLD                                                
*                                                                               
RXTR27   DS    0H                  DATA VECTOR TABLE                            
         MVC   P(21),=C'**DATA VECTOR TABLE**'                                  
         MVI   SPACING,2                                                        
         GOTO1 SPOOL,DMCB,SPOOLD                                                
         MVC   DVPNTR,RXDVTAB                                                   
*                                                                               
RXTR41   DS    0H                                                               
         LA    R5,P4                                                            
         LA    R3,P                                                             
         L     R4,RXPCMTAB                                                      
         USING PCMDD,R4                                                         
*                                                                               
RXTR42   DS    0H                                                               
         CLI   PCBASE,0            EOL                                          
         BE    RXTR80                                                           
         CLI   PCQUAL,Q#           DO ONLY BASE DATA                            
         BNE   RXTR78                                                           
         ICM   R2,15,PCWHERE       SLOT FOR DATA                                
         A     R2,DVPNTR                                                        
*                                                                               
         CLI   PCBASE,BVCOD        VEHICLE CODE                                 
         BNE   RXTR43                                                           
         MVC   0(8,R5),=C'PUB CODE'                                             
         MVC   0(12,R3),0(R2)                                                   
         LA    R1,12                                                            
         B     RXTR77                                                           
*                                                                               
RXTR43   DS    0H                                                               
         CLI   PCBASE,BVQCD        Q CODE                                       
         BNE   RXTR44                                                           
         MVC   0(6,R5),=C'Q CODE'                                               
         MVC   0(8,R3),0(R2)                                                    
         LA    R1,8                                                             
         B     RXTR77                                                           
*                                                                               
RXTR44   DS    0H                                                               
         CLI   PCBASE,BVCTL        CONTROL                                      
         BNE   RXTR45                                                           
         MVC   0(7,R5),=C'CONTROL'                                              
         GOTO1 HEXOUT,DMCB,(R2),(R3),12,=C'N'                                   
         LA    R1,24                                                            
         B     RXTR77                                                           
*                                                                               
RXTR45   DS    0H                                                               
         CLI   PCBASE,BVADJ        ADJUSTMENT                                   
         BNE   RXTR46                                                           
         MVC   0(6,R5),=C'ADJUST'                                               
         B     RXTR76                                                           
*                                                                               
RXTR46   DS    0H                                                               
         CLI   PCBASE,BVIMPF       IMPACT FACTOR                                
         BNE   RXTR47                                                           
         MVC   0(6,R5),=C'IMPACT'                                               
         B     RXTR76                                                           
*                                                                               
RXTR47   DS    0H                                                               
         CLI   PCBASE,BVDUPF       PROTO DUPLICATION FACTOR                     
         BNE   RXTR48                                                           
         MVC   0(6,R5),=C'PRODUP'                                               
         B     RXTR76                                                           
*                                                                               
RXTR48   DS    0H                                                               
         CLI   PCBASE,BAUD         AUDIENCE (C1)                                
         BNE   RXTR49                                                           
         MVC   1(8,R5),=C'AUDIENCE'                                             
         B     RXTR75                                                           
*                                                                               
RXTR49   DS    0H                                                               
         CLI   PCBASE,BBRD         BASE AUDIENCE                                
         BNE   RXTR50                                                           
         MVC   1(8,R5),=C'BASE AUD'                                             
         B     RXTR75                                                           
*                                                                               
RXTR50   DS    0H                                                               
         CLI   PCBASE,BVINS        INSERT COUNT                                 
         BNE   RXTR51                                                           
         MVC   2(7,R5),=C'INSERTS'                                              
         B     RXTR75                                                           
*                                                                               
RXTR51   DS    0H                                                               
         CLI   PCBASE,BRCH         REACH N                                      
         BNE   RXTR52                                                           
         MVC   1(8,R5),=C'RCH-XXXX'                                             
         B     RXTR74                                                           
*                                                                               
RXTR52   DS    0H                                                               
         CLI   PCBASE,BUNIQ        UNIQUE N                                     
         BNE   RXTR53                                                           
         MVC   1(8,R5),=C'UNI-XXXX'                                             
         B     RXTR74                                                           
*                                                                               
RXTR53   DS    0H                                                               
         CLI   PCBASE,BNET         NET                                          
         BNE   RXTR54                                                           
         MVC   1(8,R5),=C'NET-XXXX'                                             
         B     RXTR74                                                           
*                                                                               
RXTR54   DS    0H                                                               
         CLI   PCBASE,BDUP         DUPE                                         
         BNE   RXTR55                                                           
         MVC   1(8,R5),=C'DUP-XXXX'                                             
         B     RXTR74                                                           
*                                                                               
RXTR55   DS    0H                                                               
         CLI   PCBASE,BSUM         SUM                                          
         BNE   RXTR56                                                           
         MVC   1(8,R5),=C'SUM-XXXX'                                             
         B     RXTR74                                                           
*                                                                               
RXTR56   DS    0H                                                               
         B     RXTR78                                                           
*                                                                               
RXTR74   DS    0H                  SET SUFFIX                                   
         GOTO1 HEXOUT,DMCB,PCSFX,5(R5),2,=C'N'                                  
*                                                                               
RXTR75   DS    0H                                                               
         EDIT  (B4,0(R2)),(9,0(R3)),ZERO=NOBLANK                                
         LA    R1,9                                                             
         B     RXTR77                                                           
*                                                                               
RXTR76   DS    0H                                                               
         EDIT  (B4,0(R2)),(6,0(R3)),3                                           
         LA    R1,6                                                             
*                                                                               
RXTR77   DS    0H                                                               
         LA    R5,1(R5,R1)         NEXT LINE POSITION                           
         LA    R3,1(R3,R1)                                                      
*                                                                               
RXTR78   DS    0H                                                               
         LA    R4,PCMDL(R4)        NEXT COMMAND                                 
         B     RXTR42                                                           
*                                                                               
RXTR80   DS    0H                                                               
         L     R2,DVPNTR           CURRENT DATA VECTOR                          
         C     R2,RXDVTAB          TEST FIRST LINE                              
         BNE   RXTR82                                                           
         MVC   P3,P1                                                            
         MVI   P2,0                                                             
         MVC   P1,P4                                                            
*                                                                               
RXTR82   DS    0H                                                               
         MVC   P4,SPACES                                                        
         GOTO1 SPOOL,DMCB,SPOOLD                                                
         A     R2,RXDVL            NEXT VECTOR                                  
         ST    R2,DVPNTR                                                        
         C     R2,DVTABX           TEST AT END                                  
         BL    RXTR41                                                           
*                                                                               
RXTRCX   XIT1                                                                   
         SPACE 3                                                                
         DS    0D                                                               
TOTWDS   DS    0X                                                               
         DC    CL20'SURVEY SAMPLE SIZE ='                                       
         DC    CL20'       POPULATION  ='                                       
         DC    CL20'BASE   SAMPLE SIZE ='                                       
         DC    CL20'       POPULATION  ='                                       
         DC    CL20'TARGET SAMPLE SIZE ='                                       
         DC    CL20'       POPULATION  ='                                       
         SPACE 2                                                                
         LTORG                                                                  
         SPACE 3                                                                
*        DSECT TO COVER WAVE TABLE                                              
         SPACE 2                                                                
WVTABD   DSECT                                                                  
WVDATE   DS    XL2                 WAVE DATE                                    
WVSTAT   DS    XL1                 STATUS  - X'80'= ACTIVE THIS REQ             
*                                            X'40'= ALREADY DONE                
         DS    XL1                 SPARE                                        
WVSAM    DS    XL4                 SURVEY SAMPLE                                
WVPOP    DS    XL4                 POPULATION                                   
WVBASSAM DS    XL4                 BASE SAMPLE                                  
WVBASPOP DS    XL4                 POPULATION                                   
WVTRGSAM DS    XL4                 TARGET SAMPLE                                
WVTRGPOP DS    XL4                 POPULATION                                   
WVTABDL  EQU   *-WVTABD                                                         
         SPACE 3                                                                
*        DSECT TO COVER VEHICLE LIST                                            
         SPACE 2                                                                
VLISTD   DSECT                                                                  
VLQCD    DS    CL8                 Q CODE                                       
VLCOD    DS    CL12                VEH CODE                                     
VLKEYL   EQU   *-VLISTD            'KEY' LENGTH                                 
VLCTL    DS    XL2                 2 BYTES FROM VECTOR 'CONTROL'                
VLVEHNO  DS    XL1                 VEHNO FOR SCHEDULES                          
VLINS    DS    XL2                 INSERTIONS                                   
VLDVCT   DS    XL4                 A(DATA VECTOR)                               
VLC1     DS    XL4                 C1 POP                                       
VLC2     DS    XL4                 C2 POP                                       
VLC1W    DS    XL4                 C1 POP FOR CURRENT WAVE                      
VLBC1    DS    XL4                 BASE C1 POP                                  
VLC1SAM  DS    XL4                 C1 SAMPLE SIZE                               
VLNWVS   DS    XL1                 NUMBER OF WAVES FOR VEH                      
VLWVBLST DS    XL2                 BITS FOR ACTIVE WAVES                        
VLISTDL  EQU   *-VLISTD                                                         
         SPACE 3                                                                
*        DSECT TO COVER QVTAB                                                   
         SPACE 2                                                                
QVTABD   DSECT                                                                  
QVVEH    DS    CL8                 QSPEC VEHICLE CODE                           
QVQTYP   DS    CL3                 QSPEC TYPE CODE                              
QVWAVE   DS    XL2                 WAVE DATE                                    
QVKEYL   EQU   *-QVTABD            'KEY' LENGTH                                 
QVVECT   DS    XL4                 VECTOR ADDRESS (0 = 'NOT DEFINED')           
QVCALLNO DS    XL1                 MOST RECENT CALL NUMBER                      
QVTABDL EQU    *-QVTABD                                                         
         SPACE 3                                                                
*        DSECT TO COVER COUNT TABLE                                             
         SPACE 2                                                                
CNTABD   DSECT                                                                  
CNQVEH   DS    CL8                 QSPEC VEHICLE CODE                           
CNTYP    DS    CL1                 TYPE (1=C1, 2=C2)                            
CNWAVE   DS    XL2                 WAVE DATE                                    
CNKEYL   EQU   *-CNTABD            'KEY' LENGTH                                 
CNSAM    DS    XL4                 SAMPLE                                       
CNPOP    DS    XL4                 POP                                          
CNCALLNO DS    XL1                 MOST RECENT CALL NUMBER                      
CNTABDL EQU    *-CNTABD                                                         
         SPACE 3                                                                
*        DSECT TO COVER PAIR COUNT TABLE                                        
         SPACE 2                                                                
PRTABD   DSECT                                                                  
PRQVEH1  DS    CL8                 FIRST VEHICLE                                
PRQVEH2  DS    CL8                 SECOND                                       
PRWAVE   DS    XL2                 WAVE DATE                                    
PRKEYL   EQU   *-PRTABD            'KEY' LENGTH                                 
PRPOP    DS    XL4                 POP                                          
PRTABDL EQU    *-PRTABD                                                         
         SPACE 3                                                                
MAXQVS   EQU   2000                MAXIMUM QVTAB ENTRIES                        
MAXCTS   EQU   2400                MAXIMUM SAVED COUNTS                         
MAXPRS   EQU   (MAXSCHD*(MAXSCHD-1))/2    MAXIMUM PAIRS (SCHEDULE)              
ARRAYTL  EQU   (NVARRS*MAXVEHS*4)+(NPRARRS*MAXPRS*4)  TOT LEN OF ARRAYS         
WVTABLN  EQU   (WVTABDL*MAXWAVS)+1 TOTAL LENGTH OF WAVE TABLE                   
*                                  (FOR REACH REPORT)                           
         SPACE 3                                                                
WORKD    DSECT                     READREX WORK AREA DSECT                      
NVEHS    DS    F                   ACTUAL UNIQUE VEHICLE COUNT                  
TNINS    DS    F                   TOTAL INSERTIONS                             
NLINS    DS    F                   LINES IN VEHICLE LIST                        
*                                                                               
DVDISPS  DS    0F                  DATA VECTOR DISPLACEMENTS                    
VCODDSP  DS    F                                                                
VQCDDSP  DS    F                                                                
INSDSP   DS    F                                                                
ADJDSP   DS    F                                                                
IMPFDSP  DS    F                                                                
DUPFDSP  DS    F                                                                
C1DSP    DS    F                                                                
BC1DSP   DS    F                                                                
CTLDSP   DS    F                                                                
DVDISPL  EQU   *-DVDISPS                                                        
*                                                                               
DATASWS  DS    0C                                                               
C1SW     DS    C                   C1'S                                         
C2SW     DS    C                   C2'S                                         
BC1SW    DS    C                   BASE C1'S                                    
RCHNSW   DS    C                   MULTI ISSUE REACH COUNTS                     
SCHEDSW  DS    C                   DOING A SCHEDULE                             
UNIQSW   DS    C                   UNIQUES                                      
PNSW     DS    C                   PAIR NETS                                    
PDSW     DS    C                   PAIR DUPES                                   
PSSW     DS    C                   PAIR SUMS                                    
DATASWL  EQU   *-DATASWS                                                        
*                                                                               
X        DS    XL20                                                             
X2       DS    XL20                                                             
WKSAM    DS    F                                                                
WKPOP    DS    F                                                                
WKSAM2   DS    F                                                                
WKPOP2   DS    F                                                                
WKC1     DS    F                                                                
WKC2     DS    F                                                                
WKBC1    DS    F                                                                
HIRCH    DS    F                   HIGHEST REACH NEEDED                         
THISWAV  DS    XL2                 WAVE DATE                                    
FILTLST  DS    XL12                                                             
FELSW    DS    C                                                                
C1PRSW   DS    C                                                                
CNWORK   DS    XL(CNTABDL)                                                      
QVWORK   DS    CL(QVTABDL)                                                      
PRWORK   DS    CL(PRTABDL)                                                      
DVPNTR   DS    A                                                                
DVTABX   DS    A                                                                
AVECT    DS    A                                                                
WVECT1   DS    XL4                                                              
WVECT2   DS    XL4                                                              
WVECT3   DS    XL4                                                              
WVECT4   DS    XL4                                                              
PRSLOT   DS    F                                                                
LASTVEH  DS    CL12                                                             
VLISTX   DS    A                   END OF VLIST                                 
RFARGLST DS    40F                                                              
SAVRD    DS    F                                                                
RFRGSAV  DS    18F                                                              
AFRSTV   DS    A                                                                
FRSTVCD  DS    CL12                                                             
FRSTVDV  DS    F                                                                
FRSTVC1W DS    F                                                                
SUFX     DS    H                                                                
THISDV   DS    F                                                                
BWVPOP   DS    F                                                                
TWVPOP   DS    F                                                                
NWAVES   DS    F                                                                
WVBEQU   DS    XL2                                                              
WVBN     EQU   L'WVBEQU            NO. OF BYTES                                 
RFRTRN   DS    F                   RFMOD RETRUN CODE                            
*                                                                               
*                                                                               
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
*                                                                               
       ++INCLUDE MPRXCTLD                                                       
         SPACE 2                                                                
BINSRCH  EQU   RXBINSCH                                                         
         SPACE 2                                                                
RXCTLD   DSECT                                                                  
         ORG   RXWORK              REDEFINE RXWORK                              
RFMOD    DS    A                   A(RF MODULE)                                 
RXCORE   DS    A                   A(START OF TABLE AREA)                       
RXCOREX  DS    A                   A(END)                                       
RXCORLEN DS    F                   CORE AREA LENGTH                             
MAXVECTS DS    F                   MAXIMUM VECTORS                              
NVECTS   DS    F                   VECTORS SO FAR                               
NXTVECT  DS    A                   NEXT AVAILABLE VECTOR SLOT                   
*                                                                               
RXTARG   DS    CL8                 THIS TARGET                                  
RXBASE   DS    CL8                 THIS BASE                                    
*                                                                               
SVCODES  DS    0CL28                                                            
SVSURV   DS    CL8                 LAST SURVEY                                  
SVTARG   DS    CL8                 LAST TARGET                                  
SVBASE   DS    CL8                 LAST BASE                                    
SVWAVE   DS    XL2                 LAST WAVE                                    
WGTWAV   DS    XL2                 HAVE WGTS FOR THIS WAVE                      
*                                                                               
         DS    0F                                                               
QVPARS   DS    XL24                BINSRCH PARS FOR QVTAB                       
WVPARS   DS    XL24                BINSRCH PARS FOR WAVE TAB                    
CNPARS   DS    XL24                BINSRCH PARS FOR COUNT TABLE                 
PRPARS   DS    XL24                BINSRCH PARS FOR PAIR COUNT TABLE            
NRES     DS    F                   RESPONDENT COUNT                             
BVLEN    DS    F                   LENGTH OF BIT VECTOR                         
CALLNO   DS    XL1                 NUMBER OF READREX CALL                       
*                                  VARIOUS ADDRESSES                            
*                                  -----------------                            
ADDRS    DS    0F                                                               
WVTAB    DS    A                   WAVE TABLE AREA                              
VLIST    DS    A                   VEHICLE LIST AREA                            
QVTAB    DS    A                   Q/VECTOR LIST AREA                           
CNTAB    DS    A                   COUNT TABLE                                  
PRTAB    DS    A                   PAIR COUNT TABLE                             
WSVAREA  DS    A                   SAVE AREA FOR SURVEY WGTS                    
TRGWV    DS    A                   TARGET COMPONENT WEIGHT VECTOR               
BASBV    DS    A                   BASE BIT VECTOR                              
TRGBV    DS    A                   TARGET BIT VECTOR                            
BVW1     DS    A                   WORK BIT VECTOR 1                            
BVW2     DS    A                   WORK BIT VECTOR 2                            
*                                                                               
INSARR   DS    A                   INSERT COUNT ARRAY                           
C1ARR    DS    A                   C1 ARRAY                                     
C2ARR    DS    A                   C2 ARRAY                                     
IMPFARR  DS    A                   IMP FACTOR ARRAY                             
VERRARR  DS    A                   VEHICLE ERROR ARRAY                          
PERRARR  DS    A                   PAIR ERROR ARRAY                             
NVARRS   EQU   (*-C1ARR)/4         NUMBER OF 'VEHICLE' ARRAYS                   
*                                                                               
PNARR    DS    A                   PAIR NET - ARRAY                             
NPRARRS  EQU   (*-PNARR)/4         NUMBER OF 'PAIR' ARRAYS                      
*                                                                               
RVSAV    DS    A                   SAVE AREA FOR RDRSHP VECTORS                 
ADDRSL   EQU   *-ADDRS                                                          
*                                                                               
         EJECT                                                                  
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE DDSPOOLD                                                       
       ++INCLUDE MPRDRFFD                                                       
       ++INCLUDE DDGENTWA                                                       
       ++INCLUDE DDREPMASTD                                                     
       ++INCLUDE DDSPLWORKD                                                     
       ++INCLUDE MPGENQS                                                        
       ++INCLUDE MPQBLKD                                                        
       ++INCLUDE MPQERRQS                                                       
       ++INCLUDE DDNODBLKD                                                      
         PRINT ON                                                               
*                                                                               
       ++INCLUDE MPRDRWORKD                                                     
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'142MPRX      05/01/02'                                      
         END                                                                    
