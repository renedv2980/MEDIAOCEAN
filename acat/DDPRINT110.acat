*          DATA SET DDPRINT110 AT LEVEL 015 AS OF 12/05/13                      
*$PANAPT01S$ *******  FIRST LINE TO DELETE  *****************                   
*                                                           *                   
*  ATTN: THE LEVEL STAMPS ARE *LEGITIMATELY* OUT-OF-SYNC!   *                   
*        DELETE THIS COMMENT BLOCK ON THE NEXT PROMOTION!   *                   
*                                                           *                   
*  THIS SOURCE MODULE IS AT A HIGHER LEVEL NUMBER THAN ITS  *                   
*  CORRESPONDING LOAD OR OBJECT MODULE, BECAUSE THE MEMBER  *                   
*  WAS PROMOTED USING THE SRCE LIBCODE VIA MR# 043178.      *                   
*                                                           *                   
*  THIS COMMENT BLOCK WAS INSERTED *PROGRAMMATICALLY* BY    *                   
*  PANAPT, TO EXPLAIN WHY THE LEVEL STAMPS ARE OUT-OF-SYNC. *                   
*  BEFORE THIS MEMBER CAN BE PROMOTED AGAIN, THIS ENTIRE    *                   
*  COMMENT BLOCK *MUST* BE MANUALLY DELETED.                *                   
*                                                           *                   
*$PANAPT01X$ *******  LAST LINE TO DELETE  ******************                   
***********************************************************************         
*                                                                     *         
*                                                                     *         
*                                                                     *         
*                                                                     *         
*                                                                     *         
*                                                                     *         
*                                                                     *         
*                     THIS MODULE IS DEAD                             *         
*                                                                     *         
*         USE DDPRINTER, DDPRINT132, OR DDPRINTERL INSTEAD.           *         
*                                                                     *         
*                                                                     *         
*                                                                     *         
*                                                                     *         
*                                                                     *         
*                                                                     *         
*                                                                     *         
*                                                                     *         
***********************************************************************         
*CATALP PRINT110                                                                
         TITLE 'PRINT CONTROLLING MODULE'                                       
PRINTER CSECT                                                                   
         NMOD1 42,PRINTER                                                       
         USING DCONT,RC                                                         
         L     RA,=A(CPRINT)                                                    
         LA    R7,RELO                                                          
         S     R7,RELO                                                          
         AR    RA,R7                                                            
         USING DPRINT,RA                                                        
         LA    R2,PBYTE                                                         
         LA    R3,PCONTROL                                                      
         STM   R2,R3,PARAS                                                      
         EJECT                                                                  
*                  CONTROL OF HEADLINE PRINTING                                 
         SPACE 3                                                                
         CP    LINE,MAXLINE        HEADLINES NEEDED                             
         BL    PRNTUSER                                                         
         MVC   PLINE,SPACES        SKIP TO CHANNEL 1                            
         MVC   PCONTROL,=C'BC01'                                                
         BAS   R9,PRINTEM                                                       
         SPACE 2                                                                
         DATE  DDSDATE                                                          
         LA    R1,DDSDATE                                                       
         CLC   SPECDATE(5),=C'DATE='                                            
         BNE   *+8                                                              
         LA    R1,SPECDATE+5                                                    
         MVC   HEAD1+5(2),4(R1)    DAY (US)                                     
         CLI   HEAD1+5,C'0'                                                     
         BNE   *+8                                                              
         MVI   HEAD1+5,C' '                                                     
         PACK  DUB,2(2,R1)         MONTH                                        
         CVB   R2,DUB                                                           
         BCTR  R2,R0                                                            
         MH    R2,=H'3'                                                         
         LA    R2,MONTHS(R2)                                                    
         MVC   HEAD1+8(3),0(R2)                                                 
         MVC   HEAD1+12(2),0(R1)   AND YEAR                                     
         SPACE 2                                                                
       ++INCLUDE DDGETIME                                                       
         ST    R1,DUB                                                           
         UNPK  WORK(6),DUB(4)                                                   
         MVC   HEAD1+24(2),WORK    HOURS                                        
         CLI   HEAD1+24,C'0'                                                    
         BNE   *+8                                                              
         MVI   HEAD1+24,C' '                                                    
         MVC   HEAD1+27(2),WORK+2  AND MINUTES                                  
         MVC   PLINE(15),HEAD1                                                  
         MVC   PLINE+30(60),TITLE                                               
         MVC   HEAD1+100(4),=C'PAGE'                                            
         LA    R2,HEAD1+105                                                     
         EDIT  (P4,PAGE),(4,0(R2)),ALIGN=LEFT                                   
         MVC   PLINE+100(10),HEAD1+100                                          
         MVI   PCONTROL+1,C'L'                                                  
         BAS   R9,PRINTEM                                                       
         EJECT                                                                  
*                  ROUTINE TO UNDERLINE TITLE                                   
         SPACE 3                                                                
         MVC   PLINE,SPACES                                                     
         LA    R2,TITLE                                                         
         LA    R5,PLINE+30                                                      
         LA    R4,60                                                            
         SPACE 2                                                                
PR6      CLI   0(R2),C' '          A(FIRST NON-SPACE) TO R2                     
         BNE   PR8                                                              
         LA    R2,1(R2)                                                         
         LA    R5,1(R5)                                                         
         BCT   R4,PR6                                                           
         B     PR14                (NONE)                                       
         SPACE 2                                                                
PR8      LA    R3,TITLE+59         A(LAST NON-SPACE) TO R3                      
         LA    R6,PLINE+89                                                      
         SPACE 2                                                                
PR10     CLI   0(R3),C' '                                                       
         BNE   PR12                                                             
         BCTR  R6,R0                                                            
         BCT   R3,PR10                                                          
         SPACE 2                                                                
PR12     MVI   0(R5),C'-'          FILL WITH DASHES                             
         CR    R5,R6                                                            
         BE    PR14                                                             
         LA    R5,1(R5)                                                         
         B     PR12                                                             
         SPACE 2                                                                
PR14     MVI   PCONTROL+3,C'2'                                                  
         BAS   R9,PRINTEM                                                       
         EJECT                                                                  
*                  CONTROL OF MIDLINE PRINTING                                  
         SPACE 3                                                                
         ZAP   LINE,=P'7'                                                       
         AP    PAGE,=P'1'                                                       
         CLC   MID1,SPACES                                                      
         BE    SUBLINES                                                         
         MVC   PLINE,MID1                                                       
         CLC   MID2,SPACES                                                      
         BE    LASTMID                                                          
         MVI   PCONTROL+3,C'1'                                                  
         BAS   R9,PRINTEM                                                       
         AP    LINE,=P'1'                                                       
         MVC   PLINE,MID2                                                       
         CLC   MID3,SPACES                                                      
         BE    LASTMID                                                          
         BAS   R9,PRINTEM                                                       
         AP    LINE,=P'1'                                                       
         MVC   PLINE,MID3                                                       
         CLC   MID4,SPACES                                                      
         BE    LASTMID                                                          
         BAS   R9,PRINTEM                                                       
         AP    LINE,=P'1'                                                       
         MVC   PLINE,MID4                                                       
         SPACE 2                                                                
LASTMID  MVI   PCONTROL+3,C'2'                                                  
         BAS   R9,PRINTEM                                                       
         AP    LINE,=P'2'                                                       
         EJECT                                                                  
*                  CONTROL OF SUBLINE PRINTING                                  
         SPACE 3                                                                
SUBLINES EQU   *                                                                
         CLC   SUB1,SPACES                                                      
         BE    PRNTUSER                                                         
         MVC   PLINE,SUB1                                                       
         CLC   SUB2,SPACES                                                      
         BE    PR16                                                             
         MVI   PCONTROL+3,C'1'                                                  
         BAS   R9,PRINTEM                                                       
         AP    LINE,=P'1'                                                       
         MVC   PLINE,SUB2                                                       
         CLC   SUB3,SPACES                                                      
         BE    PR16                                                             
         BAS   R9,PRINTEM                                                       
         AP    LINE,=P'1'                                                       
         MVC   PLINE,SUB3                                                       
         SPACE 2                                                                
PR16     AP    LINE,=P'2'                                                       
         MVI   PCONTROL+3,C'2'                                                  
         BAS   R9,PRINTEM                                                       
         EJECT                                                                  
*                  PRINT USERS LINE AND RETURN                                  
         SPACE 3                                                                
PRNTUSER MVC   PLINE,P                                                          
         MVC   PCONTROL,SPACING                                                 
         BAS   R9,PRINTEM                                                       
         MVC   P,SPACES                                                         
         PACK  DUB,SPACING+2(2)                                                 
         AP    LINE,DUB                                                         
         XMOD1 1                                                                
         SPACE 2                                                                
PRINTEM  GOTO1 =V(PRINT),PARAS,RR=R7                                            
         BR    R9                                                               
         SPACE 2                                                                
RELO     DC    A(*)                                                             
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
*                  DSECT FOR PRINT CONTROLLER                                   
         SPACE 3                                                                
DCONT    DSECT                                                                  
PBYTE    DS    CL1                                                              
PLINE    DS    CL132                                                            
DUB      DS    D                                                                
WORK     DS    CL32                                                             
PCONTROL DS    CL4                                                              
PARAS    DS    CL8                                                              
DDSDATE  DS    CL8                                                              
       ++INCLUDE DDDPRINT                                                       
       ++INCLUDE DDCPRINT                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'015DDPRINT11012/05/13'                                      
         END                                                                    
         EJECT                                                                  
