*          DATA SET DEMININAM  AT LEVEL 035 AS OF 10/04/11                      
*$PANAPT01S$ *******  FIRST LINE TO DELETE  *****************                   
*                                                           *                   
*  ATTN: THE LEVEL STAMPS ARE *LEGITIMATELY* OUT-OF-SYNC!   *                   
*        DELETE THIS COMMENT BLOCK ON THE NEXT PROMOTION!   *                   
*                                                           *                   
*  THIS SOURCE MODULE IS AT A HIGHER LEVEL NUMBER THAN ITS  *                   
*  CORRESPONDING LOAD OR OBJECT MODULE, BECAUSE THE MEMBER  *                   
*  WAS PROMOTED USING THE SRCE LIBCODE VIA MR# 038914.      *                   
*                                                           *                   
*  THIS COMMENT BLOCK WAS INSERTED *PROGRAMMATICALLY* BY    *                   
*  PANAPT, TO EXPLAIN WHY THE LEVEL STAMPS ARE OUT-OF-SYNC. *                   
*  BEFORE THIS MEMBER CAN BE PROMOTED AGAIN, THIS ENTIRE    *                   
*  COMMENT BLOCK *MUST* BE MANUALLY DELETED.                *                   
*                                                           *                   
*$PANAPT01X$ *******  LAST LINE TO DELETE  ******************                   
*PROCESS USING(WARN(15))                                                        
*CATALP MININAM                                                                 
         TITLE 'ROUTINE TO SHORTEN A NAME'                                      
*                                                                               
* PARAMETER 1  BYTE 1    (L'INPUT)                                              
*                   2-4 A(INPUT)                                                
*                                                                               
* PARAMETER 2  BYTE 1    (L'OUTPUT)                                             
*                   2-4 A(OUTPUT)                                               
*                                                                               
* PARAMETER 3           A(COMMAND) VALID COMMANDS                               
*                                  --------------                               
*                                  C'PNABBR' ABBREVIATE PROGRAM NAME            
*                                  C'PNCODE' SOUNDEX CODE(PROG NAME)            
*                                  C'PNCAT ' PROGRAM CATAGORY                   
*                                                                               
         PRINT NOGEN                                                            
MININAM  CSECT                                                                  
         NMOD1 MAWORKX-MAWORK,MININAM,CLEAR=YES                                 
         USING MAWORK,RC                                                        
         MVC   MNPARAM,0(R1)                                                    
         L     RE,MNPARAM3                                                      
         MVC   MNCOMND,0(RE)                                                    
         CLC   MNCOMND,=C'PNABBR'                                               
         BNE   EXIT                                                             
         SPACE 1                                                                
* INITIALIZATION                                                                
         MVI   MNSTART,X'08'       FORCE FIRST CHARACTER                        
         L     RF,MNPARAM2         CLEAR OUTPUT                                 
         LA    RF,0(RF)                                                         
         ZIC   RE,MNPARAM2                                                      
         BCTR  RE,0                                                             
         BCTR  RE,0                                                             
         MVI   0(RF),C' '                                                       
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   1(0,RF),0(RF)                                                    
         ZIC   RE,MNPARAM1                                                      
         L     RF,MNPARAM1                                                      
         LA    RF,0(RF)                                                         
         BCTR  RE,0                                                             
         AR    RF,RE                                                            
         SPACE 1                                                                
MALCHK   CLI   0(RF),C' '          DO WE NEED ABBREVIATION                      
         BE    *+12                                                             
         CLI   0(RF),0                                                          
         BNE   MALCHK2                                                          
         BCTR  RE,0                                                             
         BCTR  RF,0                                                             
         LTR   RE,RE                                                            
         BZ    MALCHK2                                                          
         B     MALCHK                                                           
         SPACE 1                                                                
MALCHK2  LA    RE,1(RE)                                                         
         L     R9,MNPARAM1                                                      
         LA    R9,0(R9)                                                         
         STC   RE,BYTE             SAVE LENGTH                                  
         CLC   BYTE,MNPARAM2                                                    
         BH    MALCHKX                                                          
         ZIC   RE,MNPARAM2                                                      
         BCTR  RE,0                                                             
         L     RF,MNPARAM2                                                      
         LA    RF,0(RF)                                                         
         EX    RE,*+8                                                           
         B     CHKNUM                                                           
         MVC   0(0,RF),0(R9)                                                    
         SPACE 1                                                                
MALCHKX  DS    0H                                                               
         MVI   MNWORK,C' '         SAVE NAME                                    
         MVC   MNWORK+1(59),MNWORK                                              
         ZIC   RE,BYTE             GET INPUT LENGTH                             
         BCTR  RE,0                                                             
         L     RF,MNPARAM1                                                      
         LA    RF,0(RF)                                                         
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   MNWORK(0),0(RF)                                                  
         EX    RE,*+8                                                           
         B     *+10                                                             
         TR    MNWORK(0),TRTAB     TRANSLATE NAME                               
         BAS   R9,ROMAN            ALLOW ROMAN NUMERALS                         
         STC   R8,BYTE3            NUMBER OF ROMANS IN LAST WORD                
         LA    R9,MNWORK                                                        
         L     RE,MNPARAM1                                                      
         LA    RE,0(RE)                                                         
         L     RF,MNPARAM2                                                      
         LA    RF,0(RF)                                                         
         ZIC   R1,BYTE                                                          
         LTR   R1,R1               LOOP CHECK                                   
         BNZ   *+6                                                              
         DC    H'0'                                                             
         ZIC   R2,MNPARAM2                                                      
         SR    R1,R2               GET NUMBER OF CHARACTERS TO DELETE           
         STC   R1,BYTE2                                                         
         LR    R2,R1                                                            
         ZIC   R1,BYTE                                                          
         SPACE 1                                                                
* SCAN FOR DUPLICATE ADJACENT LETTERS                                           
MADUP    CLC   1(1,R9),0(R9)                                                    
         BNE   MADUP10                                                          
         CLI   0(R9),X'09'                                                      
         BE    MADUP10                                                          
         TM    0(R9),X'80'         DONT SUPPRESS ROMAN NUMERALS                 
         BO    MADUP10                                                          
         MVI   1(R9),X'FF'                                                      
MADUP2   BCTR  R2,0                                                             
         LTR   R2,R2                                                            
         BZ    MAMOVE                                                           
MADUP10  LA    R9,1(R9)                                                         
         BCT   R1,MADUP                                                         
         SPACE 2                                                                
* SHORTEN NAME                                                                  
MASHRT   ZIC   R1,BYTE                                                          
         LA    R9,MNWORK                                                        
         AR    R9,R1               START AT END                                 
         BCTR  R9,0                                                             
         SPACE 1                                                                
MASHRT2  LR    R8,R9               CHECK FOR SEPARATION CHAR.                   
         TM    0(R9),X'80'         DONT DELETE ROMAN NUMERALS                   
         BO    MASHRT3                                                          
         BCTR  R8,0                                                             
         CLI   0(R8),8             FIRST LETTER                                 
         BE    MASHRT3               YES - DONT DELETE                          
         CLI   0(R8),9             NUMERIC                                      
         BE    MASHRT3              FOR NEXT                                    
         MVI   PREVOUT,C' '                                                     
         CLI   1(R9),8             END IN VOWEL                                 
         BE    MASHRT3               YES - DONT DELETE                          
         CLI   0(R9),0             DELETABLE CHARACTER                          
         BNE   *+10                                                             
         MVI   0(R9),X'FF'         YES - SET DELETE FLAG                        
         BCTR  R2,0                                                             
         LTR   R2,R2                                                            
         BZ    MAMOVE                                                           
         SPACE 1                                                                
MASHRT3  BCTR  R9,0                                                             
         BCT   R1,MASHRT2                                                       
         SPACE 2                                                                
* MOVE ABBREVIATION TO OUTPUT                                                   
MAMOVE   ZIC   R1,MNPARAM2                                                      
         ZIC   R2,BYTE                                                          
         LTR   R1,R1                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         LA    R9,MNWORK                                                        
         SPACE 1                                                                
MAMOVE2  CLI   0(R9),X'FF'                                                      
         BE    MAMOVE3                                                          
         CLI   0(RE),X'F0'         IF NUMERIC, THEN DON'T                       
         BL    MAMOVE2A                                                         
         CLI   0(RE),X'F9'          DO "SAME LETTER AS PREV" TEST               
         BNH   MAMOVE2B                                                         
MAMOVE2A CLC   PREVOUT,0(RE)       SAME LETTER AS PREV                          
         BE    MAMOVE3              YES - DELETE IT                             
MAMOVE2B MVC   0(1,RF),0(RE)                                                    
         MVC   PREVOUT,0(RE)       SET PREVIOUS CHARACTER                       
*                                                                               
         ZIC   R5,PREVOUT          CHECK TRANS TABLE FOR ROMANS                 
         LA    R5,TRTAB(R5)                                                     
         TM    0(R5),X'80'                                                      
         BZ    *+8                                                              
         MVI   PREVOUT,0           ALLOW DUPS FOR ROMANS                        
*                                                                               
         LR    R8,R9               ALLOW DUPS IF FIRST                          
         BCTR  R8,0                                                             
         CLI   0(R8),8                                                          
         BNE   *+8                                                              
         MVI   PREVOUT,0                                                        
         LA    RF,1(RF)                                                         
         BCTR  R1,0                                                             
         SPACE 1                                                                
MAMOVE3  BCTR  R2,0                                                             
         LTR   R1,R1                                                            
         BZ    MAMOVE4                                                          
         LA    RE,1(RE)                                                         
         LA    R9,1(R9)                                                         
         LTR   R2,R2                                                            
         BZ    MAPROCX                                                          
         B     MAMOVE2                                                          
         SPACE 1                                                                
* MAKE SURE ROMAN NUMERALS GET IN                                               
MAMOVE4  LTR   R2,R2                                                            
         BZ    MAPROCX                                                          
         OC    BYTE3,BYTE3                                                      
         BZ    MAPROCX                                                          
*                                                                               
         ZIC   R8,MNPARAM1                                                      
         BCTR  R8,0                                                             
         ZIC   RE,MNPARAM2                                                      
         BCTR  RE,0                                                             
         L     R9,MNPARAM1                                                      
         LA    R9,0(R9)                                                         
         L     RF,MNPARAM2                                                      
         LA    RF,0(RF)                                                         
         AR    R9,R8                                                            
         AR    RF,RE                                                            
*                                                                               
         ZIC   RE,BYTE3                                                         
         SR    R9,RE                                                            
         SR    RF,RE                                                            
         EX    RE,*+8                                                           
         B     EXIT                                                             
         MVC   0(0,RF),0(R9)                                                    
MAPROCX  B     CHKNUM                                                           
         EJECT                                                                  
ROMAN    LA    R1,MNWORK                                                        
         LA    RE,1(RE)                                                         
         SR    R8,R8                                                            
         B     ROMAN01                                                          
ROMAN00  LA    R1,1(R1)                                                         
         SR    R8,R8                                                            
ROMAN01  TM    0(R1),X'80'         ROMAN CHARACTER                              
         BO    ROMAN03             YES - CHECK IF THEY ALL ARE                  
ROMAN02  CLI   0(R1),X'08'         NO  - RESET ROMAN INDICATORS                 
         BE    ROMAN00             GET NEXT WORD                                
         NI    0(R1),X'7F'         RESET ENTIRE WORD                            
         LA    R1,1(R1)                                                         
         BCT   RE,ROMAN02                                                       
         BR    R9                                                               
ROMAN03  LR    RF,RE               SAVE CHARACTER COUNT                         
         LR    R2,R1               SAVE WORD START                              
         SR    R8,R8               LENGTH OF LAST ROMAN STRING                  
ROMAN04  CLI   0(R1),X'08'         NEXT WORD                                    
         BE    ROMAN00                                                          
         CLI   0(R1),X'40'                                                      
         BER   R9                                                               
         TM    0(R1),X'80'         STILL ROMAN                                  
         BZ    ROMAN05             NO - RESET ROMAN BITS                        
         LA    R1,1(R1)            YES - LOOK AT THE NEXT ONE                   
         LA    R8,1(R8)                                                         
         BCT   RE,ROMAN04                                                       
         BR    R9                                                               
ROMAN05  LR    RE,RF               RESET LETTER COUNTER                         
         LR    R1,R2               RESET TO WORD START                          
         SR    R8,R8               LENGTH OF LAST ROMAN STRING                  
         B     ROMAN02                                                          
         EJECT                                                                  
* IF LAST CHARACTER OF INPUT IS NUMERIC                                         
* MAKE SURE IT IS TRANSFERRED TO OUTPUT                                         
CHKNUM   ZIC   R8,MNPARAM1                                                      
         BCTR  R8,0                                                             
         ZIC   RE,MNPARAM2                                                      
         BCTR  RE,0                                                             
         L     R9,MNPARAM1                                                      
         LA    R9,0(R9)                                                         
         L     RF,MNPARAM2                                                      
         LA    RF,0(RF)                                                         
*                                                                               
         AR    R9,R8                                                            
         AR    RF,RE                                                            
         CLI   0(RF),X'40'                                                      
         BE    EXIT                                                             
         CLC   0(1,R9),0(RF)                                                    
         BE    EXIT                                                             
         CLI   0(R9),X'F0'                                                      
         BL    EXIT                                                             
         CLI   0(R9),X'F9'                                                      
         BH    EXIT                                                             
         MVC   0(1,RF),0(R9)                                                    
         EJECT                                                                  
EXIT     XMOD1 1                                                                
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
* TRANSLATION TABLE FOR SOUNDEX ALGORITHM                                       
*                                                                               
         DS    0D                                                               
TRTAB    DS    0CL256                                                           
         DC    64X'00'                                                          
         DC    X'08'               BLANK                                        
         DC    15X'00'                                                          
         DC    X'08'               AMPER                                        
         DC    15X'00'                                                          
         DC    X'08'               DASH                                         
         DC    X'08'               SLASH                                        
         DC    95X'00'                                                          
         DC    X'00'               A                                            
         DC    X'01'               B                                            
         DC    X'82'               C ROMAN MODIFIER                             
         DC    X'83'               D ROMAN MODIFIER                             
         DC    X'00'               E                                            
         DC    X'01'               F                                            
         DC    X'02'               G                                            
         DC    X'07'               H                                            
         DC    X'80'               I VOWEL + ROMAN REPEATER                     
         DC    7X'00'                                                           
         DC    X'02'               J                                            
         DC    X'02'               K                                            
         DC    X'84'               L ROMAN MODIFIER                             
         DC    X'85'               M ROMAN MODIFIER                             
         DC    X'05'               N                                            
         DC    X'00'               O                                            
         DC    X'01'               P                                            
         DC    X'07'               Q                                            
         DC    X'06'               R                                            
         DC    8X'00'                                                           
         DC    X'02'               S                                            
         DC    X'03'               T                                            
         DC    X'00'               U                                            
         DC    X'81'               V ROMAN MODIFIER                             
         DC    X'07'               W                                            
         DC    X'02'               X SOUND 2 + ROMAN REPEATER                   
         DC    X'07'               Y                                            
         DC    X'02'               Z                                            
         DC    6X'00'                                                           
         DC    10X'09'             NUMERICS                                     
         DC    7X'00'                                                           
         EJECT                                                                  
* WORKING STORAGE DSECT                                                         
*                                                                               
MAWORK   DSECT                                                                  
         DS    0F                                                               
MNPARAM  DS    0CL24                                                            
MNPARAM1 DS    F                                                                
MNPARAM2 DS    F                                                                
MNPARAM3 DS    F                                                                
MNPARAM4 DS    F                                                                
MNPARAM5 DS    F                                                                
MNPARAM6 DS    F                                                                
BYTE     DS    C                                                                
BYTE2    DS    C                                                                
BYTE3    DS    C                                                                
MNCOMND  DS    CL6                                                              
PREVOUT  DS    C                                                                
MNSTART  DS    CL1                                                              
MNWORK   DS    CL60                                                             
MAWORKX  EQU   *                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'035DEMININAM 10/04/11'                                      
         END                                                                    
