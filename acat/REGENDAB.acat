*          DATA SET REGENDAB   AT LEVEL 002 AS OF 08/30/12                      
*CATALP REGENDAB                                                                
***********************************************************************         
*                                                                     *         
*         REGENDAB --- DARE BUY LIST ROUTINES                         *         
*                                                                     *         
* ------------------------------------------------------------------- *         
* UPDATE HISTORY:                                                     *         
*                                                                     *         
* 02JUN94 (SKU) --- DATE OF BIRTH                                     *         
*                                                                     *         
* 26JAN95 (SKU) --- INCREASE MAX BUY COMMENT LENGTH TO 70             *         
*                   SUPPORT DAILY BUY                                 *         
*                   DISPLAY SINGLE TIME FOR IDENTICAL START-END TIMES *         
*                                                                     *         
* 07APR95 (SKU) --- SPECIFY BUYS MARKED DELETED AS 'C' AND REMOVE     *         
*                   NPW/RATE/SPT                                      *         
*                                                                     *         
* 22JUL96 (SKU) --- FIX OOWR BUG                                      *         
*                                                                     *         
* 02OCT97 (SKU) --- ACCEPT ZERO SPOT AGENCY BUYS                      *         
*                                                                     *         
* 12NOV97 (JRD) --- TAKE OVER CHOPPING                                *         
*                                                                     *         
* 17FEB98 (JRD) --- MG PRINTING                                       *         
*                                                                     *         
* 05MAY98 (SKU) --- SUPPORT AGENCY DEMOS                              *         
*                                                                     *         
* 11SEP98 (SKU) --- TAKE OVER BUG FIX                                 *         
*                                                                     *         
* 28JUN00 (SKU) --- SKIP USER DEFINED DEMOS                           *         
*                                                                     *         
* 17NOV00 (SKU) --- INCREASE BUY ORBIT BUFFER                         *         
*                                                                     *         
* 21DEC00 (SKU) --- CHECK FOR NON-NULL OR NON-BLANKS IN ROTATION FLD  *         
*                                                                     *         
* 26SEP01 (SKU) --- FIX TAKEOVER                                      *         
*                                                                     *         
* 29JUL02 (HQ ) --- DISPLAY CHANGE COMMENT                            *         
*                                                                     *         
* 03NOV03 (HQ ) --- HANDLE CONFLICT END DATE DARE RECORD              *         
*                   ADD NEW PARAMETER FOR NEW REVISION/DIFF REPORT    *         
*                                                                     *         
* 04FEB08 (SKU) --- OOWR FIX FOR XML ORDERS                           *         
*                                                                     *         
* 07JUL08 (KUI) --- CHECK FOR BLANK DEMOS                             *         
*                                                                     *         
* 12APR10 (SKU) --- ESPERANTO OOWR FIX                                *         
*                                                                     *         
* 30AUG12 (SKU) --- AGENCY CHANGE COMMENT FIX                         *         
***********************************************************************         
*  INPUT:                                                                       
*              PARAMETER 1 = BYTE 1 - X'80' PRINT OPTION                        
*                                     PROCESS ONLY ONE BUY PER PASS             
*                                     X'40' PROCESS TKO'S                       
*                                     X'20' SHOW MG INDICATOR                   
*                                     X'10' DON'T PRINT DEMOS                   
*                                     X'04' DON'T SHOW BUY CHANGE COMMT         
*                                     X'02' ONLY SHOW BUY CHANGE COMMT          
*                          = BYTES 2-4 - A(AGENCY DARE ORDER KEY)               
*                        2 = BYTE 1 - MAX ROW AT 1ST BYTE                       
*                            BYTES 2-4 - A(BUY LIST AREA)                       
*                              ASSUMES MULTIPLES OF 81 COLUMNS                  
*                        3 = BYTE 1 - BUYLINE NUMBER TO START                   
*                            BYTES 2-4 - A(IOAREA)                              
*                        4 = A(ROUTINE ADDRESS BLOCK)                           
*                                  WORD 1 = A(COMFACS)                          
*                                  WORD 2 = A(OUTDAY)                           
*                                  WORD 3 = A(UNTIME)                           
*                                  WORD 4 = A(DEMOCON)                          
*                        5 = A(TAKEOVER BLOCK)                                  
*                                 A(REDARTKO)                                   
*                                 A(PERVERT)                                    
*                                 XL3 TKODATE (YMD BINARY)                      
*  OUTPUT:                                                                      
*              PARAMETER 3 = BYTE 1 - NUMBER OF ROWS USED                       
*                            BYTE 2 - NEXT BUY NUMBER OR X'00' NO MORE          
*                            BYTE 3 - FOR PRINT, X'08' IF BUY IS DAILY          
*                            BYTE 4 - X'80', SERIOUS EDI ERROR                  
***********************************************************************         
         TITLE 'REGENDAB - DARE BUY LIST ROUTINES'                              
REGENDAB CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,REGENDAB,R9                                          
         USING WORKD,RC                                                         
         ST    R1,SVDMCB                                                        
                                                                                
         MVC   PRINTOPT,0(R1)      FIRST BYTE IS PRINT OPTION                   
                                                                                
         L     R6,0(R1)                                                         
         ST    R6,ABUYKEY                                                       
         MVC   BUYKEY,0(R6)                                                     
                                                                                
         MVC   BLISTNUM,4(R1)      FIRST BYTE IS MAX ROW                        
         L     R4,4(R1)                                                         
         USING LBUYD,R4                                                         
         LR    RE,R4                                                            
         ZIC   RF,BLISTNUM                                                      
         MH    RF,=Y(LBUYLENQ)                                                  
         XCEF                                                                   
                                                                                
         MVC   BBUYNUM,8(R1)       FIRST BYTE IS START BUY NUMBER               
         MVC   AIOAREA,8(R1)                                                    
         L     R6,12(R1)                                                        
         MVC   VCOMFACS(16),0(R6)                                               
                                                                                
         L     RE,16(R1)                                                        
         MVC   VREDARTK(11),0(RE)                                               
                                                                                
         MVC   DATADISP,=H'34'                                                  
         MVI   DMINBTS,0           DO NOT PASS DELETED RECORDS                  
*                                                                               
         TM    PRINTOPT,X'02'                                                   
         BZ    MAIN50                                                           
         L     R6,VCOMFACS                                                      
         USING COMFACSD,R6                                                      
         MVC   VDATAMGR,CDATAMGR                                                
         MVC   VDATCON,CDATCON                                                  
         MVC   VADDAY,CADDAY                                                    
         MVC   VGETDAY,CGETDAY                                                  
         DROP  R6                                                               
         BAS   RE,DISPCHG                                                       
         B     EXIT                                                             
*                                                                               
MAIN50   DS    0H                                                               
         BAS   RE,INIT             SET ADDRESSES                                
*                                                                               
         B     DISPAGE                                                          
*                                                                               
EXIT     DS    0H                                                               
         XMOD1                                                                  
         EJECT                                                                  
***********************************************************************         
* SET ADDRESSES                                                                 
* RETRIEVE DEMO VALUES, IF ANY                                                  
***********************************************************************         
INIT     NTR1                                                                   
         L     R6,VCOMFACS                                                      
         USING COMFACSD,R6                                                      
         MVC   VDATAMGR,CDATAMGR                                                
         MVC   VDATCON,CDATCON                                                  
         MVC   VADDAY,CADDAY                                                    
         MVC   VGETDAY,CGETDAY                                                  
         DROP  R6                                                               
*                                                                               
* RETRIEVE DEMO CATEGORIES FROM HEADER RECORD                                   
*                                                                               
         XC    MISCFLG1,MISCFLG1                                                
*                                                                               
         XC    DEMOCATS,DEMOCATS                                                
         MVC   KEY(L'RDARKEY),BUYKEY                                            
         LA    R6,KEY                                                           
         USING RDARKEY,R6                                                       
         XC    RDARKRT(3),RDARKRT                                               
         MVI   RDARKRT,X'10'                                                    
         DROP  R6                                                               
*                                                                               
         GOTO1 SVDEMCT,DMCB,(4,DEMOCATS)                                        
*                                                                               
INITX    DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* DISPLAY ONE PAGE OF BUYS                                                      
***********************************************************************         
DISPAGE  DS    0H                                                               
         MVC   KEY(L'RDARKEY),BUYKEY                                            
         XC    BROWUSED,BROWUSED                                                
         XC    LBUYMC(LBUYLENQ),LBUYMC                                          
         XC    BUYDAILY,BUYDAILY   CLEAR RETURN STATUS BYTE                     
                                                                                
         GOTO1 HIGH                                                             
         CLC   KEY(RDARKSEQ-RDARKEY),KEYSAVE                                    
         BNE   DISPAGEX                                                         
                                                                                
DISP10   DS    0H                                                               
         XC    SPTCOST,SPTCOST                                                  
         XCEFL BUYORBIT,636                                                     
         XC    ORBITSTT,ORBITSTT                                                
         XC    ORBITEND,ORBITEND                                                
         XC    BSTRTDAY,BSTRTDAY                                                
         XC    BENDDAY,BENDDAY                                                  
         XC    BSTATUS,BSTATUS     CLEAR STATUS BYTE                            
                                                                                
         LA    R6,KEY                                                           
         USING RDARKEY,R6                                                       
         OC    RDARKSRT,RDARKSRT                                                
         BNZ   DISPAGEX            MUST BE BUY HEADER                           
         DROP  R6                                                               
                                                                                
         GOTO1 GETREC                                                           
                                                                                
         XC    XMLBUYNM,XMLBUYNM                                                
         L     R6,AIOAREA                                                       
         MVI   ELCODE,X'10'        GET XML RENUMBERING ELEMENT                  
         BAS   RE,GETEL                                                         
         BNE   DISP12              NONE FOUND, SKIP                             
         USING RDARRVEL,R6                                                      
         MVC   XMLBUYNM,RDARLXML                                                
         DROP  R6                                                               
                                                                                
DISP12   DS    0H                                                               
         L     R6,AIOAREA                                                       
         MVI   ELCODE,X'01'        HEADER DESCRIPTION ELEMENT                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         USING RDARBYEL,R6                                                      
         TM    PRINTOPT,X'01'      IF PRINT, IS THIS JUST TO GET NEXT           
         BO    DISP15               BUY #?                                      
         CLI   RDARBYLN,RDARBYOL                                                
         BNH   DISP15                                                           
         TM    RDARBYDL,X'40'      SKIP FOR HARD DELETE                         
         BZ    DISP15                                                           
         MVI   KEY+RDARKSRT-RDARKEY,X'FF'                                       
         GOTO1 HIGH                                                             
         CLC   KEY(RDARKSEQ-RDARKEY),KEYSAVE                                    
         BE    DISP13                                                           
         MVI   BBUYNUM,0           NO MORE BUYS TO DISPLAY                      
         B     DISPAGEX                                                         
                                                                                
DISP13   DS    0H                                                               
         L     RF,ABUYKEY          UPDATE BUYKEY IN CALLER                      
         MVC   0(L'BUYKEY,RF),KEY                                               
         B     DISP10                                                           
                                                                                
DISP15   DS    0H                                                               
         MVC   BUYHEAD,RDARBYEL    SAVE OFF BUY HEADER X'01' ELEMENT            
         DROP  R6                                                               
                                                                                
         GOTO1 GETODMO                                                          
         XC    BUYDEMOS,BUYDEMOS                                                
         L     R6,AIOAREA                                                       
         MVI   ELCODE,X'20'        BUY DEMO VALUES                              
         BAS   RE,GETEL                                                         
         BNE   DISP18                                                           
         USING RDARBMEL,R6                                                      
         MVC   BUYDEMOS,RDARBDM1   SAVE OFF BUY DEMO VALUES                     
         DROP  R6                                                               
                                                                                
DISP18   DS    0H                                                               
         NI    BSTATUS,X'FF'-BSTMGOOD                                           
                                                                                
         L     R6,AIOAREA                                                       
         MVI   ELCODE,X'10'        REVISION ELEMENT                             
         BAS   RE,GETEL                                                         
         BNE   DISP20                                                           
                                                                                
         USING RDARRVEL,R6                                                      
         CLI   RDARRVBY,0          MAKE GOOD?                                   
         BE    *+14                NO                                           
         OI    BSTATUS,BSTMGOOD                                                 
         MVC   MGLINK,RDARRVBY                                                  
         DROP  R6                                                               
                                                                                
DISP20   DS    0H                                                               
         GOTO1 SEQ                                                              
         LA    R6,KEY                                                           
         USING RDARKEY,R6                                                       
         CLC   KEY(RDARKSRT-RDARKEY),KEYSAVE                                    
         BNE   DISP300             FOR THE SAME BUYLINE, CHECK:                 
         CLI   RDARKSRT,X'10'      IF ORBIT FOUND, PROCESS                      
         BE    DISP100                                                          
         CLI   RDARKSRT,X'20'      IF BUY COMMENT FOUND, SKIP FOR NOW           
         BE    DISP20                                                           
         CLI   RDARKSRT,X'30'      IF DETAIL, PROCESS                           
         BE    DISP200                                                          
         B     DISP300             IF NONE OF THE ABOVE, GET NEXT               
         DROP  R6                  BUYLINE                                      
         EJECT                                                                  
**********************************************************************          
* PROCESS BUY ORBITS                                                            
**********************************************************************          
DISP100  DS    0H                  PROCESS ORBITS                               
         GOTO1 GETREC                                                           
                                                                                
         L     R6,AIOAREA                                                       
         USING RDAROEEL,R6                                                      
         MVI   ELCODE,X'02'        GET ORBIT ELEMENT                            
         BAS   RE,GETEL                                                         
         BNE   DISP20              NONE FOUND                                   
                                                                                
         LA    R3,BUYORBIT         SAVE ORBIT INFO IN ARRAY                     
DISP110  MVC   0(L'BUYORBIT,R3),RDAROERO                                        
         LA    R3,L'BUYORBIT(R3)                                                
         MVC   BUYROTAT(12),RDAROERO                                            
         XC    BUYSTEN,BUYSTEN                                                  
         XC    BSTRTDAY,BSTRTDAY                                                
         XC    BENDDAY,BENDDAY                                                  
         OC    XMLBUYNM,XMLBUYNM   IF XML, USE ORDER ROTATION START DAY         
         BZ    *+18                                                             
         CLI   ORDROTST,0                                                       
         BE    *+10                                                             
         MVC   BUYROTST,ORDROTST                                                
         GOTO1 STARTEND,DMCB,BUYROTAT,BUYSTEN                                   
         OC    ORBITSTT,ORBITSTT                                                
         BNZ   *+10                                                             
         MVC   ORBITSTT,BSTRTDAY    FIRST ORBIT FOR EFFECTIVE DATE CAL          
         BAS   RE,NEXTEL                                                        
         BE    DISP110                                                          
         MVC   ORBITEND,BENDDAY     LAST ORBIT FOR EFFECTIVE DATE CAL           
         B     DISP20                                                           
         DROP  R6                                                               
         EJECT                                                                  
**********************************************************************          
* PROCESS BUY DETAILS                                                           
**********************************************************************          
DISP200  DS    0H                  PROCESS BUY DETAILS                          
         GOTO1 GETREC                                                           
         L     R6,AIOAREA                                                       
         USING RDARKEY,R6          BUYLINE NUMBER                               
         MVC   BBUYNUM,RDARKSEQ                                                 
                                                                                
         TM    PRINTOPT,X'01'      IF PRINT, IS THIS JUST TO GET NEXT           
         BO    DISPAGEX             BUY #?                                      
                                                                                
         EDIT  RDARKSEQ,(3,LBUYLINE)                                            
         DROP  R6                                                               
                                                                                
         OC    XMLBUYNM,XMLBUYNM                                                
         BZ    DISP201                                                          
         EDIT  XMLBUYNM,(4,LBUYLINE-1)                                          
                                                                                
DISP201  DS    0H                                                               
         TM    PRINTOPT,X'20'      SHOW MG INDICATOR?                           
         BZ    DISP202             NO                                           
         TM    BSTATUS,BSTMGOOD    MAKEGOOD LINE?                               
         BZ    DISP202             NO                                           
                                                                                
         MVI   LBUYTYPE,C'*'                                                    
         MVC   LBUYMGI,=C'MG'                                                   
         EDIT  (B1,MGLINK),LBUYMGL,ALIGN=LEFT,ZERO=NOBLANK                      
                                                                                
DISP202  DS    0H                                                               
         MVC   BBUYSTRW,BROWUSED                                                
                                                                                
         NI    BSTATUS,X'FF'-BSTDAILY                                           
         L     R6,AIOAREA                                                       
         MVI   ELCODE,X'01'        GET DESCRIPTION ELEMENT                      
         BAS   RE,GETEL                                                         
         BNE   DISP205             NONE FOUND, NEXT BUYLINE                     
         USING RDARBDEL,R6                                                      
         TM    RDARBDFL,X'80'      FLAG STATUS IF DAILY BUY                     
         BZ    DISP205                                                          
         OI    BSTATUS,BSTDAILY                                                 
         OI    BUYDAILY,BSTDAILY                                                
         DROP  R6                                                               
                                                                                
DISP205  DS    0H                                                               
         L     R6,AIOAREA                                                       
         MVI   ELCODE,X'02'        GET DETAIL ELEMENT                           
         BAS   RE,GETEL                                                         
         BNE   DISP300             NONE FOUND, NEXT BUYLINE                     
                                                                                
         LA    R3,BUYORBIT                                                      
                                                                                
DISP210  DS    0H                                                               
         XC    BSTRTDAY,BSTRTDAY                                                
         XC    BENDDAY,BENDDAY                                                  
         LA    RF,BUYHEAD          DEFAULT TO USE BUY HEADER DATA               
         USING RDARBYEL,RF                                                      
         MVC   BUYROTAT(12),RDARBYRO                                            
         OC    XMLBUYNM,XMLBUYNM   IF XML, USE ORDER ROTATION START DAY         
         BZ    *+18                                                             
         CLI   ORDROTST,0                                                       
         BE    *+10                                                             
         MVC   BUYROTST,ORDROTST                                                
*                                                                               
E        USING RDARBUEL,R6                                                      
         CLI   E.RDARBULN,RDARBUL2   TESTING/CONFLICT END DATE                  
         BNE   *+10                                                             
         MVC   BUYROTAT(L'RDARBURO+L'RDARBURS),E.RDARBURO                       
         DROP  E                                                                
         DROP  RF                                                               
                                                                                
         OC    0(L'BUYORBIT,R3),0(R3)                                           
         BZ    DISP220             IF ORBIT INFO PRESENT, USE IT                
                                                                                
DISP215  DS    0H                                                               
         MVC   BUYROTAT(12),0(R3)                                               
         GOTO1 BUYROT,DMCB,BUYROTAT                                             
         XC    BUYSTEN,BUYSTEN                                                  
         XC    BSTRTDAY,BSTRTDAY                                                
         XC    BENDDAY,BENDDAY                                                  
         OC    XMLBUYNM,XMLBUYNM   IF XML, USE ORDER ROTATION START DAY         
         BZ    *+18                                                             
         CLI   ORDROTST,0                                                       
         BE    *+10                                                             
         MVC   BUYROTST,ORDROTST                                                
         GOTO1 STARTEND,DMCB,BUYROTAT,BUYSTEN                                   
         B     DISP260                                                          
                                                                                
DISP220  DS    0H                                                               
         TM    BSTATUS,BSTDAILY                                                 
         BO    DISP260                                                          
         GOTO1 BUYROT,DMCB,BUYROTAT                                             
         XC    BUYSTEN,BUYSTEN                                                  
         OC    XMLBUYNM,XMLBUYNM   IF XML, USE ORDER ROTATION START DAY         
         BZ    *+18                                                             
         CLI   ORDROTST,0                                                       
         BE    *+10                                                             
         MVC   BUYROTST,ORDROTST                                                
         GOTO1 STARTEND,DMCB,BUYROTAT,BUYSTEN                                   
                                                                                
DISP260  DS    0H                                                               
         TM    PRINTOPT,X'40'      NEED TKO PROCESSING?                         
         BNO   DISP270             NO                                           
*                                                                               
         XC    BRTSTDAY,BRTSTDAY                                                
         MVN   BRTSTDAY,BUYROTST   CONVERT ROTATION START DAY TO BINARY         
*                                                                               
         MVC   DMCB(4),AIOAREA     SETUP CHOPPING CALL                          
         MVC   DMCB+4(3),TKODATE                                                
         MVC   DMCB+8(1),BSTRTDAY                                               
         MVC   DMCB+9(1),BENDDAY                                                
         MVC   DMCB+10(1),BRTSTDAY                                              
         MVC   DMCB+11(1),ORBITSTT                                              
         XC    WORK,WORK                                                        
         MVC   WORK(4),VDATCON                                                  
         MVC   WORK+4(4),VGETDAY                                                
         MVC   WORK+8(4),VADDAY                                                 
         MVC   WORK+12(4),VPERVERT                                              
         LA    RE,WORK                                                          
         ST    RE,DMCB+12                                                       
*                                                                               
         GOTO1 VREDARTK,DMCB                                                    
         BNE   DISP300             BUY DELETED, EXIT                            
*                                                                               
         CLI   0(R6),X'02'                                                      
         BE    DISP270                                                          
         L     R6,AIOAREA                                                       
         MVI   ELCODE,X'02'        GET DETAIL ELEMENT                           
         BAS   RE,GETEL                                                         
         BE    DISP270             NONE FOUND, NEXT BUYLINE                     
         DC    H'0'                                                             
                                                                                
DISP270  DS    0H                                                               
         GOTO1 BUYLINE             SEND A BUYLINE TO OUTPUT                     
*****TESTING                                                                    
*        EDIT  BROWUSED,LBUYNW                                                  
*****TESTING                                                                    
         ZIC   RF,BROWUSED         NEXT PRINT LINE                              
         LA    RF,1(RF)                                                         
         STC   RF,BROWUSED                                                      
         CLC   BROWUSED,BLISTNUM   NEW LINE EXCEEDED DISPLAY LINES              
         BL    DISP280                                                          
         ZIC   RF,BROWUSED                                                      
         BCTR  RF,0                                                             
         STC   RF,BROWUSED                                                      
         TM    BSTATUS,BSTSAME     SAME BUYLINE?                                
         BZ    DISPAGEX            NO, NEW BUYLINE SO DON'T SHOW                
         CLI   BLISTNUM,15         **** FOR DISPLAY ONLY                        
         BNL   *+14                                                             
         MVC   BROWUSED,BBUYSTRW   REST OF BUY DIDN'T FIT                       
         B     DISPAGEX                                                         
                                                                                
*****                                                                           
***** WE SHOULD CONTINUE FOR PRINT, SET FLAG FOR DISPLAY                        
*****                                                                           
*        TM    PRINTOPT,X'80'      ARE WE PRINTING?                             
*        BZ    DISP310                                                          
         OI    PRINTOPT,X'01'      GET NEXT BUY NUM AND EXIT                    
         B     DISP310                                                          
                                                                                
DISP280  DS    0H                                                               
         LA    R4,81(R4)                                                        
                                                                                
         OI    BSTATUS,BSTSAME     SAME BUYLINE                                 
         LA    R3,L'BUYORBIT(R3)                                                
         BAS   RE,NEXTEL                                                        
         BE    DISP210                                                          
         OI    BSTATUS,BSTORBIT    IF NO MORE BUYLINES BUT                      
         OC    0(L'BUYORBIT,R3),0(R3) ORBIT INFO PRESENT, DISPLAY DAY           
         BNZ   DISP215             AND TIMES ONLY                               
                                                                                
* SHOW PROGRAM NAME                                                             
         LA    RF,BUYHEAD                                                       
         USING RDARBYEL,RF                                                      
         CLC   RDARBYPN,MYSPACES                                                
         BE    DISP290                                                          
         MVC   9(2,R4),=C'P='                                                   
         MVC   11(L'RDARBYPN,R4),RDARBYPN                                       
         DROP  RF                                                               
                                                                                
         LA    R4,81(R4)                                                        
                                                                                
         ZIC   RF,BROWUSED         NEXT PRINT LINE                              
         LA    RF,1(RF)                                                         
         STC   RF,BROWUSED                                                      
                                                                                
         CLC   BROWUSED,BLISTNUM                                                
         BNH   DISP290                                                          
*                                                                               
* INSERT A BETTER CHECK HERE !!!!!!!!!!!!!                                      
*                                                                               
         TM    BSTATUS,BSTSAME     SAME BUYLINE?                                
         BZ    DISPAGEX            NO, NEW BUYLINE SO DON'T SHOW                
         CLI   BLISTNUM,15         **** FOR DISPLAY ONLY                        
         BNL   DISPAGEX                                                         
         MVC   BROWUSED,BBUYSTRW   REST OF BUY DIDN'T FIT                       
         B     DISPAGEX                                                         
                                                                                
* SHOW BUY COMMENTS                                                             
DISP290  DS    0H                                                               
         BAS   RE,GETCOMMT                                                      
         CLC   BROWUSED,BLISTNUM                                                
         BNH   DISP295                                                          
         TM    BSTATUS,BSTSAME     SAME BUYLINE?                                
         BZ    DISPAGEX            NO, NEW BUYLINE SO DON'T SHOW                
         CLI   BLISTNUM,15         **** FOR DISPLAY ONLY                        
         BH    DISPAGEX                                                         
         MVC   BROWUSED,BBUYSTRW   REST OF BUY DIDN'T FIT                       
         B     DISPAGEX                                                         
                                                                                
* SHOW DEMO VALUES                                                              
DISP295  DS    0H                                                               
         TM    PRINTOPT,X'10'      OPTION TO PRINT DEMO VALUES??                
         BNZ   DISP300                                                          
         BAS   RE,DISDEMO                                                       
*                                                                               
         TM    PRINTOPT,X'80'                                                   
         BZ    DISP298             NOT PRINTING - SKIP                          
*                                                                               
         TM    BSTATUS,BSTMGOOD    MAKEGOOD LINE?                               
         BZ    DISP298             NO - SKIP                                    
*                                                                               
         ZIC   RF,BROWUSED         NEXT PRINT LINE                              
         LA    RF,1(RF)                                                         
         STC   RF,BROWUSED                                                      
*                                                                               
         MVC   9(23,R4),=C'THIS IS A MAKEGOOD LINE'                             
         LA    R4,81(R4)                                                        
*                                                                               
DISP298  DS    0H                                                               
         CLC   BROWUSED,BLISTNUM                                                
         BNH   DISP300                                                          
         TM    BSTATUS,BSTSAME     SAME BUYLINE?                                
         BZ    DISPAGEX            NO, NEW BUYLINE SO DON'T SHOW                
         CLI   BLISTNUM,15         **** FOR DISPLAY ONLY                        
         BH    DISPAGEX                                                         
         MVC   BROWUSED,BBUYSTRW   REST OF BUY DIDN'T FIT                       
         B     DISPAGEX                                                         
                                                                                
DISP300  DS    0H                                                               
         TM    PRINTOPT,X'80'      ARE WE PRINTING?                             
         BZ    DISP310                                                          
         OI    PRINTOPT,X'01'      GET NEXT BUY# AND EXIT                       
                                                                                
DISP310  DS    0H                                                               
         GOTO1 SEQ                 GET NEXT BUYLINE                             
         CLC   KEY(RDARKSEQ-RDARKEY),KEYSAVE                                    
         BE    DISP10                                                           
         MVI   BBUYNUM,0           NO MORE BUYS TO DISPLAY                      
                                                                                
DISPAGEX DS    0H                                                               
         L     R1,SVDMCB                                                        
         XC    8(4,R1),8(R1)                                                    
         MVC   8(1,R1),BROWUSED                                                 
         MVC   9(1,R1),BBUYNUM                                                  
         TM    BUYDAILY,BSTDAILY   RETURN IF DAILY FLAG                         
         BZ    *+8                                                              
         MVI   10(R1),BSTDAILY                                                  
         TM    MISCFLG1,MF1EDIER                                                
         BZ    *+8                                                              
         OI    11(R1),X'80'                                                     
         B     EXIT                                                             
         EJECT                                                                  
*****************************************************************               
* SVDEMCT, SAVE OLD DEMO CATEGORIES                                             
* P1 :     BYTE 1 = NUMBER(S) OF DEMO TO BE SAVED AND CONVERT                   
*          BYTE 2-3 A(CONVERTED DEMO SAVE AREA)                                 
*                                                                               
* ALSO SAVE OFF ORDER ROTATION START DAY                                        
*****************************************************************               
SVDEMCT  NTR1                                                                   
         ZIC   R8,0(R1)            NUMBER OF DEMO TO CONVERT                    
         L     R5,0(R1)            A(DEMO SAVE)                                 
*                                                                               
         XC    WORK2,WORK2         A(USER DEFINE DEMO)                          
         LA    R7,WORK2                                                         
*                                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(L'RDARKEY),KEYSAVE                                           
         BNE   SVDEMCTN                                                         
*                                                                               
         GOTO1 GETREC                                                           
*                                                                               
         L     R6,AIOAREA                                                       
         MVI   ELCODE,X'0F'        GET ORDER ROTATION START DAY                 
         BAS   RE,GETEL                                                         
         BNE   SVDM03                                                           
         USING RDARFLEM,R6                                                      
         MVC   ORDROTST,RDAROROT                                                
         DROP  R6                                                               
*                                                                               
SVDM03   DS    0H                                                               
         L     R6,AIOAREA                                                       
         USING RDARREC,R6                                                       
         CLC   =C'$EDI$',RDARAGAD  EDI DOES NOT SUPPORT DEMOS                   
         BE    SVDEMCTY                                                         
         DROP  R6                                                               
*                                                                               
         CLC   KEY(2),=XL2'4100'                                                
         BNE   SVDM05                                                           
*                                                                               
         TM    RDARMISC,X'80'       IS THIS A RESENT?                           
         BO    SVDM05               YES                                         
         CLI   RDARRNUM,0           NO, IF REV# = 0                             
         BE    SVDM05                                                           
         OI    MISCFLG1,MF1REVNW   THIS IS A REV NEW                            
*                                                                               
SVDM05   DS    0H                                                               
         MVI   ELCODE,X'03'        GET DEMO CATEGORIES                          
         BAS   RE,GETEL                                                         
         BNE   SVDEMCTN                                                         
         USING RDARDMEL,R6                                                      
         CLC   RDARDEM1(16),MYSPACES                                            
         BE    SVDEMCTY                                                         
*                                                                               
         LA    R3,0                                                             
         LA    R2,RDARDMEL+RDARDMLQ                                             
         LA    R6,RDARDEM1                                                      
         LA    R4,WORK                                                          
         XC    WORK,WORK                                                        
         DROP  R6                                                               
*                                                                               
SVDM10   DS    0H                                                               
         CLI   0(R6),C'('          USER DEFINED DEMO?                           
         BNE   SVDM12              NO                                           
         MVC   0(3,R4),=X'00D901'  YES, STICK IN DUMMY DEMO                     
         MVC   0(L'RDARDEM1,R7),0(R6)  SAVE USER DEFINE IN OTHER PLACE          
         B     SVDM13                                                           
*                                                                               
SVDM12   DS    0H                                                               
         CLC   0(4,R6),MYSPACES                                                 
         BE    SVDM15                                                           
         MVC   1(1,R4),0(R6)                                                    
         CLI   1(R4),C'T'          FUDGE FOR DEMOCON                            
         BNE   *+8                                                              
         MVI   1(R4),C'I'                                                       
         PACK  DUB(8),1(3,R6)                                                   
         CVB   RF,DUB                                                           
         STC   RF,2(R4)                                                         
*                                                                               
SVDM13   DS    0H                                                               
         AHI   R3,1                                                             
         LA    R4,3(R4)                                                         
         LA    R7,DEMOSIZE(R7)                                                  
*                                                                               
SVDM15   DS    0H                                                               
         LA    R6,L'RDARDEM1(R6)                                                
         CR    R6,R2                                                            
         BNL   SVDM20                                                           
*        CLC   0(4,R6),MYSPACES                                                 
*        BE    SVDM20                                                           
         CR    R3,R8                                                            
         BL    SVDM10                                                           
*                                                                               
SVDM20   DS    0H                                                               
         LA    R6,BLOCKX                                                        
         USING DBLOCKD,R6                                                       
         XC    DBLOCK,DBLOCK                                                    
         MVC   DBFILE,=C'INV'                                                   
         MVC   DBCOMFCS,VCOMFACS                                                
         MVI   DBSELMED,C'T'                                                    
                                                                                
         GOTO1 VDEMOCON,DMCB,((R3),WORK),(2,(R5)),(0,DBLOCKD)                   
         DROP  R6                                                               
*                                                                               
         SR    R3,R3               MERGE USER DEMO AND SYSTEM DEMO              
         LA    R3,MAXDEMO                                                       
         LA    R7,WORK2                                                         
*                                                                               
SVDM50   DS    0H                                                               
         CHI   R3,0                                                             
         BNH   SVDM150                                                          
         OC    0(DEMOSIZE,R7),0(R7)                                             
         BZ    SVDM100                                                          
         MVC   0(DEMOSIZE,R5),0(R7)                                             
SVDM100  DS    0H                                                               
         SHI   R3,1                                                             
         LA    R7,DEMOSIZE(R7)                                                  
         LA    R5,DEMOSIZE(R5)                                                  
         B     SVDM50                                                           
SVDM150  DS    0H                                                               
*                                                                               
SVDEMCTY SR    RC,RC                                                            
SVDEMCTN LTR   RC,RC                                                            
SVDEMCTX DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* DISPLAY DEMO VALUES                                                           
* DEMOCATS = DEMO CATEGORIES                                                    
* BUYDEMOS = DEMO VALUES                                                        
***********************************************************************         
DISDEMO  NTR1                                                                   
         LA    R6,DEMOCATS                                                      
         LA    R2,BUYDEMOS                                                      
         OC    BUYDEMOS,BUYDEMOS                                                
         BZ    DDEMX                                                            
         CLI   0(R6),0                                                          
         BE    DDEMX                                                            
*                                                                               
         ZIC   RF,BROWUSED         NEXT PRINT LINE                              
         LA    RF,1(RF)                                                         
         STC   RF,BROWUSED                                                      
*                                                                               
         LR    R8,R4                                                            
         MVC   9(7,R8),=C'DEMOS: '                                              
         AHI   R8,16                                                            
         LA    R4,81(R4)                                                        
         LA    R1,0                                                             
*                                                                               
DDEM08   DS    0H                                                               
         CLC   0(L'RDARBDM1,R2),MYSPACES                                        
         BE    DDEMX                                                            
         OC    0(L'RDARBDM1,R2),0(R2)                                           
         BZ    DDEMX                                                            
*                                                                               
         CHI   R1,0                  ONLY FLAG FIRST DEMO                       
         BNE   DDEM10                                                           
         TM    OLDDEMOF,DIFCATQ      ORIGINAL CAT = NEW CAT?                    
         BZ    DDEM10                                                           
*                                                                               
         MVI   0(R8),C'*'                                                       
         LA    R8,1(R8)                                                         
*                                                                               
DDEM10   MVC   0(7,R8),0(R6)                                                    
         LA    R8,7(R8)                                                         
         MVI   0(R8),C'/'                                                       
         LA    R8,1(R8)                                                         
*                                                                               
         CHI   R1,0                ONLY FLAG THE FIRST DEMO                     
         BNE   DDEM30                                                           
         TM    OLDDEMOF,DIFDMVAL     ORIGINAL VALUE = NEW VALUE?                
         BZ    DDEM30                YES, DO NOT *                              
         MVI   0(R8),C'*'                                                       
         LA    R8,1(R8)                                                         
*                                                                               
DDEM30   DS    0H                                                               
         EDIT  (C7,0(R2)),(8,(R8)),1,ALIGN=LEFT                                 
         AR    R8,R0                                                            
         AHI   R2,7                                                             
         AHI   R6,7                                                             
         CLI   0(R6),0                                                          
         BE    DDEMX                                                            
         MVC   0(2,R8),=C', '                                                   
         AHI   R8,2                                                             
         LA    R1,1(R1)            INCREASE R1 BY 1                             
         B     DDEM08                                                           
*                                                                               
DDEMX    DS    0H                                                               
         XIT1  REGS=(R4)                                                        
         EJECT                                                                  
***********************************************************************         
* GET BUY COMMENTS                                                              
***********************************************************************         
GETCOMMT NTR1                                                                   
         MVC   SVKEY,KEY                                                        
         LA    R6,KEY                                                           
         USING RDARKEY,R6                                                       
         MVI   RDARKSRT,X'20'      GET BUY COMMENTS                             
         DROP  R6                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(L'RDARKEY),KEYSAVE                                           
         BNE   GETCX                                                            
         GOTO1 GETREC                                                           
                                                                                
         L     R6,AIOAREA                                                       
         USING RDARCTCD,R6                                                      
         MVI   ELCODE,X'02'        COMMENTS                                     
         BAS   RE,GETEL                                                         
         BNE   GETC200                                                          
                                                                                
GETC10   DS    0H                                                               
         CLI   RDARCTLN,2           SKIP IF NO COMMENT                          
         BNH   GETC100                                                          
         CLI   RDARCTLN,72          SHORTEN TO MAX 70 IF TOO LONG               
         BL    *+12                                                             
         LA    R1,69                                                            
         B     GETC20                                                           
                                                                                
         ZIC   R1,RDARCTLN                                                      
         SH    R1,=H'3'             SUBTRACT OVERHEAD LENGTH                    
GETC20   DS    0H                                                               
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   9(0,R4),RDARCTCM                                                 
                                                                                
         ZIC   RF,BROWUSED          NEXT PRINT LINE                             
         LA    RF,1(RF)                                                         
         STC   RF,BROWUSED                                                      
                                                                                
         CLC   BROWUSED,BLISTNUM                                                
         BH    GETCX                                                            
                                                                                
GETC50   DS    0H                                                               
         LA    R4,81(R4)                                                        
                                                                                
GETC100  DS    0H                                                               
         BAS   RE,NEXTEL                                                        
         BE    GETC10                                                           
         DROP  R6                                                               
*                                                                               
GETC200  DS    0H                                                               
         TM    PRINTOPT,X'04'      DON'T SHOW BUY CHANGE ON DAO REPORT          
         BO    GETCX                                                            
         GOTO1 DISPCHG              DISPLAY CHANGE                              
GETCX    DS    0H                                                               
         MVC   KEY,SVKEY                                                        
         GOTO1 HIGH                                                             
         XIT1  REGS=(R4)                                                        
*                                                                               
DISPCHG  NTR1                                                                   
         L     R6,AIOAREA                                                       
         USING RDARCHCD,R6                                                      
         MVI   ELCODE,X'10'         CHANGE COMMENT                              
         BAS   RE,GETEL                                                         
         BNE   DISCHGX                                                          
                                                                                
         ST    R4,FULL              FULL->A(BEGINNING OF LINE)                  
         LA    R3,76(R4)            R3->LINE BOUNDRY                            
         LA    R4,9(R4)             ALIGN WITH PROGRAM NAME                     
         ST    R4,FULL2                                                         
DISG210  DS    0H                                                               
         CLI   RDARCHLN,2           SKIP IF NO COMMENT                          
         BNH   DISG700                                                          
                                                                                
DISG220  DS    0H                                                               
         OI    MISCFLG1,MF1BUMP                                                 
         XC    WORK2,WORK2                                                      
         CLI   RDARCHTP,RDARDTMQ    DAY/TIME                                    
         BE    DISGDTM                                                          
         CLI   RDARCHTP,RDARLENQ    LENGTH                                      
         BE    DISGLEN                                                          
         CLI   RDARCHTP,RDARRATQ    RATE/SPOT                                   
         BE    DISGRAT                                                          
         CLI   RDARCHTP,RDARPRGQ    PROGRAM NAME                                
         BE    DISGPRG                                                          
         CLI   RDARCHTP,RDARDEMQ    DEMO                                        
         BE    DISGDEM                                                          
         CLI   RDARCHTP,RDARDATQ    DATE,SPOT                                   
         BE    DISGDAT                                                          
         CLI   RDARCHTP,RDARDT2Q    DATE,SPOT                                   
         BE    DISGDT2                                                          
         CLI   RDARCHTP,RDARSPTQ    SPOT/WK                                     
         BE    DISGSPT                                                          
         CLI   2(R6),RDARNBYQ       NEW BUY                                     
         BE    DISGNBY                                                          
         B     DISG700                                                          
*                                                                               
*****************                                                               
* DAY/TIME                                                                      
*****************                                                               
DISGDTM  DS    0H                                                               
         MVC   WORK2(13),=C'DAY/TIME WAS '                                      
         SR    R0,R0                                                            
         ZIC   R1,1(R6)                                                         
         SHI   R1,3                                                             
         D     R0,=F'12'                                                        
         LR    R2,R1                R0=NUMBER OF DATE/SPOT PAIR                 
*                                                                               
         LA    R7,3(R6)                                                         
         LA    R5,WORK2+13                                                      
*                                                                               
         USING RDARBYRO,R7                                                      
DISGDM50 DS    0H                                                               
         MVC   BUYROTAT(8),0(R7)                                                
         OC    XMLBUYNM,XMLBUYNM   IF XML, USE ORDER ROTATION START DAY         
         BZ    *+18                                                             
         CLI   ORDROTST,0                                                       
         BE    *+10                                                             
         MVC   BUYROTST,ORDROTST                                                
         GOTO1 BUYROT,DMCB,BUYROTAT                                             
         XC    BUYSTEN,BUYSTEN                                                  
         XC    BSTRTDAY,BSTRTDAY                                                
         XC    BENDDAY,BENDDAY                                                  
         GOTO1 STARTEND,DMCB,BUYROTAT,BUYSTEN                                   
         GOTO1 VOUTDAY,DMCB,BUYDAYS,BUYSTEN,0(R5)                               
         GOTO1 MOVEEOL             MOVE TO END OF LINE                          
*                                                                               
         MVI   0(R4),C'/'                                                       
         LA    R4,1(R4)                                                         
*                                                                               
         XC    WORK2,WORK2                                                      
         CLC   RDARBYST,RDARBYET   DISPLAY SINGLE TIME FOR                      
         BNE   *+10                IDENTICAL START AND END TIMES                
         XC    RDARBYET,RDARBYET                                                
         GOTO1 VUNTIME,DMCB,RDARBYST,WORK2                                      
         OI    MISCFLG1,MF1NOCOM                                                
         GOTO1 MOVEEOL                                                          
         LA    R7,12(R7)                                                        
         LA    R5,WORK2                                                         
         BCT   R2,DISGDM50                                                      
         DROP  R7                                                               
         B     DISG700              NEXT ELEMENT                                
*****************                                                               
* LENGTH                                                                        
*****************                                                               
DISGLEN  DS    0H                                                               
         MVC   WORK2(11),=C'LENGTH WAS '                                        
         LA    R7,3(R6)                                                         
         USING RDARBYSL,R7                                                      
         EDIT  RDARBYSL,(3,WORK2+11),ALIGN=LEFT                                 
         CLI   RDARBYSU,C'M'        IF UNIT IS MINUTES                          
         BNE   *+8                                                              
         MVI   WORK2+13,C'M'                                                    
         GOTO1 MOVEEOL                                                          
         DROP  R7                                                               
         B     DISG700              NEXT ELEMENT                                
*****************                                                               
* RATE/SPOT                                                                     
*****************                                                               
DISGRAT  DS    0H                   RATE/SPOT                                   
         SR    R0,R0                                                            
         ZIC   R1,1(R6)                                                         
         SHI   R1,3                                                             
         D     R0,=F'8'                                                         
         LR    R2,R1                R2=NUMBER OF DATE/SPOT PAIR                 
*                                                                               
         LA    R7,4(R6)                                                         
         USING RDARCHS1,R7                                                      
         MVC   WORK2(14),=C'RATE/SPOT WAS '                                     
         LA    R5,WORK2+14                                                      
*                                                                               
         TM    3(R6),X'80'          COST OVERRIDE?                              
         BO    DISGR20                                                          
         EDIT  (4,RDARCHRT),(8,0(R5)),2,ZERO=NOBLANK,FLOAT=$,ALIGN=LEFT         
         GOTO1 MOVEEOL                                                          
         B     DISGRATX                                                         
*                                                                               
DISGR20  DS    0H                                                               
         GOTO1 VDATCON,DMCB,(2,RDARCHS1),(4,0(R5))                              
         LA    R5,5(R5)                                                         
         CLC   RDARCHSD,RDARCHED                                                
         BE    DISGR30                                                          
         MVI   0(R5),C'-'                                                       
         GOTO1 VDATCON,DMCB,(2,RDARCHE1),(4,1(R5))                              
         LA    R5,6(R5)                                                         
DISGR30  DS    0H                                                               
         MVI   0(R5),C'/'                                                       
         LA    R5,1(R5)                                                         
*                                                                               
DISGR40  DS    0H                                                               
         EDIT  (4,RDARCHRT),(8,0(R5)),2,ZERO=NOBLANK,FLOAT=$,ALIGN=LEFT         
         GOTO1 MOVEEOL                                                          
         XC    WORK2,WORK2                                                      
         LA    R5,WORK2                                                         
*                                                                               
         LA    R7,8(R7)                                                         
         BCT   R2,DISGR20                                                       
DISGRATX DS    0H                                                               
         B     DISG700                                                          
         DROP  R7                                                               
*                                                                               
*****************                                                               
* PROGRAM NAME                                                                  
*****************                                                               
DISGPRG  DS    0H                                                               
         MVC   WORK2(12),=C'PROGRAM WAS '                                       
         ZIC   R1,1(R6)                                                         
         SHI   R1,4                  ELT CODE+EX+LENGTH+SUBCODE                 
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   WORK2+12(0),3(R6)                                                
         GOTO1 MOVEEOL                                                          
         B     DISG700                                                          
*                                                                               
*****************                                                               
* DEMO                                                                          
*****************                                                               
DISGDEM  DS    0H                                                               
         MVC   WORK2(9),=CL9'DEMO WAS '                                         
         LA    R5,WORK2+9                                                       
         CLI   1(R6),RDARCDQ1                                                   
         BE    DISGDO40                                                         
         CLC   RDARCHDC,MYSPACES                                                
         BE    DISGDO40                                                         
*                                                                               
         CLI   RDARCHDC,C'('          USER DEFINED DEMO?                        
         BNE   *+14                                                             
         MVC   0(L'RDARCHDC,R5),RDARCHDC                                        
         B     DISGDO07                                                         
*                                                                               
         MVC   WORK+1(1),RDARCHDC                                               
         CLI   WORK+1,C'T'          FUDGE FOR DEMOCON                           
         BNE   *+8                                                              
         MVI   WORK+1,C'I'                                                      
         PACK  DUB(8),RDARCHDC+1(3)                                             
         CVB   RF,DUB                                                           
         STC   RF,WORK+2                                                        
*                                                                               
         LA    R2,BLOCKX                                                        
         USING DBLOCKD,R2                                                       
         XC    DBLOCK,DBLOCK                                                    
         MVC   DBFILE,=C'INV'                                                   
         MVC   DBCOMFCS,VCOMFACS                                                
         MVI   DBSELMED,C'T'                                                    
*                                                                               
DISGDO05 DS    0H                                                               
         GOTO1 VDEMOCON,DMCB,(1,WORK),(2,(R5)),(0,DBLOCKD)                      
         DROP  R2                                                               
DISGDO07 DS    0H                                                               
         LA    R5,7(R5)                                                         
*                                                                               
DISGDO10 DS    0H                                                               
         CLC   RDARCHDM,MYSPACES                                                
         BE    DISGDO50                                                         
         MVI   0(R5),C'/'                                                       
         LA    R5,1(R5)                                                         
DISGDO40 DS    0H                                                               
         EDIT  (C7,RDARCHDM),(8,(R5)),1,ALIGN=LEFT                              
*                                                                               
DISGDO50 DS    0H                                                               
         GOTO1 MOVEEOL                                                          
         B     DISG700                                                          
*                                                                               
*****************                                                               
* DATE/SPOT                                                                     
*****************                                                               
DISGDAT  DS    0H                                                               
         CLI   RDARDTFG,RDARDTFW    BOTH PREV/CURR BUY ARE WEEKLY?              
         BE    DISGD10              YES, THAT IS OK                             
         CLI   RDARDTFG,RDARDTFD    BOTH PREV/CURR BUY ARE DAILY?               
         BE    DISGD10              OK TOO                                      
*                                                                               
         MVC   WORK2(24),=CL24'THIS BUY WAS A DAILY BUY'                        
         CLI   RDARDTFG,RDARDTFC                                                
         BNE   *+14                                                             
         MVC   WORK2(28),=CL28'THIS BUY WAS NOT A DAILY BUY'                    
         B     DISGD05                                                          
*                                                                               
         CLI   RDARDTFG,RDARDTFO                                                
         BNE   *+10                                                             
         MVC   WORK2(28),=CL28'OVRFLOW, CANT COMPARE DT/SPT'                    
*                                                                               
DISGD05  DS    0H                                                               
         GOTO1 MOVEEOL                                                          
         B     DISG700              NEXT ELEMENT                                
*                                                                               
DISGD10  DS    0H                                                               
         SR    R0,R0                                                            
         ZIC   R1,1(R6)                                                         
         SHI   R1,3                                                             
         C     R1,=F'6'                                                         
         BL    DISG700                                                          
         D     R0,=F'6'                                                         
         LR    R2,R1                R0=NUMBER OF DATE/SPOT PAIR                 
*                                                                               
         MVC   WORK2(22),=CL22'DATES/SPOTS CHANGES = '                          
         LA    R7,4(R6)                                                         
         USING RDARCHSD,R7                                                      
         LA    R5,WORK2+22                                                      
DISGD20  DS    0H                                                               
         GOTO1 VDATCON,DMCB,(2,RDARCHSD),(4,0(R5))                              
         LA    R5,5(R5)                                                         
         CLC   RDARCHSD,RDARCHED                                                
         BE    DISGD40                                                          
         MVI   0(R5),C'-'                                                       
         GOTO1 VDATCON,DMCB,(2,RDARCHED),(4,1(R5))                              
         LA    R5,6(R5)                                                         
*                                                                               
DISGD40  DS    0H                                                               
         MVI   0(R5),C'('                                                       
         EDIT  (2,RDARCHSP),(3,2(R5)),ZERO=NOBLANK,ALIGN=LEFT                   
         MVI   1(R5),C'+'          SIGN                                         
         MVC   HALF,RDARCHSP                                                    
         LH    R1,HALF                                                          
         CHI   R1,0                                                             
         BH    *+8                                                              
         MVI   1(R5),C'-'                                                       
*                                                                               
         LA    R1,5(R5)            GET TO THE END OF INPUT                      
         CLI   0(R1),X'40'                                                      
         BH    *+8                                                              
         BCT   R1,*-8                                                           
         LR    R5,R1                                                            
*                                                                               
         MVC   1(4,R5),=C'/WK)'                                                 
*                                                                               
         CLI   RDARDTFG,RDARDTFD                                                
         BNE   *+10                                                             
         MVC   2(2,R5),=C'DY'                                                   
*                                                                               
         LA    R5,5(R5)                                                         
         GOTO1 MOVEEOL                                                          
         XC    WORK2,WORK2                                                      
         LA    R7,6(R7)                                                         
         LA    R5,WORK2                                                         
         BCT   R2,DISGD20                                                       
         B     DISG700                                                          
         DROP  R7                                                               
*                                                                               
*****************                                                               
* SPOT/WK                                                                       
*****************                                                               
DISGSPT  DS    0H                                                               
         MVC   WORK2(12),=CL12'SPOT/WK WAS '                                    
         EDIT  (1,3(R6)),(3,WORK2+12),ZERO=NOBLANK,ALIGN=LEFT                   
         GOTO1 MOVEEOL                                                          
         B     DISG700                                                          
*                                                                               
******************                                                              
* DATES/SPOTS/COST                                                              
******************                                                              
DISGDT2  DS    0H                  DUP START DAY                                
         MVC   WORK2(21),=CL21'DATES/SPOTS/COST WAS '                           
*                                                                               
         SR    R0,R0                                                            
         ZIC   R1,1(R6)                                                         
         SHI   R1,3                                                             
         D     R0,=F'10'                                                        
         LR    R2,R1               R0=NUMBER OF DATE/SPOT PAIR                  
*                                                                               
         LA    R5,WORK2+21                                                      
         LA    R7,3(R6)                                                         
         USING RDARCHSD,R7                                                      
*                                                                               
DGDT200  DS    0H                                                               
         GOTO1 VDATCON,DMCB,(2,RDARCHSD),(4,0(R5))                              
         LA    R5,5(R5)                                                         
         CLC   RDARCHSD,RDARCHED                                                
         BE    DISGD40                                                          
         MVI   0(R5),C'-'                                                       
         GOTO1 VDATCON,DMCB,(2,RDARCHED),(4,1(R5))                              
         LA    R5,6(R5)                                                         
*                                                                               
         MVI   0(R5),C'/'                                                       
         EDIT  RDARCHSP,(3,1(R5)),ALIGN=LEFT,ZERO=NOBLANK                       
*                                                                               
         LA    R5,WORK2+L'WORK2-1  GET TO THE END OF INPUT                      
         CLI   0(R5),X'40'                                                      
         BH    *+8                                                              
         BCT   R5,*-8                                                           
*                                                                               
         MVI   1(R5),C'/'                                                       
         EDIT  RDARCHCT,(8,2(R5)),2,ALIGN=LEFT,ZERO=NOBLANK,FLOAT=$             
*                                                                               
         GOTO1 MOVEEOL                                                          
         XC    WORK2,WORK2                                                      
         LA    R5,WORK2                                                         
         LA    R7,10(R7)                                                        
         BCT   R2,DGDT200                                                       
*                                                                               
         DROP  R7                                                               
         B     DISG700                                                          
*                                                                               
DISGNBY  DS    0H                                                               
         CLI   3(R6),1                                                          
         BNE   *+10                                                             
         MVC   WORK2(17),=CL17'THIS IS A NEW BUY'                               
         GOTO1 MOVEEOL                                                          
         B     DISG700                                                          
*                                                                               
DISG700  DS    0H                                                               
         BAS   RE,NEXTEL                                                        
         BE    DISG210                                                          
         L     R4,FULL                                                          
         LA    R4,81(R4)           NEXT LINE                                    
*                                                                               
         TM    MISCFLG1,MF1BUMP                                                 
         BZ    DISCHGX                                                          
         ZIC   RF,BROWUSED                                                      
         LA    RF,1(RF)                                                         
         STC   RF,BROWUSED                                                      
         NI    MISCFLG1,X'FF'-MF1BUMP                                           
*                                                                               
DISCHGX  DS    0H                                                               
         MVC   KEY,SVKEY                                                        
         GOTO1 HIGH                                                             
         XIT1  REGS=(R4)                                                        
         DROP  R6                                                               
         EJECT                                                                  
MOVEEOL  NTR1                                                                   
         LA    R1,WORK2+L'WORK2-1  GET TO THE END OF INPUT                      
         CLI   0(R1),X'40'                                                      
         BH    *+8                                                              
         BCT   R1,*-8                                                           
*                                                                               
         LR    R5,R3               R3->END OF THIS LINE                         
         SR    R3,R4               R4->PRINT LINE                               
         LA    R0,WORK2                                                         
         SR    R1,R0               R1->L'INPUT SO FAR                           
         CR    R3,R1               R3 = SPACE LEFT ON THE LINE                  
         BNL   MOVEE20                                                          
         ZIC   RF,BROWUSED                                                      
         AHI   RF,1                                                             
         STC   RF,BROWUSED                                                      
         CLC   BROWUSED,BLISTNUM                                                
         BH    DISCHGX                                                          
*                                                                               
         L     R4,FULL                                                          
         LA    R4,81(R4)                                                        
         ST    R4,FULL                                                          
         LA    R5,76(R4)           R5-> END OF THIS LINE                        
*                                                                               
         LA    R4,9(R4)                                                         
         ST    R4,FULL2                                                         
*                                                                               
MOVEE20  LR    R3,R5               RESTORE R3                                   
*                                                                               
         TM    MISCFLG1,MF1NOCOM                                                
         BO    MOVEE50                                                          
         L     R5,FULL2                                                         
         CR    R5,R4                                                            
         BE    *+14                                                             
         MVC   0(2,R4),=C', '                                                   
         LA    R4,2(R4)                                                         
*                                                                               
MOVEE50  DS    0H                                                               
         NI    MISCFLG1,X'FF'-MF1NOCOM                                          
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),WORK2                                                    
         AHI   R1,1                                                             
         AR    R4,R1                NEXT SPACE TO WRITE                         
         XIT1  REGS=(R3,R4)                                                     
***********************************************************************         
* CALULATE ONE-BYTE DAYS OF WEEK                                                
***********************************************************************         
BUYROT   NTR1                                                                   
         L     R2,0(R1)            ROTATION                                     
                                                                                
         LA    R1,7                                                             
         ZIC   RE,=X'40'                                                        
         SR    R0,R0                                                            
*                                                                               
BUYR30   DS    0H                                                               
         CLI   0(R2),C' '                                                       
         BE    BUYR40                                                           
         CLI   0(R2),0                                                          
         BE    BUYR40                                                           
*                                                                               
         OR    R0,RE                                                            
*                                                                               
BUYR40   DS    0H                                                               
         LA    R2,1(R2)                                                         
         SRL   RE,1                                                             
         BCT   R1,BUYR30                                                        
                                                                                
         STC   R0,BUYDAYS                                                       
         B     EXIT                                                             
         EJECT                                                                  
**********************************************************************          
*   STARTEND:  CONVERTS ROTATION MATRIX AND START DAY TO ONE-BYTE               
*        START/END DAY, INSERTS INTO ADDRESS                                    
**********************************************************************          
STARTEND NTR1                                                                   
         L     R2,0(R1)            A(INPUT: ROTATION+START DAY)                 
         L     R3,4(R1)            A(RECEIVING FIELD)                           
*                                                                               
         ZIC   RF,7(R2)            ROTATION START DAY                           
         SLL   RF,28               STRIP OFF ZONE BITS                          
         SRL   RF,28               SHIFT START DAY BACK                         
*                                                                               
         LA    R8,0(R2)            A(ROTATION FIELD)                            
         LR    RE,R8               CALCULATE END OF ROTATION FIELD              
         LA    RE,7(RE)                                                         
         AR    R8,RF               GET A(1ST DAY+1)                             
         BCTR  R8,0                BACK OFF TO A(1ST ENTRY)                     
         LR    RF,R8               SAVE A(1ST ENTRY)                            
*                                     FOR WHEN 1ST ENTRY IS ONLY ENTRY          
         LA    R0,7                SET LOOP CONTROL TO SIX DAYS                 
STEN0040 EQU   *                                                                
         CR    R8,RE               END OF ROTATION FIELD REACHED?               
         BNE   STEN0080            NO                                           
         LA    R8,0(R2)            YES  - GO BACK TO FIRST LOCATION             
STEN0080 EQU   *                                                                
         CLI   0(R8),C' '          ARRAY POSITION USED?                         
         BE    STEN0200            NO  - SKIP IT                                
         CLI   0(R8),0             ARRAY POSITION USED?                         
         BE    STEN0200            NO  - SKIP IT                                
*                                                                               
         CLI   BSTRTDAY,0          ANYTHING IN START DAY?                       
         BNE   STEN0120            YES - DON'T REPLACE                          
         LA    R4,7                NO  - CALCULATE STARTDAY                     
         SR    R4,R0               SUBTRACT REMAINING DAYS                      
                                                                                
         ZIC   R5,7(R2)            ROTATION START DAY                           
         SLL   R5,28               STRIP OFF ZONE BITS                          
         SRL   R5,28               SHIFT START DAY BACK                         
                                                                                
         AR    R4,R5               ADD ROTATION START DAY                       
         CH    R4,=H'7'            OUT OF WEEK ROTATOR?                         
         BNH   STEN0090                                                         
         SH    R4,=H'7'                                                         
*                                                                               
STEN0090 DS    0H                                                               
         STC   R4,BSTRTDAY         SAVE CALCULATED STARTDAY                     
STEN0120 EQU   *                                                                
         OC    0(1,R3),0(R3)       RECEIVING FIELD ENTRY MADE?                  
         BNZ   STEN0160            YES - START DAY ENTERED                      
         SLL   R4,4                NO  - MOVE START DAY TO HIGH NYBBLE          
         STC   R4,0(R3)            INSERT INTO RECORD                           
STEN0160 EQU   *                                                                
         LR    RF,R8               YES - SAVE NEW ARRAY POSITION                
STEN0200 EQU   *                                                                
         LA    R8,1(R8)            BUMP TO NEXT ARRAY LOCATION                  
         BCT   R0,STEN0040         GO BACK AND CHECK NEXT                       
         LA    R8,0(R2)            A(START OF ARRAY)                            
         BCTR  R8,0                BACK UP 1 POSITION                           
         SR    RF,R8               CALCULATED DISPLACEMENT                      
         STC   RF,BENDDAY          SAVE END DAY FOR EFF DATE SETTING            
         ZIC   RE,0(R3)            RETRIEVE START DAY                           
         AR    RF,RE               ADD START TO END                             
         STC   RF,0(R3)            PUT IT BACK IN RECORD                        
         LA    R8,0(R2)            COUNT NUMBER OF DAYS                         
         SR    RF,RF                                                            
         LA    R0,7                                                             
STEN0240 EQU   *                                                                
         CLI   0(R8),C' '          DAY ACTIVE?                                  
         BE    STEN0280            NO                                           
         CLI   0(R8),0             DAY ACTIVE?                                  
         BE    STEN0280            NO                                           
         LA    RF,1(RF)            YES - ADD 1                                  
STEN0280 EQU   *                                                                
         LA    R8,1(R8)            BUMP TO NEXT POSITION                        
         BCT   R0,STEN0240         GO BACK AND CHECK NEXT                       
         C     RF,=F'1'            COUNT = 1?                                   
         BH    STEN0320            NO  - HIGHER - EXIT                          
         BE    STEN0300            YES - SET START=END IN RECORD                
         OI    MISCFLG1,MF1EDIER   SHOULD NEVER HAPPEN                          
*                                     MEANS EMPTY ARRAY!!!                      
STEN0300 EQU   *                                                                
         ZIC   RF,0(R3)            RETRIEVE START/END DAY                       
         SLL   RF,28               DROP START DAY                               
         SRL   RF,24               MOVE END DAY BACK TO HI NYBBLE               
         NI    0(R3),X'0F'         CLEAR START DAY                              
         ZIC   RE,0(R3)            RETRIEVE 0/END DAY                           
         AR    RE,RF               ADD NEW START DAY                            
         STC   RE,0(R3)            MOVE IT BACK                                 
STEN0320 EQU   *                                                                
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* BUYROTAT HAS BUY ROTATION INFO                                                
* R6 IS POINTING TO BUY DETAIL X'02' ELEMENT                                    
* BUYHEAD HAS BUY HEADER X'01' ELEMENT                                          
***********************************************************************         
BUYLINE  NTR1                                                                   
         LA    R3,BUYHEAD          NO MOD CODES FOR ALL BUY                     
         USING RDARBYEL,R3                                                      
*                                                                               
* CHECK IF RECORD IS MARKED AS SOFT DELETED                                     
*                                                                               
         CLI   RDARBYLN,RDARBYOL   NEW LENGTH ONLY                              
         BNH   BUYL10                                                           
                                                                                
         TM    RDARBYDL,X'80'                                                   
         BZ    *+8                                                              
         OI    BSTATUS,BSTCANCL    SOFT DELETE                                  
*        DROP  R3                                                               
         USING RDARBUEL,R6                                                      
                                                                                
* DISPLAY START-END TIMES                                                       
BUYL10   DS    0H                  IF SAME BUY AND DAILY, SKIP TIME             
         TM    BSTATUS,BSTSAME+BSTDAILY                                         
         BO    BUYL30                                                           
                                                                                
         CLC   BUYSTTIM,BUYENTIM   DISPLAY SINGLE TIME FOR                      
         BNE   *+10                IDENTICAL START AND END TIMES                
         XC    BUYENTIM,BUYENTIM                                                
         GOTO1 VUNTIME,DMCB,BUYSTTIM,LBUYTIME                                   
                                                                                
* DISPLAY START-END DAYS                                                        
         TM    BSTATUS,BSTDAILY    CHECK IF DAILY BUY                           
         BZ    BUYL40                                                           
*                                                                               
* DAILY BUYS DISPLAY SINGLE DAY AND EFFECTIVE DATE                              
*                                                                               
BUYL30   DS    0H                                                               
         GOTO1 VDATCON,DMCB,(2,RDARBUSD),(0,WORK)                               
         GOTO1 VGETDAY,DMCB,WORK,LBUYDAYS                                       
         GOTO1 VDATCON,DMCB,(2,RDARBUSD),(4,LBUYDATE)                           
         B     BUYL120                                                          
                                                                                
BUYL40   DS    0H                                                               
         GOTO1 VOUTDAY,DMCB,BUYDAYS,BUYSTEN,LBUYDAYS                            
                                                                                
         TM    BSTATUS,BSTORBIT    ORBIT BUY, DON'T SHOW REST                   
         BO    BUYLX                                                            
*                                                                               
* CALULATE EFFECTIVE DATES                                                      
*                                                                               
         GOTO1 VDATCON,DMCB,(2,RDARBUSD),(0,WORK)                               
*                                  CONVERT START DATE TO EBCDIC                 
         ZIC   RF,RDARBUWK         CALCULATE NUMBER OF DAYS                     
         LTR   RF,RF               AGENCY CAN SEND ZERO WEEKS                   
         BZ    *+6                                                              
         BCTR  RF,0                MAKE WEEKS ZERO RELATIVE                     
         MH    RF,=H'7'            MULTIPLY WEEKS BY 7                          
         GOTO1 VADDAY,DMCB,WORK,WORK+6,(RF)                                     
*                                                                               
         GOTO1 VGETDAY,DMCB,WORK,WORK+12                                        
*                                  GET DAY OF START WEEK                        
         ZIC   RF,DMCB             GET DAY OF WEEK                              
         BCTR  RF,0                MAKE DAY ZERO RELATIVE                       
*                                                                               
         LTR   RF,RF               MONDAY?                                      
         BNZ   BUYL45                                                           
         OC    XMLBUYNM,XMLBUYNM   DON'T CHECK MONDAY IF XML                    
         BZ    BUYL100                                                          
*                                                                               
BUYL45   DS    0H                                                               
         LNR   RF,RF               NEGATE REGISTER                              
         GOTO1 VADDAY,DMCB,WORK,WORK+12,(RF)                                    
*                                  BACK UP TO MONDAY                            
*                                                                               
* IN CASE OF OUT OF WEEK ROTATORS, COMPARE BUY START DAY AND ROTATION           
* START DAY TO GET THE CORRECT MONDAY DATE                                      
*                                                                               
         XC    BRTSTDAY,BRTSTDAY                                                
         MVN   BRTSTDAY,BUYROTST   CONVERT ROTATION START DAY TO BINARY         
         CLC   BSTRTDAY,BRTSTDAY                                                
         BNL   BUYL50                                                           
         MVC   WORK(6),WORK+12                                                  
         LA    RF,7                                                             
         GOTO1 VADDAY,DMCB,WORK,WORK+12,(RF)                                    
*                                                                               
BUYL50   DS    0H                                                               
         MVC   WORK(6),WORK+12                                                  
                                                                                
BUYL100  DS    0H                                                               
         OC    ORBITSTT,ORBITSTT   IF ORBIT, USE 1ST DAY OF 1ST ORBIT           
         BZ    *+10                                                             
         MVC   BSTRTDAY,ORBITSTT                                                
                                                                                
         ZIC   RF,BSTRTDAY         ADD START DAY                                
         BCTR  RF,0                MAKE ZERO RELATIVE                           
         GOTO1 VADDAY,DMCB,WORK,WORK+12,(RF)                                    
*                                  BUMP TO START DAY IN WEEK                    
         GOTO1 VDATCON,DMCB,(0,WORK+12),(4,LBUYDATE)                            
         MVC   BUYEFFST,WORK+12                                                 
*                                                                               
*INSERT EFFECTIVE START DATE                                                    
*                                                                               
         GOTO1 VGETDAY,DMCB,WORK+6,WORK+12                                      
*                                  GET DAY OF END   WEEK                        
         ZIC   RF,DMCB             GET DAY OF WEEK                              
         BCTR  RF,0                MAKE DAY ZERO RELATIVE                       
         LTR   RF,RF               MONDAY?                                      
         BZ    BUYL110             YES - LEAVE                                  
         LNR   RF,RF               NEGATE REGISTER                              
         CLC   BSTRTDAY,BENDDAY    START/END ON SAME DAY?                       
*                                    (SINGLE-DAY BUY?)                          
         BNH   BUYL105             YES - DON'T BUMP TO NEXT WEEK                
*                                  START < END DAY: NOT AN OOWR                 
*                                   DON'T BUMP                                  
         LA    RF,7(RF)            BUMP IT INTO NEXT WEEK                       
                                                                                
BUYL105  DS    0H                                                               
         GOTO1 VADDAY,DMCB,WORK+6,WORK+12,(RF)                                  
*                                  BACK UP TO MONDAY                            
*                                                                               
* IN CASE OF OUT OF WEEK ROTATORS, COMPARE BUY START DAY AND ROTATION           
* START DAY TO GET THE CORRECT MONDAY DATE                                      
*                                                                               
         XC    BRTSTDAY,BRTSTDAY                                                
         MVN   BRTSTDAY,BUYROTST   CONVERT ROTATION START DAY TO BINARY         
         CLC   BSTRTDAY,BRTSTDAY                                                
         BNL   BUYL108                                                          
         MVC   WORK+6(6),WORK+12                                                
         LA    RF,7                                                             
         GOTO1 VADDAY,DMCB,WORK+6,WORK+12,(RF)                                  
*                                                                               
BUYL108  DS    0H                                                               
         MVC   WORK+6(6),WORK+12                                                
                                                                                
BUYL110  DS    0H                                                               
         OC    ORBITEND,ORBITEND   IF ORBIT, USE LAST DAY OF LAST ORBIT         
         BZ    *+10                                                             
         MVC   BENDDAY,ORBITEND                                                 
                                                                                
         ZIC   RF,BENDDAY          ADD END   DAY                                
         BCTR  RF,0                MAKE ZERO RELATIVE                           
         GOTO1 VADDAY,DMCB,WORK+6,WORK+12,(RF)                                  
*                                  BUMP TO END   DAY IN WEEK                    
*INSERT EFFECTIVE END DATE                                                      
*                                                                               
         CLC   BUYEFFST,WORK+12                                                 
         BE    BUYL120                                                          
         MVI   LBUYDATE+5,C'-'                                                  
         LA    R3,LBUYDATE                                                      
         GOTO1 VDATCON,DMCB,(0,WORK+12),(4,6(R3))                               
                                                                                
BUYL120  DS    0H                                                               
* NUMBER OF WEEKS                                                               
         EDIT  RDARBUWK,(2,LBUYNW),ZERO=NOBLANK                                 
         MVI   LBUYNW+2,C'W'                                                    
                                                                                
* NUMBER PER WEEK                                                               
         TM    BSTATUS,BSTCANCL                                                 
         BO    BUYLX               DON'T SHOW NPW/RATE/SPT IF DELETED           
         EDIT  RDARBUSW,(3,LBUYNPW),ZERO=NOBLANK                                
                                                                                
         ZIC   RF,RDARBUWK                                                      
         XC    HALF,HALF                                                        
         MVC   HALF+1(1),RDARBUSW                                               
         MH    RF,HALF                                                          
                                                                                
* NUMBER OF SPOTS                                                               
         EDIT  (RF),(4,LBUYSPT),ZERO=NOBLANK                                    
                                                                                
         TM    BSTATUS,BSTSAME     IF SAME BUYLINE, SKIP SPOT LENGTH            
         BZ    BUYL125                                                          
         CLC   SPTCOST,RDARBU$$    SAME BUYLINE, BUT CHK IF MULTI-RATE          
         BNE   BUYL140                                                          
         B     BUYLX                                                            
                                                                                
* SPOT LENGTH                                                                   
BUYL125  DS    0H                                                               
         LA    R3,BUYHEAD                                                       
         USING RDARBYEL,R3                                                      
                                                                                
         CLI   RDARBYSU,C'M'       IF UNIT IS MINUTES                           
         BNE   BUYL130                                                          
         EDIT  RDARBYSL,(3,LBUYLENM)                                            
         MVI   LBUYLEN+2,C'M'                                                   
         B     BUYL140                                                          
                                                                                
BUYL130  DS    0H                                                               
         EDIT  RDARBYSL,(3,LBUYLEN)                                             
                                                                                
* RATE                                                                          
BUYL140  DS    0H                                                               
         EDIT  RDARBU$$,(10,LBUYRATE),2,FLOAT=-,COMMAS=YES                      
         MVC   SPTCOST,RDARBU$$                                                 
                                                                                
BUYLX    DS    0H                                                               
         B     EXIT                                                             
         DROP  R3,R4,R6                                                         
         EJECT                                                                  
***********************************************************************         
* GETODMO - GET THE BUY COMMENT RECORD                                          
*           READ THE CHANGE COMMENT TO SEE IF THERE IS CHANGE IN DEMO           
*           CAT AND VAL, COMPARISONS ARE DONE IN REDAR01 MODULE                 
***********************************************************************         
GETODMO  NTR1                                                                   
*                                                                               
         NI    OLDDEMOF,X'FF'-DIFCATQ-DIFDMVAL                                  
*                                                                               
         MVC   SVKEY,KEY                                                        
         XC    OLDDEMOF,OLDDEMOF                                                
         MVC   KEY,SVKEY           WE DON'T HAVE A 4101 REC                     
K        USING RDARKEY,KEY         SO CHECK COMMENT RECORD FOR PRV DEM          
         MVI   K.RDARKRT,X'40'                                                  
         MVI   K.RDARKSRT,X'20'                                                 
         DROP  K                                                                
         GOTO1 HIGH                                                             
         CLC   KEYSAVE(27),KEY                                                  
         BNE   RESTORE                                                          
         GOTO1 GETREC                                                           
*                                                                               
         L     R6,AIOAREA                                                       
         MVI   ELCODE,X'10'                                                     
         BAS   RE,GETEL                                                         
         BNE   RESTORE             NO CHG COMMENT ELT                           
GETO020  DS    0H                                                               
         USING RDARCHEL,R6                                                      
         CLI   RDARCHTP,RDARDEMQ   SUBTYPE = DEMO?                              
         BNE   GETO050                                                          
*                                                                               
         CLC   RDARCHDM,MYSPACES                                                
         BE    *+8                                                              
         OI    OLDDEMOF,DIFDMVAL   SAVE DEMO VALUE                              
*                                                                               
         CLI   RDARCHLN,RDARCDQ1   OLD FORMAT DEMO ELEMENT?                     
         BE    RESTORE             YES, ONLY HAVE DEMO VALUE                    
*                                                                               
         CLC   RDARCHDC,MYSPACES                                                
         BE    *+8                                                              
         OI    OLDDEMOF,DIFCATQ                                                 
*                                                                               
GETO050  DS    0H                                                               
         BAS   RE,NEXTEL           GET NEXT CHANGE COMMENT ELT                  
         BNE   RESTORE                                                          
         B     GETO020             CHECK NEXT ONE                               
*                                                                               
RESTORE  MVC   KEY,SVKEY                                                        
         GOTO1 HIGH                RESTORE SEQUENCE                             
         GOTO1 GETREC                                                           
GETODMOX XIT1                                                                   
***********************************************************************         
* DATAMGR CALLS                                                                 
***********************************************************************         
HIGH     NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,(DMINBTS,=C'DMRDHI'),=C'REPDIR',          X        
               KEYSAVE,KEY,0                                                    
         XIT1                                                                   
*                                                                               
SEQ      NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,(DMINBTS,=C'DMRSEQ'),=C'REPDIR',          X        
               KEYSAVE,KEY,0                                                    
         XIT1                                                                   
*                                                                               
GETREC   NTR1                                                                   
         GOTO1 VDATAMGR,DMCB,(DMINBTS,=C'GETREC'),=C'REPFILE',KEY+28,  X        
               AIOAREA,DMWORK                                                   
         XIT1                                                                   
*                                                                               
         EJECT                                                                  
         GETEL R6,DATADISP,ELCODE                                               
         LTORG                                                                  
         EJECT                                                                  
MYSPACES DC    40C' '                                                           
*                                                                               
WORKD    DSECT                                                                  
DMWORK   DS    12D                                                              
*                                                                               
VCOMFACS DS    A                   A(COMFACS)                                   
VOUTDAY  DS    A                   A(OUTDAY)                                    
VUNTIME  DS    A                   A(VUNTIME)                                   
VDEMOCON DS    A                   A(DEMOCON)                                   
*                                                                               
VDATAMGR DS    A                   A(DATAMGR)                                   
VDATCON  DS    A                   A(VDATCON)                                   
VADDAY   DS    A                   A(VADDAY)                                    
VGETDAY  DS    A                   A(VGETDAY)                                   
*                                                                               
VREDARTK DS    A                   A(REDARTK)                                   
VPERVERT DS    A                   A(PERVERT)                                   
TKODATE  DS    CL3                 TAKODATE(YMD BINARY)                         
MGLINK   DS    X                   AGENCY MAKEGOOD TARGET LINE                  
*                                                                               
ABUYKEY  DS    A                                                                
BUYKEY   DS    CL32                                                             
KEY      DS    CL32                                                             
KEYSAVE  DS    CL32                                                             
SVKEY    DS    CL32                                                             
DMCB     DS    6F                                                               
FULL     DS    F                                                                
FULL2    DS    F                                                                
DUB      DS    D                                                                
HALF     DS    H                                                                
DMINBTS  DS    X                                                                
AIOAREA  DS    A                                                                
SVDMCB   DS    A                                                                
DATADISP DS    H                                                                
ELCODE   DS    X                                                                
WORK     DS    CL64                                                             
WORK2    DS    CL64                                                             
DEMOCATS DS    CL64                                                             
*                                                                               
ORDROTST DS    CL1                 ORDER ROTATION START DAY                     
*                                                                               
BUYORBIT DS    53CL12              BUY ORBIT ARRAY                              
*                                                                               
BUYROTAT DS    CL7                 BUY ROTATION                                 
BUYROTST DS    CL1                 BUY ROTATION START DAY                       
BUYSTTIM DS    CL2                 BUY START TIME                               
BUYENTIM DS    CL2                 BUY END TIME                                 
*                                                                               
BUYHEAD  DS    CL(RDARBYLE)        BUY HEADER X'01' DESCRIPTIVE ELEMT           
*                                                                               
BUYDEMOS DS    CL28                BUY DEMOS                                    
*                                                                               
SVDEMOS  DS    0CL14               OLD DEMOS                                    
OLDDEMOS DS    CL7                 FIRST DEMO VALUE                             
OLDDMCAT DS    CL7                 FIRST DEMO CATEGORY                          
*                                                                               
BUYDAYS DS     X                   1-BYTE DAYS OF WEEK   X'40' - MON            
*                                                        X'20' - TUE            
*                                                        ...     ...            
*                                                        X'01' - SUN            
BUYSTEN DS     X                   1-BYTE START-END DAYS                        
*                                    BITS 7-4 START      X'01' - MON            
*                                    BITS 3-0 END        X'02' - TUE            
*                                                        ...     ...            
*                                                        X'07' - SUN            
BSTRTDAY DS    X                   BUY START DAY                                
BENDDAY  DS    X                   BUY END DAY                                  
BRTSTDAY DS    X                   BUY ROTATION START DAY                       
BUYEFFST DS    CL6                 BUY EFFECTIVE START DATE                     
ORBITSTT DS    X                   FIRST DAY OF FIRST ORBIT                     
ORBITEND DS    X                   LAST DAY OF LAST ORBIT                       
BSTATUS  DS    X                   DISPLAY FLAG                                 
BSTSAME  EQU   X'02'               SAME BUY DON'T PRINT BUY#/RATE/LEN           
BSTORBIT EQU   X'04'               ORBIT BUY, PRINT DAY/TIME ONLY               
BSTMGOOD EQU   X'40'               BUY IS MAKEGOOD                              
BSTDAILY EQU   X'80'               BUY IS DAILY                                 
BSTCANCL EQU   X'20'               BUY IS CANCELLED                             
BLISTNUM DS    X                   MAX ROWS FREE TO LIST                        
BROWUSED DS    X                   ACTUALLY NUMBER OF ROWS USED                 
BBUYSTRW DS    X                   START ROW NUMBER OF CURRENT BUY              
BBUYNUM  DS    X                   LAST BUY NUMBER                              
PRINTOPT DS    X                   PRINT OPTION                                 
*                                  X'80' PRINT ONLY ONE BUY AND EXIT            
*                                  X'40' DO TAKEOVER CHOPPING                   
*                                  X'20' SHOW MG INDICATOR                      
*                                  X'10' DON'T PRINT DEMOS                      
*                                  X'08' PRINT SHADOW(PASS BK DEL)              
*                                  X'04' DO NOT SHOW BUY CHANGE COMMT           
*                                  X'02' ONLY SHOW CHANGE COMMT                 
*                                  X'01' GET NEXT BUYLINE # AND EXIT            
SPTCOST  DS    XL4                 PREVIOUS BUY'S SPOT COST                     
BUYDAILY DS    X                   RETURN BUY DAILY STATUS                      
EFFDATES DS    CL12                AGENCY HEADER EFFECTIVE DATES EBCDIC         
OLDDEMOF DS    XL1                 OLD DEMO FLAG                                
DIFDMVAL EQU   X'80'               NO OLD DEMO                                  
DIFCATQ  EQU   X'40'               NO CORRESPONDING OLD DEMO RECORD             
MISCFLG1 DS    XL1                                                              
MF1NOCOM EQU   X'80'               DO NOT PRINT COMMA                           
MF1BUMP  EQU   X'40'               BUMP AT LEAST ONE LINE                       
MF1REVNW EQU   X'20'               THIS IS A NEW REVISION                       
MF1EDIER EQU   X'01'               EDI TRANSLATION ERROR ENCOUNTERED            
XMLBUYNM DS    XL2                 AGENCY BUY NUMBER IF XML ORDER               
BLOCKX   DS    XL265               FOR DEMOCON                                  
WORKX    EQU   *                                                                
         EJECT                                                                  
DEMOSIZE EQU   7                   DEMO SIZE ON DISPLAY                         
MAXDEMO  EQU   4                   MAXIMUM NUMBER OF DEMO SUPPORTED             
*                                                                               
* BUY LIST LINE                                                                 
*                                                                               
LBUYD    DSECT                                                                  
LBUYMC   DS    CL2                                                              
         DS    CL1                                                              
LBUYLINE DS    CL3                                                              
LBUYTYPE DS    CL1                                                              
LBUYDAYS DS    CL12                                                             
         DS    CL1                                                              
LBUYTIME DS    CL11                                                             
LBUYLENM DS    CL1                                                              
LBUYLEN  DS    CL3                                                              
         DS    CL1                                                              
LBUYDATE DS    CL12                                                             
         DS    CL1                                                              
LBUYNW   DS    CL3                                                              
         DS    CL2                                                              
LBUYMGI  DS    CL2                                                              
         DS    C                                                                
LBUYMGL  DS    CL3                                                              
         DS    CL1                                                              
LBUYNPW  DS    CL3                                                              
         DS    CL1                                                              
LBUYRATE DS    CL10                                                             
LBUYSPT  DS    CL4                                                              
         DS    CL2                                                              
LBUYLENQ EQU   *-LBUYMC                                                         
         ORG   LBUYDAYS                                                         
LBUYPROG DS    CL34                                                             
         ORG   LBUYDAYS                                                         
LBUYCMMT DS    CL71                                                             
*                                                                               
* REGENCON                                                                      
* REGENDAR                                                                      
*                                                                               
       ++INCLUDE REDARDSECT                                                     
       ++INCLUDE DDCOMFACS                                                      
DBLOCKD  DSECT                                                                  
       ++INCLUDE DEDBLOCK                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002REGENDAB  08/30/12'                                      
         END                                                                    
