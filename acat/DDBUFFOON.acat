*          DATA SET DDBUFFOON  AT LEVEL 005 AS OF 05/01/02                      
*CATALP BUFFOON                                                                 
         TITLE 'BUFFOON - CONTROLS CORE BUFFER ALLOCATION'                      
BUFFOON  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 MYEND-MYD,**BUFF**,R8,RR=R3                                      
         USING MYD,RC                                                           
         L     RA,4(R1)                                                         
         USING BUFFALOD,RA                                                      
         LR    R9,R1                                                            
         ST    R3,RELO                                                          
         EJECT                                                                  
*              SELECT APPROPRIATE ROUTINE                                       
         SPACE 3                                                                
         SR    R4,R4                                                            
         LA    R2,ACTLIST                                                       
         L     R3,0(R9)                                                         
         SPACE 1                                                                
ACT2     CLI   0(R2),X'FF'                                                      
         BE    ACTERR                                                           
         CLC   0(3,R2),0(R3)                                                    
         BE    ACT4                                                             
         LA    R4,4(R4)                                                         
         LA    R2,3(R2)                                                         
         B     ACT2                                                             
         SPACE 1                                                                
ACT4     B     BRANCH(R4)                                                       
         SPACE 1                                                                
ACTERR   DC    H'0'                                                             
         DC    C'ACTION NOT RECOGNIZED'                                         
         SPACE 1                                                                
ACTLIST  DC    C'SET'                                                           
         DC    C'PUT'                                                           
         DC    C'GET'                                                           
         DC    C'HIG'                                                           
         DC    C'SEQ'                                                           
         DC    C'ADD'                                                           
         DC    C'CLE'                                                           
         DC    C'RES'                                                           
         DC    X'FF'                                                            
         SPACE 1                                                                
BRANCH   B     SET                                                              
         B     PUT                                                              
         B     GET                                                              
         B     HIGH                                                             
         B     SEQ                                                              
         B     ADD                                                              
         B     CLEAR                                                            
         B     RESET                                                            
         EJECT                                                                  
*              COMPLETE CSECT                                                   
         SPACE 3                                                                
SET      DS    0H                                                               
         SPACE 1                                                                
RESET    DS    0H                  COMPLETE CSECT DETAILS                       
         BAS   RE,CHECKEY                                                       
         LA    R5,4                                                             
         CLI   BUFFFLVR,C'P'                                                    
         BNE   *+8                                                              
         LA    R5,8                                                             
         CLI   BUFFFLVR,C'D'                                                    
         BNE   *+6                                                              
         SR    R5,R5                                                            
         ST    R5,BUFFWCOL                                                      
         M     R4,BUFFCOLS                                                      
         ST    R5,BUFFWROW                                                      
         XC    BUFFSOFA,BUFFSOFA                                                
         LA    R2,BUFFAREA                                                      
         A     R2,BUFFLALL                                                      
         ST    R2,BUFFADDR                                                      
         L     R2,=V(BINSRCH)                                                   
         A     R2,RELO                                                          
         ST    R2,BUFFBIN                                                       
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO REESTABLISH KEY/RECORD SIZE                           
         SPACE 3                                                                
CHECKEY  NTR1                                                                   
         LA    R2,BUFFLIST                                                      
         SR    R3,R3                                                            
         SPACE 1                                                                
CK2      CLI   0(R2),X'FF'         END OF LIST                                  
         BE    CK4                                                              
         IC    R3,0(R2)            L'CUMULATIVE KEY                             
         LA    R2,2(R2)                                                         
         B     CK2                                                              
         SPACE 1                                                                
CK4      ST    R3,BUFFLKEY         SET KEY LENGTH                               
         L     R3,BUFFROWS         RECHECK RECORD LENGTH                        
         M     R2,BUFFCOLS                                                      
         SLL   R3,2                                                             
         CLI   BUFFFLVR,C'P'                                                    
         BNE   *+8                                                              
         SLL   R3,1                                                             
         CLI   BUFFFLVR,C'D'                                                    
         BNE   *+6                                                              
         SR    R3,R3                                                            
         ST    R3,BUFFLDTA                                                      
         A     R3,BUFFLKEY                                                      
         A     R3,BUFFLCOM                                                      
         ST    R3,BUFFLALL         SET TOTAL LENGTH                             
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINES TO PUT AN ITEM INTO THE BUFFER                          
*              OR TO ADD TO ONE THAT IS ALREADY THERE                           
         SPACE 3                                                                
PUT      L     R2,8(R9)            A(RECORD TO BE PUT)                          
         OC    BUFFHOOK,BUFFHOOK                                                
         BZ    PUT1                                                             
         GOTO1 BUFFHOOK,PARAS,(R2),(RA)                                         
         SPACE 1                                                                
PUT1     L     R3,BUFFADDR         OTHER PARAS FOR BINSRCH                      
         L     R4,BUFFSOFA                                                      
         L     R5,BUFFLALL                                                      
         L     R6,BUFFLKEY                                                      
         L     R7,BUFFCRMX                                                      
         ST    R2,PARAS                                                         
         MVC   FIELD,=CL20'RECORD BEFORE BINSRCH'                               
         BAS   RE,TRACE                                                         
         STM   R2,R7,PARAS                                                      
         MVI   PARAS,X'01'                                                      
         GOTO1 BUFFBIN,PARAS                                                    
         MVC   BUFFSOFA,PARAS+8    BINSRCH PASSES BACK NUMBER USED              
         CLI   PARAS,X'01'         WAS THE RECORD THERE ALREADY                 
         BE    PUT2                                                             
         OC    PARAS+1(3),PARAS+1                                               
         BZ    PUT2                                                             
         L     R2,PARAS            YES - SO ADD IN USERS VALUES                 
         L     R3,8(R9)                                                         
         ST    R2,PARAS                                                         
         MVC   FIELD,=CL20'BINSRCH PREVIOUS'                                    
         BAS   RE,TRACE                                                         
         ST    R3,PARAS                                                         
         MVC   FIELD,=CL20'MY RECORD'                                           
         BAS   RE,TRACE                                                         
         BAS   RE,RECADD                                                        
         ST    R2,PARAS                                                         
         MVC   FIELD,=CL20'ADDED RECORD'                                        
         BAS   RE,TRACE                                                         
         B     XIT                                                              
         SPACE 1                                                                
PUT2     OC    PARAS+1(3),PARAS+1  NO - DID IT FIT IN THE CORE BUFFER           
         BZ    EOFEXIT             NO - RETURN EOF                              
         L     R2,PARAS            NO - BUT IT DID FIT                          
         ST    R2,PARAS                                                         
         MVC   FIELD,=CL20'NEW BINSRCH RECORD'                                  
         BAS   RE,TRACE                                                         
         SLL   R2,8                                                             
         SRL   R2,8                R2 HAS ADDRESS OF WHERE                      
         CLI   BUFFFLVR,C'D'                                                    
         BE    XIT                                                              
         L     R3,BUFFCOLS         JUST NEED TO CLEAR ROWS 2-N                  
*                                  (USER SUPPLIER ROW1)                         
         L     R4,BUFFWCOL         R4=WIDTH OF COLUMN                           
         L     R7,BUFFROWS                                                      
         BCTR  R7,0                                                             
         LTR   R7,R7                                                            
         BZ    XIT                                                              
         MR    R6,R3                                                            
         LR    R5,R7               R5=NUMBER OF BUCKETS TO BE CLEARED           
         A     R2,BUFFLKEY                                                      
         A     R2,BUFFLCOM                                                      
         A     R2,BUFFWROW         BUMP TO ROW 2                                
         CLI   BUFFFLVR,C'P'                                                    
         BE    PUT8                                                             
         S     R2,BUFFWROW         CHECK FOR BIG BINARY NUMBERS                 
         L     R0,BUFFCOLS                                                      
         SPACE 1                                                                
PUT5     TM    0(R2),X'C0'                                                      
         BNM   PUT5B                                                            
         CVDX  DUB,0(R2)                                                        
         CVBX  0(R2),DUB                                                        
         SPACE 1                                                                
PUT5B    LA    R2,4(R2)                                                         
         BCT   R0,PUT5                                                          
         SPACE 1                                                                
PUT6     XC    0(4,R2),0(R2)                                                    
         AR    R2,R4                                                            
         BCT   R5,PUT6                                                          
         B     XIT                                                              
         SPACE 1                                                                
PUT8     ZAP   0(8,R2),=P'0'                                                    
         AR    R2,R4                                                            
         BCT   R5,PUT8                                                          
         B     XIT                                                              
         EJECT                                                                  
*              LOGICAL READ ROUTINES - GET AND HIGH                             
         SPACE 3                                                                
GET      DS    0H                                                               
         SPACE 1                                                                
HIGH     LM    R2,R3,8(R9)         A(RECORD), LEVEL                             
         L     R4,BUFFLKEY                                                      
         BCTR  R4,0                R4=L'KEY - 1                                 
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   CBKEY(0),0(R2)                                                   
         L     R6,BUFFADDR                                                      
         L     R5,BUFFSOFA                                                      
         LTR   R5,R5                                                            
         BZ    EOFEXIT                                                          
         SPACE 1                                                                
HIGH10   BAS   RE,FILTER           NOW LOOK FOR RECORD IN THIS BLOCK            
         BE    HIGH11                                                           
         CLI   TOOFAR,C'Y'                                                      
         BE    EOFEXIT                                                          
         B     HIGH12                                                           
         SPACE 1                                                                
HIGH11   EX    R4,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R2),0(R6)                                                    
         BNH   HIGH14                                                           
         SPACE 1                                                                
HIGH12   A     R6,BUFFLALL                                                      
         BCT   R5,HIGH10                                                        
         B     EOFEXIT                                                          
         SPACE 1                                                                
HIGH14   BE    READOK              MISSED                                       
         L     R1,0(R9)                                                         
         CLC   0(4,R1),=C'HIGH'    OK FOR HIGH                                  
         BNE   MISSEXIT                                                         
         B     READOK                                                           
         EJECT                                                                  
*              SEQUENTIAL READING                                               
         SPACE 1                                                                
SEQ      LM    R2,R3,8(R9)                                                      
         L     R4,BUFFLKEY                                                      
         BCTR  R4,0                                                             
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   CBKEY(0),0(R2)                                                   
         L     R5,BUFFLEFT         SEQUENTIAL - IS NEXT RECORD IN CORE          
         L     R6,BUFFALST                                                      
         B     SEQ4                                                             
         SPACE 1                                                                
SEQ2     BAS   RE,FILTER                                                        
         BE    READOK                                                           
         CLI   TOOFAR,C'Y'                                                      
         BE    EOFEXIT                                                          
         SPACE 1                                                                
SEQ4     A     R6,BUFFLALL                                                      
         BCT   R5,SEQ2                                                          
         B     EOFEXIT                                                          
         SPACE 1                                                                
READOK   ST    R5,BUFFLEFT                                                      
         ST    R6,BUFFALST                                                      
         BAS   RE,BREAKS                                                        
         L     R5,BUFFLALL                                                      
         LTR   R3,R3                                                            
         BZ    READOK2                                                          
         SPACE 1                                                                
         L     R5,BUFFLKEY         OPTION TO READ AT LEVEL N (R3)               
         A     R5,BUFFLCOM                                                      
         BCTR  R5,0                                                             
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),0(R6)       SO PUT OUT KEY AND DATA                      
         CLI   BUFFFLVR,C'D'                                                    
         BE    XIT                                                              
         SPACE 1                                                                
         LA    R2,1(R5,R2)         BUMP PAST KEY                                
         LA    R6,1(R5,R6)                                                      
         BCTR  R3,0                                                             
         MH    R3,BUFFWROW+2                                                    
         AR    R6,R3               BUMP TO LEVEL N                              
         L     R5,BUFFWROW                                                      
         SPACE 1                                                                
READOK2  MOVE  ((R2),(R5)),(R6)                                                 
         B     XIT                                                              
         SPACE 1                                                                
EOFEXIT  MVI   8(R9),X'80'                                                      
         B     XIT                                                              
         SPACE 1                                                                
MISSEXIT MVI   8(R9),X'10'                                                      
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO ESTABLISH CONTROL BREAKS                              
         SPACE 1                                                                
BREAKS   NTR1                                                                   
         LA    R3,BUFFLIST                                                      
         LA    R4,1                                                             
         SR    R5,R5                                                            
         SPACE 1                                                                
BREAKS2  IC    R5,0(R3)                                                         
         BCTR  R5,0                                                             
         EX    R5,*+8                                                           
         B     *+10                                                             
         CLC   CBKEY(0),0(R6)                                                   
         BNE   BREAKS4                                                          
         LA    R3,2(R3)                                                         
         LA    R4,1(R4)                                                         
         CLI   0(R3),X'FF'                                                      
         BNE   BREAKS2                                                          
         SR    R4,R4                                                            
         SPACE 1                                                                
BREAKS4  STC   R4,BUFFCB                                                        
         MVC   BUFFUSER,1(R3)                                                   
         B     XIT                                                              
         EJECT                                                                  
*              FILTERING RECORDS                                                
         SPACE 3                                                                
FILTER   NTR1                                                                   
         MVI   TOOFAR,C'N'                                                      
         CLI   4(R9),0             OPTION TO FILTER ON FIRST BYTE               
         BE    FILTER2                                                          
         CLC   4(1,R9),0(R6)                                                    
         BE    FILTER2                                                          
         BH    NO                                                               
         MVI   TOOFAR,C'Y'                                                      
         B     NO                                                               
         SPACE 1                                                                
FILTER2  CLI   BUFFFLVR,C'D'                                                    
         BE    YES                                                              
         LTR   R3,R3                                                            
         BZ    YES                                                              
         A     R6,BUFFLKEY         NOW CHECK FOR ACTIVITY                       
         A     R6,BUFFLCOM                                                      
         L     R2,BUFFCOLS                                                      
         BCTR  R3,0                                                             
         MH    R3,BUFFWROW+2                                                    
         AR    R6,R3                                                            
         CLI   BUFFFLVR,C'B'                                                    
         BNE   FILTER6                                                          
         SPACE 1                                                                
FILTER4  OC    0(4,R6),0(R6)                                                    
         BNZ   YES                                                              
         LA    R6,4(R6)                                                         
         BCT   R2,FILTER4                                                       
         B     NO                                                               
         SPACE 1                                                                
FILTER6  CP    0(8,R6),=P'0'                                                    
         BNE   YES                                                              
         LA    R6,8(R6)                                                         
         BCT   R2,FILTER6                                                       
         SPACE 1                                                                
NO       LA    R2,1                                                             
         LTR   R2,R2                                                            
         B     XIT                                                              
         SPACE 1                                                                
YES      SR    R2,R2                                                            
         LTR   R2,R2                                                            
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINES TO ADD AND CLEAR                                        
         SPACE 3                                                                
ADD      DS    0H                                                               
         SPACE 1                                                                
CLEAR    DS    0H                                                               
         SPACE 1                                                                
         L     R2,BUFFADDR                                                      
         L     R3,BUFFSOFA                                                      
         LTR   R3,R3                                                            
         BZ    XIT                                                              
         SPACE 1                                                                
AC4      BAS   RE,MAINT                                                         
         CLI   TOOFAR,C'Y'                                                      
         BE    XIT                                                              
         BAS   RE,ANYDATA                                                       
         BNE   AC6                                                              
         A     R2,BUFFLALL                                                      
         BCT   R3,AC4                                                           
         B     XIT                                                              
         SPACE 1                                                                
AC6      MVC   P2,BUFFADDR         TAKE ZERO RECORD OUT OF CORE                 
         MVC   P3,BUFFSOFA                                                      
         MVC   P4,BUFFLALL                                                      
         MVC   P5,BUFFLKEY                                                      
         MVC   P6,P3                                                            
         GOTO1 BUFFBIN,PARAS,(X'80',(R2))                                       
         CLI   PARAS,X'01'                                                      
         BNE   *+6                                                              
         DC    H'0'                                                             
         L     R5,BUFFSOFA                                                      
         BCTR  R5,0                                                             
         ST    R5,BUFFSOFA                                                      
         BCT   R3,AC4                                                           
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO CHECK IF RECORD HAS SIGNIFICANT DATA                  
         SPACE 3                                                                
ANYDATA  NTR1                                                                   
         CLI   BUFFFLVR,C'D'                                                    
         BE    ANYYES                                                           
         L     RF,BUFFCOLS                                                      
         M     RE,BUFFROWS                                                      
         L     RE,BUFFLKEY                                                      
         A     RE,BUFFLCOM                                                      
         AR    RE,R2                                                            
         CLI   BUFFFLVR,C'B'                                                    
         BE    ANYDATA4                                                         
         SPACE 1                                                                
ANYDATA2 CP    0(8,RE),=P'0'                                                    
         BNE   ANYYES                                                           
         LA    RE,8(RE)                                                         
         BCT   RF,ANYDATA2                                                      
         B     ANYNO                                                            
         SPACE 1                                                                
ANYDATA4 OC    0(4,RE),0(RE)                                                    
         BNZ   ANYYES                                                           
         LA    RE,4(RE)                                                         
         BCT   RF,ANYDATA4                                                      
         SPACE 1                                                                
ANYNO    SR    RF,RF                                                            
         B     ANYALL                                                           
         SPACE 1                                                                
ANYYES   LA    RF,1                                                             
         SPACE 1                                                                
ANYALL   CH    RF,=H'1'                                                         
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO CARRY OUT MAINTENANCE ON RECORD (CLEAR)               
         SPACE 3                                                                
MAINT    NTR1                                                                   
         MVI   TOOFAR,C'N'                                                      
         CLI   4(R9),0             OPTION TO FILTER ON FIRST BYTE               
         BE    MAINT1                                                           
         CLC   4(1,R9),0(R2)                                                    
         BE    MAINT1                                                           
         BH    ANYYES                                                           
         MVI   TOOFAR,C'Y'                                                      
         B     ANYYES                                                           
         SPACE 1                                                                
MAINT1   L     RF,0(R9)                                                         
         LA    R9,8(R9)                                                         
         A     R2,BUFFLKEY         R2=A(ROW 1)                                  
         A     R2,BUFFLCOM                                                      
         L     R3,BUFFWCOL         R3=DISPLACEMENT BETWEEN COLUMNS              
         L     R5,BUFFWROW         R5=WIDTH OF ROW                              
         L     R4,BUFFCOLS         R4=NUMBER OF COLUMNS                         
         CLC   0(3,RF),=C'ADD'                                                  
         BE    MAINT10                                                          
         SPACE 1                                                                
MAINT2   L     R7,0(R9)            CLEAR ROW N                                  
         SLL   R7,24                                                            
         SRL   R7,24               R7=N                                         
         C     R7,BUFFROWS                                                      
         BNH   *+6                                                              
         DC    H'0'                                                             
         BCTR  R7,0                                                             
         MR    R6,R5               R7=DISPLACEMENT FROM START OF ACCUMS         
         AR    R7,R2                                                            
         LR    R6,R4                                                            
         CLI   BUFFFLVR,C'P'                                                    
         BE    MAINT6                                                           
         SPACE 1                                                                
MAINT4   XC    0(4,R7),0(R7)                                                    
         AR    R7,R3                                                            
         BCT   R6,MAINT4                                                        
         B     MAINT8                                                           
         SPACE 1                                                                
MAINT6   ZAP   0(8,R7),=P'0'       CLEAR PACKED                                 
         AR    R7,R3                                                            
         BCT   R6,MAINT6                                                        
         SPACE 1                                                                
MAINT8   CLI   0(R9),X'80'         IF WE'RE NOT AN END OF PARAMETERS,           
         BE    XIT                                                              
         LA    R9,4(R9)            BUMP TO NEXT                                 
         B     MAINT2                                                           
         EJECT                                                                  
*              ROUTINES TO ADD ROW R1 TO ROWS R2...RN                           
         SPACE 3                                                                
MAINT10  L     R7,0(R9)            ADDRESS ROW R1                               
         BCTR  R7,0                                                             
         MR    R6,R5                                                            
         AR    R7,R2                                                            
         ST    R7,SAVEROW1         AND SAVE IT                                  
         LA    R9,4(R9)            BUMP TO R2 IN PARAMETERS                     
         SPACE 1                                                                
MAINT12  L     R7,0(R9)            R7=R2 THRU RN                                
         SLL   R7,24                                                            
         SRL   R7,24                                                            
         C     R7,BUFFROWS                                                      
         BNH   *+6                                                              
         DC    H'0'                                                             
         BCTR  R7,0                                                             
         MR    R6,R5                                                            
         AR    R7,R2               R7=DISPLACEMENT OF ROW TO ADD INTO           
         LR    R6,R4               R6=NUMBER OF COLUMNS                         
         L     R1,SAVEROW1         R1=A(ROW1)                                   
         CLI   BUFFFLVR,C'P'                                                    
         BE    MAINT16                                                          
         SPACE 1                                                                
MAINT14  MVC   SAVEA,0(R1)                                                      
         MVC   SAVEB,0(R7)                                                      
*&&US                                                                           
         TM    BUFFXOPT,BUFFXBIG                                                
         BO    MAINT15D             DON'T COMPRESS LARGE BINARY NUMBERS         
*&&                                                                             
         TM    0(R1),X'C0'                                                      
         BM    MAINT15                                                          
         TM    0(R7),X'C0'                                                      
         BM    MAINT15                                                          
         L     R0,0(R1)            BINARY ADDS                                  
         A     R0,0(R7)                                                         
         ST    R0,0(R7)                                                         
         TM    0(R7),X'C0'                                                      
         BNM   MAINT15B                                                         
         SPACE 1                                                                
MAINT15  LR    R0,R1                                                            
         CVDX  DUB,SAVEA                                                        
         CVDX  DUB2,SAVEB                                                       
         AP    DUB,DUB2                                                         
         CVBX  0(R7),DUB                                                        
         LR    R1,R0                                                            
         SPACE 1                                                                
MAINT15B AR    R1,R3                                                            
         AR    R7,R3                                                            
         BCT   R6,MAINT14                                                       
         B     MAINT18                                                          
         SPACE 1                                                                
MAINT15D L     R0,0(R1)            BINARY ADDS (NO COMPRESSION)                 
         A     R0,0(R7)                                                         
         ST    R0,0(R7)                                                         
         B     MAINT15B                                                         
         SPACE 1                                                                
MAINT16  AP    0(8,R7),0(8,R1)     PACKED ADDS                                  
         AR    R1,R3                                                            
         AR    R7,R3                                                            
         BCT   R6,MAINT16                                                       
         SPACE 1                                                                
MAINT18  CLI   0(R9),X'80'         IF WE'RE NOT AT END OF PARAMETERS,           
         BE    ANYYES                                                           
         LA    R9,4(R9)            BUMP TO NEXT IN LIST                         
         B     MAINT12                                                          
         EJECT                                                                  
*              ROUTINE TO ADD TWO RECORDS TOGETHER                              
         SPACE 3                                                                
RECADD   NTR1                                                                   
*        CLI   BUFFFLVR,C'D'       **NO-OP 7/13/87 GRANT**                      
*        BE    XIT                                                              
         L     R5,BUFFLKEY         (ADD R3 RECORD TO R2 RECORD)                 
         AR    R2,R5               BUMP PAST KEYS                               
         AR    R3,R5                                                            
         L     R5,BUFFLCOM                                                      
         LTR   R5,R5                                                            
         BZ    RECADD1                                                          
         BCTR  R5,0                                                             
         EX    R5,*+8              ALLOW UPDATE OF COMMENT IF                   
         B     *+10                PREVIOUS ONE IS SPACES                       
         CLC   0(0,R2),SPACES                                                   
         BNE   *+18                                                             
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),0(R3)                                                    
         CLI   BUFFDOPT,C'Y'       USE OPTION TO OVERRIDE COMMENT               
         BNE   *+18                                                             
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),0(R3)                                                    
         LA    R5,1(R5)                                                         
         AR    R2,R5                                                            
         AR    R3,R5                                                            
         SPACE 1                                                                
RECADD1  CLI   BUFFFLVR,C'D'                                                    
         BE    XIT                                                              
         L     R5,BUFFCOLS                                                      
         L     R4,BUFFWCOL                                                      
         CLI   BUFFFLVR,C'P'                                                    
         BE    RECADD4                                                          
         SPACE 1                                                                
RECADD2  MVC   SAVEA,0(R3)                                                      
         MVC   SAVEB,0(R2)                                                      
         TM    0(R3),X'C0'                                                      
         BM    RECADD3                                                          
         TM    0(R2),X'C0'                                                      
         BM    RECADD3                                                          
         L     R0,0(R3)            BINARY ADDITION                              
         A     R0,0(R2)                                                         
         ST    R0,0(R2)                                                         
         TM    0(R2),X'C0'                                                      
         BNM   RECADD3B                                                         
         SPACE 1                                                                
RECADD3  CVDX  DUB,SAVEA                                                        
         CVDX  DUB2,SAVEB                                                       
         AP    DUB,DUB2                                                         
         CVBX  0(R2),DUB                                                        
         SPACE 1                                                                
RECADD3B AR    R2,R4                                                            
         AR    R3,R4                                                            
         BCT   R5,RECADD2                                                       
         B     XIT                                                              
         SPACE 1                                                                
RECADD4  AP    0(8,R2),0(8,R3)                                                  
         AR    R2,R4                                                            
         AR    R3,R4                                                            
         BCT   R5,RECADD4                                                       
         SPACE 1                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*              ROUTINE TO TRACE                                                 
         SPACE 1                                                                
TRACE    BR    RE                  NOOP THIS TO ENABLE TRACING                  
         NTR1                                                                   
*                                  COULD PUT SOMETHING IN HERE                  
*                                  USER WOULD NEED TO PASS                      
*                                  A(PRINT) AND A(HEXOUT)                       
         B     XIT                                                              
         SPACE 1                                                                
         EJECT                                                                  
*        LTORG AND THINGS                                                       
RELO     DS    A                                                                
         SPACE 1                                                                
SPACES   DC    CL132' '                                                         
         SPACE 3                                                                
         LTORG                                                                  
         EJECT                                                                  
*              DSECT FOR MODULE                                                 
         SPACE 3                                                                
MYD      DSECT                                                                  
DUB      DS    D                                                                
DUB2     DS    D                                                                
SAVEA    DS    F                                                                
SAVEB    DS    F                                                                
SAVEROW1 DS    F                                                                
PARAS    DS    6F                                                               
         ORG   PARAS                                                            
P1       DS    F                                                                
P2       DS    F                                                                
P3       DS    F                                                                
P4       DS    F                                                                
P5       DS    F                                                                
P6       DS    F                                                                
CBKEY    DS    CL255                                                            
TOOFAR   DS    CL1                                                              
FIELD    DS    CL20                                                             
MYEND    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE DDBUFFALOD                                                     
         SPACE 3                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005DDBUFFOON 05/01/02'                                      
         END                                                                    
