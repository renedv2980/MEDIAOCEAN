*          DATA SET NEGETPAY   AT LEVEL 088 AS OF 02/23/12                      
*CATALP NEGETPAY                                                                
         SPACE 2                                                                
* NEGETPAY - MODULE TO GET PAYABLE AMOUNTS/GENERATE PAY ELEMENTS FOR A          
*            UNIT                                                               
*                                                                               
* PARA 1   = A(PAY PROGRAM WORKING STORAGE)                                     
* PARA 2   = A(PAY BLOCK)                                                       
*                                                                               
* PAY BLOCK IS COVERED BY PAYBLKD DSECT INCLUDED IN PAY PROGRAM WORKING         
* STORAGE                                                                       
*                                                                               
******************* HISTORY ****************************************            
* LEVEL 88 - MIDAS SPECIAL CHARGES SKIP FOR PAYING                              
*                                                                               
*                                                                               
         TITLE 'NEGETPAY - MODULE TO GET PAY DATA/ELEMENTS FOR A UNIT'          
GETPAY   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,**GTPY**                                                       
         LM    R6,R7,0(R1)                                                      
         USING PAYWRKD,R6                                                       
         USING PAYBLKD,R7                                                       
*                                                                               
         BAS   RE,GETCP            GET CASH% VALUES                             
*                                                                               
* READ BS1 PROFILE FOR NEW BILL REC READ                                        
*                                                                               
         MVI   BILLTYPE,0                                                       
*                                                                               
         OI    NBINDS2,NBNOBLRD    DEFAULT-DONT READ NEGENUBILL RECS            
         XC    WORK,WORK                                                        
         MVC   WORK(4),=C'SB1S'                                                 
         NI    WORK,X'BF'          LOWER CASE                                   
         MVC   WORK+4(2),NBSELAGY     AGENCY                                    
         GOTO1 NBGTPROF,DMCB,(X'C0',WORK),WORK+20,NBDM                          
         MVC   BILLTYPE,WORK+23                                                 
*                                                                               
         CLI   BILLTYPE,C'Y'       NEW UNIT BILLING?                            
         BE    GETPAY0                                                          
*                                                                               
         L     R4,NBAIO                                                         
         MVI   ELCODE,X'02'                                                     
         BAS   RE,GETEL                                                         
         BNE   GETPAY0                                                          
         USING NUSDRD,R4                                                        
*                                                                               
         OI    NUSDST4,X'04'       OLD STYLE PAID                               
         DROP  R4                                                               
*                                                                               
GETPAY0  XC    PAYGROSS(8),PAYGROSS INITIALIZE BLOCK FIELDS                     
         MVI   PAYSTAT,0                                                        
         MVI   PAYERROR,0                                                       
         MVI   PAYRSN,0                                                         
         MVI   PAYNELS,0                                                        
         TM    NBUNITST,X'42'      TEST PRE-EMPT/MISSED                         
         BZ    GETPAY4             NEITHER                                      
*        OC    NBPAYTGR,NBPAYTGR   TEST IF ANY TIME PAID                        
*        BNZ   *+14                YES-SO GENERATE A MINUS                      
*        OC    NBPAYIGR,NBPAYIGR   NO-NOW TEST FOR PAID INT                     
*        BZ    GETPAYX             NO-NOTHING TO PAY SO EXIT                    
         SPACE 1                                                                
* MISSING OR PRE-EMPTED UNIT                                                    
*                                                                               
GETPAY1  TM    SPECREP,X'80'       TEST FOR ANY SPEC REP FILTER                 
         BO    *+18                YES                                          
         CLC   SPECREP,NBSREP      TEST FOR MATCH ON SPECIAL REP                
         BNE   GETPAY3             YES                                          
         B     GETPAY1A                                                         
*                                                                               
         OC    NBSREP,NBSREP       TEST FOR ANY SPECIAL REP                     
         BZ    GETPAY3             NO-OMIT RECORD                               
*                                                                               
GETPAY1A CLI   PAYTYPE,0                                                        
         BE    GETPAY1B                                                         
         CLI   PAYTYPE,C'Z'        TEST FOR TIME AND INTEGRATION                
         BE    *+12                                                             
         CLI   PAYTYPE,C'T'        TEST FOR TIME PAID ONLY                      
         BNE   GETPAY2             YES                                          
GETPAY1B OC    NBPAYTGR,NBPAYTGR   TEST FOR TIME PAID                           
         BZ    GETPAY2             NO                                           
         LM    R2,R3,NBPAYTGR      GET PAID GROSS/NET                           
         LCR   R2,R2                                                            
         LCR   R3,R3                                                            
         STM   R2,R3,DUB                                                        
         BAS   RE,UPPAY                                                         
         MVC   DUB2,DUB            SAVE OFF TIME VALUES                         
         OC    NBPAYCGR,NBPAYCGR   TEST FOR CASH% PAID                          
         BZ    GETPAY1C                                                         
         LM    R2,R3,NBPAYCGR      GET PAID GROSS/NET                           
         LCR   R2,R2                                                            
         LCR   R3,R3                                                            
         STM   R2,R3,CPGRS                                                      
         STM   R2,R3,DUB                                                        
         BAS   RE,UPPAY                                                         
         MVC   DUB,DUB2            RESTORE TIME VALUES                          
GETPAY1C OI    PAYSTAT,PAYABLE+TPAY                                             
         CLI   PAYFUNCT,PAYELS                                                  
         BNE   GETPAY2                                                          
         MVI   BYTE,C'T'                                                        
         BAS   RE,GENELS                                                        
         SPACE                                                                  
GETPAY2  CLI   PAYTYPE,0                                                        
         BE    GETPAY2A                                                         
         CLI   PAYTYPE,C'Z'        TEST FOR TIME AND INTEGRATION                
         BE    *+12                                                             
         CLI   PAYTYPE,C'I'        TEST FOR INT ONLY                            
         BNE   GETPAY3             YES                                          
GETPAY2A OC    NBPAYIGR,NBPAYIGR   TEST IF ANY INT PAID                         
         BZ    GETPAY3             NO-ALL DONE                                  
         LM    R2,R3,NBPAYIGR                                                   
         LCR   R2,R2                                                            
         LCR   R3,R3                                                            
         STM   R2,R3,DUB                                                        
         BAS   RE,UPPAY                                                         
         OI    PAYSTAT,PAYABLE+IPAY                                             
         CLI   PAYFUNCT,PAYELS                                                  
         BNE   GETPAY3                                                          
         MVI   BYTE,C'I'                                                        
         BAS   RE,GENELS                                                        
         B     GETPAY3                                                          
         SPACE                                                                  
*--HANDLES SPECIAL RATE PAY ELEMENTS                                            
GETPAY3  XC    DUB,DUB                                                          
         CLI   PAYTYPE,C'R'                                                     
         BE    GETPAY3A                                                         
         CLI   PAYTYPE,0                                                        
         BNE   GETPAY3B                                                         
GETPAY3A LA    R3,10                                                            
         LA    R4,SRTABLE                                                       
         B     GETPAY3E                                                         
*                                                                               
GETPAY3B LA    R3,10                                                            
         LA    R4,SRTABLE                                                       
*                                                                               
GETPAY3C CLC   0(1,R4),PAYTYPE                                                  
         BE    GETPAY3D                                                         
         LA    R4,1(R4)                                                         
         BCT   R3,GETPAY3C                                                      
*                                                                               
GETPAY3D LA    R3,1                                                             
*                                                                               
GETPAY3E MVC   BYTE,0(R4)                                                       
         BAS   RE,SRIRREG                                                       
         LA    R4,1(R4)                                                         
         BCT   R3,GETPAY3E                                                      
         B     GETPAYX                                                          
         SPACE                                                                  
* REGULAR UNIT                                                                  
*                                                                               
GETPAY4  TM    SPECREP,X'80'       TEST FOR ANY SPEC REP FILTER                 
         BO    *+18                YES                                          
         CLC   SPECREP,NBSREP      TEST FOR MATCH ON SPECIAL REP                
         BNE   GETPAY8             YES                                          
         B     GETPAY4A                                                         
*                                                                               
         OC    NBSREP,NBSREP       TEST FOR ANY SPECIAL REP                     
         BZ    GETPAY8             NO-OMIT RECORD                               
*                                                                               
GETPAY4A CLI   NBPRDLST,0          TEST FOR UNALLOCATED UNIT                    
         BNE   *+14                NO                                           
         OC    NBPR1CL3,NBPR1CL3                                                
         BZ    PAYRSN1             YES                                          
*                                                                               
         TM    PAYFLAGS,FACINPUT   FORCE ACTUAL COST INPUT (FOR NO              
         BO    *+12                ACTUAL COST - NEPAY04)                       
*                                                                               
         TM    NBUNITST,X'20'      TEST FOR ACTUAL COST                         
         BZ    PAYRSN2             NO-THAT'S AN ERROR                           
*                                                                               
         CLI   PAYTYPE,0           TEST FOR INT ONLY                            
         BE    GETPAY4B                                                         
         CLI   PAYTYPE,C'Z'        TEST FOR TIME AND INTEGRATION                
         BE    *+12                                                             
         CLI   PAYTYPE,C'T'        TEST FOR INT ONLY                            
         BNE   GETPAY6                                                          
GETPAY4B L     R2,NBACTUAL         GET ACTUAL COST                              
         LR    RE,R2                                                            
         TM    NBUNITST,X'80'      TEST FOR MINUS UNIT                          
         BZ    *+6                                                              
         LNR   RE,RE                                                            
         S     RE,NBPAYTGR         DERIVE PAYABLE BALANCE                       
         BNZ   GETPAY5             NON-ZERO-THEREFORE TIME TO PAY               
*                                                                               
         GOTO1 FINDNET,DMCB2,C'T'  CHECK FOR NET PAYABLE BALANCE                
         LR    RE,R3                                                            
         TM    NBUNITST,X'80'      TEST FOR MINUS UNIT                          
         BZ    *+6                                                              
         LNR   RE,RE                                                            
         S     RE,NBPAYTNT         DERIVE PAYABLE BALANCE                       
         BNZ   GETPAY5             NON-ZERO-THEREFORE TIME TO PAY               
*                                  ZERO-LOOK FOR PAYABLE 0 ACTUAL COST          
         OC    NBACTUAL,NBACTUAL   TEST FOR ZERO ACTUAL COST                    
         BNZ   GETPAY6             NO-UNIT NOT PAYABLE                          
         MVI   BYTE,C'T'           LOOK FOR PREVIOUS PAYMENT                    
         GOTO1 VHELLO,DMCB2,(C'G',UNTFILE),(X'12',NBAIO),(1,BYTE)               
         CLI   12(R1),0            TEST IF ELEMENT FOUND                        
         BE    GETPAY6             YES-UNIT IS NOT PAYABLE                      
         SPACE 1                                                                
GETPAY5  GOTO1 FINDNET,DMCB2,C'T'                                               
         TM    NBUNITST,X'80'      TEST FOR MINUS UNIT                          
         BZ    *+8                                                              
         LNR   R2,R2                                                            
         LNR   R3,R3                                                            
         S     R2,NBPAYTGR         DERIVE PAYABLE BALANCE                       
         S     R3,NBPAYTNT                                                      
         STM   R2,R3,DUB                                                        
         BAS   RE,UPPAY                                                         
*                                                                               
         TM    BYTE2,CASHCP        DOES UNIT HAVE CASH %?                       
         BZ    GETPAY5B                                                         
         MVC   DUB2,DUB            SAVE OFF TIME VALUES                         
         L     R2,CPGRS            GET CASH %                                   
         GOTO1 FINDNET,DMCB2,C'C'                                               
         TM    NBUNITST,X'80'      TEST FOR MINUS UNIT                          
         BZ    *+8                                                              
         LNR   R2,R2                                                            
         LNR   R3,R3                                                            
         S     R2,NBPAYCGR         DERIVE PAYABLE BALANCE                       
         S     R3,NBPAYCNT                                                      
         STM   R2,R3,CPGRS                                                      
         STM   R2,R3,DUB                                                        
         BAS   RE,UPPAY                                                         
         MVC   DUB,DUB2            RESTORE TIME VALUES                          
*                                                                               
GETPAY5B OI    PAYSTAT,PAYABLE+TPAY                                             
         CLI   PAYFUNCT,PAYELS                                                  
         BNE   GETPAY6                                                          
         MVI   BYTE,C'T'                                                        
         BAS   RE,GENELS                                                        
         SPACE                                                                  
GETPAY6  CLI   PAYTYPE,0           TEST FOR TIME ONLY                           
         BE    GETPAY6A            YES-ALL DONE                                 
         CLI   PAYTYPE,C'Z'        TEST FOR TIME AND INTEGRATION                
         BE    *+12                                                             
         CLI   PAYTYPE,C'I'        TEST FOR TIME ONLY                           
         BNE   GETPAY8             YES-ALL DONE                                 
GETPAY6A L     R2,NBINTEG          GET INTEGRATION                              
         LR    RE,R2                                                            
         TM    NBUNITST,X'80'      TEST FOR MINUS UNIT                          
         BZ    *+6                                                              
         LNR   RE,RE                                                            
         S     RE,NBPAYIGR         DERIVE PAYABLE BALANCE                       
         BNZ   GETPAY6C            NON-ZERO-THEREFORE TIME TO PAY               
*                                                                               
         GOTO1 FINDNET,DMCB2,C'I'  CHECK FOR NET PAYABLE BALANCE                
         LR    RE,R3                                                            
         TM    NBUNITST,X'80'      TEST FOR MINUS UNIT                          
         BZ    *+6                                                              
         LNR   RE,RE                                                            
         S     RE,NBPAYINT         DERIVE PAYABLE BALANCE                       
         BNZ   GETPAY6C            NON-ZERO-THEREFORE TIME TO PAY               
*                                  ZERO-LOOK FOR PAYABLE 0 INTEG COST           
         OC    NBINTEG,NBINTEG     TEST FOR ZERO INTEG COST                     
         BNZ   GETPAY8             NO-UNIT NOT PAYABLE                          
         MVI   BYTE,C'I'           LOOK FOR PREVIOUS PAYMENT                    
         GOTO1 VHELLO,DMCB2,(C'G',UNTFILE),(X'12',NBAIO),(1,BYTE)               
         CLI   12(R1),0            TEST IF ELEMENT FOUND                        
         BE    GETPAY8             YES-UNIT IS NOT PAYABLE                      
         SPACE 1                                                                
GETPAY6C GOTO1 FINDNET,DMCB2,C'I'                                               
         LTR   R2,R2                                                            
         BNZ   GETPAY6D                                                         
         OC    NBPAYIGR,NBPAYIGR   TEST PAYABLE AMOUNT                          
         BNZ   GETPAY6D                                                         
         TM    NBUNST4,X'80'       INTEGRATION COST INPUTTED?                   
         BZ    GETPAY8             ZERO-ALL DONE                                
GETPAY6D TM    NBPACKST,X'04'      TEST FOR NON-COMMISSIONABLE INT              
         BZ    *+6                                                              
         LR    R3,R2               IF ON USE GROSS FIGURE                       
         S     R2,NBPAYIGR                                                      
         S     R3,NBPAYINT                                                      
         STM   R2,R3,DUB                                                        
         CLI   PAYTYPE,C'I'        TEST FOR INT ONLY                            
         BNE   GETPAY7             NO                                           
         MVI   BYTE,C'T'           YES-NOW SEE IF TIME PAID                     
         GOTO1 VHELLO,DMCB2,(C'G',UNTFILE),(X'12',NBAIO),(1,BYTE)               
         CLI   12(R1),0                                                         
         BE    GETPAY7             FOUND A TIME PAYMENT                         
         CLI   ACTION,CLEAR        FOR ACTIONS CLEAR OR TEST, THIS              
         BE    PAYRSN3             IS AN ERROR                                  
         CLI   ACTION,TEST                                                      
         BE    PAYRSN3                                                          
         SPACE                                                                  
GETPAY7  OI    PAYSTAT,PAYABLE+IPAY                                             
         BAS   RE,UPPAY                                                         
         MVI   BYTE,C'I'                                                        
         CLI   PAYFUNCT,PAYELS                                                  
         BNE   GETPAY8                                                          
         BAS   RE,GENELS                                                        
         SPACE                                                                  
*--HANDLES SPECIAL RATE PAY ELEMENTS                                            
GETPAY8  CLI   NBPRDLST,0          TEST FOR UNALLOCATED UNIT                    
         BNE   *+14                NO                                           
         OC    NBPR1CL3,NBPR1CL3                                                
         BZ    PAYRSN1             YES                                          
         CLI   PAYTYPE,C'R'                                                     
         BE    GETPAY8A                                                         
         CLI   PAYTYPE,0                                                        
         BNE   GETPAY8B                                                         
GETPAY8A LA    R3,10                                                            
         LA    R4,SRTABLE                                                       
         B     GETPAY8E                                                         
*                                                                               
GETPAY8B LA    R3,10                                                            
         LA    R4,SRTABLE                                                       
*                                                                               
GETPAY8C CLC   0(1,R4),PAYTYPE                                                  
         BE    GETPAY8D                                                         
         LA    R4,1(R4)                                                         
         BCT   R3,GETPAY8C                                                      
         B     GETPAYX                                                          
*                                                                               
GETPAY8D LA    R3,1                                                             
*                                                                               
GETPAY8E MVC   FULL(1),0(R4)                                                    
         BAS   RE,SRREG                                                         
         LA    R4,1(R4)                                                         
         BCT   R3,GETPAY8E                                                      
         B     GETPAYX                                                          
*                                                                               
SRTABLE  DC    CL10'UBSXEALODQ'                                                 
*                                                                               
GETPAYX  XMOD1 1                                                                
         SPACE 2                                                                
PAYRSN1  MVI   PAYRSN,UNALLOC                                                   
         B     GETPAYX                                                          
         SPACE 1                                                                
PAYRSN2  MVI   PAYERROR,ACTERR                                                  
         MVI   PAYRSN,NOACTUAL                                                  
         B     GETPAYX                                                          
         SPACE 1                                                                
PAYRSN3  MVI   PAYERROR,TFRSTERR                                                
         MVI   PAYRSN,TIMEUNPD                                                  
         B     GETPAYX                                                          
         EJECT                                                                  
* SUB-ROUTINE TO UPDATE PAY BUCKETS - AT ENTRY DUB CONTAINS VALUES              
*                                                                               
UPPAY    LM    R0,R1,DUB                                                        
         A     R0,PAYGROSS                                                      
         A     R1,PAYNT                                                         
         STM   R0,R1,PAYGROSS                                                   
         BR    RE                                                               
         SPACE 2                                                                
* SUB-ROUTINE TO CALCULATE NET - AT ENTRY R2 CONTAINS GROSS, ON EXIT            
* R3 CONTAINS RESULT                                                            
*                                                                               
FINDNET  LR    R3,RE                                                            
*                                                                               
         CLI   0(R1),C'T'                                                       
         BE    FINDN010                                                         
         CLI   0(R1),C'C'                                                       
         BE    FINDN010                                                         
         CLI   0(R1),C'I'                                                       
         BE    FINDN020                                                         
*---SET UP DMCB FOR NETNET CALL FOR SPECIAL CHARGE INPUT                        
         MVI   DMCB2+16,C'F'       SET GROSS=NET                                
         CLI   4(R4),C'C'          SEE IF SPECIAL RATE IS COMMISIONABLE         
         BE    FINDN005                                                         
         CLI   4(R4),C'Y'                                                       
         BNE   FINDN050            NO SET GROSS=NET                             
FINDN005 MVC   DMCB2+16(1),NBRTTYPE                                             
         CLI   NBSDRTCV,0          COVERAGE=0                                   
         BE    FINDN050            WORK OFF OF RATE TYPE                        
         CLI   NBSDRTCV,C'A'       DOES RATE COVER SPECIAL CHARGES              
         BE    FINDN050            YES WORK OFF OF RATE TYPE                    
         MVI   DMCB2+16,0          SET NET=85%GROSS                             
         B     FINDN050                                                         
*---SET UP DMCB FOR NETNET CALL FOR TIME CHARGE INPUT                           
FINDN010 MVC   DMCB2+16,NBRTTYPE                                                
         B     FINDN050                                                         
*---SET UP DMCB FOR NETNET CALL FOR INTEGRATION CHARGE INPUT                    
FINDN020 MVC   DMCB2+16,NBRTTYPE                                                
         CLI   NBSDRTCV,0          COVERAGE=0                                   
         BE    FINDN050            WORK OFF OF RATE TYPE                        
         CLI   NBSDRTCV,C'I'       DOES RATE COVER INTEGRATION                  
         BE    FINDN050            YES WORK OFF OF RATE TYPE                    
         MVI   DMCB2+16,0          SET NET=85%GROSS                             
*                                                                               
FINDN050 ST    R2,DMCB2+8                                                       
         LA    RE,DMCB2+8                                                       
         ST    RE,DMCB2                                                         
         MVC   DMCB2(1),DMCB2+16   RATE TYPE                                    
*                                                                               
         LA    RE,DMCB2+12                                                      
         ST    RE,DMCB2+4                                                       
         GOTO1 VNETNET,DMCB2                                                    
         LR    RE,R3                                                            
         ICM   R2,15,DMCB2+12                                                   
         ICM   R3,15,DMCB2+16                                                   
         BR    RE                                                               
*        LR    R1,R2                                                            
*        M     R0,=F'8500'                                                      
*        SLDA  R0,1                DIVIDEND X 2                                 
*        D     R0,=F'10000'                                                     
*        LTR   R1,R1                                                            
*        BM    *+8                                                              
*        AH    R1,=H'1'                                                         
*        SRA   R1,1                COMPLETE ROUNDING                            
*        LR    R3,R1                                                            
*        BR    RE                                                               
         SPACE 2                                                                
* SUB-ROUTINE TO GENERATE A PAY ELEMENT - AT ENTRY DUB CONTAINS AMOUNTS         
*                                                                               
GENELS   ST    RE,FULL             SAVE RETURN POINT                            
         ICM   RE,15,PAYAELS       GET POINTER TO ELEMENT AREA                  
         BNZ   *+6                                                              
         DC    H'0'                BLOW UP - USER DID NOT SUPPLY ONE            
         ZIC   R1,PAYNELS          INCREMENT ELEMENT COUNT                      
         LA    R1,1(R1)                                                         
         STC   R1,PAYNELS                                                       
         BCTR  R1,0                                                             
         MH    R1,=Y(NUPAYELN)     INDEX INTO ELEMENT AREA                      
         LA    RE,0(R1,RE)         POINT TO NEXT ELEMENT POSITION               
         USING NUPAYD,RE                                                        
         XC    NUPAYD(NUPAYELN),NUPAYD                                          
         MVI   NUPAYEL,X'12'                                                    
         MVI   NUPAYLEN,NUPAYELN                                                
         MVC   NUPAYTYP,BYTE                                                    
         MVC   NUPAYDAT,TODAYC                                                  
         MVC   NUPAYRTY,REPTYPE                                                 
         MVC   NUPAYREP,REP                                                     
         MVC   NUPAYGRS,DUB                                                     
         MVC   NUPAYNET,DUB+4                                                   
         MVC   NUPAYSRP,SRATREP                                                 
*                                                                               
         TM    NBPPDOPT,NBPPDCR    CREDIT?                                      
         BZ    *+12                                                             
         OI    NUPAYCR,X'20'                                                    
         B     GENELS50                                                         
*                                                                               
         TM    NBPPDOPT,NBPPDCK    CASH RECEIPT?                                
         BZ    *+12                                                             
         OI    NUPAYCR,X'10'                                                    
         B     GENELS50                                                         
*                                                                               
GENELS50 DS    0H                                                               
         CLI   BYTE,C'T'           CLEARED TIME?                                
         BNE   GENELSX                                                          
         TM    BYTE2,CASHCP        DOES UNIT HAVE CASH %?                       
         BZ    GENELSX                                                          
*                                                                               
         L     R4,NBAIO                                                         
         MVI   ELCODE,X'03'                                                     
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING NUSPRD,R4                                                        
         CLI   NUSPRTYP,C'C'       CASH %                                       
         BE    *+6                                                              
         DC    H'0'                                                             
         OI    NUSPRSTA,NUSPRCPD   MARK THIS PAID                               
         DROP  R4                                                               
*                                                                               
         ICM   RE,15,PAYAELS       CREATE CASH% PAY ELEMENT                     
         BNZ   *+6                                                              
         DC    H'0'                BLOW UP - USER DID NOT SUPPLY ONE            
         ZIC   R1,PAYNELS          INCREMENT ELEMENT COUNT                      
         LA    R1,1(R1)                                                         
         STC   R1,PAYNELS                                                       
         BCTR  R1,0                                                             
         MH    R1,=Y(NUPAYELN)     INDEX INTO ELEMENT AREA                      
         LA    RE,0(R1,RE)         POINT TO NEXT ELEMENT POSITION               
         USING NUPAYD,RE                                                        
         XC    NUPAYD(NUPAYELN),NUPAYD                                          
         MVI   NUPAYEL,X'12'                                                    
         MVI   NUPAYLEN,NUPAYELN                                                
         MVI   NUPAYTYP,C'C'                                                    
         MVC   NUPAYDAT,TODAYC                                                  
         MVC   NUPAYRTY,REPTYPE                                                 
         MVC   NUPAYREP,REP                                                     
         MVC   NUPAYGRS,CPGRS                                                   
         MVC   NUPAYNET,CPNET                                                   
         MVC   NUPAYSRP,SRATREP                                                 
*                                                                               
         TM    NBPPDOPT,NBPPDCR    CREDIT?                                      
         BZ    *+12                                                             
         OI    NUPAYCR,X'20'                                                    
         B     GENELS60                                                         
*                                                                               
         TM    NBPPDOPT,NBPPDCK    CASH RECEIPT?                                
         BZ    *+12                                                             
         OI    NUPAYCR,X'10'                                                    
         B     GENELS60                                                         
*                                                                               
GENELS60 DS    0H                                                               
GENELSX  L     RE,FULL                                                          
         BR    RE                                                               
         DROP  RE                                                               
         SPACE                                                                  
*                                                                               
*--RETRIEVE SPECIAL RATE INFORMATION FROM PAYING ELEMENT                        
GTPRATE  NTR1                                                                   
         MVC   DATADISP,=X'001B'                                                
         XC    DUB,DUB                                                          
*--CALCULATE NUMBERS FROM PAY ELEMENT                                           
         L     R4,NBAIO                                                         
         MVI   ELCODE,X'12'                                                     
         BAS   RE,GETEL                                                         
         BNE   GTPREXIT                                                         
         USING NUPAYD,R4                                                        
         B     GTPR020                                                          
GTPR010  BAS   RE,NEXTEL                                                        
         BNE   GTPREXIT                                                         
GTPR020  CLC   NUPAYTYP,BYTE                                                    
         BNE   GTPR010                                                          
         TM    SPECREP,X'80'       TEST FOR ANY SPEC REP FILTER                 
         BO    GTPR028             YES                                          
         OC    SRATREP,SRATREP                                                  
         BNZ   GTPR025                                                          
         CLC   NUPAYSRP(3),=CL3'000'   TEST FOR ANY SPECIAL REP                 
         BE    GTPR030             NO-OMIT RECORD                               
         OC    NUPAYSRP,NUPAYSRP       TEST FOR ANY SPECIAL REP                 
         BZ    GTPR030             NO-OMIT RECORD                               
GTPR025  CLC   NUPAYSRP,SRATREP    TEST FOR MATCH ON SPECIAL REP                
         BNE   GTPR010             YES                                          
         B     GTPR030                                                          
*                                                                               
GTPR028  CLC   NUPAYSRP(3),=CL3'000'   TEST FOR ANY SPECIAL REP                 
         BE    GTPR010             NO-OMIT RECORD                               
         OC    NUPAYSRP,NUPAYSRP       TEST FOR ANY SPECIAL REP                 
         BZ    GTPR010             NO-OMIT RECORD                               
*                                                                               
GTPR030  LM    R2,R3,NUPAYGRS                                                   
         A     R2,DUB                                                           
         A     R3,DUB+4                                                         
         STM   R2,R3,DUB                                                        
         B     GTPR010                                                          
GTPREXIT B     EXIT                                                             
         SPACE                                                                  
*                                                                               
*--RETRIEVE SPECIAL RATE INFORMATION FROM SPECIAL RATE ELEMENT                  
GETRATE  NTR1                                                                   
         MVC   DATADISP,=X'001B'                                                
         XC    DUB,DUB                                                          
*--CALCULATE NUMBERS FROM PAY ELEMENT                                           
         L     R4,NBAIO                                                         
         MVI   ELCODE,X'03'                                                     
         BAS   RE,GETEL                                                         
         BNE   GETREXIT                                                         
         USING NUSPRD,R4                                                        
         B     GETR020                                                          
GETR010  BAS   RE,NEXTEL                                                        
         BNE   GETREXIT                                                         
GETR020  DS    0H                                                               
         CLI   NUSPRTYP,C'C'       SKIP CASH%                                   
         BE    GETR010                                                          
         CLI   NUSPRTYP,C'M'       SKIP MIDAS                                   
         BE    GETR010                                                          
         CLC   NUSPRTYP,BYTE                                                    
         BNE   GETR010                                                          
         TM    SPECREP,X'80'       TEST FOR ANY SPEC REP FILTER                 
         BO    *+18                YES                                          
         CLC   NUSPRREP,SRATREP    TEST FOR MATCH ON SPECIAL REP                
         BNE   GETR010             YES                                          
         B     GETR030                                                          
*                                                                               
         OC    NUSPRREP,NUSPRREP   TEST FOR ANY SPECIAL REP                     
         BZ    GETR010             NO-OMIT RECORD                               
*                                                                               
GETR030  ICM   R2,15,NUSPRAMT                                                   
         ICM   R3,15,NUSPRAMT                                                   
         CLI   NUSPRCOM,C'C'       TEST FOR NON-COMMISSIONABLE INT              
         BE    GETR040             YES                                          
         CLI   NUSPRCOM,C'Y'       TEST FOR NON-COMMISSIONABLE INT              
         BE    GETR040             YES                                          
         OC    NBRTTYPE,NBRTTYPE                                                
         BZ    GETR050                                                          
GETR040  GOTO1 FINDNET,DMCB2,C'S'                                               
GETR050  A     R2,DUB                                                           
         A     R3,DUB+4                                                         
         STM   R2,R3,DUB                                                        
         B     GETR010                                                          
GETREXIT B     EXIT                                                             
         SPACE                                                                  
*--BUILD ELEMENT FOR PRE-EMPTED OR MISING SPECIAL RATE INFO                     
*--BYTE=PAYTYPE                                                                 
SRIRREG  NTR1                                                                   
         BAS   RE,GTPRATE          TEST IF ANY INT PAID                         
         CLC   DUB(4),=4X'00'                                                   
         BE    IRREXIT             NO-ALL DONE                                  
         LM    R2,R3,DUB                                                        
         LCR   R2,R2                                                            
         LCR   R3,R3                                                            
         STM   R2,R3,DUB                                                        
         BAS   RE,UPPAY                                                         
         OI    PAYSTAT,PAYABLE+SPAY                                             
         CLI   PAYFUNCT,PAYELS                                                  
         BNE   IRREXIT                                                          
         BAS   RE,GENELS                                                        
IRREXIT  B     EXIT                                                             
         SPACE                                                                  
*--BUILD ELEMENT FOR REGULAR SPECIAL RATE INFO                                  
*--BYTE=PAYTYPE                                                                 
SRREG    NTR1                                                                   
         MVC   BYTE,FULL                                                        
         BAS   RE,GETRATE          GET SPECIAL RATE                             
         LM    R2,R3,DUB                                                        
         BAS   RE,GTPRATE          TEST IF ANY SPECIAL RATE PAID                
         S     R2,DUB              DERIVE PAYABLE SPECIAL RATE GROSS            
         BZ    SREGEXIT            ZERO-ALL DONE                                
         S     R3,DUB+4            DERIVE PAYABLE SPECIAL RATE NET              
*                                                                               
         STM   R2,R3,DUB                                                        
         CLI   PAYTYPE,0           TEST FOR INT ONLY                            
         BE    SRREG9              NO                                           
         MVI   BYTE,C'T'                                                        
         GOTO1 VHELLO,DMCB2,(C'G',UNTFILE),(X'12',NBAIO),(1,BYTE)               
         CLI   12(R1),0                                                         
         BE    SRREG9              FOUND A TIME PAYMENT                         
         CLI   ACTION,CLEAR        FOR ACTIONS CLEAR OR TEST, THIS              
         BE    PAYRSN3             IS AN ERROR                                  
         CLI   ACTION,TEST                                                      
         BE    PAYRSN3                                                          
         SPACE                                                                  
SRREG9   OI    PAYSTAT,PAYABLE                                                  
         BAS   RE,UPPAY                                                         
         MVC   BYTE,FULL                                                        
         CLI   PAYFUNCT,PAYELS                                                  
         BNE   SREGEXIT                                                         
         BAS   RE,GENELS                                                        
SREGEXIT B     EXIT                                                             
*                                                                               
* GET CASH% VALUES                                                              
*                                                                               
GETCP    NTR1                                                                   
         NI    BYTE2,X'FF'-CASHCP                                               
         XC    CPGRS,CPGRS                                                      
         XC    CPNET,CPNET                                                      
*                                                                               
         L     R4,NBAIO                                                         
         MVI   ELCODE,X'03'                                                     
         BAS   RE,GETEL                                                         
         BNE   GETCP20                                                          
         USING NUSPRD,R4                                                        
*                                                                               
         CLI   NUSPRTYP,C'C'       CASH %                                       
         BNE   GETCP20                                                          
         OI    BYTE2,CASHCP                                                     
         MVC   CPGRS,NUSPRAMT      GROSS AMOUNT                                 
         DROP  R4                                                               
*                                                                               
         TM    NBUNITST,X'42'      TEST PRE-EMPT/MISSED                         
         BZ    GETCP20             NEITHER                                      
         L     R2,CPGRS            GET CASH %                                   
         GOTO1 FINDNET,DMCB2,C'C'                                               
         TM    NBUNITST,X'80'      TEST FOR MINUS UNIT                          
         BZ    *+8                                                              
         LNR   R2,R2                                                            
         LNR   R3,R3                                                            
         ST    R3,CPNET                                                         
*                                                                               
GETCP20  DS    0H                  GET PAID CP                                  
         XC    NBPAYCGR,NBPAYCGR                                                
         XC    NBPAYCNT,NBPAYCNT                                                
*                                                                               
         L     R4,NBAIO                                                         
         MVI   ELCODE,X'12'                                                     
         BAS   RE,GETEL                                                         
         B     *+8                                                              
         USING NUPAYD,R4                                                        
*                                                                               
GETCP25  BAS   RE,NEXTEL                                                        
         BNE   GETCPX                                                           
*                                                                               
         CLI   NUPAYTYP,C'C'        CASH% ?                                     
         BNE   GETCP25                                                          
         L     RF,NBPAYCGR                                                      
         A     RF,NUPAYGRS                                                      
         ST    RF,NBPAYCGR                                                      
*                                                                               
         L     RF,NBPAYCNT                                                      
         A     RF,NUPAYNET                                                      
         ST    RF,NBPAYCNT                                                      
         B     GETCP25                                                          
*                                                                               
GETCPX   B     EXIT                                                             
*                                                                               
EXIT     XIT1                                                                   
         SPACE                                                                  
         GETEL R4,DATADISP,ELCODE                                               
         EJECT                                                                  
CASHCP   EQU   X'01'                                                            
*                                  LTORG                                        
         LTORG                                                                  
         EJECT                                                                  
* LITERAL POOL                                                                  
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
       ++INCLUDE NEPAYWRK                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'088NEGETPAY  02/23/12'                                      
         END                                                                    
