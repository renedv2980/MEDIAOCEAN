*          DATA SET SPTRAUPD   AT LEVEL 010 AS OF 11/23/93                      
*CATALP TRFUPD                                                                  
         TITLE 'TRFUPD -- UPDATE TRAFFIC FILE FROM SPOT PROCESSING'             
***********************************************************************         
*                                                                     *         
* LEV 00    NOV16/93                                                  *         
*                                                                     *         
***********************************************************************         
         SPACE                                                                  
TRFUPD   CSECT                                                                  
         PRINT NOGEN                                                            
         ENTRY UTL                                                              
         ENTRY SSB                                                              
*                                                                               
         NBASE 0,*TRFUPD,VREGSAVE,RA                                            
*                                                                               
         L     R8,=V(CPRINT)                                                    
         USING DPRINT,R8                                                        
*                                                                               
         MVC   DUB,SPACES          LOAD MSUNPK                                  
         MVC   DUB(4),=C'T00A'                                                  
         MVI   WORK,QMSUNPK                                                     
         GOTO1 =V(HEXOUT),DMCB,WORK,DUB+4,1,0                                   
         GOTO1 =V(LOADER),DMCB,DUB,0                                            
         ICM   RE,15,4(R1)                                                      
         BNZ   *+6                                                              
         DC    H'0'                                                             
         ST    RE,VMSUNPK                                                       
*                                                                               
         GOTO1 =V(STXITER),DMCB,A(DUMPLIST)                                     
         XC    WORK,WORK                                                        
         ST    RB,WORK                                                          
         L     R4,VREGSAVE                                                      
         A     R4,=F'20000'                                                     
         ST    R4,WORK+4                                                        
         OI    WORK+4,X'80'                                                     
         GOTO1 =V(STXITER),DMCB,WORK                                            
         B     INIT2                                                            
*                                                                               
VREGSAVE DC    V(REGSAVE)                                                       
         EJECT                                                                  
INIT2    GOTO1 =V(CARDS),DMCB,CARD,=C'RE00'                                     
*                                                                               
         CLC   =C'TRF',CARD        MUST HAVE TRFFILE CARD                       
         BNE   INITSPER                                                         
         L     RE,=V(UTL)                                                       
         MVI   4(RE),X'41'                                                      
         CLI   CARD+3,C'1'                                                      
         BE    INIT4                                                            
         MVI   4(RE),X'03'                                                      
         CLI   CARD+3,C'2'                                                      
         BE    INIT4                                                            
         MVI   4(RE),X'07'                                                      
         CLI   CARD+3,C'3'                                                      
         BE    INIT4                                                            
         MVI   4(RE),X'0D'                                                      
         CLI   CARD+3,C'4'                                                      
         BE    INIT4                                                            
         MVI   4(RE),X'11'                                                      
         CLI   CARD+3,C'5'                                                      
         BE    INIT4                                                            
         MVI   4(RE),X'15'                                                      
         CLI   CARD+3,C'6'                                                      
         BE    INIT4                                                            
         MVI   4(RE),X'22'                                                      
         CLI   CARD+3,C'7'                                                      
         BE    INIT4                                                            
         MVI   4(RE),X'23'                                                      
         CLI   CARD+3,C'8'                                                      
         BE    INIT4                                                            
         MVI   4(RE),X'25'                                                      
         CLI   CARD+3,C'9'                                                      
         BE    INIT4                                                            
         MVI   4(RE),X'27'                                                      
         CLI   CARD+3,C'B'                                                      
         BE    INIT4                                                            
         MVI   4(RE),X'E2'                                                      
         CLI   CARD+3,C'E'                                                      
         BE    INIT4                                                            
         MVI   4(RE),X'F2'                                                      
         CLI   CARD+3,C'F'                                                      
         BE    INIT4                                                            
         MVI   4(RE),X'F3'                                                      
         CLI   CARD+3,C'G'                                                      
         BE    INIT4                                                            
         MVI   4(RE),X'F4'                                                      
         CLI   CARD+3,C'H'                                                      
         BE    INIT4                                                            
         MVI   4(RE),X'F5'                                                      
         CLI   CARD+3,C'L'                                                      
         BE    INIT4                                                            
         MVI   4(RE),X'F6'                                                      
         CLI   CARD+3,C'M'                                                      
         BE    INIT4                                                            
         MVI   4(RE),X'32'                                                      
         CLI   CARD+3,C'Q'                                                      
         BE    INIT4                                                            
         MVI   4(RE),X'33'                                                      
         CLI   CARD+3,C'S'                                                      
         BE    INIT4                                                            
*                                                                               
INITSPER GOTO1 =V(PRINTER)                                                      
         MVC   P(80),CARD                                                       
         GOTO1 =V(PRINTER)                                                      
         DC    H'0'                                                             
*                                                                               
INIT4    MVC   SVSYS,CARD                                                       
         ZAP   LINE,=P'99'                                                      
         GOTO1 =V(DATCON),DMCB,(5,0),(3,TODAY)                                  
*                                                                               
INIT6    GOTO1 =V(CARDS),DMCB,CARD,=C'RE00'                                     
         CLC   =C'/*',CARD                                                      
         BE    INIT14                                                           
*                                                                               
         CLC   =C'WRITE=NO',CARD   DON'T WRITE TO TRFFILE                       
         BNE   *+16                                                             
         MVI   TLIST,C' '          SUPPRESS EOF SEARCH ON TRFFILE               
         MVI   WRITESW,C'N'                                                     
         B     INIT6                                                            
*                                                                               
         CLC   =C'TEST=YES',CARD   WRITE TBA RECORDS TO OUTPUT FILE             
         BNE   *+12                                                             
         MVI   TESTSW,C'Y'                                                      
         B     INIT6                                                            
*                                                                               
         CLC   =C'TRACE=YES',CARD  TRACE ALL I/O?                               
         BNE   *+12                                                             
         MVI   TRACESW,C'Y'                                                     
         B     INIT6                                                            
*                                                                               
         CLC   =C'BLOWKEY=',CARD   FOR TESTING                                  
         BNE   INIT10                                                           
         XC    BLOWKEY,BLOWKEY                                                  
         GOTO1 =V(HEXIN),DMCB,CARD+8,BLOWKEY,26                                 
         B     INIT6                                                            
*                                                                               
INIT10   CLC   =C'TODAY=',CARD     FOR TESTING                                  
         BNE   INIT12                                                           
         GOTO1 =V(DATCON),DMCB,(4,CARD+6),(3,TODAY)                             
         B     INIT6                                                            
*                                                                               
INIT12   MVC   P(32),=C'*TRFUPDT* UNKNOWN PARAMETER CARD'                       
         GOTO1 =V(PRINTER)                                                      
         DC    H'0'                                                             
*                                                                               
* OPEN FILES *                                                                  
*                                                                               
INIT14   DS   0H                                                                
         SPACE                                                                  
         GOTO1 =V(DATAMGR),DMCB,=C'DMOPEN',=C'STR',TLIST,TRFWORK                
         SPACE                                                                  
         OPEN  (TRFILE,(INPUT))                                                 
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLI   TESTSW,C'Y'                                                      
         BNE   INIT16                                                           
         OPEN  (TESTOUT,(OUTPUT))                                               
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
INIT16   DS   0H                                                                
*                                                                               
         MVC   TITLE(30),=CL30'TRFN TRAFFIC UPDATE REPORT'                      
         MVC   TITLE(4),SVSYS                                                   
         MVC   MID1(L'TBAMID1),TBAMID1                                          
         MVC   MID2(L'TBAMID2),TBAMID2                                          
*                                                                               
         B     IN10                                                             
*                                                                               
TLIST    DS   0D                                                                
         DC    CL8'UTRFFILE'                                                    
         DC    CL8' TRFDIR '                                                    
         DC    CL8'UTRFRVC '                                                    
         DC    CL8'X       '                                                    
         EJECT                                                                  
*** READ INPUT TRAFFIC RECORDS FILE ***                                         
*                                                                               
IN10     DS   0H                                                                
*                                                                               
IN20     LA    R0,TRFWORK-5                                                     
         GET   TRFILE,(0)                                                       
*                                                                               
         MVI   RECSW,X'FF'                                                      
*                                                                               
         CLI   TRFCODE,C'A'                                                     
         BE    CKADD                                                            
         SPACE                                                                  
         CLI   TRFCODE,C'P'                                                     
         BE    CKPUT                                                            
         SPACE                                                                  
         CLI   TRFCODE,C'D'                                                     
         BE    CKDEL                                                            
         SPACE                                                                  
         DC    H'0'                                                             
         SPACE                                                                  
* CHECK OUT ADD RECORDS, THEN ADD THEM *                                        
         SPACE                                                                  
CKADD    MVC   KEY(13),TRFWORK                                                  
         BAS   RE,HIGH                                                          
         SPACE                                                                  
         CLC   KEY(13),KEYSAVE                                                  
         BNE   CKADD10                                                          
         SPACE                                                                  
         MVC   P(26),=C'ADD RECORD ALREADY ON FILE'                             
         GOTO1 =V(PRINTER)                                                      
         MVC   P+2(13),TRFWORK                                                  
         MVC   P+16(1),TRFCODE                                                  
         GOTO1 =V(HEXOUT),DMCB,TRFWORK,P+35,2,=C'TOG'                           
         GOTO1 =V(HEXOUT),DMCB,TRFWORK+2,P+40,1,=C'TOG'                         
         GOTO1 =V(HEXOUT),DMCB,TRFWORK+3,P+43,2,=C'TOG'                         
         GOTO1 =V(HEXOUT),DMCB,TRFWORK+5,P+48,8,=C'TOG'                         
         GOTO1 =V(PRINTER)                                                      
         B     IN20                                                             
*                                                                               
CKADD10  DS    0H                                                               
         SPACE                                                                  
         XC    KEY,KEY                                                          
         MVC   KEY(13),TRFWORK                                                  
         SPACE                                                                  
         LA    R1,TRFWORK                                                       
         ST    R1,AIO                                                           
         SPACE                                                                  
         CLI   TRACESW,C'Y'                                                     
         BNE   CKADD20                                                          
         SPACE                                                                  
         MVC   P(10),=C'ADD RECORD'                                             
         MVC   P+12(13),TRFWORK                                                 
         GOTO1 =V(HEXOUT),DMCB,TRFWORK,P+35,2,=C'TOG'                           
         GOTO1 =V(HEXOUT),DMCB,TRFWORK+2,P+40,1,=C'TOG'                         
         GOTO1 =V(HEXOUT),DMCB,TRFWORK+3,P+43,2,=C'TOG'                         
         GOTO1 =V(HEXOUT),DMCB,TRFWORK+5,P+48,8,=C'TOG'                         
         GOTO1 =V(PRINTER)                                                      
         SPACE                                                                  
CKADD20  DS    0H                                                               
         SPACE                                                                  
         BAS   RE,ADDREC                                                        
         SPACE                                                                  
         L     R1,ADDCT                                                         
         LA    R1,1(,R1)                                                        
         ST    R1,ADDCT                                                         
         SPACE                                                                  
         CLC   =X'0A21',KEY        IF COMML, ADD PASSIVE KEY                    
         BNE   IN20                 NOPE                                        
         SPACE                                                                  
         OI    KEY+1,X'80'                                                      
         XC    KEY+5(8),KEY+5      ZERO OUT COMML CODE                          
         MVC   KEY+5(3),TRFWORK+CMLSEQ-CMLRECD                                  
         SPACE                                                                  
         CLI   WRITESW,C'Y'        WRITE ALLOWED                                
         BNE   CKADD26                                                          
         SPACE                                                                  
         GOTO1 =V(DATAMGR),DMCB,=C'DMADD',=C'TRFDIR',KEY,KEY                    
         SPACE                                                                  
CKADD26  L     R1,ADPCT                                                         
         LA    R1,1(,R1)                                                        
         ST    R1,ADPCT                                                         
         SPACE                                                                  
         B     IN20                                                             
         SPACE                                                                  
* CHECK OUT PUT RECORDS, THEN UPDATE THEM *                                     
         SPACE                                                                  
CKPUT    MVC   KEY(13),TRFWORK                                                  
         BAS   RE,HIGH                                                          
         SPACE                                                                  
         CLC   KEY(13),KEYSAVE                                                  
         BE    CKPUT10                                                          
         SPACE                                                                  
         MVC   P(25),=C'PUT RECORD DOES NOT EXIST'                              
         GOTO1 =V(PRINTER)                                                      
         GOTO1 =V(HEXOUT),DMCB,TRFWORK,P+35,2,=C'TOG'                           
         GOTO1 =V(HEXOUT),DMCB,TRFWORK+2,P+40,1,=C'TOG'                         
         GOTO1 =V(HEXOUT),DMCB,TRFWORK+3,P+43,2,=C'TOG'                         
         GOTO1 =V(HEXOUT),DMCB,TRFWORK+5,P+48,8,=C'TOG'                         
         GOTO1 =V(PRINTER)                                                      
         B     IN20                                                             
*                                                                               
CKPUT10  DS    0H                                                               
         L     R1,=A(REC)                                                       
         ST    R1,AIO                                                           
         BAS   RE,GETREC                                                        
         SPACE                                                                  
         CLI   TRACESW,C'Y'                                                     
         BNE   CKPUT20                                                          
         SPACE                                                                  
         MVC   P(10),=C'PUT RECORD'                                             
         MVC   P+12(13),TRFWORK                                                 
         GOTO1 =V(HEXOUT),DMCB,TRFWORK,P+35,2,=C'TOG'                           
         GOTO1 =V(HEXOUT),DMCB,TRFWORK+2,P+40,1,=C'TOG'                         
         GOTO1 =V(HEXOUT),DMCB,TRFWORK+3,P+43,2,=C'TOG'                         
         GOTO1 =V(HEXOUT),DMCB,TRFWORK+5,P+48,8,=C'TOG'                         
         GOTO1 =V(PRINTER)                                                      
         SPACE                                                                  
CKPUT20  DS    0H                                                               
         LA    R1,TRFWORK                                                       
         ST    R1,AIO                                                           
         SPACE                                                                  
         BAS   RE,PUTREC                                                        
         SPACE                                                                  
         L     R1,PUTCT                                                         
         LA    R1,1(,R1)                                                        
         ST    R1,PUTCT                                                         
         SPACE                                                                  
         B     IN20                                                             
         SPACE                                                                  
* CHECK OUT DELETE RECORDS, THEN UPDATE THEM *                                  
         SPACE                                                                  
CKDEL    MVC   KEY(13),TRFWORK                                                  
         BAS   RE,HIGH                                                          
         SPACE                                                                  
         CLC   KEY(13),KEYSAVE                                                  
         BE    CKDEL10                                                          
         SPACE                                                                  
         MVC   P(25),=C'DEL RECORD DOES NOT EXIST'                              
         GOTO1 =V(PRINTER)                                                      
         GOTO1 =V(HEXOUT),DMCB,TRFWORK,P+35,2,=C'TOG'                           
         GOTO1 =V(HEXOUT),DMCB,TRFWORK+2,P+40,1,=C'TOG'                         
         GOTO1 =V(HEXOUT),DMCB,TRFWORK+3,P+43,2,=C'TOG'                         
         GOTO1 =V(HEXOUT),DMCB,TRFWORK+5,P+48,8,=C'TOG'                         
         GOTO1 =V(PRINTER)                                                      
         B     IN20                                                             
*                                                                               
CKDEL10  DS    0H                                                               
         L     R1,=A(REC)                                                       
         ST    R1,AIO                                                           
         BAS   RE,GETREC                                                        
         SPACE                                                                  
         CLI   TRACESW,C'Y'                                                     
         BNE   CKDEL20                                                          
         SPACE                                                                  
         MVC   P(10),=C'DEL RECORD'                                             
         MVC   P+12(13),TRFWORK                                                 
         GOTO1 =V(HEXOUT),DMCB,TRFWORK,P+35,2,=C'TOG'                           
         GOTO1 =V(HEXOUT),DMCB,TRFWORK+2,P+40,1,=C'TOG'                         
         GOTO1 =V(HEXOUT),DMCB,TRFWORK+3,P+43,2,=C'TOG'                         
         GOTO1 =V(HEXOUT),DMCB,TRFWORK+5,P+48,8,=C'TOG'                         
         GOTO1 =V(PRINTER)                                                      
         SPACE                                                                  
CKDEL20  DS    0H                                                               
         LA    R1,TRFWORK                                                       
         ST    R1,AIO                                                           
         SPACE                                                                  
         BAS   RE,DELREC                                                        
         SPACE                                                                  
         L     R1,DELCT                                                         
         LA    R1,1(,R1)                                                        
         ST    R1,DELCT                                                         
         SPACE                                                                  
         B     IN20                                                             
         EJECT                                                                  
EOFTRF   CLOSE (TRFILE,)                                                        
         SPACE                                                                  
         EDIT  PUTCT,(7,P+6),COMMAS=YES                                         
         MVC   P+14(15),=C'CHANGED RECORDS'                                     
         GOTO1 =V(PRINTER)                                                      
         EDIT  DELCT,(7,P+6),COMMAS=YES                                         
         MVC   P+14(15),=C'DELETED RECORDS'                                     
         GOTO1 =V(PRINTER)                                                      
         EDIT  ADDCT,(7,P+6),COMMAS=YES                                         
         MVC   P+14(13),=C'ADDED RECORDS'                                       
         EDIT  ADPCT,(7,P+30),COMMAS=YES                                        
         MVC   P+38(18),=C'ADDED PASSIVE KEYS'                                  
         GOTO1 =V(PRINTER)                                                      
         GOTO1 =V(PRINTER)                                                      
*                                                                               
* AT EOF, SEE IF ANY RECORDS PROCESSED *                                        
*                                                                               
*                                                                               
         CLI   RECSW,X'FF'         TEST ANY RECORDS READ                        
         BE    EOJ                                                              
         MVC   P(11),=C'NO ACTIVITY'                                            
         GOTO1 =V(PRINTER)                                                      
         B     EOJ                                                              
*                                                                               
         EJECT                                                                  
*                                                                               
PATCH1   DS    CL32                                                             
PATCH2   DS    CL32                                                             
PATCH3   DS    CL32                                                             
PATCH4   DS    CL32                                                             
         EJECT                                                                  
HIGH     NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         SPACE                                                                  
         CLI   KEY,X'0A'           THIS A TRAFFIC REC                           
         BE    *+6                                                              
         DC    H'0'                                                             
         SPACE                                                                  
         GOTO1 =V(DATAMGR),DMCB,=C'DMRDHI',=C'TRFDIR',KEYSAVE,KEY               
         B     PUTRECX                                                          
*                                                                               
SEQ      NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         SPACE                                                                  
         CLI   KEY,X'0A'           THIS A TRAFFIC REC                           
         BE    *+6                                                              
         DC    H'0'                                                             
         SPACE                                                                  
         GOTO1 =V(DATAMGR),DMCB,=C'DMRSEQ',=C'TRFDIR',KEYSAVE,KEY               
         B     PUTRECX                                                          
*                                                                               
GETREC   LA    RF,=C'GETREC'                                                    
         B     COMREC                                                           
*                                                                               
ADDREC   LA    RF,=C'ADDREC'                                                    
         B     COMREC                                                           
*                                                                               
PUTREC   LA    RF,=C'PUTREC'                                                    
         B     COMREC                                                           
*                                                                               
DELREC   LA    RF,=C'DMDEL '                                                    
*                                                                               
         DS    0H                                                               
COMREC   NTR1                                                                   
         ST    RF,DMCB                                                          
         MVC   DMCB(1),DMINBTS                                                  
         SPACE                                                                  
         CLI   KEY,X'0A'           THIS A TRAFFIC REC                           
         BE    *+6                                                              
         DC    H'0'                                                             
         SPACE                                                                  
         LA    RE,=C'GETREC'                                                    
         CR    RE,RF                                                            
         BE    PUTREC4                                                          
         CLI   WRITESW,C'Y'        SEE IF UPDATED ALLOWED                       
         BNE   PUTRECX                                                          
         SPACE                                                                  
PUTREC4  DS    0H                                                               
         GOTO1 =V(DATAMGR),DMCB,,=C'TRFFILE',KEY+14,AIO,DMWORK                  
*                                                                               
PUTRECX  TM    8(R1),X'FD'         SET CC ON EXIT                               
*                                                                               
EXIT     XIT1                                                                   
*                                                                               
EOJ      CLI   TESTSW,C'Y'                                                      
         BNE   EOJ2                                                             
         CLOSE (TESTOUT,)                                                       
EOJ2     XBASE                                                                  
         EJECT                                                                  
UTL      DC    F'0',X'00'                                                       
SSB      DC    F'0'                                                             
*                                                                               
DUMPLIST DS    0F                                                               
         DC    A(TRFUPD,65000)                                                  
         ORG   *-4                                                              
         DC    X'80'                                                            
         ORG                                                                    
*                                                                               
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
BYTE2    DS    X                                                                
         DC    C'**DMCB**'                                                      
DMCB     DS    6F                                                               
         DC    C'***KEY**'                                                      
KEY      DS    XL20                                                             
KEYSAVE  DS    XL20                                                             
         DS    0D                                                               
DMWORK   DS    XL96                                                             
*                                                                               
WORK     DS    CL64                                                             
CARD     DS    CL80                                                             
BLOWKEY  DS    XL13                                                             
ELCODE   DS    X                                                                
RECSW    DC    X'00'                                                            
TESTSW   DC    X'00'                                                            
TRACESW  DC    X'00'                                                            
WRITESW  DC    X'00'                                                            
SVSYS    DS    CL8                                                              
DMINBTS  DS    X                                                                
TODAY    DS    XL3                                                              
*                                                                               
AIO      DC    A(REC)                                                           
PUTCT    DC    F'0'                                                             
DELCT    DC    F'0'                                                             
ADDCT    DC    F'0'                                                             
ADPCT    DC    F'0'                                                             
*                                                                               
VMSUNPK  DS    V                                                                
*                                                                               
WKADDR   DS    A                                                                
WKCTR    DC    PL2'0'                                                           
LNCTR    DC    PL2'0'                                                           
         EJECT                                                                  
*                                                                               
TBAMID1  DC    CL132'AG M CLT MKT  STAT    PRODUCT EST ACTIVE WEEKS'            
TBAMID2  DC    CL132'-- - --- ---  ----    ------- --- ------------'            
*                                                                               
P1       DC    CL132' '                                                         
P2       DC    CL132' '                                                         
P3       DC    CL132' '                                                         
P4       DC    CL132' '                                                         
P5       DC    CL132' '                                                         
P6       DC    CL132' '                                                         
P7       DC    CL132' '                                                         
P8       DC    CL132' '                                                         
P9       DC    CL132' '                                                         
P10      DC    CL132' '                                                         
P11      DC    CL132' '                                                         
P12      DC    CL132' '                                                         
*                                                                               
         ORG   P1                                                               
PAGY     DS    CL2                                                              
         DS    CL1                                                              
PMED     DS    CL1                                                              
         DS    CL1                                                              
PCLT     DS    CL3                                                              
         DS    CL1                                                              
PMKT     DS    CL4                                                              
         DS    CL1                                                              
PSTA     DS    CL6                                                              
         DS    CL2                                                              
PPRD     DS    CL7                                                              
         DS    CL1                                                              
PEST     DS    CL3                                                              
         DS    CL1                                                              
PWEEKS   DS    CL72                                                             
         ORG                                                                    
*                                                                               
*                                                                               
TRFILE   DCB   DDNAME=TRFILE,DSORG=PS,RECFM=VB,LRECL=2000,             +        
               BLKSIZE=2048,MACRF=GM,EODAD=EOFTRF                               
*                                                                               
TESTOUT  DCB   DDNAME=TESTOUT,DSORG=PS,RECFM=VB,LRECL=2100,            +        
               BLKSIZE=2104,MACRF=PM                                            
*                                                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
         DS    0D                                                               
         DC    C'*TRFWORK'                                                      
TRFLEN   DS    H                   LOGICAL IOCS LEN                             
         DS    H                                                                
TRFCODE  DS    CL1                                                              
TRFWORK  DS    CL2000                                                           
*                                                                               
*                                                                               
*                                                                               
REC      DS    CL2000                                                           
COMFACS  DS    64A                                                              
       ++INCLUDE DDCOMFACS                                                      
         SPACE 3                                                                
         EJECT                                                                  
       ++INCLUDE DDDPRINT                                                       
         EJECT                                                                  
       ++INCLUDE SPTRCMML                                                       
         EJECT                                                                  
* DDCOREQUS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOREQUS                                                      
         PRINT ON                                                               
         SPACE                                                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'010SPTRAUPD  11/23/93'                                      
         END                                                                    
