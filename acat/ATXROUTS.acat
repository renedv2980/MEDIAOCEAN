*          DATA SET ATXROUTS   AT LEVEL 009 AS OF 06/09/03                      
*CATALP TXROUTS                                                                 
         TITLE 'EXTRACT ROUTINES FOR ATXTRACT'                                  
         PRINT NOGEN                                                            
ATXROUTS CSECT                                                                  
         ENTRY ATXAGRC             ACCOUNT GROUP                                
         ENTRY ATXCPYC             COMPANY                                      
         ENTRY ATXCLIC             CLIENT                                       
         ENTRY ATXPROC             PRODUCT                                      
         ENTRY ATXJOBC             JOB                                          
         ENTRY ATXMEDC             MEDIA                                        
         ENTRY ATXMGRC             MEDIA GROUP                                  
         ENTRY ATXTSKC             TASK                                         
         ENTRY ATXSTKC             SUB-TASK                                     
         ENTRY ATXTGRC             TASK GROUP                                   
         ENTRY ATXPOFC             PRODUCTION OFFICE                            
         ENTRY ATXOGRC             PRODUCTION OFFICE GROUP                      
         ENTRY ATXOFLC             PRODUCTION OFFICE LIST                       
         ENTRY ATXOLEC             PRODUCTION OFFICE LIST ENTRY                 
         ENTRY ATXOFFC             1R OFFICE                                    
         ENTRY ATXDEPC             1R DEPARTMENT                                
         ENTRY ATXSBDC             1R SUB-DEPARTMENT                            
         ENTRY ATXPERC             PERSON                                       
         ENTRY ATXPEAC             PERSON ASSIGNMENT                            
         ENTRY ATXCSAC             COSTING ACCOUNT                              
         ENTRY ATXLEDC             LEDGER                                       
         ENTRY ATXLESC             LEDGER STRUCTURE                             
*        ENTRY ATXOPTC             OPTIONS                                      
         ENTRY ATXSTCC             STANDARD COMMENT                             
         ENTRY ATXUSRC             USERFIELD RECORD                             
         ENTRY ATXCAGC             CONTROL AGENCY RECORD                        
         ENTRY ATXCPEC             CONTROL PERSON RECORD                        
*                                                                               
EXIT     XMOD1                                                                  
         EJECT                                                                  
***********************************************************************         
* ATXAGRC - EXTRACT ACCOUNT GROUP RECORDS FOR ATXTRACT                *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
ATXAGRC  NMOD1 WORKX-WORKD,*ATXAGR*,RR=RA                                       
         USING WORKD,RC                                                         
         L     RA,ATXAGRLT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING ATXRECD,R2          R2=A(EXTRACT RECORD)                         
         USING AGRRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         XC    ATXRELEN,ATXRELEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   ATXRELEN(2),=Y(ATXAGRL)                                          
         MVC   ATXRETYP,ATXAGRQ                                                 
         MVI   ATXAGAID-1,MXTRTQ                                                
         MVI   ATXAGRGT-1,MXTRTQ                                                
         MVI   ATXAGRGC-1,MXTRTQ                                                
         MVI   ATXAGRNM-1,MXTRTQ                                                
         MVI   ATXAGROF-1,MXTRTQ                                                
         MVI   ATXAGRX,MXTRTQ                                                   
         MVI   ATXAGRX+1,MXTRTQ                                                 
*                                                                               
         MVC   ATXAGAID,SXDTAGY    ALPHA AGENCY ID CODE                         
         TR    ATXAGAID,MXTRT                                                   
         MVC   ATXAGRGT,AGRKGTYP   GROUP TYPE                                   
         TR    ATXAGRGT,MXTRT                                                   
         MVC   ATXAGRGC,AGRKAGR    GROUP CODE                                   
         TR    ATXAGRGC,MXTRT                                                   
*                                                                               
         USING NAMELD,R5                                                        
         LA    R5,AGRRFST          R5=A(FIRST ELEMENT)                          
ATXAGR10 CLI   NAMEL,0             GET ELEMENT FROM RECORD                      
         JE    ATXAGRXX                                                         
         CLI   NAMEL,NAMELQ        X'20' - NAME ELEMENT                         
         JE    ATXAGR30                                                         
         CLI   NAMEL,RSTELQ        X'30' - RECORD STATUS ELEMENT                
         JE    ATXAGR40                                                         
ATXAGR20 SR    RF,RF                                                            
         IC    RF,1(R5)                                                         
         AR    R5,RF                                                            
         J     ATXAGR10                                                         
*                                                                               
ATXAGR30 MVC   ATXAGRNM,SPACES     ACCOUNT GROUP NAME                           
         SR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         JM    ATXAGRXX                                                         
         EX    RF,*+4                                                           
         MVC   ATXAGRNM(0),NAMEREC                                              
         TR    ATXAGRNM,MXTRT                                                   
         J     ATXAGR20                                                         
*                                                                               
         USING RSTELD,R5                                                        
ATXAGR40 MVC   ATXAGROF,RSTOFFC    ACCOUNT GROUP OFFICE                         
         TR    ATXAGROF,MXTRT                                                   
         J     ATXAGR20                                                         
*                                                                               
ATXAGRXX DS    0H                                                               
         J     EXIT                                                             
         DROP  R2,R3,R4,R5                                                      
*                                                                               
ATXAGRLT DC   A(LITERAL)                                                        
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ATXCPYC - EXTRACT COMPANY RECORDS FOR ATXTRACT                      *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
ATXCPYC  NMOD1 WORKX-WORKD,*ATXCPY*,RR=RA                                       
         USING WORKD,RC                                                         
         L     RA,ATXCPLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING ATXRECD,R2          R2=A(EXTRACT RECORD)                         
         USING ACTKEY,R3           R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTABD)                               
*                                                                               
         XC    ATXRELEN,ATXRELEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   ATXRELEN(2),=Y(ATXCPYL)                                          
         MVC   ATXRETYP,ATXCPYQ                                                 
         MVI   ATXCPAID-1,MXTRTQ                                                
         MVI   ATXCPNAM-1,MXTRTQ                                                
         MVI   ATXCPGRP-1,MXTRTQ                                                
         MVI   ATXCPLDG-1,MXTRTQ                                                
         MVI   ATXCPOFF-1,MXTRTQ                                                
         MVI   ATXCPAD1-1,MXTRTQ                                                
         MVI   ATXCPAD2-1,MXTRTQ                                                
         MVI   ATXCPAD3-1,MXTRTQ                                                
         MVI   ATXCPAD4-1,MXTRTQ                                                
         MVI   ATXCPYX,MXTRTQ                                                   
         MVI   ATXCPYX+1,MXTRTQ                                                 
*                                                                               
         MVC   ATXCPAID,SXDTAGY    ALPHA AGENCY ID CODE                         
         TR    ATXCPAID,MXTRT                                                   
         MVC   ATXCPGRP,SPACES     CLEAR GROUP TO SPACES                        
         TR    ATXCPGRP,MXTRT                                                   
*                                                                               
         LA    R5,ACTRFST          R5=A(FIRST ELEMENT)                          
ATXCP10  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    ATXCPX                                                           
         CLI   0(R5),NAMELQ        X'20' - NAME ELEMENT                         
         JE    ATXCP30                                                          
         CLI   0(R5),CPYELQ        COMPANY ELEMENT                              
         JE    ATXCP40                                                          
         CLI   0(R5),ADRELQ        X'22' - ADDRESS ELEMENT                      
         JE    ATXCP50                                                          
*                                                                               
ATXCP20  SR    RF,RF               BUMP TO NEXT ELEMENT                         
         ICM   RF,1,1(R5)                                                       
         JZ    ATXCPX              EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         J     ATXCP10                                                          
*                                                                               
         USING NAMELD,R5                                                        
ATXCP30  MVC   ATXCPNAM,SPACES     COMPANY NAME                                 
         SR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         JM    ATXCP20                                                          
         EX    RF,*+4                                                           
         MVC   ATXCPNAM(0),NAMEREC                                              
         TR    ATXCPNAM,MXTRT                                                   
         J     ATXCP20                                                          
*                                                                               
         USING CPYELD,R5                                                        
ATXCP40  DS    0H                                                               
         MVI   ATXCPLDG,C'N'       SET DEFAULT AS N                             
         MVI   ATXCPOFF,C'N'       SET DEFAULT AS N                             
         TM    CPYBSEC,CPYBSSEC+CPYBSOFF  IS THERE OFF OR LDG SECURITY?         
         BZ    ATXCP20                                                          
         TM    CPYBSEC,CPYBSSEC    LEDGER SECURITY?                             
         BZ    *+14                                                             
         MVI   ATXCPLDG,C'Y'       MARK AS ON                                   
         TM    CPYBSEC,CPYBSOFF    OFFICE SECURITY?                             
         BZ    ATXCP20                                                          
         MVI   ATXCPOFF,C'Y'       MARK AS ON                                   
         J     ATXCP20                                                          
*                                                                               
         USING ADRELD,R5                                                        
ATXCP50  DS    0H                  ADDRESS ELEMENT                              
         SR    R0,R0                                                            
         ICM   R0,1,ADRNUM         NUMBER OF ADDRESS LINES                      
         BZ    ATXCP20                                                          
         LA    RF,ADRADD1          POINT TO 1ST ELEMENT LINE                    
         LA    RE,ATXCPAD1         POINT TO FIRST TABLE LINE                    
ATXCP60  MVC   0(L'ATXCPAD1,RE),0(RF)   MOVE IN CURRENT LINE                    
         TR    0(L'ATXCPAD1,RE),MXTRT                                           
         LA    RE,ATXCPALQ(RE)     SKIP TO NEXT LINE IN TAB                     
         LA    RF,L'ADRADD1(RF)    SKIP TO NEXT LINE IN ELM                     
         BCT   R0,ATXCP60                                                       
         J     ATXCP20                                                          
*                                                                               
ATXCPX   DS    0H                                                               
         J     EXIT                                                             
         DROP  R2,R3,R4,R5                                                      
*                                                                               
ATXCPLIT DC    A(LITERAL)                                                       
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ATXCLIC - EXTRACT CLIENT CODES RECORDS FOR ATXTRACT                 *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD) - CAN GET 2 BYTE ALPHA FROM THIS...                  *         
* AL4 A(LEVELS) - CAN GET LENGTHS OF LEVELS FROM HERE                 *         
***********************************************************************         
         SPACE 1                                                                
ATXCLIC  NMOD1 WORKX-WORKD,*AXCLIC*,RR=RA                                       
         USING WORKD,RC                                                         
         L     RA,ATXCLLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         MVC   ALEVELS,16(R1)                                                   
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING ATXRECD,R2          R2=A(EXTRACT RECORD)                         
         USING ACTKEY,R3           R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R5=A(SXDTABD)                                
*                                                                               
         XC    ATXRELEN,ATXRELEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   ATXRELEN(2),=Y(ATXCLILN)                                         
         MVC   ATXRETYP,ATXCLIQ    CLI. TABLE ID                                
         MVI   ATXCLAID-1,MXTRTQ                                                
         MVI   ATXCLCLI-1,MXTRTQ                                                
         MVI   ATXCLNAM-1,MXTRTQ                                                
         MVI   ATXCLF1-1,MXTRTQ                                                 
         MVI   ATXCLF2-1,MXTRTQ                                                 
         MVI   ATXCLF3-1,MXTRTQ                                                 
         MVI   ATXCLF4-1,MXTRTQ                                                 
         MVI   ATXCLF5-1,MXTRTQ                                                 
         MVI   ATXCLOFC-1,MXTRTQ                                                
         MVI   ATXCLRAC-1,MXTRTQ                                                
         MVI   ATXCLCAC-1,MXTRTQ                                                
         MVI   ATXCLAG1-1,MXTRTQ                                                
         MVI   ATXCLAG2-1,MXTRTQ                                                
         MVI   ATXCLIL-1,MXTRTQ                                                 
         MVI   ATXCLENP-1,MXTRTQ                                                
         MVI   ATXCLBA1-1,MXTRTQ                                                
         MVI   ATXCLBA2-1,MXTRTQ                                                
         MVI   ATXCLBA3-1,MXTRTQ                                                
         MVI   ATXCLBA4-1,MXTRTQ                                                
         MVI   ATXCLPOB-1,MXTRTQ                                                
         MVI   ATXCLCDT-1,MXTRTQ                                                
         MVI   ATXCLIX,MXTRTQ                                                   
         MVI   ATXCLIX+1,MXTRTQ                                                 
*                                                                               
         MVC   ATXCLAID,SXDTAGY    ALPHA AGENCY ID CODE                         
         TR    ATXCLAID,MXTRT                                                   
*                                                                               
         MVC   ATXCLCLI,SPACES     CLIENT                                       
         L     R6,ALEVELS                                                       
         SR    RF,RF                                                            
         IC    RF,0(R6)                                                         
         BCTR  RF,0                                                             
         EX    RF,*+4                                                           
         MVC   ATXCLCLI(0),ACTKACT                                              
         TR    ATXCLCLI,MXTRT                                                   
*&&US*&& MVC   ATXCLCDT,=C'1990-01-01'   DEFAULT                                
*&&UK*&& MVC   ATXCLCDT,=C'01/01/90'     DEFAULT                                
         TR    ATXCLCDT,MXTRT                                                   
*                                                                               
         LA    R5,ACTRFST          R5=A(FIRST ELEMENT)                          
ATXCL10  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    ATXCLX                                                           
         CLI   0(R5),NAMELQ        X'20' - NAME ELEMENT                         
         JE    ATXCL30                                                          
         CLI   0(R5),RSTELQ        X'30' - RECORD STATUS ELEMENT                
         JE    ATXCL40                                                          
         CLI   0(R5),PPRELQ        X'24' - PRODUCTION PROFILE ELEMENT           
         JE    ATXCL50                                                          
         CLI   0(R5),ADRELQ        X'22' - ADDRESS ELEMENT                      
         JE    ATXCL60                                                          
         CLI   0(R5),AGRELQ        X'FC' - ACC. GROUP ELEMENT                   
         JE    ATXCL80                                                          
         CLI   0(R5),PACELQ        X'A1' - PERSON ACTIVITY ELEMENT              
         JE    ATXCL90                                                          
*                                                                               
ATXCL20  SR    RF,RF               BUMP TO NEXT ELEMENT                         
         ICM   RF,1,1(R5)                                                       
         JZ    ATXCLX              EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         J     ATXCL10                                                          
*                                                                               
         USING NAMELD,R5                                                        
ATXCL30  MVC   ATXCLNAM,SPACES                                                  
         SR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         BM    ATXCL20                                                          
         EX    RF,*+4                                                           
         MVC   ATXCLNAM(0),NAMEREC                                              
         TR    ATXCLNAM,MXTRT                                                   
         J     ATXCL20                                                          
*                                                                               
         USING RSTELD,R5                                                        
ATXCL40  MVC   ATXCLF1,RSTFILT1    ACCOUNT FILTER 1                             
         TR    ATXCLF1,MXTRT                                                    
         MVC   ATXCLF2,RSTFILT2    ACCOUNT FILTER 2                             
         TR    ATXCLF2,MXTRT                                                    
         MVC   ATXCLF3,RSTFILT3    ACCOUNT FILTER 3                             
         TR    ATXCLF3,MXTRT                                                    
         MVC   ATXCLF4,RSTFILT4    ACCOUNT FILTER 4                             
         TR    ATXCLF4,MXTRT                                                    
         MVC   ATXCLF5,RSTFILT5    ACCOUNT FILTER 5                             
         TR    ATXCLF5,MXTRT                                                    
*                                                                               
         MVI   ATXCLIL,C'N'                                                     
         TM    RSTSTAT1,RSTSACIL   ACCOUNT IS LOCKED                            
         JNO   *+8                                                              
         MVI   ATXCLIL,C'Y'                                                     
*                                                                               
*&&US                                                                           
         MVI   ATXCLENP,C'Y'       ASSUME THERE ISN'T ANY BIT SET               
         CLI   RSTLN,RSTLN2Q       DO WE EVEN HAVE A STAT6?                     
         JNH   ATXCL20             NO-DONT BOTHER TESTING                       
         MVI   ATXCLENP,C'N'       ASSUME NOT TO EXCLUDE                        
         TM    RSTSTAT6,RSTSIN@T   INCLUDE CLIENT?                              
         JO    *+8                                                              
         MVI   ATXCLENP,C'Y'                                                    
*&&                                                                             
*&&UK                                                                           
         MVI   ATXCLENP,C'N'       ASSUME NOT TO EXCLUDE                        
         CLI   RSTLN,RSTLN2Q       DO WE EVEN HAVE A STAT6?                     
         JNH   ATXCL20             NO-DONT BOTHER TESTING                       
         TM    RSTSTAT6,RSTSXFNP   EXCLUDE CLIENT?                              
         JNO   *+8                                                              
         MVI   ATXCLENP,C'Y'       EXCLUDE IF SET                               
*&&                                                                             
         TR    ATXCLENP,MXTRT                                                   
         J     ATXCL20                                                          
*                                                                               
         USING PPRELD,R5                                                        
ATXCL50  MVC   ATXCLOFC,PPRGAOFF   OFFICE CODE                                  
         TR    ATXCLOFC,MXTRT                                                   
         MVC   ATXCLRAC,PPRRECVU   RECEIVABLES ACCOUNT                          
         TR    ATXCLRAC,MXTRT                                                   
         MVC   ATXCLCAC,PPRCOSTU   COSTING ACCOUNT                              
         TR    ATXCLCAC,MXTRT                                                   
         MVC   ATXCLPOB,PPRBILLP   PRINT ON BILL                                
         TR    ATXCLPOB,MXTRT                                                   
         J     ATXCL20                                                          
*                                                                               
         USING ADRELD,R5                                                        
ATXCL60  DS    0H                                                               
         SR    R0,R0                                                            
         ICM   R0,1,ADRNUM         NUMBER OF ADDRESS LINES                      
         JZ    ATXCL20                                                          
         LA    RE,ATXCLBA1         POINT RE TO 1ST ADD LINE IN TABLE            
         LA    RF,ADRADD1          POINT RF TO 1ST ADD LINE IN ELM              
ATXCL70  MVC   0(L'ATXCLBA1,RE),0(RF)                                           
         TR    0(L'ATXCLBA1,RE),MXTRT                                           
         LA    RE,ATXCLBLQ(RE)                                                  
         LA    RF,L'ADRADD1(RF)                                                 
         BCT   R0,ATXCL70                                                       
         J     ATXCL20                                                          
*                                                                               
*                                                                               
         USING AGRELD,R5                                                        
ATXCL80  MVC   ATXCLAG1,AGRAGR1                                                 
         TR    ATXCLAG1,MXTRT                                                   
         CLI   AGRLN,AGRLN1Q                                                    
         JE    ATXCL20                                                          
         MVC   ATXCLAG2,AGRAGR2                                                 
         TR    ATXCLAG2,MXTRT                                                   
         J     ATXCL20                                                          
*                                                                               
         USING PACELD,R5                                                        
ATXCL90  OC    PACDATE,PACDATE     DO WE HAVE A DATE?                           
         JZ    ATXCL20                                                          
*&&US*&& GOTO1 VDATCON,DMCB,(1,PACDATE),(23,ATXCLCDT),0                         
*&&UK*&& GOTO1 VDATCON,DMCB,(1,PACDATE),(10,ATXCLCDT),0                         
         J     ATXCL20                                                          
*                                                                               
ATXCLX   DS    0H                                                               
         J     EXIT                                                             
         DROP  R2,R3,R4,R5                                                      
*                                                                               
ATXCLLIT DC    A(LITERAL)                                                       
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ATXPROC - EXTRACT PRODUCT RECORDS FOR ATXTRACT                      *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(LEVELS) - CAN GET LENGTHS OF LEVELS FROM HERE                 *         
***********************************************************************         
         SPACE 1                                                                
ATXPROC  NMOD1 WORKX-WORKD,*ATXPRC*,RR=RA                                       
         USING WORKD,RC                                                         
         L     RA,ATXPRLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         MVC   ALEVELS,16(R1)                                                   
         USING ATXRECD,R2          R2=A(EXTRACT RECORD)                         
         USING ACTKEY,R3           R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTABD)                               
*                                                                               
         XC    ATXRELEN,ATXRELEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   ATXRELEN(2),=Y(ATXPRDL)                                          
         MVC   ATXRETYP,ATXPROQ                                                 
         MVI   ATXPRAID-1,MXTRTQ                                                
         MVI   ATXPRCLI-1,MXTRTQ                                                
         MVI   ATXPRPRO-1,MXTRTQ                                                
         MVI   ATXPRNAM-1,MXTRTQ                                                
         MVI   ATXPRF1-1,MXTRTQ                                                 
         MVI   ATXPRF2-1,MXTRTQ                                                 
         MVI   ATXPRF3-1,MXTRTQ                                                 
         MVI   ATXPRF4-1,MXTRTQ                                                 
         MVI   ATXPRF5-1,MXTRTQ                                                 
         MVI   ATXPROFC-1,MXTRTQ                                                
         MVI   ATXPRRAC-1,MXTRTQ                                                
         MVI   ATXPRCAC-1,MXTRTQ                                                
         MVI   ATXPRAG1-1,MXTRTQ                                                
         MVI   ATXPRAG2-1,MXTRTQ                                                
         MVI   ATXPRIL-1,MXTRTQ                                                 
         MVI   ATXPRENP-1,MXTRTQ                                                
         MVI   ATXPRBA1-1,MXTRTQ                                                
         MVI   ATXPRBA2-1,MXTRTQ                                                
         MVI   ATXPRBA3-1,MXTRTQ                                                
         MVI   ATXPRBA4-1,MXTRTQ                                                
         MVI   ATXPRPOB-1,MXTRTQ                                                
         MVI   ATXPRCDT-1,MXTRTQ                                                
         MVI   ATXPRDX,MXTRTQ                                                   
         MVI   ATXPRDX+1,MXTRTQ                                                 
*                                                                               
         MVC   ATXPRAID,SXDTAGY    2-BYTE AGY ALPHA                             
         TR    ATXPRAID,MXTRT                                                   
*                                                                               
         MVC   ATXPRCLI,SPACES                                                  
         L     R6,ALEVELS          CLIENT                                       
         SR    RF,RF                                                            
         IC    RF,0(R6)                                                         
         BCTR  RF,0                                                             
         EX    RF,*+4                                                           
         MVC   ATXPRCLI(0),ACTKACT                                              
         TR    ATXPRCLI,MXTRT                                                   
         CLC   ATXPRCLI,=CL6'107'                                               
         BNE   *+8                                                              
         B     *+4                                                              
*                                                                               
         LA    RE,ACTKACT+1(RF)    PRODUCT                                      
         MVC   ATXPRPRO,SPACES                                                  
         SR    RF,RF                                                            
         IC    RF,1(R6)                                                         
         BCTR  RF,0                                                             
         EX    RF,*+4                                                           
         MVC   ATXPRPRO(0),0(RE)                                                
         TR    ATXPRPRO,MXTRT                                                   
*&&US*&& MVC   ATXPRCDT,=C'1990-01-01'   DEFAULT                                
*&&UK*&& MVC   ATXPRCDT,=C'01/01/90'     DEFAULT                                
         TR    ATXPRCDT,MXTRT                                                   
*                                                                               
         LA    R5,ACTRFST          R5=A(FIRST ELEMENT)                          
ATXPR10  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    ATXPRX                                                           
         CLI   0(R5),NAMELQ        X'20' - NAME ELEMENT                         
         JE    ATXPR30                                                          
         CLI   0(R5),RSTELQ        X'30' - RECORD STATUS ELEMENT                
         JE    ATXPR40                                                          
         CLI   0(R5),PPRELQ        X'24' - PRODUCTION PROFILE ELEMENT           
         JE    ATXPR50                                                          
         CLI   0(R5),ADRELQ        X'22' - ADDRESS ELEMENT                      
         JE    ATXPR60                                                          
         CLI   0(R5),AGRELQ        X'FC' - ACC. GROUP ELEMENT                   
         JE    ATXPR80                                                          
         CLI   0(R5),PACELQ        X'A1' - PERSON ACTIVITY ELEMENT              
         JE    ATXPR90                                                          
*                                                                               
ATXPR20  SR    RF,RF               BUMP TO NEXT ELEMENT                         
         ICM   RF,1,1(R5)                                                       
         JZ    ATXPRX                                                           
         AR    R5,RF                                                            
         J     ATXPR10                                                          
*                                                                               
         USING NAMELD,R5                                                        
ATXPR30  MVC   ATXPRNAM,SPACES                                                  
         SR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         JM    ATXPR20                                                          
         EX    RF,*+4                                                           
         MVC   ATXPRNAM(0),NAMEREC                                              
         TR    ATXPRNAM,MXTRT                                                   
         J     ATXPR20                                                          
*                                                                               
         USING RSTELD,R5                                                        
ATXPR40  MVC   ATXPRF1,RSTFILT1    ACCOUNT FILTER 1                             
         TR    ATXPRF1,MXTRT                                                    
         MVC   ATXPRF2,RSTFILT2    ACCOUNT FILTER 2                             
         TR    ATXPRF2,MXTRT                                                    
         MVC   ATXPRF3,RSTFILT3    ACCOUNT FILTER 3                             
         TR    ATXPRF3,MXTRT                                                    
         MVC   ATXPRF4,RSTFILT4    ACCOUNT FILTER 4                             
         TR    ATXPRF4,MXTRT                                                    
         MVC   ATXPRF5,RSTFILT5    ACCOUNT FILTER 5                             
         TR    ATXPRF5,MXTRT                                                    
*                                                                               
         MVI   ATXPRIL,C'Y'                                                     
         TM    RSTSTAT1,RSTSACIL   ACCOUNT IS LOCKED                            
         JO    *+8                                                              
         MVI   ATXPRIL,C'N'                                                     
*&&US                                                                           
         MVI   ATXPRENP,C'Y'       ASSUME THERE ISN'T ANY BIT SET               
         CLI   RSTLN,RSTLN2Q       DO WE EVEN HAVE A STAT6?                     
         JNH   ATXPR20             NO-DONT BOTHER TESTING                       
         MVI   ATXPRENP,C'N'       ASSUME NOT TO EXCLUDE                        
         TM    RSTSTAT6,RSTSIN@T   INCLUDE CLIENT?                              
         JO    *+8                                                              
         MVI   ATXPRENP,C'Y'                                                    
*&&                                                                             
*&&UK                                                                           
         MVI   ATXPRENP,C'N'       ASSUME NOT TO EXCLUDE                        
         CLI   RSTLN,RSTLN2Q       DO WE EVEN HAVE A STAT6?                     
         JNH   ATXPR20             NO-DONT BOTHER TESTING                       
         TM    RSTSTAT6,RSTSXFNP   EXCLUDE CLIENT?                              
         JNO   *+8                                                              
         MVI   ATXPRENP,C'Y'       EXCLUDE IF SET                               
*&&                                                                             
         TR    ATXPRENP,MXTRT                                                   
         J     ATXPR20                                                          
*                                                                               
         USING PPRELD,R5                                                        
ATXPR50  MVC   ATXPROFC,PPRGAOFF   OFFICE CODE                                  
         TR    ATXPROFC,MXTRT                                                   
         MVC   ATXPRRAC,PPRRECVU   RECEIVABLES ACCOUNT                          
         TR    ATXPRRAC,MXTRT                                                   
         MVC   ATXPRCAC,PPRCOSTU   COSTING ACCOUNT                              
         TR    ATXPRCAC,MXTRT                                                   
         MVC   ATXPRPOB,PPRBILLP   PRINT ON BILL                                
         TR    ATXPRPOB,MXTRT                                                   
         J     ATXPR20                                                          
*                                                                               
         USING ADRELD,R5                                                        
ATXPR60  DS    0H                                                               
         SR    R0,R0                                                            
         ICM   R0,1,ADRNUM         NUMBER OF ADDRESS LINES                      
         JZ    ATXPR20                                                          
         LA    RE,ATXPRBA1         POINT RE TO 1ST ADD LINE IN TABLE            
         LA    RF,ADRADD1          POINT RF TO 1ST ADD LINE IN ELM              
ATXPR70  MVC   0(L'ATXPRBA1,RE),0(RF)                                           
         TR    0(L'ATXPRBA1,RE),MXTRT                                           
         LA    RE,ATXPRBLQ(RE)                                                  
         LA    RF,L'ADRADD1(RF)                                                 
         BCT   R0,ATXPR70                                                       
         J     ATXPR20                                                          
*                                                                               
         USING AGRELD,R5                                                        
ATXPR80  MVC   ATXPRAG1,AGRAGR1                                                 
         TR    ATXPRAG1,MXTRT                                                   
         CLI   AGRLN,AGRLN1Q                                                    
         JE    ATXPR20                                                          
         MVC   ATXPRAG2,AGRAGR2                                                 
         TR    ATXPRAG2,MXTRT                                                   
         J     ATXPR20                                                          
*                                                                               
         USING PACELD,R5                                                        
ATXPR90  OC    PACDATE,PACDATE     DO WE HAVE A DATE?                           
         JZ    ATXPR20                                                          
*&&US*&& GOTO1 VDATCON,DMCB,(1,PACDATE),(23,ATXPRCDT),0                         
*&&UK*&& GOTO1 VDATCON,DMCB,(1,PACDATE),(10,ATXPRCDT),0                         
         J     ATXPR20                                                          
*                                                                               
ATXPRX   DS    0H                                                               
         J     EXIT                                                             
*                                                                               
ATXPRLIT DC    A(LITERAL)                                                       
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* ATXJOBC - EXTRACT JOB RECORDS FOR ATXTRACT                         *          
*                                                                    *          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(ACCFILE RECORD)                                              *          
* AL4 0=ACCFILE-TO-EXTRACT                                           *          
* AL4 A(SXDTABD) - CAN GET 2 BYTE ALPHA FROM THIS...                 *          
* AL4 A(LEVELS) - LENGTH OF INDIVIDUAL SUB LEVELS                    *          
**********************************************************************          
         SPACE 1                                                                
         PRINT GEN                                                              
ATXJOBC  NMOD1 WORKX-WORKD,*ATXJOB*,RR=RA                                       
         USING WORKD,RC                                                         
         L     RA,ATXJBLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         MVC   ALEVELS,16(R1)                                                   
         USING ATXRECD,R2          R2=A(EXTRACT RECORD)                         
         USING ACTRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDTABD)                                
*                                                                               
         CLI   8(R1),1                                                          
         JL    ATXJBKIL            BUILD K RECORD                               
         JE    ATXJBBLD            BUILD JOB AND USERFIELD RECORDS              
         JH    ATXJBNXT            OUTPUT NEXT RECORD                           
*                                                                               
ATXJOBCC DC    A(BUFFER1)          JOB SAVE AREA                                
ATXJBLIT DC    A(LITERAL)                                                       
         EJECT                                                                  
***********************************************************************         
* BUILD 'K' RECORD AND EXIT                                           *         
***********************************************************************         
ATXJBKIL BRAS  RE,ATXJBBLK                                                      
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* GET NEXT RECORD AND PASS IT BACK                                    *         
***********************************************************************         
         SPACE 1                                                                
ATXJBNXT L     RE,ANEXT                                                         
         OC    0(8,RE),0(RE)                                                    
         JNZ   *+12                                                             
         MVI   8(R1),X'FF'         NO MORE RECORDS                              
         J     EXIT                                                             
*                                                                               
         L     RE,ANEXT            MOVE IN NEXT RECORD                          
         LA    R0,ATXRECD                                                       
         LH    RF,=Y(ATXJOBL)                                                   
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LH    RF,=Y(ATXJOBL)      UPDATE POINTER                               
         L     RE,ANEXT                                                         
         AR    RE,RF                                                            
         ST    RE,ANEXT                                                         
         J     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* BUILD SERIES OF RECORDS FOR THIS RECORD                             *         
***********************************************************************         
         SPACE 1                                                                
ATXJBBLD BRAS  RE,ATXJBBLK         SET UP BLANK RECORD (KEY DETAIL)             
*                                                                               
         LH    RF,=Y(ATXJOBL)                                                   
         STH   RF,RECLEN                                                        
         L     R2,ATXJOBCC         WHERE RECORDS TO BE BUILT                    
         XC    0(8,R2),0(R2)                                                    
         ST    R2,ANEXT            FOR NEXT TIME IN                             
*                                                                               
         L     RE,AXREC            BLANK RECORD HERE                            
         LR    R0,R2                                                            
         LH    RF,RECLEN                                                        
         LR    R1,RF                                                            
         MVCL  R0,RE               COPY IN A BLANK RECORD                       
*                                                                               
         LH    RF,RECLEN                                                        
         AR    RF,R2                                                            
         XC    0(8,RF),0(RF)       CLEAR START OF NEXT DATA SPACE               
*                                                                               
         MVC   ATXJBCRT,SPACES             DEAFULT CREATOR TO SPACES            
*&&US*&& MVC   ATXJBCDT,=C'1990-01-01'   DEFAULT                                
*&&UK*&& MVC   ATXJBCDT,=C'01/01/90'     DEFAULT                                
         TR    ATXJBCDT,MXTRT                                                   
         LA    R5,ACTRFST          R5=A(FIRST ELEMENT)                          
ATXJB10  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    ATXJB180                                                         
         CLI   0(R5),NAMELQ        X'20' - NAME ELEMENT                         
         JE    ATXJB30                                                          
         CLI   0(R5),RSTELQ        X'30' - RECORD STAUS ELEMENT                 
         JE    ATXJB40                                                          
         CLI   0(R5),JOBELQ        X'26' - JOB ELEMENT                          
         JE    ATXJB60                                                          
         CLI   0(R5),PPRELQ        X'24' - PRODUCTION PROFILE ELEMENT           
         JE    ATXJB90                                                          
         CLI   0(R5),SCMELQ        X'3E' - STANDARD COMMENT ELEMENT             
         JE    ATXJB120                                                         
         CLI   0(R5),ADRELQ        X'22' - ADDRESS ELEMENT                      
         JE    ATXJB150                                                         
         CLI   0(R5),AGRELQ        X'FC' - ACC. GROUP ELEMENT                   
         JE    ATXJB170                                                         
ATXJB20  SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R5)                                                         
         AR    R5,RF                                                            
         J     ATXJB10                                                          
*                                                                               
         USING NAMELD,R5                                                        
ATXJB30  MVC   ATXJBNAM,SPACES                                                  
         SR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         JM    ATXJB20                                                          
         EX    RF,*+4                                                           
         MVC   ATXJBNAM(0),NAMEREC                                              
         TR    ATXJBNAM,MXTRT                                                   
         J     ATXJB20                                                          
*                                                                               
         USING RSTELD,R5                                                        
ATXJB40  MVC   ATXJBF1,RSTFILT1    ACCOUNT FILTER 1                             
         TR    ATXJBF1,MXTRT                                                    
         MVC   ATXJBF2,RSTFILT2    ACCOUNT FILTER 2                             
         TR    ATXJBF2,MXTRT                                                    
         MVC   ATXJBF3,RSTFILT3    ACCOUNT FILTER 3                             
         TR    ATXJBF3,MXTRT                                                    
         MVC   ATXJBF4,RSTFILT4    ACCOUNT FILTER 4                             
         TR    ATXJBF4,MXTRT                                                    
         MVC   ATXJBF5,RSTFILT5    ACCOUNT FILTER 5                             
         TR    ATXJBF5,MXTRT                                                    
*                                                                               
         MVI   ATXJBTSH,C'Y'                                                    
         CLI   RSTLN,RSTLN3Q       ENSURE ELEMENT LONG ENOUGH                   
         JL    ATXJB50                                                          
         TM    RSTSTAT5,RSTSNOTS   EXCLUDE FROM TEMPO                           
         JZ    ATXJB50                                                          
         MVI   ATXJBTSH,C'N'                                                    
         OI    JOBSTAT,ACTSLOCK    TREAT AS LOCKED                              
ATXJB50  EDIT  JOBSTAT,(3,ATXJBSTA),FILL=0,ZERO=NOBLANK                         
*                                                                               
*&&US                                                                           
         MVI   ATXJBENP,C'Y'       ASSUME THERE ISN'T ANY BIT SET               
         CLI   RSTLN,RSTLN2Q       DO WE EVEN HAVE A STAT6?                     
         JNH   ATXJB20             NO-DONT BOTHER TESTING                       
         MVI   ATXJBENP,C'N'       ASSUME NOT TO EXCLUDE                        
         TM    RSTSTAT6,RSTSIN@T   INCLUDE CLIENT?                              
         JO    *+8                                                              
         MVI   ATXJBENP,C'Y'                                                    
*&&                                                                             
*&&UK                                                                           
         MVI   ATXJBENP,C'N'       ASSUME NOT TO EXCLUDE                        
         CLI   RSTLN,RSTLN2Q       DO WE EVEN HAVE A STAT6?                     
         JNH   ATXJB20             NO-DONT BOTHER TESTING                       
         TM    RSTSTAT6,RSTSXFNP   EXCLUDE CLIENT?                              
         JNO   *+8                                                              
         MVI   ATXJBENP,C'Y'       EXCLUDE IF SET                               
*&&                                                                             
         TR    ATXJBENP,MXTRT                                                   
         J     ATXJB20                                                          
*                                                                               
         USING JOBELD,R5                                                        
ATXJB60  MVI   ATXJBCRT,C'I'       ALWAYS PREFIX WITH AN I                      
         MVC   ATXJBCRT+1(8),=CL8'UNKNOWN'                                      
         TR    ATXJBCRT,MXTRT                                                   
         MVC   ATXJBCDT,SPACES                                                  
         OC    JOBADATE,JOBADATE   DO WE HAVE A DATE?                           
         JZ    ATXJB65                                                          
*&&US*&& GOTO1 VDATCON,DMCB,(1,JOBADATE),(23,ATXJBCDT),0                        
*&&UK*&& GOTO1 VDATCON,DMCB,(1,JOBADATE),(10,ATXJBCDT),0                        
*                                                                               
ATXJB65  MVC   ATXJBOD,SPACES      OLD DATA ON SHORT ELEMENT                    
         CLI   JOBLN,JOBLN1Q       DOESN'T HOLD OPEN DATE                       
         JNH   ATXJB70                                                          
         OC    JOBODATE,JOBODATE   ANY DATE?                                    
         BZ    ATXJB70                                                          
*&&US*&& GOTO1 VDATCON,DMCB,(1,JOBODATE),(23,ATXJBOD),0                         
*&&UK*&& GOTO1 VDATCON,DMCB,(1,JOBODATE),(10,ATXJBOD),0                         
*                                                                               
ATXJB70  MVC   ATXJBCD,SPACES                                                   
         OC    JOBCDATE,JOBCDATE   ANY DATE?                                    
         BZ    ATXJB80                                                          
*&&US*&& GOTO1 VDATCON,DMCB,(1,JOBCDATE),(23,ATXJBCD),0                         
*&&UK*&& GOTO1 VDATCON,DMCB,(1,JOBCDATE),(10,ATXJBCD),0                         
*                                                                               
ATXJB80  MVI   ATXJBIRA,C'N'       IS RATE ADJUSTED                             
         MVI   ATXJBIWO,C'N'       IS WRITE OFF                                 
         CLI   JOBLN,JOBLN2Q       IS ELEMENT LONG ENOUGH?                      
         JNH   ATXJB20             NO -> EXIT                                   
         TM    JOBSTA1,JOBSART     RATE ADJUSTMENT?                             
         JZ    *+8                                                              
         MVI   ATXJBIRA,C'Y'                                                    
         TM    JOBSTA1,JOBSXJOB    WRITTEN OFF?                                 
         JZ    *+8                                                              
         MVI   ATXJBIWO,C'Y'                                                    
         J     ATXJB20                                                          
*                                                                               
         USING PPRELD,R5                                                        
ATXJB90  MVC   ATXJBOFC,PPRGAOFF                                                
         TR    ATXJBOFC,MXTRT                                                   
         MVC   ATXJBRAC,PPRRECVU   U/L/A                                        
         TR    ATXJBRAC,MXTRT                                                   
         MVC   ATXJBCAC,PPRCOSTU   U/L/A                                        
         TR    ATXJBCAC,MXTRT                                                   
         MVC   ATXJBPOB,PPRBILLP   PRINT ON BILL                                
         TR    ATXJBPOB,MXTRT                                                   
*                                                                               
         MVC   ATXJBOI1,SPACES     CLEAR OTHER INFO LINE 1                      
         TR    ATXJBOI1,MXTRT                                                   
         MVC   ATXJBOI2,SPACES     CLEAR OTHER INFO LINE 2                      
         TR    ATXJBOI2,MXTRT                                                   
         MVC   ATXJBOI3,SPACES     CLEAR OTHER INFO LINE 3                      
         TR    ATXJBOI3,MXTRT                                                   
         LA    R0,3                NUMBER OF OTHER INFO LINES                   
         LA    R1,ATXJBOI1         SET R1 AT 1ST LINE                           
         LA    R6,PPRNARRP         POINT R6 AT THE OTHER INFO FIELD             
         SR    RE,RE                                                            
         IC    RE,PPRLN                                                         
         SHI   RE,PPRLN1Q                                                       
         JNP   ATXJB20                                                          
         LA    RF,L'ATXJBOI1       SET RF TO LENGTH OF EACH LINE                
ATXJB100 CR    RE,RF                                                            
         JL    ATXJB110                                                         
         MVC   0(L'ATXJBOI1,R1),0(R6)                                           
         TR    0(L'ATXJBOI1,R1),MXTRT                                           
         SR    RE,RF               SUBTRACT LENGTH FROM TOTAL                   
         AR    R6,RF               BUMP INFO FIELD                              
         LA    R1,ATXJBOLQ(R1)                                                  
         BCT   R0,ATXJB100                                                      
         J     ATXJB20                                                          
*                                                                               
ATXJB110 SHI   RE,1                                                             
         JNP   ATXJB20                                                          
         EX    RE,*+4                                                           
         MVC   0(0,R1),0(R6)    OTHER INFO                                      
         TR    0(L'ATXJBOI1,R1),MXTRT                                           
         J     ATXJB20                                                          
*                                                                               
         USING SCMELD,R5                                                        
ATXJB120 CLI   SCMSEQ,X'00'        COMMENTS 1                                   
         JNE   ATXJB130                                                         
         XC    ATXJBCL1,ATXJBCL1   SET ZERO AS THE DEFAULT                      
         SR    RE,RE                                                            
         IC    RE,SCMLN                                                         
         SH    RE,=Y(SCMLN1Q)                                                   
         JNH   ATXJB20                                                          
         STCM  RE,3,ATXJBCL1                                                    
         BCTR  RE,0                                                             
         EX    RE,*+4                                                           
         MVC   ATXJBCM1(0),SCMNARR                                              
         TR    ATXJBCM1,MXTRT                                                   
         J     ATXJB20                                                          
*                                                                               
ATXJB130 CLI   SCMSEQ,X'01'       COMMENTS 2                                    
         JNE   ATXJB140                                                         
         XC    ATXJBCL2,ATXJBCL2   SET ZERO AS THE DEFAULT                      
         SR    RE,RE                                                            
         IC    RE,SCMLN                                                         
         SH    RE,=Y(SCMLN1Q)                                                   
         JNH   ATXJB20                                                          
         STCM  RE,3,ATXJBCL2                                                    
         BCTR  RE,0                                                             
         EX    RE,*+4                                                           
         MVC   ATXJBCM2(0),SCMNARR                                              
         TR    ATXJBCM2,MXTRT                                                   
         J     ATXJB20                                                          
*                                                                               
ATXJB140 CLI   SCMSEQ,X'02'       COMMENTS 3                                    
         JNE   ATXJB20                                                          
         XC    ATXJBCL3,ATXJBCL3   SET ZERO AS THE DEFAULT                      
         SR    RE,RE                                                            
         IC    RE,SCMLN                                                         
         SH    RE,=Y(SCMLN1Q)                                                   
         JNH   ATXJB20                                                          
         STCM  RE,3,ATXJBCL3                                                    
         BCTR  RE,0                                                             
         EX    RE,*+4                                                           
         MVC   ATXJBCM3(0),SCMNARR                                              
         TR    ATXJBCM3,MXTRT                                                   
         J     ATXJB20                                                          
*                                                                               
         USING ADRELD,R5                                                        
ATXJB150 DS    0H                  ADDRESS ELEMENT                              
         MVC   ATXJBBA5,SPACES     ONLY 5TH ADDRESS LINE IN UK FOR NOW          
         SR    R0,R0                                                            
         ICM   R0,1,ADRNUM         NUMBER OF ADDRESS LINES                      
         BZ    ATXJB20                                                          
         LA    RF,ADRADD1          POINT TO 1ST ELEMENT LINE                    
         LA    RE,ATXJBBA1         POINT TO FIRST TABLE LINE                    
ATXJB160 MVC   0(L'ATXJBBA1,RE),0(RF)   MOVE IN CURRENT LINE                    
         TR    0(L'ATXJBBA1,RE),MXTRT                                           
         LA    RE,ATXJBBLQ(RE)     SKIP TO NEXT LINE IN TAB                     
         LA    RF,L'ADRADD1(RF)    SKIP TO NEXT LINE IN ELM                     
         BCT   R0,ATXJB160                                                      
         J     ATXJB20                                                          
*                                                                               
         USING AGRELD,R5                                                        
ATXJB170 MVC   ATXJBAG1,AGRAGR1                                                 
         TR    ATXJBAG1,MXTRT                                                   
         CLI   AGRLN,AGRLN1Q                                                    
         JE    ATXJB20                                                          
         MVC   ATXJBAG2,AGRAGR2                                                 
         TR    ATXJBAG2,MXTRT                                                   
         J     ATXJB20                                                          
*                                                                               
ATXJB180 L     R2,ATXJOBCC         WHERE RECORDS TO BE BUILT                    
         AH    R2,RECLEN           NEXT SLOT                                    
*                                                                               
         LH    RF,RECLEN                                                        
         AR    RF,R2                                                            
         XC    0(8,RF),0(RF)       CLEAR START OF NEXT DATA SPACE               
*                                                                               
         LA    R5,ACTRFST          R5=A(FIRST ELEMENT)                          
ATXJB190 CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    ATXJBEX                                                          
         CLI   0(R5),UFSELQ        USERFIELD ELEMENT                            
         JE    ATXJB210                                                         
*                                                                               
ATXJB200 SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R5)                                                         
         AR    R5,RF                                                            
         J     ATXJB190                                                         
*                                                                               
         USING UFSELD,R5                                                        
ATXJB210 DS    0H                                                               
         BRAS  RE,ATXJUBLK         SET UP BLANK RECORD (KEY DETAIL)             
*                                                                               
         MVC   ATXJUFLD,UFSCODE    USER FIELD CODE                              
         TR    ATXJUFLD,MXTRT                                                   
*                                                                               
         XC    ATXJUCON,ATXJUCON   CLEAR FIELD                                  
         SR    R1,R1                                                            
         IC    R1,UFSLN                                                         
         SHI   R1,UFSLN1Q                                                       
         JNP   ATXJB220                                                         
         CHI   R1,L'ATXJUCON       MAKE SURE LENGTH ISN'T TOO BIG               
         JNH   *+8                                                              
         LA    R1,L'ATXJUCON       ELSE SET LENGTH FOR FIELD                    
         BCTR  R1,0                                                             
         EX    R1,*+4                                                           
         MVC   ATXJUCON(0),UFSDATA                                              
ATXJB220 TR    ATXJUCON,MXTRT                                                   
         AH    R2,RECLEN           NEXT SLOT                                    
*                                                                               
         LH    RF,RECLEN                                                        
         AR    RF,R2                                                            
         XC    0(8,RF),0(RF)       CLEAR START OF NEXT DATA SPACE               
*                                                                               
         J     ATXJB200                                                         
*                                                                               
ATXJBEX  DS    0H                                                               
         J     EXIT                                                             
         DROP  R5                                                               
         EJECT                                                                  
***********************************************************************         
* BUILD BLANK RECORD AND INPUT KEY FIELDS-JOB RECORD                  *         
***********************************************************************         
         SPACE 1                                                                
ATXJBBLK NTR1                                                                   
         MVC   SVACT,ATXREACT      SAVE ACTION                                  
         MVC   SVDAT,ATXREDAT      SAVE DATE                                    
         MVC   SVTIM,ATXRETIM      SAVE TIME                                    
         XC    ATXRELEN,ATXRELEN   RECORD LENGTH                                
         MVC   ATXRELEN(2),=Y(ATXJOBL)                                          
         MVC   ATXRETYP,ATXJOBQ    RECORD TYPE                                  
         MVI   ATXJBAID-1,MXTRTQ                                                
         MVI   ATXJBCLI-1,MXTRTQ                                                
         MVI   ATXJBPRO-1,MXTRTQ                                                
         MVI   ATXJBJOB-1,MXTRTQ                                                
         MVI   ATXJBNAM-1,MXTRTQ                                                
         MVI   ATXJBF1-1,MXTRTQ                                                 
         MVI   ATXJBF2-1,MXTRTQ                                                 
         MVI   ATXJBF3-1,MXTRTQ                                                 
         MVI   ATXJBF4-1,MXTRTQ                                                 
         MVI   ATXJBF5-1,MXTRTQ                                                 
         MVI   ATXJBOD-1,MXTRTQ                                                 
         MVI   ATXJBCD-1,MXTRTQ                                                 
         MVI   ATXJBSTA-1,MXTRTQ                                                
         MVI   ATXJBIRA-1,MXTRTQ                                                
         MVI   ATXJBIWO-1,MXTRTQ                                                
         MVI   ATXJBOFC-1,MXTRTQ                                                
         MVI   ATXJBCAC-1,MXTRTQ                                                
         MVI   ATXJBRAC-1,MXTRTQ                                                
         MVI   ATXJBAG1-1,MXTRTQ                                                
         MVI   ATXJBAG2-1,MXTRTQ                                                
         MVI   ATXJBBA1-1,MXTRTQ                                                
         MVI   ATXJBBA2-1,MXTRTQ                                                
         MVI   ATXJBBA3-1,MXTRTQ                                                
         MVI   ATXJBBA4-1,MXTRTQ                                                
         MVI   ATXJBPOB-1,MXTRTQ                                                
         MVI   ATXJBOI1-1,MXTRTQ                                                
         MVI   ATXJBOI2-1,MXTRTQ                                                
         MVI   ATXJBOI3-1,MXTRTQ                                                
         MVI   ATXJBTSH-1,MXTRTQ                                                
         MVI   ATXJBENP-1,MXTRTQ                                                
         MVI   ATXJBCL1-1,MXTRTQ   COMMENT FIELD LENGTH 1                       
         MVI   ATXJBCL2-1,MXTRTQ   COMMENT FIELD LENGTH 2                       
         MVI   ATXJBCL3-1,MXTRTQ   COMMENT FIELD LENGTH 3                       
         MVI   ATXJBCRT-1,MXTRTQ   CREATOR                                      
         MVI   ATXJBCDT-1,MXTRTQ   CREATION DATE                                
         MVI   ATXJOBX,MXTRTQ                                                   
         MVI   ATXJOBX+1,MXTRTQ                                                 
*                                                                               
         MVC   ATXJBAID,SXDTAGY    2-BYTE AGY ALPHA                             
         TR    ATXJBAID,MXTRT                                                   
*                                                                               
         MVC   JOBSTAT,ACTRSTAT    SAVE STATUS FOR LATER                        
*                                                                               
         MVC   ATXJBCLI,SPACES                                                  
         L     R6,ALEVELS          CLIENT                                       
         SR    RF,RF                                                            
         IC    RF,0(R6)                                                         
         BCTR  RF,0                                                             
         EX    RF,*+4                                                           
         MVC   ATXJBCLI(0),ACTKACT                                              
         TR    ATXJBCLI,MXTRT                                                   
*                                                                               
         MVC   ATXJBPRO,SPACES                                                  
         LA    RF,ACTKACT+1(RF)                                                 
         SR    RE,RE               PRODUCT                                      
         IC    RE,1(R6)                                                         
         BCTR  RE,0                                                             
         EX    RE,*+4                                                           
         MVC   ATXJBPRO(0),0(RF)                                                
         TR    ATXJBPRO,MXTRT                                                   
*                                                                               
         MVC   ATXJBJOB,SPACES                                                  
         LA    RF,1(RE,RF)         JOB                                          
         IC    RE,2(R6)                                                         
         BCTR  RE,0                                                             
         EX    RE,*+4                                                           
         MVC   ATXJBJOB(0),0(RF)                                                
         TR    ATXJBJOB,MXTRT                                                   
*                                                                               
*&&US                                                                           
         MVC   ATXJBCL1,=X'0001'   SET DEFAULTS FOR COMMENTS                    
         MVC   ATXJBCM1,SPACES                                                  
         TR    ATXJBCM1,MXTRT                                                   
         MVC   ATXJBCL2,=X'0001'                                                
         MVC   ATXJBCM2,SPACES                                                  
         TR    ATXJBCM2,MXTRT                                                   
         MVC   ATXJBCL3,=X'0001'                                                
         MVC   ATXJBCM3,SPACES                                                  
         TR    ATXJBCM3,MXTRT                                                   
*&&                                                                             
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* BUILD BLANK RECORD AND INPUT KEY FIELDS-JOB USERFIELD RECORD        *         
***********************************************************************         
         SPACE 1                                                                
ATXJUBLK NTR1                                                                   
         XC    ATXRELEN,ATXRELEN   RECORD LENGTH                                
         MVC   ATXRELEN(2),=Y(ATXJUFL)                                          
         MVC   ATXRETYP,ATXJUFQ    RECORD TYPE                                  
         MVC   ATXREACT,SVACT      ACTION                                       
         MVC   ATXREDAT,SVDAT      DATE                                         
         MVC   ATXRETIM,SVTIM      TIME                                         
         MVI   ATXJUAID-1,MXTRTQ                                                
         MVI   ATXJUCLI-1,MXTRTQ                                                
         MVI   ATXJUPRO-1,MXTRTQ                                                
         MVI   ATXJUJOB-1,MXTRTQ                                                
         MVI   ATXJUFLD-1,MXTRTQ                                                
         MVI   ATXJUCON-1,MXTRTQ                                                
         MVI   ATXJUFX,MXTRTQ                                                   
         MVI   ATXJUFX+1,MXTRTQ                                                 
*                                                                               
         MVC   ATXJUAID,SXDTAGY    2-BYTE AGY ALPHA                             
         TR    ATXJUAID,MXTRT                                                   
*                                                                               
         MVC   ATXJUCLI,SPACES                                                  
         L     R6,ALEVELS          CLIENT                                       
         SR    RF,RF                                                            
         IC    RF,0(R6)                                                         
         BCTR  RF,0                                                             
         EX    RF,*+4                                                           
         MVC   ATXJUCLI(0),ACTKACT                                              
         TR    ATXJUCLI,MXTRT                                                   
*                                                                               
         MVC   ATXJUPRO,SPACES                                                  
         LA    RF,ACTKACT+1(RF)                                                 
         SR    RE,RE               PRODUCT                                      
         IC    RE,1(R6)                                                         
         BCTR  RE,0                                                             
         EX    RE,*+4                                                           
         MVC   ATXJUPRO(0),0(RF)                                                
         TR    ATXJUPRO,MXTRT                                                   
*                                                                               
         MVC   ATXJUJOB,SPACES                                                  
         LA    RF,1(RE,RF)         JOB                                          
         IC    RE,2(R6)                                                         
         BCTR  RE,0                                                             
         EX    RE,*+4                                                           
         MVC   ATXJUJOB(0),0(RF)                                                
         TR    ATXJUJOB,MXTRT                                                   
         J     EXIT                                                             
         DROP  R2,R3,R4                                                         
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ATXMEDC - EXTRACT MEDIA RECORDS FOR ATXTRACT                        *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
         USING ATXRECD,R2          R2=A(EXTRACT RECORD)                         
         USING PMDRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTABD)                               
ATXMEDC  NMOD1 WORKX-WORKD,*ATXME*,RR=RA                                        
         USING WORKD,RC                                                         
         L     RA,ATXMDLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
*                                                                               
         XC    ATXRELEN,ATXRELEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   ATXRELEN(2),=Y(ATXMEDL)                                          
         MVC   ATXRETYP,ATXMEDQ                                                 
         MVI   ATXMDAID-1,MXTRTQ                                                
         MVI   ATXMDMCD-1,MXTRTQ                                                
         MVI   ATXMDMDS-1,MXTRTQ                                                
         MVI   ATXMDGRP-1,MXTRTQ                                                
         MVI   ATXMNAM-1,MXTRTQ                                                 
         MVC   ATXMDAN1-1,MXTRTQ                                                
         MVI   ATXMDIXA-1,MXTRTQ                                                
         MVI   ATXMDV1A-1,MXTRTQ                                                
         MVI   ATXMDFNM-1,MXTRTQ                                                
         MVI   ATXMDFLN-1,MXTRTQ                                                
         MVC   ATXMDAN2-1,MXTRTQ                                                
         MVI   ATXMDIIA-1,MXTRTQ   UNKNOWN IN US                                
         MVI   ATXMDV2A-1,MXTRTQ   UNKNOWN IN US                                
         MVI   ATXMEDX,MXTRTQ                                                   
         MVI   ATXMEDX-1,MXTRTQ                                                 
*                                                                               
         MVC   ATXAGAID,SXDTAGY    ALPHA AGENCY ID CODE                         
         TR    ATXAGAID,MXTRT                                                   
         MVC   ATXMDMCD,PMDKMED    MEDIA CODE                                   
         TR    ATXMDMCD,MXTRT                                                   
*                                                                               
         SR    RF,RF                                                            
         LA    R5,PMDRFST          R5=A(FIRST ELEMENT)                          
ATXMD10  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),PMDELQ                                                     
         JE    ATXMD30                                                          
         CLI   0(R5),XNMELQ                                                     
         JE    ATXMD40                                                          
         CLI   0(R5),MNAELQ                                                     
         JE    ATXMD70                                                          
*                                                                               
ATXMD20  SR    RF,RF                                                            
         IC    RF,1(R5)                                                         
         AR    R5,RF                                                            
         J     ATXMD10                                                          
*                                                                               
         USING PMDELD,R5                                                        
ATXMD30  MVC   ATXMDMDS,PMDDESC    MEDIA DESCRIPTION                            
         TR    ATXMDMDS,MXTRT                                                   
         MVC   ATXMDGRP,PMDGRP     MEDIA GROUP                                  
         TR    ATXMDGRP,MXTRT                                                   
         MVC   ATXMDAN1,PMDANAL    ANALYSIS CODE                                
         TR    ATXMDAN1,MXTRT                                                   
         MVC   ATXMDIXA,PMDCOMU1   COMMISSION ACCOUNT CODE FOR BILLS            
         TR    ATXMDIXA,MXTRT                                                   
         MVC   ATXMDV1A,PMDVATU1   VAT ACCOUNT FOR BILLS                        
         TR    ATXMDV1A,MXTRT                                                   
*                                                                               
         XC    ATXMDIIA,ATXMDIIA   INTERNAL INCOME ACCOUNT                      
         XC    ATXMDV2A,ATXMDV2A   VAT 2 ACCOUNT FOR NON VATABLE CLIENT         
         XC    ATXMDAN2,ATXMDAN2   CLEAR 2ND ANALYSIS CODE                      
         CLI   PMDLN,PMDLN1Q                                                    
         TR    ATXMDIIA,MXTRT                                                   
         TR    ATXMDV2A,MXTRT                                                   
         TR    ATXMDAN2,MXTRT                                                   
         J     ATXMD20                                                          
*                                                                               
         USING XNMELD,R5                                                        
ATXMD40  ZIC   RF,XNMSUBL                                                       
         LTR   RF,RF                                                            
         JZ    ATXMD20                                                          
         SHI   RF,2                                                             
         EX    RF,*+4                                                           
         MVC   ATXMDFNM(0),XNMSUBN                                              
         TR    ATXMDFNM,MXTRT                                                   
*                                                                               
         LA    RF,LANGTAB0                                                      
ATXMD50  CLI   0(RF),X'FF'         END                                          
         BNE   *+14                                                             
         MVC   ATXMDFLN,=CL13'ENGLISH'                                          
         J     ATXMD20                                                          
         CLC   XNMSTAT,0(RF)                                                    
         JE    ATXMD60                                                          
         AH    RF,LANGTAB                                                       
         J     ATXMD50                                                          
ATXMD60  MVC   ATXMDFLN,27(RF)                                                  
         J     ATXMD20                                                          
*                                                                               
         USING MNAELD,R5                                                        
ATXMD70  XC    ATXMNAM,ATXMNAM     CLEAR NAME FIELD (LONG)                      
         SR    RE,RE                                                            
         IC    RE,MNALN                                                         
         SHI   RE,MNAME-MNAELD     SUBTRACT OVERHEAD                            
         JZ    ATXMD20                                                          
         BCTR  RE,0                                                             
         EX    RE,*+4                                                           
         MVC   ATXMNAM(0),MNAME                                                 
         J     ATXMD20                                                          
         DROP  R2,R3,R4,R5                                                      
*                                                                               
ATXMDLIT DC    A(LITERAL)                                                       
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ATXMGRC - EXTRACT MEDIA GROUP RECORDS FOR ATXTRACT                  *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
         USING ATXRECD,R2          R2=A(EXTRACT RECORD)                         
         USING MGRRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
ATXMGRC  NMOD1 WORKX-WORKD,*ATXMG*,RR=RA                                        
         USING WORKD,RC                                                         
         L     RA,ATXMGLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         MVC   ALEVELS,16(R1)                                                   
*                                                                               
         XC    ATXRELEN,ATXRELEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   ATXRELEN(2),=Y(ATXMDGL)                                          
         MVC   ATXRETYP,ATXMGRQ                                                 
         MVI   ATXMGAID-1,MXTRTQ                                                
         MVI   ATXMGGC-1,MXTRTQ                                                 
         MVI   ATXMGNAM-1,MXTRTQ                                                
         MVI   ATXMDGX,MXTRTQ                                                   
         MVI   ATXMDGX-1,MXTRTQ                                                 
*                                                                               
         MVC   ATXMGAID,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         MVC   ATXMGGC,MGRKCODE    MEDIA GROUP                                  
         TR    ATXMGGC,MXTRT                                                    
*                                                                               
         LA    R5,MGRRFST          R5=A(FIRST ELEMENT)                          
ATXMG10  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),NAMELQ        X'20' - NAME ELEMENT                         
         JE    ATXMG20                                                          
         SR    RF,RF                                                            
         IC    RF,1(R5)                                                         
         AR    R5,RF                                                            
         J     ATXMG10                                                          
*                                                                               
         USING NAMELD,R5                                                        
ATXMG20  DS    0H                                                               
         MVC   ATXMGNAM,SPACES      MEDIA GROUP NAME                            
         SR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         JM    EXIT                                                             
         EX    RF,*+4                                                           
         MVC   ATXMGNAM(0),NAMEREC                                              
         TR    ATXMGNAM,MXTRT                                                   
         J     EXIT                                                             
         DROP  R2,R3,R4,R5                                                      
*                                                                               
ATXMGLIT DC    A(LITERAL)                                                       
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
*********************************************************************           
* ATXTSKC - EXTRACT TASK CODE RECORDS FOR ATXTRACT                  *           
*                                                                   *           
* AL4 A(EXTRACT RECORD)                                             *           
* AL4 A(ACCMST RECORD)                                              *           
* AL4 0=ACCFILE-TO-EXTRACT                                          *           
* AL4 A(SXDTABD) - CAN GET 2 BYTE ALPHA FROM THIS...                *           
* AL4 A(LEVELS) - LENGTH OF INDIVIDUAL SUB LEVELS                   *           
*********************************************************************           
         SPACE 1                                                                
ATXTSKC  NMOD1 WORKX-WORKD,*ATXTK*,RR=RA                                        
         USING WORKD,RC                                                         
         L     RA,ATXTKLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         MVC   ALEVELS,16(R1)                                                   
         USING ATXRECD,R2          R2=A(EXTRACT RECORD)                         
         USING WCORECD,R3                                                       
         USING SXDTABD,R4                                                       
*                                                                               
         XC    ATXRELEN,ATXRELEN                                                
         MVC   ATXRELEN(2),=AL2(ATXWKCL)                                        
         MVC   ATXRETYP,ATXWRKQ                                                 
         MVI   ATXWKAID-1,MXTRTQ                                                
         MVI   ATXWKLDG-1,MXTRTQ                                                
         MVI   ATXWKWRK-1,MXTRTQ                                                
         MVI   ATXWKDES-1,MXTRTQ                                                
         MVI   ATXWKIC-1,MXTRTQ                                                 
         MVI   ATXWKIA-1,MXTRTQ                                                 
         MVI   ATXWKIRA-1,MXTRTQ                                                
         MVI   ATXWKIFT-1,MXTRTQ                                                
         MVI   ATXWKWGR-1,MXTRTQ                                                
         MVI   ATXWKNAM-1,MXTRTQ                                                
         MVI   ATXWKINT-1,MXTRTQ                                                
         MVI   ATXWKEXT-1,MXTRTQ                                                
         MVI   ATXWKCX,MXTRTQ                                                   
         MVI   ATXWKCX-1,MXTRTQ                                                 
*                                                                               
         MVC   ATXWKAID,SXDTAGY                                                 
         TR    ATXWKAID,MXTRT                                                   
         MVC   ATXWKLDG,WCOKUNT                                                 
         TR    ATXWKLDG,MXTRT                                                   
*                                                                               
         LA    R5,WCORFST          R5=A(FIRST ELEMENT)                          
ATXTK10  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),WCOELQ        X'12' - WORKCODE ELEMENT                     
         JE    ATXTK30                                                          
         CLI   0(R5),NAMELQ        X'20' - NAME ELEMENT                         
         JE    ATXTK40                                                          
*                                                                               
ATXTK20  SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R5)                                                         
         AR    R5,RF                                                            
         J     ATXTK10                                                          
*                                                                               
         USING WCOELD,R5                                                        
ATXTK30  MVC   ATXWKWRK,WCOCODE                                                 
         MVC   ATXWKDES,WCODESC                                                 
*                                                                               
         MVC   ATXWKWGR,WCOGRP     WORK CODE GROUP                              
         CLI   ATXWKWGR,0                                                       
         BNE   *+8                                                              
         MVI   ATXWKWGR,C' '                                                    
*                                                                               
         MVI   ATXWKIC,C'N'        SET AS NON-COMMISSIONABLE (DEFAULT)          
         MVI   ATXWKIFT,C'N'       SET AS NOT FOR TIME (DEFAULT)                
*&&US                                                                           
         TM    WCOSTAT,WCOSNONC    IS IT NON-COMMISSIONABLE                     
         BO    *+8                                                              
         MVI   ATXWKIC,C'Y'        SET AS COMMISSIONABLE                        
*&&                                                                             
         TM    WCOSTAT,WCOSHCOE    TIME TYPE WORKCODE                           
         BNO   *+8                                                              
         MVI   ATXWKIFT,C'Y'       SET AS IS FOR TIME                           
*                                                                               
         MVI   ATXWKIA,C'N'        SET AS INACTIVE (DEFAULT)                    
         MVI   ATXWKIRA,C'N'       SET AS RATE IS NOT ADJ (DEFAULT)             
*&&US                                                                           
         TM    WCOSTAT2,WCOSINA    RATE IS INACTIVE                             
         BNZ   *+8                                                              
         MVI   ATXWKIA,C'Y'        SET AS ACTIVE                                
         TM    WCOSTAT2,WCOSART    RATE ELIGIBLE FOR ADJUSTMENT                 
         BZ    *+8                                                              
         MVI   ATXWKIRA,C'Y'       SET AS RATE IS ADJ                           
*&&                                                                             
*                                                                               
         MVI   ATXWKINT,X'40'      SET AS SPACE                                 
*&&US                                                                           
         MVI   ATXWKINT,C'N'       W.C. IS INTERNAL (DEFAULT)                   
         MVI   ATXWKEXT,C'N'       W.C. IS EXTERNAL (DEFAULT)                   
         TM    WCOSTAT2,WCOSINC                                                 
         BNO   *+12                                                             
         MVI   ATXWKINT,C'Y'       W.C. IS INTERNAL                             
         J     *+8                                                              
         MVI   ATXWKEXT,C'Y'       W.C. IS EXTERNAL                             
*&&                                                                             
         J     ATXTK20                                                          
*                                                                               
         USING NAMELD,R5                                                        
ATXTK40  MVC   ATXWKNAM,SPACES     WORK CODE EXTRA NAME                         
         SR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         BM    ATXTK20                                                          
         EX    RF,*+4                                                           
         MVC   ATXWKNAM(0),NAMEREC                                              
         TR    ATXWKNAM,MXTRT                                                   
         J     ATXTK20                                                          
         DROP  R2,R3,R4,R5                                                      
*                                                                               
ATXTKLIT DC    A(LITERAL)                                                       
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
*********************************************************************           
* ATXSTKC- EXTRACT SUB TASK CODE RECORDS FOR ATXTRACT               *           
*                                                                   *           
* AL4 A(EXTRACT RECORD)                                             *           
* AL4 A(ACCMST RECORD)                                              *           
* AL4 0=ACCFILE-TO-EXTRACT                                          *           
* AL4 A(SXDTABD) - CAN GET 2 BYTE ALPHA FROM THIS...                *           
* AL4 A(LEVELS) - LENGTH OF INDIVIDUAL SUB LEVELS                   *           
* AL4 A(SUBREC) - POSITION OF SUBREC WITHIN ACCMST REC              *           
*********************************************************************           
         SPACE 1                                                                
ATXSTKC  NMOD1 WORKX-WORKD,*ATXSTK*,RR=RA                                       
         USING WORKD,RC                                                         
         L     RA,ATXSTKLT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING ATXRECD,R2           R2=A(EXTRACT RECORD)                        
         USING SXDTABD,R4                                                       
         USING WCORECD,R3                                                       
         MVC   ALEVELS,16(R1)                                                   
*                                                                               
         XC    ATXRELEN,ATXRELEN                                                
         MVC   ATXRELEN(2),=AL2(ATXSWCL)                                        
         MVC   ATXRETYP,ATXSWKQ                                                 
         MVI   ATXSWAID-1,MXTRTQ                                                
         MVI   ATXSWLDG-1,MXTRTQ                                                
         MVI   ATXSWCWC-1,MXTRTQ                                                
         MVI   ATXSWCRF-1,MXTRTQ                                                
         MVI   ATXSWCLG-1,MXTRTQ                                                
         MVI   ATXSWCDS-1,MXTRTQ                                                
         MVI   ATXSWCX,MXTRTQ                                                   
         MVI   ATXSWCX-1,MXTRTQ                                                 
*                                                                               
         MVC   ATXSWAID,SXDTAGY                                                 
         TR    ATXSWAID,MXTRT                                                   
         MVC   ATXSWLDG,WCOKUNT                                                 
         TR    ATXSWLDG,MXTRT                                                   
*                                                                               
         L     R6,20(R1)           R6=A(SUB RECORD)                             
         USING FFTELD,R6                                                        
*                                                                               
         LA    R5,WCORFST                                                       
AXSTK10  CLI   0(R5),0             END OF RECORD                                
         JNE   *+6                                                              
         DC    H'0'                                                             
         CLI   0(R5),WCOELQ        X'12' - WORK CODE ELEMENT                    
         JE    AXSTK20                                                          
*                                                                               
         SR    R1,R1               BUMP TO NEXT ELEMENT                         
         IC    R1,1(R5)                                                         
         AR    R5,R1                                                            
         J     AXSTK10                                                          
*                                                                               
         USING WCOELD,R5                                                        
AXSTK20  MVC   ATXSWCWC,WCOCODE                                                 
         TR    ATXSWCWC,MXTRT                                                   
         CLI   FFTEL,FFTELQ                                                     
         JE    *+6                                                              
         DC    H'0'                SUB RECORD SHOULD BE FFTEL                   
         MVC   ATXSWCRF,SPACES                                                  
         MVI   BYTE,0              BYTE-TEMP STORAGE AREA FOR SEQ#              
         MVC   BYTE,FFTSEQ                                                      
         MVC   ATXSWCLG,=C'ENG'                                                 
         TM    FFTSEQ,X'80'        GERMAN ?                                     
         JNO   *+14                                                             
         NI    BYTE,X'FF'-X'80'                                                 
         MVC   ATXSWCLG,=C'GER'                                                 
         TR    ATXSWCLG,MXTRT                                                   
         EDIT  BYTE,ATXSWCRF                                                    
         CLC   ATXSWCRF,SPACES     DO WE HAVE A NUMBER?                         
         BNH   *+16                NO-DONT BOTHER DOING THE REST                
         CLI   ATXSWCRF,X'40'      IS THE FIRST BYTE A VALID NUMBER?            
         BH    *+8                                                              
         MVI   ATXSWCRF,C'0'       FORCE IT TO BE A CHAR ZERO                   
         TR    ATXSWCRF,MXTRT                                                   
         MVC   ATXSWCDS,SPACES                                                  
         SR    RF,RF                                                            
         IC    RF,FFTDLEN                                                       
         BCTR  RF,0                                                             
         EX    RF,*+4                                                           
         MVC   ATXSWCDS(0),FFTDATA                                              
         TR    ATXSWCDS,MXTRT                                                   
         J     EXIT                                                             
         DROP  R2,R3,R4,R5,R6                                                   
*                                                                               
ATXSTKLT DC    A(LITERAL)                                                       
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ATXTGRC - EXTRACT TASK CODE GROUP RECORDS FOR ATXTRACT              *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
         USING ATXRECD,R2          R2=A(EXTRACT RECORD)                         
         USING WGRRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTABD)                               
ATXTGRC  NMOD1 WORKX-WORKD,*ATXWG*,RR=RA                                        
         USING WORKD,RC                                                         
         L     RA,ATXWGLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
*                                                                               
         XC    ATXRELEN,ATXRELEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   ATXRELEN(2),=Y(ATXWCGL)                                          
         MVC   ATXRETYP,ATXWGRQ                                                 
         MVI   ATXWGAID-1,MXTRTQ                                                
         MVI   ATXWGLDG-1,MXTRTQ                                                
         MVI   ATXWGCDE-1,MXTRTQ                                                
         MVI   ATXWGNAM-1,MXTRTQ                                                
         MVI   ATXWCGX,MXTRTQ                                                   
         MVI   ATXWCGX+1,MXTRTQ                                                 
*                                                                               
         MVC   ATXWGAID,SXDTAGY    2-BYTE AGY ALPHA                             
         MVC   ATXWGLDG,WGRKUNT    UNIT/LEDGER                                  
         MVC   ATXWGCDE,WGRKCODE   WORKCODE GROUP                               
*                                                                               
         LA    R5,WGRRFST          R5=A(FIRST ELEMENT)                          
ATXTG10  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),NAMELQ                                                     
         JE    ATXTG30                                                          
*                                                                               
ATXTG20  SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R5)                                                         
         AR    R5,RF                                                            
         J     ATXTG10                                                          
*                                                                               
         USING NAMELD,R5                                                        
ATXTG30  SR    RF,RF               TASK LONG NAME                               
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         JM    ATXTG20                                                          
         EX    RF,*+4                                                           
         MVC   ATXWGNAM(0),NAMEREC                                              
         TR    ATXWGNAM,MXTRT                                                   
         J     EXIT                                                             
         DROP  R2,R3,R4,R5                                                      
*                                                                               
ATXWGLIT DC    A(LITERAL)                                                       
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ATXPOFC - EXTRACT OFFICE RECORDS FOR ATXTRACT                       *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(LEVELS) - CAN GET LENGTHS OF LEVELS FROM HERE                 *         
***********************************************************************         
         SPACE 1                                                                
ATXPOFC  NMOD1 WORKX-WORKD,*ATXPOFC*,RR=RA                                      
         USING WORKD,RC                                                         
         L     RA,ATXPFLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         MVC   ALEVELS,16(R1)                                                   
         USING ATXRECD,R2          R2=A(EXTRACT RECORD)                         
         USING OGRRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         XC    ATXRELEN,ATXRELEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   ATXRELEN(2),=Y(ATXPOFL)                                          
         MVC   ATXRETYP,ATXPOFQ                                                 
         MVI   ATXPFAID-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   ATXPOFOC-1,MXTRTQ   OFFICE CODE                                  
         MVI   ATXPOFNM-1,MXTRTQ   OFFICE NAME                                  
         MVI   ATXPOFX,MXTRTQ                                                   
         MVI   ATXPOFX+1,MXTRTQ                                                 
*                                                                               
         MVC   ATXPFAID,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         TR    ATXPFAID,MXTRT                                                   
         MVC   ATXPOFOC,OGRKOFC    OFFICE CODE                                  
         TR    ATXPOFOC,MXTRT                                                   
*                                                                               
         LA    R5,OGRRFST          R5=A(FIRST ELEMENT)                          
ATXPF10  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),PGRELQ        PRODUCTION GROUP ELEMENT                     
         JE    ATXPF30                                                          
         CLI   0(R5),NAMELQ        X'20' - NAME ELEMENT                         
         JE    ATXPF40                                                          
*                                                                               
ATXPF20  SR    RF,RF               BUMP TO NEXT ELEMENT                         
         ICM   RF,1,1(R5)                                                       
         JZ    EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         J     ATXPF10                                                          
*                                                                               
         USING PGRELD,R5                                                        
ATXPF30  MVC   ATXPOFGC,PGRCODE    OFFICE GROUP CODE                            
         TR    ATXPOFGC,MXTRT                                                   
         J     ATXPF20                                                          
*                                                                               
         USING NAMELD,R5                                                        
ATXPF40  MVC   ATXPOFNM,SPACES     OFFICE NAME                                  
         SR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         JM    ATXPF20                                                          
         EX    RF,*+4                                                           
         MVC   ATXPOFNM(0),NAMEREC                                              
         TR    ATXPOFNM,MXTRT                                                   
         J     ATXPF20                                                          
         DROP  R2,R3,R4,R5                                                      
*                                                                               
ATXPFLIT DC    A(LITERAL)                                                       
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ATXOGRC - EXTRACT OFFICE GROUP RECORDS FOR ATXTRACT                 *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
ATXOGRC  NMOD1 WORKX-WORKD,*ATXOGC*,RR=RA                                       
         USING WORKD,RC                                                         
         L     RA,ATXOGLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         MVC   ALEVELS,16(R1)                                                   
         USING ATXRECD,R2          R2=A(EXTRACT RECORD)                         
         USING OGRRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         XC    ATXRELEN,ATXRELEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   ATXRELEN(2),=Y(ATXOGRL)                                          
         MVC   ATXRETYP,ATXOGRQ                                                 
         MVI   ATXOGAID-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   ATXOGRGC-1,MXTRTQ   OFFICE GROUP CODE                            
         MVI   ATXOGRNM-1,MXTRTQ   OFFICE NAME                                  
         MVI   ATXOGRX,MXTRTQ                                                   
         MVI   ATXOGRX+1,MXTRTQ                                                 
*                                                                               
         MVC   ATXOGAID,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         TR    ATXOGAID,MXTRT                                                   
         MVC   ATXOGRGC,OGRKCODE   OFFICE GROUP                                 
         TR    ATXOGRGC,MXTRT                                                   
*                                                                               
         SR    RF,RF                                                            
         LA    R5,OGRRFST          R5=A(FIRST ELEMENT)                          
ATXOG02  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),NAMELQ        X'20' - NAME ELEMENT                         
         JE    ATXOG04                                                          
         ICM   RF,1,1(R5)                                                       
         JZ    EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         J     ATXOG02                                                          
*                                                                               
         USING NAMELD,R5                                                        
ATXOG04  MVC   ATXOGRNM,SPACES     OFFICE GROUP NAME                            
         SR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         JM    EXIT                                                             
         EX    RF,*+4                                                           
         MVC   ATXOGRNM(0),NAMEREC                                              
         TR    ATXOGRNM,MXTRT                                                   
         J     EXIT                                                             
         DROP  R2,R3,R4,R5                                                      
*                                                                               
ATXOGLIT DC   A(LITERAL)                                                        
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ATXOFLC - EXTRACT OFFICE LIST RECORDS FOR ATXTRACT                  *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
ATXOFLC  NMOD1 WORKX-WORKD,*ATXOFLC*,RR=RA                                      
         USING WORKD,RC                                                         
         L     RA,ATXOLLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         MVC   ALEVELS,16(R1)                                                   
         USING ATXRECD,R2          R2=A(EXTRACT RECORD)                         
         USING OFFRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         XC    ATXRELEN,ATXRELEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   ATXRELEN(2),=Y(ATXOFLL)                                          
         MVC   ATXRETYP,ATXOFLQ                                                 
         MVI   ATXOFLX,MXTRTQ                                                   
         MVI   ATXOFLX+1,MXTRTQ                                                 
         MVI   ATXOLAID-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   ATXOFFLC-1,MXTRTQ   OFFICE LIST CODE                             
         MVI   ATXOFLSC-1,MXTRTQ   OFFICE LIST SUB CODE                         
         MVI   ATXOFLSC,C' '       SET TO A ' ' IN EXTRACT                      
         MVI   ATXOFLNM-1,MXTRTQ   OFFICE NAME                                  
*                                                                               
         MVC   ATXOLAID,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         TR    ATXOLAID,MXTRT                                                   
         MVC   ATXOFFLC,OFFKOFF    OFFICE LIST                                  
         TR    ATXOFFLC,MXTRT                                                   
*                                                                               
         SR    RF,RF                                                            
         LA    R5,OFFRFST          R5=A(FIRST ELEMENT)                          
ATXOFL10 CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),NAMELQ        X'20' - NAME ELEMENT                         
         JE    ATXOFL20                                                         
         IC    RF,1(R5)                                                         
         AR    R5,RF                                                            
         J     ATXOFL10                                                         
*                                                                               
         USING NAMELD,R5                                                        
ATXOFL20 MVC   ATXOFLNM,SPACES     OFFICE LIST NAME                             
         SR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         AHI   RF,-3                                                            
         JM    EXIT                                                             
         EX    RF,*+4                                                           
         MVC   ATXOFLNM(0),NAMEREC                                              
         TR    ATXOFLNM,MXTRT                                                   
         J     EXIT                                                             
         DROP  R2,R3,R4,R5                                                      
*                                                                               
ATXOLLIT DC    A(LITERAL)                                                       
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ATXOLEC - EXTRACT OFFICE LIST ENTRY RECORDS FOR ATXTRACT            *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL1 0=BUILD 'K' RECORD, 1=BUILD ALL RECORDS, 2=PASS BACK NEXT       *         
*     X'FF' RETURNED IF NO MORE RECORDS TO PROCESS                    *         
* AL3 0                                                               *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
ATXOLEC  NMOD1 WORKX-WORKD,*ATXOLEC*,RR=RA                                      
         USING WORKD,RC                                                         
         L     RA,ATXOELIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING ATXRECD,R2          R2=A(EXTRACT RECORD)                         
         USING OFFRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         CLI   8(R1),1                                                          
         JL    ATXOEKIL            BUILD K RECORD                               
         JE    ATXOESER            BUILD SERIES OF RECORDS                      
         JH    ATXOENXT            OUTPUT NEXT RECORD                           
*                                                                               
ATXOLECC DC    A(BUFFER1)          OFFICE LIST ENTRY SAVE AREA                  
ATXOELIT DC    A(LITERAL)                                                       
         EJECT                                                                  
***********************************************************************         
* BUILD 'K' RECORD AND EXIT                                           *         
***********************************************************************         
ATXOEKIL BRAS  RE,ATXOEBLK                                                      
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* GET NEXT RECORD AND PASS IT BACK                                    *         
***********************************************************************         
         SPACE 1                                                                
ATXOENXT L     RE,ANEXT                                                         
         OC    0(8,RE),0(RE)                                                    
         JNZ   *+12                                                             
         MVI   8(R1),X'FF'         NO MORE RECORDS                              
         J     EXIT                                                             
*                                                                               
         L     RE,ANEXT            MOVE IN NEXT RECORD                          
         LA    R0,ATXRECD                                                       
         LH    RF,=Y(ATXOLEL)                                                   
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LH    RF,=Y(ATXOLEL)      UPDATE POINTER                               
         L     RE,ANEXT                                                         
         AR    RE,RF                                                            
         ST    RE,ANEXT                                                         
         J     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* BUILD SERIES OF RECORDS FOR THIS RECORD                             *         
***********************************************************************         
         SPACE 1                                                                
ATXOESER BRAS  RE,ATXOEBLK         SET UP BLANK RECORD (KEY DETAIL)             
*                                                                               
         LH    RF,=Y(ATXOLEL)                                                   
         STH   RF,RECLEN                                                        
         L     R2,ATXOLECC         WHERE RECORDS TO BE BUILT                    
         XC    0(8,R2),0(R2)                                                    
         ST    R2,ANEXT            FOR NEXT TIME IN                             
*                                                                               
         LA    R6,OFFRFST          FIRST ONE RECORD DATA                        
ATXOE10  CLI   0(R6),0                                                          
         JE    EXIT                                                             
         CLI   0(R6),OFLELQ                                                     
         JE    ATXOE30                                                          
*                                                                               
ATXOE20  SR    R1,R1                                                            
         IC    R1,1(R6)                                                         
         AR    R6,R1                                                            
         J     ATXOE10                                                          
*                                                                               
         USING OFLELD,R6                                                        
ATXOE30  LA    R7,OFLNTRY          A(FIRST OFFICE)                              
         SR    R0,R0                                                            
         IC    R0,OFLLN                                                         
         LA    RF,OFLLN1Q                                                       
         SR    R0,RF                                                            
         JNP   ATXOE20                                                          
*                                                                               
ATXOE40  STH   R0,HALF                                                          
         L     RE,AXREC            BLANK RECORD HERE                            
         LR    R0,R2                                                            
         LH    RF,RECLEN                                                        
         LR    R1,RF                                                            
         MVCL  R0,RE               COPY IN A BLANK RECORD                       
*                                                                               
         LH    RF,RECLEN                                                        
         AR    RF,R2                                                            
         XC    0(8,RF),0(RF)       CLEAR START OF NEXT DATA SPACE               
*                                                                               
         MVC   ATXOLEOC,0(R7)      MOVE IN OFFICE CODE                          
         TR    ATXOLEOC,MXTRT                                                   
*                                                                               
         LA    R7,2(R7)            NEXT OFFICE                                  
         AH    R2,RECLEN                                                        
         LH    R0,HALF                                                          
         SH    R0,=H'2'                                                         
         JP    ATXOE40                                                          
         J     ATXOE20             REACHED THE END                              
         SPACE 1                                                                
***********************************************************************         
* BUILD BLANK RECORD AND INPUT KEY FIELDS-OFFICE LIST ENTRY RECORDS   *         
***********************************************************************         
         SPACE 1                                                                
ATXOEBLK XC    ATXRELEN,ATXRELEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   ATXRELEN(2),=Y(ATXOLEL)                                          
         MVC   ATXRETYP,ATXOLEQ                                                 
         MVI   ATXOLEX,MXTRTQ                                                   
         MVI   ATXOLEX+1,MXTRTQ                                                 
         MVI   ATXOEAID-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   ATXOFLEC-1,MXTRTQ   OFFICE LIST CODE                             
         MVI   ATXOLESC-1,MXTRTQ   OFFICE LIST SUB CODE                         
         MVI   ATXOLESC,C' '       SET TO A ' ' IN THIS EXTRACT                 
         MVI   ATXOLEOC-1,MXTRTQ   OFFICE CODE                                  
         MVC   ATXOEAID,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         TR    ATXOEAID,MXTRT                                                   
         MVC   ATXOFLEC,OFFKOFF    OFFICE LIST CODE                             
         TR    ATXOFLEC,MXTRT                                                   
         BR    RE                                                               
         DROP  R2,R3,R4,R6                                                      
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ATXOFFC - EXTRACT OFFICE RECORDS FOR ATXTRACT                       *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(LEVELS) - CAN GET LENGTHS OF LEVELS FROM HERE                 *         
***********************************************************************         
         SPACE 1                                                                
ATXOFFC  NMOD1 WORKX-WORKD,*ATXOFFC*,RR=RA                                      
         USING WORKD,RC                                                         
         L     RA,ATXOFLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         MVC   ALEVELS,16(R1)                                                   
         USING ATXRECD,R2          R2=A(EXTRACT RECORD)                         
         USING ACTKEY,R3           R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         XC    ATXRELEN,ATXRELEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   ATXRELEN(2),=Y(ATXOFFL)                                          
         MVC   ATXRETYP,ATXOFFQ                                                 
         MVI   ATXOFAID-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   ATXOFFOC-1,MXTRTQ   OFFICE CODE                                  
         MVI   ATXOFFNM-1,MXTRTQ   OFFICE NAME                                  
         MVI   ATXOFFIL-1,MXTRTQ   ISLOCKED                                     
         MVI   ATXOFFGC-1,MXTRTQ   PRODUCTION OFFICE GROUP CODE                 
         MVI   ATXOFFX,MXTRTQ                                                   
         MVI   ATXOFFX+1,MXTRTQ                                                 
*                                                                               
         MVC   ATXOFAID,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         TR    ATXOFAID,MXTRT                                                   
         MVC   ATXOFFOC,SPACES     OFFICE CODE                                  
         L     R6,ALEVELS                                                       
         SR    RF,RF                                                            
         IC    RF,0(R6)                                                         
         BCTR  RF,0                                                             
         EX    RF,*+4                                                           
         MVC   ATXOFFOC(0),ACTKACT                                              
         TR    ATXOFFOC,MXTRT                                                   
*                                                                               
         LA    R5,ACTRFST          R5=A(FIRST ELEMENT)                          
ATXOF10  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),PGRELQ        PRODUCTION GROUP ELEMENT                     
         JE    ATXOF30                                                          
         CLI   0(R5),NAMELQ        X'20' - NAME ELEMENT                         
         JE    ATXOF40                                                          
         CLI   0(R5),RSTELQ        X'30' - RECORD STATUS ELEMENT                
         JE    ATXOF50                                                          
*                                                                               
ATXOF20  SR    RF,RF               BUMP TO NEXT ELEMENT                         
         ICM   RF,1,1(R5)                                                       
         JZ    EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         J     ATXOF10                                                          
*                                                                               
         USING PGRELD,R5                                                        
ATXOF30  MVC   ATXOFFGC,PGRCODE    OFFICE GROUP CODE                            
         TR    ATXOFFGC,MXTRT                                                   
         J     ATXOF20                                                          
*                                                                               
         USING NAMELD,R5                                                        
ATXOF40  MVC   ATXOFFNM,SPACES     OFFICE NAME                                  
         SR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         JM    ATXOF20                                                          
         EX    RF,*+4                                                           
         MVC   ATXOFFNM(0),NAMEREC                                              
         TR    ATXOFFNM,MXTRT                                                   
         J     ATXOF20                                                          
*                                                                               
         USING RSTELD,R5                                                        
ATXOF50  MVI   ATXOFFIL,C'N'                                                    
         TM    RSTSTAT1,RSTSACIL   ACCOUNT IS LOCKED                            
         JNO   *+8                                                              
         MVI   ATXOFFIL,C'Y'                                                    
         J     ATXOF20                                                          
         DROP  R2,R3,R4,R5                                                      
*                                                                               
ATXOFLIT DC    A(LITERAL)                                                       
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ATXDEPC - EXTRACT DEPATMENT RECORDS FOR ATXTRACT                    *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD) - CAN GET 2 BYTE ALPHA FROM THIS...                  *         
* AL4 A(LEVELS) - CAN GET LENGTH OF EACH SUB-LEVEL FROM THESE         *         
***********************************************************************         
         SPACE 1                                                                
ATXDEPC  NMOD1 WORKX-WORKD,*ATXDEPC*,RR=RA                                      
         USING WORKD,RC                                                         
         L     RA,ATXDPLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         MVC   ALEVELS,16(R1)      SAVE LENGTHS                                 
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING ATXRECD,R2          R2=A(EXTRACT RECORD)                         
         USING ACTKEY,R3           R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R5=A(SXDTABD)                                
*                                                                               
         XC    ATXRELEN,ATXRELEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   ATXRELEN(2),=Y(ATXDEPL)                                          
         MVC   ATXRETYP,ATXDEPQ                                                 
         MVI   ATXDEPX,MXTRTQ                                                   
         MVI   ATXDEPX+1,MXTRTQ                                                 
         MVI   ATXDPAID-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   ATXDPOFC-1,MXTRTQ   OFFICE CODE                                  
         MVI   ATXDPDEC-1,MXTRTQ   DEPARTMENT CODE                              
         MVI   ATXDPNAM-1,MXTRTQ   DEPARTMENT NAME                              
         MVI   ATXDPANC-1,MXTRTQ   ANALYSIS CODE                                
         MVI   ATXDPIL-1,MXTRTQ    ACCOUNT IS LOCKED                            
*                                                                               
         MVC   ATXDPAID,SXDTAGY    2-BYTE AGY ALPHA                             
         TR    ATXDPAID,MXTRT                                                   
         MVC   ATXDPOFC,SPACES     OFFICE CODE                                  
         L     R6,ALEVELS                                                       
         SR    RF,RF                                                            
         IC    RF,0(R6)                                                         
         BCTR  RF,0                                                             
         EX    RF,*+4                                                           
         MVC   ATXDPOFC(0),ACTKACT                                              
         TR    ATXDPOFC,MXTRT                                                   
*                                                                               
         LA    RF,ACTKACT+1(RF)    A(DEPARTMENT CODE)                           
         MVC   ATXDPDEC,SPACES     DEPARTMENT CODE                              
         SR    RE,RE                                                            
         IC    RE,1(R6)                                                         
         BCTR  RE,0                                                             
         EX    RE,*+4                                                           
         MVC   ATXDPDEC(0),0(RF)                                                
         TR    ATXDPDEC,MXTRT                                                   
*                                                                               
         LA    R5,ACTRFST          R5=A(FIRST ELEMENT)                          
ATXDEP10 CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),NAMELQ        X'20' - NAME ELEMENT                         
         JE    ATXDEP30                                                         
         CLI   0(R5),RSTELQ        X'30' - RECORD STATUS ELEMENT                
         JE    ATXDEP40                                                         
*                                                                               
ATXDEP20 SR    RF,RF               BUMP TO NEXT ELEMENT                         
         ICM   RF,1,1(R5)                                                       
         JZ    EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         J     ATXDEP10                                                         
*                                                                               
         USING NAMELD,R5                                                        
ATXDEP30 MVC   ATXDPNAM,SPACES     ACCOUNT NAME                                 
         SR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         BM    ATXDEP20                                                         
         EX    RF,*+4                                                           
         MVC   ATXDPNAM(0),NAMEREC                                              
         TR    ATXDPNAM,MXTRT                                                   
         J     ATXDEP20                                                         
*                                                                               
         USING RSTELD,R5                                                        
ATXDEP40 MVC   ATXDPANC,RSTANAL    ANALYSIS CODE                                
         TR    ATXDPANC,MXTRT                                                   
         MVI   ATXDPIL,C'N'                                                     
         TM    RSTSTAT1,RSTSACIL   ACCOUNT IS LOCKED                            
         JZ    *+8                                                              
         MVI   ATXDPIL,C'Y'                                                     
         J     ATXDEP20                                                         
         DROP  R2,R3,R4,R5                                                      
*                                                                               
ATXDPLIT DC    A(LITERAL)                                                       
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ATXSBDC - EXTRACT SUB-DEPARTMENT RECORDS FOR ATXTRACT               *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(LEVELS) - CAN GET LENGTH OF EACH SBD-LEVEL FROM THESE         *         
***********************************************************************         
         SPACE 1                                                                
ATXSBDC  NMOD1 WORKX-WORKD,*ATXSBDC*,RR=RA                                      
         USING WORKD,RC                                                         
         L     RA,ATXSDLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         MVC   ALEVELS,16(R1)      SAVE LENGTHS                                 
         USING ATXRECD,R2          R2=A(EXTRACT RECORD)                         
         USING ACTKEY,R3           R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R5=A(SXDTABD)                                
*                                                                               
         XC    ATXRELEN,ATXRELEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   ATXRELEN(2),=Y(ATXSDPL)                                          
         MVC   ATXRETYP,ATXSBDQ                                                 
         MVI   ATXSDPX,MXTRTQ                                                   
         MVI   ATXSDPX+1,MXTRTQ                                                 
         MVI   ATXSDAID-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   ATXSDOFC-1,MXTRTQ   OFFICE CODE                                  
         MVI   ATXSDDEC-1,MXTRTQ   DEPARTMENT CODE                              
         MVI   ATXSDSDC-1,MXTRTQ   SUB-DEPARTMENT CODE                          
         MVI   ATXSDNAM-1,MXTRTQ   SUB-DEPARTMENT NAME                          
         MVI   ATXSDIL-1,MXTRTQ    ACCOUNT IS LOCKED                            
*                                                                               
         MVC   ATXSDAID,SXDTAGY    2-BYTE AGY ALPHA                             
         TR    ATXSDAID,MXTRT                                                   
         L     R6,ALEVELS                                                       
         MVC   ATXSDOFC,SPACES     OFFICE CODE                                  
         SR    RF,RF                                                            
         IC    RF,0(R6)                                                         
         BCTR  RF,0                                                             
         EX    RF,*+4                                                           
         MVC   ATXSDOFC(0),ACTKACT                                              
         TR    ATXSDOFC,MXTRT                                                   
*                                                                               
         LA    RF,ACTKACT+1(RF)    A(DEPARTMENT CODE)                           
         MVC   ATXSDDEC,SPACES     DEPARTMENT CODE                              
         SR    RE,RE                                                            
         IC    RE,1(R6)                                                         
         BCTR  RE,0                                                             
         EX    RE,*+4                                                           
         MVC   ATXSDDEC(0),0(RF)                                                
         TR    ATXSDDEC,MXTRT                                                   
         LA    RF,1(RE,RF)                                                      
*                                                                               
         MVC   ATXSDSDC,SPACES     SUB-DEPARTMENT CODE                          
         IC    RE,2(R6)                                                         
         BCTR  RE,0                                                             
         EX    RE,*+4                                                           
         MVC   ATXSDSDC(0),0(RF)                                                
         TR    ATXSDSDC,MXTRT                                                   
*                                                                               
         LA    R5,ACTRFST          R5=A(FIRST ELEMENT)                          
ATXSBD10 CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),NAMELQ        X'20' - NAME ELEMENT                         
         JE    ATXSBD30                                                         
         CLI   0(R5),RSTELQ        X'30' - RECORD STATUS ELEMENT                
         JE    ATXSBD40                                                         
*                                                                               
ATXSBD20 SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R5)                                                         
         AR    R5,RF                                                            
         J     ATXSBD10                                                         
*                                                                               
         USING NAMELD,R5                                                        
ATXSBD30 MVC   ATXSDNAM,SPACES     SUB-DEPARTMENT NAME                          
         SR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         JM    ATXSBD20                                                         
         EX    RF,*+4                                                           
         MVC   ATXSDNAM(0),NAMEREC                                              
         TR    ATXSDNAM,MXTRT                                                   
         J     ATXSBD20                                                         
*                                                                               
         USING RSTELD,R5                                                        
ATXSBD40 MVI   ATXSDIL,C'N'                                                     
         TM    RSTSTAT1,RSTSACIL   ACCOUNT IS LOCKED                            
         JZ    ATXSBD20                                                         
         MVI   ATXSDIL,C'Y'                                                     
         J     ATXSBD20                                                         
*                                                                               
ATXSDLIT DC    A(LITERAL)                                                       
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ATXPERC - EXTRACT PERSON RECORDS FOR ATXTRACT                       *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
ATXPERC  NMOD1 WORKX-WORKD,*ATXPERC*,RR=RA                                      
         USING WORKD,RC                                                         
         L     RA,ATXPELIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING ATXRECD,R2          R2=A(EXTRACT RECORD)                         
         USING PERRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDTABD)                                
*                                                                               
         XC    ATXRELEN,ATXRELEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   ATXRELEN(2),=Y(ATXPERL)                                          
         MVC   ATXRETYP,ATXPERQ                                                 
         MVI   ATXPERX,MXTRTQ                                                   
         MVI   ATXPERX+1,MXTRTQ                                                 
         MVI   ATXPEAID-1,MXTRTQ   COMPANY CODE                                 
         MVI   ATXPECSC-1,MXTRTQ   CONSYSPERSON CODE                            
         MVI   ATXPEPC-1,MXTRTQ    PERSON CODE                                  
         MVI   ATXPEP-1,MXTRTQ     PERSON                                       
         MVI   ATXPEFN-1,MXTRTQ    FIRST NAME                                   
         MVI   ATXPELN-1,MXTRTQ    LAST NAME                                    
         MVI   ATXPEHDT-1,MXTRTQ   HIRE DATE                                    
         MVI   ATXPETDT-1,MXTRTQ   TERMINATION DATE                             
*                                                                               
         MVC   ATXPEAID,SXDTAGY    COMPANY CODE                                 
         TR    ATXPEAID,MXTRT                                                   
         MVC   ATXPEPC,PERKCODE    PERSON CODE                                  
         TR    ATXPEPC,MXTRT                                                    
         MVI   ATXPEP,C'I'         PERS (I + PERSON CODE)                       
         MVC   ATXPEP+1(L'ATXPEPC),ATXPEPC                                      
         TR    ATXPEP,MXTRT                                                     
*                                                                               
         LA    R5,PERRFST          R5=A(FIRST ELEMENT)                          
ATXPER10 CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),EMPELQ        EMPLOYEE STATUS ELEMENT                      
         JE    ATXPER30                                                         
         CLI   0(R5),PIDELQ        PERSON ID ELEMENT                            
         JE    ATXPER40                                                         
         CLI   0(R5),GPNELQ        NAME ELEMENT                                 
         JE    ATXPER50                                                         
*                                                                               
ATXPER20 SR    RF,RF                                                            
         IC    RF,1(R5)                                                         
         AR    R5,RF                                                            
         J     ATXPER10                                                         
*                                                                               
         USING EMPELD,R5                                                        
ATXPER30 DS    0H                                                               
*&&US*&& GOTO1 VDATCON,DMCB,(1,EMPHIR),(23,ATXPEHDT),0                          
*&&UK*&& GOTO1 VDATCON,DMCB,(1,EMPHIR),(10,ATXPEHDT),0                          
         TR    ATXPEHDT,MXTRT                                                   
         MVC   ATXPETDT,SPACES     ASSUME NO TERM DATE                          
         OC    EMPTRM,EMPTRM       IS THERE A DATE?                             
         JZ    ATXPER20                                                         
*&&US*&& GOTO1 VDATCON,DMCB,(1,EMPTRM),(23,ATXPETDT),0                          
*&&UK*&& GOTO1 VDATCON,DMCB,(1,EMPTRM),(10,ATXPETDT),0                          
         TR    ATXPETDT,MXTRT                                                   
         J     ATXPER20                                                         
*                                                                               
         USING PIDELD,R5                                                        
ATXPER40 SR    R0,R0                                                            
         ICM   R0,3,PIDNO          GET PID NUMBER                               
         CVD   R0,DUB                                                           
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  ATXPECSC,DUB                                                     
         J     ATXPER20                                                         
*                                                                               
         USING GPNELD,R5                                                        
ATXPER50 SR    RF,RF                                                            
         IC    RF,GPNLN                                                         
         SH    RF,=H'4'                                                         
         JM    ATXPER20                                                         
         CLI   GPNTYP,GPNTLST      WHICH TYPE OF NAME?                          
         JE    ATXPER60                                                         
*                                                                               
         MVC   ATXPEFN,SPACES                                                   
         EX    RF,*+4                                                           
         MVC   ATXPEFN(0),GPNNME                                                
         TR    ATXPEFN,MXTRT                                                    
         J     ATXPER20                                                         
*                                                                               
ATXPER60 MVC   ATXPELN,SPACES                                                   
         EX    RF,*+4                                                           
         MVC   ATXPELN(0),GPNNME                                                
         TR    ATXPELN,MXTRT                                                    
         J     ATXPER20                                                         
*                                                                               
ATXPELIT DC    A(LITERAL)                                                       
         DROP  R2,R3,R4,R5                                                      
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ATXPEAC - EXTRACT PERSON ASSIGNMENT RECORDS FOR ATXTRACT            *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL1 1=BUILD ALL RECORDS, 2=PASS BACK NEXT                           *         
*     X'FF' RETURNED IF NO MORE RECORDS TO PROCESS                    *         
* AL3 0                                                               *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
ATXPEAC  NMOD1 WORKX-WORKD,*ATXPEAC*,RR=RA                                      
         USING WORKD,RC                                                         
         L     RA,ATXPALIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING ATXRECD,R2          R2=A(EXTRACT RECORD)                         
         USING PERRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         CLI   8(R1),1                                                          
         JL    ATXPAKIL            BUILD K RECORD                               
         JE    ATXPASER            BUILD SERIES OF RECORDS                      
         JH    ATXPANXT            OUTPUT NEXT RECORD                           
*                                                                               
ATXPACC  DC    A(BUFFER1)          PERSON ASSIGNMENT SAVE AREA                  
ATXPALIT DC    A(LITERAL)                                                       
         EJECT                                                                  
***********************************************************************         
* BUILD 'K' RECORD AND EXIT                                           *         
***********************************************************************         
ATXPAKIL BRAS  RE,ATXPABLK                                                      
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* BUILD SERIES OF RECORDS FOR THIS RECORD                             *         
***********************************************************************         
         SPACE 1                                                                
ATXPASER BRAS  RE,ATXPABLK         SET UP BLANK RECORD (KEY DETAIL)             
*                                                                               
         LH    RF,=Y(ATXPEAL)                                                   
         STH   RF,RECLEN                                                        
         L     R2,ATXPACC          WHERE RECORDS TO BE BUILT                    
         ST    R2,ANEXT            FOR NEXT TIME IN                             
         XC    0(8,R2),0(R2)                                                    
*                                                                               
         LA    R6,PERRFST          FIRST ONE RECORD DATA                        
ATXPA10  CLI   0(R6),0                                                          
         JE    EXIT                                                             
         CLI   0(R6),LOCELQ                                                     
         JE    ATXPA30                                                          
*                                                                               
ATXPA20  SR    R1,R1                                                            
         IC    R1,1(R6)                                                         
         AR    R6,R1                                                            
         J     ATXPA10                                                          
*                                                                               
ATXPA30  L     RE,AXREC            BLANK RECORD                                 
         LA    R0,ATXRECD                                                       
         LH    RF,RECLEN                                                        
         LR    R1,RF                                                            
         MVCL  R0,RE               COPY IN BLANK RECORD                         
*                                                                               
         LH    RF,RECLEN                                                        
         AR    RF,R2                                                            
         XC    0(8,RF),0(RF)       BLANK OFF START OF NEXT SLOT                 
*                                                                               
         USING LOCELD,R6                                                        
*&&US*&& GOTO1 VDATCON,DMCB,(1,LOCSTART),(23,ATXPASTD),0                        
*&&UK*&& GOTO1 VDATCON,DMCB,(1,LOCSTART),(10,ATXPASTD),0                        
         TR    ATXPASTD,MXTRT                                                   
         MVC   ATXPAEND,SPACES     CLEAR FIELD                                  
         OC    LOCEND,LOCEND       ANY END DATE?                                
         BZ    ATXPA40                                                          
*&&US*&& GOTO1 VDATCON,DMCB,(1,LOCEND),(23,ATXPAEND),0                          
*&&UK*&& GOTO1 VDATCON,DMCB,(1,LOCEND),(10,ATXPAEND),0                          
         TR    ATXPAEND,MXTRT                                                   
ATXPA40  MVC   ATXPAOFC,LOCOFF                                                  
         TR    ATXPAOFC,MXTRT                                                   
         MVC   ATXPADEC,LOCDEPT                                                 
         TR    ATXPADEC,MXTRT                                                   
         MVC   ATXPASDC,LOCSUB                                                  
         TR    ATXPASDC,MXTRT                                                   
         EDIT  LOCATTR,(3,ATXPAATT),ZERO=NOBLANK,FILL=0                         
         EDIT  LOCSTAT,(3,ATXPASTA),ZERO=NOBLANK,FILL=0                         
*                                                                               
         AH    R2,RECLEN           NEXT SLOT                                    
         J     ATXPA20                                                          
         EJECT                                                                  
***********************************************************************         
* GET NEXT RECORD AND PASS IT BACK                                    *         
***********************************************************************         
         SPACE 1                                                                
ATXPANXT L     RE,ANEXT                                                         
         OC    0(8,RE),0(RE)       RECORD TO PASS BACK?                         
         JNZ   *+12                                                             
         MVI   8(R1),X'FF'         NO MORE RECORDS                              
         J     EXIT                                                             
*                                                                               
         L     RE,ANEXT            MOVE IN RECORD                               
         LA    R0,ATXRECD                                                       
         LH    RF,=Y(ATXPEAL)                                                   
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LH    RF,=Y(ATXPEAL)      UPDATE POINTER                               
         L     RE,ANEXT                                                         
         AR    RE,RF                                                            
         ST    RE,ANEXT                                                         
         J     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* BUILD BLANK RECORD AND INPUT KEY FIELDS                             *         
***********************************************************************         
         SPACE 1                                                                
ATXPABLK XC    ATXRELEN,ATXRELEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   ATXRELEN(2),=Y(ATXPEAL)                                          
         MVC   ATXRETYP,ATXPEAQ                                                 
         MVI   ATXPEAX,MXTRTQ                                                   
         MVI   ATXPEAX+1,MXTRTQ                                                 
         MVI   ATXPAAID-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   ATXPACSC-1,MXTRTQ   CONSYSPERSON CODE                            
         MVI   ATXPAPC-1,MXTRTQ    PERSON CODE                                  
         MVI   ATXPASTD-1,MXTRTQ   START DATE                                   
         MVI   ATXPAEND-1,MXTRTQ   END DATE                                     
         MVI   ATXPAOFC-1,MXTRTQ   OFFICE CODE                                  
         MVI   ATXPADEC-1,MXTRTQ   DEPARTMENT CODE                              
         MVI   ATXPASDC-1,MXTRTQ   SUB-DEPT CODE                                
         MVI   ATXPAATT-1,MXTRTQ   ATTRIBUTES                                   
         MVI   ATXPASTA-1,MXTRTQ   STATUS                                       
         MVC   ATXPAAID,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         TR    ATXPAAID,MXTRT                                                   
         MVC   ATXPAPC,PERKCODE    PERSON CODE                                  
         TR    ATXPAPC,MXTRT                                                    
*                                                                               
         ZAP   DUB,=P'0'           DUB HOLDS PID NUMBER                         
         LA    R5,PERRFST          R5=A(FIRST ELEMENT)                          
ATXPAB10 CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    ATXPAB30                                                         
         CLI   0(R5),PIDELQ        PERSON ID ELEMENT                            
         JE    ATXPAB20                                                         
*                                                                               
         SR    R1,R1                                                            
         IC    R1,1(R5)                                                         
         AR    R5,R1                                                            
         J     ATXPAB10                                                         
*                                                                               
         USING PIDELD,R5                                                        
ATXPAB20 SR    R0,R0                                                            
         ICM   R0,3,PIDNO                                                       
         CVD   R0,DUB                                                           
         OI    DUB+L'DUB-1,X'0F'                                                
*                                                                               
ATXPAB30 UNPK  ATXPACSC,DUB        OUTPUT CONSYSPERSON CODE                     
         BR    RE                                                               
         DROP  R2,R3,R4,R5,R6                                                   
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ATXCSAC - EXTRACT COSTING ACCOUNT RECORDS FOR ATXTRACT              *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD) - CAN GET 2 BYTE ALPHA FROM THIS...                  *         
***********************************************************************         
         SPACE 1                                                                
ATXCSAC  NMOD1 WORKX-WORKD,*ATXCSAC*,RR=RA                                      
         USING WORKD,RC                                                         
         L     RA,ATXCSLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING ATXRECD,R2          R2=A(EXTRACT RECORD)                         
         USING ACTKEY,R3           R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDTABD)                                
*                                                                               
         XC    ATXRELEN,ATXRELEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   ATXRELEN(2),=Y(ATXCSAL)                                          
         MVC   ATXRETYP,ATXCSAQ                                                 
         MVI   ATXCSAX,MXTRTQ                                                   
         MVI   ATXCSAX+1,MXTRTQ                                                 
         MVI   ATXCSAID-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   ATXCSACC-1,MXTRTQ   COSTING ACCOUNT CODE                         
         MVI   ATXCSNAM-1,MXTRTQ   INCOME ACCOUNT NAME                          
         MVI   ATXCSIL-1,MXTRTQ    ACCOUNT IS LOCKED                            
*                                                                               
         MVC   ATXCSAID,SXDTAGY    2-BYTE AGY ALPHA                             
         TR    ATXCSAID,MXTRT                                                   
         MVC   ATXCSACC,ACTKUNT    INCOME ACCOUNT CODE                          
         TR    ATXCSACC,MXTRT                                                   
*                                                                               
         LA    R5,ACTRFST          R5=A(FIRST ELEMENT)                          
ATXCS10  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),NAMELQ        X'20' - NAME ELEMENT                         
         JE    ATXCS30                                                          
         CLI   0(R5),RSTELQ        X'30' - RECORD STATUS ELEMENT                
         JE    ATXCS40                                                          
*                                                                               
ATXCS20  SR    RF,RF               BUMP TO NEXT ELEMENT                         
         ICM   RF,1,1(R5)                                                       
         JZ    EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         J     ATXCS10                                                          
*                                                                               
         USING NAMELD,R5                                                        
ATXCS30  MVC   ATXCSNAM,SPACES     ACCOUNT NAME                                 
         SR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         BM    ATXCS20                                                          
         EX    RF,*+4                                                           
         MVC   ATXCSNAM(0),NAMEREC                                              
         TR    ATXCSNAM,MXTRT                                                   
         J     ATXCS20                                                          
*                                                                               
         USING RSTELD,R5                                                        
ATXCS40  MVI   ATXCSIL,C'N'                                                     
         TM    RSTSTAT1,RSTSACIL   ACCOUNT IS LOCKED                            
         JZ    ATXCS20                                                          
         MVI   ATXCSIL,C'Y'                                                     
         J     ATXCS20                                                          
         DROP  R2,R3,R4,R5                                                      
*                                                                               
ATXCSLIT DC    A(LITERAL)                                                       
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ATXLEDC - EXTRACT LEDGER RECORDS FOR ATXTRACT                       *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
ATXLEDC  NMOD1 WORKX-WORKD,*ATXLEDC*,RR=RA                                      
         USING WORKD,RC                                                         
         L     RA,ATXLELIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING ATXRECD,R2          R2=A(EXTRACT RECORD)                         
         USING ACTRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R5=A(SXDTABD)                                
*                                                                               
         XC    ATXRELEN,ATXRELEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   ATXRELEN(2),=Y(ATXLEDL)                                          
         MVC   ATXRETYP,ATXLEDQ                                                 
         MVI   ATXLEDX,MXTRTQ                                                   
         MVI   ATXLEDX+1,MXTRTQ                                                 
         MVI   ATXLEAID-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   ATXLELC-1,MXTRTQ    LEDGER CODE                                  
         MVI   ATXLENAM-1,MXTRTQ   LEDGER NAME                                  
         MVI   ATXLEDSL-1,MXTRTQ   SECURITY LEVEL                               
*                                                                               
         MVC   ATXLEAID,SXDTAGY    2-BYTE AGY ALPHA                             
         TR    ATXLEAID,MXTRT                                                   
         MVC   ATXLELC,ACTKUNT     LEDGER CODE                                  
         TR    ATXLELC,MXTRT                                                    
*                                                                               
         LA    R5,ACTRFST          R5=A(FIRST ELEMENT)                          
ATXLED10 CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),NAMELQ        X'20' - NAME ELEMENT                         
         JE    ATXLED30                                                         
         CLI   0(R5),RSTELQ        X'30' - RECORD STATUS ELEMENT                
         JE    ATXLED40                                                         
*                                                                               
ATXLED20 SR    RF,RF               BUMP TO NEXT ELEMENT                         
         ICM   RF,1,1(R5)                                                       
         JZ    EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         J     ATXLED10                                                         
*                                                                               
         USING NAMELD,R5                                                        
ATXLED30 MVC   ATXLENAM,SPACES     ACCOUNT NAME                                 
         SR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=H'3'                                                         
         JM    ATXLED20                                                         
         EX    RF,*+4                                                           
         MVC   ATXLENAM(0),NAMEREC                                              
         TR    ATXLENAM,MXTRT                                                   
         J     ATXLED20                                                         
*                                                                               
         USING RSTELD,R5                                                        
ATXLED40 EDIT  RSTSECY,(5,ATXLEDSL),ZERO=NOBLANK,FILL=0                         
         J     ATXLED20                                                         
         DROP  R2,R3,R4,R5                                                      
*                                                                               
ATXLELIT DC    A(LITERAL)                                                       
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ATXLESC - EXTRACT LEDGER STRUCTURE RECORDS FOR ATXTRACT             *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL1 1=BUILD ALL RECORDS, 2=PASS BACK NEXT                           *         
*     X'FF' RETURNED IF NO MORE RECORDS TO PROCESS                    *         
* AL3 0                                                               *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
ATXLESC  NMOD1 WORKX-WORKD,*ATXLESC*,RR=RA                                      
         USING WORKD,RC                                                         
         L     RA,ATXLSLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING ATXRECD,R2          R2=A(EXTRACT RECORD)                         
         USING LDGRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         CLI   8(R1),1                                                          
         JL    ATXLSKIL            BUILD K RECORD                               
         JE    ATXLSSER            BUILD SERIES OF RECORDS                      
         JH    ATXLSNXT            OUTPUT NEXT RECORD                           
*                                                                               
ATXLSCC  DC    A(BUFFER1)          LOCALITY INFORMATION SAVE AREA               
ATXLSLIT DC    A(LITERAL)                                                       
         EJECT                                                                  
***********************************************************************         
* BUILD 'K' RECORD AND EXIT                                           *         
***********************************************************************         
ATXLSKIL BRAS  RE,ATXLSBLK                                                      
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* BUILD SERIES OF RECORDS FOR THIS RECORD                             *         
***********************************************************************         
         SPACE 1                                                                
ATXLSSER BRAS  RE,ATXLSBLK         SET UP BLANK RECORD (KEY DETAIL)             
*                                                                               
         SR    RF,RF                                                            
         LH    RF,=Y(ATXLESL)      LENGTH OF A LEDGER STRUCTURE RECORD          
         STH   RF,RECLEN                                                        
         L     R2,ATXLSCC          WHERE RECORDS TO BE BUILT                    
         ST    R2,ANEXT            FOR NEXT TIME IN                             
         XC    0(8,R2),0(R2)                                                    
*                                                                               
         SR    R1,R1                                                            
         LA    R6,LDGRFST          FIRST ONE RECORD DATA                        
ATXLS10  CLI   0(R6),0                                                          
         JE    EXIT                                                             
         CLI   0(R6),ACLELQ                                                     
         JE    ATXLS20                                                          
         IC    R1,1(R6)                                                         
         AR    R6,R1                                                            
         J     ATXLS10                                                          
*                                                                               
         USING ACLELD,R6                                                        
ATXLS20  LA    R7,ACLVALS          A(FIRST LEDGER)                              
         SR    RE,RE                                                            
         IC    RE,ACLLN                                                         
         SRL   RE,4                                                             
         LA    R9,1                LEVEL NUMBER IN R9                           
         XC    LASTLEN,LASTLEN                                                  
         XC    LASTLEN1,LASTLEN1                                                
*                                                                               
ATXLS30  STH   RE,HALF             FOR BCT LOOP BELOW                           
*                                                                               
         L     RE,AXREC            BLANK RECORD                                 
         LR    R0,R2                                                            
         LH    RF,RECLEN           LENGTH OF A BLANK RECORD                     
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LH    RF,RECLEN           LENGTH OF A BLANK RECORD                     
         AR    RF,R2                                                            
         XC    0(8,RF),0(RF)       CLEAR START OF NEXT DATA SPACE               
*                                                                               
         EDIT  (R9),(3,ATXLSNUM),ZERO=NOBLANK,FILL=0                            
*                                                                               
         SR    RF,RF                                                            
         IC    RF,0(R7)                                                         
         STH   RF,LASTLEN1                                                      
         SH    RF,LASTLEN                                                       
         MVC   LASTLEN,LASTLEN1                                                 
         EDIT  ((RF)),(3,ATXLSLLN),ZERO=NOBLANK,FILL=0                          
*                                                                               
         MVC   ATXLSNAM,1(R7)      MOVE IN LEDGER NAME                          
*                                                                               
         LA    R9,1(R9)            BUMP LEVEL NUMBER                            
         LA    R7,16(R7)           NEXT LEVEL IN                                
         AH    R2,RECLEN           NEXT IN BUFFER                               
         CLI   0(R7),0             ANY DATA?                                    
         JE    EXIT                                                             
*                                                                               
         LH    RE,HALF                                                          
         BCT   RE,ATXLS30                                                       
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* GET NEXT LEDGER STRUCTURE RECORD AND RETURN IT                      *         
***********************************************************************         
         SPACE 1                                                                
ATXLSNXT L     RE,ANEXT                                                         
         OC    0(8,RE),0(RE)                                                    
         JNZ   *+12                                                             
         MVI   8(R1),X'FF'         FLAG NO MORE RECORDS                         
         J     EXIT                                                             
*                                                                               
         LA    R0,ATXRECD          COPY IN NEXT RECORD                          
         L     RE,ANEXT                                                         
         LH    RF,=Y(ATXLESL)                                                   
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LH    RF,=Y(ATXLESL)      UPDATE POINTER                               
         L     RE,ANEXT                                                         
         AR    RE,RF                                                            
         ST    RE,ANEXT                                                         
         J     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
* BUILD BLANK RECORD AND INPUT KEY FIELDS                             *         
***********************************************************************         
         SPACE 1                                                                
ATXLSBLK DS    0H                                                               
         XC    ATXRELEN,ATXRELEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   ATXRELEN(2),=Y(ATXLESL)                                          
         MVC   ATXRETYP,ATXLESQ                                                 
         MVI   ATXLESX,MXTRTQ                                                   
         MVI   ATXLESX+1,MXTRTQ                                                 
         MVI   ATXLSAID-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   ATXLSLC-1,MXTRTQ    LEDGER CODE                                  
         MVI   ATXLSNUM-1,MXTRTQ   LEVEL NUMBER                                 
         MVI   ATXLSNAM-1,MXTRTQ   LEVEL NAME                                   
         MVI   ATXLSLLN-1,MXTRTQ   LEVEL LENGTH                                 
         MVC   ATXLSAID,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         TR    ATXLSAID,MXTRT                                                   
         MVC   ATXLSLC,LDGKUNT     LEDGER CODE                                  
         TR    ATXLSLC,MXTRT                                                    
         BR    RE                                                               
         DROP  R2,R3,R4,R6                                                      
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* ATXOPTC - EXTRACT OPTION RECORDS FOR ATXTRACT                      *          
*                                                                    *          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(ACCFILE RECORD)                                              *          
* AL4 0=ACCFILE-TO-EXTRACT                                           *          
* AL4 A(SXDTABD)                                                     *          
**********************************************************************          
*         SPACE 1                                                               
*ATXOPTC  NMOD1 WORKX-WORKD,*ATXOPTC*,RR=RA                                     
*         USING WORKD,RC                                                        
*         L     RA,ATXOPLIT                                                     
*         USING LITERAL,RA                                                      
*         ST    R1,APARM                                                        
*         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                      
*         LM    R2,R3,0(R1)                                                     
*         L     R4,12(R1)                                                       
*         USING ATXRECD,R2          R2=A(EXTRACT RECORD)                        
*         USING POPRECD,R3          R3=A(ACCFILE RECORD)                        
*         USING SXDTABD,R4          R4=A(SXDXTAB)                               
*                                                                               
*         XC    ATXRELEN,ATXRELEN   RECORD LENGTH/TYPE/ENDOFREC                 
*         MVC   ATXRELEN(2),=Y(ATXOPTL)                                         
*         MVC   ATXRETYP,ATXOPTQ                                                
*         MVI   ATXOPTX,MXTRTQ                                                  
*         MVI   ATXOPTX+1,MXTRTQ                                                
*         MVI   ATXOPAID-1,MXTRTQ   2-BYTE AGY ALPHA                            
*         MVI   ATXOPOGC-1,MXTRTQ   OFFICE GROUP CODE                           
*         MVI   ATXOPOFC-1,MXTRTQ   OFFICE CODE                                 
*         MVI   ATXOPCLI-1,MXTRTQ   CLIENT CODE                                 
*         MVI   ATXOPPRO-1,MXTRTQ   PRODUCT CODE                                
*         MVI   ATXOPJOB-1,MXTRTQ   JOB CODE                                    
*         MVI   ATXOPMGC-1,MXTRTQ   MEDIA GROUP CODE                            
*         MVI   ATXOPMCD-1,MXTRTQ   MEDIA CODE                                  
*                                                                               
*         MVC   ATXOPAID,SXDTAGY    AGENCY 2-BYTE ALPHA                         
*         TR    ATXOPAID,MXTRT                                                  
*                                                                               
*         MVC   ATXOPOGC,POPKOFG    OFFICE GROUP CODE                           
*         TR    ATXOPOGC,MXTRT                                                  
*                                                                               
*         MVC   ATXOPOFC,POPKOFC    OFFICE CODE                                 
*         TR    ATXOPOFC,MXTRT                                                  
*                                                                               
*         MVC   ATXOPCLI,POPKCLI    CLIENT CODE                                 
*         TR    ATXOPCLI,MXTRT                                                  
*                                                                               
*         MVC   ATXOPPRO,POPKPRO    PRODUCT CODE                                
*         TR    ATXOPPRO,MXTRT                                                  
*                                                                               
*         MVC   ATXOPJOB(L'POPKJOB),POPKJOB    JOB CODE                         
*         TR    ATXOPJOB,MXTRT                                                  
*                                                                               
*         MVC   ATXOPMGC,POPKMGR    MEDIA GROUP CODE                            
*         TR    ATXOPMGC,MXTRT                                                  
*                                                                               
*         MVC   ATXOPMCD,POPKMED    MEDIA CODE                                  
*         TR    ATXOPMCD,MXTRT                                                  
*         J     EXIT                                                            
*         DROP  R2,R3,R4                                                        
*                                                                               
*ATXOPLIT DC    A(LITERAL)                                                      
*         EJECT                                                                 
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
*         SPACE 1                                                               
*         LTORG                                                                 
*         EJECT                                                                 
***********************************************************************         
* ATXSTCC - EXTRACT OFFICE RECORDS FOR ATXTRACT                       *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(LEVELS) - CAN GET LENGTHS OF LEVELS FROM HERE                 *         
***********************************************************************         
         SPACE 1                                                                
ATXSTCC  NMOD1 WORKX-WORKD,*ATXSTCC*,RR=RA                                      
         USING WORKD,RC                                                         
         L     RA,ATXSTLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         MVC   ALEVELS,16(R1)                                                   
         USING ATXRECD,R2          R2=A(EXTRACT RECORD)                         
         USING SCMRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         XC    ATXRELEN,ATXRELEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   ATXRELEN(2),=Y(ATXSTCL)                                          
         MVC   ATXRETYP,ATXSTCQ                                                 
         MVI   ATXSTAID-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   ATXSTCCD-1,MXTRTQ   OFFICE CODE                                  
         MVI   ATXSTCCM-1,MXTRTQ   OFFICE NAME                                  
         MVI   ATXSTCX,MXTRTQ                                                   
         MVI   ATXSTCX+1,MXTRTQ                                                 
*                                                                               
         MVC   ATXSTAID,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         TR    ATXSTAID,MXTRT                                                   
         MVC   ATXSTCCD,SCMKCODE   STANDARD COMMENT CODE                        
         TR    ATXSTCCD,MXTRT                                                   
*                                                                               
         LA    R5,SCMRFST          R5=A(FIRST ELEMENT)                          
ATXST10  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),SCMELQ        X'3E' - STANDARD COMMENT ELEMENT             
         JE    ATXST30                                                          
*                                                                               
ATXST20  SR    RF,RF               BUMP TO NEXT ELEMENT                         
         ICM   RF,1,1(R5)                                                       
         JZ    EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         J     ATXST10                                                          
*                                                                               
         USING SCMELD,R5                                                        
ATXST30  XC    ATXSTCCM,ATXSTCCM   CLEAR COMMENT FIELD                          
         CLI   SCMTYPE,SCMTSTND    STANDARD COMMENT?                            
         JNE   ATXST40                                                          
         SR    R1,R1                                                            
         IC    R1,SCMLN                                                         
         SHI   R1,SCMLN1Q                                                       
         BNP   ATXST40                                                          
         BCTR  R1,0                                                             
         EX    R1,*+4                                                           
         MVC   ATXSTCCM(0),SCMNARR                                              
ATXST40  TR    ATXSTCCM,MXTRT                                                   
         J     ATXST20                                                          
         DROP  R2,R3,R4,R5                                                      
*                                                                               
ATXSTLIT DC    A(LITERAL)                                                       
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ATXUSRC - EXTRACT OFFICE RECORDS FOR ATXTRACT                       *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(LEVELS) - CAN GET LENGTHS OF LEVELS FROM HERE                 *         
***********************************************************************         
         SPACE 1                                                                
ATXUSRC  NMOD1 WORKX-WORKD,*ATXUSRC*,RR=RA                                      
         USING WORKD,RC                                                         
         L     RA,ATXUSLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         MVC   ALEVELS,16(R1)                                                   
         USING ATXRECD,R2          R2=A(EXTRACT RECORD)                         
         USING UFSRECD,R3          R3=A(ACCFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         CLI   8(R1),1                                                          
         JL    ATXUSKIL            BUILD K RECORD                               
         JE    ATXUSSER            BUILD SERIES OF RECORDS                      
         JH    ATXUSNXT            OUTPUT NEXT RECORD                           
*                                                                               
ATXUSRCC DC    A(BUFFER1)          OFFICE LIST ENTRY SAVE AREA                  
ATXUSLIT DC    A(LITERAL)                                                       
         EJECT                                                                  
***********************************************************************         
* BUILD 'K' RECORD AND EXIT                                           *         
***********************************************************************         
ATXUSKIL BRAS  RE,ATXUSBLK                                                      
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* GET NEXT RECORD AND PASS IT BACK                                    *         
***********************************************************************         
         SPACE 1                                                                
ATXUSNXT L     RE,ANEXT                                                         
         OC    0(8,RE),0(RE)                                                    
         JNZ   *+12                                                             
         MVI   8(R1),X'FF'         NO MORE RECORDS                              
         J     EXIT                                                             
*                                                                               
         L     RE,ANEXT            MOVE IN NEXT RECORD                          
         LA    R0,ATXRECD                                                       
         LH    RF,=Y(ATXUSRL)                                                   
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LH    RF,=Y(ATXUSRL)      UPDATE POINTER                               
         L     RE,ANEXT                                                         
         AR    RE,RF                                                            
         ST    RE,ANEXT                                                         
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* BUILD SERIES OF RECORDS FOR THIS RECORD                             *         
***********************************************************************         
         SPACE 1                                                                
ATXUSSER BRAS  RE,ATXUSBLK         SET UP BLANK RECORD (KEY DETAIL)             
*                                                                               
         LH    RF,=Y(ATXUSRL)                                                   
         STH   RF,RECLEN                                                        
         L     R2,ATXUSRCC         WHERE RECORDS TO BE BUILT                    
         XC    0(8,R2),0(R2)                                                    
         ST    R2,ANEXT            FOR NEXT TIME IN                             
*                                                                               
         LA    R5,UFSRFST          R5=A(FIRST ELEMENT)                          
         MVI   BYTE,0              CLEAR BYTE AS THE USRFLD SEQ ACCUM           
ATXUS10  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    EXIT                                                             
         CLI   0(R5),UFSELQ        USER FIELD ELEMENT                           
         JE    ATXUS30                                                          
*                                                                               
ATXUS20  SR    RF,RF               BUMP TO NEXT ELEMENT                         
         ICM   RF,1,1(R5)                                                       
         JZ    EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,RF                                                            
         J     ATXUS10                                                          
*                                                                               
         USING UFSELD,R5                                                        
ATXUS30  DS    0H                                                               
         L     RE,AXREC            BLANK RECORD                                 
         LR    R0,R2                                                            
         LH    RF,RECLEN           LENGTH OF A BLANK RECORD                     
         LR    R1,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LH    RF,RECLEN                                                        
         AR    RF,R2                                                            
         XC    0(8,RF),0(RF)       CLEAR START OF NEXT DATA SPACE               
*                                                                               
         SR    R1,R1                                                            
         IC    R1,BYTE                                                          
         LA    R1,1(R1)                                                         
         STC   R1,BYTE                                                          
         EDIT  BYTE,(3,ATXUSSEQ),FILL=0,ZERO=NOBLANK                            
*        EDIT  UFSSEQ,(3,ATXUSSEQ),FILL=0,ZERO=NOBLANK                          
         TR    ATXUSSEQ,MXTRT      USER FIELD SEQ NUMBER                        
         MVC   ATXUSFLD,UFSCODE    USER FIELD CODE                              
         TR    ATXUSFLD,MXTRT                                                   
         MVC   ATXUSNAM,UFSDESC    USER FIELD DESCRIPTION                       
         TR    ATXUSNAM,MXTRT                                                   
         MVC   ATXUSERL,UFSEDIT    EDIT RULE                                    
         TR    ATXUSERL,MXTRT                                                   
         EDIT  UFSMXLN,(3,ATXUSMXL),FILL=0,ZERO=NOBLANK                         
         TR    ATXUSMXL,MXTRT      MAX LENGTH                                   
         MVI   ATXUSISR,C'N'       SET NOT REQUIRED AS DEFAULT                  
         TM    UFSSTAT,UFSSREQD    IS REQUIRED BIT                              
         BNO   *+8                                                              
         MVI   ATXUSISR,C'Y'       SET AS REQUIRED                              
         MVC   ATXUSCDT,SPACES     ASSUME NO CUTOFF DATE                        
         OC    UFSCUT,UFSCUT       IS THERE A CUTOFF DATE?                      
         JZ    ATXUS40                                                          
*&&US*&& GOTO1 VDATCON,DMCB,(1,UFSCUT),(23,ATXUSCDT),0                          
*&&UK*&& GOTO1 VDATCON,DMCB,(1,UFSCUT),(10,ATXUSCDT),0                          
         TR    ATXUSCDT,MXTRT                                                   
*                                                                               
ATXUS40  AH    R2,RECLEN                                                        
         J     ATXUS20                                                          
         EJECT                                                                  
***********************************************************************         
* BUILD BLANK RECORD AND INPUT KEY FIELDS-USER FIELDS RECORDS         *         
***********************************************************************         
         SPACE 1                                                                
ATXUSBLK NTR1                                                                   
         XC    ATXRELEN,ATXRELEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   ATXRELEN(2),=Y(ATXUSRL)                                          
         MVC   ATXRETYP,ATXUSRQ                                                 
         MVI   ATXUSAID-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   ATXUSLDG-1,MXTRTQ   LEDGER                                       
         MVI   ATXUSOFG-1,MXTRTQ   OFFICE GROUP                                 
         MVI   ATXUSOFF-1,MXTRTQ   OFFICE                                       
         MVI   ATXUSCLI-1,MXTRTQ   CLIENT                                       
         MVI   ATXUSPRO-1,MXTRTQ   PRODUCT                                      
         MVI   ATXUSMDG-1,MXTRTQ   MEDIA GROUP                                  
         MVI   ATXUSMED-1,MXTRTQ   MEDIA                                        
         MVI   ATXUSJOB-1,MXTRTQ   JOB                                          
         MVI   ATXUSSEQ-1,MXTRTQ   SEQUENCE NUMBER                              
         MVI   ATXUSFLD-1,MXTRTQ   USER FIELD CODE                              
         MVI   ATXUSNAM-1,MXTRTQ   NAME                                         
         MVI   ATXUSERL-1,MXTRTQ   EDIT RULE                                    
         MVI   ATXUSMXL-1,MXTRTQ   MAX LENGTH                                   
         MVI   ATXUSISR-1,MXTRTQ   ISREQUIRED BIT                               
         MVI   ATXUSCDT-1,MXTRTQ   CUTOFF DATE                                  
         MVI   ATXUSRX,MXTRTQ                                                   
         MVI   ATXUSRX+1,MXTRTQ                                                 
*                                                                               
         MVC   ATXUSAID,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         TR    ATXUSAID,MXTRT                                                   
         MVC   ATXUSLDG,UFSKUNT    UNIT/LEDGER                                  
         TR    ATXUSLDG,MXTRT                                                   
         MVC   ATXUSOFG,UFSKOFG    OFFICE GROUP                                 
         TR    ATXUSOFG,MXTRT                                                   
         MVC   ATXUSOFF,UFSKOFC    OFFICE                                       
         TR    ATXUSOFF,MXTRT                                                   
         MVC   ATXUSCLI,UFSKCLI    CLIENT                                       
         TR    ATXUSCLI,MXTRT                                                   
         MVC   ATXUSPRO,UFSKPRO    PRODUCT                                      
         TR    ATXUSPRO,MXTRT                                                   
         MVC   ATXUSMDG,UFSKMGR    MEDIA GROUP                                  
         TR    ATXUSMDG,MXTRT                                                   
         MVC   ATXUSMED,UFSKMED    MEDIA                                        
         TR    ATXUSMED,MXTRT                                                   
         MVC   ATXUSJOB,UFSKJOB    JOB                                          
         TR    ATXUSJOB,MXTRT                                                   
         J     EXIT                                                             
         DROP  R2,R3,R4,R5                                                      
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ATXCAGC - EXTRACT CONTROL AGENCY RECORDS FOR ATXTRACT               *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(CONFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
***********************************************************************         
         SPACE 1                                                                
ATXCAGC  NMOD1 WORKX-WORKD,*ATXCAGC*,RR=RA                                      
         USING WORKD,RC                                                         
         L     RA,ATXCALIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING ATXRECD,R2          R2=A(EXTRACT RECORD)                         
         USING CT5REC,R3           R3=A(CONFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         XC    ATXRELEN,ATXRELEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   ATXRELEN(2),=Y(ATXCAGL)                                          
         MVC   ATXRETYP,ATXCAGQ                                                 
         MVI   ATXCAAID-1,MXTRTQ   2-BYTE AGY ALPHA                             
         MVI   ATXCASEC-1,MXTRTQ   2-BYTE SEC AGY ALPHA                         
         MVI   ATXCAGX,MXTRTQ                                                   
         MVI   ATXCAGX+1,MXTRTQ                                                 
*                                                                               
         MVC   ATXCAAID,SXDTAGY    AGENCY 2-BYTE ALPHA                          
         TR    ATXCAAID,MXTRT                                                   
         MVC   ATXCASEC,SXDTAGY    AGENCY AS DEFAULT                            
         TR    ATXCASEC,MXTRT                                                   
*                                                                               
         LA    R5,CT5DATA          R4=A(FIRST ELEMENT)                          
ATXCAG10 CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         BE    ATXCAX                                                           
         CLI   0(R5),CTSEAELQ      GET SECURITY AGENCY ELEMENT                  
         BE    ATXCAG20                                                         
*                                                                               
         SR    R1,R1               BUMP TO NEXT ELEMENT                         
         ICM   R1,1,1(R5)                                                       
         JNH   ATXCAX              EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,R1                                                            
         J     ATXCAG10                                                         
*                                                                               
         USING CTSEAD,R5                                                        
ATXCAG20 MVC   ATXCASEC,CTSEAAID   EXTRACT SECURITY AGENCY ID                   
*                                                                               
ATXCAX   DS    0H                                                               
         J     EXIT                                                             
         DROP  R2,R3,R4,R5                                                      
*                                                                               
ATXCALIT DC   A(LITERAL)                                                        
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* ATXCPEC - EXTRACT CONTROL PERSON RECORDS FOR ATXTRACT               *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(CONFILE RECORD)                                               *         
* AL4 0=ACCFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(LEVELS) - CAN GET LENGTHS OF LEVELS FROM HERE                 *         
***********************************************************************         
         SPACE 1                                                                
ATXCPEC  NMOD1 WORKX-WORKD,*ATXCPEC*,RR=RA                                      
         USING WORKD,RC                                                         
         L     RA,ATXCOLIT                                                      
         USING LITERAL,RA                                                       
         ST    R1,APARM                                                         
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         L     R4,12(R1)                                                        
         USING ATXRECD,R2          R2=A(EXTRACT RECORD)                         
         USING SAPEREC,R3          R3=A(CONFILE RECORD)                         
         USING SXDTABD,R4          R4=A(SXDXTAB)                                
*                                                                               
         XC    ATXRELEN,ATXRELEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   ATXRELEN(2),=Y(ATXCPEL)                                          
         MVC   ATXRETYP,ATXCPEQ                                                 
         MVI   ATXCPSID-1,MXTRTQ   2-BYTE SEC AGY ALPHA                         
         MVI   ATXCPCSC-1,MXTRTQ   CONSYSPERSON CODE                            
         MVI   ATXCPEFD-1,MXTRTQ   CONTROL PERSON  EFFECTIVE DATES              
         MVI   ATXCPEIL-1,MXTRTQ   CONTROL PERSON  EMAIL ID                     
         MVI   ATXCPEX,MXTRTQ                                                   
         MVI   ATXCPEX+1,MXTRTQ                                                 
*                                                                               
         MVC   ATXCPSID,SAPEAGY    AGENCY 2-BYTE ALPHA                          
         TR    ATXCPSID,MXTRT                                                   
         MVC   EFFDATE,SAPEDEF                                                  
         XC    EFFDATE,FFILL                                                    
*&&US*&& GOTO1 VDATCON,DMCB,(2,EFFDATE),(23,ATXCPEFD)                           
*&&UK*&& GOTO1 VDATCON,DMCB,(2,EFFDATE),(10,ATXCPEFD)                           
         TR    ATXCPEFD,MXTRT                                                   
*                                                                               
         MVC   ATXCPEIL,=X'0001'   SET DEFAULT LENGTH TO 1                      
         MVC   ATXCPEID,SPACES                                                  
         LA    R5,SAPEDATA         R4=A(FIRST ELEMENT)                          
ATXCPE10 CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         JE    ATXCPEXX                                                         
         CLI   0(R5),SAPWDELQ      GET PERSON PASSWORD ELEMENT                  
         JE    ATXCPE30                                                         
         CLI   0(R5),SAPEEELQ      GET PERSON DETAILS ELEMENT                   
         JE    ATXCPE40                                                         
*                                                                               
ATXCPE20 SR    R1,R1               BUMP TO NEXT ELEMENT                         
         ICM   R1,1,1(R5)                                                       
         CH    RF,=H'1'                                                         
         JNH   ATXCPEXX            EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R5,R1                                                            
         J     ATXCPE10                                                         
*                                                                               
         USING SAPWDD,R5                                                        
ATXCPE30 SR    R0,R0               CONSYSPERSON CODE                            
         ICM   R0,3,SAPWDNUM                                                    
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  ATXCPCSC,DUB+5(3)                                                
         J     ATXCPE20                                                         
*                                                                               
         USING SAPEED,R5                                                        
ATXCPE40 SR    R1,R1               PERSON EMAIL ID                              
         IC    R1,SAPEELN                                                       
         SHI   R1,SAPEELNQ                                                      
         STCM  R1,3,ATXCPEIL       SAVE OFF LENGTH                              
         SHI   R1,1                                                             
         JNP   ATXCPE20                                                         
         EX    R1,*+4                                                           
         MVC   ATXCPEID(0),SAPEEID                                              
         TR    ATXCPEID,MXTRT                                                   
         J     ATXCPE20                                                         
*                                                                               
ATXCPEXX J     EXIT                LABELED ATXCPEXX DUE TO CONFLICT             
         DROP  R2,R3,R4,R5                                                      
*                                                                               
ATXCOLIT DC    A(LITERAL)                                                       
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* COMMON ROUTINES/UTILITIES                                          *          
**********************************************************************          
         SPACE 1                                                                
LITERAL  DS    0D                                                               
ANEXT    DC    F'0'                                                             
ANEXT2   DC    F'0'                                                             
*                                                                               
SVSECAGY DS    CL2                 SAVED AREA FOR SECURITY AGENCY               
*                                                                               
SVACT    DS    CL1                 SAVED ACTION FROM PREVIOUS REC               
SVDAT    DS    CL8                 SAVED DATE   FROM PREVIOUS REC               
SVTIM    DS    CL6                 SAVED TIME   FROM PREVIOUS REC               
*                                                                               
P6       DS    PL6                                                              
*                                                                               
EFFDATE  DS    XL2                                                              
*                                                                               
SPACES   DC    CL80' '                                                          
FFILL    DC    80X'FF'                                                          
*                                                                               
JOBSTAT  DS    XL1                                                              
VDATCON  DC    V(DATCON)                                                        
VHEXOUT  DC    V(HEXOUT)                                                        
VPROCAMT DC    V(PROCAMNT)                                                      
         EJECT                                                                  
**********************************************************************          
* INCLUDES AND BUFFERS                                               *          
**********************************************************************          
         SPACE 1                                                                
*MXTRT                                                                          
       ++INCLUDE MXTRT                                                          
         EJECT                                                                  
*ATXRECID                                                                       
       ++INCLUDE ATXRECID                                                       
         EJECT                                                                  
       ++INCLUDE FALANGTAB                                                      
         EJECT                                                                  
                                                                                
         DC    CL8'BUFFER01'                                                    
BUFFER1  DC    3000XL8'00'         HOLDS RECORDS FOR WRITING TO FILE            
BUFFER1L EQU   *-BUFFER1                                                        
         DC    8X'FF'                                                           
*                                                                               
         DC    CL8'BUFFER02'                                                    
BUFFER2  DC    3000XL8'00'         HOLDS RECORDS FOR WRITING TO FILE            
BUFFER2L EQU   *-BUFFER2                                                        
         DC    8X'FF'                                                           
*                                                                               
WORKD    DSECT                                                                  
PARM     DS    0XL20               PARAMETER LIST                               
AXREC    DS    A                   A(EXTRACT RECORD)                            
AFREC    DS    A                   A(ACCFILE RECORD)                            
         DS    A                                                                
         DS    A                                                                
         DS    A                                                                
*                                                                               
DUB      DS    D                                                                
WORK     DS    XL20                                                             
APARM    DS    A                   A(PARAMETER LIST)                            
DMWORK   DS    12D                                                              
RELO     DS    A                                                                
ALEVELS  DS    A                                                                
DMCB     DS    6F                                                               
HALF     DS    H                                                                
RECLEN   DS    H                                                                
LASTLEN  DS    H                                                                
LASTLEN1 DS    H                                                                
BYTE     DS    XL1                                                              
ACCADDR  DS    XL4                                                              
IOKEY    DS    CL64                                                             
*                                                                               
IOAREA   DS    2048X                                                            
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE DXDSECTS                                                       
       ++INCLUDE ATXRECD                                                        
       ++INCLUDE ACTOBACCOD                                                     
       ++INCLUDE ACGENFILE                                                      
       ++INCLUDE CTGENFILE                                                      
       ++INCLUDE SEACSFILE                                                      
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'009ATXROUTS  06/09/03'                                      
         END                                                                    
