*          DATA SET CPREPORT   AT LEVEL 009 AS OF 05/01/02                      
*CATALP CPREPORT                                                                
         TITLE 'MODULE EXECUTES SPECS AND CONTROLS PRINTING'                    
CPREPORT CSECT                                                                  
         NMOD1 91,**REPT**                                                      
         USING CPREPD,RC                                                        
         L     RA,=V(CPWORKC)                                                   
         USING CPWORKD,RA                                                       
         LA    R9,2048(RA)                                                      
         LA    R9,2048(R9)                                                      
         USING CPWORKD+4096,R9                                                  
         LA    R8,2048(RB)                                                      
         LA    R8,2048(R8)                                                      
         USING CPREPORT+4096,R8                                                 
         PRINT NOGEN                                                            
         EJECT                                                                  
*                   OPTION JUST TO CLEAR ALL PRINT LINES                        
         SPACE 3                                                                
         CLI   FORCECLR,C'N'                                                    
         BE    PC4                                                              
         MVI   FORCECLR,C'N'                                                    
         SPACE 2                                                                
PC1      LA    R2,HEAD1                                                         
         LA    R3,SPACES                                                        
         LA    R4,14                                                            
         SPACE 2                                                                
PC2      MVC   0(132,R2),0(R3)                                                  
         LA    R2,132(R2)                                                       
         BCT   R4,PC2                                                           
         MVC   MID1,SPACES                                                      
         MVC   MID2,SPACES                                                      
         SPACE 2                                                                
PC3      LA    R2,P                                                             
         LA    R3,14                                                            
         SPACE 2                                                                
PC3A     MVC   0(132,R2),SPACES                                                 
         LA    R2,132(R2)                                                       
         BCT   R3,PC3A                                                          
         MVC   FOOT1,SPACES                                                     
         MVC   FOOT2,SPACES                                                     
         B     PCEXT                                                            
         EJECT                                                                  
*                   CONTROL SPEC PROCESSING                                     
         SPACE 2                                                                
PC4      CLI   SKIPSPEC,C'Y'                                                    
         BE    PC6                                                              
         CLI   RCFLONLY,C'Y'                                                    
         BE    PC5                                                              
         CLI   FORCEHED,C'Y'       DONT EXECUTE SPECS IF JUST                   
         BE    PC5                 HANDLING PLINES                              
         CLI   FORCEMID,C'Y'                                                    
         BE    PC5                                                              
         CLI   FORCEFUT,C'Y'                                                    
         BE    PC5                                                              
         LA    R2,P14                                                           
         LA    R3,14                                                            
         SPACE 1                                                                
PC4D     CLC   0(132,R2),SPACES    COUNT NUMBER OF ACTIVE PRINT LINES           
         BNE   PC4F                                                             
         SH    R2,=H'132'                                                       
         BCT   R3,PC4D                                                          
         LA    R3,1                                                             
         SPACE 1                                                                
PC4F     ZIC   R2,SPACING          PLUS SPACING                                 
         AR    R3,R2                                                            
         BCTR  R3,0                -1                                           
         ZIC   R2,LINE                                                          
         AR    R3,R2               PLUS PRESENT LINE COUNT                      
         ZIC   R2,MAXLINES                                                      
         CR    R3,R2                                                            
         BNH   PC4H                                                             
         MVI   FORCEHED,C'Y'                                                    
         B     PC5                                                              
         SPACE 1                                                                
PC4H     SR    RF,RF               CHECK IF LINE + ALLOWLIN EXCEEDS MAX         
         IC    RF,LINE                                                          
         SR    R0,R0                                                            
         IC    R0,ALLOWLIN                                                      
         AR    RF,R0                                                            
         IC    R0,MAXLINES                                                      
         CR    RF,R0                                                            
         BL    PC14                                                             
         MVI   FORCEHED,C'Y'       SO ENSURE HEADLINES                          
         SPACE 2                                                                
PC5      EQU   *                                                                
         BAS   RE,PROCSPEC                                                      
         SPACE 2                                                                
PC5A     CLI   RCFLONLY,C'Y'                                                    
         BNE   PC6                                                              
         MVI   RCFLONLY,C'N'                                                    
         B     PCEXT                                                            
         EJECT                                                                  
*                   CONTROL OF HEADLINE PRINTING                                
         SPACE 3                                                                
PC6      CLI   FORCEHED,C'N'                                                    
         BE    PC12                                                             
         MVI   FORCEHED,C'N'                                                    
         SPACE 2                                                                
PC10     MVI   FORCEMID,C'Y'                                                    
         CLC   FOOT1(132),SPACES                                                
         BE    PC11                                                             
         CLI   FIRSTFUT,C'Y'                                                    
         BE    PC11                                                             
         GOTO1 PRINT,REPARA,SPACES,=C'BC09'                                     
         LA    R2,FOOT1                                                         
         LA    R3,2                                                             
         BAS   RE,PRINCON                                                       
         MVI   FORCEFUT,C'N'                                                    
         B     PC11                                                             
         SPACE 2                                                                
FIRSTFUT DC    C'Y'                                                             
         SPACE 2                                                                
PC11     MVI   FIRSTFUT,C'N'                                                    
         GOTO1 PRINT,REPARA,SPACES,=C'BC01'                                     
         OC    HEADHOOK,HEADHOOK   USER SUPPLIED HEADLINE HOOK                  
         BZ    PC11A                                                            
         GOTO1 HEADHOOK,REPARA,(RA)                                             
         SPACE 2                                                                
PC11A    MVI   LINE,1                                                           
         LA    R2,HEAD1                                                         
         LA    R3,14                                                            
         BAS   RE,PRINCON                                                       
         SR    R4,R4                                                            
         MVC   REPDUB(4),PAGE      ADD 1 TO PAGE AND SUBPAGE                    
         LH    R4,REPDUB                                                        
         SLL   R4,16                                                            
         SRL   R4,16                                                            
         AH    R4,=H'1'                                                         
         STH   R4,REPDUB                                                        
         LH    R4,REPDUB+2                                                      
         SLL   R4,16                                                            
         SRL   R4,16                                                            
         AH    R4,=H'1'                                                         
         STH   R4,REPDUB+2                                                      
         MVC   PAGE(4),REPDUB                                                   
         EJECT                                                                  
*                   CONTROL OF MIDLINE PRINTING                                 
         SPACE 3                                                                
PC12     CLI   FORCEMID,C'N'                                                    
         BE    PC14                                                             
         MVI   FORCEMID,C'N'                                                    
         OC    MIDHOOK,MIDHOOK     USER SUPPLIED MIDLINE HOOK                   
         BZ    PC12A                                                            
         GOTO1 MIDHOOK,REPARA,(RA)                                              
         SPACE 2                                                                
PC12A    LA    R2,MID1                                                          
         LA    R3,2                                                             
         BAS   RE,PRINCON                                                       
         CLC   LINE,MAXLINES                                                    
         BNL   PC10                                                             
         SPACE 2                                                                
*                   CONTROL OF PRINT LINES                                      
         SPACE 3                                                                
PC14     LA    R2,P                                                             
         LA    R3,14                                                            
         BAS   RE,PRINP                                                         
         MVI   SPACING,1                                                        
         SPACE 2                                                                
*                   CONTROL OF FOOTLINE PRINTING                                
         SPACE 3                                                                
         CLI   FORCEFUT,C'N'                                                    
         BE    PC1                                                              
         MVI   FORCEFUT,C'N'                                                    
         GOTO1 PRINT,REPARA,SPACES,=C'BC09'                                     
         LA    R2,FOOT1                                                         
         LA    R3,2                                                             
         BAS   RE,PRINCON                                                       
         MVI   FORCEHED,C'Y'                                                    
         CLI   CLEARHED,C'N'       GO BACK TO CLEAR JUST THE PRINT              
         BE    PC3                 LINES OR, OPTIONALLY, ALL LINES              
         B     PC1                                                              
         EJECT                                                                  
*                   CONTROL PRINTING OF A BLOCK                                 
         SPACE 3                                                                
PRINCON  NTR                                                                    
         MVC   REPWORK,SPACES                                                   
         LR    R4,R2                                                            
         LR    R5,R3                                                            
         LA    R6,REPWORK                                                       
         SPACE 2                                                                
PC20     CLC   0(132,R4),SPACES    BUILD A TABLE OF PRINT LINE BYTES            
         BE    *+8                                                              
         MVI   0(R6),C'P'                                                       
         LA    R4,132(R4)                                                       
         LA    R6,1(R6)                                                         
         BCT   R5,PC20                                                          
         SPACE 2                                                                
*                                  NOW CONVERT TO PRINTING NUMBERS              
         LA    R6,REPWORK                                                       
         SPACE 2                                                                
PC22     CLC   0(16,R6),SPACES     ANY LEFT                                     
         BE    PC30                NO                                           
         SPACE 2                                                                
PC26     LA    R8,2                                                             
         CLC   1(15,R6),SPACES     IS THIS THE LAST                             
         BE    PC28                                                             
         CLC   1(2,R6),=C' P'                                                   
         BE    PC28                                                             
         LA    R8,1                                                             
         CLI   1(R6),C'P'                                                       
         BE    PC28                                                             
         LA    R8,3                                                             
         SPACE 2                                                                
PC28     STC   R8,0(R6)                                                         
         AR    R6,R8                                                            
         SR    R7,R7                                                            
         IC    R7,LINE                                                          
         AR    R7,R8                                                            
         STC   R7,LINE                                                          
         B     PC22                                                             
         SPACE 2                                                                
PC30     LA    R6,REPWORK          NOW PRINT R2 = DATA                          
*                                            R3 = N'LINES                       
*                                            R6 = SKIP NUMBERS                  
PC32     CLI   0(R6),C' '                                                       
         BE    PC34                                                             
         MVC   CONTROL(3),=C'BL0'                                               
         MVC   CONTROL+3(1),0(R6)                                               
         OI    CONTROL+3,X'F0'                                                  
         GOTO1 PRINT,REPARA,(R2),CONTROL                                        
         SPACE 2                                                                
PC34     LA    R2,132(R2)                                                       
         LA    R6,1(R6)                                                         
         BCT   R3,PC32                                                          
         XIT                                                                    
         EJECT                                                                  
*                   SPECIAL ROUTINE FOR PRINT LINES                             
         SPACE 2                                                                
PRINP    NTR                                                                    
         BCTR  R3,R0                                                            
         SPACE 2                                                                
PRINP2   CLC   132(132,R2),SPACES  SINGLE PRINT UNTIL LAST                      
         BE    PC36                                                             
         GOTO1 PRINT,REPARA,(R2),=C'BL01'                                       
         SR    R1,R1                                                            
         IC    R1,LINE                                                          
         LA    R1,1(R1)                                                         
         STC   R1,LINE                                                          
         LA    R2,132(R2)                                                       
         BCT   R3,PRINP2                                                        
         SPACE 2                                                                
PC36     MVC   CONTROL(3),=C'BL0'  USERS SPACING FOR ONE OR 2/2                 
         MVC   CONTROL+3(1),SPACING                                             
         OI    CONTROL+3,X'F0'                                                  
         GOTO1 PRINT,REPARA,(R2),CONTROL                                        
         SR    R0,R0                                                            
         SR    R1,R1                                                            
         IC    R1,SPACING                                                       
         IC    R0,LINE                                                          
         AR    R1,R0                                                            
         STC   R1,LINE                                                          
         XIT                                                                    
         SPACE 2                                                                
PCEXT    MVI   SKIPSPEC,C'N'                                                    
         XMOD1 1                                                                
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
*                   EXECUTE THE SPEC POOL                                       
         SPACE 3                                                                
PROCSPEC NTR                                                                    
         L     R2,SPECS            POINT TO SPEC POOL                           
         SR    R3,R3                                                            
         MVI   PROGSW,C'Y'         PRESET SWITCHES                              
         MVI   FILTSW,C'Y'                                                      
         CLI   RCSUBPRG,0                                                       
         BE    EX1                                                              
         MVI   PROGSW,C'N'                                                      
         SPACE 2                                                                
EX1      CLI   RCFLONLY,C'Y'       PRESET I/O VALUES TO N                       
         BNE   EX4                                                              
         MVC   FCOMMAND(24),=24C'N'                                             
         B     EX4                                                              
         SPACE 2                                                                
         SPACE 2                                                                
EX2      IC    R3,1(R2)                                                         
         AR    R2,R3               GET NEXT SPEC                                
         SPACE 2                                                                
EX4      SR    R4,R4                                                            
         IC    R4,0(R2)                                                         
         SLL   R4,2                                                             
         L     R4,INBRTAB(R4)      GO TO ROUTINE DEPENDENT                      
         BR    R4                  ON THE VALUE OF ELEMENT                      
         SPACE 2                                                                
EX6      XIT                                                                    
         SPACE 2                                                                
EX8      MVI   PROGSW,C'N'                                                      
         MVI   FILTSW,C'Y'         RESET FOR SUB-PROGRAM FILTERING              
         B     EX2                                                              
         SPACE 2                                                                
EX10     CLC   RCSUBPRG(1),2(R2)   CHECK MATCH OF SUB-PROGRAM                   
         BNE   EX2                                                              
         MVI   PROGSW,C'Y'                                                      
         B     EX2                                                              
         SPACE 2                                                                
EX12     CLC   RCSUBPRG(1),2(R2)   SPROG N THRU M FEATURE                       
         BL    EX2                                                              
         CLC   RCSUBPRG(1),3(R2)                                                
         BH    EX2                                                              
         MVI   PROGSW,C'Y'                                                      
         B     EX2                                                              
         SPACE 2                                                                
EX20     MVC   FCPHASE,2(R2)       USE 'PHASE' FEATURE                          
         B     EX2                                                              
         EJECT                                                                  
*                   FILE AND RUN CONTROL SPECS                                  
         SPACE 3                                                                
EX26     CLI   RCFLONLY,C'Y'                                                    
         BNE   EX2                                                              
         CLI   FILTSW,C'Y'                                                      
         BNE   EX2                                                              
         CLI   PROGSW,C'Y'                                                      
         BNE   EX2                                                              
         SPACE 1                                                                
         SR    R4,R4                                                            
         IC    R4,2(R2)                                                         
         BCTR  R4,R0                                                            
         LA    R4,FCOMMAND(R4)                                                  
         MVI   0(R4),C'Y'                                                       
         B     EX2                                                              
         SPACE 2                                                                
EX28     CLI   RCFLONLY,C'Y'                                                    
         BNE   EX2                                                              
         CLI   FILTSW,C'Y'                                                      
         BNE   EX2                                                              
         CLI   PROGSW,C'Y'                                                      
         BNE   EX2                                                              
         CLI   2(R2),2                                                          
         BH    EX32                                                             
         BE    EX30                                                             
         MVI   SPACING,1                                                        
         B     EX2                                                              
         SPACE 2                                                                
EX30     MVI   SPACING,2                                                        
         B     EX2                                                              
         SPACE 2                                                                
EX32     CLI   2(R2),4                                                          
         BH    EX36                                                             
         BE    EX34                                                             
         MVI   SPACING,3                                                        
         B     EX2                                                              
         SPACE 2                                                                
EX34     MVI   RCLINEUP,C'Y'                                                    
         B     EX2                                                              
         SPACE 2                                                                
EX36     CLI   2(R2),6                                                          
         BH    EX40                                                             
         BE    EX38                                                             
         MVI   RCREQREP,C'Y'                                                    
         B     EX2                                                              
         SPACE 2                                                                
EX38     MVI   RCREQREP,C'N'                                                    
         B     EX2                                                              
         SPACE 2                                                                
EX40     CLI   2(R2),8                                                          
         BH    EX44                                                             
         BE    EX42                                                             
         MVI   RCREQSUM,C'Y'                                                    
         B     EX2                                                              
         SPACE 2                                                                
EX42     MVI   RCREQSUM,C'N'                                                    
         B     EX2                                                              
         SPACE 2                                                                
EX44     CLI   2(R2),9                                                          
         BH    EX45                                                             
         SR    R4,R4                                                            
         IC    R4,3(R2)                                                         
         CVD   R4,REPDUB                                                        
         UNPK  RCPROG,REPDUB                                                    
         OI    RCPROG+1,X'F0'                                                   
         B     EX2                                                              
         SPACE 2                                                                
EX45     CLI   2(R2),10                                                         
         BH    EX45A                                                            
         MVI   RCFILTER,C'N'                                                    
         B     EX2                                                              
         SPACE 2                                                                
EX45A    MVC   MAXLINES,2(R2)                                                   
         B     EX2                                                              
         EJECT                                                                  
*                   PRINT SPEC CONTROL AND LITERALS                             
         SPACE 2                                                                
EX46     CLI   RCFLONLY,C'Y'                                                    
         BE    EX2                                                              
         CLI   PROGSW,C'Y'                                                      
         BNE   EX2                                                              
         CLI   FILTSW,C'Y'                                                      
         BNE   EX2                                                              
         MVC   REPDUB(2),2(R2)     WORK OUT DISLACEMENT                         
         LH    R5,REPDUB                                                        
         LA    R5,HEAD1(R5)                                                     
         CLI   0(R2),13                                                         
         BE    EX48                                                             
         SR    R4,R4               WORK OUT BRANCH BY INSTRUCTION NUM           
         IC    R4,4(R2)                                                         
         SLL   R4,2                                                             
         L     R4,PRBRTAB(R4)                                                   
         BR    R4                                                               
         SPACE 2                                                                
EX48     SR    R4,R4               HANDLE LITERALS                              
         IC    R4,1(R2)                                                         
         SH    R4,=H'5'            LENGTH IS L'ELEMENT - 4                      
         EX    R4,MUVLIT                                                        
         B     EX2                                                              
         SPACE 2                                                                
MUVLIT   MVC   0(0,R5),4(R2)                                                    
         EJECT                                                                  
*                   TABLE OF INTERNAL BRANCH VALUES                             
         SPACE 3                                                                
         DS    0F                                                               
INBRTAB  DC    AL4(EX6)     0      END OF SPECS                                 
         DC    AL4(EX8)     1      PROGRAM RESET                                
         DC    AL4(EX10)    2              FILTER                               
         DC    AL4(EX2)     3                                                   
         DC    AL4(EX2)     4                                                   
         DC    AL4(EX2)     5                                                   
         DC    AL4(EX2)     6                                                   
         DC    AL4(EX2)     7                                                   
         DC    AL4(EX12)    8      THRU SPEC                                    
         DC    AL4(EX20)    9      USE SPEC                                     
         DC    AL4(EX26)   10      FILE CONTROL SPEC                            
         DC    AL4(EX28)   11      RUN CONTROL SPEC                             
         DC    AL4(EX46)   12      PRINT KEYWORD SPEC                           
         DC    AL4(EX46)   13      PRINT LITERAL SPEC                           
         EJECT                                                                  
*                   ROUTINES FOR PRINT SPECS (0-3)                              
         SPACE 2                                                                
EX50     MVC   0(110,R5),SPACES    SPACES                                       
         CLI   SKIPSPEC,C'T'                                                    
         BE    EX2                                                              
         MVC   0(132,R5),SPACES                                                 
         B     EX2                                                              
         SPACE 2                                                                
EX52     MVC   0(7,R5),=C'RUN ON ' RUN                                          
         GOTO1 DATCON,REPARA,(4,RCDATE),(8,7(R5))                               
         MVC   15(4,R5),=C' AT '                                                
       ++INCLUDE DDGETIME                                                       
         ST    R1,REPWORK                                                       
         XC    REPDUB,REPDUB                                                    
         MVC   REPDUB+5(3),REPWORK                                              
         OI    REPDUB+7,X'0F'                                                   
         CVB   R6,REPDUB                                                        
         EDIT  (R6),(5,19(R5)),2                                                
         B     EX2                                                              
         SPACE 2                                                                
EX54     MVC   0(7,R5),=C'CPNN ON'                                              
         MVC   2(2,R5),RCPROG                                                   
         GOTO1 DATCON,REPARA,(4,RCDATE),(8,8(R5))                               
         MVI   16(R5),C'-'                                                      
       ++INCLUDE DDGETIME                                                       
         ST    R1,REPWORK                                                       
         XC    REPDUB,REPDUB                                                    
         MVC   REPDUB+5(3),REPWORK                                              
         OI    REPDUB+7,X'0F'                                                   
         CVB   R6,REPDUB                                                        
         EDIT  (R6),(5,17(R5)),2                                                
         B     EX2                                                              
         SPACE 2                                                                
EX56     MVC   0(5,R5),=C'PAGE '                                                
         EDIT  PAGE,(4,5(R5)),ALIGN=LEFT                                        
         B     EX2                                                              
         SPACE 2                                                                
EX57     MVC   0(8,R5),=C'SUBPAGE '                                             
         EDIT  SUBPAGE,(4,8(R5)),ALIGN=LEFT                                     
         B     EX2                                                              
         EJECT                                                                  
*                   ROUTINES FOR PRINT SPECS (4-5)                              
         SPACE 2                                                                
EX58     MVC   0(23,R5),=C'PERIOD MMM/YY TO MMM/YY'                             
         LA    R6,QSTART                                                        
         LA    R7,7(R5)                                                         
         BAS   RE,DATEDIT                                                       
         LA    R6,QEND                                                          
         LA    R7,17(R5)                                                        
         BAS   RE,DATEDIT                                                       
         CLC   QINDEX,SPACES                                                    
         BE    EX2                                                              
         LA    R6,QINDEX                                                        
         LA    R7,148(R5)                                                       
         MVC   132(23,R5),=C'(PROJECTED FROM MMM/YY)'                           
         CLI   QPROJECT,C' '                                                    
         BNE   *+10                                                             
         MVC   133(14,R5),=C'INDEX BASED ON'                                    
         BAS   RE,DATEDIT                                                       
         B     EX2                                                              
         SPACE 2                                                                
DATEDIT  DS    0H                                                               
         PACK  DUB,2(2,R6)                                                      
         CVB   R1,DUB                                                           
         BCTR  R1,0                                                             
         MH    R1,=H'3'                                                         
         LA    R1,MONTHS(R1)                                                    
         MVC   0(3,R7),0(R1)                                                    
         MVC   4(2,R7),0(R6)                                                    
         BR    RE                                                               
         SPACE 2                                                                
EX64     MVC   0(13,R5),=C'REQUESTOR    '                                       
         MVC   10(12,R5),QUESTOR                                                
         B     EX2                                                              
         EJECT                                                                  
*              ROUTINES FOR PRINT SPECS (6-17)                                  
         SPACE 3                                                                
EX66     MVC   0(10,R5),MEDNM      MEDIA                                        
         B     EX2                                                              
         SPACE 2                                                                
EX68     MVC   0(33,R5),AGYNM      AGENCY NAME                                  
         B     EX2                                                              
         SPACE 2                                                                
EX70     MVC   0(33,R5),AGYADR     AGENCY ADDRESS                               
         B     EX2                                                              
         SPACE 2                                                                
EX72     DS    0H                  CLIENT ROUTINES                              
         MVC   0(6,R5),=C'CLIENT'                                               
         MVC   9(3,R5),CLT                                                      
         MVC   13(24,R5),CLTNM                                                  
         B     EX2                                                              
         SPACE 2                                                                
EX74     DS    0H                                                               
         MVC   0(24,R5),CLTNM                                                   
         B     EX2                                                              
         SPACE 2                                                                
EX76     DS    0H                  RANGE                                        
         MVC   0(09,R5),=C'BANK DATA'                                           
         CLI   QRANGE,C'B'                                                      
         BE    EX2                                                              
         MVC   0(11,R5),=C'AGENCY DATA'                                         
         CLI   QRANGE,C'A'                                                      
         BE    EX2                                                              
         MVC   0(12,R5),=C'AGENCY GROUP'                                        
         CLI   QRANGE,C'G'                                                      
         BE    EX2                                                              
         MVC   0(17,R5),=C'DATA FOR OFFICE X'                                   
         MVC   16(1,R5),QCLOFFC                                                 
         CLI   QRANGE,C'0'                                                      
         BE    EX2                                                              
         MVC   0(7,R5),=C'CLIENT '                                              
         MVC   7(20,R5),CLTNM                                                   
         B     EX2                                                              
         SPACE 2                                                                
         EJECT                                                                  
*                   BRANCH TABLE FOR PRINT SPECS                                
         SPACE 3                                                                
PRBRTAB  DC    AL4(EX50)    0      SPACES                                       
         DC    AL4(EX52)    1      RUN                                          
         DC    AL4(EX54)    2      REPORT                                       
         DC    AL4(EX56)    3      PAGE                                         
         DC    AL4(EX58)    4      PERIOD                                       
         DC    AL4(EX64)    5      REQUESTOR                                    
         DC    AL4(EX66)    6      MEDIA                                        
         DC    AL4(EX68)    7      AGYNAME                                      
         DC    AL4(EX70)    8      AGYADD                                       
         DC    AL4(EX72)    9      CLIENT                                       
         DC    AL4(EX74)   10      CLINAME                                      
         DC    AL4(EX76)   11      RANGE                                        
         EJECT                                                                  
*                   DSECT FOR THIS MODULE                                       
         SPACE 3                                                                
CPREPD   DSECT                                                                  
REPDUB   DS    D                                                                
REPWORK  DS    CL40                                                             
CONTROL  DS    CL4                                                              
PROGSW   DS    CL1                                                              
FILTSW   DS    CL1                                                              
AREA     DS    640C                                                             
REPARA   DS    6F                                                               
REPSAVE  DS    F                                                                
         PRINT OFF                                                              
       ++INCLUDE CPREPMODES                                                     
         EJECT                                                                  
       ++INCLUDE CPREPWORKD                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'009CPREPORT  05/01/02'                                      
         END                                                                    
