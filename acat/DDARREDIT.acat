*          DATA SET DDARREDIT  AT LEVEL 004 AS OF 04/23/15                      
*CATALP ARREDIT                                                                 
         TITLE 'BUFFER BUILD/MAINTAIN ROUTINE'                                  
         PRINT NOGEN                                                            
ARREDIT  CSECT                                                                  
         NMOD1 WORKL,ARREDIT*,RA,RR=R7,CLEAR=YES                                
         USING WORKD,RC                                                         
         STAR  CLEAR=ARZERO,ARS=ON                                              
         ST    R7,RELO                                                          
*                                                                               
         L     R9,0(R1)                                                         
         USING LIBUFFD,R9                                                       
         MVI   LIRTN,LIROK         RESET RETURN CODE                            
*                                                                               
         TM    LIFLAG1,LIF1ARS     USING ACCESS REGISTERS?                      
         BNZ   *+10                YES                                          
         XC    LIALET,LIALET       MAKE SURE THIS IS ZERO THEN                  
*                                                                               
         SAM31                                                                  
         L     R2,LIABUFF          R2=A(BUFFER)                                 
         LAM   AR2,AR2,LIALET                                                   
         USING LIBBUFFD,R2                                                      
*                                                                               
         LA    R1,ACTTAB           FIND AND GO TO REQUESTED ACTION              
         LA    RE,L'ACTTAB                                                      
         LA    RF,ACTTABX                                                       
LIN02    CLC   LIACTN,0(R1)                                                     
         BE    LIN04                                                            
         BXLE  R1,RE,LIN02                                                      
         DC    H'0'                                                             
         MVI   LIRTN,LIRINACT      INVALID ACTION                               
         B     LINEUPX                                                          
*                                                                               
LIN04    XR    RF,RF                                                            
         ICM   RF,7,1(R1)          PICK UP ROUTINE, GOTO IT AND EXIT            
         A     RF,RELO                                                          
         BASR  RE,RF                                                            
         B     LINEUPX                                                          
         EJECT                                                                  
***********************************************************************         
* EXIT POINTS                                                         *         
***********************************************************************         
EXITOK   CR    RB,RB                                                            
         B     EXIT                                                             
*                                                                               
EXITL    CLI   *,255                                                            
         B     EXIT                                                             
*                                                                               
EXIT     XIT1                                                                   
*                                                                               
LINEUPX  TM    LIFLAG2,LIF2SZE     TEST IF CALLER WANTS SPACE INFO              
         BZ    LINEUPX1                                                         
         NI    LIFLAG2,255-LIF2SZEU                                             
         TM    KYMFLAG,KYMFCUR     TEST IF SPACE USED THIS CALL                 
         BZ    LINEUPX1                                                         
         OI    LIFLAG2,LIF2SZEU    RETURN CURRENT SPACE UPDATED                 
         MVC   LICURL(8),LIBCURL   RETURN CURRENT AND MAXIMUM LENGTHS           
*                                                                               
LINEUPX1 SAM24                     BACK TO 24-BIT MODE                          
         REAR  ARS=OFF             RESTORE ACCESS REGISTERS                     
         XMOD1 1                   AND LEAVE                                    
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO INITIALISE BUFFER                                        *         
***********************************************************************         
LININI   NTR1                                                                   
         XC    0(LIBKEYS-LIBBUFFD,R2),0(R2)                                     
         MVC   0(8,R2),EYECATCH                                                 
         MVC   8(8,R2),0(R2)       SET FIRST 16 BYTES TO EYECATCHER             
*                                                                               
         MVC   LIBMAXL,LILBUFF     SET MAX LENGTH OF BUFFER                     
         L     RF,LILBUFF                                                       
         AHI   RF,-(LIBKEYS-LIBBUFFD)                                           
         ST    RF,LIBNREC          SET DISPLACEMENT TO NEXT RECORD              
*                                                                               
         XR    RF,RF                                                            
         ICM   RF,3,LIKEYL         PICK UP LENGTH OF KEY                        
         BNZ   LIINI02                                                          
         MVI   LIRTN,LIRNOKEY                                                   
         B     EXITL                                                            
*                                                                               
LIINI02  STH   RF,LIBKEYL          SAVE IT                                      
         BCTR  RF,0                                                             
         STH   RF,LIBKEYL1         SAVE LENGTH - 1                              
*                                                                               
         LH    RF,LIBKEYL                                                       
         LHI   R0,LIBKFIX                                                       
         TM    LIFLAG1,LIF1BIG                                                  
         BZ    *+8                                                              
         LHI   R0,LIBKBFIX                                                      
         AR    RF,R0                                                            
         STH   RF,LIBKEYL6         SAVE LENGTH + LENGTH OF FIXED HEADER         
*                                                                               
         XR    RF,RF                                                            
         ICM   RF,3,LIRECLMX       PICK UP MAX LENGTH OF RECORD                 
         BNZ   LIINI04                                                          
         MVI   LIRTN,LIRNOREC                                                   
         B     EXITL                                                            
*                                                                               
LIINI04  STH   RF,LIBRECL          SAVE IT                                      
         LA    R4,LIBKEYS          R4=A(KEYS)                                   
         LAM   AR4,AR4,LIALET                                                   
         USING LIBKEYD,R4                                                       
*                                                                               
         LH    RF,LIBKEYL6         MAKE A LOW-WATER KEY                         
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         XC    0(0,R4),0(R4)                                                    
*                                                                               
         AH    R4,LIBKEYL6                                                      
         EX    RF,*+8              MAKE A HIGH-WATER KEY                        
         B     *+10                                                             
         XC    0(0,R4),0(R4)                                                    
*                                                                               
         LA    R1,LIBKMAIN                                                      
         TM    LIFLAG1,LIF1BIG                                                  
         BZ    *+8                                                              
         LA    R1,LIBKBMN                                                       
         CPYA  AR1,AR4                                                          
         MVI   0(R1),FF            FILL KEY PORTION WITH FF'S                   
         LH    RF,LIBKEYL1                                                      
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   1(0,R1),0(R1)                                                    
         LAM   AR1,AR1,ARZERO                                                   
*                                                                               
         LH    RF,LIBKEYL6                                                      
         AH    RF,LIBKEYL6                                                      
         ST    RF,LIBNKEYA         SET DISPLACEMENT TO NEXT KEY                 
*                                                                               
         LA    RF,2                                                             
         ST    RF,LIBRECN          SET 2 RECORDS IN BUFFER                      
         AH    R4,LIBKEYL6                                                      
         SR    R4,R2                                                            
         ST    R4,LIBCURL          SET AMOUNT OF BUFFER USED                    
         OI    KYMFLAG,KYMFCUR     SET BUFFER AMOUNT UPDATED                    
         OI    LIFLAG1,LIF1INI     SET BUFFER INITIALISED                       
         B     EXITOK                                                           
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO FIX BUFFER ARRAY                                         *         
***********************************************************************         
LINFIX   NTR1                                                                   
         LA    R4,LIBKEYS                                                       
         LAM   AR4,AR4,LIALET                                                   
         USING LIBKEYD,R4                                                       
         LH    RE,LIBKEYL6                                                      
         L     RF,LIBNKEYA                                                      
         LA    RF,LIBKEYS(RF)                                                   
         SR    RF,RE               RF=A(LAST KEY)                               
         LA    R0,LIBKEYS                                                       
*                                                                               
LFIX02   LA    R1,0(RE,R4)         POINT TO NEXT KEY                            
         SR    R1,R0                                                            
*                                                                               
         TM    LIFLAG1,LIF1BIG                                                  
         BO    *+12                                                             
         STCM  R1,7,LIBKPTR        SET DISPLACEMENT TO NEXT KEY                 
         B     *+8                                                              
         STCM  R1,15,LIBKBPTR                                                   
*                                                                               
         BXLE  R4,RE,LFIX02                                                     
*                                                                               
         ICM   RF,15,LIBNKEYA      PICK UP A(END OF ARRAY)                      
         AH    RF,LIBKEYL6                                                      
         STCM  RF,15,LIBNKEYP      SET A(NEXT POINTER RECORD)                   
*                                                                               
         OI    LIFLAG1,LIF1FXD     SET BUFFER ARRAY FIXED                       
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO ADD KEY TO BUFFER                                        *         
***********************************************************************         
LINADD   NTR1                                                                   
         XC    ARECORD,ARECORD                                                  
         ICM   R3,15,LIAREC        R3=A(RECORD)                                 
         TM    LIFLAG1,LIF1VAR     FIXED LENGTH RECORD?                         
         BZ    LIADD02             YES                                          
*                                                                               
         OC    LIRECL,LIRECL       RECORD LENGTH SET?                           
         BNZ   *+12                                                             
         MVI   LIRTN,LIRNOREC                                                   
         B     EXITL                                                            
         CLC   LIRECLMX,LIRECL     RECORD BELOW MAXIMUM                         
         BNL   *+12                                                             
         MVI   LIRTN,LIRBGREC                                                   
         B     EXITL                                                            
*                                                                               
LIADD02  LH    RF,LIRECL           PICK UP RECORD LENGTH                        
         TM    LIFLAG1,LIF1VAR                                                  
         BO    *+8                                                              
         LH    RF,LIRECLMX                                                      
*                                                                               
         LHI   R0,LIBKFIX                                                       
         TM    LIFLAG1,LIF1BIG                                                  
         BZ    *+8                                                              
         LHI   R0,LIBKBFIX                                                      
         AR    RF,R0               ADD LENGTH OF POINTERS                       
         A     RF,LIBCURL          AND ADD CURRENT AMOUNT USED                  
*                                                                               
         TM    LIFLAG1,LIF1MKY     ADDITIONAL POINTERS TO ADD?                  
         BZ    LIADD04             NO                                           
         LH    R0,LIBKEYL6                                                      
         L     RE,LIKEY                                                         
         MH    R0,0(RE)                                                         
         AR    RF,R0               ADD LENGTH USED BY ADDITIONAL PTRS           
*                                                                               
LIADD04  C     RF,LIBMAXL          WILL THIS RECORD FIT?                        
         BL    *+12                YES                                          
         MVI   LIRTN,LIRFULLB                                                   
         B     EXITL                                                            
*                                                                               
LIADD06  BRAS  RE,KEYMAINT         ADD KEY TO ARRAY                             
         TM    KYMFLAG,(KYMFADD+KYMFNF)                                         
         BO    *+12                KEY ADDED OK                                 
         MVI   LIRTN,LIRDUP                                                     
         B     EXITL                                                            
*                                                                               
         L     R4,AENTRY           PICK UP ADDRESS OF KEY                       
         LAM   AR4,AR4,LIALET                                                   
         USING LIBKEYD,R4                                                       
         LH    R0,LIRECL                                                        
         SH    R0,LIBKEYL          R0=LENGTH OF DATA PORTION                    
         TM    LIFLAG1,LIF1VAR     VARIABLE RECORD?                             
         BZ    *+8                 NO                                           
         AHI   R0,2                LEAVE 2 EXTRA BYTES FOR LENGTH               
*                                                                               
         L     R1,LIBNREC          R1=CURRENT DATA DISPLACEMENT                 
         SR    R1,R0               R1=DISPLACEMENT TO MOVE TO                   
*                                                                               
         TM    LIFLAG1,LIF1BIG                                                  
         BO    *+12                                                             
         STCM  R1,7,LIBKREC                                                     
         B     *+8                                                              
         STCM  R1,15,LIBKBREC                                                   
*                                                                               
         ST    R1,LIBNREC          UPDATE DATA DISPLACEMENT                     
         ST    R1,ARECORD          SAVE A(THIS RECORD IN BUFFER)                
*                                                                               
         LA    R4,LIBKEYS(R1)      POINT TO DATA BUILD AREA                     
         TM    LIFLAG1,LIF1VAR     FIXED LENGTH RECORD?                         
         BZ    LIADD08             YES                                          
*                                                                               
         MVC   0(2,R4),LIRECL      MOVE RECORD LENGTH TO DATA AREA              
         LA    R4,2(R4)            SET 'TO' ADDRESS FOR RECORD DATA             
         LH    R5,LIRECL           RECLEN                                       
         SH    R5,LIBKEYL          LESS KEY LEN                                 
         JNP   *+2                                                              
*                                                                               
         LR    RE,R3                                                            
         AH    RE,LIBKEYL          POINT PAST KEY TO DATA                       
         LR    RF,R5                                                            
         MVCL  R4,RE                                                            
         B     LIADD10                                                          
*                                                                               
LIADD08  LH    R5,LIBRECL          FIXED LENGTH ARRAY                           
         SH    R5,LIBKEYL                                                       
         LR    RE,R3                                                            
         AH    RE,LIBKEYL          POINT PAST KEY TO DATA                       
         LR    RF,R5                                                            
         MVCL  R4,RE                                                            
*                                                                               
LIADD10  LAM   AR4,AR4,ARZERO                                                   
         LH    RF,LIRECL           LENGTH VARIABLE LENGTH RECORD                
         AHI   RF,2                LENGTH OF RECORD IS SAVED ALSO               
         TM    LIFLAG1,LIF1VAR     VARIABLE LENGTH RECORD?                      
         BO    *+8                 YES                                          
         LH    RF,LIRECLMX         PICK UP LENGTH OF FIXED RECORD               
*                                                                               
         LHI   RE,LIBKFIX                                                       
         TM    LIFLAG1,LIF1BIG                                                  
         BZ    *+8                                                              
         LHI   RE,LIBKBFIX                                                      
         AR    RF,RE                                                            
*                                                                               
         L     RE,LIBCURL                                                       
         AR    RF,RE               AND ADD CURRENT AMOUNT USED                  
         CS    RE,RF,LIBCURL                                                    
         BNZ   LIADD10                                                          
         OI    KYMFLAG,KYMFCUR     SET BUFFER AMOUNT UPDATED                    
*                                                                               
         TM    LIFLAG1,LIF1MKY     ADDITIONAL POINTERS TO ADD?                  
         BZ    EXITOK              NO                                           
*                                                                               
         MVC   AAREC,LIAREC        SAVE LIAREC                                  
         L     R5,LIKEY                                                         
         XR    R0,R0                                                            
         ICM   R0,3,0(R5)                                                       
         JZ    *+2                                                              
         AHI   R5,2                R5=A(FIRST KEY)                              
*                                                                               
LIADD12  ST    R5,LIAREC           SAVE A(THIS KEY TO ADD)                      
         BRAS  RE,KEYMAINT         ADD KEY TO ARRAY                             
         TM    KYMFLAG,(KYMFADD+KYMFNF)                                         
         BO    *+12                KEY ADDED OK                                 
         MVI   LIRTN,LIRDUP                                                     
         B     EXITL                                                            
*                                                                               
         L     R4,AENTRY           PICK UP ADDRESS OF KEY                       
         LAM   AR4,AR4,LIALET                                                   
         USING LIBKEYD,R4                                                       
         L     R1,ARECORD          R1=A(CURRENT RECORD)                         
*                                                                               
         TM    LIFLAG1,LIF1BIG                                                  
         BO    *+12                                                             
         STCM  R1,7,LIBKREC                                                     
         B     *+8                                                              
         STCM  R1,15,LIBKBREC                                                   
*                                                                               
         LAM   AR4,AR4,ARZERO                                                   
*                                                                               
LIADD14  LH    RF,LIKEYL           PICK UP LENGTH OF KEY ADDED                  
         LHI   RE,LIBKFIX                                                       
         TM    LIFLAG1,LIF1BIG                                                  
         BZ    *+8                                                              
         LHI   RE,LIBKBFIX                                                      
         AR    RF,RE               ADD OVERHEAD                                 
*                                                                               
         L     RE,LIBCURL                                                       
         AR    RF,RE               AND ADD CURRENT AMOUNT USED                  
         CS    RE,RF,LIBCURL                                                    
         BNZ   LIADD14                                                          
         OI    KYMFLAG,KYMFCUR     SET BUFFER AMOUNT UPDATED                    
*                                                                               
         AH    R5,LIKEYL                                                        
         BCT   R0,LIADD12                                                       
*                                                                               
         MVC   LIAREC,AAREC        RESTORE LIAREC                               
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO READ HIGH BY KEY FROM BUFFER                             *         
***********************************************************************         
LINHIGH  NTR1                                                                   
         BRAS  RE,KEYMAINT         READ HIGH FOR KEY                            
         GOTO1 BLDREC              BUILD RECORD INTO CALLER BUFFER              
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO READ SEQUENTIAL BY KEY FROM BUFFER                       *         
***********************************************************************         
LINSEQ   NTR1                                                                   
         BRAS  RE,KEYMAINT         READ FOR KEY                                 
         TM    KYMFLAG,KYMFNF      NOT FOUND - CANNOT READ SEQUENTIAL           
         BZ    *+12                                                             
         MVI   LIRTN,LIRSEQ                                                     
         B     EXITL                                                            
*                                                                               
         L     R4,AENTRY                                                        
         LAM   AR4,AR4,LIALET                                                   
         USING LIBKEYD,R4                                                       
*                                                                               
         XR    RF,RF                                                            
         TM    LIFLAG1,LIF1BIG                                                  
         BO    *+12                                                             
         ICM   RF,7,LIBKPTR        GET DISPLACEMENT TO NEXT KEY                 
         B     *+8                                                              
         ICM   RF,15,LIBKBPTR                                                   
*                                                                               
         LA    R4,LIBKEYS(RF)      TRAVERSE LINKED LIST                         
         ST    R4,AENTRY                                                        
*                                                                               
         BRAS  RE,BLDREC                                                        
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO WRITE BY KEY FROM BUFFER                                 *         
***********************************************************************         
LINWRT   NTR1                                                                   
         BRAS  RE,KEYMAINT         READ FOR KEY                                 
         TM    KYMFLAG,KYMFNF      NOT FOUND - CANNOT WRITE                     
         BZ    *+12                                                             
         MVI   LIRTN,LIRNF                                                      
         B     EXITL                                                            
*                                                                               
         GOTO1 WRTREC                                                           
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO DELETE FROM BUFFER --- NOT IMPLEMENTED                   *         
***********************************************************************         
LINDEL   NTR1                                                                   
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO REPORT BUFFER CONTENTS                                   *         
***********************************************************************         
LINREP   NTR1                                                                   
         L     R3,LIAREC                                                        
         MVC   0(LIBKEYS-LIBBUFFD,R3),LIBBUFFD                                  
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO BUILD RECORD FROM BUFFER INTO IO AREA                    *         
* NTRY: AENTRY = A(KEY)                                               *         
***********************************************************************         
BLDREC   NTR1                                                                   
         L     R3,LIAREC                                                        
         L     R4,AENTRY                                                        
         LAM   AR4,AR4,LIALET                                                   
         USING LIBKEYD,R4                                                       
*                                                                               
         LH    RF,LIBKEYL1                                                      
*                                                                               
         LA    RE,LIBKMAIN                                                      
         TM    LIFLAG1,LIF1BIG                                                  
         BZ    *+8                                                              
         LA    RE,LIBKBMN                                                       
         CPYA  ARE,AR4                                                          
         EX    RF,*+8              MOVE IN KEY                                  
         B     *+10                                                             
         MVC   0(0,R3),0(RE)                                                    
         LAM   ARE,ARE,ARZERO                                                   
*                                                                               
         XR    RF,RF                                                            
         TM    LIFLAG1,LIF1BIG                                                  
         BO    *+12                                                             
         ICM   RF,7,LIBKREC                                                     
         B     *+8                                                              
         ICM   RF,15,LIBKBREC                                                   
         BNZ   *+12                YES                                          
         MVI   LIRTN,LIREOF        END OF FILE                                  
         B     EXITL                                                            
*                                                                               
         LA    R4,LIBKEYS(RF)      R4=A(RECORD)                                 
         LR    RE,R3                                                            
         TM    LIFLAG1,LIF1VAR     FIXED LENGTH RECORD?                         
         BZ    BLDR02              YES                                          
*                                                                               
         MVC   LIRECL,0(R4)    *** VARIABLE LENGTH RECORD                       
         LA    R4,2(R4)            SAVE LENGTH AS FIRST 2 BYTES OF DATA         
*                                                                               
         LH    R5,LIRECL                                                        
         SH    R5,LIBKEYL          R5=LENGTH OF DATA PORTION                    
         LR    RE,R3                                                            
         AH    RE,LIBKEYL                                                       
         LR    RF,R5                                                            
         MVCL  RE,R4               MOVE IN DATA PORTION                         
         B     EXITOK                                                           
*                                                                               
BLDR02   LH    R5,LIBRECL          FIXED LENGTH ARRAY                           
         SH    R5,LIBKEYL                                                       
         AH    RE,LIBKEYL          POINT PAST KEY                               
         LR    RF,R5                                                            
         MVCL  RE,R4                                                            
         B     EXITOK                                                           
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO MOVE RECORD FROM IO AREA INTO BUFFER                     *         
* NTRY: AENTRY = A(KEY OF RECORD TO WRITE                             *         
***********************************************************************         
WRTREC   NTR1                                                                   
         L     R3,LIAREC                                                        
         L     R4,AENTRY                                                        
         LAM   AR4,AR4,LIALET                                                   
         USING LIBKEYD,R4                                                       
*                                                                               
         XR    RF,RF                                                            
         TM    LIFLAG1,LIF1BIG                                                  
         BO    *+12                                                             
         ICM   RF,7,LIBKREC                                                     
         B     *+8                                                              
         ICM   RF,15,LIBKBREC                                                   
*                                                                               
         LA    R4,LIBKEYS(RF)      R4=A(RECORD)                                 
         LR    RE,R3                                                            
         TM    LIFLAG1,LIF1VAR     FIXED LENGTH RECORD?                         
         BO    WRTN02              NO                                           
         LH    R5,LIBRECL                                                       
         SH    R5,LIBKEYL                                                       
         AH    RE,LIBKEYL          POINT PAST KEY                               
         LR    RF,R5                                                            
         MVCL  R4,RE                                                            
         B     EXITOK                                                           
*                                                                               
WRTN02   CLC   LIRECL,0(R4)        DID RECORD GET LONGER?                       
         BH    WRTN04              YES - HAS TO GO IN NEW SLOT                  
*                                                                               
         MVC   LIRECL,0(R4)                                                     
         LA    R4,2(R4)            GO PAST LENGTH                               
*                                                                               
         LH    R5,LIRECL                                                        
         SH    R5,LIBKEYL          R5=LENGTH OF RECORD PORTION                  
         LR    RE,R3                                                            
         AH    RE,LIBKEYL                                                       
         LR    RF,R5                                                            
         MVCL  R4,RE                                                            
         B     EXITOK                                                           
*                                                                               
WRTN04   LH    R0,LIRECL                                                        
         SH    R0,LIBKEYL          R0=LENGTH OF DATA PORTION                    
         AHI   R0,2                LEAVE 2 EXTRA BYTES FOR LENGTH               
         L     RF,LIBNREC                                                       
         SR    RF,R0               RF=DISPLACEMENT TO MOVE TO                   
         L     RE,LIBNREC                                                       
         CS    RE,RF,LIBNREC                                                    
         BNZ   WRTN04              UPDATE DATA DISPLACEMENT                     
*                                                                               
         LA    R4,LIBKEYS(RF)      POINT TO DATA BUILD AREA                     
         ST    R4,ADPRT                                                         
         MVC   0(2,R4),LIRECL      MOVE IN RECORD LENGTH                        
         LA    R4,2(R4)                                                         
         LH    R5,LIRECL           RECLEN                                       
         SH    R5,LIBKEYL          LESS KEY LEN                                 
         JNP   *+2                                                              
         LR    RE,R3                                                            
         AH    RE,LIBKEYL          POINT PAST KEY TO DATA                       
         LR    RF,R5                                                            
         MVCL  R4,RE                                                            
         L     R4,AENTRY                                                        
*                                                                               
WRTN06   LH    RF,LIRECL           LENGTH VARIABLE LENGTH RECORD                
         AHI   RF,2                LENGTH OF RECORD IS SAVED ALSO               
         TM    LIFLAG1,LIF1VAR     VARIABLE LENGTH RECORD?                      
         BNZ   *+8                 YES                                          
         LH    RF,LIRECLMX         PICK UP LENGTH OF FIXED RECORD               
*                                                                               
         LHI   RE,LIBKFIX                                                       
         TM    LIFLAG1,LIF1BIG                                                  
         BZ    *+8                                                              
         LHI   RE,LIBKBFIX                                                      
         AR    RF,RE               ADD OVERHEAD                                 
*                                                                               
         L     RE,LIBCURL                                                       
         AR    RF,RE               AND ADD CURRENT AMOUNT USED                  
         CS    RE,RF,LIBCURL                                                    
         BNZ   WRTN06                                                           
         OI    KYMFLAG,KYMFCUR     SET BUFFER AMOUNT UPDATED                    
*                                                                               
         L     R1,ADPRT                                                         
         LA    R0,LIBKEYS                                                       
         SR    R1,R0                                                            
*                                                                               
         TM    LIFLAG1,LIF1BIG                                                  
         BO    *+12                                                             
         STCM  R1,7,LIBKREC                                                     
         B     *+8                                                              
         STCM  R1,15,LIBKBREC                                                   
         B     EXITOK                                                           
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* KEY ARRAY MAINTENANCE ROUTINE                                       *         
* NTRY: R2=A(BUFFER)                                                  *         
*       R9=A(LIBUFFD)                                                 *         
***********************************************************************         
KEYMAINT NTR1                                                                   
         STAR  CLEAR=N,ARS=ON                                                   
*                                                                               
         L     R3,LIAREC           R3=A(RECORD)                                 
         LA    R4,LIBKEYS          R4=A(TABLE)                                  
         CPYA  AR4,AR2                                                          
*                                                                               
         LH    R5,LIBKEYL1         R5=L'KEY FOR EXECUTED COMPARE                
         LH    R6,LIBKEYL6         R6=L'ARRAY ENTRY                             
*                                                                               
         ICM   R7,15,LIBRECN       R7=NUMBER OF RECORDS IN TABLE                
         BNZ   KEYM02                                                           
         CLI   LIACTN,LIAADD       ADDING TO TABLE?                             
         BNE   KEYEXIT             NO                                           
         TM    LIFLAG1,LIF1INS     INSERT IF NOT FOUND                          
         BO    AFRST               YES                                          
         DC    H'0'                CANNOT ADD POINTER IF NO RECORDS             
*                                                                               
KEYM02   CHI   R7,10               LESS THAN 10 ENTRIES?                        
         BL    SEQSRCH             LESS - SEQUENTIAL SEARCH IS BETTER           
         B     BINSRCH             MORE - BINARY SEARCH IS BETTER               
         EJECT                                                                  
***********************************************************************         
* ADD FIRST RECORD                                                    *         
***********************************************************************         
AFRST    AHI   R7,1                BUMP RECORD COUNTER                          
         ST    R7,LIBNREC                                                       
*                                                                               
         ST    R4,AENTRY           SAVE A(RECORD)                               
         XC    0(LIBKFIX,R4),0(R4) CLEAR FIRST PART OF RECORD                   
         TM    LIFLAG1,LIF1BIG                                                  
         BZ    *+10                                                             
         XC    0(LIBKBFIX,R4),0(R4)                                             
*                                                                               
         LHI   RE,LIBKFIX                                                       
         TM    LIFLAG1,LIF1BIG                                                  
         BZ    *+8                                                              
         LHI   RE,LIBKBFIX                                                      
         AR    R4,RE               R4=A(KEY IN ARRAY ENTRY)                     
*                                                                               
         L     R5,LIBKEYL          R5=L'KEY IN ARRAY ENTRY                      
         L     RE,LIAREC           RE=A(RECORD)                                 
         L     RF,LIBKEYL                                                       
         MVCL  R4,RE               MOVE IN RECORD                               
*                                                                               
         L     RF,LIBNKEYA         UPDATE NEXT ARRAY KEY ADDRESS                
         AH    RF,LIBKEYL6                                                      
         ST    RF,LIBNKEYA                                                      
*                                                                               
         OI    KYMFLAG,KYMFNF+KYMFADD SET RECORD NOT FOUND / ADDED              
         B     KEYEXIT             EXIT TO APPLICATION                          
         EJECT                                                                  
***********************************************************************         
* LINEAR SEARCH (MORE EFFICIENT IF <10 ENTRIES IN TABLE)              *         
***********************************************************************         
         USING LIBKEYD,R4                                                       
SEQSRCH  L     RF,LIBNKEYA                                                      
         AR    RF,R4                                                            
         SR    RF,R6               RF=A(LAST RECORD)                            
         LR    RE,R6               RE=L'ARRAY                                   
*                                                                               
SEQS02   LA    R1,LIBKMAIN                                                      
         TM    LIFLAG1,LIF1BIG                                                  
         BZ    *+8                                                              
         LA    R1,LIBKBMN                                                       
         CPYA  AR1,AR4                                                          
         EX    R5,SEQCMP                                                        
         LAM   AR1,AR1,ARZERO                                                   
         BE    EXACT               EQ - R4=A(RECORD)                            
         BH    NOEXACT             FIRST HIGH IS INSERTION POINT                
         BXLE  R4,RE,SEQS02                                                     
         SR    R4,R6                                                            
         B     NOEXACT                                                          
*                                                                               
SEQCMP   CLC   0(0,R1),0(R3)                                                    
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* BINARY SEARCH ROUTINE                                               *         
***********************************************************************         
         USING LIBKEYD,R4                                                       
BINSRCH  L     R1,LIBNKEYA                                                      
         SR    R1,R6               R1=DISP TO LAST RECORD                       
         LR    R0,R6               R0=L'ARRAY ENTRY                             
*NOP*    BXLE  R0,R0,*             IDF CAN'T SINGLE-STEP THROUGH THIS!          
         AR    R0,R0                                                            
         CR    R0,R1                                                            
         BNH   *-4                                                              
         AR    R1,R4                                                            
         LH    RF,LIBKEYL1                                                      
*                                                                               
         BASR  RE,0                SET RETURN POINT                             
         SRL   R0,1                                                             
         CR    R0,R6                                                            
         BL    BSRCH06                                                          
         BXH   R4,R0,BSRCH04                                                    
*                                                                               
         LA    R7,LIBKMAIN                                                      
         TM    LIFLAG1,LIF1BIG                                                  
         BZ    *+8                                                              
         LA    R7,LIBKBMN                                                       
         CPYA  AR7,AR4                                                          
         EX    RF,BSRCMP                                                        
         LAM   AR7,AR7,ARZERO                                                   
         BE    EXACT                                                            
         BHR   RE                                                               
*                                                                               
BSRCH04  SR    R4,R0                                                            
         BR    RE                                                               
*                                                                               
BSRCH06  LA    R1,1(R1)                                                         
         AR    R4,R6                                                            
         B     NOEXACT                                                          
*                                                                               
BSRCMP   CLC   0(0,R3),0(R7)      COMPARE REQUESTED KEY WITH RECORD             
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* NO EXACT MATCH ON REQUESTED RECORD                                  *         
***********************************************************************         
NOEXACT  ST    R4,AENTRY           R4=A(ARRAY INSERTION POINT)                  
         SH    R4,LIBKEYL6         GET PREVIOUS ENTRY IN ARRAY                  
         ST    R4,APREV                                                         
         USING LIBKEYD,R4                                                       
*                                                                               
         TM    LIFLAG1,LIF1FXD     LINKED LIST BUILT?                           
         BZ    NEXA02              NO                                           
*                                                                               
         XR    RF,RF                                                            
         TM    LIFLAG1,LIF1BIG                                                  
         BO    *+12                                                             
         ICM   RF,7,LIBKPTR        GET DISPLACEMENT TO NEXT KEY                 
         B     *+8                                                              
         ICM   RF,15,LIBKBPTR                                                   
         LA    RF,LIBKEYS(RF)                                                   
         C     RF,AENTRY           NEXT IN LIST IS NEXT IN ARRAY?               
         BNE   TRAVERSE            NO - LINKED ENTRIES ADDED                    
*                                                                               
NEXA02   OI    KYMFLAG,KYMFNF      SET EXACT MATCH NOT FOUND                    
         CLI   LIACTN,LIAADD       ADD RECORD IF NOT FOUND?                     
         BNE   KEYEXIT             NO - EXIT                                    
*                                                                               
         TM    LIFLAG1,LIF1INS     INSERT INTO ARRAY ON ADD?                    
         BO    ADDARY              YES - INSERT RECORD AT AENTRY                
         B     ADDPTR              NO  - MAKE LINKED LIST ENTRY                 
                                                                                
***********************************************************************         
* LINKED LIST INSERTIONS FOUND TRAVERSE LIST FOR BETTER MATCH         *         
***********************************************************************         
TRAVERSE CPYA  ARF,AR4             RF=A(CURRENT POINTER)                        
THIS     USING LIBKEYD,RF                                                       
         LH    R1,LIBKEYL1         R1=COMPARISON LENGTH                         
*                                                                               
TRVS02   ST    RF,AENTRY                                                        
         LA    RE,THIS.LIBKMAIN                                                 
         TM    LIFLAG1,LIF1BIG                                                  
         BZ    *+8                                                              
         LA    RE,THIS.LIBKBMN                                                  
         CPYA  ARE,ARF                                                          
         EX    R1,TRVSCMP                                                       
         LAM   ARE,ARE,ARZERO                                                   
         BH    TRVS04              KEY STILL HIGH - CONTINUE                    
         BE    TRVS06              KEY MATCHED EXACTLY                          
         BL    TRVS08              KEY LOW - RF=INSERTION POINT                 
*                                                                               
TRVS04   XR    RE,RE                                                            
         TM    LIFLAG1,LIF1BIG                                                  
         BO    *+12                                                             
         ICM   RE,7,THIS.LIBKPTR    GET DISPLACEMENT TO NEXT KEY                
         B     *+8                                                              
         ICM   RE,15,THIS.LIBKBPTR                                              
         LA    RF,LIBKEYS(RE)                                                   
         MVC   APREV,AENTRY                                                     
         B     TRVS02              TRY NEXT                                     
*                                                                               
TRVSCMP  CLC   0(0,R3),0(RE)                                                    
         DROP  THIS                                                             
*                                                                               
TRVS06   LAM   ARF,ARF,ARZERO                                                   
         L     R4,AENTRY                                                        
         OI    KYMFLAG,KYMFPTR     SET POINTER RECORD FOUND                     
         B     EXACT                                                            
*                                                                               
TRVS08   LAM   ARF,ARF,ARZERO                                                   
         OI    KYMFLAG,KYMFNF+KYMFPTR RECORD NOT FOUND/POINTER RECORD           
         CLI   LIACTN,LIAADD       ADDING RECORD?                               
         BNE   KEYEXIT             NO                                           
*                                                                               
         TM    LIFLAG1,LIF1INS     INSERT IF NOT FOUND                          
         JZ    ADDPTR              NO                                           
         DC    H'0'                CANNOT INSERT IF POINTER RECORDS             
         EJECT                                                                  
***********************************************************************         
* ADD RECORD INTO ARRAY                                               *         
* NTRY: AENTRY = A(INSERTION POINT)                                   *         
***********************************************************************         
ADDARY   BRAS  RE,ARRAYINS         ARRAYINS GETS W/S FOR INSERTION              
         B     KEYEXIT                                                          
         EJECT                                                                  
***********************************************************************         
* ADD POINTER RECORD                                                  *         
* NTRY: POINTER MUST BE INSERTED BETWEEN APREV AND AENTRY             *         
***********************************************************************         
         USING LIBKEYD,R4                                                       
ADDPTR   L     R4,APREV            R4=INSERTION POINT                           
         TM    LIFLAG1,LIF1BIG                                                  
         BO    *+14                                                             
         MVC   NPTR,LIBKPTR        SAVE POINTER                                 
         B     *+10                                                             
         MVC   NPTRX,LIBKBPTR                                                   
*                                                                               
APTR02   ICM   RE,15,LIBNKEYP      GET A(NEXT FREE POINTER RECORD)              
         LR    RF,RE                                                            
         AH    RF,LIBKEYL6                                                      
         CS    RE,RF,LIBNKEYP      SET A(NEXT FREE POINTER RECORD)              
         BNE   APTR02                                                           
*                                                                               
         LA    RF,LIBKEYS(RF)                                                   
         CPYA  ARF,AR4                                                          
NEW      USING LIBKEYD,RF                                                       
*                                                                               
         LH    R1,LIBKEYL6                                                      
         BCTR  R1,0                                                             
         EX    R1,*+8              CLEAR INSERTION AREA                         
         B     *+10                                                             
         XC    NEW.LIBKEYD(0),NEW.LIBKEYD                                       
*                                                                               
         LH    R1,LIBKEYL1                                                      
         LA    RE,NEW.LIBKMAIN                                                  
         TM    LIFLAG1,LIF1BIG                                                  
         BZ    *+8                                                              
         LA    RE,NEW.LIBKBMN                                                   
         CPYA  ARE,ARF                                                          
         EX    R1,*+8              COPY IN KEY                                  
         B     *+10                                                             
         MVC   0(0,RE),0(R3)                                                    
         LAM   ARE,ARE,ARZERO                                                   
*                                                                               
         TM    LIFLAG1,LIF1BIG                                                  
         BO    *+14                                                             
         MVC   NEW.LIBKPTR,NPTR    SET POINTER TO NEXT RECORD                   
         B     *+10                                                             
         MVC   NEW.LIBKBPTR,NPTRX                                               
         DROP  NEW                                                              
*                                                                               
         LAM   ARF,ARF,ARZERO                                                   
         ST    RF,AENTRY           SET A(INSERTED POINTER RECORD)               
         OI    KYMFLAG,(KYMFNF+KYMFPTR+KYMFADD)                                 
*                                                                               
         LA    RE,LIBKEYS                                                       
         SR    RF,RE                                                            
         L     R4,APREV                                                         
         TM    LIFLAG1,LIF1BIG                                                  
         BO    *+12                                                             
         STCM  RF,7,LIBKPTR        SET NEW LINK IN ORIGINAL RECORD              
         B     *+8                                                              
         STCM  RF,15,LIBKBPTR                                                   
*                                                                               
APTR04   L     RE,LIBRECN          UPDATE NUMBER OF RECORDS                     
         LA    RF,1(RE)                                                         
         CS    RE,RF,LIBRECN                                                    
         BNE   APTR04                                                           
         B     KEYEXIT                                                          
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* EXACT MATCH ON RECORD                                               *         
***********************************************************************         
EXACT    ST    R4,AENTRY           SET RECORD ADDRESS                           
         LA    RF,LIBKEYS                                                       
         CR    R4,RF               LOW WATER RECORD MATCHED?                    
         BNE   KEYEXIT             NO                                           
         AH    R4,LIBKEYL6         GO PAST LOW-WATER RECORD                     
         ST    R4,AENTRY           SET ADDRESS                                  
         B     NOEXACT             IT CANNOT NOT MATCH EXACTLY                  
         EJECT                                                                  
***********************************************************************         
* EXIT POINT                                                          *         
***********************************************************************         
KEYEXIT  REAR  ARS=ON                                                           
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO GRAB W/S AND ADD ELEMENT INTO ARRAY                      *         
* NTRY: R1 = W/S CHAIN OF CALLING APPLICATION                         *         
***********************************************************************         
ARRAYINS NTR1  BASE=*,LABEL=*,WORK=(R8,ARRWRKL)                                 
         USING ARRWRKD,R8                                                       
*                                                                               
         L     R4,AENTRY           R4=INSERTION POINT                           
         LAM   AR4,AR4,LIALET                                                   
         USING LIBKEYD,R4                                                       
         L     R1,LIBNKEYA         R1=A(NEXT INSERTION POINT)                   
         LA    R1,LIBKEYS(R1)                                                   
         CPYA  AR1,AR4                                                          
*                                                                               
         LR    RF,R1                                                            
         SR    RF,R4               RF=LENGTH OF DATA TO MOVE                    
*                                                                               
         LR    RE,R1               RE=MOVE FROM POINT                           
         CPYA  ARE,AR1                                                          
         AH    R1,LIBKEYL6         R1=MOVE TO POINT                             
*                                                                               
ARRIN02  CHI   RF,255              MORE THAN 255 BYTES LEFT TO MOVE?            
         BL    ARRIN04             NO                                           
*                                                                               
         AHI   RE,-255             BACK UP 255 BYTES                            
         AHI   R1,-255                                                          
         MVC   ARRHOLD(255),0(RE)  COPY OUT FROM MOVE FROM POINT                
         MVC   0(255,R1),ARRHOLD   MOVE BACK TO MOVE TO POINT                   
         AHI   RF,-255                                                          
         B     ARRIN02                                                          
*                                                                               
ARRIN04  LTR   RF,RF               ANYTHING LEFT NEEDING MOVING?                
         BZ    ARRIN06             NO                                           
         SR    RE,RF               BACK UP AMOUNT LEFT                          
         SR    R1,RF                                                            
         BCTR  RF,0                                                             
         EX    RF,*+8              AND COPY IT DOWN                             
         B     *+10                                                             
         MVC   ARRHOLD(0),0(RE)                                                 
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R1),ARRHOLD                                                  
*                                                                               
ARRIN06  LAM   ARE,AR1,ARZERO      CLEAR ARS                                    
*                                                                               
         LH    RF,LIBKEYL6                                                      
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         XC    LIBKEYD(0),LIBKEYD  CLEAR INSERTION SLOT                         
*                                                                               
         LH    RF,LIBKEYL1                                                      
*                                                                               
         LA    RE,LIBKMAIN                                                      
         TM    LIFLAG1,LIF1BIG                                                  
         BZ    *+8                                                              
         LA    RE,LIBKBMN                                                       
         CPYA  ARE,AR4                                                          
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RE),0(R3)       MOVE IN KEY                                  
         LAM   ARE,ARE,ARZERO                                                   
*                                                                               
         OI    KYMFLAG,KYMFADD     SET RECORD ADDED                             
         L     RF,LIBNKEYA                                                      
         AH    RF,LIBKEYL6                                                      
         ST    RF,LIBNKEYA         UPDATE NEXT KEY POINTER                      
         L     RF,LIBRECN                                                       
         LA    RF,1(RF)                                                         
         ST    RF,LIBRECN                                                       
         XMOD1 1                                                                
         DROP  R4,R8                                                            
*                                                                               
ARRWRKD  DSECT                                                                  
ARRHOLD  DS    XL256                                                            
ARRWRKL  EQU   *-ARRWRKD                                                        
*                                                                               
ARREDIT  CSECT                                                                  
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
FF       EQU   X'FF'                                                            
*                                                                               
EYECATCH DC    CL8'*ARREDIT*'                                                   
ARZERO   DC    16F'0'                                                           
*                                                                               
ACTTAB   DS    0XL4                                                             
         DC    AL1(LIAADD),AL3(LINADD)   ADD RECORD                             
         DC    AL1(LIAHIGH),AL3(LINHIGH) READ HIGH                              
         DC    AL1(LIASEQ),AL3(LINSEQ)   READ SEQUENTIAL                        
         DC    AL1(LIAWRT),AL3(LINWRT)   WRITE RECORD                           
         DC    AL1(LIAFIX),AL3(LINFIX)   FIX ARRAY POINTERS                     
         DC    AL1(LIAINI),AL3(LININI)   INITIALISE BUFFER                      
ACTTABX  DC    AL1(LIAREP),AL3(LINREP)   REPORT ON SIZES                        
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE                                                     *         
***********************************************************************         
WORKD    DSECT                                                                  
DUB      DS    D                                                                
RELO     DS    A                                                                
AENTRY   DS    A                                                                
ARECORD  DS    A                                                                
AAREC    DS    A                                                                
ADPRT    DS    A                                                                
APREV    DS    A                                                                
NPTR     DS    XL3                                                              
NPTRX    DS    XL4                                                              
*                                                                               
KYMFLAG  DS    X                                                                
KYMFNF   EQU   X'80'               RECORD NOT FOUND                             
KYMFPTR  EQU   X'40'               RECORD RETURNED IS A POINTER RECORD          
KYMFADD  EQU   X'20'               RECORD ADDED                                 
KYMFCUR  EQU   X'10'               CURRENT BUFFER SPACE UPDATED                 
*                                                                               
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
* DDARREDITD                                                                    
       ++INCLUDE DDARREDITD                                                     
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004DDARREDIT 04/23/15'                                      
         END                                                                    
