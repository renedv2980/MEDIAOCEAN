*          DATA SET AXTRBROT   AT LEVEL 078 AS OF 10/26/20                      
*CATALP AXTRBROT                                                                
* IN UPDATE MODE, DXAXREC IS CHANGE OR ADD WITH RECOVERY HEADER!                
* DXACPYB IS THE COPY                                                           
                                                                                
         TITLE 'EXTRACT ROUTINES FOR AXTRACT'                                   
*======================================================================         
* GHOA 002 2018NOV16 USE PID AND AGY FROM PIDEL                                 
*                    FIX NET AMOOUNT                                            
*                    GET NETWORK CODE                                           
* GHOA 004 2018DEC19 GET MOS FROM MBIEL/GLDEL IF NOT FOUND FOR PAY              
* GHOA 005 2019APR12 CHANGES FOR CASH RECEIVE AND CASH PAY                      
*======================================================================         
AXTRBROT CSECT                                                                  
         ENTRY AXACCT              ACCOUNT RECORD                               
         ENTRY AXTRNS              TRANSACTION RECORD                           
         ENTRY AXTOTS              TRANSACTION RECORD                           
         ENTRY AXFRH               FILE RECON HEADER REC                        
         PRINT NOGEN                                                            
*                                                                               
EQXIT    CR    RB,RB                                                            
         J     EXIT                                                             
*                                                                               
NEQXIT   LTR   RB,RB                                                            
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
*===============================================================                
* AXACCT - EXTRACT NAMES FROM ACCOUNT RECORDS                                   
*                                                                               
* AL4 A(WORKD)                                                                  
* AL4 A(EXTRACT RECORD)                                                         
* AL4 A(ACCFILE RECORD)                                                         
*===============================================================                
                                                                                
AXACCT   NMOD1 0,*AXACCT*                                                       
         LR    R9,RB                                                            
         AHI   R9,4096                                                          
         USING AXACCT+4096,R9                                                   
*                                                                               
         L     RC,0(R1)                                                         
         USING WORKD,RC                                                         
*                                                                               
         L     RA,AADDRESS                                                      
         USING ADDRESSD,RA                                                      
*                                                                               
         ST    R1,APARM                                                         
         MVC   PARM(12),0(R1)      EXTRACT PARAMETER LIST                       
         LM    R2,R3,4(R1)                                                      
*                                                                               
         L     RE,=A(MXTRT)                                                     
         ST    RE,AMXTRT                                                        
*                                                                               
         LA    R2,0(R2)            CLEAR HOB                                    
         USING AXGMACC,R2          R2=A(EXTRACT RECORD)                         
         LA    R3,0(R3)                                                         
         USING ACTKEY,R3           R3=A(ACCFILE RECORD)                         
*                                                                               
         L     R6,12(R1)           SXDTABD                                      
         LA    R6,0(R6)            CLEAR HOB                                    
         USING SXDTABD,R6          R6=A(EXTRACT SYSTEM TABLE ENTRY)             
*                                                                               
         XC    AXGMLEN,AXGMLEN     RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXGMLEN(2),=Y(AXGMACCX-AXGMACC)                                  
         MVI   AXGMACTN,C'A'                                                    
*                                                                               
         CLI   PARM+4,C'L'         TEST LOAD MODE                               
         BE    AXA2                YES- ALL RECORDS ARE ADDS                    
*                                                                               
         L     RE,VDXBLOCK         A(EXTRACT CONTROL DATA BLOCK)                
         USING DXBLOCKD,RE                                                      
         CLI   DXACTION,C'C'       ELSE TEST UPDATE ACTION                      
         BNE   AXA2                                                             
         MVI   AXGMACTN,C'C'                                                    
         DROP  RE                                                               
                                                                                
AXA2     MVI   AXGMSEQ-1,MYMXTRTQ                                               
         MVI   AXGMMSTR-1,MYMXTRTQ                                              
         MVI   AXGMACCD-1,MYMXTRTQ                                              
         MVI   AXGMACNM-1,MYMXTRTQ                                              
         MVI   AXGMCAC-1,MYMXTRTQ                                               
         MVI   AXGMRAC-1,MYMXTRTQ                                               
         MVI   AXGMSTAT-1,MYMXTRTQ                                              
         MVI   AXGMFLT1-1,MYMXTRTQ                                              
         MVI   AXGMFLT2-1,MYMXTRTQ                                              
         MVI   AXGMFLT3-1,MYMXTRTQ                                              
         MVI   AXGMFLT4-1,MYMXTRTQ                                              
         MVI   AXGMFLT5-1,MYMXTRTQ                                              
         MVI   AXGMACCX-1,MYMXTRTQ                                              
*                                                                               
*        BRAS  RE,GETGIN                                                        
         CLI   PARM+4,C'L'         TEST LOAD MODE                               
         BE    *+8                 YES                                          
         LA    R3,28(R3)           POINT BEYOND RECOVERY HEADER                 
*                                                                               
         CLI   ACTKACT,C' '        TEST ACCOUNT PRESENT                         
         JH    AXA10               YES - EXTRACT ACCOUNT NAME                   
*                                                                               
AXASKIP  MVC   DMCB(12),PARM       RESTORE CALLING PARMS                        
         MVI   DMCB+8,X'FF'        TELL CONTROLLER NO RECORD                    
         J     EXIT                                                             
*                                                                               
AXA10    L     RE,=A(AXGMACCQ)                                                  
         MVC   AXGMTYPE,0(RE)                                                   
*                                                                               
         MVC   AXGMACCD,ACTKUNT    MOVE U/L/ACCT                                
*                                                                               
*&&DO                                                                           
* - REMOVING PER JEANNE ON 20130409                                             
*                                                                               
         CLC   =C'SX',ACTKUNT                                                   
         JE    AXASKIP                                                          
*&&                                                                             
*                                                                               
         MVI   JOBSTAT,0           INIT STATUS FIELD                            
         MVC   AXGMMSTR,=C'VE'                                                  
         CLC   =C'SR',ACTKUNT                                                   
         JNE   AXA14                                                            
         MVC   AXGMMSTR,=C'SR'     SR IS THE DEFAULT FOR SR ACCT                
         CLC   SXDTAGY,=C'UB'      CS FOR ALPHAS UB/CH/CL                       
         JE    AXA12                                                            
         CLC   SXDTAGY,=C'CH'                                                   
         JE    AXA12                                                            
         CLC   SXDTAGY,=C'CL'                                                   
         JNE   AXA14                                                            
AXA12    MVC   AXGMMSTR,=C'CS'                                                  
AXA14    CLC   =C'SJ',ACTKUNT                                                   
         JNE   AXA20                                                            
         MVC   AXGMMSTR,=C'JB'                                                  
         CLI   ACTKACT+6,C' '      TEST JOB     PRESENT                         
         JH    AXA15               YES                                          
         MVC   AXGMMSTR,=C'PR'                                                  
         CLI   ACTKACT+3,C' '      TEST PRODUCT PRESENT                         
         JH    AXA15               YES                                          
         MVC   AXGMMSTR,=C'CL'                                                  
*------->                                                                       
*------->                                                                       
AXA15    DS    0H                                                               
         MVC   JOBSTAT,ACTRSTAT    SAVE OFF REAL STATUS                         
         LA    R5,ACTRFST                                                       
         MVI   ELCODE,PPRELQ                                                    
         BRAS  RE,FRSTEL                                                        
         JE    *+6                                                              
         DC    H'0'                                                             
         CLC   =C'SS',PPRGAOFF-PPREL(R5)                                        
         JNE   AXA20                                                            
         MVC   SKIPCLT,ACTKACT                                                  
         J     AXASKIP                                                          
SKIPCLT  DC    CL3' '                                                           
         DC    X'00'                                                            
*------->                                                                       
*------->                                                                       
*                                                                               
AXA20    CLC   SKIPCLT,ACTKACT     SKIPPING THIS CLIENT                         
         JE    AXASKIP                                                          
*                                                                               
         CLC   =C'SJ',ACTKUNT                                                   
         JNE   AXA21                                                            
         CLI   PARM+4,C'L'         TEST LOAD MODE                               
         JNE   AXA21A                                                           
         TM    ACTRSTAT,ACTSDELT+ACTSCLOS+ACTSLOCK                              
         JZ    AXA21A                                                           
         CLC   AXGMMSTR,=C'CL'                                                  
         JNE   AXASKIP                                                          
         MVC   SKIPCLT,ACTKACT     SKIPPING THIS CLIENT                         
         J     AXASKIP                                                          
AXA21    TM    ACTRSTAT,ACTSDELT+ACTSCLOS                                       
         JNZ   AXASKIP                                                          
*                                                                               
AXA21A   EDIT  JOBSTAT,(3,AXGMSTAT),FILL=0,ZERO=NOBLANK                         
*                                                                               
         BRAS  RE,SETLDGR          SET LEDGER TYPE                              
*                                                                               
         CLC   =C'SV',ACTKUNT      FOR SV/SX ONLY IF NOT LOCKED                 
         JE    *+14                                                             
         CLC   =C'SX',ACTKUNT                                                   
         JNE   AXA22                                                            
         TM    ACTRSTAT,ACTSLOCK                                                
         JO    AXASKIP                                                          
         J     AXA30                                                            
*                                                                               
AXA22    MVI   ELCODE,RSTELQ                                                    
         LA    R5,ACTRFST                                                       
         BRAS  RE,FRSTEL                                                        
         JNE   AXA30                                                            
*&&DO                                                                           
         CLI   LDGPQSTU,C'Y'                                                    
         JE    AXA24                                                            
         CLC   =C'SW',ACTKUNT                                                   
         JE    AXA24                                                            
         J     AXA26                                                            
*&&                                                                             
*                                                                               
AXA24    CLC   AXGMMSTR,=C'VE'                                                  
         JNE   *+14                                                             
         CLC   =X'B60101',RSTTDATE-RSTEL(R5)                                    
         JH    AXASKIP                                                          
         MVI   AXGMFLT1,X'40'                                                   
         MVI   AXGMFLT2,X'40'                                                   
         MVI   AXGMFLT3,X'40'                                                   
         MVI   AXGMFLT4,X'40'                                                   
         MVI   AXGMFLT5,X'40'                                                   
         CLI   RSTFILT1-RSTEL(R5),X'40'                                         
         JNH   *+10                                                             
         MVC   AXGMFLT1,RSTFILT1-RSTEL(R5)                                      
         CLI   RSTFILT2-RSTEL(R5),X'40'                                         
         JNH   *+10                                                             
         MVC   AXGMFLT2,RSTFILT2-RSTEL(R5)                                      
         CLI   RSTFILT3-RSTEL(R5),X'40'                                         
         JNH   *+10                                                             
         MVC   AXGMFLT3,RSTFILT3-RSTEL(R5)                                      
         CLI   RSTFILT4-RSTEL(R5),X'40'                                         
         JNH   *+10                                                             
         MVC   AXGMFLT4,RSTFILT4-RSTEL(R5)                                      
         CLI   RSTFILT5-RSTEL(R5),X'40'                                         
         JNH   *+10                                                             
         MVC   AXGMFLT5,RSTFILT5-RSTEL(R5)                                      
         J     AXA30                                                            
*                                                                               
AXA26    CLC   =C'SY',ACTKUNT                                                   
         JNE   AXA30                                                            
         CLC   =X'B20101',RSTTDATE-RSTEL(R5)                                    
         JH    AXASKIP                                                          
         J     AXA30                                                            
*                                                                               
AXA30    AP    SEQNUM,=P'1'                                                     
         OI    SEQNUM+5,X'0F'                                                   
         UNPK  AXGMSEQ,SEQNUM                                                   
         MVC   AXGMACNM,MYSPACES                                                
*                                                                               
         MVI   ELCODE,NAMELQ                                                    
         LA    R5,ACTRFST          R5=A(FIRST ELEMENT)                          
         BRAS  RE,FRSTEL           GET ELEMENT FROM RECORD                      
         JNE   AXA40                                                            
*                                                                               
         LLC   RF,NAMLN-NAMEL(R5)                                               
         AHI   RF,-3                                                            
         JM    AXA40                                                            
         EX    RF,*+8                                                           
         J     *+10                                                             
         MVC   AXGMACNM(0),NAMEREC-NAMEL(R5)                                    
*                                                                               
         L     RF,AMXTRT                                                        
         TR    AXGMACNM,0(RF)                                                   
*                                                                               
*===============================================================                
* FOR CLIENTS AND PRODUCTS, EXTRACT RECEIVABLE AND COST A/C'S                   
*===============================================================                
                                                                                
AXA40    CLI   ACTKACT,C' '           TEST CLIENT PRESENT                       
         JNH   EXIT                   NO - EXIT                                 
         CLI   ACTKACT+6,C' '         TEST JOB                                  
         JH    EXIT                   YES - EXIT                                
                                                                                
* CLIENT OR PRODUCT - GET ACCOUNT CODES                                         
                                                                                
         LA    R5,ACTRFST          R5=A(FIRST ELEMENT)                          
         SR    R0,R0                                                            
*                                                                               
         MVI   ELCODE,PPRELQ       PRODUCTION PROFILE ELEM                      
         LA    R5,ACTRFST                                                       
         BRAS  RE,FRSTEL                                                        
         JNE   EXIT                                                             
*                                                                               
         USING PPRELD,R5                                                        
         MVC   AXGMRAC,PPRRECVA    RECVLBL ACCOUNT (NO CUL)                     
         MVC   AXGMCAC,PPRCOSTA    COSTING ACCOUNT                              
         J     EXIT                                                             
         DROP  R2,R3,R5                                                         
         DROP  R9,RB                                                            
         LTORG                                                                  
         EJECT                                                                  
*===============================================================                
* AXTOTS - PUT OUT TOTAL RECORD                                                 
*                                                                               
* AL4 A(WORKD)                                                                  
* AL4 A(EXTRACT RECORD)                                                         
*===============================================================                
AXTOTS   NMOD1 0,*AXTOTS*                                                       
         LR    R9,RB                                                            
         AHI   R9,4096                                                          
         USING AXACCT+4096,R9                                                   
*                                                                               
         L     RC,0(R1)                                                         
         USING WORKD,RC                                                         
*                                                                               
         L     RA,AADDRESS                                                      
         USING ADDRESSD,RA                                                      
*                                                                               
         ST    R1,APARM                                                         
         MVC   PARM(12),0(R1)      EXTRACT PARAMETER LIST                       
         L     R2,4(R1)                                                         
*                                                                               
         L     RE,=A(MXTRT)                                                     
         ST    RE,AMXTRT                                                        
*                                                                               
         CP    SEQNUM,=P'0'        TEST NO OUTPUT RECORDS                       
         JNE   AXTOT2                                                           
         MVC   DMCB(12),PARM       RESTORE PARMS                                
         MVI   DMCB+8,X'FF'        TELL CONTROLLER TO SKIP REC                  
         J     EXIT                                                             
*                                                                               
AXTOT2   LA    R2,0(R2)            CLEAR HOB                                    
         USING AXGTTOT,R2                                                       
*                                                                               
         MVC   0(2,R2),=Y(AXGTTOTX-AXGTTOT) SET RECLEN                          
*                                                                               
         MVI   AXGTDRS-1,MYMXTRTQ                                               
         MVI   AXGTCRS-1,MYMXTRTQ                                               
         MVI   AXGTRECS-1,MYMXTRTQ                                              
         MVI   AXGTTOTX-1,MYMXTRTQ                                              
*                                                                               
         L     RE,=A(AXGMTOTQ)                                                  
         MVC   AXGTTYPE,0(RE)                                                   
*                                                                               
         ZAP   DUB,TOTALDR                                                      
         BRAS  RE,TOTCVDP                                                       
         MVC   AXGTDRS,P16                                                      
*                                                                               
         ZAP   DUB,TOTALCR                                                      
         BRAS  RE,TOTCVDP                                                       
         MVC   AXGTCRS,P16                                                      
*                                                                               
         EDIT  (P6,SEQNUM),(8,AXGTRECS),ALIGN=LEFT,ZERO=NOBLANK                 
         J     EXIT                                                             
*                                                                               
TOTCVDP  EDIT  (P8,DUB),P16,2,FLOAT=-,COMMAS=NO,ZERO=NOBLANK,          X        
               ALIGN=LEFT                                                       
         BR    RE                                                               
         LTORG                                                                  
         EJECT                                                                  
*===============================================================                
* AXFRH - CREATE HEADER FOR FILE RECON RUN                                      
*                                                                               
* AL4 A(WORKD)                                                                  
* AL4 A(EXTRACT RECORD)                                                         
*===============================================================                
                                                                                
AXFRH    NMOD1 0,*AXHDR*                                                        
         LR    R9,RB                                                            
         AHI   R9,4096                                                          
         USING AXACCT+4096,R9                                                   
*                                                                               
         L     RC,0(R1)                                                         
         USING WORKD,RC                                                         
*                                                                               
         L     RA,AADDRESS                                                      
         USING ADDRESSD,RA                                                      
*                                                                               
         ST    R1,APARM                                                         
         MVC   PARM(12),0(R1)      EXTRACT PARAMETER LIST                       
         L     R2,4(R1)                                                         
*                                                                               
         L     RE,=A(MXTRT)                                                     
         ST    RE,AMXTRT                                                        
*                                                                               
         LA    R2,0(R2)            CLEAR HOB                                    
         USING AXGMFRH,R2                                                       
*                                                                               
         MVC   0(2,R2),=Y(AXGMFRHX-AXGMFRH) SET RECLEN                          
*                                                                               
         MVI   AXGMFTY-1,MYMXTRTQ                                               
         MVI   AXGMFACT-1,MYMXTRTQ                                              
         MVI   AXGMFDT-1,MYMXTRTQ                                               
         MVI   AXGMFTM-1,MYMXTRTQ                                               
         MVI   AXGMFNM-1,MYMXTRTQ                                               
         MVI   AXGMFSDT-1,MYMXTRTQ                                              
         MVI   AXGMFEDT-1,MYMXTRTQ                                              
         MVI   AXGMFRHX-1,MYMXTRTQ                                              
*                                                                               
         L     RE,=A(AXGMFRHQ)                                                  
         MVC   AXGMFTY,0(RE)                                                    
*                                                                               
         L     RE,VDXBLOCK         A(EXTRACT CONTROL DATA BLOCK)                
         USING DXBLOCKD,RE                                                      
*                                                                               
         MVC   AXGMFNM(19),=C'FILE RECONCILIATION'                              
         MVC   AXGMFSDT,DXUSER                                                  
         MVC   AXGMFEDT,DXUSER+6                                                
         DROP  RE                                                               
*                                                                               
         J     EXIT                                                             
         LTORG                                                                  
                                                                                
         EJECT                                                                  
*=========================================================                      
* AXTRNC - EXTRACT TRANSACTION DETAILS                                          
* AL4 A(WORK AREA)                                                              
* AL4 A(EXTRACT RECORD)                                                         
* AL4 A(ACCFILE RECORD)                                                         
*=========================================================                      
*                                                                               
AXTRNS   NMOD1 0,*AXTRNS*                                                       
*                                                                               
         LR    R9,RB                                                            
         AHI   R9,4096                                                          
         USING AXTRNS+4096,R9                                                   
*&&DO                                                                           
         LR    R8,R9                                                            
         AHI   R8,4096                                                          
         USING AXACCT+8192,R8                                                   
*&&                                                                             
         L     RC,0(R1)                                                         
         USING WORKD,RC                                                         
*                                                                               
         L     RA,AADDRESS                                                      
         USING ADDRESSD,RA                                                      
*                                                                               
         ST    R1,APARM                                                         
         MVC   PARM(16),0(R1)      EXTRACT PARAMETER LIST                       
*                                                                               
         L     R2,4(R1)                                                         
         LA    R2,0(R2)            CLEAR HOB                                    
         L     R6,12(R1)           SXDTABD                                      
         LA    R6,0(R6)            CLEAR HOB                                    
         USING AXG$TRN,R2          R2=A(EXTRACT RECORD)                         
         USING SXDTABD,R6          R6=A(EXTRACT SYSTEM TABLE ENTRY)             
*                                                                               
         XC    AXG$LEN,AXG$LEN                                                  
         XC    AXG$PID(2),AXG$PID                                               
         MVC   AXG$LEN(2),=Y(AXG$TRNX-AXG$TRN)                                  
         MVC   AXG$TYPE,AXGMTRNQ                                                
         MVI   AXG$ACTN,C'A'                                                    
         CLI   PARM+4,C'L'         TEST LOAD MODE                               
         BE    AXT2                YES- ALL RECORDS ARE ADDS                    
                                                                                
*============================================================                   
* UPDATE MODE - SKIP ADDS THAT ARE IN DRAFT STATUS                              
* SKIP ADD IF IT IS IN DRAFT STATUS                                             
* SKIP CHG IF IT IS IN DRAFT STATUS  AND                                        
* SKIP COPY/CHG IF COPY IS NOT IN DRAFT STATUS                                  
*============================================================                   
                                                                                
         L     R3,PARM+8           POINT TO ADD OR CHG RECORD                   
         LA    R3,28(R3)                                                        
         USING TRNRECD,R3                                                       
*                                                                               
         TM    TRNRSTAT,TRNSDRFT   IF IT'S DRAFT                                
         JO    AXTSKIP             SKIP IT                                      
*                                                                               
         L     RE,VDXBLOCK         R7=A(EXTRACT CONTROL DATA BLOCK)             
         USING DXBLOCKD,RE                                                      
         CLI   DXACTION,C'C'       TEST ACTION IS CHANGE                        
         JNE   AXT2                NO - GO PROCESS ADD                          
**NOP**  MVI   AXG$ACTN,C'C'       DRAFT --> LIVE IS AN ADD!                    
                                                                                
         L     R3,DXACPYB          POINT TO COPY RECORD                         
         LA    R3,28(R3)                                                        
         USING TRNRECD,R3                                                       
         DROP  RE                                                               
*                                                                               
         TM    TRNRSTAT,TRNSDRFT   IF COPY IS DRAFT                             
         JO    AXT2                PROCESS THIS TRANSACTION                     
*                                                                               
AXTSKIP  MVC   DMCB(12),PARM       RESTORE PARMS                                
         MVI   DMCB+8,X'FF'        TELL CONTROLLER TO SKIP REC                  
         J     EXIT                                                             
                                                                                
*======================================================                         
* PUT DELIMITER AFTER EACH FIELD                                                
*======================================================                         
                                                                                
AXT2     LA    R1,AXTRNTAB                                                      
         LA    R0,(AXTRNTBX-AXTRNTAB)/L'AXTRNTAB                                
*                                                                               
AXT4     LH    RE,0(R1)            GET DSPL TO DELIM LOCATION                   
         AR    RE,R2               POINT TO IT IN RECORD                        
         MVI   0(RE),MYMXTRTQ                                                   
         LA    R1,L'AXTRNTAB(R1)                                                
         JCT   R0,AXT4                                                          
*                                                                               
*        BRAS  RE,GETGIN                                                        
*                                                                               
         L     R3,PARM+8           GET ADDRESS OF NEW RECORD                    
         LA    R3,0(R3)            CLEAR HOB                                    
         USING TRNRECD,R3          R3=A(ACCFILE RECORD)                         
         CLI   PARM+4,C'L'         TEST LOAD MODE                               
         JE    *+8                                                              
         LA    R3,28(R3)           POINT BEYOND RECOVERY DATA                   
*                                                                               
         MVC   AXG$ACCD,TRNKULA     MOVE U/L/ACCT                               
         MVC   AXG$CONT,TRNKULC     MOVE CONTRA U/L/ACCT                        
         MVC   AXG$AGY,SXDTAGY     MOVE AGENCY ALPHA                            
*                                                                               
         BAS   RE,SETLDGR          SET LEDGER TYPE                              
         XC    ELADDRS(ELADDRX-ELADDRS),ELADDRS                                 
*                                                                               
         BAS   RE,GETELEMS         GET ELEMENT ADDRESSES                        
*                                                                               
         MVI   SAPRUN,C'N'                                                      
         MVI   OMGRUN,C'N'                                                      
         L     RE,VDXBLOCK         A(EXTRACT CONTROL DATA BLOCK)                
         USING DXBLOCKD,RE                                                      
         CLC   =C'OMG',DXUSER                                                   
         JNE   *+8                                                              
         MVI   OMGRUN,C'Y'                                                      
         CLC   =C'SAP',DXUSER                                                   
         JNE   AXT4A                                                            
         TM    LDG,RCVLDGQ+PAYLDGQ                                              
         JZ    AXTSKIP                                                          
         MVI   SAPRUN,C'Y'                                                      
         DROP  RE                                                               
*                                                                               
AXT4A    CLI   PARM+4,C'L'         TEST LOAD MODE                               
         JNE   AXT5                                                             
*                                                                               
         LT    R5,ATRSEL           FILTER ON ACTIVITY DATE!                     
         JNZ   *+6                                                              
         DC    H'0'                                                             
         CLC   MYSDATE2,TRSDATE-TRSEL(R5)                                       
         JH    AXTSKIP                                                          
         CLC   MYEDATE2,TRSDATE-TRSEL(R5)                                       
         JL    AXTSKIP                                                          
*                                                                               
AXT5     LT    R5,ATRNEL                                                        
         JNZ   *+6                                                              
         DC    H'0'                                                             
         USING TRNELD,R5                                                        
         CLI   TRNTYPE,12  <===    JCLE SAYS NO TYPE 12'S                       
         JE    AXTSKIP     <===                                                 
*                                                                               
         LLC   R0,TRNTYPE                                                       
         BRAS  RE,CVD                                                           
         MVC   AXG$TTYP,P16+13     TRANSACTION TYPE                             
*                                                                               
         CLC   =C'SJ',TRNKUNT      SKIP SJ/TYPE 49'S                            
         JNE   AXT6                                                             
         CLI   TRNTYPE,49                                                       
         JE    AXTSKIP                                                          
*                                                                               
AXT6     AP    SEQNUM,=P'1'                                                     
         OI    SEQNUM+5,X'0F'                                                   
         UNPK  AXG$SEQ,SEQNUM                                                   
*                                                                               
         TM    LDG,RCVLDGQ                                                      
         BZ    *+10                                                             
         MVC   AXG$RTYP,=C'RCV'                                                 
         TM    LDG,PAYLDGQ                                                      
         BZ    *+10                                                             
         MVC   AXG$RTYP,=C'PAY'                                                 
*                                                                               
         GOTO1 VDATCON,DMCB,(1,TRNDATE),(20,AXG$TRDT) TRANS DATE                
         GOTO1 ENGDATE,DMCB,AXG$TRDT                                            
         MVC   AXG$LINI(8),AXG$TRDT                                             
         MVI   AXG$LINI+8,C'-'                                                  
*                                                                               
         BAS   RE,GETOFFC                                                       
*                                                                               
         MVC   AXG$TRRF,TRNREF                                                  
         LLC   R0,TRNSUB                                                        
         BRAS  RE,CVD                                                           
         MVC   AXG$TRSR,P16+13                                                  
*                                                                               
         ZAP   DUB,TRNAMNT         TRANSACTION AMOUNT                           
*                                                                               
         LA    RE,TOTALDR          ADD TO REPORT TOTALS                         
         LA    RF,AXG$DR                                                        
         MVI   DRORCR,C'D'                                                      
*                                                                               
         TM    TRNSTAT,TRNSDR                                                   
         JO    AXT7                                                             
*                                                                               
         LA    RE,TOTALCR                                                       
         LA    RF,AXG$CR                                                        
         MVI   DRORCR,C'C'                                                      
*                                                                               
AXT7     AP    0(8,RE),DUB                                                      
         MVC   AXG$STYP,=C'INV'    ASSUME INV                                   
         CP    DUB,=P'0'                                                        
         JNL   *+10                                                             
         MVC   AXG$STYP,=C'CRM'    OTHERWISE CRM                                
*                                                                               
         BAS   RE,TRNCVDP                                                       
         MVC   0(16,RF),P16                                                     
*                                                                               
         MVC   AXG$TAXC(2),=C'MI'  DEFAULT FOR PAYABLES                         
         TM    LDG,PAYLDGQ                                                      
         JO    *+10                                                             
         MVC   AXG$TAXC(2),=C'MS'  RECEIVABLES                                  
*                                                                               
AXT8     GOTO1 VDATCON,DMCB,(1,TRNDATE),(20,AXG$ACDT)  ACTV DT                  
         GOTO1 ENGDATE,DMCB,AXG$ACDT                                            
*                                                                               
         MVC   AXG$BREF,TRNBTCH                                                 
         OC    AXG$BREF,MYSPACES   REMOVE IMBEDDED BINARY ZEROES.               
         LLC   RE,1(R5)            GET ELEMENT LENGTH                           
         AHI   RE,-TRNLN1Q         - LEN TO START OF NARRATIVE                  
         JNP   AXT10B                                                           
*                                                                               
         TM    LDG,SJLDGQ          TEST SJ                                      
         JZ    AXT10A                                                           
         MVC   AXG$SYS,TRNKLDG     FOR PRODUCTION, USE LEDGER                   
         CLC   =C'99',TRNANAL      WORK CODE 99 IS SPECIAL                      
         JNE   AXT10A                                                           
         LA    RE,15               JUST 15 BYTES OF NARRATIVE                   
*                                                                               
AXT10A   L     RF,NARCOUNT         BUMP COUNT OF RECS WITH NARR                 
         AHI   RF,1                                                             
         ST    RF,NARCOUNT                                                      
*                                                                               
         LA    RF,NARCNT20                                                      
         CHI   RE,20                                                            
         JNH   AXT10CNT                                                         
         LA    RF,NARCNT40                                                      
         CHI   RE,40                                                            
         JNH   AXT10CNT                                                         
         LA    RF,NARCNT60                                                      
         CHI   RE,60                                                            
         JNH   AXT10CNT                                                         
         LA    RF,NARCNT80                                                      
*                                                                               
AXT10CNT L     R0,0(RF)                                                         
         AHI   R0,1                                                             
         ST    R0,0(RF)                                                         
*                                                                               
         BCTR  RE,0                                                             
         LA    R1,TRNNARR                                                       
         EXRL  RE,MVNARR           MVC  AXG$NARR,0(R1)                          
         TR    AXG$NARR,MXTRT                                                   
*                                                                               
AXT10B   LT    R5,ATRSEL           X'60' TRANS STATUS ELEMENT                   
         JNZ   *+6                                                              
         DC    H'0'                                                             
         USING TRSELD,R5                                                        
*                                                                               
         MVC   FULL(2),TRSPMOS                                                  
         MVI   FULL+2,1                                                         
         LA    R4,FULL                                                          
         BAS   RE,GODATCON                                                      
         MVC   AXG$MOA,WORK+2      ACTIVITY MONTH YYMM ONLY                     
*                                                                               
         GOTO1 VDATCON,DMCB,(2,TRSBSTDT),(20,AXG$STDT) STMNT DATE               
         GOTO1 ENGDATE,DMCB,AXG$STDT                                            
* THIS DATE IS COMPRESSED - SO BE CAREFUL!                                      
         GOTO1 VDATCON,DMCB,(2,TRSDATE),(20,AXG$ACDT) ACVTY DATE                
         GOTO1 ENGDATE,DMCB,AXG$ACDT                                            
         DROP  R5                                                               
*                                                                               
         CLC   =C'SG',TRNKUNT      ARE WE DOING TAXES?                          
         JE    *+12                                                             
         TM    LDG,RCVLDGQ+INCLDGQ+PAYLDGQ                                      
         JZ    AXT20                                                            
*                                                                               
         TM    LDG,PAYLDGQ                                                      
         JZ    AXT12                                                            
         MVC   AXG$SYS,TRNKLDG            FOR PAYABLES, USE LEDGER              
         CLI   AXG$SYS,C'U'               FOR NET                               
         JNE   *+8                                                              
         MVI   AXG$SYS,C'N'               MAKE IT N                             
         CLI   AXG$SYS,C'Q'               FOR CANADIAN PRINT                    
         JNE   *+8                                                              
         MVI   AXG$SYS,C'P'               MAKE IT P                             
         CLI   AXG$SYS,C'T'               FOR CANADIAN SPOT                     
         JNE   *+8                                                              
         MVI   AXG$SYS,C'S'               MAKE IT P                             
*                                                                               
AXT12    LT    R5,AMDTEL           X'1A' = MEDIA TRANSFER ELEM                  
         JZ    *+12                                                             
         LA    RE,MDTMOS-MDTELD(R5)                                             
         J     AXT16                                                            
         LT    R5,AMDPEL           X'6A' = PACKED VERSION                       
         JZ    *+12                                                             
         LA    RE,MDPMOS-MDPELD(R5)                                             
         J     AXT16                                                            
         LT    R5,AGDAEL           X'EF' = GENERAL DATE ELEM                    
         JZ    AXT20                                                            
         SR    R1,R1                                                            
AXT14    CLI   0(R5),0             END OF ELEMENTS?                             
         JE    AXT20                                                            
         CLI   0(R5),GDAELQ                                                     
         JNE   *+20                                                             
         CLI   GDATYPE-GDAELD(R5),GDAMMOS                                       
         JNE   *+12                                                             
         LA    RE,GDAYYMM-GDAELD(R5)                                            
         J     AXT16                                                            
         IC    R1,1(R5)                                                         
         AR    R5,R1                                                            
         J     AXT14                                                            
*                                                                               
AXT16    MVC   AXG$MOS,AXG$TRDT    DEFAULT MOS IS TRNS DATE YYMM                
         MVC   AXG$VMOS,AXG$TRDT                                                
         CLC   0(2,RE),MYSPACES    DO WE HAVE A REAL DATE?                      
         JNH   AXT18                                                            
         MVC   FULL(2),0(RE)                                                    
         OC    FULL(2),FULL        DO WE HAVE A REAL DATE?                      
         JZ    AXT18                                                            
         MVI   FULL+2,1                                                         
         LA    R4,FULL                                                          
         BAS   RE,GODATCON                                                      
         MVC   AXG$MOS,WORK+2             MOVE YYMM ONLY                        
         MVC   AXG$VMOS,WORK+2                                                  
*                                                                               
AXT18    CLI   0(R5),GDAELQ                                                     
         JE    *+10                                                             
         MVC   AXG$SYS,MDTSYS-MDTELD(R5)  GOOD FOR BOTH MDT/MBI ELEMS           
*                                                                               
         CLI   0(R5),MDTELQ                                                     
         JNE   AXT20                                                            
         USING MDTELD,R5                                                        
         TM    MDTSTAT,MDTSMIQ                                                  
         JZ    AXT20                                                            
         MVC   AXG$SYS,MDTMED2                                                  
         DROP  R5                                                               
*                                                                               
AXT20    BAS   RE,GETCLT                                                        
                                                                                
         BAS   RE,GETMED                                                        
                                                                                
         BAS   RE,GETPRD                                                        
*                                                                               
         TM    LDG,MEDLDGQ         GET ESTIMATE FOR SZ                          
         JZ    AXT20AAA                                                         
         CLI   AXG$SYS,C' '        IS THE SYSTEM FIELD BLANK?                   
         JNE   AXT20AAA                                                         
         MVC   AXG$SYS,TRNKACT     MOVE IN FIRST BYTE OF ACCOUNT                
         CLI   AXG$MED,C' '        IS THE MEDIA FIELD BLANK?                    
         JNE   AXT20AAA                                                         
         MVC   AXG$MED,TRNKACT+1   MOVE IN NEXT BYTE OF ACCOUNT                 
AXT20AAA CLI   AXG$SYS,C'S'        ARE WE DOING SPOT?                           
         JNE   AXT20AAB                                                         
         CLI   AXG$MED,C'T'        ARE WE DOING SPOT TV?                        
         JE    AXT20AAB                                                         
         CLI   AXG$MED,C'R'        ARE WE DOING SPOT RADIO?                     
         JE    AXT20AAB                                                         
         CLI   AXG$MED,C'X'        ARE WE DOING SPOT NET RADIO?                 
         JE    AXT20AAB                                                         
         MVI   AXG$SYS,C'N'        IF NOT, WE MUST BE DOING NET                 
*                                                                               
AXT20AAB CLI   AXG$SYS,C'P'        PRINT?                                       
         BNE   *+12                                                             
         MVI   AXG$SYS+1,C'M'        PM                                         
         B     AXT20AAC                                                         
         CLI   AXG$SYS,C'N'        NET?                                         
         BNE   *+12                                                             
         MVI   AXG$SYS+1,C'E'        NE                                         
         B     AXT20AAC                                                         
         CLI   AXG$SYS,C'S'        SPOT?                                        
         BNE   *+8                                                              
         MVI   AXG$SYS+1,C'P'        SP                                         
                                                                                
AXT20AAC CLC   =C'SG',TRNKUNT      ARE WE DOING TAXES?                          
         JE    *+12                                                             
         TM    LDG,RCVLDGQ+INCLDGQ+PAYLDGQ                                      
         JZ    *+8                                                              
         BAS   RE,GETEST                                                        
*                                                                               
         CLC   OMGRUN,=C'Y'        ARE WE RUNNING FOR OMG?                      
*        JNE   AXT20AB                                                          
         JNE   AXT20AAD                                                         
         CLC   =C'SG',TRNKUNT      ARE WE DOING TAXES?                          
         JE    *+16                                                             
         TM    LDG,MEDLDGQ         GET ESTIMATE FOR SZ                          
         JZ    AXT20AB                                                          
         BAS   RE,GETEST                                                        
*                                                                               
AXT20AAD DS    0H                                                               
*                                                                               
         CLC   =C'SP',TRNKUNT      FOR SP/SS THAN IT'S USD                      
         JE    AXT20AAF                                                         
         CLC   =C'SS',TRNKUNT                                                   
         JNE   AXT20AAH                                                         
         CLC   =C'NE',AXG$SYS      IF (SS AND NET) LEAVE DEFAULT                
         JE    AXT20AAJ                                                         
                                                                                
AXT20AAF MVC   AXG$CURR,=C'USD'    CHANGE TO USD FOR US LEDGERS                 
*                                                                               
AXT20AAH CLC   =C'ST',TRNKUNT      FOR ST/SQ THAN IT'S CAD                      
         JE    AXT20AAI                                                         
         CLC   =C'SQ',TRNKUNT                                                   
         JNE   AXT20AAJ                                                         
AXT20AAI MVC   AXG$CURR,=C'CAD'    CHANGE TO CAD FOR CANADIAN LEDGERS           
*                                                                               
AXT20AAJ CLC   AXG$MOS,MYSPACES    ANY MOS?                                     
         JH    AXT20AB                                                          
*                                                                               
         LT    R5,AMBIEL           X'F3' = NEW BILLING TRANSFER ELEM            
         JZ    AXT20AA                                                          
         MVC   FULL(2),MBIMOS-MBIELD(R5)                                        
         MVI   FULL+2,1                                                         
         LA    R4,FULL                                                          
         BAS   RE,GODATCON                                                      
         MVC   AXG$MOS,WORK+2      MOVE YYMM ONLY                               
         MVC   AXG$VMOS,WORK+2                                                  
         J     AXT20AB                                                          
*                                                                               
AXT20AA  LT    R5,AGLDEL           X'63' = GENERAL LEDGER DETAIL ELEM           
         JZ    AXT20AB                                                          
         MVC   FULL(2),GLDPMOS-GLDELD(R5)                                       
         MVI   FULL+2,1                                                         
         LA    R4,FULL                                                          
         BAS   RE,GODATCON                                                      
         MVC   AXG$MOS,WORK+2      MOVE YYMM ONLY                               
         MVC   AXG$VMOS,WORK+2                                                  
*                                                                               
AXT20AB  TM    LDG,RCVLDGQ+INCLDGQ+PAYLDGQ+EXPLDGQ+SJLDGQ+SCLDGQ                
*        JZ    *+8                                                              
*        BAS   RE,GETJOB                                                        
*                                                                               
         TM    LDG,EXPLDGQ         TEST EXPENSE LDGR                            
         BZ    AXT21                                                            
         L     R5,ATRNEL                                                        
         CLI   TRNTYPE-TRNEL(R5),12                                             
         JNE   AXT20A                                                           
         LT    R5,AEXOEL                                                        
         JZ    AXT20A                                                           
*        MVC   AXG$DEPT+2(3),EXODEP-EXOEL(R5)                                   
         J     AXT21                                                            
*                                                                               
AXT20A   DS    0H                                                               
*&&DO                                                                           
         CLC   =C'SE',TRNKUNT                                                   
         JNE   AXT21                                                            
         LT    R5,AAPEEL               THIS FOR SJ ONLY!                        
         JZ    AXT20B                                                           
         LLC   RE,APENUM-APEELD(R5)    GET NUMBER OF SUBELS                     
         LA    R4,APENTRY-APEELD(R5)                                            
         USING APENTRY,R4                                                       
*                                                                               
AXT20B   CLC   =C'2D',APENACT      TEST FOR DEPARTMENT                          
         JE    AXT20C                                                           
         LLC   R0,0(R4)            GET LENGTH OF SUBEL                          
         AR    R4,R0               POINT TO NEXT ONE                            
         JCT   RE,AXT20B                                                        
         J     AXT21                                                            
*                                                                               
AXT20C   LLC   RE,0(R4)            GET LENGTH OF SUBEL                          
         AHI   RE,-7               LEN/STAT/2D/OF/-1                            
         CHI   RE,6                CAN ONLY HANDLE 7 CHARS OF DEPT              
         JNH   *+8                                                              
         LA    RE,6                                                             
         EX    RE,*+8                                                           
         J     *+10                                                             
         MVC   AXG$DEPT(0),APENACT+4   MOVE DEPARTMENT ONLY                     
         DROP  R4                                                               
*&&                                                                             
AXT21    DS    0H                                                               
**NOP**  MVC   AXG$BSRC,TRNKCACT    BILLING SOURCE=CONTRA ACCT                  
*                                                                               
         BAS   RE,GETINV            INVOICE NUMBER                              
         ZAP   DUB3,=P'0'                                                       
*                                                                               
         CLC   =C'PAY',AXG$RTYP    ONLY LOOKING FOR PAY                         
         JE    AXT21A                                                           
*                                                                               
         LT    R5,AMDTEL                                                        
         JZ    AXT22                                                            
         ICM   R0,15,MDTNET-MDTELD(R5)                                          
         CVD   R0,DUB                                                           
         BRAS  RE,TRNCVDP                                                       
         MVC   AXG$NET,P16         NET AMOUNT                                   
         MP    DUB,=P'-1'                                                       
         BRAS  RE,TRNCVDP                                                       
         MVC   AXG$NETH,P16        NET AMOUNT FOR HEADER                        
*                                                                               
         ICM   R0,15,MDTCOM-MDTELD(R5)                                          
         CVD   R0,DUB                                                           
         BRAS  RE,TRNCVDP                                                       
         MVC   AXG$COMH,P16        COMMISSION FOR HEADER                        
         MP    DUB,=P'-1'                                                       
         BRAS  RE,TRNCVDP                                                       
         MVC   AXG$BINC,P16        COMMISSION                                   
*                                                                               
         ICM   R0,15,MDTGRS-MDTELD(R5)                                          
         JZ    AXT22                                                            
         CVD   R0,DUB                                                           
         BRAS  RE,TRNCVDP                                                       
         MVC   AXG$BGRS,P16        BILLING GROSS                                
         J     AXT22                                                            
*                                                                               
AXT21A   LT    R5,ATRNEL                                                        
         ZAP   DUB,TRNAMNT-TRNELD(6,R5)                                         
         ZAP   DUB3,DUB                                                         
         BRAS  RE,TRNCVDP                                                       
         MVC   AXG$NET,P16         NET AMOUNT                                   
         MVC   AXG$BGRS,P16                                                     
         MVC   AXG$BINC,MYSPACES                                                
         MVC   AXG$BINC(3),=C'.00'                                              
         MP    DUB,=P'-1'                                                       
         BRAS  RE,TRNCVDP                                                       
         MVC   AXG$NETH,P16        NET AMOUNT FOR HEADER                        
         MVC   AXG$COMH,MYSPACES                                                
         MVC   AXG$COMH(3),=C'.00'                                              
*                                                                               
AXT22    ZAP   DUB2,=P'0'                                                       
         LT    R5,ASCIEL                                                        
         JZ    AXT30                                                            
         MVC   ELCODE,0(R5)                                                     
AXT22A   CLI   SCITYPE-SCIEL(R5),SCITGRSS  GROSS ELEMENT?                       
         JE    AXT22B                                                           
         CLI   SCITYPE-SCIEL(R5),SCITTAXP  GST TAX?                             
         BE    AXT22C                                                           
         CLI   SCITYPE-SCIEL(R5),SCITTQST    GST OR HST TAX?                    
         JE    AXT22C                                                           
AXT22A1  BRAS  RE,NEXTEL                                                        
         JE    AXT22A                                                           
         J     AXT22D                                                           
*                                                                               
AXT22B   ZAP   DUB,SCIAMNT-SCIELD(6,R5)                                         
         BRAS  RE,TRNCVDP                                                       
         MVC   AXG$BGRS,P16        BILLING GROSS                                
         J     AXT22A1                                                          
*                                                                               
AXT22C   AP    DUB2,SCIAMNT-SCIELD(6,R5)                                        
         J     AXT22A1                                                          
*                                                                               
AXT22D   DS    0H                                                               
         AP    DUB3,DUB2                                                        
         CLI   AXG$RTYP,C'P'                                                    
         JE    *+10                                                             
         MP    DUB2,=P'-1'                                                      
         ZAP   DUB,DUB2                                                         
         BRAS  RE,TRNCVDP                                                       
         MVC   AXG$TAXA,P16                                                     
         MP    DUB,=P'-1'                                                       
         BRAS  RE,TRNCVDP                                                       
         MVC   AXG$TAXH,P16                                                     
*                                                                               
         ZAP   DUB,DUB3                                                         
         BRAS  RE,TRNCVDP                                                       
         MVC   AXG$GRS,P16                                                      
*                                                                               
AXT30    CLI   DRORCR,C'D'             TEST DEBIT                               
         JNE   AXT31                   YES - GET FROM MPYEL                     
*        L     R5,ATRNEL                                                        
*        CLI   TRNTYPE-TRNEL(R5),129   ONLY FOR CHECK DEBITS                    
*        JNE   AXT31                                                            
         LT    R5,AMPYEL                                                        
         JZ    AXT31                                                            
         MVC   AXG$CHEK(6),MPYNO-MPYEL(R5)   CHECK NUMBER                       
         MVC   WORK(2),MPYDTE-MPYEL(R5)      DATE=CHECK DATE                    
         GOTO1 VDATCON,DMCB,(2,WORK),(20,AXG$CKDT) TRANS DATE                   
         GOTO1 ENGDATE,DMCB,AXG$CKDT                                            
         MVC   AXG$BKCD,MPYBNK-MPYEL(R5)     BANK ACCT                          
*                                                                               
AXT31    LT    R5,ARALEL                                                        
         JZ    AXT36                                                            
         MVC   ELCODE,0(R5)                                                     
         CLI   DRORCR,C'C'         TEST CREDIT                                  
         BE    AXT40               YES - GET FROM NARRATIVE                     
*                                                                               
AXT32    CLI   RALTYPE-RALEL(R5),RALTALC   CHECK NUMBER                         
         JE    AXT34                                                            
         BRAS  RE,NEXTEL                                                        
         JE    AXT32                                                            
         J     AXT36                                                            
*                                                                               
AXT34    MVC   AXG$CHEK,RALAREF-RALEL(R5)                                       
         LA    R4,RALADAT-RALEL(R5) CHECK DATE                                  
         BAS   RE,GODATCON                                                      
         MVC   AXG$CKDT,WORK                                                    
         GOTO1 ENGDATE,DMCB,AXG$CKDT                                            
         J     AXT40                                                            
*                                                                               
AXT36    TM    LDG,SCLDGQ          TEST SC                                      
         JZ    AXT40                                                            
         L     R5,ATRNEL                                                        
         MVC   AXG$CHEK(6),TRNREF-TRNEL(R5)                                     
         MVC   AXG$CKDT,AXG$TRDT   TRANS DATE=CHECK DATE                        
*                                                                               
AXT40    TM    LDG,RCVLDGQ+PAYLDGQ+SJLDGQ  DUE DATE                             
         JZ    AXT50                                                            
         LT    R5,ADUEEL                                                        
         JZ    AXT50                                                            
         LA    R4,DUEDATE-DUEEL(R5)                                             
* THIS DATE IS COMPRESSED - SO BE CAREFUL!                                      
         GOTO1 VDATCON,DMCB,(2,(R4)),(20,AXG$DUDT)                              
         GOTO1 ENGDATE,DMCB,AXG$DUDT                                            
         J     AXT50                                                            
*                                                                               
AXT42    TM    LDG,SJLDGQ                  NOT HERE FOR SJ                      
         JO    AXT50                                                            
         LA    R4,TRNKDATE                                                      
*                                                                               
AXT44    BAS   RE,GODATCON                                                      
         MVC   AXG$DUDT,WORK                                                    
         GOTO1 ENGDATE,DMCB,AXG$DUDT                                            
*                                                                               
AXT50    DS    0H                                                               
*&&DO                                                                           
AXT50    TM    LDG,RCVLDGQ+SJLDGQ    BILL DATE                                  
         JZ    AXT52                                                            
*                                                                               
         L     R5,ATRNEL                                                        
         LA    R4,TRNDATE-TRNEL(R5)                                             
         BAS   RE,GODATCON                                                      
         MVC   AXG$BLDT,WORK                                                    
         GOTO1 ENGDATE,DMCB,AXG$BLDT                                            
*&&                                                                             
*                                                                               
AXT52    LT    R5,AFFTEL                                                        
         JZ    AXT54                                                            
         MVC   ELCODE,0(R5)                                                     
*                                                                               
AXT52A   DS    0H                                                               
*XT52A   CLI   FFTTYPE-FFTEL(R5),FFTTINVN                                       
*        JE    AXT52B                                                           
         CLI   FFTTYPE-FFTEL(R5),FFTTACUR                                       
         JE    AXT52C                                                           
         CLI   FFTTYPE-FFTEL(R5),FFTTPYAC                                       
         JE    AXT52D                                                           
AXT52A1  BRAS  RE,NEXTEL                                                        
         JE    AXT52A                                                           
         J     AXT60                                                            
*&&DO                                                                           
AXT52B   LLC   RE,FFTDLEN-FFTEL(R5)  GET TEXT LENGTH                            
         LA    R0,L'AXG$BILL                                                    
         CR    RE,R0                                                            
         JNH   *+6                                                              
         LR    RE,R0                                                            
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   AXG$BILL(0),FFTDATA-FFTEL(R5)                                    
         J     AXT52A1                                                          
*&&                                                                             
AXT52C   MVC   AXG$CURR,MYSPACES     CLEAR FIELD AS WE HAVE OVERRIDE            
         LLC   RE,FFTDLEN-FFTEL(R5)  GET TEXT LENGTH                            
         LA    R0,AXG$CURR                                                      
         CR    RE,R0                                                            
         JNH   *+6                                                              
         LR    RE,R0                                                            
         BCTR  RE,0                                                             
         MVC   AXG$CURR(0),FFTDATA-FFTEL(R5)                                    
         EX    RE,*-6                                                           
         J     AXT52A1                                                          
*                                                                               
AXT52D   MVC   AXG$NETW,MYSPACES     CLEAR FIELD AS WE HAVE OVERRIDE            
         LLC   RE,FFTDLEN-FFTEL(R5)  GET TEXT LENGTH                            
         LA    R0,AXG$NETW                                                      
         CR    RE,R0                                                            
         JNH   *+6                                                              
         LR    RE,R0                                                            
         BCTR  RE,0                                                             
         MVC   AXG$NETW(0),FFTDATA-FFTEL(R5)                                    
         EX    RE,*-6                                                           
         J     AXT52A1                                                          
*                                                                               
AXT54    DS    0H                                                               
*&&DO                                                                           
AXT54    TM    LDG,PAYLDGQ+INCLDGQ+RCVLDGQ+SCLDGQ+SJLDGQ                        
         JNZ   *+14                                                             
         CLC   =C'SZ',TRNKUNT                                                   
         JNE   AXT60                                                            
         MVC   AXG$BILL(6),TRNKREF                                              
         CLI   AXG$BILL,C' '                                                    
         JH    AXT60                                                            
         L     R5,ATRNEL                                                        
         MVC   AXG$BILL(6),TRNREF-TRNEL(R5)                                     
*&&                                                                             
AXT60    TM    LDG,RCVLDGQ         DEPOSIT DATE                                 
         JZ    AXT70                                                            
         CLI   DRORCR,C'C'         TEST CREDIT                                  
         JE    AXT70                                                            
*                                                                               
         L     R5,ATRNEL                                                        
         CLI   TRNTYPE-TRNEL(R5),9   TYPES 9 ONLY                               
         JNE   *+8                                                              
         OI    CLXFLAG,CLXFTY9       DID WE FIND A TYPE 9?                      
*                                                                               
         LT    R5,ARALEL                                                        
         MVC   ELCODE,0(R5)                                                     
*                                                                               
AXT62    CLI   RALTYPE-RALEL(R5),RALTALC                                        
         JE    AXT64                                                            
         BRAS  RE,NEXTEL                                                        
         JE    AXT62                                                            
         J     AXT70                                                            
*                                                                               
AXT64    LA    R4,RALADEP-RALEL(R5)                                             
         BAS   RE,GODATCON                                                      
         MVC   AXG$DPDT,WORK                                                    
         GOTO1 ENGDATE,DMCB,AXG$DPDT                                            
*                                                                               
AXT70    TM    LDG,PAYLDGQ+RCVLDGQ    OFFSET DATE                               
         JZ    AXT80                                                            
*                                                                               
         CLI   DRORCR,C'C'         TEST CREDIT                                  
         JE    AXT80                                                            
*                                                                               
         LT    R5,ARALEL                                                        
         JZ    AXT80                                                            
         MVC   ELCODE,0(R5)                                                     
*                                                                               
AXT72    CLI   RALTYPE-RALEL(R5),RALTOFS                                        
         JE    AXT74                                                            
         BRAS  RE,NEXTEL                                                        
         JE    AXT72                                                            
         J     AXT80                                                            
*                                                                               
AXT74    LA    R4,RALODAT-RALEL(R5)                                             
         BAS   RE,GODATCON                                                      
         MVC   AXG$OFDT,WORK                                                    
         GOTO1 ENGDATE,DMCB,AXG$OFDT                                            
*                                                                               
AXT80    TM    LDG,PAYLDGQ          CLEARED DATE (PAY ONLY)                     
         JZ    AXT90                                                            
         LT    R5,AGDAEL                                                        
         JZ    AXT90                                                            
         MVC   ELCODE,0(R5)                                                     
*                                                                               
AXT82    CLI   GDATYPE-GDAEL(R5),GDATRECN                                       
         JE    AXT84                                                            
         BRAS  RE,NEXTEL                                                        
         JE    AXT82                                                            
         J     AXT90                                                            
*                                                                               
AXT84    LA    R4,GDADATE2-GDAEL(R5)                                            
         BAS   RE,GODATCON                                                      
         MVC   AXG$CLDT,WORK                                                    
         GOTO1 ENGDATE,DMCB,AXG$CLDT                                            
*                                                                               
AXT90    TM    LDG,RCVLDGQ+SCLDGQ    WRITE-OFF DATE                             
         JZ    AXT100                                                           
         LT    R5,ARALEL                                                        
         JZ    AXT100                                                           
         CLI   DRORCR,C'C'         TEST CREDIT                                  
         JE    AXT100                                                           
         MVC   ELCODE,0(R5)                                                     
*                                                                               
AXT92    CLI   RALTYPE-RALEL(R5),RALTWOF                                        
         JE    AXT94                                                            
         BRAS  RE,NEXTEL                                                        
         JE    AXT92                                                            
         J     AXT100                                                           
*                                                                               
AXT94    LA    R4,RALWDAT-RALEL(R5)                                             
         BAS   RE,GODATCON                                                      
         MVC   AXG$WODT,WORK                                                    
         GOTO1 ENGDATE,DMCB,AXG$WODT                                            
*                                                                               
AXT100   TM    LDG,RCVLDGQ           TRANSFER DATE                              
         JZ    AXT110                                                           
         LT    R5,ARALEL                                                        
         JZ    AXT110                                                           
         CLI   DRORCR,C'C'         TEST CREDIT                                  
         JE    AXT110                                                           
         MVC   ELCODE,0(R5)                                                     
*                                                                               
AXT102   CLI   RALTYPE-RALEL(R5),RALTTTO                                        
         JE    AXT104                                                           
         BRAS  RE,NEXTEL                                                        
         JE    AXT102                                                           
         J     AXT110                                                           
*                                                                               
AXT104   LA    R4,RALTDAT-RALEL(R5)                                             
         BAS   RE,GODATCON                                                      
         MVC   AXG$XFDT,WORK                                                    
         GOTO1 ENGDATE,DMCB,AXG$XFDT                                            
*                                                                               
AXT110   TM    LDG,RCVLDGQ                                                      
         JZ    AXT130                                                           
         LT    R5,AUFSEL             USER FIELDS                                
         JZ    AXT130                                                           
         MVC   ELCODE,0(R5)                                                     
         J     AXT115                                                           
*                                                                               
AXT111   BRAS  RE,NEXTEL                                                        
         JNE   AXT130                                                           
*                                                                               
         USING UFSELD,R5                                                        
AXT115   LA    RE,UFSTAB                                                        
         SR    R0,R0                                                            
AXT118   CLI   0(RE),0                                                          
         JE    AXT111                                                           
         CLC   UFSCODE,0(RE)                                                    
         JE    *+12                                                             
         LA    RE,L'UFSTAB(RE)                                                  
         J     AXT118                                                           
*                                                                               
         LR    RF,R2               SET UP RF FOR ADDING DISPLACEMENT            
         ICM   R0,3,2(RE)                                                       
         AR    RF,R0                                                            
         SR    R1,R1                                                            
         IC    R1,UFSLN                                                         
         SHI   R1,UFSLN1Q+1                                                     
         MVC   0(0,RF),UFSDATA                                                  
         EX    R1,*-6                                                           
         J     AXT111                                                           
         DROP  R5                                                               
*                                                                               
AXT130   TM    LDG,PAYLDGQ+RCVLDGQ   BANK CODE                                  
         JZ    AXT140                                                           
         LT    R5,ASPAEL                                                        
         JZ    AXT140                                                           
         MVC   ELCODE,0(R5)                                                     
*                                                                               
AXT132   CLI   SPATYPE-SPAEL(R5),SPATBANK                                       
         JE    AXT134                                                           
         BRAS  RE,NEXTEL                                                        
         JE    AXT132                                                           
         J     AXT140                                                           
*                                                                               
AXT134   MVC   AXG$BKCD,SPAAULA-SPAEL(R5)                                       
*                                                                               
AXT140   DS    0H                                                               
*&&DO                                                                           
AXT140   TM    LDG,EXPLDGQ+SCLDGQ                                               
         JZ    AXT150                                                           
         LT    R5,AAPEEL                                                        
         JZ    AXT150                                                           
         CLC   =C'SV',APENACT-APEEL(R5)                                         
         JE    AXT142                                                           
         CLC   =C'SW',APENACT-APEEL(R5)                                         
         JE    AXT142                                                           
         CLC   =C'SX',APENACT-APEEL(R5)                                         
         JE    AXT142                                                           
         CLC   =C'SY',APENACT-APEEL(R5)                                         
         JNE   AXT150                                                           
*                                                                               
AXT142   MVC   AXG$VNCD,APENACT-APEEL(R5)                                       
*&&                                                                             
AXT150   TM    LDG,RCVLDGQ+SJLDGQ                                               
         JZ    AXT160                                                           
         LT    R5,ARALEL                                                        
         JZ    AXT160                                                           
         CLI   DRORCR,C'C'         TEST CREDIT                                  
         JE    AXT160                                                           
         MVC   ELCODE,0(R5)                                                     
*                                                                               
AXT152   CLI   RALTYPE-RALEL(R5),RALTWOF                                        
         JE    AXT154                                                           
         BRAS  RE,NEXTEL                                                        
         JE    AXT152                                                           
         J     AXT160                                                           
*                                                                               
AXT154   MVC   AXG$WOAC,RALWULA-RALEL(R5)                                       
         MVC   AXG$WONU,RALWREF-RALEL(R5)                                       
         MVC   AXG$WODT,RALWDAT-RALEL(R5)                                       
*                                                                               
AXT160   CLI   LDGVWXY,C'Y'        INVOICE NUMBER                               
         JE    AXT162                                                           
         CLC   =C'SZ',TRNKUNT                                                   
         JE    AXT162                                                           
         TM    LDG,INCLDGQ+RCVLDGQ+SCLDGQ                                       
         JNZ   AXT162                                                           
         TM    LDG,SJLDGQ                                                       
         JZ    AXT170                                                           
         L     R5,ATRNEL                                                        
         CLI   TRNTYPE-TRNEL(R5),6   TYPES 6+7 ONLY                             
         JE    AXT162                                                           
         CLI   TRNTYPE-TRNEL(R5),7                                              
         JNE   AXT170                                                           
*                                                                               
AXT162   LT    R5,AFFTEL                                                        
         JZ    AXT170                                                           
         MVC   ELCODE,0(R5)                                                     
*                                                                               
AXT164   CLI   FFTTYPE-FFTEL(R5),FFTTINVN                                       
         JE    AXT166                                                           
         BRAS  RE,NEXTEL                                                        
         JE    AXT164                                                           
         J     AXT170                                                           
*                                                                               
AXT166   LLC   RE,FFTDLEN-FFTEL(R5)  GET DATA LENGTH                            
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         J     *+10                                                             
         MVC   AXG$INV(0),FFTDATA-FFTEL(R5)                                     
         MVC   AXG$VINV,AXG$INV                                                 
         J     AXT190                                                           
*                                                                               
AXT170   CLI   LDGVWXY,C'Y'                                                     
         JE    AXT172                                                           
         CLC   =C'SZ',TRNKUNT                                                   
         JE    AXT172                                                           
         TM    LDG,INCLDGQ+RCVLDGQ+SCLDGQ                                       
         JZ    AXT174                                                           
*                                                                               
AXT172   L     R5,ATRNEL                                                        
         MVC   AXG$INV(L'TRNREF),TRNREF-TRNEL(R5)                               
         MVC   AXG$VINV,AXG$INV                                                 
         J     AXT190                                                           
*                                                                               
AXT174   CLI   LDGPQSTU,C'Y'                                                    
         JNE   AXT180                                                           
         LT    R5,AXPYEL                                                        
         JZ    AXT180                                                           
         MVC   AXG$INV(L'XPYINV),XPYINV-XPYEL(R5)                               
         MVC   AXG$VINV,AXG$INV                                                 
         MVC   AXG$MKTC,XPYMKT-XPYEL(R5)                                        
         J     AXT190                                                           
*                                                                               
AXT180   TM    LDG,SJLDGQ                                                       
         JZ    AXT190                                                           
         L     R5,ATRNEL                                                        
         CLI   TRNTYPE-TRNEL(R5),6   NOT FOR TYPES 6/7                          
         JE    AXT190                                                           
         CLI   TRNTYPE-TRNEL(R5),7                                              
         JNE   AXT190                                                           
*                                                                               
         LT    R5,APTAEL                                                        
         JNE   AXT190                                                           
         TM    PTASTAT1-PTAEL(R5),PTASPEND  IGNORE IF THIS BIT ON               
         JO    AXT190                                                           
*                                                                               
         LA    R1,PTARBLNO-PTAEL(R5)                                            
         CLC   1(5,R1),MYSPACES    ANY INV NUMBER?                              
         JNH   AXT190              NO DATA                                      
         MVC   AXG$INV(1),0(R1)    MOVE IN MEDIA PORTION                        
         MVI   AXG$INV+1,C'-'                                                   
         MVC   AXG$INV+2(2),1(R1) MOVE IN YM PORTION                            
         MVI   AXG$INV+4,C'-'                                                   
         MVC   AXG$INV+5(4),3(R1) MOVE IN REF PORTION                           
         MVC   AXG$VINV,AXG$INV                                                 
*                                                                               
AXT190   TM    LDG,RCVLDGQ+INCLDGQ                                              
         JZ    AXT200                                                           
         LT    R5,AFFTEL                                                        
         JZ    AXT200                                                           
         MVC   ELCODE,0(R5)                                                     
*                                                                               
AXT192   CLI   FFTTYPE-FFTEL(R5),FFTTAOR                                        
         JE    AXT194                                                           
         CLI   FFTTYPE-FFTEL(R5),FFTTMRKT                                       
         JE    AXT196                                                           
AXT193   BRAS  RE,NEXTEL                                                        
         JE    AXT192                                                           
         J     AXT200                                                           
*                                                                               
AXT194   MVC   AXG$AOAC,FFTPRAOR-FFTEL(R5)   AOR ACCOUNT                        
         MVC   AXG$AOBL(L'FFTRTAOR),FFTRTAOR-FFTEL(R5) AOR BILLNUM              
         ZAP   DUB,FFTAMAOR-FFTEL(6,R5)      AOR AMOUNT                         
         TM    FFTSTAT2-FFTEL(R5),FFTCR                                         
         JZ    *+10                                                             
         MP    DUB,=P'-1'                                                       
         BRAS  RE,TRNCVDP                                                       
         MVC   AXG$AOR$,P16                                                     
         J     AXT193                                                           
*                                                                               
AXT196   MVC   AXG$MKTC,FFTMRKTC-FFTEL(R5)   MARKET CODE AND NAME               
         ZIC   RF,FFTDLEN-FFTEL(R5)          LENGTH OF DATA                     
         AHI   RF,-5                         MINUS MARKET CODE +1               
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   AXG$MKTN(0),FFTMRKTN-FFTEL(R5)                                   
         J     AXT193                                                           
*                                                                               
AXT200   CLC   =C'SI',TRNKULA                                                   
         JE    *+14                                                             
         CLC   =C'SK',TRNKULA                                                   
         JNE   AXT210                                                           
         LT    R5,ARFLEL                                                        
         JZ    AXT210                                                           
         MVC   ELCODE,0(R5)                                                     
*                                                                               
AXT202   CLI   RFLTYPE-RFLEL(R5),RFLWC                                          
         JE    AXT204                                                           
         BRAS  RE,NEXTEL                                                        
         JE    AXT202                                                           
         J     AXT210                                                           
*                                                                               
AXT204   MVC   AXG$WKCD,RFLDATA-RFLEL(R5)                                       
         J     AXT220                                                           
*                                                                               
AXT210   TM    LDG,SJLDGQ          IS IT SJ                                     
         JZ    AXT220                                                           
         L     R5,ATRNEL                                                        
         CLC   =C'**',TRNANAL-TRNEL(R5)                                         
         JE    AXT220                                                           
         MVC   AXG$WKCD,TRNANAL-TRNEL(R5)                                       
*                                                                               
AXT220   TM    LDG,SJLDGQ          TEST SJ                                      
         JNE   AXT230                                                           
         LT    R5,ASPDEL                                                        
         JZ    AXT230                                                           
         CLC   =C'SI',SPDACCS-SPDEL(R5)                                         
         JE    *+14                                                             
         CLC   =C'SK',SPDACCS-SPDEL(R5)                                         
         JNE   AXT230                                                           
         MVC   AXG$INAC,SPDACCS-SPDEL(R5)                                       
*                                                                               
AXT230   BRAS  RE,GETNARR          GET NARRATIVE DATA                           
*                                                                               
         USING PIDELD,R5                                                        
AXT250   LT    R5,APIDEL           GET PID FOR PAYABLES ONLY                    
         JZ    AXT280                                                           
         TM    LDG,PAYLDGQ                                                      
         JZ    AXT280                                                           
         MVC   AXG$PID(2),PIDNO                                                 
         MVC   AXG$AGY(2),PIDAGY                                                
*                                                                               
AXT280   DS    0H                                                               
         J     EXIT                                                             
         EJECT                                                                  
UFSTAB   DS    0CL4                                                             
         DC    C'P1',AL2(AXG$PRD1-AXG$TRN)                                      
         DC    C'P2',AL2(AXG$PRD2-AXG$TRN)                                      
         DC    C'E1',AL2(AXG$EST1-AXG$TRN)                                      
         DC    C'E2',AL2(AXG$EST2-AXG$TRN)                                      
         DC    X'00'                                                            
         EJECT                                                                  
GODATCON NTR1                                                                   
         MVC   WORK(6),MYSPACES                                                 
         OC    0(2,R4),0(R4)       IF Y/M ARE ZERO, EXIT                        
         JZ    EXIT                                                             
         GOTO1 VDATCON,DMCB,(1,(R4)),(20,WORK)                                  
         J     EXIT                                                             
         EJECT                                                                  
*===========================================================                    
* GET ADDRESSES OF ELEMENTS THAT MAY BE USED                                    
*===========================================================                    
                                                                                
GETELEMS NTR1                                                                   
         LA    R4,ELLIST                                                        
*                                                                               
GETELEM2 MVC   ELCODE,0(R4)                                                     
         LA    R5,TRNRFST          R5=A(FIRST ELEMENT)                          
         BRAS  RE,FRSTEL                                                        
         JNE   GETELEM4                                                         
         XR    RE,RE                                                            
         ICM   RE,7,1(R4)                                                       
         ST    R5,0(RE)            SAVE ELEM ADDR                               
*                                                                               
GETELEM4 LA    R4,4(R4)                                                         
         CLI   0(R4),X'FF'                                                      
         JNE   GETELEM2                                                         
         J     EXIT                                                             
         DS    0A                                                               
ELLIST   DS    0XL24                                                            
         DC    X'1A',AL3(AMDTEL)                                                
         DC    X'23',AL3(AOTHEL)                                                
         DC    X'2C',AL3(ASPAEL)                                                
         DC    X'30',AL3(ARSTEL)                                                
         DC    X'44',AL3(ATRNEL)                                                
         DC    X'46',AL3(AXPYEL)                                                
         DC    X'4C',AL3(ASPDEL)                                                
         DC    X'4F',AL3(ACPJEL)                                                
         DC    X'50',AL3(ASCIEL)                                                
         DC    X'60',AL3(ATRSEL)                                                
         DC    X'61',AL3(ADUEEL)                                                
         DC    X'63',AL3(AGLDEL)                                                
         DC    X'64',AL3(AMPYEL)                                                
         DC    X'65',AL3(AANOEL)                                                
         DC    X'6A',AL3(AMDPEL)                                                
         DC    X'77',AL3(APTAEL)                                                
         DC    X'97',AL3(AEXOEL)                                                
         DC    X'A2',AL3(AUFSEL)                                                
         DC    X'BA',AL3(ALNKEL)                                                
         DC    X'C0',AL3(AAPEEL)                                                
         DC    X'C5',AL3(ARFLEL)                                                
         DC    X'D4',AL3(APAKEL)                                                
         DC    X'D8',AL3(APIDEL)                                                
         DC    X'D9',AL3(ARALEL)                                                
         DC    X'DB',AL3(AFFTEL)                                                
         DC    X'E5',AL3(AGDAEL)                                                
         DC    X'F3',AL3(AMBIEL)                                                
         DC    X'FF',AL3(0)                                                     
*                                                                               
ELADDRS  DS    0A                                                               
AANOEL   DS    A                                                                
AAPEEL   DS    A                                                                
ACPJEL   DS    A                                                                
ADUEEL   DS    A                                                                
AEXOEL   DS    A                                                                
AFFTEL   DS    A                                                                
APIDEL   DS    A                                                                
AGDAEL   DS    A                                                                
AGLDEL   DS    A                                                                
ALNKEL   DS    A                                                                
AMPYEL   DS    A                                                                
AMBIEL   DS    A                                                                
AMDPEL   DS    A                                                                
AMDTEL   DS    A                                                                
AOTHEL   DS    A                                                                
APAKEL   DS    A                                                                
APTAEL   DS    A                                                                
ARALEL   DS    A                                                                
ARFLEL   DS    A                                                                
ARSTEL   DS    A                                                                
ASPAEL   DS    A                                                                
ASPDEL   DS    A                                                                
ATRNEL   DS    A                                                                
ATRSEL   DS    A                                                                
AXPYEL   DS    A                                                                
ASCIEL   DS    A                                                                
AUFSEL   DS    A                                                                
ELADDRX  EQU   *                                                                
         EJECT                                                                  
*==========================================================                     
* FIND THE OFFICE CODE                                                          
*==========================================================                     
                                                                                
         USING TRNRECD,R3                                                       
GETOFFC  NTR1                                                                   
         CLC   =C'SJ',TRNKUNT                                                   
         JE    GETOFF2                                                          
         L     R5,ATRNEL                                                        
         MVC   AXG$OFFC,TRNOFFC-TRNEL(R5)                                       
         CLI   AXG$OFFC,C' '                                                    
         JH    EXIT                                                             
*                                                                               
GETOFF2  LT    R5,AGLDEL                                                        
         JZ    GETOFF4                                                          
         MVC   AXG$OFFC,GLDOFFC-GLDEL(R5)                                       
         CLC   AXG$OFFC,MYSPACES   DID WE GET AN OFFICE FROM THE GB EL?         
         JNH   GETOFF4                                                          
         J     EXIT                                                             
*                                                                               
GETOFF4  LT    R5,APAKEL                                                        
         JZ    GETOFF6                                                          
         MVC   AXG$OFFC,PAKOFF-PAKEL(R5)                                        
         CLC   AXG$OFFC,MYSPACES   DID WE GET AN OFFICE FROM THE PAY?           
         JNH   GETOFF6                                                          
         J     EXIT                                                             
*                                                                               
GETOFF6  LT    R5,AANOEL                                                        
         JZ    EXIT                                                             
         MVC   AXG$OFFC,ANOOFFC-ANOELD(R5)                                      
         J     EXIT                                                             
         EJECT                                                                  
*==========================================================                     
* EXTRACT THE MEDIA CODE FROM SOMEWHERE IN THE RECORD                           
*==========================================================                     
                                                                                
         USING TRNRECD,R3                                                       
GETMED   NTR1                                                                   
         CLC   =C'SJ',TRNKUNT                                                   
         JNE   GETM2                                                            
         MVC   AXG$MED,TRNKACT+6   FIRST CHAR OF JOB FOR SJ                     
         J     GETM20                                                           
                                                                                
GETM2    LT    R5,AMDTEL                                                        
         JZ    GETM4                                                            
         MVC   AXG$MED,MDTMED-MDTEL(R5)                                         
         CLI   AXG$MED,C' '                                                     
         JNE   GETM20                                                           
*                                                                               
         MVC   AXG$MED,MDTMED2-MDTEL(R5)                                        
         TM    MDTSTAT-MDTEL(R5),MDTSMIQ                                        
         JZ    GETM4                                                            
         CLI   AXG$MED,C' '                                                     
         JNE   GETM20                                                           
*                                                                               
GETM4    LT    R5,ACPJEL                                                        
         JZ    GETM6                                                            
         MVC   AXG$MED,CPJTYPE-CPJELD(R5)                                       
         CLI   AXG$MED,C' '                                                     
         JH    GETM20                                                           
*                                                                               
GETM6    LT    R5,AOTHEL                                                        
         JZ    GETM8                                                            
         MVC   AXG$MED,OTHPROF-OTHELD(R5)                                       
         CLC   =C'SJ',TRNKULC      TEST CONTRA IS SJ                            
         JNE   GETM6X                                                           
         MVC   AXG$MED,OTHNUM+6-OTHELD(R5)                                      
*                                                                               
GETM6X   CLI   AXG$MED,C' '                                                     
         JH    GETM20                                                           
*                                                                               
GETM8    LT    R5,AMBIEL                                                        
         JZ    GETM10                                                           
         MVC   AXG$MED,MBIMED-MBIELD(R5)                                        
         J     GETM20                                                           
*                                                                               
GETM10   TM    LDG,PAYLDGQ                                                      
         JZ    GETM20                                                           
         LA    R1,TRNKACT                                                       
         CLI   ITMREP,C'Y'         IS ITEM A REP?                               
         JNE   GETM15                                                           
         LA    R1,TRNKCACT                                                      
         CLI   AXG$SYS,C'P'        IS THIS PRINT?                               
         JNE   GETM15                                                           
         LA    R1,TRNKCCPY                                                      
GETM15   MVC   AXG$MED,0(R1)                                                    
*                                                                               
GETM20   TM    LDG,SCLDGQ                                                       
         JZ    GETM25                                                           
*                                                                               
         L     R5,ATRNEL                                                        
         LA    RE,TRNKCACT         TYPE 13 FORMAT IS M/CLT/PRD IN CACT          
         CLI   TRNTYPE-TRNEL(R5),13                                             
         JE    GETM22                                                           
         LA    RE,TRNKCULC+1       MEDIA PAYABLES - M/CLT/PRD CULA+1            
         CLI   TRNTYPE-TRNEL(R5),33                                             
         JE    GETM22                                                           
         CLI   TRNTYPE-TRNEL(R5),34                                             
         JE    GETM22                                                           
         CLI   TRNTYPE-TRNEL(R5),35                                             
         JE    GETM22                                                           
         CLI   TRNTYPE-TRNEL(R5),49                                             
         JE    GETM22                                                           
         CLI   TRNTYPE-TRNEL(R5),50                                             
         JNE   GETM25                                                           
GETM22   MVC   AXG$MED,0(RE)                                                    
*                                                                               
GETM25   CLI   AXG$SYS,C'S'        ARE WE DOING SPOT?                           
         JE    *+12                                                             
         CLI   AXG$SYS,C'N'        OR NET?                                      
         JNE   GETMEDX                                                          
         CLI   AXG$MED,C'N'        DO WE HAVE MEDIA N?                          
         JNE   GETMEDX                                                          
         LT    R5,AXPYEL                                                        
         JZ    GETMEDX                                                          
         MVC   AXG$MED,XPYSMED-XPYELD(R5)                                       
*                                                                               
GETMEDX  J     EXIT                                                             
*                                                                               
         EJECT                                                                  
*==============================================================                 
* EXTRACT CLIENT CODE                                                           
*==============================================================                 
                                                                                
         USING TRNRECD,R3                                                       
GETCLT   NTR1                                                                   
         MVI   MEDPYBL,C'N'                                                     
         MVI   ITMREP,C'N'         ASSUME IT'S NOT A REP                        
*                                                                               
*==============>                                                                
*==============>                                                                
*==============>                                                                
         CP    SEQNUM,IDFSEQ                                                    
         JNE   GETC0                                                            
         J     GETC0                                                            
IDFSEQ   DC    PL4'991'                                                         
*==============>                                                                
*==============>                                                                
*==============>                                                                
*                                                                               
GETC0    CLC   =C'SJ',TRNKCUNT                                                  
         JNE   *+10                                                             
         MVC   AXG$CLT,TRNKCACT                                                 
*                                                                               
         CLI   AXG$CLT,C' '                                                     
         JH    GETC80                                                           
*                                                                               
GETC2    LT    R5,AMDTEL                                                        
         JNZ   GETC4                                                            
         LT    R5,AMDPEL                                                        
         JZ    GETC6                                                            
*                                                                               
GETC4    MVC   AXG$CLT,MDTCLI-MDTEL(R5)                                         
         CLI   AXG$CLT,C' '                                                     
         JH    GETC80                                                           
*                                                                               
GETC6    LT    R5,ACPJEL                                                        
         JZ    GETC8                                                            
         CLI   CPJTYPE-CPJELD(R5),C'J'        TEST PRODUCTION                   
         JNE   GETC8                                                            
         MVC   AXG$CLT,CPJCLI-CPJELD(R5)                                        
         J     GETC80                                                           
*                                                                               
GETC8    CLC   =C'SR',TRNKCACT     TEST CONTRA=SR                               
         JNE   GETC10                                                           
         CLI   SRLDGCLT,0          TEST CLIENT CODE IN SR                       
         JE    GETC10                                                           
         LLC   RE,SRLDGCLT                                                      
         LA    RE,TRNKCACT(RE)     POINT TO CLIENT IN CONTRA                    
         MVC   AXG$CLT,0(RE)                                                    
         J     GETC80                                                           
*                                                                               
GETC10   CLI   LDGCLT,0            TEST ACCOUNT HAS CLT CODE                    
         JE    GETC12              NO                                           
         LLC   RE,LDGCLT                                                        
         LA    RE,TRNKACT-1(RE)    POINT TO CLIENT IN ACCT                      
         MVC   AXG$CLT,0(RE)                                                    
         J     GETC80                                                           
*                                                                               
GETC12   TM    LDG,SCLDGQ                                                       
         JZ    GETC16                                                           
*                                                                               
         L     R5,ATRNEL                                                        
         LA    RE,TRNKCACT+2       TYPE 13 FORMAT IS M/CLT/PRD IN CACT          
         CLI   TRNTYPE-TRNEL(R5),13                                             
         JE    GETC14                                                           
         LA    RE,TRNKCULC+3       MEDIA PAYABLES - M/CLT/PRD CULA+1            
         CLI   TRNTYPE-TRNEL(R5),33                                             
         JE    GETC14                                                           
         CLI   TRNTYPE-TRNEL(R5),34                                             
         JE    GETC14                                                           
         CLI   TRNTYPE-TRNEL(R5),35                                             
         JE    GETC14                                                           
         CLI   TRNTYPE-TRNEL(R5),49                                             
         JE    GETC14                                                           
         CLI   TRNTYPE-TRNEL(R5),50                                             
         JNE   GETC15                                                           
GETC14   MVC   AXG$CLT,0(RE)                                                    
         J     GETC16                                                           
*                                                                               
GETC15   LT    R5,ASPDEL                                                        
         JZ    GETC16                                                           
         CLC   =C'SJ',SPDACCS-SPDEL(R5)                                         
         JNE   GETC16                                                           
         MVC   AXG$CLT,SPDACCS+2-SPDEL(R5)                                      
         J     GETC80                                                           
*                                                                               
GETC16   CLC   =C'SB',TRNKUNT                                                   
         JE    *+14                                                             
         CLC   =C'SE',TRNKUNT                                                   
         JNE   GETC18                                                           
*                                                                               
         LT    R5,AEXOEL                                                        
         JZ    GETC18                                                           
         MVC   AXG$CLT,EXOCLI-EXOEL(R5)                                         
         J     GETC80                                                           
*                                                                               
GETC18   LT    R5,AFFTEL                                                        
         JZ    GETC20                                                           
         MVC   ELCODE,0(R5)                                                     
*                                                                               
GETC18A  CLI   FFTTYPE-FFTEL(R5),FFTTCLPR                                       
         JE    GETC18X                                                          
         BRAS  RE,NEXTEL                                                        
         JE    GETC18A                                                          
         J     GETC20                                                           
*                                                                               
GETC18X  MVC   AXG$CLT,FFTCLAC-FFTEL(R5)                                        
         J     GETC80                                                           
*                                                                               
GETC20   CLC   =C'SJ',TRNKUNT                                                   
         JNE   *+14                                                             
         MVC   AXG$CLT,TRNKACT                                                  
         J     GETC80                                                           
*                                                                               
         L     R5,ATRNEL           NOW TEST FOR MEDIA PAYABLE                   
         MVI   ITMREP,C'N'         ASSUME IT'S NOT A REP                        
         CLI   TRNTYPE-TRNEL(R5),129                                            
         JE    GETC22              CONSIDER ALL PAY DR ARE MEDIA PAY            
         CLI   TRNTYPE-TRNEL(R5),36                                             
         JE    GETC22                 ALL PAY DR INCLUDE TYPE 36                
         CLI   TRNTYPE-TRNEL(R5),37                                             
         JE    GETC22                 AND TYPE 37                               
         CLI   TRNTYPE-TRNEL(R5),12                                             
         JE    GETC22                                                           
         CLI   TRNTYPE-TRNEL(R5),33                                             
         JE    GETC22                                                           
         MVI   ITMREP,C'Y'         FOR SPOT/NET 34/35 ARE REPS                  
         CLI   TRNTYPE-TRNEL(R5),34                                             
         JE    GETC22                                                           
         CLI   TRNTYPE-TRNEL(R5),35                                             
         JE    GETC22                                                           
         MVI   ITMREP,C'N'         FOR PRINT 49 IS NOT A REP                    
         CLI   TRNTYPE-TRNEL(R5),49                                             
         JE    GETC22                                                           
         MVI   ITMREP,C'Y'         BUT 50 IS                                    
         CLI   TRNTYPE-TRNEL(R5),50                                             
         JE    GETC22                                                           
         MVI   ITMREP,C'N'         FOR TYPE 13, CHECK TO SEE IF REP             
         CLI   TRNTYPE-TRNEL(R5),13                                             
         JNE   GETC30                                                           
         TM    LDG,PAYLDGQ                                                      
         JZ    GETC30                                                           
         CLI   TRNKCACT,X'40'      IS THERE SOMETHING IN CONTRA?                
         JNH   GETC22                                                           
         MVI   ITMREP,C'Y'                                                      
*                                                                               
GETC22   MVI   MEDPYBL,C'Y'                                                     
         MVC   AXG$CLT,TRNKCACT+9                                               
         LA    R1,TRNKACT+1                                                     
         CLI   ITMREP,C'Y'         IS ITEM A REP?                               
         JNE   GETC24                                                           
         LA    R1,TRNKULC                                                       
         CLI   AXG$SYS,C'P'                                                     
         BE    GETC24                                                           
         LA    R1,TRNKCACT+1                                                    
GETC24   MVC   AXG$NETW(8),0(R1)                                                
         J     GETCLTX                                                          
*                                                                               
GETC30   TM    LDG,PAYLDGQ                                                      
         JZ    GETC80                                                           
         LT    R5,ALNKEL                                                        
         JZ    *+10                                                             
         MVC   AXG$CLT,LNKAGJB+2-LNKELD(R5)                                     
*                                                                               
GETC80   DS    0H                                                               
*        GOTO1 =A(GTSJNAME),DMCB,(3,AXG$CLT),AXG$CLNM                           
         OC    AXG$CLNM,MYSPACES                                                
GETCLTX  J     EXIT                                                             
         EJECT                                                                  
*==============================================================                 
* EXTRACT PRODUCT CODE                                                          
*==============================================================                 
                                                                                
         USING TRNRECD,R3                                                       
GETPRD   NTR1                                                                   
         MVC   AXG$PRD(3),AXG$CLT  NO KIDDING - CONCATENATE!                    
         CLI   AXG$PRD,C' '        IF NO CLIENT                                 
         JNH   GETPRDX             THEN CERTAINLY NO PRODUCT!                   
*                                                                               
         CLC   =C'SJ',TRNKCUNT                                                  
         JNE   *+14                                                             
         MVC   AXG$PRD+3(3),TRNKCACT+3                                          
         J     GETP2                                                            
*                                                                               
GET      CLC   =C'SJ',TRNKUNT                                                   
         JNE   *+10                                                             
         MVC   AXG$PRD+3(3),TRNKACT+3                                           
*                                                                               
GETP2    CLI   AXG$PRD+3,C' '                                                   
         JH    GETPRDX                                                          
*                                                                               
         TM    LDG,PAYLDGQ                                                      
         JZ    GETP4                                                            
         CLI   MEDPYBL,C'Y'        IS IT A MEDIA PAYABLE                        
         JNE   GETP4               NO                                           
         MVC   AXG$PRD+3(3),TRNKREF                                             
         OI    AXG$PRD+5,X'40'                                                  
         J     GETPRDX                                                          
*                                                                               
GETP4    LT    R5,AMDTEL                                                        
         JNZ   *+14                                                             
         LT    R5,AMDPEL                                                        
         JZ    GETP6                                                            
         MVC   AXG$PRD+3(3),MDTPRD-MDTELD(R5)                                   
*                                                                               
GETP6    CLI   AXG$PRD+3,C' '                                                   
         JH    GETPRDX                                                          
*                                                                               
         LT    R5,AOTHEL                                                        
         JZ    GETP10                                                           
         TM    LDG,PAYLDGQ         TEST PAYABLE LEDGER                          
         JO    GETP8                                                            
         TM    LDG,SCLDGQ          OR SC                                        
         JO    GETP8                                                            
         CLI   OTHPROF-OTHELD(R5),C'P'                                          
         JE    GETP8                                                            
         CLI   OTHPROF-OTHELD(R5),C'S'                                          
         JE    GETP8                                                            
         CLI   OTHPROF-OTHELD(R5),C'J'                                          
         JNE   GETP10                                                           
         L     RE,ATRNEL                                                        
         CLI   TRNTYPE-TRNEL(RE),9   EXCLUDE TYPE 9                             
         JE    GETP10                                                           
*                                                                               
GETP8    MVC   AXG$PRD+3(3),OTHNUM-OTHELD(R5)                                   
*                                                                               
GETP10   CLI   AXG$PRD+3,C' '                                                   
         JH    GETPRDX                                                          
*                                                                               
         LT    R5,ACPJEL                                                        
         JZ    GETP12                                                           
         CLI   CPJTYPE-CPJEL(R5),C'J'                                           
         JNE   GETP12                                                           
         MVC   AXG$PRD+3(3),CPJPRO-CPJELD(R5)                                   
*                                                                               
GETP12   CLI   AXG$PRD+3,C' '                                                   
         JH    GETPRDX                                                          
*                                                                               
         CLC   =C'SJ',TRNKCULC                                                  
         JNE   GETP14                                                           
         MVC   AXG$PRD+3(3),TRNKCACT+3                                          
*                                                                               
GETP14   CLI   AXG$PRD+3,C' '                                                   
         JH    GETPRDX                                                          
*                                                                               
         TM    LDG,SCLDGQ                                                       
         JZ    GETP20                                                           
         L     R5,ATRNEL                                                        
         LA    R1,GETP8LST                                                      
         LA    R0,GETP8LSX-GETP8LST                                             
*                                                                               
GETP16   CLC   0(1,R1),TRNTYPE-TRNEL(R5)                                        
         JE    GETP18                                                           
         LA    R1,1(R1)                                                         
         JCT   R0,GETP16                                                        
         J     GETP20                                                           
*                                                                               
GETP18   MVC   AXG$PRD+3(3),TRNKCULC+7 FORMAT T/CLT/PRD IN TRNKCULA+1           
         J     GETP20                                                           
*                                                                               
GETP8LST DC    AL1(33,34,35,49,50)                                              
GETP8LSX EQU   *                                                                
*                                                                               
GETP20   TM    LDG,SCLDGQ          IS IT SC                                     
         JZ    GETP28                                                           
*                                                                               
         L     R5,ATRNEL                                                        
         CLI   TRNTYPE-TRNEL(R5),13                                             
         JNE   GETP26                                                           
GETP24   MVC   AXG$PRD,TRNKCACT+6  FORMAT IS T/CLT/PRD IN TRNKCACT              
         J     GETP28                                                           
*                                                                               
GETP26   LT    R5,ASPDEL                                                        
         JZ    GETP28                                                           
         CLC   =C'SJ',SPDACCS-SPDEL(R5)                                         
         JNE   GETP28                                                           
         MVC   AXG$PRD,SPDACCS+5-SPDEL(R5)                                      
*                                                                               
GETP28   CLI   AXG$PRD+3,C' '                                                   
         JH    GETPRDX                                                          
*                                                                               
         TM    LDG,EXPLDGQ         TEST EXPENSE LEDGER                          
         JZ    GETP30                                                           
         LT    R5,AEXOEL                                                        
         JZ    GETP30                                                           
         L     RE,ATRNEL                                                        
         CLI   TRNTYPE-TRNEL(RE),12                                             
         JNE   GETP30                                                           
         MVC   AXG$PRD+3(3),EXOPRO-EXOEL(R5)                                    
*                                                                               
GETP30   CLI   AXG$PRD+3,C' '                                                   
         JH    GETPRDX                                                          
*                                                                               
         LT    R5,AFFTEL                                                        
         JZ    GETP40                                                           
*                                                                               
         MVC   ELCODE,0(R5)                                                     
GETP34   CLI   FFTTYPE-FFTEL(R5),FFTTCLPR                                       
         JE    GETP36                                                           
         BRAS  RE,NEXTEL                                                        
         JE    GETP34                                                           
         J     GETP40                                                           
*                                                                               
GETP36   MVC   AXG$PRD+3(3),FFTPRAC-FFTEL(R5)                                   
         CLI   AXG$PRD+3,C' '                                                   
         JH    GETPRDX                                                          
*                                                                               
GETP38   TM    LDG,PAYLDGQ                                                      
         JZ    GETP40                                                           
         LT    R5,ALNKEL                                                        
         JZ    *+10                                                             
         MVC   AXG$PRD,LNKAGJB+5-LNKELD(R5)                                     
*                                                                               
GETP40   CLI   AXG$PRD+3,C' '                                                   
         JH    GETPRDX                                                          
*                                                                               
         LT    R5,AMBIEL                                                        
         JZ    GETPRDX                                                          
*                                                                               
         MVC   AXG$PRD+3(3),MBIPRD-MBIEL(R5)                                    
*                                                                               
GETPRDX  OC    AXG$PRD,MYSPACES    MAKE SURE ALL BLANKS ARE SPACES              
*        GOTO1 =A(GTSJNAME),DMCB,(6,AXG$PRD),AXG$PRNM                           
         OC    AXG$PRNM,MYSPACES                                                
         J     EXIT                                                             
         EJECT                                                                  
*==============================================================                 
* EXTRACT ESTIMATE                                                              
*==============================================================                 
                                                                                
         USING TRNRECD,R3                                                       
GETEST   NTR1                                                                   
*                                                                               
         L     RE,ATRNEL                                                        
         CLI   TRNTYPE-TRNELD(RE),10  TYPE 10 DOES NOT HAVE EST                 
         JE    GETESTX                                                          
*                                                                               
         TM    LDG,PAYLDGQ+SCLDGQ  TEST CLEARANCE (PAYABLE+SC)                  
         JNZ   GETE2                                                            
         CLC   =C'SZ',TRNKULA      +SZ                                          
         JNE   GETE4                                                            
*                                                                               
GETE2    L     R5,ATRNEL                                                        
         CLI   TRNTYPE-TRNELD(R5),9  SKIP TYPE 9 TRANSACTIONS                   
         JE    GETE4                                                            
*                                                                               
         LT    R5,AXPYEL                                                        
         JZ    GETE4                                                            
         XR    R0,R0                                                            
         ICM   R0,3,XPYEST-XPYELD(R5)                                           
         JZ    GETE4                                                            
         CVD   R0,DUB                                                           
         EDIT  (R0),(6,AXG$EST),ALIGN=LEFT                                      
         J     GETESTX                                                          
*                                                                               
GETE4    LT    R5,AMDTEL                                                        
         JNZ   GETE6                                                            
         LT    R5,AMDPEL                                                        
         JZ    GETE10                                                           
*                                                                               
GETE6    MVC   AXG$EDSC,MDTDSCP-MDTEL(R5)    SAVE DESCRIPTION                   
         CLI   MDTSYS-MDTEL(R5),C'J'         TEST PROD                          
         JNE   GETE6X                        NO                                 
         L     RE,ATRNEL                                                        
         CLI   TRNTYPE-TRNELD(RE),9          TYPE 9 HAS ESTIMATE!               
         JNE   GETE10                                                           
GETE6X   MVC   AXG$EST,MDTEST-MDTEL(R5)                                         
         J     EXIT                                                             
*                                                                               
GETE10   LT    R5,AOTHEL                                                        
         JZ    GETE12                                                           
         CLI   OTHPROF-OTHELD(R5),C'J'  NOT FOR PROD                            
         JE    GETE12                                                           
         MVC   AXG$EST,MYSPACES    DO WE HAVE AN ESTIMATE?                      
         LA    RE,OTHNUM-OTHELD(R5)  SET RE=A(OTHNUM)                           
         LA    R1,5                SET EX LENGTH                                
         CLC   OMGRUN,=C'Y'        ARE WE RUNNING FOR OMG?                      
         JNE   *+12                                                             
         LA    RE,3(RE)            BUMP PAST PRODUCT IN THE EST FIELD           
         LA    R1,2                SET EX LENGTH                                
         MVC   AXG$EST(0),0(RE)                                                 
         EX    R1,*-6                                                           
         CLC   AXG$EST,MYSPACES    DO WE HAVE AN ESTIMATE?                      
         JH    GETESTX                                                          
*                                                                               
GETE12   LT    R5,AMBIEL                                                        
         JZ    GETE14                                                           
         MVC   AXG$EST,MYSPACES                                                 
         MVC   AXG$EST(L'MBIEST),MBIEST-MBIELD(R5)                              
         CLC   AXG$EST,MYSPACES    DO WE HAVE AN ESTIMATE?                      
         JH    GETESTX                                                          
*                                                                               
GETE14   LT    R5,AFFTEL                                                        
         JZ    GETESTX                                                          
         MVC   AXG$EST,MYSPACES                                                 
         MVI   ELCODE,FFTELQ                                                    
*                                                                               
GETE16   CLI   FFTTYPE-FFTEL(R5),FFTTESTN                                       
         JE    GETE18                                                           
         BRAS  RE,NEXTEL                                                        
         JE    GETE16                                                           
         J     GETESTX                                                          
*                                                                               
GETE18   MVC   AXG$EST(3),FFTOESTN-FFTEL(R5)  ESTIMATE NUMBER IN CHAR           
*                                                                               
GETESTX  J     EXIT                                                             
         EJECT                                                                  
*===========================================================                    
* GET JOB NUMBER                                                                
*===========================================================                    
*&&DO                                                                           
GETJOB   NTR1                                                                   
*                                                                               
         TM    LDG,SJLDGQ          TEST SJ                                      
         JZ    GETJ1                                                            
         MVC   AXG$JOB,TRNKACT+6   MOVE JOB FROM KEY                            
         J     EXIT                                                             
*                                                                               
GETJ1    TM    LDG,PAYLDGQ                                                      
         JZ    GETJ2                                                            
         LT    R5,AOTHEL                                                        
         JZ    GETJ2                                                            
         CLI   OTHNUM-OTHEL(R5),C' '                                            
         JNH   GETJ2                                                            
         MVC   AXG$JOB,OTHNUM+6-OTHELD(R5)                                      
         J     EXIT                                                             
*                                                                               
GETJ2    LT    R5,AMDTEL                                                        
         JNZ   *+14                                                             
         LT    R5,AMDPEL                                                        
         JZ    GETJ10                                                           
*                                                                               
         CLI   MDTSYS-MDTEL(R5),C'J'                                            
         JNE   GETJ4                                                            
         MVC   AXG$JOB,MDTJOB-MDTEL(R5)                                         
         J     EXIT                                                             
*                                                                               
GETJ4    CLI   MDTJOB+3-MDTEL(R5),C' '                                          
         JNH   GETJ10                                                           
*                                                                               
         L     RE,ATRNEL                                                        
         LA    R0,GETJ4LSX-GETJ4LST                                             
         LA    R1,GETJ4LST                                                      
*                                                                               
GETJ6    CLC   0(1,R1),TRNTYPE-TRNEL(RE)                                        
         JE    GETJ8                                                            
         LA    R1,1(R1)                                                         
         JCT   R0,GETJ6                                                         
         J     GETJ10                                                           
*                                                                               
GETJ4LST DC    AL1(26,30,55,56,58)                                              
GETJ4LSX EQU   *                                                                
*                                                                               
GETJ8    MVC   AXG$JOB,MDTJOB-MDTEL(R5)                                         
         J     EXIT                                                             
*                                                                               
GETJ10   LT    R5,AOTHEL                                                        
         JZ    GETJ12                                                           
         CLI   OTHPROF-OTHEL(R5),C'J'                                           
         JNE   GETJ12                                                           
         MVC   AXG$JOB,OTHNUM-OTHEL(R5)                                         
         J     EXIT                                                             
*                                                                               
GETJ12   LT    R5,ACPJEL                                                        
         JZ    GETJ14                                                           
         CLI   CPJTYPE-CPJEL(R5),C'J'                                           
         JNE   GETJ14                                                           
         MVC   AXG$JOB,CPJJOB-CPJEL(R5)                                         
         J     EXIT                                                             
*                                                                               
GETJ14   TM    LDG,SCLDGQ          IS IT SC                                     
         JZ    GETJ16                                                           
         LT    R5,ASPDEL                                                        
         JZ    GETJ16                                                           
         CLC   =C'SJ',SPDACCS-SPDEL(R5)                                         
         JNE   GETJ16                                                           
         MVC   AXG$JOB,SPDACCS+8-SPDEL(R5)                                      
         J     EXIT                                                             
*                                                                               
GETJ16   TM    LDG,SCLDGQ                                                       
         JZ    GETJ18                                                           
         LT    R5,AOTHEL                                                        
         JZ    GETJ18                                                           
         L     RE,ATRNEL                                                        
         CLI   TRNTYPE-TRNEL(RE),3    TYPE 3 TRANS ONLY                         
         JNE   GETJ18                                                           
         MVC   AXG$JOB,OTHNUM-OTHEL(R5)                                         
         J     EXIT                                                             
*                                                                               
GETJ18   TM    LDG,PAYLDGQ                                                      
         JZ    GETJOBX                                                          
         LT    R5,ALNKEL                                                        
         JZ    *+10                                                             
         MVC   AXG$JOB,LNKAGJB+8-LNKELD(R5)                                     
*                                                                               
GETJOBX  J     EXIT                                                             
*&&                                                                             
         EJECT                                                                  
*===========================================================                    
* GET INVOICE NUMBER                                                            
*===========================================================                    
                                                                                
GETINV   NTR1                                                                   
         L     R5,ATRNEL                                                        
         CLI   LDGVWXY,C'Y'        IS IT SV,SW,SX,SY                            
         JNE   GETINV2                                                          
         MVC   AXG$INV(L'TRNREF),TRNREF-TRNELD(R5)                              
         MVC   AXG$VINV,AXG$INV                                                 
         J     GETINVX                                                          
*                                                                               
GETINV2  LT    R5,AXPYEL                                                        
         JZ    GETINV10                                                         
         CLI   LDGPQSTU,C'Y'       IS IT SP,SQ,SS,ST,SU                         
         JNE   GETINV10                                                         
         MVC   AXG$INV(L'XPYINV),XPYINV-XPYELD(R5)                              
         MVC   AXG$VINV,AXG$INV                                                 
         J     GETINVX                                                          
*                                                                               
GETINV10 LT    R5,AFFTEL                                                        
         JZ    GETINVX                                                          
         MVI   ELCODE,FFTELQ                                                    
*                                                                               
GETINV12 CLI   FFTTYPE-FFTEL(R5),FFTTINVN                                       
         JE    GETINV14                                                         
         BRAS  RE,NEXTEL                                                        
         JE    GETINV12                                                         
         J     GETINVX                                                          
*                                                                               
GETINV14 LLC   RE,FFTDLEN-FFTEL(R5)  GET TEXT LEN                               
         CHI   RE,L'AXG$INV                                                     
         JNH   *+8                                                              
         LA    RE,L'AXG$INV                                                     
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   AXG$INV(0),FFTDATA-FFTEL(R5)                                     
         MVC   AXG$VINV,AXG$INV                                                 
*                                                                               
GETINVX  J     EXIT                                                             
         EJECT                                                                  
*===============================================================                
* MATCH TEXT IN NARRATIVE ELEMENT TO TABLE ENTRIES                              
* WRITE-OFF #SS11 DATE APR26/12 A/C SES0303 SHIRL SE                            
*===============================================================                
GETNARR  NTR1                                                                   
*                                                                               
         L     R5,ATRNEL                                                        
         USING TRNELD,R5                                                        
         LLC   R0,1(R5)            GET ELEMENT LENGTH                           
         AHI   R0,-TRNLN1Q         - LEN TO START OF NARRATIVE                  
         JNP   NEQXIT                                                           
         CHI   R0,12              NEED AT LEAST 12 CHARS                        
         JNH   NEQXIT                                                           
         LA    R1,TRNNARR                                                       
         STM   R0,R1,DUB                                                        
*                                                                               
         CLI   DRORCR,C'C'         TEST CREDIT                                  
         JNE   GETN30                                                           
*                                                                               
* ROUTINES PRIOR TO GETN30 ARE FOR CREDITS ONLY!                                
*                                                                               
         LA    R0,12                                                            
         LA    R1,=CL12'CHECK NUMBER'                                           
         BAS   RE,NARRSCAN                                                      
         JNE   GETN10                                                           
*                                                                               
         MVC   AXG$CHEK,13(R5)                                                  
*                                                                               
         LA    R1,AXG$CHEK                                                      
         LA    R0,L'AXG$CHEK                                                    
         BAS   RE,GETBLNK                                                       
*                                                                               
GETN04   LA    R0,5                                                             
         LA    R1,=C'DATED'                                                     
         BAS   RE,NARRSCAN                                                      
         JNE   GETN06                                                           
         LA    R0,6(R5)                                                         
         BAS   RE,NARRDT                                                        
         MVC   AXG$CKDT,WORK                                                    
*                                                                               
GETN06   LA    R0,12                                                            
         LA    R1,=CL12'DEPOSITED ON'                                           
         BAS   RE,NARRSCAN                                                      
         JNE   GETN10                                                           
         LA    R0,13(R5)           POINT TO DATE                                
         BAS   RE,NARRDT                                                        
         MVC   AXG$DPDT,WORK                                                    
*                                                                               
GETN10   LA    R0,17                                                            
         LA    R1,=CL17'WRITE-OFF ACCOUNT'    ??????????                        
         BAS   RE,NARRSCAN                                                      
         JNE   GETN12                                                           
         MVC   AXG$WOAC,18(R5)     WRITE-OFF ACCOUNT                            
         LA    R0,L'AXG$WOAC                                                    
         LA    R1,AXG$WOAC                                                      
         BAS   RE,GETBLNK                                                       
         J     GETN14                                                           
*                                                                               
GETN12   LA    R0,11                                                            
         LA    R1,=CL11'WRITE-OFF #'                                            
         BAS   RE,NARRSCAN                                                      
         JNE   GETN20                                                           
         MVC   AXG$WONU,11(R5)     WRITE-OFF NUM (NO SPACE AFTER #)             
         LA    R0,L'AXG$WONU                                                    
         LA    R1,AXG$WONU                                                      
         BAS   RE,GETBLNK                                                       
*                                                                               
GETN14   LA    R0,4                                                             
         LA    R1,=CL4'DATE'                                                    
         BAS   RE,NARRSCAN                                                      
         JNE   GETN16                                                           
         LA    R0,5(R5)            WRITE-OFF DATE (TEXT2)                       
         BAS   RE,NARRDT                                                        
         MVC   AXG$WODT,WORK                                                    
*                                                                               
GETN16   LA    R0,3                                                             
         LA    R1,=CL3'A/C'                                                     
         BAS   RE,NARRSCAN                                                      
         JNE   GETN18                                                           
         MVC   AXG$WOAC,4(R5)                                                   
*                                                                               
GETN18   LA    R0,9                                                             
         LA    R1,=CL9'REFERENCE'                                               
         BAS   RE,NARRSCAN                                                      
         JNE   GETN20                                                           
         MVC   AXG$WONU,10(R5)     WRITE-OFF NUMBER                             
*                                                                               
GETN20   LA    R0,9                                                             
         LA    R1,=CL9'OFFSET ON'                                               
         BAS   RE,NARRSCAN                                                      
         JNE   GETN30                                                           
         LA    R0,10(R5)           TRANSFER DATE                                
         BAS   RE,NARRDT                                                        
         MVC   AXG$OFDT,WORK                                                    
*                                                                               
GETN30   LA    R0,14                                                            
         LA    R1,=C'TRANSFERRED TO'                                            
         BAS   RE,NARRSCAN                                                      
         JNE   GETN40                                                           
         MVC   AXG$XFAC,15(R5)     TRANSFER TO ACCOUNT                          
*                                                                               
GETN40   LA    R0,15                                                            
         LA    R1,=CL15'CHECK VOIDED ON'                                        
         BAS   RE,NARRSCAN                                                      
         JNE   GETNX                                                            
         LA    R0,16(R5)           VOID DATE                                    
         BAS   RE,NARRDT                                                        
         MVC   AXG$VDDT,WORK                                                    
*                                                                               
GETNX    J     EXIT                                                             
*                                                                               
* CONVERT MMMDD/YY  --> YYMMDD - NO FUNNIES                                     
*                                                                               
NARRDT   NTR1                                                                   
         MVC   WORK,MYSPACES                                                    
         GOTO1 VDATVAL,DMCB,(0,(R0)),(X'80',WORK)                               
         J     EXIT                                                             
*                                                                               
* FIND LAST CHAR AND SET BLANKS AFTER IT                                        
*                                                                               
GETBLNK  CLI   0(R1),C' '                                                       
         JNH   GETBLNK4                                                         
         LA    R1,1(R1)                                                         
         JCT   R0,GETBLNK                                                       
         BR    RE                                                               
*                                                                               
GETBLNK2 MVI   0(R1),C' '                                                       
*                                                                               
GETBLNK4 LA    R1,1(R1)                                                         
         JCT   R0,GETBLNK2                                                      
         BR    RE                                                               
                                                                                
*==========================================================                     
* DUB+0 IS ADDRESS OF NARRATIVE                                                 
* DUB+4 IS LENGTH OF NARRATIVE                                                  
* R0 IS LENGTH OF SEARCH STRING                                                 
* R1 IS ADDRESS OF TEXT STRING                                                  
*==========================================================                     
                                                                                
NARRSCAN NTR1                                                                   
         LM    R4,R5,DUB           NARRATIVE LENGTH/ADDRESS                     
         LR    RE,R0               GET SEARCH STRING LENGTH                     
         BCTR  RE,0                                                             
*                                                                               
NARRSCN2 EX    RE,*+8              MATCH TEXT 1                                 
         J     *+10                                                             
         CLC   0(0,R1),0(R5)       SEARCH STRING TO NARR                        
         JE    NARRSCNX                                                         
         LA    R5,1(R5)                                                         
         CR    R4,RE               ENOUGH CHARS LEFT TO MATCH?                  
         JNH   NEQXIT                                                           
         JCT   R4,NARRSCN2                                                      
         J     NEQXIT                                                           
*                                                                               
NARRSCNX XIT1 REGS=(R5)                                                         
*                                                                               
CVD      CVD   R0,DUB                                                           
         LTR   R0,R0                                                            
         BM    *+8                                                              
         OI    DUB+7,X'0F'                                                      
         UNPK  P16,DUB                                                          
         BR    RE                                                               
*                                                                               
*** CAN BE CALLED ONLY FROM AXTRNS!!!! ***                                      
TRNCVDP  EDIT  (P8,DUB),P16,2,FLOAT=-,COMMAS=NO,ZERO=NOBLANK,          X        
               ALIGN=LEFT                                                       
         BR    RE                                                               
         PRINT NOGEN                                                            
         EJECT                                                                  
AXTRNTAB DS    0H                                                               
         DC    AL2(AXG#01-AXG$TRN)                                              
         DC    AL2(AXG#02-AXG$TRN)                                              
         DC    AL2(AXG#03-AXG$TRN)                                              
         DC    AL2(AXG#04-AXG$TRN)                                              
         DC    AL2(AXG#05-AXG$TRN)                                              
         DC    AL2(AXG#06-AXG$TRN)                                              
         DC    AL2(AXG#07-AXG$TRN)                                              
         DC    AL2(AXG#08-AXG$TRN)                                              
         DC    AL2(AXG#09-AXG$TRN)                                              
         DC    AL2(AXG#10-AXG$TRN)                                              
         DC    AL2(AXG#11-AXG$TRN)                                              
         DC    AL2(AXG#12-AXG$TRN)                                              
         DC    AL2(AXG#13-AXG$TRN)                                              
         DC    AL2(AXG#14-AXG$TRN)                                              
         DC    AL2(AXG#15-AXG$TRN)                                              
         DC    AL2(AXG#16-AXG$TRN)                                              
         DC    AL2(AXG#17-AXG$TRN)                                              
         DC    AL2(AXG#18-AXG$TRN)                                              
*        DC    AL2(AXG#19-AXG$TRN)                                              
         DC    AL2(AXG#20-AXG$TRN)                                              
         DC    AL2(AXG#21-AXG$TRN)                                              
         DC    AL2(AXG#22-AXG$TRN)                                              
         DC    AL2(AXG#23-AXG$TRN)                                              
         DC    AL2(AXG#24-AXG$TRN)                                              
         DC    AL2(AXG#25-AXG$TRN)                                              
*        DC    AL2(AXG#26-AXG$TRN)                                              
         DC    AL2(AXG#28-AXG$TRN)                                              
         DC    AL2(AXG#29-AXG$TRN)                                              
         DC    AL2(AXG#30-AXG$TRN)                                              
         DC    AL2(AXG#31-AXG$TRN)                                              
         DC    AL2(AXG#32-AXG$TRN)                                              
         DC    AL2(AXG#33-AXG$TRN)                                              
         DC    AL2(AXG#34-AXG$TRN)                                              
         DC    AL2(AXG#35-AXG$TRN)                                              
         DC    AL2(AXG#36-AXG$TRN)                                              
         DC    AL2(AXG#39-AXG$TRN)                                              
         DC    AL2(AXG#41-AXG$TRN)                                              
         DC    AL2(AXG#42-AXG$TRN)                                              
         DC    AL2(AXG#43-AXG$TRN)                                              
         DC    AL2(AXG#45-AXG$TRN)                                              
         DC    AL2(AXG#46-AXG$TRN)                                              
         DC    AL2(AXG#47-AXG$TRN)                                              
         DC    AL2(AXG#48-AXG$TRN)                                              
         DC    AL2(AXG#49-AXG$TRN)                                              
         DC    AL2(AXG#50-AXG$TRN)                                              
         DC    AL2(AXG#51-AXG$TRN)                                              
         DC    AL2(AXG#52-AXG$TRN)                                              
         DC    AL2(AXG#53-AXG$TRN)                                              
         DC    AL2(AXG#54-AXG$TRN)                                              
         DC    AL2(AXG#55-AXG$TRN)                                              
         DC    AL2(AXG#56-AXG$TRN)                                              
         DC    AL2(AXG#57-AXG$TRN)                                              
         DC    AL2(AXG#58-AXG$TRN)                                              
         DC    AL2(AXG#59-AXG$TRN)                                              
         DC    AL2(AXG#60-AXG$TRN)                                              
         DC    AL2(AXG#61-AXG$TRN)                                              
         DC    AL2(AXG#62-AXG$TRN)                                              
         DC    AL2(AXG#63-AXG$TRN)                                              
         DC    AL2(AXG#64-AXG$TRN)                                              
         DC    AL2(AXG#65-AXG$TRN)                                              
         DC    AL2(AXG#66-AXG$TRN)                                              
         DC    AL2(AXG#67-AXG$TRN)                                              
         DC    AL2(AXG#68-AXG$TRN)                                              
         DC    AL2(AXG#69-AXG$TRN)                                              
         DC    AL2(AXG#70-AXG$TRN)                                              
         DC    AL2(AXG#71-AXG$TRN)                                              
         DC    AL2(AXG#72-AXG$TRN)                                              
         DC    AL2(AXG#73-AXG$TRN)                                              
         DC    AL2(AXG#74-AXG$TRN)                                              
         DC    AL2(AXG#75-AXG$TRN)                                              
         DC    AL2(AXG#76-AXG$TRN)                                              
         DC    AL2(AXG#77-AXG$TRN)                                              
         DC    AL2(AXG#78-AXG$TRN)                                              
         DC    AL2(AXG#79-AXG$TRN)                                              
         DC    AL2(AXG#80-AXG$TRN)                                              
         DC    AL2(AXG#81-AXG$TRN)                                              
         DC    AL2(AXG#82-AXG$TRN)                                              
         DC    AL2(AXG#83-AXG$TRN)                                              
         DC    AL2(AXG#84-AXG$TRN)                                              
         DC    AL2(AXG#85-AXG$TRN)                                              
         DC    AL2(AXG#86-AXG$TRN)                                              
         DC    AL2(AXG#87-AXG$TRN)                                              
         DC    AL2(AXG#88-AXG$TRN)                                              
         DC    AL2(AXG#89-AXG$TRN)                                              
         DC    AL2(AXG#90-AXG$TRN)                                              
         DC    AL2(AXG#91-AXG$TRN)                                              
         DC    AL2(AXG#92-AXG$TRN)                                              
         DC    AL2(AXG#93-AXG$TRN)                                              
         DC    AL2(AXG#94-AXG$TRN)                                              
         DC    AL2(AXG#95-AXG$TRN)                                              
         DC    AL2(AXG#96-AXG$TRN)                                              
         DC    AL2(AXG#97-AXG$TRN)                                              
         DC    AL2(AXG#98-AXG$TRN)                                              
         DC    AL2(AXG#99-AXG$TRN)                                              
         DC    AL2(AXG#100-AXG$TRN)                                             
         DC    AL2(AXG#101-AXG$TRN)                                             
AXTRNTBX EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE AXTRBRID                                                       
         EJECT                                                                  
         PRINT OFF                                                              
       ++INCLUDE MXTRT                                                          
         PRINT ON                                                               
         EJECT                                                                  
*----------------------------------------------------------------------         
* CONVERT YYYYMMDD TO DDMMYYYY                                                  
*----------------------------------------------------------------------         
ENGDATE  NTR1  BASE=*,LABEL=*                                                   
         L     RF,0(R1)            ADDRESS OF DATE TO CHANGE                    
         MVC   WORK(8),0(RF)                                                    
         MVC   0(2,RF),WORK+6                                                   
         MVC   2(2,RF),WORK+4                                                   
         MVC   4(4,RF),WORK                                                     
         J     EXIT                                                             
         LTORG                                                                  
         EJECT                                                                  
SETLDGR  NTR1  BASE=*,LABEL=*                                                   
         MVI   LDG,0                                                            
         MVI   LDGVWXY,C'N'                                                     
         MVI   LDGPQSTU,C'N'                                                    
*                                                                               
         BAS   RE,CHKRCV                                                        
         BNE   *+8                                                              
         OI    LDG,RCVLDGQ                                                      
*                                                                               
         BAS   RE,CHKPAY                                                        
         BNE   *+8                                                              
         OI    LDG,PAYLDGQ                                                      
*                                                                               
         BAS   RE,CHKINC                                                        
         BNE   *+8                                                              
         OI    LDG,INCLDGQ                                                      
*                                                                               
         BAS   RE,CHKEXP                                                        
         BNE   *+8                                                              
         OI    LDG,EXPLDGQ                                                      
*                                                                               
         BAS   RE,CHKMED                                                        
         BNE   *+8                                                              
         OI    LDG,MEDLDGQ                                                      
*                                                                               
         CLC   =C'SC',TRNKUNT                                                   
         BNE   *+8                                                              
         OI    LDG,SCLDGQ                                                       
*                                                                               
         CLC   =C'SE',TRNKUNT                                                   
         BNE   *+8                                                              
         OI    LDG,SELDGQ                                                       
*                                                                               
         CLC   =C'SJ',TRNKUNT                                                   
         BNE   *+8                                                              
         OI    LDG,SJLDGQ                                                       
*                                                                               
         BAS   RE,CHKPQST                                                       
         BNE   *+8                                                              
         MVI   LDGPQSTU,C'Y'                                                    
*                                                                               
         BAS   RE,CHKVWXY                                                       
         BNE   *+8                                                              
         MVI   LDGVWXY,C'Y'                                                     
*                                                                               
         J     EXIT                                                             
*                                                                               
RCVLDGQ  EQU   X'80'                                                            
PAYLDGQ  EQU   X'40'                                                            
INCLDGQ  EQU   X'20'                                                            
EXPLDGQ  EQU   X'10'                                                            
MEDLDGQ  EQU   X'08'                                                            
SCLDGQ   EQU   X'04'                                                            
SJLDGQ   EQU   X'02'                                                            
SELDGQ   EQU   X'01'                                                            
*                                                                               
CHKRCV   LA    R1,RCVLDG                                                        
         LA    R0,(RCVLDGX-RCVLDG)/2                                            
         B     CHKLDG                                                           
*                                                                               
CHKPAY   LA    R1,PAYLDG                                                        
         LA    R0,(PAYLDGX-PAYLDG)/2                                            
         B     CHKLDG                                                           
*                                                                               
CHKINC   LA    R1,INCLDG                                                        
         LA    R0,(INCLDGX-INCLDG)/2                                            
         B     CHKLDG                                                           
*                                                                               
CHKEXP   LA    R1,EXPLDG                                                        
         LA    R0,(EXPLDGX-EXPLDG)/2                                            
         B     CHKLDG                                                           
*                                                                               
CHKMED   LA    R1,MEDLDG                                                        
         LA    R0,(MEDLDGX-MEDLDG)/2                                            
         B     CHKLDG                                                           
*                                                                               
CHKPQST  LA    R1,PQSTULST                                                      
         LA    R0,(PQSTULSTX-PQSTULST)/2                                        
         B     CHKLDG                                                           
*                                                                               
CHKVWXY  LA    R1,VWXYLST                                                       
         LA    R0,(VWXYLSTX-VWXYLST)/2                                          
         B     CHKLDG                                                           
*                                                                               
CHKLDG   CLC   TRNKUNT(2),0(R1)                                                 
         BER   RE                                                               
         LA    R1,2(R1)                                                         
         BCT   R0,CHKLDG                                                        
         LTR   RB,RB                                                            
         BR    RE                                                               
*                                                                               
RCVLDG   DC    C'SBSR'                                                          
RCVLDGX  EQU   *                                                                
INCLDG   DC    C'SI'                                                            
INCLDGX  EQU   *                                                                
PAYLDG   DC    C'SPSQSSSTSUSVSWSXSY'                                            
PAYLDGX  EQU   *                                                                
EXPLDG   DC    C'SBSE'                                                          
EXPLDGX  EQU   *                                                                
MEDLDG   DC    C'SZ'                                                            
MEDLDGX  EQU   *                                                                
*                                                                               
PQSTULST DC    C'SPSQSSSTSU'                                                    
PQSTULSTX EQU  *                                                                
VWXYLST  DC    C'SVSWSXSY'                                                      
VWXYLSTX EQU   *                                                                
         LTORG                                                                  
*                                                                               
NEXTEL   SR    R0,R0                                                            
         ICM   R0,1,1(R5)                                                       
         JNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         AR    R5,R0                                                            
         CLI   0(R5),0                                                          
         JE    NEXTELX                                                          
*                                                                               
FRSTEL   CLC   ELCODE,0(R5)                                                     
         JNE   NEXTEL                                                           
         BR    RE                                                               
*                                                                               
NEXTELX  LTR   RB,RB                                                            
         BR    RE                                                               
         EJECT                                                                  
         USING AXG$TRN,R2                                                       
MVNARR   MVC   AXG$NARR(0),0(R1)                                                
         DROP  R2                                                               
MYMXTRTQ EQU   C'|'                                                             
*                                                                               
         EJECT                                                                  
*============================================================                   
* EXTRACT THE GIN FROM THE RECOVERY TRAILER                                     
*============================================================                   
*&&DO                                                                           
         USING AXG$TRN,R2          R2=A(EXTRACT RECORD)                         
         USING TRNRECD,R3          R3=A(ACCFILE RECORD)                         
*                                                                               
GETGIN   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         L     RE,VDXBLOCK         A(EXTRACT CONTROL DATA BLOCK)                
         USING DXBLOCKD,RE                                                      
         MVC   DUB,DXRGIN                                                       
         OC    DUB,DUB                                                          
         JZ    GETGIN2                                                          
*                                                                               
         GOTO1 VHEXOUT,DMCB,DUB,AXG$GIN,4,=C'TOG'                               
         L     R0,DUB+4                                                         
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  AXG$GIN+8(8),DUB                                                 
         J     EXIT                                                             
*                                                                               
GETGIN2  J     EXIT                                                             
         LTORG                                                                  
*&&                                                                             
*          DATA SET TAXROUTS   AT LEVEL 028 AS OF 02/24/11                      
         EJECT                                                                  
*          DATA SET TASYSVAL   AT LEVEL 185 AS OF 05/12/10                      
*=====================================================================          
*              SSNPCK - ENCRYPTS SS#                                            
*                                  P1=A(SS#)                                    
*                                  P2=A(PID)                                    
*                                    CC=EQ GOOD ENCRYPTION                      
*                                    CC=NEQ BAD ENCRYPTION                      
*=====================================================================          
                                                                                
SSNPCK   NTR1  BASE=*,LABEL=*                                                   
         L     R2,4(R1)                                                         
         MVC   0(6,R2),MYSPACES    BLANK OUT PID                                
*                                                                               
         LA    R4,9                TEST SS# FOR NON NUMERIC                     
         L     R3,0(R1)                                                         
SSNPCK5  CLI   0(R3),C'0'                                                       
         BL    *+12                                                             
         CLI   0(R3),C'9'                                                       
         BNH   SSNPCK7                                                          
         LTR   RB,RB               CC=NEQ                                       
         B     SSNPKXT                                                          
*                                                                               
SSNPCK7  LA    R3,1(R3)            BUMP TO NEXT CHAR                            
         BCT   R4,SSNPCK5                                                       
*                                                                               
         L     R3,0(R1)                                                         
         PACK  DUB,0(9,R3)         CONVERT CHARACTER SSN TO HEX SSN             
         NI    DUB+7,X'FC'                                                      
         CVB   RE,DUB                                                           
         ST    RE,FULL             FULL=HEX SSN                                 
*                                                                               
         LA    R3,6                6 DIGITS                                     
         L     R2,4(R1)            PID # ADDRESS                                
         LA    RF,BASE36TB                                                      
SSNPK10  CLI   0(RF),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'00'                                                            
         BAS   RE,SSNPCALC                                                      
         BNE   SSNPKXT                                                          
         MVC   0(1,R2),BYTE        NEXT DIGIT OF PID #                          
         LA    RF,L'BASE36TB(RF)                                                
         LA    R2,1(R2)                                                         
         BCT   R3,SSNPK10                                                       
         CR    RB,RB               CC=EQ                                        
*                                                                               
SSNPKXT  XIT1                                                                   
         EJECT                                                                  
*                                                                               
SSNPCALC NTR1                                                                   
         XR    R0,R0                                                            
         L     R1,FULL             SSN                                          
         D     R0,0(RF)            DIVIDE BY BASE 36 NUMBER                     
         XC    HALF,HALF                                                        
         STC   R1,HALF+1                                                        
         ZICM  R0,0(RF),4                                                       
         MH    R0,HALF             MULTIPLY QUOTIENT BY POWER OF 36             
         L     RE,FULL                                                          
         SR    RE,R0               SUBTRACT PRODUCT FROM REMAINING SSN          
         ST    RE,FULL             KEEP TRACK OF REMAINING SSN                  
*                                                                               
         LA    R3,BASE36T2                                                      
SCALC30  CLI   0(R3),X'FF'                                                      
         BE    SCALCNO                                                          
         CLC   HALF+1(1),0(R3)     FIND MATCH IN TABLE                          
         BE    SCALC40                                                          
         LA    R3,L'BASE36T2(R3)                                                
         B     SCALC30                                                          
SCALC40  MVC   BYTE,1(R3)          COMPLEMENTED BASE 36 NUMBER                  
*                                                                               
SCALCYES CR    RB,RB                                                            
         B     *+6                                                              
SCALCNO  LTR   RB,RB                                                            
         B     SSNUPXT                                                          
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*=====================================================================          
*              SSNUNPK - DECRYPTS PID                                           
*                                  P1=A(PID)                                    
*                                  P2=A(SSN)                                    
*                                    CC=EQ GOOD DECRYPTION                      
*                                    CC=NEQ BAD DECRYPTION                      
*=====================================================================          
                                                                                
SSNUPK   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         L     R3,4(R1)            CLEAR OUT SS#, IN CASE PID INVALID           
         MVC   0(9,R3),MYSPACES                                                 
*                                                                               
         L     R2,0(R1)            PID #                                        
         LA    R3,6                6 DIGITS                                     
         LA    RF,BASE36TB                                                      
         XR    R0,R0                                                            
SSNUP10  CLI   0(RF),X'FF'         EOT?                                         
         BNE   SSNUP20                                                          
         LTR   RB,RB                                                            
         B     SSNUPXT             LEAVE, WITH SS# AS BLANK                     
SSNUP20  BAS   RE,SSNUCALC         UNPACK CALCULATIONS                          
         BNE   SSNUPXT                                                          
         A     R0,FULL                                                          
         LA    RF,L'BASE36TB(RF)                                                
         LA    R2,1(R2)                                                         
         BCT   R3,SSNUP10                                                       
         L     R3,4(R1)                                                         
         EDIT  (R0),(9,(R3)),ZERO=NOBLANK,FILL=0                                
         CR    RB,RB                                                            
SSNUPXT  XIT1                                                                   
         EJECT                                                                  
*                                                                               
SSNUCALC NTR1                                                                   
         LA    R3,BASE36T2                                                      
SCALU10  CLI   0(R3),X'FF'                                                      
         BE    SCALUNO                                                          
         CLC   0(1,R2),1(R3)       FIND MATCH OF BASE 36 NUM IN TABLE           
         BE    SCALU20                                                          
         LA    R3,L'BASE36T2(R3)                                                
         B     SCALU10                                                          
SCALU20  XC    HALF,HALF                                                        
         MVC   HALF+1(1),0(R3)     HEX EQUIVALENT NUMBER                        
         ZICM  RE,0(RF),4                                                       
         MH    RE,HALF                                                          
         STCM  RE,15,FULL                                                       
*                                                                               
SCALUYES CR    RB,RB                                                            
         B     *+6                                                              
SCALUNO  LTR   RB,RB                                                            
         B     SSNUPXT                                                          
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
BASE36TB DS    0F                                                               
         DC    F'60466176'         36 TO THE 5TH POWER                          
         DC    F'1679616'          36 TO THE 4TH POWER                          
         DC    F'46656'            36 TO THE 3RD POWER                          
         DC    F'1296'             36 TO THE 2ND POWER                          
         DC    F'36'               36 TO THE 1ST POWER                          
         DC    F'1'                36 TO THE ZERO POWER                         
         DC    X'FFFFFFFF'                                                      
         SPACE                                                                  
BASE36T2 DS    0XL2                                                             
         DC    XL1'0',CL1'C'                                                    
         DC    XL1'1',CL1'U'                                                    
         DC    XL1'2',CL1'7'                                                    
         DC    XL1'3',CL1'N'                                                    
         DC    XL1'4',CL1'2'                                                    
         DC    XL1'5',CL1'A'                                                    
         DC    XL1'6',CL1'T'                                                    
         DC    XL1'7',CL1'K'                                                    
         DC    XL1'8',CL1'Z'                                                    
         DC    XL1'9',CL1'8'                                                    
         DC    XL1'A',CL1'R'                                                    
         DC    XL1'B',CL1'Q'                                                    
         DC    XL1'C',CL1'D'                                                    
         DC    XL1'D',CL1'0'                                                    
         DC    XL1'E',CL1'X'                                                    
         DC    XL1'F',CL1'B'                                                    
         DC    XL1'10',CL1'J'                                                   
         DC    XL1'11',CL1'V'                                                   
         DC    XL1'12',CL1'5'                                                   
         DC    XL1'13',CL1'S'                                                   
         DC    XL1'14',CL1'G'                                                   
         DC    XL1'15',CL1'P'                                                   
         DC    XL1'16',CL1'W'                                                   
         DC    XL1'17',CL1'L'                                                   
         DC    XL1'18',CL1'9'                                                   
         DC    XL1'19',CL1'6'                                                   
         DC    XL1'1A',CL1'Y'                                                   
         DC    XL1'1B',CL1'1'                                                   
         DC    XL1'1C',CL1'F'                                                   
         DC    XL1'1D',CL1'M'                                                   
         DC    XL1'1E',CL1'4'                                                   
         DC    XL1'1F',CL1'I'                                                   
         DC    XL1'20',CL1'3'                                                   
         DC    XL1'21',CL1'E'                                                   
         DC    XL1'22',CL1'H'                                                   
         DC    XL1'23',CL1'O'                                                   
         DC    X'FF'                                                            
         EJECT                                                                  
         EJECT                                                                  
*==============================================================                 
* GET NAME FROM SJ RECORD                                                       
*==============================================================                 
         USING ACTRECD,R2                                                       
GTSJNAME NTR1  BASE=*,LABEL=*                                                   
         XR    RF,RF                                                            
         IC    RF,0(R1)            LENGTH                                       
         L     R3,0(R1)                                                         
         L     R4,4(R1)            OUTPUT ADDRESS                               
*                                                                               
         LA    R2,MYKEY                                                         
         XC    MYKEY,MYKEY                                                      
*                                                                               
         MVC   ACTKCPY,COMPANY                                                  
         MVC   ACTKUNT(2),=C'SJ'                                                
         LA    R5,ACTKACT                                                       
         TM    4(R1),X'80'         MIGHT HAVE A REP                             
         JZ    *+8                                                              
         LA    R5,ACTKUNT                                                       
*                                                                               
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         J     *+10                                                             
*        MVC   ACTKACT(0),0(R3)                                                 
         MVC   0(0,R5),0(R3)                                                    
         OC    ACTKACT,MYSPACES                                                 
*                                                                               
         MVC   MYKEYSAV,MYKEY                                                   
         LA    R2,MYKEY                                                         
         GOTO1 VDATAMGR,DMCB,DMRDHI,ACCDIR,MYKEY,(R2),DMWORK                    
         CLI   DMCB+8,0                                                         
         JE    GTSJ100                                                          
         DC    H'0'                                                             
*                                                                               
GTSJ100  CLC   MYKEY(ACTKEND),MYKEYSAV                                          
         JE    *+6                                                              
         DC    H'0'                                                             
         MVC   SVDSKDA,ACTKDA                                                   
*                                                                               
         LA    R2,MYIO                                                          
         GOTO1 VDATAMGR,DMCB,GETREC,ACCMST,SVDSKDA,(R2),DMWORK                  
*                                                                               
         XR    RF,RF                                                            
         LA    R5,ACTRFST                                                       
GTSJ200  CLI   0(R5),0             EOR                                          
         BNE   *+6                                                              
         DC    H'0'                HAS TO HAVE NAME                             
*                                                                               
         CLI   0(R5),NAMELQ        X'20'                                        
         BE    GTSJ300                                                          
         IC    RF,1(R5)                                                         
         AR    R5,RF                                                            
         J     GTSJ200                                                          
*                                                                               
GTSJ300  IC    RF,1(R5)                                                         
         SHI   RF,3                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),2(R5)                                                    
*                                                                               
         J     EXIT                                                             
*                                                                               
SVDSKDA  DS    XL4                                                              
         DROP  R2                                                               
         LTORG                                                                  
         EJECT                                                                  
*=====================================================                          
* RECOVERY RECORD TRAILER                                                       
*=====================================================                          
                                                                                
RCVTRLD  DSECT                                                                  
       ++INCLUDE DMRCVREXT                                                      
         EJECT                                                                  
       ++INCLUDE AXTRBWRK                                                       
*                                                                               
WORKD    DSECT                                                                  
         ORG   OVWORK                                                           
P16      DS    XL16                                                             
APARM    DS    A                                                                
AMXTRT   DS    A                                                                
ELCODE   DS    X                                                                
LDG      DS    X                                                                
DRORCR   DS    C                                                                
MEDPYBL  DS    C                                                                
OMGRUN   DS    C                                                                
SAPRUN   DS    C                                                                
ITMREP   DS    C                                                                
DUB2     DS    D                                                                
DUB3     DS    D                                                                
*                                                                               
JOBSTAT  DS    XL1                                                              
*                                                                               
LDGPQSTU DC    X'00'                                                            
LDGVWXY  DC    X'00'                                                            
*                                                                               
MYKEY    DS    XL64                                                             
MYKEYSAV DS    CL42                                                             
*                                                                               
MYIO     DS    1000C                                                            
*                                                                               
NARRTABD DSECT                                                                  
NARLEN1   DS    AL1                                                             
NARRSCAN1 DS   CL15                                                             
NARLEN2   DS    AL1                                                             
NARRSCAN2 DS   CL15                                                             
NARADDR  DS    A                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'078AXTRBROT  10/26/20'                                      
         END                                                                    
