*          DATA SET DDBUCKPRT  AT LEVEL 007 AS OF 05/01/02                      
*CATALP BUCKPRT                                                                 
         TITLE 'BUCKPRT - PRINT EST BUCKET DETAILS'                             
BUCKPRT  CSECT                                                                  
         PRINT NOGEN                                                            
*                                                                               
*        PARAM1     A(RECORD)         HOB - S=SPOT,P=PRINT                      
*        PARAM2     A(PRINT LINE)                                               
*        PARAM3     A(TOTALS)         12PL6 - 6 REG, 6 SPEC                     
*        PARAM4     V(DATCON)                                                   
*        PARAM5     V(PRINT ROUTINE)  IF ZERO NO PRINTING                       
*                                                                               
         NMOD1 20,BUCKPRT                                                       
         USING BPWK,RC                                                          
*                                                                               
         ST    R1,SAVR1                                                         
         MVC   PARMS,0(R1)                                                      
         L     R2,AREC                                                          
         L     R1,ATOTS            CLEAR TOTALS                                 
         LA    R0,12                                                            
         ZAP   0(6,R1),=PL6'0'                                                  
         LA    R1,6(R1)                                                         
         BCT   R0,*-10                                                          
*                                                                               
         L     R3,APL                                                           
         USING PLD,R3                                                           
         OC    PRNT,PRNT                                                        
         BZ    BP9                 SKIP PRINT                                   
         MVI   PL,C' '                                                          
         MVC   PL+1(L'PL-1),PL                                                  
         CLI   SYS,C'P'                                                         
         BNE   BP4                                                              
*                                                                               
*                                  PRINTPAK KEY                                 
         MVC   0(7,R3),0(R2)       AGM/CLT                                      
         MVI   3(R3),C'/'                                                       
         MVI   7(R3),C'/'                                                       
         MVC   8(3,R3),7(R2)                                                    
         MVI   11(R3),C'/'                                                      
         EDIT  (B2,10(R2)),(3,12(R3)),FILL=0                                    
         B     BP8                                                              
*                                                                               
BP4      DS    0H                  SPOTPAK KEY                                  
*                                                                               
BP8      DS    0H                                                               
         GOTO1 PRNT                                                             
         GOTO1 PRNT                SKIP A LINE                                  
*                                                                               
BP9      DS    0H                                                               
         LA    R4,33(R2)                                                        
         CLI   SYS,C'P'                                                         
         BE    *+8                                                              
         LA    R4,24(R2)                                                        
*                                                                               
         USING BKELEM,R4                                                        
BP10     DS    0H                                                               
         CLI   0(R4),0                                                          
         BE    BP40                EOR                                          
         CLI   0(R4),X'21'         TODAY                                        
         BE    BP12                                                             
         CLI   0(R4),X'22'         REG                                          
         BE    BP14                                                             
         CLI   0(R4),X'23'         SPEC                                         
         BE    BP16                                                             
*                                                                               
BP11     DS    0H                                                               
         ZIC   R0,1(R4)                                                         
         AR    R4,R0                                                            
         B     BP10                                                             
*                                                                               
BP12     DS    0H                  TODAY                                        
         OC    PRNT,PRNT                                                        
         BZ    BP11                SKIP PRINT                                   
         MVC   PLEL,=C'TD'                                                      
         GOTO1 DATCON,DMCB,(3,BKDATE),(5,PLMOS)                                 
         B     BP30                                                             
*                                                                               
BP14     DS    0H                  REG                                          
         BAS   RE,RTOTS                                                         
         OC    PRNT,PRNT                                                        
         BZ    BP11                SKIP PRINT                                   
         MVC   PLEL,=C'RG'                                                      
         B     BP18                                                             
*                                                                               
BP16     DS    0H                  SPEC                                         
         BAS   RE,RTOTS                                                         
         OC    PRNT,PRNT                                                        
         BZ    BP11                SKIP PRINT                                   
         MVC   PLEL,=C'SP'                                                      
*                                                                               
BP18     DS    0H                                                               
         GOTO1 DATCON,DMCB,(3,BKYM),(6,PLMOS)                                   
         GOTO1 (RF),(R1),(3,BKDATE),(5,WORK)                                    
         MVC   PLSTRT,WORK                                                      
         MVC   PLTYPE,BKTYPE                                                    
         CLI   PLTYPE,C'A'                                                      
         BNL   *+8                                                              
         OI    PLTYPE,X'F0'        MAKE VALUE READABLE                          
*                                                                               
BP30     DS    0H                                                               
         LA    R5,BKOGRS                                                        
         BAS   RE,PTOTS                                                         
         GOTO1 PRNT                                                             
         B     BP11                                                             
         SPACE 3                                                                
BP40     DS    0H                  TOTALS                                       
         OC    PRNT,PRNT                                                        
         BZ    BP60                                                             
         L     R5,ATOTS                                                         
         CLC   0(36,R5),36(R5)     OF RG + SP = PRINT ONLY 1 TOTAL              
         BE    BP42                                                             
*                                                                               
         MVC   PLEL,=C'RG'                                                      
         MVC   PLMOS,=C'TOTAL '                                                 
         L     R5,ATOTS                                                         
         BAS   RE,PTOTS                                                         
         GOTO1 PRNT                                                             
*                                                                               
         MVC   PLEL,=C'SP'                                                      
BP42     DS    0H                                                               
         MVC   PLMOS,=C'TOTAL '                                                 
         BAS   RE,PTOTS                                                         
         GOTO1 PRNT                                                             
         GOTO1 PRNT                                                             
*                                                                               
BP60     DS    0H                                                               
         L     R1,SAVR1                                                         
         MVC   0(24,R1),PARMS      RESTORE PARAM LIST                           
         XIT1                                                                   
         SPACE 3                                                                
PTOTS    DS    0H                                                               
         LA    R7,6                                                             
         LA    R6,PLAMTS                                                        
*                                                                               
*                                                                               
PT04     DS    0H                                                               
         EDIT  (P6,0(R5)),(15,0(R6)),2,FLOAT=-                                  
         LA    R5,6(R5)                                                         
         LA    R6,15(R6)                                                        
         BCT   R7,PT04                                                          
         BR    RE                                                               
         SPACE 3                                                                
RTOTS    DS    0H                                                               
         L     R1,ATOTS                                                         
         CLI   BKELEM,X'22'                                                     
         BE    *+8                                                              
         LA    R1,36(R1)                                                        
*                                                                               
         LA    R0,6                                                             
         LA    R5,BKOGRS                                                        
RT04     DS    0H                                                               
         AP    0(6,R1),0(6,R5)                                                  
         LA    R1,6(R1)                                                         
         LA    R5,6(R5)                                                         
         BCT   R0,RT04                                                          
         BR    RE                                                               
         SPACE 3                                                                
         LTORG                                                                  
         SPACE 3                                                                
BPWK     DSECT                                                                  
PARMS    DS    0CL24                                                            
AREC     DS    A                                                                
         ORG   AREC                                                             
SYS      DS    XL1                                                              
         DS    XL3                                                              
APL      DS    A                                                                
ATOTS    DS    A                                                                
DATCON   DS    V                                                                
PRNT     DS    V                                                                
         DS    F                                                                
SAVR1    DS    F                                                                
*                                                                               
DMCB     DS    6F                                                               
DUB      DS    D                                                                
WORK     DS    XL64                                                             
         SPACE 3                                                                
PLD      DSECT                                                                  
PL       DS    0CL110                                                           
PLEL     DS    CL2                                                              
         DS    CL1                                                              
PLMOS    DS    CL6                                                              
         DS    CL1                                                              
PLSTRT   DS    CL5                                                              
         DS    CL1                                                              
PLTYPE   DS    CL1                                                              
         DS    CL3                                                              
PLAMTS   DS    6CL15                                                            
         SPACE 3                                                                
*                                                                               
       ++INCLUDE DDBKELEM                                                       
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'007DDBUCKPRT 05/01/02'                                      
         END                                                                    
