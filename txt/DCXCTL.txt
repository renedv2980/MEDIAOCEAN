Passing control to another FACPAK application program                           
-----------------------------------------------------                           
A FACPAK application can pass control to another application by creating        
a transfer control global element. This is put to the global pool by a          
call to GLOBBER before exiting back to FAMONITOR. The DSECT for the             
XCTL element is given by the PAN book DDGLVXCTLD (shown below).                 
                                                                                
The caller has to identify its own SYS/PRG, the callee SYS/PRG, and some        
info on the session to use. If the callee does not use any tempest or           
tempstr pages and can return a reply with a single call then it can run         
in the same session as the caller. This does not apply to most FACPAK           
applications and so the callee probably needs to run in a seperate              
session. This is done by setting on GLV1SEPS flag in GLVXFLG1.                  
                                                                                
The caller can also specify which session the callee is to run in. If           
nothing is specified FACPAK will chose a session to run in, and if there        
are no unused sessions it will use an active session and thus will              
destroy it. It will tend to use a non recently used session.                    
                                                                                
When FAMONITOR spots a new XCTL global element which is passing control         
to another program it sets on the ignore flag GLV1IGN (to show that it          
has been processed), its sets on GLV1SIDR and puts the session id of            
the caller in GLVXSESR. FAMONITOR then sets up a special call to the            
connect program which will set up a connection to the callee program.           
                                                                                
Sample code to call a program in a seperate session shown below.                
                                                                                
CALL     XC    XCTL,XCTL           BUILD XCTL ELEMENT                           
         LA    R2,XCTL                                                          
         USING GLVXCTLD,R2                                                      
         MVI   GLVXCODE,4          ELEMENT CODE/LENGTH                          
         MVI   GLVXLEN,24                                                       
         MVC   GLVXFRSY,MYSYS      CALLER SYSTEM PROGRAM                        
         MVC   GLVXFRPR,MYPRG                                                   
         MVC   GLVXTOSY,HISSYS     CALLEE SYSTEM/PROGMAM                        
         MVC   GLVXTOPR,HISPRG                                                  
         MVI   GLVXFLG1,GLV1SEPS   CALLEE RUNS IN SEPERATE SESSION              
         GOTO1 CGLOBBER,DMCB,=C'PUTD',XCTL+2,22,04                              
         ...                                                                    
XCTL     DS    XL24                XCTL ELEMENT                                 
         ...                                                                    
*      ++INCLUDE DDGLVXCTLD                                                     
         ...                                                                    
GLVXCTLD       DSECT               TRANSFER CONTROL VARIABLE                    
GLVXCODE DS    XL1                 ELEMENT CODE                                 
GLVXCODQ EQU   04                                                               
GLVXLEN  DS    XL1                 ELEMENT LENGTH                               
GLVXLENQ EQU   24                                                               
*                                                                               
GLVXFRSY DS    CL3                 FROM SYSTEM                                  
GLVXFRPR DS    CL3                 FROM PROGRAM                                 
GLVXTOSY DS    CL3                 TO SYSTEM                                    
GLVXTOPR DS    CL3                 TO PROGRAM                                   
*                                                                               
GLVXFLG1 DS    XL1                 FLAG BYTE 1                                  
GLV1RETN EQU   X'40'               THIS IS A RETURN CALL                        
GLV1SEPD EQU   X'20'               SET DIALOGUE MODE IF SEP SESSION             
GLV1SEPS EQU   X'10'               USE SEPERATE SESSION FOR CALLEE              
GLV1RETG EQU   X'08'               RETURN GLOBALS ON RETURN CALL                
GLV1SIDR EQU   X'04'               SESSION ID OF CALLER SET IN GLVXSESR         
GLV1SIDE EQU   X'02'               SESSION ID OF CALLEE SET IN GLVXSESE         
GLV1IGN  EQU   X'01'               FLAG FOR MONITOR TO IGNORE ELEMENT           
*                                                                               
GLVXSESR DS    XL1                 SESSION ID OF CALLER                         
GLVXSESE DS    XL1                 SESSION ID OF CALLEE                         
GLVSRSRV EQU   X'FF'               RESERVED SESSION                             
GLVSTRAN EQU   X'FE'               TRANSFER SESSION                             
*                                                                               
         DS    XL7                 SPARE                                        
                                                                                
Receiving control from another FACPAK application program                       
---------------------------------------------------------                       
Any FACPAK application that can receive control from another FACPAK             
application must find out how it was called. This is done by calling            
GLOBBER to see if there is an XCTL element, and if so the application           
should save the XCTL element, and delete it from the globber pool.              
Once we have saved the input XCTL element we know who called us and             
what session the caller is running in. This info will be required to            
return back to the caller. See the later section "applications that             
can call and be called" for more details.                                       
                                                                                
WHY      XC    XCTLIN,XCTLIN       SET NOT CALLED BY XCTL                       
         GOTO1 CGLOBBER,DMCB,=C'GETD',XCTL+2,22,04                              
         TM    DMCB+8,X'10'                                                     
         BO    WHYX                                                             
         MVC   XCTLIN,XCTL         SAVE COPY OF INPUT XCTL ELEMENT              
         GOTO1 CGLOBBER,DMCB,=C'DELE' WE MUST DELETE THE XCTL ELEMENT           
WHYX     EQU   *                                                                
         ...                                                                    
XCTL     DS    XL24                XCTL ELEMENT                                 
XCTLIN   DS    XL24                XCTL ELEMENT INPUT SAVED                     
                                                                                
Returning control to another FACPAK application program                         
---------------------------------------------------------                       
One a callee program has done what it was asked to do and has called            
GLOBBER to return the answers into the global pool, it must also                
create an XCTL element to return back to the caller before it exits             
back to FAMONITOR.                                                              
                                                                                
RETURN   XC    XCTL,XCTL           BUILD RETURN                                 
         LA    R2,XCTL                                                          
         USING GLVXCTLD,R2                                                      
         MVI   GLVXCODE,4                                                       
         MVI   GLVXLEN,24                                                       
         MVC   GLVXFRSY,MYSYS                                                   
         MVC   GLVXFRPR,MYPRG                                                   
         MVC   GLVXTOSY,XCTLIN+GLVXFRSY-GLVXCTLD                                
         MVC   GLVXTOPR,XCTLIN+GLVXFRPR-GLVXCTLD                                
         OI    GLVXFLG1,GLV1RETN   SET RETURN CALL                              
         OI    GLVXFLG1,GLV1RETG   SET RETURN GLOBALS                           
         TM    XCTLIN+GLVXFLG1-GLVXCTLD,GLV1SEPS                                
         BZ    *+8                                                              
         OI    GLVXFLG1,GLV1SEPS   SET SEPERATE SESSION                         
         GOTO1 CGLOBBER,DMCB,=C'PUTD',XCTL+2,22,04                              
         ...                                                                    
XCTL     DS    XL24                XCTL ELEMENT                                 
XCTLIN   DS    XL24                XCTL ELEMENT INPUT TO ME                     
                                                                                
Applications that can call and be called                                        
----------------------------------------                                        
A FACPAK application that can be called and that can also call other            
FACPAK applications has to examine the XCTL input element to see if its         
a return call from the callee or a call from a caller. It is necessary          
that the original XCTL from a caller be saved in TWA save area so that          
control can be returned properly.                                               
                                                                                
Suppose PrgA can call PrgB who returns back to PrgA. Also suppose that          
PrgB can call PrgA who returns back to PrgB. In this case PrgA will             
see XCTL elements both showing the from sys/prg to be PrgA. However             
if it is the return call from PrgB the return flag GLV1RETN will be on.         
So PrgA can distinguish between the two reasons for being called.               
                                                                                
If PrgA calls PrgB which then calls PrgC and C returns to B and then B          
returns to A. In this case B can be invoked by a call from A and also by        
a return call from C. B must save (in the TWA save area) A's XCTL               
element. In order that B can return to A we must add some extra code            
to the RETURN code shown above. We must set the session Id of the caller        
(A) as well as the other standard return info. We do this by setting the        
flag that says we are setting the caller's session id and by moving the         
saved caller's session id to GLVXSESR.                                          
                                                                                
         ...                                                                    
         OI    GLVXFLG1,GLV1SIDR   SET CALLERS SESSION ID SET                   
         MVC   GLVXSESR,XCTLIN+GLVXSESR-GLVXCTLD                                
         ...                                                                    
                                                                                
Test programs showing XCTL in action                                            
------------------------------------                                            
There is a program XCTL in the control system (CTXCT00/FF) and a twin           
program XCTL in the person system (PEXCT00/FF) that can call each other         
in the various modes available. You can test the various actions by             
connecting to one of the programs and inputting an Action and Option            
to cause the other program to be called.                                        
Action                                                                          
------                                                                          
SAME   - Pass control to partner using same session (can also use CALL).        
SEPS   - Pass control to partner creating a seperate session for partner        
RETN   - Returns control to caller. Only valid if program was called            
         with SEPS/EXIT. Cant return if called in same session as the           
         caller no longer exists. Use SAME/EXIT to reinvoke caller.             
TEST   - Just does simple return display - used for normal input                
                                                                                
Option                                                                          
------                                                                          
TIME   - Special text string that is passed as a global to partner.             
         Partner program gets time and passes it back to caller in a            
         Global. This tests the ability to pass back a new global.              
EXIT   - Special text string that is passed as a global to partner.             
         Partner program takes over transaction and doesnt return.              
XXX..  - Any other text string is passed as a global to partner.                
         Partner program display it and returns to caller without               
         attempting to pass anything back.                                      
                                                                                
FAMONITOR - Logging of XCTL globals                                             
-----------------------------------                                             
Special code is present in FAMONITOR which logs in a table MLOG special         
monitor events. 1st four bytes of table has C'MLOG' and the next 4 bytes        
point to the next available 16 bytre entry. I patch this table before           
each test so that the a(next entry) is the next 16 byte modulo and set          
earlier test data to zeros. The entries will then diplay all nicely             
lined up via =DISP. Table will scroll to start if full.                         
                                                                                
XL1  Monitor event number (hex code)                                            
XL1  TOVSYS                                                                     
XL1  TPRG                                                                       
XL13 XCTL global - last byte is XCTL flag byte                                  
                                                                                
Monitor event hex codes as follows for when an XCTL global is looked for        
                                                                                
01 Found on input  - Monitor passes control to program as normal                
02 Found on input  - Monitor loads a FF scr for TPRG before calling prg         
                                                                                
11 Found on output - Normal exit via write TEMPSTR                              
12 Found on output - Dont write TEMPSTR exit                                    
                                                                                
21 PUTTWA routine  - Write TEMPSTR                                              
22 PUTTWA routine  - Dont write TEMPSTR exit                                    
