<T>Print queue - A guide for systems                                            
Introduction                                                                    
____________                                                                    
This document is meant to serve as a guide for program conversion to use        
the new print queue. As the print queue has never been documented it is         
also meant to serve as a guide to using the print queue. The main change        
that could affect existing programs is the requirement for a larger             
buffer. The old code required a 4000 chr buffer and the new now requires        
a 14336 chr buffer. If the online program uses the TIA as a print queue         
buffer there is no problem as the TIA is 14336 chrs long. If an offline         
program provides its print queue buffer via *INCLUDE DMPQBUFF there is          
also no problem as this module now defines the larger buffer required.          
                                                                                
                                                                                
Relo Modules                                                                    
____________                                                                    
A typical program or link edit deck will contain the following modules.         
There is no need to change these, as after the conversion the new code          
will be copied into the old modules. (Including the 4k to 6k shift).            
                                                                                
*INCLUDE DMPRTQUE                  The basic code module     V(DMPRTQUE)        
*INCLUDE DMPQOPEN                  The open and init module  V(PQOPEN)          
*INCLUDE DMPQBUFF                  The 4k buffer             V(PQBUFF)          
                                                                                
A new program or a converted program should use the new relo modules.           
There is no need to include an open module as the new basic module              
contains the open and initialise code ie. the call to =V(PQOPEN) is no          
longer needed and MUST be removed.The only program that now needs to            
call PQOPEN is FACPAK which has a new specialised open module DMPRTQO.          
The typical offline program or link edit deck should contain:-                  
                                                                                
*INCLUDE DMPRTQ                    The basic/init module     V(DMPRTQUE)        
*INCLUDE DMPRTQB                   The 6K buffer             V(PQBUFF)          
                                                                                
How to add a report into the Print Queue                                        
________________________________________                                        
Printer infomation is assumed to be created in a 133chr print line              
which has been pre-filled with spaces.The first chr of the line is              
assumed to be a valid IBM carraige control chr (cc chr). Print lines            
are added to the print queue by use of the c'DMPRINT' datamgr command           
with the c'PRTQUE' datamgr file name. To terminate the report the               
convention is to set the cc chr in the first print line to an X'FF'.            
                                                                                
Before any print lines can be added some basic information to define            
the report is required ie the report must be opened. The report is              
opened by setting the whole of the print line to binary zeros, moving           
in some required values, and by issuing the first DMPRINT/PRTQUE.               
Coding examples of the basic write loop and terminate logic is given            
later. There is no change in the old and new print queue for these              
functions, but the new coding example gives better error handling.              
                                                                                
The print queue values are defined by a PQPLD DSECT given by the Pan            
book DMPRTQD. All values must be set to binary zeros and the field              
QLEXTRA at +26 MUST be set to x'FF' to indicate a new call. Examples            
of the code involved in both the new and the old print queue are given          
later. One conversion problem must be noted. As the old open data only          
consisted of a few fields then some programs provided a short print line        
to the open call. The new print queue defines more attributes of a              
report and requires a full 133 chr line into which some default values          
will be moved. The whole 133 chr line must be XC'ed so that non defined         
fields will not be processed. The fields are defined below. The fields          
that are required are defined below. If any field values are missing            
or are invalid the DMPRINT/PRTQUE call will return X'41' in DMCB+8 to           
indicate a format error.                                                        
                                                                                
QLSRCID  The user id number of the origin data. If this field is zero           
         for an online program the connect user id in the UTL will be           
         moved into this field. An offline program must set this field.         
                                                                                
QLSUBID  Each report for a given user id is sub defined by a three chr          
         subid. For an individual online report this is typically the           
         creator's initials. For a formal offline report this is usually        
         the system and request eg A55. The value of this field cannot          
         be set to ALL, and any special chrs including spaces will be           
         changed to dots eg c'A B' will be set to C'A.B'.                       
                                                                                
QLCLASS  A report can be assigned a class value. This value can be C'A'         
         thru c'Z' or C'0' thru C'9'. Any other value will be set to            
         the default undefined class value of C'*'.                             
                                                                                
QLSTAT   A report will be created with an ACTV status. If the HOLD              
         status bit is set on (X'40') the report will be created with an        
         initial status of HOLD. If the KEEP bit is set on (X'08') the          
         report will be assigned the KEEP sub status.                           
                                                                                
QLLINET  This field defines the structure of the print line data that           
         will be passed in the print line. The value of the bits are            
         defined by equates in the DSECT. A normal print line should            
         be defined by X'C0' ie with cc chr and fixed length (given by          
         the QLLINEW field) NB if both QLLINET and QLLINEW are set to           
         binary zero they are set to X'C0' and 132 ie the system default        
         print line.                                                            
                                                                                
QLLINEW  If the X'40' fixed length data bit is set in QLLINET the fixed         
         data length must be set in this field. For a standard print            
         line this should be set to 132. A card deck could be stored            
         in the print queue by setting QLLINET=X'40' and QLLINEW=80.            
                                                                                
QLLPP    The number of lines per page can be set for documentation              
         purposes only. The value of this field has no real effect.             
         If set to zero a default value of 66 or 88 is moved in.                
                                                                                
QLRETNL  This field must be set to the LIVE retain hours. This defines          
         the number of hours the report will remain on the print queue          
         if its status remains live (ie it is not printed or sent). If          
         this field is set to X'FFFF' the report will be assigned a             
         permanent live retain date.                                            
                                                                                
QLRETND  This field must be set to the DEAD retain hours. This defines          
         the number of hours the report will remain on the print queue          
         after its status changes from live to dead (ie when it is              
         printed or sent this value is used to compute the new retain           
         date). If this value is set to X'FFFF' the report will be              
         permanently retained after printing/sending.                           
                                                                                
QLPSWD   This field contains password data. The format of this field            
         is to be defined. If the value in this field is non zero the           
         report index entry will be flagged as password protected and           
         some password data must be available for printing or online            
         viewing of the report data.                                            
                                                                                
QLDESC   This is the origional 11 chr report description field. In some         
         online generated reports it contains a reference number (eg a          
         REP system contract number). In offline programs it has been           
         redefined to formally define paper type and num of copies.             
                                                                                
QLCOMNT  A comment or description of up to 16 chrs can be assigned to           
         a report at open time by moving that info to this field.               
                                                                                
Information passed back to caller                                               
_________________________________                                               
                                                                                
The process of creating a report on the prtque requires the use of a            
buffer. This buffer must be at least 14336 bytes long and is used to            
hold the print lines for writing to disk. The same buffer that was              
passed at open must be used for all subsequent DMPRINT's including              
the final close. This is because the end of the buffer contains save            
storage used by the print queue module to control the sequence of events        
required to create a report. If a program needs to create two reports           
at the same time two 6k buffers must be provided - one for each report.         
                                                                                
Each report in the print queue is assigned a unique sequence number for         
a given user id. This number runs from 1 thru 9999 and is assigned when         
the report is opened. This number is returned back in the print line            
passed at report open at the field QLREPRNO at +19(2). Also returned in         
the open print line is the disk address of the first control interval           
in the field QLREPRCI at +23(2). If the  current user id has a sequence         
number of 9999 the subsequent opens will return X'80' end of file for           
attempts to add more reports under that user id. The end of file value          
will also be returned at open when there is no more space in the print          
queue ie when no existing reports have passed their expiration time.            
Also note the certain default fields will have been moved in and that           
the QLSUBID field will have been changed if it contains invalid chrs.           
                                                                                
Normal print lines are passed by setting the data in the print line and         
the IBM cc chr in the first byte. These print lines are buffered into           
the passed 6k buffer and written to disk when full. If an X'40' is              
returned at DMCB+8 a disk write error has occurred. If the report needs         
more space on disk a new control interval is searched for. If the print         
queue is full then an X'80' eof error will be passed back at DMCB+8.            
                                                                                
The report is closed by setting an X'FF' in the first byte of the print         
line. The close process consists of turning all control intervals and           
index entrys from temporary to live status and by linking together in a         
forward chain all control intervals aquired by report creation. A normal        
completion is returned in the normal way. If an X'40' is returned a disk        
error was encountered during the close process. If an X'41' is returned         
at DMCB+8 a format error occurred during the close ie the control               
interval linkage could not be performed due to some corruption in the           
file.                                                                           
                                                                                
After a normal close the 6k buffer that has been used during the report         
creation contains the first record of the first control interval. This          
record contains the report header which holds all data avialable for            
the report. The report header is defined by PQRECD DSECT given by the           
Pan book DMPRTQD. This dsect provides infomation such as the report             
sequence number (also passed back at open time) and key data as well as         
the number of lines and pages in the report. The coding examples show           
how this info can be extracted from the buffer after a close.                   
                                                                                
Print Queue - Typical old coding                                                
________________________________                                                
                                                                                
OFFLINE  CSECT                     CODE FOUND IN A TYPICAL OFFLINE PROG         
         ...                                                                    
         GOTO1  =V(PQOPEN),DMCB    OPEN AND INIT PRTQUE FILE                    
         BE     *+6                                                             
         DC     H'0'               DIE IF CANT OPEN PRTQUE                      
         ...                                                                    
PQOPEN   LA     R5,OPENINFO        POINT TO PRINT OPEN DATA                     
         USING  PQPLD,R5                                                        
         XC     OPENINFO,OPENINFO  MUST CLEAR ALL FIELDS                        
*                                                                               
         MVC    PLDESC,...         SET REPORT NAME DESCRIPTION                  
         MVC    PLSUBID,...        SET REPORT 3CHR ID (INITIALS)                
         MVI    PLSTAT,X'..'       SET REPORT INIT STAT HOLD/KEEP               
         MVC    PLUSER,...         SET REPORT USER ID                           
         MVC    PLCLASS,...        SET REPORT CLASS                             
*                                                                               
         GOTO1  DATAMGR,DMCB,=C'DMPRINT',=C'PRTQUE',0,OPENINFO,ATIA             
         CLI    DMCB+8,0                                                        
         BNE    EOFERR             ERROR ON OPEN - PROBABLY EOF                 
         MVC    SAVENUM,PLREPNO    SAVE RETURNED REPORT NUMBER                  
*                                                                               
PQFIRST  MVI    P,X'89'            SET SKIP TO TOP OF PAGE                      
         MVC    P+1(132),SPACES                                                 
         GOTO1  DATAMGR,DMCB,=C'DMPRINT',=C'PRTQUE',0,P,ATIA                    
         CLI    DMCB+8,0                                                        
         BNE    DSKERR             ERROR ON FIRST LINE - PROBABLY DISK          
*                                                                               
PQLOOP   MVI    P,X'09'            SET SINGLE SPACE WRITE                       
         MVC    P+1(132),SPACES                                                 
         MVC    P+1(..),......     SET DATA IN PRINT LINE                       
         GOTO1  DATAMGR,DMCB,=C'DMPRINT',=C'PRTQUE',0,P,ATIA                    
         CLI    DMCB+8,0                                                        
         BNE    EOFERR             ERROR ON LINE - PROBABLY EOF                 
         ...                                                                    
         BE     PQLAST             TEST END OF REPORT                           
         ...                                                                    
         B      PQLOOP             BACK FOR NEXT PRINT LINE                     
*                                                                               
PQLAST   MVI    P,X'FF'            SET END OF REPORT                            
         GOTO1  DATAMGR,DMCB,=C'DMPRINT',=C'PRTQUE',0,P,ATIA                    
         CLI    DMCB+8,0                                                        
         BNE    DSKERR             ERROR ON CLOSE - PROBABLY DISK               
*                                                                               
         L      R5,ATIA            POINT TO 4K RECORD BUFFER                    
         USING  PQRECD,R5                                                       
         MVC    SVLINES,PQLINES    SAVE NUMBER OF LINES (XL2)                   
         MVC    SVPAGES,PQPAGES    SAVE NUMBER OF PAGES (XL2)                   
         ...                                                                    
*                                                                               
OPENINFO DS     CL32               PRTQUE REPORT OPEN INFO                      
P        DS     CL133              PRTQUE NORMAL PRINT LINE                     
         ...                                                                    
       INCLUDE  FAPQPL             PQPLD DSECT FOR PRINT LINE OPEN INFO         
       INCLUDE  FAPQRECD           PQRECD DSECT FOR BLOCK REPORT HEADER         
         ...                                                                    
         END                                                                    
                                                                                
Print Queue - Recomended new coding                                             
___________________________________                                             
                                                                                
PQOPEN   LA     R5,P               POINT TO FULL PRINT LINE FOR OPEN            
         USING  PQPLD,R5                                                        
         XC     P,P                MUST CLEAR ALL FIELDS IN WHOLE LINE          
         MVI    QLEXTRA,X'FF'      SET NEW CALL FLAG FOR EXTRA INFO             
*                                                                               
         MVC    QLSRCID,...        SET REPORT USER ID                           
         MVC    QLSUBID,...        SET REPORT 3CHR ID (INITIALS)                
         MVC    QLCLASS,...        SET REPORT CLASS                             
         MVI    QLSTAT,X'..'       SET REPORT INIT STAT HOLD/KEEP               
         MVI    QLLINET,X'C0'      SET LINE HAS CC CHR & FIXED LENGTH           
         MVI    QLLINEW,132        SET 132 CHR FIXED LENGTH PRINT LINE          
         MVC    QLRETNL,...        SET LIVE RETENTION HOURS                     
         MVC    QLRETND,...        SET DEAD RETENTION HOURS                     
         MVC    QLPSWD,...         SET PASSWORD INFO (TO BE DEFINED)            
         MVC    QLDESC,...         SET REPORT NAME DESCRIPTION                  
         MVC    QLCOMNT,...        SET REPORT 16CHR COMMENT INFO                
*                                                                               
         GOTO1  DATAMGR,DMCB,=C'DMPRINT',C'PRTQUE',0,P,ATIA                     
         CLI    DMCB+8,0                                                        
         BE     PQOPENOK                                                        
         TM     DMCB+8,X'80'       EOF - CANT ADD REPORT FOR USER ID            
         BO     EOFERR                                                          
         TM     DMCB+8,X'41'       FMT - INVALID OPEN INFO PASSED               
         BO     FMTERR                                                          
         B      DSKERR             DSK - DISK ERROR ON OPEN                     
*                                                                               
PQOPENOK MVC    SAVEID,QLSUBID     SAVE RETURNED REPORT SUB ID                  
         MVC    SAVENO,QLREPRNO    SAVE RETURNED REPORT SEQ NUM                 
         MVC    SAVEDA,QLREPRCI    SAVE RETURNED DISK ADDR                      
*                                                                               
PQFIRST  MVI    P,X'89'            SET SKIP TO TOP OF PAGE                      
         MVC    P+1(132),SPACES                                                 
         GOTO1  DATAMGR,DMCB,=C'DMPRINT',=C'PRTQUE',0,P,ATIA                    
         CLI    DMCB+8,0                                                        
         BNE    DSKERR             ERROR ON FIRST LINE - PROBABLY DISK          
*                                                                               
PQLOOP   MVI    P,X'09'            SET SINGLE SPACE WRITE                       
         MVC    P+1(132),SPACES                                                 
         MVC    P+1(..),...        SET DATA IN PRINT LINE                       
         GOTO1  DATAMGR,DMCB,=C'DMPRINT',=C'PRTQUE',0,P,ATIA                    
         CLI    DMCB+8,0                                                        
         BE     PQLOOP1                                                         
         TM     DMCB+8,X'80'       EOF - REPORT TOO BIG OR PRTQUE FULL          
         BO     EOFERR                                                          
         B      DSKERR             DSK - DISK ERROR ON WRITE                    
PQLOOP1  ...                                                                    
         BE     PQLAST             TEST END OF REPORT                           
         ...                                                                    
         B      PQLOOP             BACK FOR NEXT PRINT LINE                     
*                                                                               
PQLAST   MVI    P,X'FF'            SET END OF REPORT                            
         GOTO1  DATAMGR,DMCB,=C'DMPRINT',=C'PRTQUE',0,P,ATIA                    
         CLI    DMCB+8,0                                                        
         BE     PQLAST1                                                         
         TM     DMCB+8,X'41'       FMT - PROBLEM WITH PRTQUE STRUCTURE          
         BO     FMTERR                                                          
         B      DSKERR             DSK - DISK ERROR ON WRITE                    
*                                                                               
PQLAST1  L      R5,ATIA            POINT TO 6K RECORD BUFFER                    
         USING  PQRECD,R5                                                       
         MVC    SVLINES,PQLINES+1  SAVE NUMBER OF LINES (XL3)                   
         MVC    SVPAGES,PQPAGES    SAVE NUMBER OF PAGES (XL2)                   
         ...                                                                    
*                                                                               
P        DS     CL133              PRTQUE PRINT LINE FOR OPEN/NORMAL            
         ...                                                                    
       INCLUDE  DMPRTQL            PQPLD DSECT FOR PRINT LINE OPEN INFO         
       INCLUDE  DMPRTQD            PQRECD DSECT FOR BLOCK REPORT HEADER         
         ...                                                                    
         END                                                                    
                                                                                
FAPQPL Pan book - Old print line dsect                                          
______________________________________                                          
                                                                                
PQPLD    DSECT                     ***** PRINT QUEUE PRINT LINE *****           
PLCC     DS    XL1       +00       CONTROL CHR X'00'=OPEN,X'FF'=CLOSE           
PLDESC   DS    CL11      +01       REPORT DESCRIPTION                           
PLSUBID  DS    CL3       +12       REPORT SUB ID                                
PLSTAT   DS    XL1       +15       REPORT INITIAL STATUS                        
PLLPP    DS    XL1       +16       LINES PER PAGE                               
PLUSER   DS    XL2       +17       USER ID NUMBER (OFFLINE ONLY)                
PLREPNO  DS    XL2       +19       REPORT SEQUENCE NUMBER                       
PLREPNOX DS    XL2       +21       REPORT SEQUENCE NUMBER MAXIMUM               
PLSTRCI  DS    XL2       +23       START CONTROL INTERVAL TRACK                 
PLCLASS  DS    CL1       +25       REPORT CLASS                                 
                                                                                
FAPQREC Pan book - Old record header dsect                                      
__________________________________________                                      
                                                                                
PQRECD   DSECT                     ***** PRINT QUEUE RECORD *****               
*                                                                               
* FIRST PART OF RECORD IS INDEX ENTRY                                           
*                                                                               
PQINDEX  DS    0CL14                                                            
PQKEY    DS    0CL8                                                             
PQSRCID  DS    XL2                 SOURCE ID NUMBER                             
PQSUBID  DS    CL3                 SOURCE SUB-ID                                
PQREPNO  DS    XL2                 REPORT SEQUENCE NUMBER                       
PQCLASS  DS    CL1                 REPORT CLASS                                 
PQSEQ    DS    XL1                 SEQUENCE NUMBER (0=ONLY ONE CI)              
PQSTAT   DS    XL1                 REPORT STATUS                                
PQAGED   DS    XL2                 AGE DATE                                     
PQAGET   DS    XL1                 AGE TIME (PACKED HOURS)                      
PQAGES   DS    XL1                 AGE SIZE (NUM OF CI'S)                       
*                                                                               
STACTV   EQU   X'80'               CREATION COMPLETE - ACTIVE                   
STHOLD   EQU   X'40'               CREATION COMPLETE - HOLD                     
STPRTD   EQU   X'20'               PRINTING COMPLETE                            
STCREAT  EQU   X'10'               CREATION IN PROCESS                          
STPNTG   EQU   X'10'               PRINTING IN PROCESS                          
STKEEP   EQU   X'08'               KEEP REPORT AFTER PRINTING                   
STSENT   EQU   X'02'               SENT TO REMOTE MINI                          
STLIVE   EQU   X'C0'               LIVE - ACTIVE OR HOLD                        
STDEAD   EQU   X'22'               DEAD - PRINTED OR SENT                       
STPURGE  EQU   X'00'               PURGED                                       
*                                                                               
* SECOND PART OF RECORD IS REPORT ATTRIBUTES                                    
*                                                                               
PQLINES  DS    H                   NUMBER OF LINES                              
PQPAGES  DS    H                   NUMBER OF PAGES                              
PQCPL    DS    XL1                 AVERAGE NUMBER OF CHRS PER LINE              
PQLPP    DS    XL1                 LINES PER PAGE                               
PQHIPAGE DS    H                   HIGHEST PAGE NUMBER IN THIS BLOCK            
PQCINEXT DS    H                   TTTT OF NEXT CI                              
*                                                                               
PQDATEC  DS    XL3                 DATE CREATED - P'YMD'                        
PQTIMEC  DS    XL2                 TIME CREATED - P'HM'                         
PQDATEP  DS    XL3                 DATE PRINTED - P'YMD'                        
PQTIMEP  DS    XL2                 TIME PRINTED - P'HM'                         
*                                                                               
PQPRLOC  DS    XL2                 LOCATATION ID WHERE PRINTED                  
PQPRNUM  DS    XL1                 PRINTER NUMBER WHERE PRINTED                 
PQDESC   DS    CL11                REPORT DESCRIPTION                           
*                                                                               
* THIRD PART OF RECORD IS PRINT LINE ELEMENTS                                   
*                                                                               
PQDATA   DS    0C                  XL1=LEN,XL1=CCC,XLN=DATA                     
                                                                                
DMPRTQL Pan book - new print line dsect                                         
_______________________________________                                         
                                                                                
PQPLD    DSECT                     ***** PRINT QUEUE PRINT LINE *****           
*                                                                               
PLOLD    DS    0CL28 ============= 0TH PART OF LINE IS OLD OPEN DATA            
PLCC     DS    XL1       +00       CONTROL CHR X'00'=OPEN,X'FF'=CLOSE           
PLDESC   DS    CL11      +01       REPORT DESCRIPTION                           
PLSUBID  DS    CL3       +12       REPORT SUB ID                                
PLSTAT   DS    XL1       +15       REPORT INITIAL STATUS                        
PLLPP    DS    XL1       +16       LINES PER PAGE                               
PLUSER   DS    XL2       +17       USER ID NUMBER (OFFLINE ONLY)                
PLREPNO  DS    XL2       +19     * REPORT SEQUENCE NUMBER                       
PLREPNOX DS    XL2       +21       REPORT SEQUENCE NUMBER MAXIMUM               
PLSTRCI  DS    XL2       +23     * START CONTROL INTERVAL TRACK                 
PLCLASS  DS    CL1       +25       REPORT CLASS                                 
*                                                                               
         ORG   PLOLD                                                            
QLSOFLAB DS    CL10      +00       **SOFSOF** START OF REPORT LABEL             
         DS    CL9                 N/D                                          
QLREPRNO DS    XL2       +19       RETURNED REPORT NUMBER                       
         DS    XL2                 N/D                                          
QLREPRCI DS    XL2       +23       RETURNED CONTROL INTERVAL DISK ADDR          
         DS    XL1                 N/D                                          
*                                                                               
QLEXTRA  DS    XL1       +26       MUST SET TO X'FF' FOR EXTRA VALUES           
QLFLAG   DS    XL1       +27       FLAGS FOR PASSING ATTRIBUTES                 
*                                                                               
QLINDEX  DS    0CL20 ============= 1ST PART OF REC IS INDEX                     
QLKEY    DS    0CL7                                                             
QLSRCID  DS    XL2                 SOURCE ID NUMBER                             
QLSUBID  DS    CL3                 SOURCE SUB-ID                                
QLREPNO  DS    XL2                 REPORT SEQUENCE NUMBER                       
QLCLASS  DS    CL1                 REPORT CLASS                                 
QLTYPE   DS    CL1                 REPORT TYPE                                  
QLATTB   DS    CL1                 REPORT ATTRIBUTES                            
QLSTAT   DS    XL1                 REPORT STATUS                                
         DS    XL1                                                              
QLAGES   DS    XL1                 REPORT NUM OF CI'S                           
QLAGELD  DS    XL2                 REPORT AGE LIVE                              
QLAGEDD  DS    XL2                 REPORT AGE DEAD                              
QLAGERD  DS    XL2                 REPORT AGE RETAIN                            
         DS    XL1                                                              
*                                                                               
QLBATTR  DS    0CL16 ============= 2ND PART OF REC IS BLOCK ATTRIBUTES          
         DS    XL2                                                              
QLLINES  DS    XL3                 NUMBER OF LINES                              
QLPAGES  DS    XL2                 NUMBER OF PAGES                              
         DS    XL5                                                              
QLLINET  DS    XL1                 LINE TYPE                                    
QLLINEW  DS    XL1                 LINE WIDTH IN CHARACTERS                     
QLLPP    DS    XL1                 LINES PER PAGE                               
         DS    XL1                                                              
*                                                                               
QLLTCC   EQU   X'80'               1ST CHR OF DATA IS CARRIAGE CONTROL          
QLLTFL   EQU   X'40'               LINE HAS FIXED LEN GIVEN BY QLLINEW          
QLLTDC   EQU   X'20'               DATA IS COMPESSABLE                          
QLLTLA   EQU   X'10'               DATA CONTAINS LASER SPECIAL CHRS             
*                                                                               
QLFATTR  DS    0CL64 ============= 3RD PART OF REC IS REPORT ATTR               
         DS    XL2                                                              
QLDATEL  DS    XL2                 DATE CREATED - CMPRSD                        
QLTIMEL  DS    XL2                 TIME CREATED - BINARY                        
QLRETNL  DS    XL2                 RETENTION HOURS FOR LIVE REPORT              
QLDATED  DS    XL2                 DATE PRINTED - CMPRSD                        
QLTIMED  DS    XL2                 TIME PRINTED - BINARY                        
QLRETND  DS    XL2                 RETENTION HOURS FOR DEAD REPORT              
QLPRCNT  DS    XL1                 PRTD/SENT COUNTER                            
QLPRLOC  DS    XL2                 PRTD/SENT LOCATION ID                        
QLPRNUM  DS    XL1                 PRTD/SENT PRINTER NUM IN LOCATION            
QLPRREF  DS    XL2                 PRTD/SENT PRINTER REF ID                     
QLPRLINE DS    CL4                 PRTD/SENT LINE ID                            
QLPRADDR DS    CL2                 PRTD/SENT TERM ID (HEX)                      
         DS    CL7                 N/D                                          
QLPSWD   DS    XL4                 REPORT PASSWORD                              
                                                                                
DMPRTQD Pan book - New record header dsect                                      
__________________________________________                                      
                                                                                
PQRECD   DSECT                     ***** PRINT QUEUE RECORD *****               
*                                                                               
PQINDEX  DS    0CL20 ============= 1ST PART OF REC IS INDEX                     
PQKEY    DS    0CL7                                                             
PQSRCID  DS    XL2                 SOURCE ID NUMBER                             
PQSUBID  DS    CL3                 SOURCE SUB-ID                                
PQREPNO  DS    XL2                 REPORT SEQUENCE NUMBER                       
PQCLASS  DS    CL1                 REPORT CLASS                                 
PQTYPE   DS    CL1                 REPORT TYPE                                  
PQATTB   DS    XL1                 REPORT ATTRIBUTES                            
PQSTAT   DS    XL1                 REPORT STATUS                                
PQSEQ    DS    XL1                 CI SEQ NUMBER    (0=ONLY ONE CI)             
PQAGES   DS    XL1                 AGE SIZE         (NUM OF CI'S)               
PQAGELD  DS    XL2                 AGE DATE LIVE    (BINARY COMPR)              
PQAGEDD  DS    XL2                 AGE DATE DEAD    (BINARY COMPR)              
PQAGERD  DS    XL2                 AGE DATE RETAIN  (BINARY COMPR)              
PQAGERT  DS    XL1                 AGE TIME RETAIN  (BINARY 10MIN)              
*                                                                               
PQATPW   EQU   X'80'               ATTRIBUTE PASSWORD PROTECTED                 
PQATNP   EQU   X'40'               ATTRIBUTE NON PRINTABLE (NO CC)              
*                                                                               
PQSTAC   EQU   X'80'               STATUS ACTIVE                                
PQSTHO   EQU   X'40'               STATUS HOLD                                  
PQSTLIVE EQU   X'C0'               STATUS LIVE - ACTIVE OR HOLD                 
PQSTPR   EQU   X'20'               STATUS PRINTED                               
PQSTSE   EQU   X'10'               STATUS SENT                                  
PQSTDEAD EQU   X'30'               STATUS DEAD - PRINTED OR SENT                
PQSTKE   EQU   X'08'               STATUS KEEP                                  
PQSTTE   EQU   X'01'               STATUS TEMP - CREATION IN PROCESS            
PQSTPG   EQU   X'01'               STATUS TEMP - PRINTING IN PROCESS            
PQSTPU   EQU   X'00'               STATUS PURGED                                
         SPACE 1                                                                
PQBATTR  DS    0CL16 ============= 2ND PART OF REC IS BLOCK ATTRIBUTES          
PQCINEXT DS    XL2                 TTTT OF NEXT CI                              
PQLINES  DS    XL3                 NUMBER OF LINES (LOWEST IN BLOCK)            
PQPAGES  DS    XL2                 NUMBER OF PAGES (LOWEST IN BLOCK)            
PQLINEH  DS    XL3                 HIGHEST LINE NUMBER IN THIS BLOCK            
PQPAGEH  DS    XL2                 HIGHEST PAGE NUMBER IN THIS BLOCK            
PQLINET  DS    XL1                 LINE TYPE                                    
PQLINEW  DS    XL1                 LINE WIDTH IN CHARACTERS                     
PQLPP    DS    XL1                 LINES PER PAGE                               
PQBFLAG  DS    XL1                 BLOCK FLAGS                                  
*                                                                               
PQLTCC   EQU   X'80'               1ST CHR OF DATA IS CARRIAGE CONTROL          
PQLTFL   EQU   X'40'               LINE HAS FIXED LEN GIVEN BY PQLINEW          
PQLTDC   EQU   X'20'               DATA IS COMPESSABLE                          
PQLTLA   EQU   X'10'               DATA CONTAINS LASER SPECIAL CHRS             
PQBFNP   EQU   X'80'               BLOCK CONTAINS NEW PAGE CC CHR               
         SPACE 1                                                                
PQDATA   DS    0C    ============= 3RD PART OF REC IS REPORT ATTR/DATA          
PQFATTR  DS    0CL64                                                            
PQAVCPL  DS    XL2                 AVERAGE NUMBER OF CHRS PER LINE              
*                                                                               
PQDATEL  DS    XL2                 DATE CREATED - CMPRSD                        
PQTIMEL  DS    XL2                 TIME CREATED - BINARY                        
PQRETNL  DS    XL2                 RETENTION HOURS FOR LIVE REPORT              
PQDATED  DS    XL2                 DATE PRINTED - CMPRSD                        
PQTIMED  DS    XL2                 TIME PRINTED - BINARY                        
PQRETND  DS    XL2                 RETENTION HOURS FOR DEAD REPORT              
PQPRCNT  DS    XL1                 PRTD/SENT COUNTER                            
PQPRLOC  DS    XL2                 PRTD/SENT LOCATION ID                        
PQPRNUM  DS    XL1                 PRTD/SENT PRINTER NUM IN LOCATION            
PQPRREF  DS    XL2                 PRTD/SENT PRINTER REF ID                     
PQPRLINE DS    CL4                 PRTD/SENT LINE ID                            
PQPRADDR DS    XL2                 PRTD/SENT TERM ID (HEX)                      
*                                                                               
         DS    CL7                 N/D                                          
PQPSWD   DS    XL4                 REPORT PASSWORD                              
PQDESC   DS    CL11                REPORT DESCRIPTION                           
PQCOMNT  DS    CL16                REPORT COMMENT                               
*                                                                               
PQDATA1  DS    0C                  XL2=LEN (XL1=CCC) XLN=DATA                   
