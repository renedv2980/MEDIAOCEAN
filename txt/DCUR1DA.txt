*          DATA SET DCUR1DA    AT LEVEL 160 AS OF 05/01/02                      
*&&      SET   NOP=N                                                            
*PHASE T6231DA                                                                  
         SPACE 1                                                                
FIL1D    TITLE 'GST RULE RECORD'                                                
         SPACE 2                                                                
FIL1D    CSECT                                                                  
         PRINT NOGEN                                                            
         USING WORKD,R9                                                         
         USING GWORKD,R8                                                        
         NMOD1 0,**FL1D**,R6,RR=RE                                              
         L     R8,AGWORK                                                        
*                                                                               
         USING OVERWRKD,RC                                                      
         L     RC,AOVERWRK                                                      
         ST    RE,BORELO                                                        
         ST    R1,CALLR1                                                        
         MVC   SVPARMS,0(R1)                                                    
*                                                                               
         USING TWUSER,R7           POINT R7 TO SAVED STORAGE                    
         LHI   R7,(TWUSER-TWAD)                                                 
         A     R7,ATWA                                                          
*                                                                               
         SRL   RF,24                                                            
         CLM   RF,1,=AL1(ROUTSN)                                                
         BNH   *+6                 UNKNOWN ROUTINE                              
         DC    H'0'                                                             
         SLL   RF,2                                                             
         B     ROUTS(RF)                                                        
*                                                                               
ROUTS    DS    0XL4                                                             
         B     OBJECT              OBJECT INVOKER                               
         B     INIT                INITIALIZATION CALL                          
*                                                                               
ROUTSN   EQU   (*-ROUTS)/4                                                      
         EJECT                                                                  
***********************************************************************         
* EXITS                                                               *         
***********************************************************************         
         SPACE 1                                                                
EXITH    CLI   *,0                 SET CC HIGH                                  
         B     EXIT                                                             
*                                                                               
EXITL    CLI   *,FF                SET CC LOW                                   
         B     EXIT                                                             
*                                                                               
EXITOK   CR    RB,RB               SET CC EQUAL                                 
         L     R1,CALLR1                                                        
         MVC   0(L'SVPARMS,R1),SVPARMS                                          
*                                                                               
EXIT     XIT1  ,                   EXIT WITH CC SET                             
*                                                                               
EXITNV   MVC   FVMSGNO,=AL2(FVFNOTV)                                            
         B     EXITL               EXIT WITH FIELD NOT VALID SET                
*                                                                               
EXITNO   MVC   FVMSGNO,=AL2(FVFNONE)                                            
         B     EXITL               EXIT WITH FIELD NOT INPUT SET                
*                                                                               
EXITNOTN MVC   FVMSGNO,=AL2(FVFNOTN)                                            
         B     EXITL               EXIT WITH FIELD NOT NUMERIC SET              
*                                                                               
FLTXL    MVI   SVPARMS,DFLTL       EXIT LOW FOR FILTER                          
         B     EXITOK                                                           
*                                                                               
FLTXE    MVI   SVPARMS,DFLTE       EXIT EQUAL FOR FILTER                        
         B     EXITOK                                                           
*                                                                               
FLTXH    MVI   SVPARMS,DFLTH       EXIT HIGH FOR FILTER                         
         B     EXITOK                                                           
*                                                                               
FLTXX    MVI   SVPARMS,DFLTX       EXIT DEFINATELY NOT VALID                    
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* INITIALIZATION                                                      *         
***********************************************************************         
         SPACE 1                                                                
INIT     MVI   VATINDS,0           CLEAR INDICATOR                              
         MVI   FILINDS1,0                                                       
         TM    BCCPYST1,CPYSOROE   OFFICE REQUIRED ON EXPENSES                  
         BZ    *+8                                                              
         OI    VATINDS,VATCOFF     COMPANY IS ON OFFICE                         
         TM    BCCPYST4,CPYSOFF2                                                
         BZ    *+8                                                              
         OI    VATINDS,VATNOFFS    NEW OFFICE SYSTEM                            
*                                                                               
         USING CPYRECD,R2                                                       
         LA    R2,IOKEY                                                         
         MVC   CPYKEY,BCSPACES   READ COMPANY RECORD                            
         MVC   CPYKCPY,CUABIN      CONNECTED ID                                 
         LHI   R1,XOREAD+XOACCDIR+XIO1                                          
         GOTO1 AIO                                                              
         BE    *+6                                                              
         DC    H'0'                                                             
         LHI   R1,XOGET+XOACCMST+XIO1                                           
         GOTO1 AIO                                                              
         BE    *+6                                                              
         DC    H'0'                BAD COMPANY RECORD                           
         GOTO1 AGETEL,BOPARM,('CPYELQ',AIO1),0                                  
         BE    *+6                                                              
         DC    H'0'                NO COMPANY ELEMENT                           
*                                                                               
         USING CPYELD,R2                                                        
         LA    R2,BOELEM                                                        
         MVC   TAXUL,CPYTAX        SAVE THE TAX U/L                             
         DROP  R2                                                               
*                                                                               
T        USING LDGRECD,IOKEY                                                    
         MVC   IOKEY,BCSPACES                                                   
         MVC   T.LDGKCPY,CUABIN    CONNECTED ID                                 
         MVC   T.LDGKUNT(L'TAXUL),TAXUL  READ TAX LEDGER RECORD                 
         DROP  T                                                                
*                                                                               
         GOTO1 AGETLDG                                                          
         BNE   EXITL               LEDGER RECORD MISSING                        
*                                                                               
         USING LDGTABD,R2                                                       
         ICM   R2,15,ACALDG                                                     
         MVI   ACTHILEN,0                                                       
         CLI   LDGTLVA,L'ACTKACT   ONLY 1 LEVEL                                 
         BE    INIT02                                                           
         LA    RE,LDGTLVD          LEVEL D LENGTH                               
         CLI   0(RE),0                                                          
         BNE   *+10                                                             
         BCTR  RE,0                                                             
         B     *-10                                                             
*                                                                               
         BCTR  RE,0                                                             
         MVC   ACTHILEN,0(RE)     PENULTIMATE LEVEL                             
*                                                                               
INIT02   MVC   SLDGOPOS,LDGTOFFP   SAVE LOCATION OF OFFICE FOR LEDGER           
         CLI   SLDGOPOS,LDGOTRAN   IF CODE IN TRANSACTION RECORD                
         BNE   *+8                 COMPANY EFFECTIVELY NOT ON OFFICES           
         NI    VATINDS,FF-VATCOFF                                               
*                                                                               
         GOTO1 VDICTAT,BOPARM,C'LU  ',DCLISTU,DSLISTU                           
         GOTO1 (RF),(R1),C'LL  ',DCLISTL,DSLISTL                                
         B     EXITOK                                                           
         DROP  R2                                                               
***********************************************************************         
* TABLE  ITERATION ROUTINE - EXPECTS R1 TO HOLD EQUATED VERB          *         
*                          - EXPECTS RF TO HOLD A(TABLE)              *         
***********************************************************************         
         SPACE 1                                                                
         USING OBJTABD,RF                                                       
ITER     CLI   OBJVERB,EOT         E.O.T.                                       
         BE    EXITOK           ** NEED TO SET HIGH IF NOT OVERRIDE             
         CLM   R1,1,OBJVERB        R1 HOLDS EQUATE                              
         BE    ITER02              MATCHED                                      
         LA    RF,OBJTABL(RF)                                                   
         B     ITER                ITERATE TABLE                                
*                                                                               
ITER02   ICM   RF,15,OBJADR        INVOKE OBJECT                                
         A     RF,BORELO                                                        
         BR    RF                                                               
         DROP  RF                                                               
         EJECT 1                                                                
***********************************************************************         
* OBJECT CONTROLLER - INTERFACES TO ALL USER OBJECTS                  *         
*                                                                     *         
* P1 HOLDS EQUATED VERB                                               *         
***********************************************************************         
         SPACE 1                                                                
OBJECT   L     R1,SVPARMS                                                       
         LA    RF,TABLEOO                                                       
         B     ITER                                                             
*                                                                               
TABLEOO  DC    AL1(OKEY),AL1(0,0,0),AL4(KEY)                                    
         DC    AL1(ODATA),AL1(0,0,0),AL4(DATA)                                  
         DC    AL1(OLIST),AL1(0,0,0),AL4(LIST)                                  
         DC    AL1(OSES),AL1(0,0,0),AL4(NTRSES)                                 
         DC    AL1(OSUBACT),AL1(0,0,0),AL4(EXITH)                               
         DC    AL1(OACTH),AL1(0,0,0),AL4(EXITH)                                 
         SPACE 2                                                                
***********************************************************************         
* KEY OBJECT                                                          *         
*                                                                     *         
* P1 HOLDS EQUATED OBJECT                                             *         
* P2 HOLDS EQUATED VERB                                               *         
* P3 A(KEY)                                                           *         
* P4 HOLDS SUB-ACTION                                                 *         
***********************************************************************         
         SPACE 1                                                                
         USING TAXRECD,R2                                                       
KEY      LM    R0,R3,SVPARMS                                                    
         LA    RF,KEYTABL                                                       
         B     ITER                                                             
*                                                                               
KEYTABL  DC    AL1(KFIRST),AL1(0,0,0),AL4(KEYFRST)                              
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* FIRST TIME FOR KEY OBJECT                                           *         
***********************************************************************         
         SPACE 1                                                                
KEYFRST  L     R1,SVPARMS4                                                      
         LA    RF,KFTABL           TABLE OF KNOWN INVOKERS                      
         B     ITER                                                             
*                                                                               
KFTABL   DC    AL1(KVAL),AL1(0,0,0),AL4(KFKVAL)      VALIDATE                   
         DC    AL1(KFVAL),AL1(0,0,0),AL4(KFKFVAL)    VALIDATE FILTER            
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* FIRST TIME FOR VALIDATE OF A KEY OBJECT                             *         
***********************************************************************         
         SPACE 1                                                                
KFKVAL   XC    TAXKEY,TAXKEY       INITIALIZE KEY OF VAT RULE RECORD            
         MVI   TAXKTYP,TAXKTYPQ    TAX RECORD TYPE                              
         MVC   TAXKCPY,CUABIN      CONNECTED ID                                 
         B     EXITOK                                                           
         SPACE 2                                                                
***********************************************************************         
* FIRST TIME FOR VALIDATE OF A KEY FILTER                             *         
***********************************************************************         
         SPACE 1                                                                
KFKFVAL  XC    TAXKEY,TAXKEY       INITIALIZE KEY OF VAT RULE RECORD            
         MVI   TAXKTYP,TAXKTYPQ    TAX RECORD TYPE                              
         MVC   TAXKCPY,CUABIN      CONNECTED ID                                 
         MVC   TAXKOFF,EFFS        DEFAULT OFFICE                               
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT                                                         *         
*                                                                     *         
* P1 HOLDS EQUATED OBJECT IDENTIFIER                                  *         
* P2 HOLDS EQUATED DATA IDENTIFIER OR 0 IF GLOBAL DATA ACTION         *         
* P3 BYTE  0   HOLDS EQUATED DATA VERB IF P2 IS ZERO                  *         
* P3 BYTES 1-3 HOLDS EQUATED ACTION VERB                              *         
* P4 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
* P5 HOLDS A(FIELD TABLE ENTRY) OR ZERO IF P2 IS ZERO                 *         
*                                                                     *         
* FIELD DATA IS EXTRACTED/OUTPUT INTO FVIFLD                          *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
DATA     ICM   R1,15,SVPARMS2      R1 HOLDS DATA IDENTIFIER                     
         BNZ   DATA02              ACTION IS ON A DATA OBJECT                   
*                                                                               
         USING TAXRECD,R2                                                       
         L     R2,SVPARMS+12                                                    
         XR    R1,R1                                                            
         IC    R1,SVPARMS+8        GET GLOBAL VERB                              
         LA    RF,DTATABL                                                       
         B     ITER                                                             
*                                                                               
         USING KNOWTABD,RF                                                      
DATA02   LA    RF,KNOWTAB          TABLE OF KNOWN OBJECTS                       
*                                                                               
DATA04   CLC   KNOWID,=AL2(EOT)    REACH END - NOT A KNOWN DATA TYPE            
         BE    EXITH                                                            
         CLM   R1,3,KNOWID         IS THIS A KNOWN TYPE?                        
         BE    DATA06                                                           
         LA    RF,KNOWLQ(RF)                                                    
         B     DATA04                                                           
*                                                                               
         USING TAXRECD,R2          R2 HOLDS A(RECORD)                           
         USING FDRELD,R3           R3 HOLDS A(FIELD TABLE ENTRY)                
DATA06   ICM   RF,15,KNOWADD       A(KNOWN OBJECT)                              
         A     RF,BORELO           RELOCATE IT                                  
         LM    R1,R3,SVPARMS3      R1 HOLDS VERB                                
         BR    RF                                                               
*                                                                               
DTATABL  DC    AL1(DFIRST),AL1(0,0,0),AL4(DTAFRST)                              
         DC    AL1(DLAST),AL1(0,0,0),AL4(DTALAST)                               
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* TABLE OF KNOWN RECORD OBJECTS                                       *         
***********************************************************************         
KNOWTAB  DC    AL2(VAT#OFFC),AL4(OFCDTA)  OFFICE CODE          (KEY)            
         DC    AL2(VAT#PRV),AL4(PRVDTA)   PROVENCE             (KEY)            
         DC    AL2(VAT#EFFDT),AL4(EDTDTA) EFFECTIVE DATE       (KEY)            
         DC    AL2(VAT#TAXRG),AL4(TRGDTA) TAX REGISTRATION                      
         DC    AL2(VAT#OFFNM),AL4(NAMDTA) OFFICE NAME                           
*                                                                               
         DC    AL2(VAT#TXCDI),AL4(TXCDTA) INPUT TAX CODE                        
         DC    AL2(VAT#TYPEI),AL4(TTYDTA) INPUT TAX TYPE                        
         DC    AL2(VAT#TXACCI),AL4(TACDTA) INPUT ACCOUNT CODE                   
         DC    AL2(VAT#TXNMI),AL4(TXNDTA) INPUT ACCOUNT NAME                    
         DC    AL2(VAT#TXRTI),AL4(TXRDTA) INPUT TAX RATE                        
*                                                                               
         DC    AL2(VAT#TXCDOS),AL4(TXCDTA) OUTPUT TAX CODE "S"                  
         DC    AL2(VAT#TXCDOX),AL4(TXCDTA) OUTPUT TAX CODE "X"                  
         DC    AL2(VAT#TXCDOZ),AL4(TXCDTA) OUTPUT TAX CODE "Z"                  
         DC    AL2(VAT#TXCDO),AL4(TXCDTA) OUTPUT TAX CODE (THE REST)            
         DC    AL2(VAT#TYPEO),AL4(TTYDTA) OUTPUT TAX TYPE                       
         DC    AL2(VAT#TXACCO),AL4(TACDTA) OUTPUT ACCOUNT CODE                  
         DC    AL2(VAT#TXNMO),AL4(TXNDTA) OUTPUT ACCOUNT NAME                   
         DC    AL2(VAT#TXRTO),AL4(TXRDTA) OUTPUT TAX RATE                       
         DC    AL2(EOT)                                                         
         SPACE 1                                                                
KNOWTABD DSECT                                                                  
KNOWID   DS    XL2                 IDENTIFIER                                   
KNOWADD  DS    AL4                 A(OBJECT)                                    
KNOWLQ   EQU   *-KNOWTABD                                                       
*                                                                               
FIL1D    CSECT                                                                  
REQOPVC  DC    C'X'                REQUIRED OUTPUT CODES                        
         DC    C'S'                                                             
         DC    C'Z'                                                             
REQOPVCN EQU   *-REQOPVC           # REQUIRED OUTPUT CODES                      
         SPACE 2                                                                
***********************************************************************         
* FIRST TIME FOR DATA OBJECT                                          *         
***********************************************************************         
         SPACE 1                                                                
DTAFRST  L     R1,SVPARMS3                                                      
         LA    RF,DFTABL           TABLE OF KNOWN INVOKERS                      
         B     ITER                                                             
*                                                                               
DFTABL   DC    AL1(DDIS),AL1(0,0,0),AL4(DFDDIS)      DISPLAY                    
         DC    AL1(DVAL),AL1(0,0,0),AL4(DFDVAL)      VALIDATE                   
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* FIRST TIME FOR DISPLAY OF A DATA OBJECT                             *         
***********************************************************************         
         SPACE 1                                                                
DFDDIS   DS    0H                                                               
         MVI   DISCNTER,1                                                       
         B     EXITOK                                                           
***********************************************************************         
* FIRST TIME FOR VALIDATE OF A DATA OBJECT                            *         
***********************************************************************         
         SPACE 1                                                                
DFDVAL   MVI   VALCNTER,1                                                       
         MVI   DISCNTER,1                                                       
         MVI   INPINDS,0                                                        
         NI    FILINDS1,FF-FILDNTAX                                             
         NI    VATINDS,FF-VATNEFVT                                              
         XC    TAXCTAB,TAXCTAB     CLEAR TAX ACCOUNT CODE TABLE                 
         XC    ACCDTAB,ACCDTAB     CLEAR ACCOUNT CODE TABLE                     
         XC    TAXRSTA,TAXRSTA     CLEAR TAX RECORD STATUS                      
         L     RE,AIO6             CLEAR IO6 FOR STORING                        
         LA    RF,IOAREALN         NEW TAX ELEMENTS                             
         XCEF                                                                   
         L     RE,AIO6                                                          
         ST    RE,ACURTXEL         A(CURRENT NEW TAX ELEMENT)                   
DFDVALX  B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* LAST TIME FOR DATA OBJECT                                           *         
***********************************************************************         
         SPACE 1                                                                
DTALAST  L     R1,SVPARMS3                                                      
         LA    RF,DLTABL           TABLE OF KNOWN INVOKERS                      
         B     ITER                                                             
*                             *** LAST TIME FOR DATA OBJECT ***                 
*                                 ------------------------                      
DLTABL   DC    AL1(DVAL),AL1(0,0,0),AL4(DLDVAL)      VALIDATE                   
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* LAST TIME FOR VALIDATE OF A DATA OBJECT                             *         
***********************************************************************         
         SPACE 1                                                                
DLDVAL   GOTO1 VHELLO,BOPARM,(C'D',GCFILNAM),('TAXIELQ',TAXRECD),0              
         GOTO1 VHELLO,BOPARM,(C'D',GCFILNAM),('TAXOELQ',TAXRECD),0              
*                                                                               
         L     R4,AIO6             SAVE TAX ELEMENTS BUFFER                     
         USING SVTXELD,R4                                                       
         PUSH  USING                                                            
         USING TAXELD,BOELEM                                                    
         SR    R5,R5                                                            
DLDV02   XC    BOELEM,BOELEM                                                    
         CLI   SVTXCODE,0                                                       
         BE    DLDV06              NO MORE TAX ELEMENT                          
         MVC   TAXEL,SVTXCDE       BUILD TAX ELEMENT                            
         MVC   TAXLN,SVTXLN                                                     
         MVC   TAXTYPE,SVTXTYPE                                                 
         MVC   TAXCODE,SVTXCODE                                                 
         MVC   TAXINDS,SVTXINDS                                                 
         MVC   TAXACTU(L'TAXUL),TAXUL                                           
         MVC   TAXACTA,SVTXACTA                                                 
         MVC   TAXRATE,SVTXRATE                                                 
         IC    R5,TAXLN                                                         
         SH    R5,=Y(TAXLN1Q+1)                                                 
         EX    R5,*+4                                                           
         MVC   TAXNAME,SVTXNAME                                                 
DLDV04   GOTO1 AADDEL,BOPARM,TAXRECD                                            
         LA    R4,SVTXELLN(R4)                                                  
         B     DLDV02                                                           
*                                                                               
DLDV06   TM    VATINDS,VATOFREC    SKIP CHECKING IF OFFICE RECORD               
         BNZ   DLDV08                                                           
         TM    INPINDS,INPFNDCD    AT LEAST ONE INPUT SHOULD                    
         BNZ   *+14                BE DEFINED                                   
         MVC   FVADDR,ATAXCODE     PUT CURSOR INTO TAX CODE FIELD               
         B     EXITNO                                                           
         POP   USING                                                            
*                                                                               
*        ADD/WRITE SG ACCOUNT RECORDS                                           
*                                                                               
DLDV08   DS    0H                                                               
         LA    R4,TAXRFST                                                       
         USING TAXELD,R4           R4=A(TAX ELEMENT)                            
DLDV10   CLI   TAXEL,0             TEST E-O-R                                   
         BE    DLDVALX                                                          
         CLC   TAXEL,SVTAXEL       TEST TAX RULES ELEMENT                       
         BNE   DLDV26                                                           
*                                                                               
         USING ACTRECD,R5          R5=A(ACCOUNT RECORD KEY)                     
         LA    R5,IOKEY                                                         
         MVC   ACTKEY,BCSPACES                                                  
         MVC   ACTKCPY,TAXKCPY                                                  
         MVC   ACTKUNT(L'TAXUL),TAXUL                                           
         MVC   ACTKACT,TAXACTA                                                  
         NI    FILINDS1,FF-(FILADDAC+FILWRINM)                                  
         L     R5,AIO2                                                          
         L     R1,=AL4(XIO2+XOACCDIR+XORDUP)                                    
         GOTO1 AIO                                                              
         BNE   DLDV12                                                           
         L     R1,=AL4(XIO2+XOACCMST+XOGETRUP)                                  
         GOTO1 AIO                                                              
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   ACTKEY,IOKEY                                                     
         B     DLDV14                                                           
*                                                                               
DLDV12   OI    FILINDS1,FILADDAC   BUILD NEW ACCOUNT RECORD                     
         MVC   ACTKEY,IOKEYSAV                                                  
         XC    ACTKSTA,ACTKSTA                                                  
         LA    RE,ACTRFST                                                       
         MVI   0(RE),0                                                          
         SR    RE,R5                                                            
         LA    RE,1(RE)            LENGTH OF ACCOUNT RECORD                     
         STCM  RE,3,ACTRLEN                                                     
*                                                                               
         GOTO1 AADDRST,ACTRECD     ADD STATUS ELEMENT                           
         BE    *+6                                                              
         DC    H'0'                ERROR CAN'T FIND STATUS ELEMENT              
*                                                                               
         GOTO1 AGETEL,BOPARM,('RSTELQ',ACTRECD),0                               
         PUSH  USING                                                            
*                                                                               
         USING RSTELD,BOELEM                                                    
*&&UK*&& OI    RSTSTAT,RSTSIVAT    SET TO INPUT                                 
*&&US*&& OI    RSTSTAT2,RSTSIVAT    SET TO INPUT                                
         CLI   TAXEL,TAXIELQ                                                    
         BE    *+8                                                              
*&&UK*&& NI    RSTSTAT,FF-RSTSIVAT   OR OUTPUT                                  
*&&US*&& NI    RSTSTAT2,FF-RSTSIVAT   OR OUTPUT                                 
         GOTO1 OFTOFLT,RSTELD      TEST OFPOS ON ACCOUNT FILTER                 
         BZ    *+10                                                             
         MVC   0(1,RF),TAXKOFF     SAVE OFFICE CODE INTO FILTER                 
         GOTO1 AREPEL,BOPARM,('RSTELQ',ACTRECD),0,BOELEM                        
         GOTO1 AADDBAL,ACTRECD     ADD BALANCE ELEMENT                          
         B     DLDV16              IF ADDING DO NOT SAVE NAME                   
*                                                                               
DLDV14   GOTO1 AGETEL,BOPARM,('NAMELQ',ACTRECD),0                               
         BNE   DLDV16                                                           
         MVC   NAMESAVE,BOELEM                                                  
*                                                                               
DLDV16   XC    BOELEM,BOELEM                                                    
         USING NAMELD,BOELEM       ADD/CHANGE ACCOUNT NAME ELEMENT              
         MVI   NAMEL,NAMELQ                                                     
         MVC   NAMEREC,TAXNAME                                                  
         ZIC   RF,TAXLN                                                         
         SH    RF,=Y(TAXLN1Q-NAMLN1Q)                                           
         STC   RF,NAMLN                                                         
         GOTO1 AREPEL,BOPARM,('NAMELQ',ACTRECD),0,BOELEM                        
                                                                                
         TM    FILINDS1,FILADDAC   ADD PNTR IF CHANGING NAME                    
         BNZ   DLDV20                                                           
*                                                                               
SV       USING NAMELD,NAMESAVE                                                  
         CLC   NAMLN,SV.NAMLN      LENGTH CHANGE                                
         BNE   DLDV18                                                           
         ZIC   RE,NAMLN                                                         
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         BE    DLDV20              NO CHANGE TO NAME                            
         CLC   NAMEL(0),SV.NAMEL                                                
*                                                                               
         USING RATELD,BOELEM       ADD/CHANGE ACCOUNT RATE ELEMENT              
DLDV18   OI    FILINDS1,FILWRINM   WRITE NAME CHANGE POINTER                    
DLDV20   XC    BOELEM,BOELEM                                                    
         MVI   RATEL,RATEVATQ                                                   
         MVI   RATLN,RATLNQ                                                     
         MVC   RATRATE,TAXRATE                                                  
         GOTO1 AREPEL,BOPARM,('RATEVATQ',ACTRECD),0,BOELEM                      
*                                                                               
         LHI   R1,XOADDREC+XOACCMST+XIO2                                        
         TM    FILINDS1,FILADDAC                                                
         BNZ   *+8                                                              
         LHI   R1,XOPUTREC+XOACCMST+XIO2   CHANGE ACCOUNT RECORD                
         GOTO1 AIO                                                              
         OI    FILINDS1,FILDNTAX   DISPLAY NEW TAX RECORD                       
         TM    FILINDS1,FILWRINM                                                
         BZ    DLDV26                                                           
         GOTO1 ANAMCHA,ACTRECD     WRITE NAME CHANGE POINTER                    
*                                                                               
DLDV26   ZIC   RF,TAXLN            BUMP R4 TO NEXT ELEMENT                      
         AR    R4,RF                                                            
         B     DLDV10                                                           
*                                                                               
DLDVALX  MVC   FVADDR,AEFFDATE     PUT CURSOR INTO EFFDATE FIELD                
         B     EXITOK                                                           
         POP   USING                                                            
         DROP  R4,R5                                                            
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT FOR OFFICE CODE                                         *         
*                                                                     *         
* R1 HOLDS EQUATED VERB                                               *         
* R2 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
* R3 HOLDS A(FIELD TABLE ENTRY)                                       *         
***********************************************************************         
         SPACE 1                                                                
OFCDTA   LA    RF,OFCTBL                                                        
         B     ITER                                                             
*                                                                               
OFCTBL   DC    AL1(DDIS),AL1(0,0,0),AL4(DISOFC)                                 
         DC    AL1(DVAL),AL1(0,0,0),AL4(VALOFC)                                 
         DC    AL1(DNTR),AL1(0,0,0),AL4(DISOFC)                                 
         DC    AL1(DSET),AL1(0,0,0),AL4(DSETOFC)                                
         DC    AL1(DFDIS),AL1(0,0,0),AL4(DFLTOFC)                               
         DC    AL1(DFVAL),AL1(0,0,0),AL4(VALOFC)                                
         DC    AL1(DFDO),AL1(0,0,0),AL4(DOFTOFC)                                
         DC    AL1(DSRCH),AL1(0,0,0),AL4(SRCHOFF)                               
         DC    AL1(DHED),AL1(0,0,0),AL4(DHDOFC)                                 
         DC    AL1(DMHED),AL1(0,0,0),AL4(HEDOFC)                                
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* UNPROTECT FIELD ON NTRSES IF REQUIRED                               *         
***********************************************************************         
         SPACE 1                                                                
DSETOFC  DS    0H                                                               
         B     FLTXX               UNPROTECT FIELD                              
         SPACE 2                                                                
***********************************************************************         
* DISPLAY A OFFICE CODE FIELD                                         *         
***********************************************************************         
         SPACE 1                                                                
DISOFC   TM    VATINDS,VATCOFF     OFFICE IS NOT REQUIRED                       
         BZ    EXITOK                                                           
         CLC   TAXKOFF,EFFS        IF COMPANY OFFICE RECORD,                    
         BE    EXITOK              DON'T DISPLAY OFFICE CODE                    
         MVC   FVIFLD(L'TAXKOFF),TAXKOFF                                        
         B     EXITOK                                                           
         SPACE 2                                                                
***********************************************************************         
* VALIDATE A OFFICE CODE FIELD                                        *         
***********************************************************************         
         SPACE 1                                                                
VALOFC   NI    VATINDS,FF-VATOFREC-VATCPREC                                     
         MVC   TAXKOFF,EFFS                                                     
         TM    VATINDS,VATCOFF     OFFICE IS NOT REQUIRED                       
         BZ    VOFC01                                                           
         CLI   FVILEN,0            NO INPUT                                     
         BNE   VOFC02                                                           
         OI    VATINDS,VATCPREC                                                 
         B     EXITOK                                                           
*                                                                               
VOFC01   OI    FVATRB,FVAPROT+FVAZERO  PROTECT FIELD                            
         OI    FVOIND,FVOXMT           TRANSMIT                                 
         OI    VATINDS,VATCPREC                                                 
         B     EXITOK                                                           
*                                                                               
*OFC01A  MVC   AP.TAXKOFF,EFFS                                                  
*        OI    VATINDS,VATCPREC                                                 
*        MVC   IOKEY,AP.TAXKEY                                                  
*        BAS   RE,GETTXHI          DO A READ HIGH TO FIND CLOSET KEY            
*        MVC   TAXKEY,IOKEY        RETURN KEY FOUND                             
*        MVC   AP.TAXKEY,TAXKEY                                                 
*        B     EXITOK                                                           
*                                                                               
VOFC02   OI    VATINDS,VATOFREC    OFFICE RECORD                                
         MVC   TAXKOFF,FVIFLD                                                   
         TM    FVIIND,FVIVAL                                                    
         BNZ   VALOFCX             OFFICE ALREADY VALIDATED                     
         CLI   CSACT,A#LST         ACTION = LIST                                
         BE    VALOFCX                                                          
*                                                                               
         MVC   IOKEY,BCSPACES      VALIDATE OFFICE ENTERED                      
         TM    VATINDS,VATNOFFS    NEW OR OLD OFFICE                            
         BZ    VOFC06              OLD                                          
*                                                                               
T        USING OFFRECD,IOKEY       NEW OFFICE SYSTEM                            
                                                                                
         MVI   T.OFFKTYP,OFFKTYPQ  SET UP OFFICE RECORD                         
         MVC   T.OFFKCPY,TAXKCPY                                                
         MVC   T.OFFKOFF,FVIFLD                                                 
         B     VOFC08                                                           
         DROP  T                                                                
*                                                                               
T        USING ACTRECD,IOKEY                                                    
                                                                                
VOFC06   MVC   FVMSGNO,=AL2(AE$FLDTL)  OFFICE TOO LONG                          
         CLI   FVILEN,1            OFFICE MUST BE ONE CHAR.                     
         BNE   EXITL                                                            
         MVC   T.ACTKCPY,TAXKCPY   SET UP ACCOUNT RECORD                        
         MVC   T.ACTKUNT(L'DEPTUL),DEPTUL                                       
         MVC   T.ACTKACT(1),FVIFLD                                              
*                                                                               
VOFC08   MVC   FVMSGNO,=AL2(AE$OFCNF)                                           
         LHI   R1,XOREAD+XOACCDIR+XIO5                                          
         GOTO1 AIO                                                              
         BNE   EXITL               OFFICE DOES NOT EXIST                        
         LHI   R1,XOGET+XOACCMST+XIO5                                           
         GOTO1 AIO                                                              
         BE    *+6                                                              
         DC    H'0'                BAD ACCMST RECORD                            
*                                                                               
         MVC   SAVEKEY,IOKEYSAV                                                 
         TM    VATINDS,VATNOFFS    NEW OFFICE SYSTEM ? (NOT SO NEW)             
         BZ    VALOFCX                                                          
         GOTO1 AGETEL,BOPARM,('OFIELQ',AIO5),0  IF NEW SYSTEM                   
         BNE   VALOFCX                                                          
         MVC   FVMSGNO,=AL2(AE$TODAL)  MUST NOT BE AN OFFICE LIST               
         LA    RF,BOELEM               OFFICE INFO ELEMENT                      
         TM    OFISTAT-OFIELD(RF),OFISLIST                                      
         BNZ   EXITL                                                            
*                                                                               
*ALOFCX  MVC   TAXKEY,AP.TAXKEY                                                 
VALOFCX  MVC   FLTIFLD(L'TAXKOFF),FVIFLD                                        
         B     EXITOK                                                           
         DROP  T                                                                
         SPACE 2                                                                
***********************************************************************         
* DISPLAY A OFFICE CODE FILTER FIELD                                  *         
***********************************************************************         
         SPACE 1                                                                
DFLTOFC  TM    VATINDS,VATCOFF     OFFICE IS NOT REQUIRED                       
         BZ    EXITOK                                                           
         MVC   FVIFLD(L'TAXKOFF),FLTIFLD                                        
         B     EXITOK                                                           
         SPACE 2                                                                
***********************************************************************         
* DO FILTERING FOR OFFICE CODE                                        *         
***********************************************************************         
         SPACE 1                                                                
DOFTOFC  CLC   TAXKOFF,BCSPACES    IS THERE A OFFICE CODE                       
         BE    FLTXX               NO - WE DON'T WANT IT THEN.                  
         CLC   TAXKOFF,EFFS        COMPANY RECORD                               
         BNE   DOFOFC2                                                          
         TM    VATINDS,VATCPREC    IS IT A COMPANY RECORD,                      
         BNZ   FLTXE               YES - WE WANT IT THEN.                       
         B     FLTXX                                                            
DOFOFC2  CLC   TAXKOFF,FLTIFLD                                                  
         BL    FLTXL                                                            
         BE    FLTXE                                                            
         BH    FLTXH                                                            
         SPACE 2                                                                
***********************************************************************         
* SEARCH ON OFFICE CODE FIELD                                         *         
***********************************************************************         
         SPACE 1                                                                
SRCHOFF  CLI   CSACT,A#LST         FOR ACTION LIST                              
         BE    EXITOK              DON'T SEARCH(MESSES UP FILTERING)            
         GOTO1 VACSRCHC,BOPARM,(3,FVADDR),ATWA,DEPTUL,ACOM,(X'11',0)            
         B     EXITOK                                                           
         SPACE 2                                                                
***********************************************************************         
* MODIFY COLUMN HEADINGS FOR OFFICE CODE FIELD                        *         
***********************************************************************         
DHDOFC   TM    VATINDS,VATCOFF     OFFICE IS NOT REQUIRED                       
         BNZ   EXITOK                                                           
         L     RE,SVPARMS5         HEADLINE 1                                   
         L     RF,SVPARMS6         HEADLINE 2                                   
         XC    0(6,RE),0(RE)       CLEAR                                        
         XC    0(6,RF),0(RF)                                                    
         B     EXITOK                                                           
         SPACE 1                                                                
***********************************************************************         
* SET OFFICE CODE TAG FIELD                                           *         
***********************************************************************         
         SPACE 1                                                                
HEDOFC   TM    VATINDS,VATCOFF     OFFICE IS NOT REQUIRED                       
         BNZ   EXITOK                                                           
         L     RF,SVPARMS6                                                      
         MVI   16(RF),C'A'         SUPPRESS TAG                                 
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT FOR PROVENCE                                            *         
*                                                                     *         
* R1 HOLDS EQUATED VERB                                               *         
* R2 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
* R3 HOLDS A(FIELD TABLE ENTRY)                                       *         
* AIO5 = A(SAVE TAX RECORD)                                           *         
***********************************************************************         
         SPACE 1                                                                
PRVDTA   LA    RF,PRVTBL                                                        
         B     ITER                                                             
*                                                                               
PRVTBL   DC    AL1(DDIS),AL1(0,0,0),AL4(DISPRV)                                 
         DC    AL1(DVAL),AL1(0,0,0),AL4(VALPRV)                                 
         DC    AL1(DNTR),AL1(0,0,0),AL4(DISPRV)                                 
*        DC    AL1(DSET),AL1(0,0,0),AL4(DSETPRV)                                
*        DC    AL1(DFDIS),AL1(0,0,0),AL4(DFLTPRV)                               
*        DC    AL1(DDFLTF),AL1(0,0,0),AL4(DFTPRV)                               
*        DC    AL1(DFVAL),AL1(0,0,0),AL4(VFLTPRV)                               
*        DC    AL1(DFDO),AL1(0,0,0),AL4(DOFTPRV)                                
         DC    AL1(EOT)                                                         
*                                                                               
***********************************************************************         
* DISPLAY A PROVENCE FIELD                                            *         
***********************************************************************         
         SPACE 1                                                                
DISPRV   XC    FVIFLD,FVIFLD       DISPLAY PRV OF RECORD                        
         MVC   SPRVCODE,TAXKPRV                                                 
         MVC   FVIFLD(L'TAXKPRV),TAXKPRV                                        
         B     EXITOK                                                           
         SPACE 2                                                                
***********************************************************************         
* VALIDATE PROVENCE FIELD                                             *         
***********************************************************************         
         SPACE 1                                                                
VALPRV   XC    TAXKPRV,TAXKPRV                                                  
         CLI   FVILEN,0            ANY PROVENCE?                                
         BE    EXITOK              NO                                           
         LA    RF,PRVTAB                                                        
         MVC   FVMSGNO,=AL2(2025)                                               
         CLC   FVIFLD,BCSPACES                                                  
         BNH   EXITOK              NO INPUT                                     
*                                                                               
VALPRV10 CLI   0(RF),X'FF'         END OF TABLE ?                               
         BE    EXITL                                                            
         CLC   FVIFLD(L'PRVTAB),0(RF)                                           
         BE    VALPRV20                                                         
         LA    RF,L'PRVTAB(,RF)                                                 
         B     VALPRV10                                                         
*                                                                               
VALPRV20 MVC   TAXKPRV,FVIFLD                                                   
         B     EXITOK                                                           
*                                                                               
PRVTAB   DS    0CL2                                                             
         DC    C'BC'               BRITISH COLUMBIA                             
         DC    C'AL'               ALBERTA                                      
         DC    C'SA'               SASKATCHEWAN                                 
         DC    C'MA'               MANITOBA                                     
         DC    C'ON'               ONTARIO                                      
         DC    C'PQ'               QUEBEC                                       
         DC    C'NB'               NEW BRUNSWICK                                
         DC    C'NS'               NOVA SCOTIA                                  
         DC    C'PE'               PRINCE EDWARD ISLAND                         
         DC    C'NF'               NEWFOUNDLAND                                 
         DC    X'FF'               EOT                                          
         EJECT                                                                  
*&&DO                                                                           
***********************************************************************         
* GET POSSIBLE TAX RECORD                                             *         
***********************************************************************         
         SPACE 1                                                                
GETTXHI  NTR1                                                                   
         LHI   R1,XOACCDIR+XIO1+XOHIGH                                          
         GOTO1 AIO                                                              
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
GETTX10  LHI   R1,XOGET+XOACCMST+XIO1                                           
         GOTO1 AIO                                                              
         MVC   SAVEKEY,IOKEYSAV                                                 
         BE    EXIT                                                             
         DC    H'0'                                                             
         EJECT                                                                  
*&&                                                                             
***********************************************************************         
* DATA OBJECT FOR EFFECTIVE DATE                                      *         
*                                                                     *         
* R1 HOLDS EQUATED VERB                                               *         
* R2 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
* R3 HOLDS A(FIELD TABLE ENTRY)                                       *         
* AIO5 = A(SAVE TAX RECORD)                                           *         
***********************************************************************         
         SPACE 1                                                                
EDTDTA   LA    RF,EDTTBL                                                        
         B     ITER                                                             
*                                                                               
EDTTBL   DC    AL1(DDIS),AL1(0,0,0),AL4(DISEDT)                                 
         DC    AL1(DVAL),AL1(0,0,0),AL4(VALEDT)                                 
         DC    AL1(DNTR),AL1(0,0,0),AL4(DISEDT)                                 
         DC    AL1(DSET),AL1(0,0,0),AL4(DSETEDT)                                
         DC    AL1(DFDIS),AL1(0,0,0),AL4(DFLTEDT)                               
         DC    AL1(DDFLTF),AL1(0,0,0),AL4(DFTEDT)                               
         DC    AL1(DFVAL),AL1(0,0,0),AL4(VFLTEDT)                               
         DC    AL1(DFDO),AL1(0,0,0),AL4(DOFTEDT)                                
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* UNPROTECT FIELD ON NTRSES IF REQUIRED                               *         
***********************************************************************         
         SPACE 1                                                                
DSETEDT  DS    0H                                                               
         B     FLTXX               UNPROTECT FIELD                              
         SPACE 2                                                                
***********************************************************************         
* DISPLAY A EFFECTIVE DATE FIELD                                      *         
***********************************************************************         
         SPACE 1                                                                
DISEDT   L     R1,SVPARMS2                                                      
         STCM  R1,3,SVFLDN         SAVE FIELD #                                 
         XC    FVIFLD,FVIFLD       DISPLAY DATE OF RECORD                       
         MVC   STAXDATE,TAXKDATE   TAXKDATE = COMPLEMENT OF DATE                
         XC    STAXDATE,EFFS                                                    
         GOTO1 VDATCON,BODMCB,(1,STAXDATE),(8,FVIFLD)                           
         B     EXITOK                                                           
         SPACE 2                                                                
***********************************************************************         
* VALIDATE A EFFECTIVE DATE FIELD                                     *         
***********************************************************************         
         SPACE 1                                                                
VALEDT   NI    VATINDS,FF-VATCDFNO                                              
         MVC   AEFFDATE,FVADDR     SAVE A(EFFECTIVE DATE FIELD)                 
         CLI   CSACT,A#ADD         IF ADD OR RESTORE A RECORD                   
         BE    VEDT08              THEN A REAL DATE IS REQUIRED                 
         CLI   CSACT,A#RES                                                      
         BE    VEDT08                                                           
         ZIC   RF,FVXLEN                                                        
         EX    RF,*+8                                                           
         BNE   VEDT02                                                           
         CLC   FVIFLD(0),UC@TODAY  TODAY                                        
         MVI   DATTYPE,DATTTDAY                                                 
         MVC   EFFDATE,BCTODAYP                                                 
         B     VEDT09                                                           
*                                                                               
VEDT02   EX    RF,*+8                                                           
         BNE   VEDT04                                                           
         CLC   FVIFLD(0),UC@LAST   LAST                                         
         MVI   DATTYPE,DATTLAST                                                 
         MVC   EFFDATE,EFFS                                                     
         B     VEDT09                                                           
*                                                                               
VEDT04   EX    RF,*+8              'PREVIOUS'/ 'EARLIER' DATE                   
         BE    VEDT06                                                           
         CLC   FVIFLD(0),UC@PRV                                                 
         EX    RF,*+8                                                           
         BNE   VEDT08                                                           
*                                                                               
VEDT06   MVI   DATTYPE,DATTPREV                                                 
         SR    RE,RE                                                            
         ICM   RE,7,EFFDATE                                                     
         BCTR  RE,0                                                             
         STCM  RE,7,EFFDATE                                                     
         B     VEDT09                                                           
*                                                                               
VEDT08   GOTO1 VDATVAL,BODMCB,FVIFLD,BODUB1    A REAL DATE                      
         MVC   FVMSGNO,=AL2(AE$INDAT)                                           
         OC    0(4,R1),0(R1)                                                    
         BZ    EXITL                           INVAILD DATE                     
         MVI   DATTYPE,DATTDATE                                                 
         GOTO1 VDATCON,BODMCB,BODUB1,(1,EFFDATE)                                
*                                                                               
VEDT09   MVC   TAXKDATE,EFFDATE    TAXKDATE = COMPLEMENT OF DATE                
         XC    TAXKDATE,EFFS                                                    
*                                                                               
         CLI   CSACT,A#CPY         IF COPY OR ADD A RECORD,                     
         BE    VEDT10              COMPANY RECORD MUST EXIST                    
         CLI   CSACT,A#ADD                                                      
         BE    VEDT10                                                           
         CLI   CSACT,A#RES         IF RESTORE A RECORD,                         
         BE    VEDT16              DON'T FIND MOST RECENT DATE                  
         B     VEDT13                                                           
*                                                                               
CPY      USING TAXRECD,IOKEY                                                    
VEDT10   TM    VATINDS,VATCPREC         SKIP IF NOT OFFICE TAX RECORD           
         BO    VEDT16                                                           
         MVC   CPY.TAXKEY,TAXKEY                                                
         MVC   CPY.TAXKOFF,EFFS                                                 
         MVC   FVMSGNO,=AL2(AE$CVRDE)                                           
         LHI   R1,XOREAD+XOACCDIR+XIO1                                          
         GOTO1 AIO                                                              
         BNE   EXITL                    COMPANY TAX RECORD MUST EXIST           
         LHI   R1,XOGET+XOACCMST+XIO1                                           
         GOTO1 AIO                                                              
         BE    VEDT16                                                           
         DC    H'0'                                                             
         DROP  CPY                                                              
*                                                                               
VEDT13   MVC   IOKEY,BCSPACES                                                   
         MVC   IOKEY(L'TAXKEY),TAXKEY                                           
         LHI   R1,XOHIGH+XOACCDIR+XIO1                                          
         GOTO1 AIO                 READ FOR MOST RECENT DATE                    
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   TAXKEY,IOKEY        SAVE NEW KEY                                 
         CLC   TAXKEY(TAXKDATE-TAXKEY),IOKEYSAV                                 
         BE    VEDT14                                                           
*                                                                               
         CLI   DATTYPE,DATTLAST    TEST 'LAST'     ENTERED FOR DATE             
         BNE   *+10                                                             
         MVC   FVMSGNO,=AL2(AE$RCNOF)                                           
         CLI   DATTYPE,DATTTDAY    TEST 'TODAY'    ENTERED FOR DATE             
         BE    VEDT14                                                           
         MVC   FVMSGNO,=AL2(AE$NRICU)                                           
         CLI   DATTYPE,DATTPREV    TEST 'PREVIOUS' ENTERED FOR DATE             
         BNE   *+10                                                             
         MVC   FVMSGNO,=AL2(AE$NPROF)                                           
         CLI   DATTYPE,DATTDATE    TEST A DATE ENTERED FOR DATE                 
         BNE   *+10                                                             
         MVC   FVMSGNO,=AL2(AE$NRUED)                                           
         B     EXITL                                                            
*                                                                               
VEDT14   MVC   EFFDATE,TAXKDATE    SET EFFDATE TO DATE OF RECORD                
         XC    EFFDATE,EFFS                                                     
         B     VALEDTX                                                          
*                                                                               
VEDT16   XC    TAXRSTA,TAXRSTA                                                  
*                                                                               
*ALEDTX  MVC   TAXKEY,AP.TAXKEY                                                 
VALEDTX  B     EXITOK                                                           
         SPACE 2                                                                
***********************************************************************         
* DISPLAY A EFFECTIVE DATE FILTER FIELD                               *         
***********************************************************************         
         SPACE 1                                                                
DFLTEDT  OC    EFFDATE,EFFDATE                                                  
         BZ    EXITOK                                                           
         XC    FVIFLD,FVIFLD       DISPLAY DATE OF RECORD                       
         GOTO1 VDATCON,BODMCB,(1,EFFDATE),(8,FVIFLD)                            
         B     EXITOK                                                           
         SPACE 2                                                                
***********************************************************************         
* SET DEFAULTS FOR FILTER FIELD                                       *         
***********************************************************************         
         SPACE 1                                                                
DFTEDT   XC    FVIFLD,FVIFLD       DISPLAY TODAY                                
*        GOTO1 VDATCON,BODMCB,(1,BCTODAYP),(8,FVIFLD)                           
         B     EXITOK                                                           
         SPACE 2                                                                
***********************************************************************         
* VALIDATE A EFFECTIVE DATE FILTER FIELD                              *         
***********************************************************************         
         SPACE 1                                                                
VFLTEDT  GOTO1 VDATVAL,BODMCB,FVIFLD,BODUB1  EFFECTIVE DATE                     
         MVC   FVMSGNO,=AL2(AE$INDAT)                                           
         OC    0(4,R1),0(R1)                                                    
         BZ    EXITL               INVAILD DATE                                 
         GOTO1 VDATCON,BODMCB,BODUB1,(1,EFFDATE)                                
         MVC   TAXKDATE,EFFDATE                                                 
         XC    TAXKDATE,EFFS       TAXKDATE = COMPLEMENT DATE                   
         TM    FVIIND,FVITHIS      HAS THIS FIELD BEEN CHANGED                  
         BZ    EXITOK                                                           
         OI    LSSCIND1,LSSCIFLT   SET FILTER HAS BEEN CHANGED                  
         B     EXITOK                                                           
         SPACE 2                                                                
***********************************************************************         
* DO FILTERING FOR EFFECTIVE DATE                                     *         
***********************************************************************         
         SPACE 1                                                                
DOFTEDT  MVC   STAXDATE,TAXKDATE                                                
         XC    STAXDATE,EFFS       TAXKDATE = COMPLEMENT OF DATE                
         CLC   STAXDATE,EFFDATE                                                 
         BL    FLTXL                                                            
         BE    FLTXE                                                            
         BH    FLTXH                                                            
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT FOR REGISTRATION #                                      *         
*                                                                     *         
* R1 HOLDS EQUATED VERB                                               *         
* R2 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
* R3 HOLDS A(FIELD TABLE ENTRY)                                       *         
* AIO5 = A(SAVE TAX RECORD)                                           *         
***********************************************************************         
         SPACE 1                                                                
TRGDTA   LA    RF,TRGTBL                                                        
         B     ITER                                                             
*                                                                               
TRGTBL   DC    AL1(DDIS),AL1(0,0,0),AL4(DISTRG)                                 
         DC    AL1(DVAL),AL1(0,0,0),AL4(VALTRG)                                 
         DC    AL1(DNTR),AL1(0,0,0),AL4(DISTRG)                                 
         DC    AL1(EOT)                                                         
*                                                                               
***********************************************************************         
* DISPLAY A REGISTRATION # FIELD                                      *         
***********************************************************************         
         SPACE 1                                                                
         USING FFTELD,RF                                                        
DISTRG   XC    FVIFLD,FVIFLD       DISPLAY TRG OF RECORD                        
         LA    RF,TAXRFST                                                       
         SR    R1,R1                                                            
*                                                                               
DISTRG05 CLI   0(RF),FFTELQ        FREE FORM ELEMENT                            
         BNE   DISTRG10                                                         
         CLI   FFTTYPE,FFTTFREE    FREE FORM REGISTRATION                       
         BE    DISTRG20                                                         
DISTRG10 IC    R1,1(,RF)                                                        
         AR    RF,R1                                                            
         B     DISTRG05                                                         
*                                                                               
DISTRG20 IC    R1,1(,RF)                                                        
         SHI   R1,FFTLN1Q+1                                                     
         BM    EXITOK                                                           
         EX    R1,*+8                                                           
         B     EXITOK                                                           
         MVC   FVIFLD(0),FFTDATA   SEQUENCE NUMBER                              
         DROP  RF                                                               
         SPACE 2                                                                
***********************************************************************         
* VALIDATE THE REGISTRATION #                                         *         
***********************************************************************         
         SPACE 1                                                                
VALTRG   CLI   FVILEN,0            ANY INPUT                                    
         BNE   VTRG10                                                           
         CLI   CSACT,A#ADD         MUST HAVE WHEN ADDING                        
         BE    EXITNO                                                           
         B     EXITOK                                                           
*                                                                               
VTRG10   GOTO1 VHELLO,BOPARM,(C'D',GCFILNAM),('FFTELQ',TAXRECD),0               
*                                                                               
         USING FFTELD,R4                                                        
         LA    R4,BOELEM                                                        
         XC    BOELEM,BOELEM                                                    
         MVI   FFTEL,FFTELQ                                                     
         MVI   FFTTYPE,FFTTFREE                                                 
         MVC   FFTDLEN,FVILEN                                                   
         ZIC   R1,FVXLEN                                                        
         EXMVC R1,FFTDATA,FVIFLD   R1=EXECUTED LENGTH OF DATA                   
         AHI   R1,2                BUMP UP 1 FOR TRUE LENGTH AND 1 FOR          
         AHI   R1,FFTLN1Q          ELEMENT LENGTH                               
         STC   R1,FFTLN            ADD WITH ELEMENT OVERHEAD                    
         GOTO1 VHELLO,BOPARM,(C'P',GCFILNAM),TAXRECD,BOELEM,0                   
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT FOR DISPLAYING A OFFICE NAME FIELD                      *         
*                                                                     *         
* R1 HOLDS EQUATED VERB                                               *         
* R2 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
* R3 HOLDS A(FIELD TABLE ENTRY)                                       *         
***********************************************************************         
         SPACE 1                                                                
NAMDTA   LA    RF,NAMTBL                                                        
         B     ITER                                                             
*                                                                               
NAMTBL   DC    AL1(DDIS),AL1(0,0,0),AL4(DISNAM)                                 
         DC    AL1(DHED),AL1(0,0,0),AL4(DHDNAM)                                 
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* DISPLAY A OFFICE NAME FIELD                                         *         
***********************************************************************         
         SPACE 1                                                                
DISNAM   TM    VATINDS,VATCOFF     OFFICE IS NOT REQUIRED                       
         BZ    EXITOK                                                           
         MVC   IOKEY,BCSPACES                                                   
         TM    VATINDS,VATCPREC    COMPANY RECORD                               
         BZ    DNAM02                                                           
T        USING CPYRECD,IOKEY                                                    
         MVC   T.CPYKCPY,CUABIN    READ COMPANY RECORD                          
         DROP  T                                                                
         LHI   R1,XOREAD+XOACCDIR+XIO5                                          
         GOTO1 AIO                                                              
         BE    *+6                                                              
         DC    H'0'                COMPANY RECORD MISSING                       
         B     DISNAMX             DISPLAY COMPANY NAME                         
*                                                                               
DNAM02   TM    VATINDS,VATNOFFS                                                 
         BZ    DNAM04                                                           
T        USING OFFRECD,IOKEY       FOR NEW OFFICE SYSTEM                        
         MVI   T.OFFKTYP,OFFKTYPQ  SET UP OFFICE RECORD                         
         MVC   T.OFFKCPY,TAXKCPY                                                
         MVC   T.OFFKOFF,TAXKOFF                                                
         B     DNAM06                                                           
         DROP  T                                                                
T        USING ACTRECD,IOKEY                                                    
DNAM04   MVC   T.ACTKCPY,TAXKCPY   SET UP ACCOUNT RECORD                        
         MVC   T.ACTKUNT(L'DEPTUL),DEPTUL                                       
         MVC   T.ACTKACT(1),TAXKOFF                                             
         SPACE 1                                                                
         DROP  T                                                                
DNAM06   LHI   R1,XOREAD+XOACCDIR+XIO5                                          
         GOTO1 AIO                                                              
         BNE   EXITL               OFFICE DOESN'T EXIST                         
DISNAMX  LHI   R1,XOGET+XOACCMST+XIO5                                           
         GOTO1 AIO                                                              
         BE    *+6                                                              
         DC    H'0'                BAD ACCMST RECORD                            
         L     R1,AIO5             A(OFFICE RECORD)                             
         GOTO1 AGETNAM                                                          
         B     EXITOK                                                           
         SPACE 2                                                                
***********************************************************************         
* MODIFY COLUMN HEADINGS FOR OFFICE NAME FIELD                        *         
***********************************************************************         
DHDNAM   TM    VATINDS,VATCOFF     OFFICE IS NOT REQUIRED                       
         BNZ   EXITOK                                                           
         L     RE,SVPARMS5         HEADLINE 1                                   
         L     RF,SVPARMS6         HEADLINE 2                                   
         XC    0(L'NAMEREC,RE),0(RE)      CLEAR                                 
         XC    0(L'NAMEREC,RF),0(RF)                                            
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT FOR TAX CODE FIELD                                      *         
*                                                                     *         
* R1 HOLDS EQUATED VERB                                               *         
* R2 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
* R3 HOLDS A(FIELD TABLE ENTRY)                                       *         
* AIO5 = A(COMPANY TAX RECORD)                                        *         
***********************************************************************         
         SPACE 1                                                                
TXCDTA   LA    RF,TXCTBL                                                        
         B     ITER                                                             
*                                                                               
TXCTBL   DC    AL1(DDIS),AL1(0,0,0),AL4(DISTXC)                                 
         DC    AL1(DDFLT),AL1(0,0,0),AL4(DFLTXC)                                
         DC    AL1(DVAL),AL1(0,0,0),AL4(VALTXC)                                 
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* SET UP DEFAULT OUTPUT TAX CODE                                                
***********************************************************************         
         SPACE 1                                                                
         USING SVTXELD,R4                                                       
DFLTXC   CLI   CSACT,A#ADD         FOR ACTION ADD ONLY                          
         BNE   EXITOK                                                           
         TM    VATINDS,VATOFREC    OFFICE LEVEL RECORD?                         
         BO    EXITOK              THEN CODES WILL BE FILLED IN FROM CO         
         L     R4,ACURTXEL                                                      
         L     R1,SVPARMS2         R1=CURRENT FIELD #                           
         CLM   R1,3,=AL2(VAT#TXCDOS) OUTPUT CODE "S"                            
         BNE   *+12                                                             
         MVI   SVTXCDE,C'S'                                                     
         B     EXITOK                                                           
*                                                                               
         CLM   R1,3,=AL2(VAT#TXCDOX) OUTPUT CODE "X"                            
         BNE   *+12                                                             
         MVI   SVTXCDE,C'X'                                                     
         B     EXITOK                                                           
*                                                                               
         CLM   R1,3,=AL2(VAT#TXCDOZ) OUTPUT CODE "Z"                            
         BNE   EXITOK                                                           
         MVI   SVTXCDE,C'Z'                                                     
         B     EXITOK                                                           
         DROP  R4                                                               
         SPACE 2                                                                
***********************************************************************         
* DISPLAY TAX CODE                                                    *         
***********************************************************************         
         SPACE 1                                                                
         USING SVTXELD,R3                                                       
         USING TAXELD,R4                                                        
*        CLI   DISCNTER,1                                                       
*        BNE   DISTX01                                                          
*        L     R1,SVPARMS2         R1=CURRENT FIELD #                           
*        CLM   R1,3,=AL2(VAT#TXCDOS) IF THE FIELD IS 1 OF THE PROTECTED         
*        BNE   *+12                  OUTPUT CODE FIELDS (S,X OR Z)              
*        MVI   BCBYTE1,C'S'                                                     
*        B     DISTXCA                                                          
*        CLM   R1,3,=AL2(VAT#TXCDOX) THAN DON'T DISPLAY (ONLY GETS HERE         
*        BNE   *+12                  BECAUSE THEY ARE PROTECTED FIELDS)         
*        MVI   BCBYTE1,C'X'                                                     
*        B     DISTXCA                                                          
*        CLM   R1,3,=AL2(VAT#TXCDOZ)                                            
*        BE    *+8                                                              
*        B     DISTX01                                                          
*        MVI   BCBYTE1,C'Z'                                                     
DISTXCA  DS    0H                                                               
DISTXC   CLI   CSACT,A#CHA         FOR ACTION CHANGE ONLY                       
         BNE   DISTX01                                                          
         TM    FVATRB,FVAPROT      AND FOR PROTECTED TAX CODES ONLY             
         BZ    DISTX01                                                          
         CLI   FVILEN,0                                                         
         BNE   *+14                                                             
         XC    ATAXEL,ATAXEL                                                    
         B     EXITOK                                                           
         OI    INPINDS,PROTVAL                                                  
         BAS   RE,VTXCODE          CALL VALIDATE ROUTINE                        
         NI    INPINDS,X'FF'-PROTVAL                                            
         L     R3,ACURTXEL                                                      
         MVC   FVIFLD(L'SVTXCODE),SVTXCODE                                      
         TM    SVTXINDS,TAXIDFLT            DEFAULT INPUT TAX RATE              
         BZ    *+8                                                              
         MVI   FVIFLD+1,C'*'                                                    
         OI    FVATRB,FVAPROT      AND PROTECT                                  
         BAS   RE,GETTAXEL         GET THE CORRECT TAX ELEMENT                  
*        MVC   SVTXCODE,BCBYTE1                                                 
         B     EXITOK                                                           
*                                                                               
DISTX01  XC    ATAXEL,ATAXEL                                                    
         NI    FVATRB,FF-FVAPROT   UNPROTECT FIELD IF COMPANY RECORD            
         TM    VATINDS,VATOFREC                                                 
         BZ    *+8                                                              
         OI    FVATRB,FVAPROT      PROTECT FIELD   IF OFFICE  RECORD            
*                                                                               
         ZIC   R5,DISCNTER         POSINTION OF TX ELEMENT IN TX RECORD         
         LR    RE,R5                                                            
         MVI   SVTAXEL,TAXIELQ     INPUT  TYPE                                  
         CLI   DISCNTER,DISMAX                                                  
         BNH   DISTXC02                                                         
         SHI   R5,DISMAX           MAX # OF ENTIES OF INPUT/OUTPUT TYPE         
         MVI   SVTAXEL,TAXOELQ     OUTPUT TYPE                                  
*        CLI   DISCNTER,2                                                       
*        BNE   *+6                                                              
*        DC    H'0'                                                             
*                                                                               
DISTXC02 LA    RE,1(,RE)                                                        
         STC   RE,DISCNTER         INCREMENT DISPLAY COUNTER                    
         SR    RE,RE                                                            
         LA    R4,TAXRFST                                                       
*                                                                               
CPY      USING TAXRECD,RF                                                       
         L     RF,AIO5                                                          
         TM    VATINDS,VATCDFNO    DISPLAY COMPANY TAX RECORD                   
         BZ    DTXC04                                                           
         TM    FILINDS1,FILDNTAX   DISPLAY NEW TAX RECORD                       
         BNZ   *+8                                                              
         LA    R4,CPY.TAXRFST      NO - USE COMPANY TAX RECORD THEN.            
*                                                                               
DTXC04   CLI   TAXEL,0                                                          
         BE    EXITOK                                                           
         CLC   TAXEL,SVTAXEL                                                    
         BE    DTXC08                                                           
DTXC06   ZIC   RE,TAXLN                                                         
         AR    R4,RE                                                            
         B     DTXC04                                                           
*                                                                               
DTXC08   BCT   R5,DTXC06           FIND THE RIGHT ELEMENT IN TAX REC            
         ST    R4,ATAXEL           SAVE TAX ELEMENT                             
*                                                                               
*        CLI   SVTAXEL,TAXOELQ     OUTPUT TYPE?                                 
*        BNE   *+6                                                              
*        DC    H'0'                                                             
         MVC   FVIFLD(L'TAXCODE),TAXCODE   DISPLAY TAX CODE                     
         TM    TAXINDS,TAXIDFLT            DEFAULT INPUT TAX RATE               
         BZ    *+8                                                              
         MVI   FVIFLD+1,C'*'                                                    
DTXC09   CLI   SVTAXEL,TAXOELQ     OUTPUT TYPE?                                 
         BNE   EXITOK              NO SO EXIT                                   
         LA    R0,REQOPVCN         IF A REQUIRED TAX CODE THAN PROTECT          
         LA    RF,REQOPVC                                                       
DTXC10   CLC   TAXCODE,0(RF)                                                    
         BNE   *+12                                                             
         OI    FVATRB,FVAPROT                                                   
         B     EXITOK                                                           
         LA    RF,1(RF)                                                         
         BCT   R0,DTXC10                                                        
         B     EXITOK                                                           
         DROP  R3,R4,CPY                                                        
         SPACE 2                                                                
***********************************************************************         
* VALIDATE TAX CODE                                                   *         
***********************************************************************         
         SPACE 1                                                                
*                                                                               
VALTXC   BAS   RE,VTXCODE                                                       
         B     EXITOK                                                           
         SPACE 2                                                                
***********************************************************************         
* VALIDATE TAX CODE SUBROUTINE                                                  
***********************************************************************         
         SPACE 1                                                                
         USING SVTXELD,R4                                                       
VTXCODE  NTR1                                                                   
         MVI   SVTAXEL,TAXIELQ     DEFAULT TO INPUT TYPE                        
         L     R1,SVPARMS2                                                      
         CLM   R1,3,=AL2(VAT#TXCDI) INPUT TAX CODE?                             
         BE    VTXC02                                                           
         CLI   SVTAXEL,TAXOELQ     WAS THE LAST ONE OUTPUT TOO?                 
         BE    *+10                YES                                          
         XC    TAXCTAB,TAXCTAB     NO SO CLEAR TABLE OF CODES                   
         MVI   SVTAXEL,TAXOELQ                                                  
VTXC02   L     R4,ACURTXEL         CURRENT SAVE TAX ELEMENT                     
         TM    INPINDS,INPVAL      TAX CODE FIELD HAS BEEN VALIDATED            
         BNZ   *+10                                                             
         MVC   ATAXCODE,FVADDR     SAVE A(TAX CODE FIELD)                       
*                                                                               
         NI    FVATRB,FF-FVAPROT   UNPROTECT FIELD IF COMPANY RECORD            
         TM    VATINDS,VATOFREC                                                 
         BZ    VTXC04                                                           
         OI    FVATRB,FVAPROT      PROTECT FIELD IF OFFICE RECORD               
*        TM    INPINDS,INPVAL      TAX CODE FIELD HAS BEEN VALIDATED            
*        BNZ   VTXC04                                                           
         CLI   CSACT,A#ADD                                                      
         BNE   VTXC04                                                           
         BAS   RE,DISREC           IF ADD, DISPLAY COMPANY TAX RECORD           
         B     VTXCX                                                            
VTXC04   CLI   FVILEN,0            NO INPUT - EXIT                              
         BE    VTXCX                                                            
         OI    INPINDS,INPFNDCD    FIND A TAX CODE                              
         MVC   SVTXCDE,SVTAXEL     SAVE TAX ELEMENT CODE                        
         MVI   SVTXLN,TAXLN1Q      SAVE ELEMENT LENGTH                          
         MVC   SVTXCODE,FVIFLD     SAVE TAX CODE                                
         TM    INPINDS,PROTVAL     ARE WE VALIDATING A PROTECTED FIELD?         
         BZ    VTXC06                                                           
         USING FHD,RF              THEN MUST LOOK AT THE FIELD ADDRESS          
         L     RF,FVADDR                                                        
         MVC   SVTXCODE,FHDA                                                    
         CLI   FHDA+1,C' '                                                      
         BE    VTXC40                                                           
         B     VTXC08                                                           
*                                                                               
VTXC06   CLI   FVIFLD+1,C' '                                                    
         BE    VTXC40                                                           
VTXC08   MVC   FVMSGNO,=AL2(AE$FLDTL) CODE HAS LENGTH OF 2 CHARACTERS           
         CLI   SVTAXEL,TAXIELQ                                                  
         BNE   EXITL               ONLY VALID FOR INPUT TYPE                    
*        BE    VTXC10                                                           
*        CLI   FVIFLD+1,C'X'       TEST FOR EXEMPT OUTPUT TAX RATE              
*        BNE   EXITL                                                            
*        OI    SVTXINDS,TAXIXMPT                                                
*        B     VTXCX                                                            
VTXC10   TM    INPINDS,PROTVAL     ARE WE VALIDATING A PROTECTED FIELD?         
         BZ    VTXC30                                                           
         USING FHD,RF              THEN MUST LOOK AT THE FIELD ADDRESS          
         L     RF,FVADDR                                                        
         CLI   FHDA+1,C'*'                                                      
         BNE   EXITL                                                            
         B     VTXC35                                                           
VTXC30   CLI   FVIFLD+1,C'*'       TEST FOR STANDARD INPUT DEFAULT              
         BNE   EXITL                                                            
VTXC35   MVC   FVMSGNO,=AL2(AE$1IVTD)                                           
         TM    INPINDS,INPFNDID    1 TYPE MUST SPECIFIED AS DEFAULT             
         BNZ   EXITL                                                            
         OI    INPINDS,INPFNDID                                                 
         OI    SVTXINDS,TAXIDFLT                                                
*                                                                               
VTXC40   MVC   FVMSGNO,=AL2(AE$DUPIF) DUPLICATED INPUT FIELD                    
         LA    RF,TAXCTAB          MAKE SURE CODE IS NOT DUPLICATED             
         LA    RE,DISMAX           MAX # OF INPUT(OR OUTPUT) TYPES              
VTXC45   CLI   0(RF),0                                                          
         BE    VTXC50                                                           
         CLC   SVTXCODE,0(RF)                                                   
         BNE   *+8                                                              
         B     EXITL                                                            
         LA    RF,L'SVTXCODE(RF)   NEXT ENTRY                                   
         BCT   RE,VTXC45                                                        
*        CLI   FVIFLD,C'S'                                                      
*        BNE   *+6                                                              
*        DC    H'0'                                                             
VTXC50   MVC   0(L'SVTXCODE,RF),SVTXCODE  ADD CURRENT CODE TO TABLE             
VTXCX    OI    INPINDS,INPVAL      TAX CODE HAS BEEN VALIDATED                  
         B     EXITOK                                                           
         DROP  R4,RF                                                            
         SPACE 2                                                                
***********************************************************************         
* GET THE CORRECT TAX ELEMENT AND SAVE THE ADDRESS                              
***********************************************************************         
         SPACE 1                                                                
         USING SVTXELD,R3                                                       
         USING TAXELD,R4                                                        
GETTAXEL NTR1                                                                   
         XC    ATAXEL,ATAXEL                                                    
         LA    R4,TAXRFST                                                       
GTXEL05  CLI   TAXEL,0                                                          
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLC   TAXEL,SVTXCDE                                                    
         BE    GTXEL15                                                          
GTXEL10  ZIC   RE,TAXLN                                                         
         AR    R4,RE                                                            
         B     GTXEL05                                                          
*                                                                               
GTXEL15  CLC   TAXCODE,SVTXCODE    MUST BE SAME CODE                            
         BNE   GTXEL10                                                          
         ST    R4,ATAXEL           SAVE THE ELEMENT ADDRESS                     
         B     EXITOK                                                           
         DROP  R3,R4                                                            
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT FOR TAX TYPE                                            *         
*                                                                     *         
* R1 HOLDS EQUATED VERB                                               *         
* R2 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
* R3 HOLDS A(FIELD TABLE ENTRY)                                       *         
***********************************************************************         
         SPACE 1                                                                
TTYDTA   LA    RF,TTYTBL                                                        
         B     ITER                                                             
*                                                                               
TTYTBL   DC    AL1(DDIS),AL1(0,0,0),AL4(DISTTY)                                 
         DC    AL1(DVAL),AL1(0,0,0),AL4(VALTTY)                                 
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* DISPLAY TAX TYPE                                                    *         
***********************************************************************         
         SPACE 1                                                                
DISTTY   NI    FVATRB,FF-FVAPROT   UNPROTECT FIELD IF COMPANY RECORD            
         OC    ATAXEL,ATAXEL                                                    
         BNZ   DISTY10                                                          
         TM    VATINDS,VATOFREC                                                 
         BZ    *+8                                                              
         OI    FVATRB,FVAPROT      PROTECT FIELD IF OFFICE RECORD               
         B     EXITOK                                                           
DISTY10  L     R4,ATAXEL                                                        
         MVC   FVIFLD(L'TAXTYPE),TAXTYPE-TAXELD(R4) DISPLAY TAX TYPE            
         B     EXITOK                                                           
         SPACE 2                                                                
***********************************************************************         
* VALIDATE TAX TYPE                                                   *         
***********************************************************************         
         SPACE 1                                                                
         USING SVTXELD,R4                                                       
VALTTY   NI    FVATRB,FF-FVAPROT   UNPROTECT FIELD IF COMPANY RECORD            
         CLI   SVTXCODE,0          NO TAX CODE - EXIT                           
         BNE   VTTY10                                                           
         TM    VATINDS,VATOFREC                                                 
         BZ    *+8                                                              
         OI    FVATRB,FVAPROT      PROTECT FIELD IF OFFICE RECORD               
         B     EXITOK                                                           
*                                                                               
VTTY10   L     R4,ACURTXEL                                                      
         CLI   FVILEN,0                                                         
         BNE   *+8                 NO INPUT - ERROR                             
         B     EXITNO                                                           
         MVC   SVTXTYPE,FVIFLD                                                  
         B     EXITOK                                                           
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT FOR TAX ACCOUNT CODE FIELD                              *         
*                                                                     *         
* R1 HOLDS EQUATED VERB                                               *         
* R2 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
* R3 HOLDS A(FIELD TABLE ENTRY)                                       *         
***********************************************************************         
         SPACE 1                                                                
TACDTA   LA    RF,TACTBL                                                        
         B     ITER                                                             
*                                                                               
TACTBL   DC    AL1(DDIS),AL1(0,0,0),AL4(DISTAC)                                 
         DC    AL1(DVAL),AL1(0,0,0),AL4(VALTAC)                                 
         DC    AL1(DSRCH),AL1(0,0,0),AL4(SRCHTAC)                               
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* DISPLAY TAX ACCOUNT CODE                                            *         
***********************************************************************         
         SPACE 1                                                                
DISTAC   NI    FVATRB,FF-FVAPROT                                                
         OC    ATAXEL,ATAXEL       NO ELEMENT                                   
         BNZ   DTAC02                                                           
         TM    VATINDS,VATOFREC                                                 
         BZ    *+8                                                              
         OI    FVATRB,FVAPROT      PROTECT FIELD IF OFFICE RECORD               
         B     EXITOK                                                           
*                                                                               
DTAC02   DS    0H                                                               
         L     R4,ATAXEL                                                        
         MVC   FVIFLD(L'TAXACTA),TAXACTA-TAXELD(R4)  DISPLAY TAX CODE           
         B     EXITOK                                                           
         SPACE 2                                                                
***********************************************************************         
* VALIDATE TAX ACCOUNT CODE                                           *         
***********************************************************************         
         SPACE 1                                                                
         USING SVTXELD,R4                                                       
VALTAC   L     R4,ACURTXEL                                                      
         NI    FVATRB,FF-FVAPROT   UNPROTECT FIELD IF FIND TAX CODE             
*                                                                               
         CLI   SVTXCODE,0          NO TAX CODE - EXIT                           
         BNE   VTAC06                                                           
         TM    VATINDS,VATOFREC                                                 
         BZ    *+8                                                              
         OI    FVATRB,FVAPROT      PROTECT FILED, IF OFFICE RECORD              
         B     EXITOK                                                           
                                                                                
*        TM    VATINDS,VATOFREC    IF OFFICE RECORD, BOTH TAX CODE &            
*        BZ    VTAC06              SO GET THEM FROM SAVE TAX RECORD             
*        LA    R5,TAXRFST                                                       
*                                                                               
*PY      USING TAXRECD,RE                                                       
*        L     RE,AIO5                                                          
*        TM    VATINDS,VATCDFNO   DISPLAY COMPANY TAX RECORD                    
*        BZ    *+8                                                              
*        LA    R5,CPY.TAXRFST     COMPANY TAX RECORD                            
*        ZIC   RF,VALCNTER        VALIDATION COUNTER                            
*        SR    RE,RE                                                            
*                                                                               
*        USING TAXELD,R5                                                        
*TAC02   CLI   TAXEL,0                                                          
*        BNE   *+12                                                             
*        OI    FVATRB,FVAPROT      PROTECT FIELD, IF END OF RECORD.             
*        B     VALTACX                                                          
*        CLC   TAXEL,SVTAXEL                                                    
*        BE    *+14                                                             
*        IC    RE,TAXLN                                                         
*        AR    R5,RE                                                            
*        B     VTAC02                                                           
*        BCT   RF,*-10             FIND THE CORRECT ELEMENT                     
*                                                                               
*        MVC   SVTXCODE,TAXCODE    BUILD TAX ELEMENT FROM SAVE                  
*        MVI   SVTXLN,TAXLN1Q                                                   
*        MVC   SVTXINDS,TAXINDS                                                 
*        MVC   SVTXTYPE,TAXTYPE                                                 
*        DROP  R5,CPY                                                           
VTAC06   CLI   SVTXCODE,0          NO TAX CODE - EXIT                           
         BNE   VTAC08                                                           
         TM    INPINDS,INPVAL                                                   
         BNZ   VALTACX                                                          
         OI    VATINDS,VATNEFVT    NO ELEMENT FOR VAT TYPE                      
         B     VALTACX                                                          
*                                                                               
*TAC08   OI    INPINDS,INPVAL      TAX CODE FIELD HAS BEEN VALIDATED            
VTAC08   CLI   FVILEN,0            ERROR - NO ACCOUNT CODE                      
         BE    EXITNO                                                           
         SR    RF,RF                                                            
         IC    RF,FVILEN                                                        
         CLM   RF,1,ACTHILEN                                                    
         BH    *+14                                                             
         MVC   FVMSGNO,=AL2(AE$NLOWA)                                           
         B     EXITL               ACCOUNT MUST BE LOW LEVEL                    
*                                                                               
         TM    VATINDS,VATOFREC    TEST FOR OFFICE RECORD                       
         BZ    VTAC12                                                           
         SR    RF,RF                                                            
         ICM   RF,1,SLDGOPOS       TEST OFFICE IN ACCOUNT CODE                  
         BZ    VTAC12                                                           
         CLI   SLDGOPOS,LDGONKHI                                                
         BH    VTAC12                                                           
         MVC   FVMSGNO,=AL2(AE$MIXOF)   MIX OFFICE CODE                         
         LA    RE,X'0F'                                                         
         NR    RF,RE                                                            
         LA    RF,FVIFLD-1(RF)                                                  
         CLC   TAXKOFF(1),0(RF)                                                 
         BNE   EXITL                                                            
         TM    SLDGOPOS,LDGOKEY2   TEST FOR 2 CHAR. OFFICE                      
         BZ    VTAC12                                                           
         CLC   TAXKOFF+1(1),1(RF)                                               
         BNE   EXITL                                                            
         SPACE 1                                                                
VTAC12   MVC   SVTXACTA,FVIFLD                                                  
         SPACE 1                                                                
         LA    RF,ACCDTAB          SEE IF DUPLICATE ACCOUNT CODE                
         LA    R0,MAXTXEL          MAXIMUM ELEMENTS IN A RECORD                 
         MVC   FVMSGNO,=AL2(AE$DUPAC)                                           
VTAC14   OC    0(L'SVTXACTA,RF),0(RF)                                           
         BZ    VTAC16                                                           
         CLC   SVTXACTA,0(RF)                                                   
         BE    EXITL                                                            
         LA    RF,L'SVTXACTA(RF)   NEXT ACCOUNT CODE IN TABLE                   
         BCT   R0,VTAC14                                                        
VTAC16   MVC   0(L'SVTXACTA,RF),SVTXACTA  ADD CURRENT CODE TO TABLE             
         LA    R5,IOKEY            CHECK HIGH LEVEL ACCOUNT EXISTS              
         USING ACTRECD,R5                                                       
         MVC   ACTKEY,BCSPACES                                                  
         MVC   ACTKCPY,TAXKCPY                                                  
         MVC   ACTKUNT(L'TAXUL),TAXUL                                           
*                                                                               
         XR    RE,RE                                                            
         ICM   RE,1,ACTHILEN                                                    
         BZ    VTAC18              FORGET IT IF NO HIGHER LEVEL ACCOUNT         
         BCTR  RE,0                                                             
         EX    RE,*+4                                                           
         MVC   ACTKACT(0),FVIFLD                                                
         MVC   FVMSGNO,=AL2(AE$HLACM)                                           
         LHI   R1,XOREAD+XOACCDIR+XIO2                                          
         GOTO1 AIO                                                              
         BNE   EXITL                                                            
         SPACE 1                                                                
VTAC18   MVC   ACTKACT,SVTXACTA    SEE IF LOW LEVEL ACCOUNT EXISTS              
         L     R1,=AL4(XIO2+XOACCDIR+XORDUPD)                                   
         GOTO1 AIO                                                              
         BE    VTAC20                                                           
         TM    IOERR,IOEDEL        TEST ACCOUNT IS DELETED                      
         BZ    VALTACX                                                          
         MVC   FVMSGNO,=AL2(AE$RECID)                                           
         B     EXITL                                                            
         SPACE 1                                                                
VTAC20   L     R1,=AL4(XIO2+XOACCMST+XOGETRUP)                                  
         GOTO1 AIO                                                              
         BE    *+6                                                              
         DC    H'0'                BAD MASTER RECORD                            
         L     R5,AIO2                                                          
         MVC   ACTKEY,IOKEY                                                     
         PUSH  USING               CHECK THAT TAXEL AND THE STATUS              
         USING RSTELD,BOELEM       ELEMENT ARE CONSISTENT                       
         GOTO1 AGETEL,BOPARM,('RSTELQ',ACTRECD),0                               
         BE    VTAC22                                                           
         GOTO1 AADDRST,ACTRECD     ADD A RSTEL IF IT DOESN`T EXIST              
         B     VTAC25                                                           
VTAC22   CLI   RSTLN,RSTLN3Q                                                    
         BNL   VTAC26                                                           
         CLI   RSTLN,RSTLN2Q                                                    
         BNL   VTAC24                                                           
         XC    RSTSTAT2(RSTLN3Q-RSTLN1Q),RSTSTAT2                               
         MVI   RSTFILT5,C' '                                                    
         B     *+10                                                             
VTAC24   XC    RSTSTAT5-1(RSTLN3Q-RSTLN2Q),RSTSTAT5-1                           
         MVI   RSTLN,RSTLN3Q       NEW LENGTH                                   
         GOTO1 AREPEL,BOPARM,('RSTELQ',ACTRECD),0,BOELEM                        
VTAC25   LHI   R1,XOPUTREC+XOACCMST+XIO2                                        
         GOTO1 AIO                 CHANGE ACCOUNT RECORD                        
VTAC26   LA    RF,TAXIELQ                                                       
*&&UK*&& TM    RSTSTAT,RSTSIVAT                                                 
*&&US*&& TM    RSTSTAT2,RSTSIVAT                                                
         BNZ   *+8                                                              
         LA    RF,TAXOELQ                                                       
         MVC   FVMSGNO,=AL2(AE$WTOVA)                                           
         CLM   RF,1,SVTAXEL                                                     
         BE    *+8                                                              
         B     EXITL                                                            
         MVC   FVMSGNO,=AL2(AE$MIXOF)                                           
         GOTO1 OFTOFLT,RSTELD      TEST MIXED OFFICE CODE ON FILTER             
         BZ    *+14                                                             
         CLC   TAXKOFF(1),0(RF)                                                 
         BNE   EXITL                                                            
         SPACE 1                                                                
         TM    VATINDS,VATNEFVT                                                 
         BNZ   VTAC27                                                           
         CLI   CSACT,A#ADD         TEST ACTION ADD OR COPY                      
         BNE   VALTACX                                                          
         SPACE 1                                                                
* OVERIDE NAME & RATE WITH ACCOUNT RECORD VALUES                                
*                                                                               
VTAC27   GOTO1 AGETEL,BOPARM,('NAMELQ',ACTRECD),0                               
         BNE   VTAC28                                                           
         USING NAMELD,BOELEM                                                    
         ZIC   RE,NAMLN                                                         
         SH    RE,=Y(NAMLN1Q+1)                                                 
         LA    RF,TAXLN1Q+1(RE)                                                 
         STC   RF,SVTXLN            SAVE ELEMENT LENGTH                         
         EX    RE,*+4                                                           
         MVC   SVTXNAME(0),NAMEREC  SAVE ACCOUNT NAME                           
*                                                                               
VTAC28   GOTO1 AGETEL,BOPARM,('RATEVATQ',ACTRECD),0                             
         BNE   VALTACX                                                          
         USING RATELD,BOELEM                                                    
         MVC   SVTXRATE,RATRATE    SAVE RATE                                    
*ALTACX  OI    INPINDS,INPVAL                                                   
VALTACX  B     EXITOK                                                           
         POP   USING                                                            
         DROP  R4,R5                                                            
         SPACE 2                                                                
***********************************************************************         
* SEARCH ON A CLIENT CODE FIELD                                       *         
***********************************************************************         
         SPACE 1                                                                
SRCHTAC  GOTO1 VACSRCHC,BOPARM,('STMPSTRQ',FVADDR),ATWA,TAXUL,ACOM,    C        
               (X'14',0)                                                        
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT FOR TAX NAME                                            *         
*                                                                     *         
* R1 HOLDS EQUATED VERB                                               *         
* R2 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
* R3 HOLDS A(FIELD TABLE ENTRY)                                       *         
***********************************************************************         
         SPACE 1                                                                
TXNDTA   LA    RF,TXNTBL                                                        
         B     ITER                                                             
*                                                                               
TXNTBL   DC    AL1(DDIS),AL1(0,0,0),AL4(DISTXN)                                 
         DC    AL1(DVAL),AL1(0,0,0),AL4(VALTXN)                                 
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* DISPLAY TAX NAME                                                    *         
***********************************************************************         
         SPACE 1                                                                
         USING TAXELD,R4                                                        
DISTXN   NI    FVATRB,FF-FVAPROT                                                
         OC    ATAXEL,ATAXEL       NO ELEMENT                                   
         BNZ   DTXN02                                                           
         TM    VATINDS,VATOFREC                                                 
         BZ    *+8                                                              
         OI    FVATRB,FVAPROT      PROTECT FIELD IF OFFICE RECORD               
         B     EXITOK                                                           
*                                                                               
DTXN02   DS    0H                                                               
         L     R4,ATAXEL                                                        
         ZIC   RE,TAXLN                                                         
         SHI   RE,TAXLN1Q+1                                                     
         BM    EXITOK                                                           
         EX    RE,*+4                                                           
         MVC   FVIFLD(0),TAXNAME                                                
         B     EXITOK                                                           
         DROP  R4                                                               
         SPACE 2                                                                
***********************************************************************         
* VALIDATE TAX NAME                                                   *         
***********************************************************************         
         SPACE 1                                                                
         USING SVTXELD,R4                                                       
VALTXN   NI    FVATRB,FF-FVAPROT   UNPROTECT FIELD                              
         L     R4,ACURTXEL                                                      
         CLI   SVTXCODE,0          NO TAX CODE - EXIT                           
         BNE   VTXN02                                                           
         TM    VATINDS,VATOFREC                                                 
         BZ    *+8                                                              
         OI    FVATRB,FVAPROT      PROTECT FILED, IF OFFICE RECORD              
         B     EXITOK                                                           
*                                                                               
VTXN02   DS    0H                                                               
         OC    SVTXNAME,SVTXNAME                                                
         BZ    VTXN03              OVERRIDE NAME WITH ACCOUNT                   
         MVC   FVIFLD(L'SVTXNAME),SVTXNAME      RECORD VALUES                   
         OI    FVOIND,FVOXMT       RETRANSMIT                                   
         B     VALTXNX                                                          
*                                                                               
VTXN03   CLI   FVILEN,0            ERROR - NO ACCOUNT NAME                      
         BE    EXITNO                                                           
         ZIC   RE,FVXLEN                                                        
         LA    RF,TAXLN1Q+1(RE)    SET TAX ELEMENT LENGTH                       
         STC   RF,SVTXLN                                                        
         EX    RE,*+4                                                           
         MVC   SVTXNAME,FVIFLD                                                  
*                                                                               
VALTXNX  B     EXITOK                                                           
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* DATA OBJECT FOR TAX RATE                                            *         
*                                                                     *         
* R1 HOLDS EQUATED VERB                                               *         
* R2 HOLDS A(RECORD AT CORRECT LEVEL)                                 *         
* R3 HOLDS A(FIELD TABLE ENTRY)                                       *         
***********************************************************************         
         SPACE 1                                                                
TXRDTA   LA    RF,TXRTBL                                                        
         B     ITER                                                             
*                                                                               
TXRTBL   DC    AL1(DDIS),AL1(0,0,0),AL4(DISTXR)                                 
         DC    AL1(DVAL),AL1(0,0,0),AL4(VALTXR)                                 
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* DISPLAY TAX RATE                                                    *         
***********************************************************************         
         SPACE 1                                                                
         USING TAXELD,R4                                                        
DISTXR   NI    FVATRB,FF-FVAPROT                                                
         OC    ATAXEL,ATAXEL       NO ELEMENT                                   
         BNZ   DTXR02                                                           
         TM    VATINDS,VATOFREC                                                 
         BZ    *+8                                                              
         OI    FVATRB,FVAPROT      PROTECT FIELD IF OFFICE RECORD               
         B     EXITOK                                                           
*                                                                               
DTXR02   DS    0H                                                               
         L     R4,ATAXEL                                                        
         OC    TAXRATE,TAXRATE     DISPLAY RATE                                 
         BNZ   DTXR04                                                           
         MVC   FVIFLD(L'UC@ZERO),UC@ZERO                                        
         B     EXITOK                                                           
*                                                                               
DTXR04   CURED (2,TAXRATE),(6,FVIFLD),2,ALIGN=LEFT,DMCB=BODMCB                  
         B     EXITOK                                                           
         DROP  R4                                                               
         SPACE 2                                                                
***********************************************************************         
* VALIDATE TAX RATE                                                   *         
***********************************************************************         
         SPACE 1                                                                
         USING SVTXELD,R4                                                       
VALTXR   ZIC   RF,VALCNTER                                                      
         LA    RF,1(,RF)                                                        
         STC   RF,VALCNTER         INCREMENT THE COUNTER                        
         NI    FVATRB,FF-FVAPROT   UNPROTECT FIELD                              
         L     R4,ACURTXEL                                                      
         CLI   SVTXCODE,0          NO TAX CODE - EXIT                           
         BNE   VTXR01                                                           
         TM    VATINDS,VATOFREC    IF OFFICE, PROTECT FIELD                     
         BZ    *+8                                                              
         OI    FVATRB,FVAPROT                                                   
         B     EXITOK                                                           
*                                                                               
VTXR01   DS    0H                                                               
         OC    SVTXRATE,SVTXRATE                                                
         BZ    VTXR02              OVERRIDE RATE                                
         CURED (2,SVTXRATE),(6,FVIFLD),2,ALIGN=LEFT,DMCB=BODMCB                 
         OI    FVOIND,FVOXMT       RETRANSMIT                                   
         B     VALTXRX                                                          
*                                                                               
VTXR02   CLI   FVILEN,0            ERROR - NO ACCOUNT RATE                      
         BE    EXITNO                                                           
         ZIC   RF,FVILEN                                                        
         LA    RE,FVIFLD-1(RF)       ALLOW PERCENTAGE SIGN AT END               
         CLI   0(RE),C'%'                                                       
         BNE   *+6                                                              
         BCTR  RF,0                                                             
         GOTO1 VCASHVAL,BOPARM,(2,FVIFLD),(RF)                                  
         CLI   BOPARM,FF                                                        
         BE    EXITNOTN            NOT A NUMBER                                 
         MVC   FVMSGNO,=AL2(AE$INVVR)                                           
         CLC   4(4,R1),=F'10000'   ENSURE REASONABLE TAX RATE                   
         BH    EXITL                                                            
         MVC   SVTXRATE,BOPARM+6                                                
         OC    SVTXRATE,SVTXRATE                                                
         BNZ   *+10                                                             
         MVC   FVIFLD,UC@ZERO                                                   
VALTXRX  LA    R4,SVTXELLN(R4)                                                  
         ST    R4,ACURTXEL          NEXT LINE IN BUFFER                         
         B     EXITOK                                                           
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO FIND A(ACCOUNT FILTER) FOR OFFICE                        *         
*                                                                     *         
* NTRY: R1=A(STATUS ELEMENT)                                          *         
* EXIT: RF=A(ACCOUNT FILTER) OR CC=ZERO IF THERE IS NOT ONE           *         
***********************************************************************         
         SPACE 1                                                                
         USING RSTELD,R1                                                        
OFTOFLT  XR    RF,RF                                                            
         TM    VATINDS,VATOFREC     TEST IS AN OFFICE RECORD                    
         BZ    OFTOFLTX                                                         
*                                                                               
         CLI   SLDGOPOS,LDGOFLT1   TEST IS ACCOUNT FILTER 1                     
         BL    OFTOFLTX            (OFFICE NOT ON A FILTER)                     
         BNE   *+12                                                             
         LA    RF,RSTFILT1                                                      
         B     OFTOFLTX                                                         
*                                                                               
         CLI   SLDGOPOS,LDGOFLT2   TEST IS ACCOUNT FILTER 2                     
         BNE   *+12                                                             
         LA    RF,RSTFILT2                                                      
         B     OFTOFLTX                                                         
*                                                                               
         CLI   SLDGOPOS,LDGOFLT3   TEST IS ACCOUNT FILTER 3                     
         BNE   *+12                                                             
         LA    RF,RSTFILT3                                                      
         B     OFTOFLTX                                                         
*                                                                               
         CLI   SLDGOPOS,LDGOFLT4   TEST IS ACCOUNT FILTER 4                     
         BNE   *+12                                                             
         LA    RF,RSTFILT4                                                      
         B     OFTOFLTX                                                         
*                                                                               
         CLI   SLDGOPOS,LDGOFLT4+1 TEST IS ACCOUNT FILTER 5                     
         BNE   *+8                                                              
         LA    RF,RSTFILT5                                                      
*                                                                               
OFTOFLTX LTR   RF,RF                                                            
         BR    RE                                                               
         DROP  R1                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO DISPLAY COMPANY TAX RECORD                               *         
***********************************************************************         
         SPACE 1                                                                
DISREC   ST    RE,AREGSAV          SAVE REGISTER E                              
         L     RF,ATAXCODE         A(TAX CODE FIELD)                            
         SR    R5,R5                                                            
         SR    R4,R4                                                            
         LA    RE,MAXTXEL                                                       
         MH    RE,=H'5'            NUMBER OF ALL FIELDS                         
         B     *+10                                                             
DREC02   IC    R5,FVTLEN-FVIHDR(RF)                                             
         AR    RF,R5               NEXT FIELD                                   
*                                                                               
         IC    R4,FVXLEN-FVIHDR(RF)                                             
         EX    R4,*+4                                                           
         MVC   FVIFLD-FVIHDR(0,RF),BCSPACES   CLEAR FIELD                       
         OI    FVOIND-FVIHDR(RF),FVOXMT       TRANSMINT FIELD                   
         BCT   RE,DREC02                                                        
*                                                                               
CPY      USING TAXRECD,R4                                                       
*                                                                               
         L     RF,ATAXCODE                                                      
         L     R4,AIO5             COMPANY TAX RECORD                           
         LA    R4,CPY.TAXRFST                                                   
         LA    RE,MAXTXEL          MAXIMUM NUMBER OF TAX ELEMENTS               
         DROP  CPY                                                              
*                                                                               
         USING TAXELD,R4                                                        
DREC04   CLI   TAXEL,0             ALL ELEMENT DONE?                            
         BE    DISRECX                                                          
         CLC   TAXEL,SVTAXEL       INPUT ELEMENT                                
         BNE   DREC08                                                           
         MVC   FVIFLD-FVIHDR(L'TAXCODE,RF),TAXCODE  DISPLAY TAX CODE            
         TM    TAXINDS,TAXIDFLT    DEFAULT INPUT TAX RATE                       
         BZ    *+8                                                              
         MVI   FVIFLD-FVIHDR+1(RF),C'*'                                         
         IC    R5,FVTLEN-FVIHDR(RF)                                             
         AR    RF,R5                                                            
         MVC   FVIFLD-FVIHDR(L'TAXTYPE,RF),TAXTYPE  DISPLAY TAX TYPE            
         LA    R0,4                                                             
         IC    R5,FVTLEN-FVIHDR(RF)  JUMP TO NEXT LINE                          
         AR    RF,R5                                                            
         BCT   R0,*-6                                                           
         BCT   RE,DREC08             NEXT TAX LINE                              
         B     DISRECX                                                          
*                                                                               
DREC08   IC    R5,TAXLN                                                         
         AR    R4,R5                                                            
         B     DREC04                                                           
*                                                                               
DISRECX  L     RE,AREGSAV          RESTORE REGISTER E                           
         BR    RE                                                               
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* NTRSES OBJECT                                                       *         
*                                                                     *         
* P1 HOLDS EQUATED OBJECT                                             *         
* P2 HOLDS EQUATED VERB                                               *         
* P3 HOLDS A(BLOCK) COVERED BY SSAVD                                  *         
***********************************************************************         
         SPACE 1                                                                
         USING SSAVD,R2                                                         
NTRSES   LM    R0,R3,SVPARMS                                                    
         LA    RF,NSSTABL                                                       
         B     ITER                                                             
*                                                                               
NSSTABL  DC    AL1(SNTROUT),AL1(0,0,0),AL4(NTROUT)                              
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* BUILD PARAMETER LIST FOR NTRSES TRANSFER                            *         
***********************************************************************         
         SPACE 1                                                                
NTROUT   CLI   SACT,A#LST          ARE WE GOING TO LIST A RECORD?               
         BNE   EXITOK                                                           
         CLI   SREC,R#VATRU        VAT RULE RECORD                              
         BE    *+12                                                             
         CLI   SREC,R#FILE         FILE RECORD                                  
         BNE   EXITOK                                                           
         OI    SNINDS1,SNIPARMS                                                 
         B     EXITOK                                                           
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* LIST OBJECT                                                         *         
*                                                                     *         
* P1 HOLDS EQUATED OBJECT                                             *         
* P2 HOLDS EQUATED VERB                                               *         
* P3 HOLDS CURRENT KEY BUILD AREA                                     *         
* P4 HOLDS PREVIOUS KEY                                               *         
***********************************************************************         
         SPACE 1                                                                
THIS     USING TAXRECD,R2                                                       
LAST     USING TAXRECD,R3                                                       
                                                                                
LIST     LM    R0,R3,SVPARMS                                                    
         LA    RF,LISTABL                                                       
         B     ITER                                                             
*                                                                               
LISTABL  DC    AL1(LGETFRST),AL1(0,0,0),AL4(FLST)                               
         DC    AL1(LGETNEXT),AL1(0,0,0),AL4(NLST)                               
         DC    AL1(LLSTFRST),AL1(0,0,0),AL4(FTFLST)                             
         DC    AL1(EOT)                                                         
         SPACE 2                                                                
***********************************************************************         
* FIRST TIME FOR LIST                                                 *         
***********************************************************************         
         SPACE 1                                                                
FTFLST   XC    THIS.TAXKDATE,THIS.TAXKDATE    READ FROM BEGINNING               
         B     EXITOK                                                           
         SPACE 2                                                                
***********************************************************************         
* FIRST FOR LIST PAGE                                                 *         
***********************************************************************         
         SPACE 1                                                                
FLST     MVC   IOKEY(L'TAXKEY),THIS.TAXKEY                                      
         LHI   R1,XOHIGH+XOACCDIR+XIO11                                         
         GOTO1 AIO                                                              
         BNE   EXITL                                                            
         B     NLST02                                                           
         SPACE 2                                                                
***********************************************************************         
* NEXT FOR LIST                                                       *         
***********************************************************************         
         SPACE 1                                                                
X        USING TAXRECD,IOKEY                                                    
NLST     LHI   R1,XOSEQ+XOACCDIR+XIO11                                          
         GOTO1 AIO                                                              
         BNE   EXITL               END OF FILE                                  
*                                                                               
NLST02   CLI   X.TAXKTYP,TAXKTYPQ                                               
         BNE   EXITL               EXIT - NOT A TAX RECORD                      
         CLC   X.TAXKCPY,CUABIN                                                 
         BNE   EXITL               EXIT - CHANGE COMPANY                        
         MVC   THIS.TAXKEY(ACCKLEN),IOKEY                                       
         B     EXITOK                                                           
*                                                                               
         CLC   X.TAXKOFF,BCSPACES                                               
         BNH   NLST                MUST HAVE AN OFFICE CODE                     
         LHI   R1,XOREAD+XOACCDIR+XIO5                                          
         GOTO1 AIO                                                              
         BNE   NLST                                                             
         LHI   R1,XOGET+XOACCMST+XIO5                                           
         GOTO1 AIO                                                              
         BE    *+6                                                              
         DC    H'0'                CAN'T FIND TAX RECORD                        
*                                                                               
         GOTO1 AGETEL,BOPARM,(SVTAXEL,AIO5),0                                   
         BNE   NLST                GET NEXT - DIFFERENT VAT TYPE                
*                                                                               
         MVC   THIS.TAXKEY(ACCKLEN),IOKEY                                       
         B     EXITOK                                                           
         DROP  THIS,LAST,X                                                      
         EJECT                                                                  
***********************************************************************         
* LITERALS & CONSTANTS                                                *         
***********************************************************************         
         SPACE 1                                                                
         LTORG                                                                  
FF       EQU   X'FF'                                                            
STMPSTRQ EQU   X'03'               TEMPSTORE PAGE NO. FOR USE IN SEARCH         
DISMAX   EQU   7                   MAX INPUT OR OUTPUT TAX ENTRIES              
MAXTXEL  EQU   14                  MAXIMUM TAX ELEMENTS IN TAX RECORD           
TAXPLN   EQU   10                                                               
*                                                                               
EFFS     DC    8XL1'FF'                                                         
DEPTUL   DC    C'2D'               DEPARTMENT UNIT LEDGER                       
TAXUL    DC    C'SG'               TAX UNIT LEDGER                              
*                                                                               
DCLISTU  DS    0D                                                               
         DCDDL AC#LAST,L'UC@LAST,L                                              
         DCDDL AC#PRV,L'UC@PRV,L                                                
         DCDDL AC#ERLR,L'UC@ERLR,L                                              
         DCDDL AC#TODAY,L'UC@TODAY,L                                            
         DCDDL AC#ZERO,L'UC@ZERO,L                                              
         DCDDL AC#INP,L'UC@VTINP,L                                              
         DCDDL AC#OTPT,L'UC@VTOUT,L                                             
DCLISTUX DC    X'00'                                                            
*                                                                               
DCLISTL  DS    0D                                                               
         DCDDL AC#VATIT,L'LC@VATIT,L                                            
         DCDDL AC#VATOT,L'LC@VATOT,L                                            
DCLISTLX DC    X'00'                                                            
         EJECT                                                                  
***********************************************************************         
* INCLUDED BOOKS                                                      *         
***********************************************************************         
         SPACE 1                                                                
* ACFILWORK                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACFILWORK                                                      
         PRINT ON                                                               
         SPACE 2                                                                
***********************************************************************         
* DSECT TO COVER SAVED STORAGE @ TWUSER                               *         
***********************************************************************         
         SPACE                                                                  
TWAD     DSECT                                                                  
         ORG   TWUSER                                                           
SAVEVALS EQU   *                                                                
AREGSAV  DS    A                   A(SAVE REGISTER)                             
ATAXCODE DS    A                   A(TAX CODE FIELD)                            
AEFFDATE DS    A                   A(EFFECTIVE DATE FIELD)                      
ATAXEL   DS    A                   A(TAXEL)                                     
ACURTXEL DS    A                   A(CURRENT SAVE TAX ELEMENT)                  
         SPACE 1                                                                
SVTAXEL  DS    CL1                 SAVE TAX ELEMENT CODE                        
SVFLDN   DS    XL2                 SAVED FIELD NUMBER                           
EFFDATE  DS    PL3                 SAVE EFFECTIVE DATE (PWOS)                   
DISCNTER DS    XL1                 DISPLAY  TAX ELEMENT COUNTER                 
VALCNTER DS    XL1                 VALIDATE TAX ELEMENT COUNTER                 
ACTHILEN DS    XL1                 LENGTH OF HIGH LEVEL ACCOUNT                 
SLDGOPOS DS    XL1                 SAVED LDGOPOS                                
STAXDATE DS    PL3                                                              
SPRVCODE DS    CL2                 SAVED PROVENCE                               
         SPACE 1                                                                
FILINDS1 DS    XL1                 * FILE INDICATORS *                          
FILADDAC EQU   X'80'               ADD ACCOUNT RECORED, NOT WRITE               
FILWRINM EQU   X'40'               WRITE NAME CHANGE POINTER                    
FILDNTAX EQU   X'20'                                                            
         SPACE 1                                                                
VATINDS  DS    XL1                 * GENERAL INDICATORS *                       
VATCOFF  EQU   X'80'               COMPANY IS ON OFFICES                        
VATNOFFS EQU   X'40'               NEW OFFICE SYSTEM                            
VATCPREC EQU   X'20'               COMPANY RECORD                               
VATOFREC EQU   X'10'               OFFICE RECORD                                
VATCDFNO EQU   X'08'               COMPANY DISPLAY FOR A NEW OFFICE             
VATNEFVT EQU   X'04'               NO ELEMENTS FOR VAT TYPE                     
VATINPUT EQU   X'02'               INPUT  TYPE                                  
VATOUTPT EQU   X'01'               OUTPUT TYPE                                  
         SPACE 1                                                                
INPINDS  DS    XL1                 * INPUT INDICATORS *                         
INPFNDID EQU   X'80'               FOUND STANDARD INPUT DEFAULT                 
INPFNDCD EQU   X'40'               FOUND AT LEAST ONE CODE INPUT                
INPVAL   EQU   X'20'               TAX CODE FIELD HAS BEEN VALIDATED            
PROTVAL  EQU   X'10'               VALIDATE PROTECTED TAX CODE FIELD            
         SPACE 1                                                                
DATTYPE  DS    XL1                 * TYPE OF INPUT TO DATE *                    
DATTPREV EQU   0                   'PREVIOUS' KEYWORD ENTERED                   
DATTTDAY EQU   1                   'TODAY' KEYWORD ENTERED                      
DATTLAST EQU   2                   'LAST' KEYWORD ENTERED                       
DATTDATE EQU   3                   ACTUAL DATE ENTERED                          
         SPACE 1                                                                
SAVEKEY  DS    XL(L'TAXKEY)                                                     
NAMESAVE DS    CL(NAMLN1Q+L'NAMEREC)                                            
         ORG   TWUSER+L'TWUSER-(*-SAVEVALS)                                     
         EJECT                                                                  
***********************************************************************         
* OVERLAY WORKING STORAGE                                             *         
***********************************************************************         
         SPACE 1                                                                
OVERWRKD DSECT                                                                  
CALLR1   DS    A                                                                
SVPARMS  DS    0XL24                                                            
SVPARMS1 DS    A                                                                
SVPARMS2 DS    A                                                                
SVPARMS3 DS    A                                                                
SVPARMS4 DS    A                                                                
SVPARMS5 DS    A                                                                
SVPARMS6 DS    A                                                                
*                                                                               
TAXCTAB  DS    7CL(L'TAXCODE)      TABLE OF INPUTTED TAX CODES                  
ACCDTAB  DS    14CL(L'TAXACTA)     TABLE OF INPUTTED ACCOUNT CODES              
         SPACE 1                                                                
DSLISTU  DS    0C                  DICTIONARY EQUATES USED                      
UC@LAST  DS    CL(TAXPLN)                                                       
UC@PRV   DS    CL(TAXPLN)                                                       
UC@ERLR  DS    CL(TAXPLN)                                                       
UC@TODAY DS    CL(TAXPLN)                                                       
UC@ZERO  DS    CL(TAXPLN)                                                       
UC@VTINP DS    CL(TAXPLN)                                                       
UC@VTOUT DS    CL(TAXPLN)                                                       
DSLISTL  DS    0C                                                               
LC@VATIT DS    CL(TAXPLN)                                                       
LC@VATOT DS    CL(TAXPLN)                                                       
         SPACE 2                                                                
OVERWRKN EQU   *-OVERWRKD                                                       
         EJECT                                                                  
*--------------------------------------------------------------------*          
*        DSECT                                                       *          
*--------------------------------------------------------------------*          
         SPACE 2                                                                
SVTXELD  DSECT                     ** SAVE TAX ELEMENT **                       
SVTXCDE  DS    CL(L'TAXEL)         ELEMENT CODE                                 
SVTXLN   DS    CL(L'TAXLN)         ELEMENT LENGTH                               
SVTXTYPE DS    CL(L'TAXTYPE)       TAX TYPE                                     
SVTXACTA DS    CL(L'TAXACTA)       TAX ACCOUNT CODE                             
SVTXRATE DS    CL(L'TAXRATE)       TAX RATE                                     
SVTXCODE DS    CL(L'TAXCODE)       TAX CODE                                     
SVTXINDS DS    CL(L'TAXINDS)       TAX INDICATOR                                
SVTXNAME DS    CL(L'TAXNAME)       TAX NAME                                     
SVTXELLN EQU   *-SVTXELD                                                        
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'160DCUR1DA   05/01/02'                                      
         END                                                                    
