*          DATA SET SCACINP46C AT LEVEL 020 AS OF 03/09/16                      
********************************************************************            
* ACC / INPUT, TYPE 46 (CANADA)                                    *            
********************************************************************            
#SCRIPT ACINP46C                                                                
*                                                                               
SETWP 0;LBLW W1;ADDWP 1;LBLW W2;     //SET UP WORKING STORAGE                   
USRI 8;SYSS ACC;PRGS INP;PWDI 8;$CT;                                            
TSTOK;EXITNE;                                                                   
FU;TAB;INPS BATCH;TAB;INPS OPEN;ENTER;                                          
TSTOK;EXITNE;                                                                   
* READ THE BATCH HEADER DATA RECORD                                             
READ;EXITNE;                                                                    
* BATCH REF     BATCH NAME            BATCH TYPE                                
LOCNUM 3;INPI 4;LOCNUM 5;INPI 15;LOCNUM 7;INPI 3;                               
*   MOA        EFF DT                                                           
LOCNUM 8;INPI 6;LOCNUM 12;INPI 8;                                               
*   ITM CNT      BATCH TOT                                                      
LOCNUM 20;INPS 250;LOCNUM 24;INPS 999999999;                                    
*   COMMENTS                                                                    
LOCNUM 32;INPI 50;                                                              
*----------------------------        HANDLE ENDING STATUS                       
LOCWRK W1;                                                                      
CMIS " ";BE S1;CMIS C;BE S1;CMIS c;BNE S2;                                      
.S1;MVSW C;BR S7;                                                               
.S2;                                                                            
CMIS S;BE S3;CMIS s;BNE S4;                                                     
.S3;MVSW S;BR S7;                                                               
.S4;                                                                            
CMIS U;BE S5;CMIS u;BNE S6;                                                     
.S5;MVSW U;BR S7;                                                               
.S6;OUTERR "EA#9999 INVALID ENDING STATUS FOR BATCH";                           
WRITE;EXIT;                                                                     
.S7;                                                                            
*----------------------------                                                   
ENTER;TSTOK;BE L0;                                                              
* NOTE THAT NEXT READ FORCES PREVIOUS WRITE !!! (MH JAN/95)                     
SAVERR;RESO;WRITE;READ;EXIT;                                                    
.L0;                                                                            
* NEED TO WRITE A RESULT CODE TO BATCH HEADER RECORD!                           
FSTPRO;RESO;WRITE;                                                              
*------------------------------------------------------------------             
* INPUT SCREEN SHOULD BE IN PLACE NOW -- POSN TO DOCUMENT NUMBER                
*------------------------------------------------------------------             
.L1;                                                                            
READ;BNE L9;                                                                    
NTHUNP 7;INPS "//";ENTER;                                                       
NTHUNP 7;                                                                       
* DOC NO   DATE       URGENT                                                    
INPI 6;TAB;INPI 8;TAB;INPI 1;TAB;                                               
* CASH      PROD VNDR   C/D        EXP VNDR    C/D                              
INPI 15;TAB;INPI 15;TAB;INPI 1;TAB;INPI 15;TAB;INPI 1;TAB;                      
* TYPE                                                                          
INPI 1;TAB;                                                                     
* GROSS / NET                                                                   
CMIS "G";BNE F3;INPI 1;BR F4;  // "G" GROSS                                     
.F3;INPS N;                    // NET "N" IF NOT GROSS "G"                      
.F4;                                                                            
* NARRATIVES                                                                    
TAB;SETIP 139;INPI 55;TAB;INPI 55;TAB;                                          
* PST PROV                                                                      
SETIP 64;INPI 2;TAB;                                                            
*------------------------------------------------------------------             
* 2ND HALF                                                                      
*------------------------------------------------------------------             
* WC COM   NCOM       CPJ / EXP ACCT                                            
INPI 2;TAB;INPI 2;TAB;INPI 14;TAB;                                              
LOCWRK W2;MVSW "              ";LOCWRK W2;                                      
CMIS "ººººººººººººººº";BNH F5;                                                  
* OFFICE               DEPARTMENT             STAFF                             
MVIW 15;LOCWRK W2;                                                              
INPW 2;BACKUP;INPS ",";INPW 4;BACKUP;INPS ",";INPW 9;BR F6;                     
.F5;ADDIP 15;                                                                   
.F6;TAB;LOCWRK W2;MVSW "              ";LOCWRK W2;                              
CMIS "ºººººº";BNH F8;                                                           
* CLIENT               PRODUCT    AMOUNT                                        
MVIW 6;LOCWRK W2;                                                               
INPW 3;BACKUP;INPS ",";INPW 3;BR F9;                                            
* GST AMOUNT                                                                    
.F8;ADDIP 6;                                                                    
.F9;TAB;INPI 11;NTHUNP 45;INPI 11;                                              
*------------------------------------------------------------------             
* TRY TO SWTICH TO GST SCREEN                                                   
*------------------------------------------------------------------             
PF07;                                                                           
.G0;                                                                            
NTHUNP 7;NXTPRO;                                                                
* CHECK WE ARE IN GST SCREEN. NO, ERROR OUT                                     
CMTS GST;BNE A1;                                                                
*         PST TYPE   PST AMOUNT                                                 
NTHUNP 11;INPI 1;TAB;INPI 11;                                                   
ENTER;TSTOK;BE G2;                                                              
* SAVE ERROR AND RETURN TO INVOICE SCREEN TO GET NEXT RECORD                    
* TEST PST SPECIFIC ERRORS ON 2ND SCREEN                                        
LOCCUR;TSTLIN 12;BH G1;                                                         
PF15;ENTER;SAVERR;RESO;WRITE;BR L1;                                             
.G1;                                                                            
SAVERR;RESO;WRITE;PF15;BR L1;                                                   
*------------------------------------------------------------------             
* RETURN TO INVOICE SCREEN                                                      
*------------------------------------------------------------------             
.G2;                                                                            
PF15;ENTER;                                                                     
TSTOK;BNE X1;                                                                   
.A1;                                                                            
TSTOK;BNE X1;                                                                   
FSTPRO;RESO;WRITE;                                                              
BR L1;                                                                          
*------------------------------------------------------------------             
* ERRORS!                                                                       
*------------------------------------------------------------------             
.X1;SAVERR;RESO;WRITE;                                                          
BR L1;                                                                          
*-------------------------------------------------------------------            
* THIS ENDS THE BATCH BASED ON THE ENDING STATUS                                
*-------------------------------------------------------------------            
.L9;                                                                            
LOCWRK W1;CMWS S;BNE R1;PF02;EXIT;         // SAVE                              
.R1;LOCWRK W1;CMWS U;BNE R2;PF06;BR R3;    // UPDATE                            
.R2;PF05;                                  // CLOSE                             
.R3;TSTOK;EXITNE;                                                               
NTHUNP 7;INPS &&;TAB;INPS &&;ENTER;        // TELL BATCH TO SET                 
TSTOK;EXITNE;                              // CONTROL TOTALS ITSELF             
END;                                                                            
