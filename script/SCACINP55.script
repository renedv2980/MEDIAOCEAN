*          DATA SET SCACINP55  AT LEVEL 006 AS OF 03/16/09                      
********************************************************************            
* ACC / INPUT, TYPE 55 (USA)                                       *            
********************************************************************            
#SCRIPT ACINP55                                                                 
SETWP 0;LBLW W1;                     // SET UP WORKING STORAGE                  
USRI 8;SYSS ACC;PRGS INP;PWDI 8;$CT;                                            
TSTOK;EXITNE;                                                                   
FU;TAB;INPS BATCH;TAB;INPS OPEN;ENTER;                                          
TSTOK;EXITNE;                                                                   
* READ THE BATCH HEADER DATA RECORD                                             
READ;EXITNE;                                                                    
* BATCH REF     BATCH NAME            BATCH TYPE                                
LOCNUM 3;INPI 4;LOCNUM 5;INPI 15;LOCNUM 7;INPI 3;                               
*   MOA        EFF DT                                                           
LOCNUM 8;INPI 6;LOCNUM 12;INPI 8;                                               
*   ITM CNT      BATCH TOT                                                      
LOCNUM 20;INPS 250;LOCNUM 24;INPS 999999999;                                    
*   COMMENTS                                                                    
LOCNUM 32;INPI 50;                                                              
*----------------------------        HANDLE ENDING STATUS                       
LOCWRK W1;                                                                      
CMIS " ";BE S1;CMIS C;BE S1;CMIS c;BNE S2;                                      
.S1;MVSW C;BR S7;                                                               
.S2;                                                                            
CMIS S;BE S3;CMIS s;BNE S4;                                                     
.S3;MVSW S;BR S7;                                                               
.S4;                                                                            
CMIS U;BE S5;CMIS u;BNE S6;                                                     
.S5;MVSW U;BR S7;                                                               
.S6;OUTERR "EA#9999 INVALID ENDING STATUS FOR BATCH";                           
WRITE;EXIT;                                                                     
.S7;                                                                            
*----------------------------                                                   
ENTER;TSTOK;BE L0;                                                              
SAVERR;RESO;WRITE;READ;EXIT;                                                    
.L0;                                                                            
* NEED TO WRITE A RESULT CODE TO BATCH HEADER RECORD !                          
FSTPRO;RESO;WRITE;                                                              
*-------------------------------------------------------------------            
* INPUT SCREEN SHOULD BE IN PLACE NOW -- POSN TO BILL DATE                      
*-------------------------------------------------------------------            
.L1;                                                                            
READ;BNE L9;                                                                    
NTHUNP 7;                                                                       
* BILL DATE   DUE DATE                                                          
INPI 8;TAB;INPI 9;TAB;                                                          
* SYSTEM/MEDIA                                                                  
INPI 25;TAB;                                                                    
* CLIENT   PRODUCT    EST/JOB    ADV MONTH  DESCRIPTION                         
INPI 3;TAB;INPI 3;TAB;INPI 6;TAB;INPI 6;TAB;INPI 36;TAB;                        
* BILL NUM AOR BILL AMT    NET BILL                                             
INPI 6;TAB;TAB;INPI 11;TAB;INPI 11;TAB;                                         
* COMM     SPCL AMT     A/R ACCT    A/P ACCT                                    
INPI 20;TAB;INPI 11;TAB;INPI 14;TAB;INPI 15;TAB;                                
* CR OFF   ANL OFF    DEPT       PERSON                                         
INPI 2;TAB;INPI 2;TAB;INPI 3;TAB;INPI 10;TAB;                                   
* BILL SRC  CASH DISC   SPCL NUM    NARR 1      NARR 2                          
INPI 12;TAB;INPI 11;TAB;INPI 60;TAB;INPI 50;TAB;INPI 50;                        
ENTER;TSTOK;BNE X1;                                                             
FSTPRO;RESO;WRITE;                                                              
BR L1;                                                                          
*-------------------------------------------------------------------            
* ERRORS!                                                                       
*-------------------------------------------------------------------            
.X1;                                                                            
SAVERR;RESO;WRITE;                                                              
BR L1;                                                                          
*-------------------------------------------------------------------            
* THIS ENDS THE BATCH BASED ON THE ENDING STATUS                                
*-------------------------------------------------------------------            
.L9;                                                                            
LOCWRK W1;CMWS S;BNE R1;PF02;EXIT;         // SAVE                              
.R1;LOCWRK W1;CMWS U;BNE R2;PF06;BR R3;    // UPDATE                            
.R2;PF05;                                  // CLOSE                             
.R3;TSTOK;EXITNE;                                                               
NTHUNP 7;INPS &&;TAB;INPS &&;ENTER;        // TELL BATCH TO SET                 
TSTOK;EXITNE;                              // CONTROL TOTALS ITSELF             
END;                                                                            
