*          DATA SET SCACINP53  AT LEVEL 004 AS OF 05/29/02                      
********************************************************************            
* ACC / INPUT, TYPE 53                                             *            
********************************************************************            
*                                                                               
* SET UP WORKING STORAGE                                                        
SETWP 0;LBLW W1;                                                                
USRI 8;SYSS ACC;PRGS INP;PWDI 8;$CT;                                            
TSTOK;EXITNE;                                                                   
FU;TAB;INPS BATCH;TAB;INPS OPEN;ENTER;                                          
TSTOK;EXITNE;                                                                   
* READ THE BATCH HEADER DATA RECORD                                             
READ;EXITNE;                                                                    
* BATCH REF     BATCH NAME            BATCH TYPE                                
LOCNUM 3;INPI 4;LOCNUM 5;INPI 15;LOCNUM 7;INPI 3;                               
*   MOA        EFF DT                                                           
LOCNUM 8;INPI 6;LOCNUM 12;INPI 8;                                               
*   ITM CNT      BATCH TOT                                                      
ADDIP 1;LOCNUM 20;INPI 3;LOCNUM 24;INPS 999999999;                              
*   COMMENTS                                                                    
LOCNUM 32;INPI 50;                                                              
*------------------------------------------------------------------             
LOCNUM 32;                                                                      
SETIP 36;CMIS "Y";BE K1;                // DEBITS EQUAL CREDITS                 
OUTERR "EA#9999 DEBITS <> CREDITS";WRITE;                                       
LOCWRK W1;MVSW "E";                     // WE HAVE AN ERROR                     
.K1;ENTER;TSTOK;BE L0;                                                          
* NOTE THAT NEXT READ FORCES PREVIOUS WRITE !!! (MH JAN/95)                     
.E3;SAVERR;RESO;WRITE;READ;EXIT;                                                
.L0;                                                                            
* NEED TO WRITE A RESULT CODE TO BATCH HEADER RECORD!                           
LOCWRK W1;CMWS "E";BE L1;                                                       
FSTPRO;RESO;WRITE;                                                              
*------------------------------------------------------------------             
* INPUT SCREEN SHOULD BE IN PLACE NOW -- POSN TO DOCUMENT NUMBER                
*------------------------------------------------------------------             
.L1;                                                                            
READ;BNE L9;                                                                    
NTHUNP 7;                                                                       
* DOC NO   DATE                                                                 
INPI 6;TAB;INPI 8;TAB;TAB;TAB;                                                  
*------------------------------------------------------------------             
* INPUT LINE                                                                    
*------------------------------------------------------------------             
* DEBIT    ACCOUNT     CONTRA                                                   
INPI 1;TAB;INPI 14;TAB;INPI 14;TAB;                                             
* OFF,DEPT,PERSON,ANOF                                                          
CMIS "              ";BNE D1;INPI 14;BR D3;                                     
.D1;                                                                            
LOCWRK W1;MVSW "              ";                                                
LOCWRK W1;MVIW 14;SUBIP 14;      // NEED SUBIP FOR THE PC                       
NTHUNP 14;LOCWRK W1;             // TO POINT CURSOR CORRECTLY                   
INPW 2;BACKUP;INPS ",";INPW 3;BACKUP;INPS ",";                                  
INPW 7;BACKUP;INPS ",";INPW 2;                                                  
ADDIP 14;                        // POINT BACK TO NEXT FIELD                    
.D3;                                                                            
TAB;                                                                            
* CLI,PRD                                                                       
CMIS "      ";BNE D4;INPI 6;BR D6;                                              
.D4;                                                                            
LOCWRK W1;MVSW "              ";                                                
LOCWRK W1;MVIW 6;SUBIP 6;        // NEED SUBIP FOR THE PC                       
NTHUNP 15;LOCWRK W1;             // TO POINT CURSOR CORRECTLY                   
INPW 3;BACKUP;                                                                  
CMWS "   ";BE D5;                                                               
INPS ",";INPW 3;                                                                
.D5;                                                                            
ADDIP 6;                         // POINT BACK TO NEXT FIELD                    
.D6;                                                                            
TAB;                                                                            
* AMOUNT                                                                        
INPI 11;                                                                        
*------------------------------------------------------------------             
* NARRATIVES 1 AND 2                                                            
*------------------------------------------------------------------             
NTHUNP 9;INPI 55;TAB;INPI 55;                                                   
ENTER;TSTOK;BNE X1;                                                             
FSTPRO;RESO;WRITE;                                                              
BR L1;                                                                          
*------------------------------------------------------------------             
* ERRORS!                                                                       
*------------------------------------------------------------------             
.X1;SAVERR;RESO;WRITE;                                                          
LOCWRK W1;MVSW "E";BR L1;             // WE HAVE AN ERROR                       
*------------------------------------------------------------------             
* THIS BRINGS UP BATCH/CLOSE SCREEN                                             
*------------------------------------------------------------------             
.L9;LOCWRK W1;CMWS "E";BE LX;         // DON'T UPDATE WITH ERRORS               
PF05;TSTOK;BE LA;                                                               
*-----SAVERR;RESO;WRITE;BR LX;                                                  
BR LX;                                                                          
* TELL BATCH/CLOSE TO SET CONTROL TOTALS ITSELF                                 
.LA;NTHUNP 7;INPS &&;TAB;INPS &&;ENTER;                                         
TSTOK;EXITNE;                                                                   
*------------------------------------------------------------------             
* EXIT                                                                          
*------------------------------------------------------------------             
.LX;END;                                                                        
