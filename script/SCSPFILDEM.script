*          DATA SET SCSPFILDEM AT LEVEL 020 AS OF 03/30/06                      
UC;                                                                             
USRI 8;SYSS SPOT;PRGS SFM;PWDI 8;$CT;                                           
TSTOK;EXITNE;                                                                   
* READ A RECORD                                                                 
.RD;                                                                            
READ;EXITNE;                                                                    
CMIS DEMO;BNE RD;ADDIP 4;                                                       
FU;TAB;INPS "//";ENTER;         // CLEAR INPUT                                  
FU;TAB;INPS "DEMODEF";TAB;INPS DIS;ENTER;                                       
TSTOK;BNE E1;                                                                   
NTHUNP 9;                                                                       
INPI 4;TAB;                     // NETWORK                                      
INPI 4;TAB;                     // SHOW CODE                                    
INPS "BBM";ADDIP 3;TAB;         // FORCE RATING SERVICE TO BBM                  
CMIS "*";BNE C2;                // CLIENT LEVEL RECORD?                         
ADDIP 1;                        // SKIP PAST CLIENT INDICATOR                   
INPI 3;BACKUP;BR C5;            // YES, ENTER CLIENT CODE                       
.C2;                                                                            
ADDIP 4;                        // NO, SKIP PAST CLI CODE AND INDICAT           
.C5;                                                                            
TAB;INPI 3;ENTER;               // SEQUENCE NUMBER                              
TSTERR;BE R2;                                                                   
FU;TAB;TAB;INPS "CHA";TAB;BR R4;                                                
.R2;                                                                            
FU;TAB;TAB;INPS "ADD";                                                          
.R4;                                                                            
NTHUNP 14;                      // BASE BOOK FIELD                              
CMIS A;BL B2;                   // IS THERE BASE BOOK IN INPUT FIELD?           
INPI 6;                         // YES, INPUT IT                                
BR B5;                                                                          
.B2;                                                                            
ADDIP 6;                        // NO BASE BK IN INPUT FLD--MOVE PAST           
TSTFX C1;BNL B5;                // IF BASE BOOK ON SCREEN, LEAVE IT             
INPS "MAR/01";                  // OTHERWISE, MAR/01 IS DEFAULT                 
.B5;                                                                            
TAB;                            // GO TO USE TILL BOOK FIELD                    
CMIS A;BL B7;                   // USE TILL BOOK IN INPUT FIELD?                
INPI 6;                         // YES, INPUT IT                                
BR B9;                                                                          
.B7;                                                                            
ADDIP 6;                        // NO USE TIL IN INPUT FLD--MOVE PAST           
TSTFX C1;BNL B9;                // IF USE TILL BK ON SCREEN, LEAVE IT           
INPS "DEC/09";                  // OTHERWISE, DEC/99 IS DEFAULT                 
.B9;                                                                            
TAB;                            // GO TO FIRST DEMO IN LIST                     
.N2;                                                                            
CMTS "DEMO";BE WH;             // NO MORE ROOM--PUT ! IN DEMO OVERFLOW          
CMTI 7;BE N5;                  // RATING VAL IN LIST...DISPLAY IMP              
.N3;                                                                            
TSTFX C1;BL N4;                // TEST FIELD TO C'A' AND IF LOW,                
NEXT;BNE AB;BR N2;             // DAT BE WHERE DA DATA GO                       
.N4;                                                                            
INPI 7;SUBIP 7;                // MOVE RATING VALUE, AND SET POINTER            
.N5;                                                                            
CMIS "U";BE U2;                // SPECIAL USER DEMO?                            
ADDIP 1;                       // NO, SKIP PAST FIRST CHAR FOR IMP              
NTHUNP 26;                     // TO INPUT IMPRESSIONS FIELD                    
.N6;                           // FIND A PLACE TO PUT THE IMPRESSIONS           
TSTFX C1;BL N8;                // COMPARE FIELD TO C'A'                         
CMTI 6;BE N8;                  // COMPARE SCREEN TO WKR FILE INPT               
TAB;BNE AB;BR N6;              // AND IF IT'S LOW, IT'S EMPTY !                 
.N8;                                                                            
INPI 6;BACKUP;INPS "=";INPI 6; // IMPRESSIONS                                   
ENTER;TSTOK;BNE ER;            // OK?                                           
FSTPRO;RESO;WRITE;                                                              
BR UX;                                                                          
*   SPECIAL NEW USER DEMOS                                                      
.U2;                           // PUT THE NEW USER DEMO IMPRESSION IN           
NTHUNP 26;                     // TO INPUT IMPRESSIONS FIELD                    
.U4;                           // FIND A PLACE TO PUT THE IMPRESSIONS           
TSTFX C1;BL U6;                // COMPARE FIELD TO C'A'                         
CMTI 7;BE U6;                  // COMPARE SCREEN TO WKR FILE                    
TAB;BNE AB;BR U4;              // AND IF IT'S LOW, IT'S EMPTY !                 
.U6;                                                                            
INPI 7;BACKUP;INPS "=";INPI 6; // IMPRESSIONS                                   
ENTER;TSTOK;BNE ER;            // OK?                                           
FSTPRO;RESO;WRITE;                                                              
.UX;                                                                            
* DEMO OVERRIDE SCREEN                                                          
FU;TAB;INPS DEMOVER;TAB;INPS CHA;TAB;                                           
.NA;                                                                            
SETIP 4;                        // GO BACK TO ENTER HEADER INFO                 
INPI 4;BACKUP;INPS ",";         // NETWORK                                      
INPI 4;BACKUP;INPS ",";         // SHOW CODE                                    
INPS "BBM";ADDIP 3;INPS ",";    // FORCE RATING SERVICE TO BBM                  
SETIP 34;                       // GET DEMO CODE                                
INPI 7;BACKUP;INPS ",";         // DEMO CODE                                    
SUBIP 7;SETWP 0;MVIW 1;SETWP 0; // MOVE 1ST CHAR OF DEMO TO WORK AREA           
CMWS E;BNE W2;                  // IF IT IS AN E,                               
MVSW R;SETWP 0;                 // REPLACE IT WITH AN R (SPEC. CASE)            
.W2;                                                                            
CMWS U;BNE W4;                  // IF IT IS AN U, SPECIAL USER DEMOS            
MVSW R;SETWP 0;                 // REPLACE IT WITH AN R (SPEC. CASE)            
.W4;                                                                            
SETIP 15;                       // GO BACK TO SEE IF NEED CLIENT CODE           
CMIS "*";BNE W5;                // CLIENT LEVEL RECORD?                         
ADDIP 1;                        // SKIP PAST CLIENT INDICATOR                   
INPI 3;BACKUP;INPS ",";BR W8;   // YES, ENTER CLIENT CODE                       
.W5;                                                                            
ADDIP 4;INPS ",";               // NO, SKIP PAST CLI CODE AND INDICAT           
.W8;                                                                            
INPI 3;ENTER;                   // SEQUENCE NUMBER                              
TSTOK;BNE ER;                                                                   
SETIP 47;NTHUNP 16;SETWP 0;     // GO TO FIRST STATION                          
.S2;                                                                            
CMIS *;BL S6;            // IF INPUT NOT A * OR CHAR, GET OUT                   
CMIS /Z;BE S6;           // IF INPUT FIELD IS /Z, GET OUT                       
CMWS R;BE S3;            // IF DEMO IS R OR E, INPUT VALUE                      
CMIS *;BNE S3;           // IF NOT, THEN IF NOT SPILL, INPUT VALUE              
ADDIP 11;BR S4;          // DEMO IS NOT R OR E--SKIP SPILL VALUE                
.S3;                                                                            
ADDIP 5;INPI 4;ADDIP 2;         // SKIP STA/MKT, INPUT 4, SKIP 2                
.S4;                                                                            
TAB;BNE AB;BR S2;               // GO TO NEXT FIELD ON SCREEN                   
.S6;                                                                            
ENTER;TSTOK;BNE ER;            // OK?                                           
FSTPRO;RESO;WRITE;BR RD;                                                        
.E1;                                                                            
SAVERR;RESO;WRITE;EXIT;                                                         
.ER;                                                                            
SAVERR;RESO;WRITE;                                                              
*NTHUNP 5;                     // SET TO FIRST DATA FIELD AND                   
*.E2;                          // CLEAR ALL UNPROTECTED FIELDS                  
*ERASE;TAB;BE E2;                                                               
BR RD;                                                                          
.AB;                                                                            
ABEND;                                                                          
.WH;                                                                            
NTHUNP 42;                     // TO INPUT DEMO OVERFLOW FIELD                  
INPS "!";BE S6;                                                                 
