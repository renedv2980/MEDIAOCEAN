*          DATA SET SCACINP08  AT LEVEL 006 AS OF 03/06/13                      
********************************************************************            
* ACC / INPUT, TYPE 8                                              *            
********************************************************************            
#SCRIPT ACINP08                                                                 
SETWP 0;LBLW W1;ADDWP 1;LBLW W2; // SET UP WORKING STORAGE                      
USRI 8;SYSS ACC;PRGS INP;PWDI 8;$CT;                                            
TSTOK;EXITNE;                                                                   
FU;TAB;INPS BATCH;TAB;INPS OPEN;ENTER;                                          
TSTOK;EXITNE;                                                                   
* READ THE BATCH HEADER DATA RECORD                                             
READ;EXITNE;                                                                    
* BATCH REF     BATCH NAME            BATCH TYPE                                
LOCNUM 3;INPI 4;LOCNUM 5;INPI 15;LOCNUM 7;INPI 3;                               
*   MOA        EFF DT                                                           
LOCNUM 8;INPI 6;LOCNUM 12;INPI 8;                                               
*   ITM CNT      BATCH TOT                                                      
LOCNUM 20;INPS 250;LOCNUM 24;INPS 9999999;                                      
*   COMMENTS                                                                    
LOCNUM 32;INPI 50;                                                              
*----------------------------        HANDLE ENDING STATUS                       
LOCWRK W1;                                                                      
CMIS " ";BE S1;CMIS C;BE S1;CMIS c;BNE S2;                                      
.S1;MVSW C;BR S7;                                                               
.S2;                                                                            
CMIS S;BE S3;CMIS s;BNE S4;                                                     
.S3;MVSW S;BR S7;                                                               
.S4;                                                                            
CMIS U;BE S5;CMIS u;BNE S6;                                                     
.S5;MVSW U;BR S7;                                                               
.S6;OUTERR "EA#9999 INVALID ENDING STATUS FOR BATCH";                           
WRITE;EXIT;                                                                     
.S7;                                                                            
*----------------------------                                                   
ENTER;TSTOK;BE L0;                                                              
SAVERR;RESO;WRITE;READ;EXIT;                                                    
.L0;                                                                            
* NEED TO WRITE A RESULT CODE TO BATCH HEADER RECORD !                          
FSTPRO;RESO;WRITE;                                                              
*-------------------------------------------------------------------            
* INPUT SCREEN SHOULD BE IN PLACE NOW -- POSN TO OFFC/DEPT                      
*-------------------------------------------------------------------            
.L1;                                                                            
READ;BNE L9;                                                                    
NTHUNP 7;                                                                       
INPS "//";ENTER;              //MAKE SURE SCREEN IS CLEAR                       
NTHUNP 7;                                                                       
* DOC NUM  DATE                                                                 
INPI 6;TAB;INPI 8;TAB;                                                          
* CLIENT   PRODUCT    JOB        W/C        CREDIT A/C                          
INPI 6;TAB;INPI 6;TAB;INPI 6;TAB;INPI 2;TAB;INPI 15;TAB;                        
* COMM     AMOUNT      EXP A/C     FIN OFF                                      
INPI 1;TAB;INPI 10;TAB;INPI 15;TAB;INPI 2;TAB;                                  
* ANL OFF  DEPT       PERSON                                                    
INPI 2;TAB;INPI 3;TAB;INPI 6;TAB;                                               
* ACC,WC,AMT   TAX 1                                                            
CMIS "ºººººººººººººººººººººººººººº";BNE D1;INPI 28;BR D4;                       
.D1;                                                                            
LOCWRK W2;MVSW "ºººººººººººººººººººººººººººº";                                  
LOCWRK W2;MVIW 28;SUBIP 28;        // NEED SUBIP FOR THE PC TO                  
NTHUNP 21;LOCWRK W2;               // POINT CURSOR CORRECTLY                    
CMWS "ººººººººººººººº";BNE D2;INPS ",";ADDWP 15;BR D3;                          
.D2;                                                                            
INPW 15;BACKUP;INPS ",";                                                        
.D3;                                                                            
INPW 2;BACKUP;INPS ",";                                                         
INPW 11;                                                                        
ADDIP 28;                         //REPOSITION INPUT PTR                        
.D4;                                                                            
* ACC,WC,AMT   TAX 2                                                            
CMIS "ºººººººººººººººººººººººººººº";BNE D5;INPI 28;BR D9;                       
.D5;                                                                            
LOCWRK W2;MVSW "ºººººººººººººººººººººººººººº";                                  
LOCWRK W2;MVIW 28;SUBIP 28;        // NEED SUBIP FOR THE PC TO                  
NTHUNP 22;LOCWRK W2;               // POINT CURSOR CORRECTLY                    
CMWS "ººººººººººººººº";BNE D6;INPS ",";ADDWP 15;BR D7;                          
.D6;                                                                            
INPW 15;BACKUP;INPS ",";                                                        
.D7;                                                                            
INPW 2;BACKUP;INPS ",";                                                         
INPW 11;                                                                        
ADDIP 28;                         //REPOSITION INPUT PTR                        
.D9;                                                                            
*------------------------------------------------------------------             
* NARRATIVES 1 AND 2                                                            
*------------------------------------------------------------------             
NTHUNP 23;INPI 50;TAB;INPI 50;                                                  
ENTER;TSTOK;BNE X1;                                                             
FSTPRO;RESO;WRITE;                                                              
BR L1;                                                                          
*-------------------------------------------------------------------            
* ERRORS!                                                                       
*-------------------------------------------------------------------            
.X1;                                                                            
SAVERR;RESO;WRITE;                                                              
BR L1;                                                                          
*-------------------------------------------------------------------            
* THIS ENDS THE BATCH BASED ON THE ENDING STATUS                                
*-------------------------------------------------------------------            
.L9;                                                                            
LOCWRK W1;CMWS S;BNE R1;PF02;EXIT;         // SAVE                              
.R1;LOCWRK W1;CMWS U;BNE R2;PF06;BR R3;    // UPDATE                            
.R2;PF05;                                  // CLOSE                             
.R3;TSTOK;EXITNE;                                                               
NTHUNP 7;INPS &&;TAB;INPS &&;ENTER;        // TELL BATCH TO SET                 
TSTOK;EXITNE;                              // CONTROL TOTALS ITSELF             
END;                                                                            
