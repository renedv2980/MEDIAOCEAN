*          DATA SET SCACINP01  AT LEVEL 002 AS OF 04/15/05                      
********************************************************************            
* ACC / INPUT, TYPE 01                                             *            
********************************************************************            
#SCRIPT ACINP01                                                                 
SETWP 0;LBLW W1;                // SET UP WORKING STORAGE                       
USRI 8;SYSS ACC;PRGS INP;PWDI 8;$CT;                                            
TSTOK;EXITNE;                                                                   
FU;TAB;INPS BATCH;TAB;INPS OPEN;ENTER;                                          
TSTOK;EXITNE;                                                                   
* READ THE BATCH HEADER DATA RECORD                                             
READ;EXITNE;                                                                    
* BATCH REF     BATCH NAME            BATCH TYPE                                
LOCNUM 3;INPI 4;LOCNUM 5;INPI 15;LOCNUM 7;INPI 3;                               
*   MOA        EFF DT                                                           
LOCNUM 8;INPI 6;LOCNUM 12;INPI 8;                                               
*   ITM CNT      BATCH TOT                                                      
LOCNUM 20;INPS 250;LOCNUM 24;INPS 999999999;                                    
*   COMMENTS                                                                    
LOCNUM 32;INPI 50;                                                              
*-------------------------------------------------------------------            
* HANDLE ENDING STATUS                                                          
*-------------------------------------------------------------------            
LOCWRK W1;                                                                      
CMIS " ";BE S1;CMIS C;BE S1;CMIS c;BNE S2;                                      
.S1;MVSW C;BR S7;                                                               
.S2;                                                                            
CMIS S;BE S3;CMIS s;BNE S4;                                                     
.S3;MVSW S;BR S7;                                                               
.S4;                                                                            
CMIS U;BE S5;CMIS u;BNE S6;                                                     
.S5;MVSW U;BR S7;                                                               
.S6;OUTERR "EA#9999 INVALID ENDING STATUS FOR BATCH";                           
WRITE;EXIT;                                                                     
*                                                                               
.S7;                                                                            
ENTER;TSTOK;BE L0;                                                              
SAVERR;RESO;WRITE;READ;EXIT;          // NEXT READ FORCES PREV WRITE            
.L0;FSTPRO;RESO;WRITE;                // RESULT CODE TO BATCH HEADER            
*-------------------------------------------------------------------            
* INPUT SCREEN SHOULD BE IN PLACE NOW -- POSN TO DOCUMNET NUMBER                
*-------------------------------------------------------------------            
.L1;READ;BNE L9;                                                                
NTHUNP 7;INPS "//";ENTER;NTHUNP 7;        // CLEAR THE SCREEN                   
CMIS "           ";BNE L2;INPI 11;BR L4;  // CHECK FOR ORDER NUMBER             
.L2;INPI 11;ENTER;TSTOK;BNE X1;           // ORDER NUMBER                       
*------------------------------------                                           
.L4;NTHUNP 8;                                                                   
INPI 6;TAB;                           // DOCUMENT NUMBER                        
INPI 8;TAB;                           // DATE                                   
INPI 6;TAB;INPI 6;TAB;INPI 6;TAB;     // CLIENT, PRODUCT, JOB                   
INPI 15;TAB;                          // VENDOR                                 
INPI 1;TAB;INPI 1;TAB;                // URGENT, C/D                            
INPI 11;TAB;INPI 40;TAB;              // WORKCODE, AMOUNT (COMMISSION)          
INPI 2;TAB;                           // CR OFFICE                              
INPI 11;TAB;INPI 40;TAB;              // WORKCODE, AMOUNT (NON-COMM)            
INPI 15;TAB;                          // EXPENSE ACCOUNT                        
INPI 2;TAB;                           // FINANCIAL OFFICE                       
INPI 2;TAB;INPI 3;TAB;INPI 6;TAB;     // ANALYSIS OFFICE, DEPT, PERSON          
INPI 49;TAB;INPI 49;                  // NARRATIVE LINES 1 AND 2                
*------------------------------------                                           
ENTER;TSTOK;BNE X1;                                                             
FSTPRO;RESO;WRITE;                                                              
BR L1;                                                                          
*-------------------------------------------------------------------            
* ERRORS!                                                                       
*-------------------------------------------------------------------            
.X1;                                                                            
SAVERR;LOCCUR;SAVFP;RESO;WRITE;                                                 
BR L1;                                                                          
*-------------------------------------------------------------------            
* THIS ENDS THE BATCH BASED ON THE ENDING STATUS                                
*-------------------------------------------------------------------            
.L9;                                                                            
LOCWRK W1;CMWS S;BNE R1;PF02;EXIT;         // SAVE                              
.R1;LOCWRK W1;CMWS U;BNE R2;PF06;BR R3;    // UPDATE                            
.R2;PF05;                                  // CLOSE                             
.R3;TSTOK;EXITNE;                                                               
NTHUNP 7;INPS &&;TAB;INPS &&;ENTER;        // TELL BATCH TO SET                 
TSTOK;EXITNE;                              // CONTROL TOTALS ITSELF             
END;                                                                            
