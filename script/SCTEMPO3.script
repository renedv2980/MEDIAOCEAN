*          DATA SET SCTEMPO3   AT LEVEL 002 AS OF 05/10/01                      
#SCRIPT TEMPO3                                                                  
*                                                                               
* DECLARE WORK AREA VARIABLES                                                   
*                                                                               
SETWP 0;LBLW EF;ADDWP 1;       // SET TO 'E' IF LINE ERROR                      
*                                                                               
* CONNECT TO $COST PROGRAM IN ACC                                               
*                                                                               
USRI 8;SYSS ACC;PRGS COS;PWDI 8;$CT;                                            
TSTOK;BNE L8;                                                                   
*                                                                               
* GO TO TIME/ADD SCREEN                                                         
*                                                                               
NTHUNP 2;INPS TIME;TAB;INPS ADD;ENTER;                                          
TSTOK;BNE L8;                                                                   
FSTPRO;RESO;WRITE;                                                              
*                                                                               
* INPUT KEY FIELDS                                                              
*                                                                               
READ;BNE L9;                                                                    
LOCTXT PERIOD;TAB;INPIF;            // PERIOD                                   
TAB;INPIF;                          // PERSON                                   
TAB;INPIF;                          // HOURS WORKED                             
ENTER;                                                                          
TSTOK;BNE L8;                                                                   
FSTPRO;RESO;WRITE;                                                              
*                                                                               
* INPUT ALLOCATION LINE(S)                                                      
*                                                                               
.L5;                                                                            
READ;BNE L9;                                                                    
CMIS EOD;BE L7;              // TEST IF THIS IS END OF DATA                     
FSTPRO;LOCTXT BASIS;LOCTXT LOCAL;LOCTXT WC;    //1ST LINELINE                   
TAB;NXT;NXT;NXT;NXT;NXT;NXT;NXT;      // GET TO 2ND ALLOCATION LINE             
NXT;NXT;NXT;NXT;NXT;NXT;NXT;NXT;NXT;  // NEED TO TAB PAST 1ST LINE              
INPIF;                              // TYPE                                     
TAB;INPIF;                          // HOURS                                    
TAB;INPIF;                          // CLIENT                                   
TAB;INPIF;                          // PRODUCT                                  
TAB;INPIF;                          // JOB                                      
TAB;INPIF;                          // TASK                                     
TAB;INPIF;                          // MONTH OF ACTIVITY                        
TAB;INPIF;                          // TAX?                                     
TAB;INPIF;                          // RATE                                     
TAB;INPIF;                          // AMOUNT                                   
TAB;INPIF;                          // INCOME ACCOUNT OVERRIDE                  
TAB;INPIF;                          // TEMPO LINE NUMBER                        
TAB;INPIF;                          // COMMENT                                  
PF08;                               // CYCLE TO NEXT LINE                       
TSTOK;BE L6;                                                                    
LOCWRK EF;FSTPRO;OUTW 1;      // SET LINE ERROR FLAG                            
.L6;                                                                            
FSTPRO;RESO;WRITE;                  // SAVE ERROR/SUCCESS MSG TO O/P            
BR L5;                                                                          
*                                                                               
* NO MORE LINES - PERFORM UPDATE (PF6)                                          
*                                                                               
.L7;                                                                            
LOCWRK EF;CMWS E;BE L9;                                                         
PF06;                                                                           
*                                                                               
* SAVE LAST SCREEN MESSAGE                                                      
*                                                                               
.L8;                                                                            
FSTPRO;RESO;WRITE;READ;                                                         
*                                                                               
* EXIT SCRIPT                                                                   
*                                                                               
.L9;                                                                            
END;                                                                            
