*          DATA SET SCACINP13  AT LEVEL 001 AS OF 01/06/06                      
********************************************************************            
* ACC / INPUT, TYPE 13                                             *            
********************************************************************            
#SCRIPT ACINP13                                                                 
SETWP 0;LBLW W1;                // SET UP WORKING STORAGE                       
USRI 8;SYSS ACC;PRGS INP;PWDI 8;$CT;   //SIGN ON TO INPUT                       
TSTOK;EXITNE;                                                                   
*-------------------------------------------------------------------            
* BATCH HEADER - ITM1                                                           
*-------------------------------------------------------------------            
FU;TAB;INPS BATCH;TAB;INPS OPEN;ENTER;                                          
TSTOK;EXITNE;                                                                   
READ;EXITNE;                      // READ BATCH HEADER                          
LOCNUM 3;INPI 4;                  // BATCH REF                                  
LOCNUM 5;INPI 15;                 // BATCH NAME                                 
LOCNUM 7;INPI 3;                  // BATCH TYPE                                 
LOCNUM 8;INPI 6;LOCNUM 12;INPI 8; // MOA, EFFECTIVE DATE                        
LOCNUM 20;INPS 250;               // ITEM COUNT                                 
LOCNUM 24;INPS 999999999;         // BATCH TOT                                  
LOCNUM 32;INPI 50;                // COMMENT                                    
*-------------------------------------------------------------------            
* HANDLE ENDING STATUS                                                          
*-------------------------------------------------------------------            
LOCWRK W1;                                                                      
CMIS " ";BE S1;CMIS C;BE S1;CMIS c;BNE S2;    // BATCH CLOSE                    
.S1;MVSW C;BR S7;                                                               
.S2;CMIS S;BE S3;CMIS s;BNE S4;               // BATCH SAVE                     
.S3;MVSW S;BR S7;                                                               
.S4;CMIS U;BE S5;CMIS u;BNE S6;               // BATCH UPDATE                   
.S5;MVSW U;BR S7;                                                               
.S6;OUTERR "EA#9999 INVALID ENDING STATUS FOR BATCH";                           
WRITE;EXIT;                                                                     
.S7;                                                                            
ENTER;TSTOK;BE L0;                                                              
SAVERR;RESO;WRITE;READ;EXIT;          // NEXT READ FORCES PREV WRITE            
.L0;FSTPRO;RESO;WRITE;                // RESULT CODE TO BATCH HEADER            
*-------------------------------------------------------------------            
* INPUT SCREEN SHOULD BE IN PLACE NOW -- POSN TO INVOICE NUMBER                 
*-------------------------------------------------------------------            
.L1;READ;BNE L9;                                                                
NTHUNP 7;INPS "//";ENTER;NTHUNP 7;        // CLEAR THE SCREEN                   
INPI 11;TAB;                          // INVOICE NUMBER                         
INPI 8;TAB;                           // DATE                                   
INPI 12;TAB;                          // MEDIA CONTROL ACCNT SZ ASSUMED         
INPI 12;TAB;                          // CASH ACCOUNT SC ASSUMED                
INPI 14;TAB;                          // VENDOR                                 
INPI 8;TAB;                           // PAYING REP                             
INPI 3;TAB;                           // CLIENT                                 
INPI 3;TAB;                           // PRODUCT                                
INPI 3;TAB;                           // ESTIMATE                               
INPI 8;TAB;                           // MOS                                    
INPI 1;TAB;                           // NET SUB MEDIA                          
INPI 2;TAB;                           // ACC OFFICE                             
INPI 12;TAB;                          // NET AMOUNT                             
INPI 12;TAB;                          // GROSS MEMO                             
INPI 60;TAB;                          // NARRATIVE LINE 1                       
INPI 60;TAB;                          // NARRATIVE LINE 2                       
*------------------------------------                                           
ENTER;TSTOK;BNE X1;                                                             
FSTPRO;RESO;WRITE;                                                              
BR L1;                                                                          
*-------------------------------------------------------------------            
* ERRORS!                                                                       
*-------------------------------------------------------------------            
.X1;                                                                            
SAVERR;LOCCUR;SAVFP;RESO;WRITE;                                                 
BR L1;                                                                          
*-------------------------------------------------------------------            
* THIS ENDS THE BATCH BASED ON THE ENDING STATUS                                
*-------------------------------------------------------------------            
.L9;                                                                            
LOCWRK W1;CMWS S;BNE R1;PF02;EXIT;         // SAVE                              
.R1;LOCWRK W1;CMWS U;BNE R2;PF06;BR R3;    // UPDATE                            
.R2;PF05;                                  // CLOSE                             
.R3;TSTOK;EXITNE;                                                               
NTHUNP 7;INPS &&;TAB;INPS &&;ENTER;        // TELL BATCH TO SET                 
TSTOK;EXITNE;                              // CONTROL TOTALS ITSELF             
END;                                                                            
