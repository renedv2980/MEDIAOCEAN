*          DATA SET SCACTMSUPL AT LEVEL 068 AS OF 05/13/09                      
#SCRIPT ACTMSUPL                                                                
*                                                                               
*************************************************************                   
* $COST / TIME / ADD/CHANGE HELLOOOOOO NEWMAN               *                   
*************************************************************                   
*                                                                               
* SET UP WORKING STORAGE                                                        
SETWP 0;LBLW W1;ADDWP 17;          // FOR PERIOD AND PERSON                     
LBLW WE;ADDWP 1;                   // SET TO 'E' IF LINE ERROR                  
USRI 8;SYSS ACC;PRGS COST;PWDI 8;$CT;                                           
TSTOK;EXITNE;                                                                   
*-----------------------------------------------------------*                   
* PROCESS TIME SHEET HEADER                                 *                   
*-----------------------------------------------------------*                   
READ;BNE X9;                                                                    
NTHUNP 2;INPS TIME;TAB;                                                         
SETIP 17;CMIS " ";BNE S1;          // ACTION DEFINED?                           
INPS ADD;BR S2;                    // NO, DEFAULT TO ADD                        
.S1;INPI 3;                                                                     
.S2;                                                                            
ENTER;TSTOK;BNE X9;                                                             
SETIP 0;                                                                        
*                                                                               
NTH 23;INPI 9;TAB;INPI 8;          // PERIOD AND PERSON                         
NTHUNP 3;CMTS "CHANGE";BE S3;      // IF CHA, HRS WRKED NOT NEEDED              
NTHUNP 12;INPS 1;                  // IF ADD, USE 1 HRS WRKED                   
.S3;                                                                            
ENTER;TSTOK;BNE X9;                                                             
PF05;BR OK;                        // B+                                        
*-----------------------------------------------------------*                   
* PROCESS TMS LINES                                         *                   
*-----------------------------------------------------------*                   
.L1;READ;BNE L9;                                                                
NTHUNP 3;CMTS "ADD";BE L3;            // ADD AND CHA USE DIFF SCREENS           
.L2;NTH 127;INPS "//";ENTER;          // CLEAR THE LAST LINE                    
NTH 127;TSTFX 00;BE L4;               // IS LAST LINE OPEN?                     
PF08;BR L2;                           // NO,PAGE DOWN & TRY AGAIN               
.L3;                                                                            
NTH 114;INPS "//";ENTER;NTH 114;      // CLEAR THE LAST LINE                    
.L4;                                                                            
INPI 2;TAB;                           // TYPE OF TIME                           
INPI 7;TAB;                           // HOURS                                  
INPI 7;TAB;                           // CLIENT                                 
INPI 4;TAB;                           // PROD                                   
INPI 6;TAB;                           // JOB                                    
INPI 2;TAB;                           // TASK                                   
INPI 6;NXT;NXT;                       // MOA                                    
INPI 8;NXT;NXT;                                                                 
INPI 15;TAB;                                                                    
INPI 60;                           // NARR                                      
PF08;TSTOK;BNE X1;                                                              
*-----------------------------------------------------------*                   
* TIMESHEET OK, LOOP BACK FOR MORE TIMESHEETS               *                   
*-----------------------------------------------------------*                   
.OK;FSTPRO;RESO;WRITE;BR L1;                                                    
*-----------------------------------------------------------*                   
* ERRORS!                                                   *                   
*-----------------------------------------------------------*                   
.X1;SAVERR;RESO;WRITE;                                                          
LOCWRK WE;MVSW "E";BR L1;             // WE HAVE AN ERROR                       
.X9;SAVERR;RESO;WRITE;BR LX;                                                    
*-----------------------------------------------------------*                   
* DONE READING FILE                                         *                   
*-----------------------------------------------------------*                   
.L9;LOCWRK WE;CMWS "E";BE LX;         // DON'T UPDATE WITH ERRORS               
PF06;TSTOK;BE LX;                     // OTHERWISE, UPDATE TIMESHEET            
READ;WRITE;                                                                     
READ;RESO;WRITE;                      // WRITE ERROR TO FIRST ITEM              
*-----------------------------------------------------------*                   
* EXIT                                                      *                   
*-----------------------------------------------------------*                   
.LX;END;                                                                        
