*          DATA SET SCSPXATPAY AT LEVEL 001 AS OF 05/11/11                      
#SCRIPT SPXATPAY                                                                
USRI 8;SYSS SPOT;PRGS PAY;PWDI 8;$CT;                                           
TSTOK;EXITNE;                                                                   
.RD;                           // READ A RECORD                                 
READ;EXITNE;                                                                    
FU;TAB;                        // FIRST INPUT FIELD                             
INPI 12;TAB;TAB;               // PAYER                                         
INPI 1;INPS "!@##";TAB;        // MEDIA                                         
INPI 3;TAB;                    // CLIENT                                        
INPI 3;                        // PRODUCT                                       
CMIS " ";                      // PARTNER PRESENT?                              
BNH R1;                        // NO                                            
INPS "-";INPI 3;BR R2;         // YES - PARTNER TO SCREEN                       
.R1;                                                                            
ADDIP 3;                       // SKIP PARTNER - NOT PRESENT                    
.R2;                                                                            
CMIS 000;                      // ESTIMATE = 000?                               
BNE R3;                        // NO - GO INPUT ESTIMATE                        
ADDIP 3;TAB;BR R4;             // SKIP ESTIMATE - NOT PRESENT                   
.R3;                                                                            
INPS "/";INPI 3;TAB;           // ESTIMATE                                      
.R4;                                                                            
INPI 10;TAB;                   // STATION                                       
CMIS " ";                      // SPECIAL REP PRESENT?                          
BNH R5;                        // NO                                            
INPI 4;TAB;BR R6;              // SPECIAL REP                                   
.R5;                                                                            
ADDIP 4;INPS "     ";TAB;      // SKIP SREP - NOT PRESENT                       
.R6;                                                                            
INPI 6;TAB;                    // MONTH                                         
.R7;                                                                            
CMIS " ";                      // HAVE INVOICE/AMOUNT?                          
BNH R8;                        // NO - DONE                                     
INPI 10;TAB;INPI 11;TAB;TAB;   // INPUT INVOICE AND AMT                         
BR R7;                         // LOOP BACK AND CHECK NEXT INVOICE              
.R8;                                                                            
ENTER;                         // CLEAR INVOICES LEFT ON SCREEN                 
TSTOK;BNE ER;                  // ANY ERRORS?                                   
RESO;WRITE;                    // WRITE BACK CHANGES & READ NEXT ENTRY          
LU 9;INPS //;ENTER;BR RD;      // CLEAR THE SCREEN                              
.ER;                           // ERROR                                         
SAVERR;RESO;WRITE;             // SAVE ERROR                                    
LU 9;INPS //;ENTER;BR RD;      // CLEAR THE SCREEN                              
.AB;                                                                            
ABEND;                                                                          
