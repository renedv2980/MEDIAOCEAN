*          DATA SET SCACINP10  AT LEVEL 004 AS OF 04/17/08                      
********************************************************************            
* ACC / INPUT, TYPE 10                                             *            
********************************************************************            
#SCRIPT ACINP10                                                                 
SETWP 0;LBLW W1;                   // BATCH ENDING STATUS                       
ADDWP 1;LBLW W2;                   // DO NOT POST ERROR INDICATOR               
ADDWP 1;LBLW W3;                   // INDICATOR FOR END OF BATCH                
ADDWP 1;LBLW W4;                   // SKIP DETAILS, INVOICE HDR ERROR           
ADDWP 1;LBLW W5;                   // DETAILS WERE PROCESSED                    
USRI 8;SYSS ACC;PRGS INP;PWDI 8;$CT;   //SIGN ON TO INPUT                       
TSTOK;EXITNE;                                                                   
*-------------------------------------------------------------------            
* BATCH HEADER - ITM1                                                           
*-------------------------------------------------------------------            
FU;TAB;INPS BATCH;TAB;INPS OPEN;ENTER;                                          
TSTOK;EXITNE;                                                                   
READ;EXITNE;                       // READ BATCH HEADER                         
LOCNUM 3;INPI 4;                   // BATCH REF                                 
LOCNUM 5;INPI 15;                  // BATCH NAME                                
LOCNUM 7;INPS "10,D";ADDIP 3;      // BATCH TYPE                                
LOCNUM 8;INPI 6;LOCNUM 12;INPI 8;  // MOA, EFFECTIVE DATE                       
LOCNUM 20;INPS 250;                // ITEM COUNT                                
LOCNUM 24;INPS 999999999;          // BATCH OUT                                 
LOCNUM 32;INPI 50;                 // COMMENT                                   
*----------------------------------                                             
* HANDLE ENDING STATUS                                                          
*----------------------------------                                             
LOCWRK W1;                                                                      
CMIS " ";BE S1;CMIS C;BE S1;CMIS c;BNE S2; // BATCH CLOSE                       
.S1;MVSW C;BR S7;                                                               
.S2;CMIS S;BE S3;CMIS s;BNE S4;            // BATCH SAVE                        
.S3;MVSW S;BR S7;                                                               
.S4;CMIS U;BE S5;CMIS u;BNE S6;            // BATCH UPDATE                      
.S5;MVSW U;BR S7;                                                               
.S6;OUTERR "EA#9999 Invalid Ending Status for Batch";                           
WRITE;EXIT;                                                                     
.S7;                                                                            
*----------------------------------                                             
ENTER;TSTOK;BE L0;                                                              
SAVERR;RESO;WRITE;READ;EXIT;                                                    
.L0;FSTPRO;RESO;WRITE;                                                          
*---------------------------------------------------------------------          
* CHECK INPUT TO SEE WETHER IT IS (INV HEADER) ITM2 OR (DETAILS) ITM3           
*---------------------------------------------------------------------          
READ;EXITNE;BR I1;                    // ITM2 HAS TO BE THERE                   
.C1;                                  // CHECK NEXT                             
READ;BNE P0;                          // FINISHED IF NOTHING IS FOUND           
CMIS D;BE D1;CMIS d;BE D1;            // IS IT A DETAIL LINE                    
CMIS H;BE P1;CMIS h;BE P1;BR E0;      // IT THIS A HEADER                       
*---------------------------------------------------------------------          
* INV HEADER  ITM2                                                              
*---------------------------------------------------------------------          
.I1;ADDIP 1;                                                                    
LOCWRK W2;MVSW P;                     // GOOD FOR POSTING                       
LOCWRK W3;MVSW G;                     // STILL PROCESSING                       
LOCWRK W4;MVSW H;                     // PROCESS DETAILS                        
LOCWRK W5;MVSW I;                     // INVOICE PROCESSED                      
LOCNUM 8;                                                                       
CMIS "ºººººººº";BE I2;INPI 8;         // CHECK FOR ORDER NUMBER                 
ENTER;TSTOK;BNE E8;BR I3;                                                       
.I2;ADDIP 8;                                                                    
.I3;                                                                            
LOCNUM 14;INPI 27;TAB;                // INVOICE NUMBER                         
INPI 8;TAB;                           // INVOICE DATE                           
INPI 8;TAB;                           // DUE DATE                               
INPI 15;TAB;                          // PRODUCTION VENDOR                      
INPI 1;TAB;                           // CASH DISCOUNT                          
INPI 15;TAB;                          // EXPENSE VENDOR                         
INPI 1;TAB;                           // CASH DISCOUNT                          
INPI 15;TAB;                          // CASH ACCOUNT                           
ADDIP 1;                              // **UNUSED**                             
INPI 1;TAB;                           // URGENT                                 
ENTER;TSTOK;BNE E5;                                                             
.I4;                                                                            
RESO;WRITE;                                                                     
BR C1;                                                                          
*---------------------------------------------------------------------          
* INVOICE DETAILS ITM3                                                          
*---------------------------------------------------------------------          
.D1;                                                                            
LOCWRK W4;CMWS E;BE C1;               // DO WE WANT TO SKIP DETAILS             
LOCWRK W5;MVSW D;                     // DETAILS PROCESSED                      
ADDIP 1;                              // SKIP RECORD INDICATOR                  
LOCNUM 48;                                                                      
INPI 12;TAB;                          // AMOUNT                                 
INPI 4;TAB;                           // WORKCODE                               
INPI 3;TAB;                           // CLIENT                                 
INPI 3;TAB;                           // PRODUCT                                
INPI 6;TAB;                           // JOB                                    
INPI 15;TAB;                          // EXPENSE ACCOUNT                        
INPI 2;TAB;                           // DEBIT OFFICE                           
INPI 2;TAB;                           // CREDIT OFFICE                          
INPI 2;TAB;                           // ANALYSIS OFFICE                        
SETIP 210;                            // GO TO DPT OVRD FIELD IN SCRIPT         
CMIS "ººººººº";BE D2;                 // DEPARTMENT OVERRIDE AVLBL ?            
INPI 7;                               // OVERRIDE DEPARTMENT FIELD              
SETIP 53;BR D3;                       // GO TO PERSON FLD IN SCRIPT             
.D2;                                                                            
SETIP 50;                                                                       
INPI 3;                               // DEPARTMENT                             
.D3;TAB;                                                                        
INPI 6;TAB;                           // PERSON                                 
INPI 60;TAB;                          // NARRATIVE LINE 1                       
INPI 60;TAB;                          // NARRATIVE LINE 2                       
INPI 12;TAB;                          // TAX BASIS                              
TAB;                                  // SKIP TAX LOCALITY FOR NOW.             
INPI 2;                               // TAX WORKCODE                           
LOCNUM 95;                            // GO BACK TO TAX LOCALITY FIELD          
INPI 17;                              // TAX LOCALITY                           
TAB;                                  //                                        
ENTER;TSTOK;BNE E9;                                                             
RESO;WRITE;PF02;TSTOK;BNE E1;                                                   
BR C1;                                                                          
*--------------------                                                           
* POST ROUTINE                                                                  
*--------------------                                                           
.P0;LOCWRK W3;MVSW D;                        // DONE PROCESSING                 
.P1;                                                                            
LOCWRK W5;CMWS D;BE D4;                      // DELETE EXTRA ITMS(ORD)          
.PA;                                                                            
LOCWRK W2;CMWS E;BE P2;                                                         
PF03;TSTOK;EXITNE;BR P3;                    // SHOULD NOT HAVE ERROR            
.P2;                                                                            
PF09;LOCNUM 10;INPS "CLEARºººººººººººººººººººººº"; // CLEAR INV BUFFER          
ENTER;PF09;LOCWRK W3;CMWS D;BNE I1;BR L9;                                       
.P3;                                                                            
LOCWRK W3;CMWS D;BNE I1;BR L9;                 // ANY MORE RECORDS              
*------------------------------------------------------------------             
* DELETE LOGIC                                                                  
*------------------------------------------------------------------             
.D4;                                                                            
LOCNUM 48;CMTS "ºººººººººººº";BE PA;  // DO WE HAVE SOMETHING IN AMNT?          
LOCNUM 48;TSTFX 00;BE PA;             // DO WE HAVE SOMETHING?                  
PF04;TSTOK;EXITNE;BR D4;                                                        
*------------------------------------------------------------------             
* ERRORS!                                                                       
*------------------------------------------------------------------             
.E0;                                                                            
OUTERR "EA#9999 INVALID LINE DESCRIPTION";BR E4;                                
.E1;                                                                            
LOCWRK W2;MVSW E;                     // DO NOT POST                            
LOCWRK W4;MVSW E;                     // SKIP DETAILS                           
SAVERR;RESO;                                                                    
.E4;                                                                            
WRITE;BR C1;                                                                    
*------------------------------------------------------------------             
* ERRORS : INVOICE HEADER HAS ERRORS                                            
*------------------------------------------------------------------             
.E5;                                                                            
TSTERN 0515;BE E6;                    // Duplicate Invoice on prod              
TSTERN 0516;BE E6;                    // Duplicate Invoice on expense           
TSTERN 0517;BNE E8;                   // Duplicate Invoice on prod/exp          
.E6;                                                                            
LOCNUM 40;NXTPRO;NXTPRO;CMTS U;BNE E8; // Point to Urgent field                 
LOCNUM 42;SETIP 99;                   // Point to Override field                
CMIS Y;BE E7;CMIS y;BNE E8;           // Check for Override = Yes               
.E7;INPI 1;ENTER;TSTOK;BE I4;                                                   
.E8;                                                                            
LOCWRK W2;MVSW E;                     // DO NOT POST                            
LOCWRK W4;MVSW E;                     // SKIP DETAILS                           
SAVERR;RESO;WRITE;                                                              
LOCNUM 14;INPS "CLEARºººººººººººººººººººººº"; // CLEAR INV BUFFER               
ENTER;BR C1;                                                                    
*------------------------------------------------------------------             
* ERRORS : INVOICE DETAIL HAS ERRORS                                            
*------------------------------------------------------------------             
.E9;                                                                            
LOCWRK W2;MVSW E;                   // DO NOT POST                              
SAVERR;RESO;WRITE;                                                              
LOCNUM 48;INPS "//";                // PUT "//" IN AMT FLD TO CLR ITM3          
ENTER;BR C1;                                                                    
*------------------------------------------------------------------             
* THIS ENDS THE BATCH BASED ON THE ENDING STATUS                                
*-------------------------------------------------------------------            
.L9;                                                                            
LOCWRK W1;CMWS S;BNE R1;PF02;EXIT;         // SAVE                              
.R1;LOCWRK W1;CMWS U;BNE R2;PF06;BR R3;    // UPDATE                            
.R2;PF05;                                  // CLOSE                             
.R3;TSTOK;EXITNE;                                                               
NTHUNP 7;INPS &&;TAB;INPS &&;ENTER;        // TELL BATCH TO SET                 
TSTOK;EXITNE;                              // CONTROL TOTALS ITSELF             
END;                                                                            
