*          DATA SET SCACINP45P AT LEVEL 005 AS OF 05/29/02                      
********************************************************************            
* ACC / INPUT, TYPE 45 DEBIT & CREDIT PAIRS                        *            
********************************************************************            
#SCRIPT ACINP45P                                                                
*                                                                               
SETWP 0;LBLW W1;ADDWP 1;LBLW W2;      // SET UP WORKING STORAGE                 
USRI 8;SYSS ACC;PRGS INP;PWDI 8;$CT;                                            
TSTOK;EXITNE;                                                                   
FU;TAB;INPS BATCH;TAB;INPS OPEN;ENTER;                                          
TSTOK;EXITNE;                                                                   
* READ THE BATCH HEADER DATA RECORD                                             
READ;EXITNE;                                                                    
* BATCH REF     BATCH NAME            BATCH TYPE                                
LOCNUM 3;INPI 4;LOCNUM 5;INPI 15;LOCNUM 7;INPI 3;                               
*   MOA        EFF DT                                                           
LOCNUM 8;INPI 6;LOCNUM 12;INPI 8;                                               
*   ITM CNT      BATCH TOT                                                      
LOCNUM 20;INPS 250;LOCNUM 24;INPS 999999999;                                    
*   COMMENTS                                                                    
LOCNUM 32;INPI 50;                                                              
*----------------------------        HANDLE ENDING STATUS                       
LOCWRK W1;                                                                      
CMIS " ";BE S1;CMIS C;BE S1;CMIS c;BNE S2;                                      
.S1;MVSW C;BR S7;                                                               
.S2;                                                                            
CMIS S;BE S3;CMIS s;BNE S4;                                                     
.S3;MVSW S;BR S7;                                                               
.S4;                                                                            
CMIS U;BE S5;CMIS u;BNE S6;                                                     
.S5;MVSW U;BR S7;                                                               
.S6;OUTERR "EA#9999 INVALID ENDING STATUS FOR BATCH";                           
WRITE;EXIT;                                                                     
.S7;                                                                            
*----------------------------                                                   
ENTER;TSTOK;BE L0;                                                              
* NOTE THAT NEXT READ FORCES PREVIOUS WRITE !!! (MH JAN/95)                     
SAVERR;RESO;WRITE;READ;EXIT;                                                    
.L0;                                                                            
* NEED TO WRITE A RESULT CODE TO BATCH HEADER RECORD!                           
FSTPRO;RESO;WRITE;                                                              
*------------------------------------------------------------------             
* INPUT SCREEN SHOULD BE IN PLACE NOW -- POSN TO DOCUMENT NUMBER                
*------------------------------------------------------------------             
.L1;                                                                            
READ;BNE L9;                                                                    
NTHUNP 7;                                                                       
* DOC NO   DATE                                                                 
INPI 6;TAB;INPI 8;TAB;TAB;TAB;                                                  
*------------------------------------------------------------------             
* DEBIT LINE                                                                    
*------------------------------------------------------------------             
* DEBIT    ACCOUNT     CONTRA                                                   
INPS "D";ADDIP 1;TAB;INPI 14;TAB;INPI 14;TAB;                                   
* OFF,DEPT,PERSON,ANOF                                                          
CMIS "              ";BNE D1;INPI 14;BR D3;                                     
.D1;                                                                            
LOCWRK W2;MVSW "              ";                                                
LOCWRK W2;MVIW 14;SUBIP 14;      // NEED SUBIP FOR THE PC                       
NTHUNP 14;LOCWRK W2;             // TO POINT CURSOR CORRECTLY                   
INPW 2;BACKUP;INPS ",";INPW 3;BACKUP;INPS ",";                                  
INPW 7;BACKUP;INPS ",";INPW 2;                                                  
ADDIP 14;                        // POINT BACK TO NEXT FIELD                    
.D3;                                                                            
TAB;                                                                            
* CLI,PRD                                                                       
CMIS "      ";BNE D4;INPI 6;BR D6;                                              
.D4;                                                                            
LOCWRK W2;MVSW "              ";                                                
LOCWRK W2;MVIW 6;SUBIP 6;        // NEED SUBIP FOR THE PC                       
NTHUNP 15;LOCWRK W2;             // TO POINT CURSOR CORRECTLY                   
INPW 3;BACKUP;                                                                  
CMWS "   ";BE D5;                                                               
INPS ",";INPW 3;                                                                
.D5;                                                                            
ADDIP 6;                         // POINT BACK TO NEXT FIELD                    
.D6;                                                                            
TAB;                                                                            
* AMOUNT                                                                        
INPI 11;SUBIP 11;NTHUNP 22;INPI 11;NTHUNP 17;                                   
*------------------------------------------------------------------             
* CREDIT LINE                                                                   
*------------------------------------------------------------------             
* CREDIT   ACCOUNT     CONTRA                                                   
INPS "C";ADDIP 1;TAB;INPI 14;TAB;INPI 14;TAB;                                   
* OFF,DEPT,PERSON,ANOF                                                          
CMIS "              ";BNE C1;INPI 14;BR C3;                                     
.C1;                                                                            
LOCWRK W2;MVSW "              ";                                                
LOCWRK W2;MVIW 14;SUBIP 14;      // NEED SUBIP FOR THE PC                       
NTHUNP 20;LOCWRK W2;             // TO POINT CURSOR CORRECTLY                   
INPW 2;BACKUP;INPS ",";INPW 3;BACKUP;INPS ",";                                  
INPW 7;BACKUP;INPS ",";INPW 2;                                                  
ADDIP 14;                        // POINT BACK TO NEXT FIELD                    
.C3;                                                                            
TAB;                                                                            
* CLI,PRD                                                                       
CMIS "      ";BNE C4;INPI 6;BR C6;                                              
.C4;                                                                            
LOCWRK W2;MVSW "              ";                                                
LOCWRK W2;MVIW 6;SUBIP 6;        // NEED SUBIP FOR THE PC                       
NTHUNP 21;LOCWRK W2;             // TO POINT CURSOR CORRECTLY                   
INPW 3;BACKUP;                                                                  
CMWS "   ";BE C5;                                                               
INPS ",";INPW 3;                                                                
.C5;                                                                            
ADDIP 6;                         // POINT BACK TO NEXT FIELD                    
.C6;                                                                            
*------------------------------------------------------------------             
* NARRATIVES 1 AND 2                                                            
*------------------------------------------------------------------             
NTHUNP 9;INPI 55;TAB;INPI 55;                                                   
ENTER;TSTOK;BNE X1;                                                             
FSTPRO;RESO;WRITE;                                                              
BR L1;                                                                          
*------------------------------------------------------------------             
* ERRORS!                                                                       
*------------------------------------------------------------------             
.X1;                                                                            
SAVERR;LOCCUR;SAVFP;RESO;WRITE;                                                 
BR L1;                                                                          
*-------------------------------------------------------------------            
* THIS ENDS THE BATCH BASED ON THE ENDING STATUS                                
*-------------------------------------------------------------------            
.L9;                                                                            
LOCWRK W1;CMWS S;BNE R1;PF02;EXIT;         // SAVE                              
.R1;LOCWRK W1;CMWS U;BNE R2;PF06;BR R3;    // UPDATE                            
.R2;PF05;                                  // CLOSE                             
.R3;TSTOK;EXITNE;                                                               
NTHUNP 7;INPS &&;TAB;INPS &&;ENTER;        // TELL BATCH TO SET                 
TSTOK;EXITNE;                              // CONTROL TOTALS ITSELF             
END;                                                                            
