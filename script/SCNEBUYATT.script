*          DATA SET SCNEBUYATT AT LEVEL 003 AS OF 05/10/01                      
*  SET UP WORKING STORAGE                                                       
SETWP 0;LBLW EF;ADDWP 2;LBLW PP;                                                
USRI 8;SYSS NET;PRGS NBUY;PWDI 8;$CT;                                           
TSTOK;EXITNE;                                                                   
.AA;                                                                            
READ;BNE Z1;                                                                    
*      CLT        EST        NET        PKG        PROG                         
FU;TAB;INPI 3;TAB;INPI 3;TAB;INPI 4;TAB;INPI 3;TAB;INPI 6;TAB;                  
* MOVE TRANSACTION RIGHT OFF THE RECORD                                         
INPI 2;BACKUP;                                                                  
* IF D ACTION DONT MOVE THE U OUT                                               
SETIP 19;CMIS D;BNE A2;INPS "EL";BR A3;                                         
* IF CD ACTION DONT MOVE THE U OUT                                              
.A2;                                                                            
SETIP 19;CMIS CD;BE A3;                                                         
INPS "U";                                                                       
.A3;                                                                            
*CHECK IF LINE NUMBER INPUTTED                                                  
ADDIP 2;INPS ",";INPI 5;ADDIP 3;CMIS " ";BE A4;                                 
*        SUBLINE                                                                
INPS "-";INPI 3;                                                                
.A4;                                                                            
ENTER;TSTOK;BE C1;                                                              
* WRITE ERROR CONDITION TO PC                                                   
FSTPRO;RESO;WRITE;BR AA;                                                        
.C1;                                                                            
* CHECK IF "CD" ACTION (CHANGE DEMO)                                            
SETIP 19;CMIS CD;BE T1;                                                         
* CHECK FOR MAKEGOOD INPUT                                                      
SETIP 33; CMIS " ";BE F1;                                                       
*        OLD PRG                OLD DATE                                        
NTHUNP 8;INPI 6;BACKUP;INPS "*";INPI 5;ADDIP 3;                                 
* CHECK IF OLD LINE NUMBER INPUTTED                                             
CMIS " ";BE E1;                                                                 
*        OLD LINE #                                                             
INPS "-";INPI 3;                                                                
.E1;                                                                            
*   MG DEMO                                                                     
TAB;INPS "N";                                                                   
.F1;                                                                            
* CHECK FOR PRE-EMPT                                                            
SETIP 51;NTHUNP 10;CMIS P;BNE I1;                                               
* SET PREEMPT INDICATOR                                                         
INPS "Y";BR N1;                                                                 
.I1;                                                                            
* CHECK FOR BONUS                                                               
CMIS B;BNE J1;                                                                  
* SET BONUS INDICATOR                                                           
INPS "B";BR N1;                                                                 
.J1;                                                                            
* CHECK FOR ADU                                                                 
CMIS A;BNE N1;                                                                  
* CHECK IF ACTUAL COST INPUTTED                                                 
SETIP 78;CMIS " ";BNE J2;                                                       
* SET ADU INDICATOR                                                             
INPS "A";BR N1;                                                                 
.J2;                                                                            
* IF COST INPUTTED SET ERROR CONDITION                                          
INPS Z;                                                                         
.N1;                                                                            
*                  DAY        TIME            LENGTH                            
NTHUNP 11;SETIP 52;INPI 3;TAB;INPI 11;TAB;TAB;INPI 3;ADDIP 1;                   
*   PRODUCT                                                                     
TAB;INPI 3;                                                                     
*         ASSIGNED   ACTUAL      INTEGRATION                                    
NTHUNP 17;INPI 8;TAB;INPI 10;TAB;INPI 8;                                        
*         ROTATION                                                              
NTHUNP 27;INPI 12;                                                              
*         COMMENT                                                               
NTHUNP 36;INPI 53;                                                              
* CHECK IF "D" ACTION (DELETE)                                                  
SETIP 19;CMIS D;BE V1;                                                          
* HIT ENTER CHECK FOR ERRORS                                                    
ENTER;TSTOK;BE P1;                                                              
* WRITE ERROR CONDITION TO PC                                                   
FSTPRO;RESO;WRITE;                                                              
* DO A DISPLAY PACKAGE TO RESET THE SCREEN/READ NEXT RECORD                     
NTHUNP 7;INPS DP;ENTER;BR AA;                                                   
.P1;                                                                            
* SAVE ACTION LINE                                                              
NTHUNP 7;LOCWRK EF;OUTW 20;                                                     
*                                                                               
* DEMO ACTION NOW STARTED                                                       
*                                                                               
NTHUNP 7;INPS "CD";LOCWRK PP;INPW 18;                                           
ENTER;TSTOK;BE T1;                                                              
* WRITE ERROR CONDITION TO PC                                                   
FSTPRO;RESO;WRITE;BR AA;                                                        
.T1;                                                                            
*                  RATING                                                       
NTHUNP 10;SETIP 164;INPI 7;                                                     
*         VPH1                                                                  
NTHUNP 20;INPI 7;                                                               
*         VPH2                                                                  
NTHUNP 24;INPI 7;                                                               
*         VPH3                                                                  
NTHUNP 28;INPI 7;                                                               
*         VPH4                                                                  
NTHUNP 32;INPI 7;                                                               
*         VPH5                                                                  
NTHUNP 36;INPI 7;                                                               
* HIT ENTER TEST FOR ERRORS                                                     
.V1;                                                                            
ENTER;                                                                          
*WRITE ERROR CONDITION TO PC                                                    
FSTPRO;RESO;WRITE;                                                              
BR AA;                                                                          
* FINI                                                                          
.Z1;                                                                            
END;                                                                            
