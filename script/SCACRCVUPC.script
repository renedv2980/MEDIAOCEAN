*          DATA SET SCACRCVUPC AT LEVEL 002 AS OF 08/28/09                      
***********************************************************************         
* ACC / RCV - APPLY CHECKS AND MARK RECEIVABLES                       *         
***********************************************************************         
#SCRIPT ACRCVUPC                                                                
SETWP 0;LBLW WE;ADDWP 1;LBLW WT;        // SET UP WORKING STORAGE               
ADDWP 1;LBLW PS;                        // PREVIOUS SCREEN WORK STORAGE         
USRI 8;SYSS ACC;PRGS RCV;PWDI 8;$CT;    // SIGN ON TO RCV                       
TSTOK;EXITNE;                                                                   
*----------------------------------------------------------------------         
* BATCH HEADER DATA RECORD                                                      
*----------------------------------------------------------------------         
READ;EXITNE;                                                                    
NTHUNP 2;                                                                       
INPS "HEADER";ENTER;TSTOK;EXITNE; // BATCH HEADER                               
*                                                                               
NTHUNP 6;                                                                       
INPI 4;TAB;                       // BATCH REFERENCE                            
INPI 15;TAB;                      // BATCH NAME                                 
INPI 6;TAB;                       // BATCH MONTH                                
INPI 15;TAB;                      // BANK ACCOUNT                               
INPI 6;TAB;                       // CHECK NUMBER                               
INPI 8;TAB;                       // DEPOSIT DATE                               
INPI 14;TAB;                      // CHECK AMOUNT                               
INPI 8;TAB;                       // CHECK DATE                                 
INPI 57;TAB;                      // RECEIVABLE ACCOUNT LINE 1                  
INPI 57;TAB;                      // RECEIVABLE ACCOUNT LINE 2                  
INPI 14;TAB;                      // DISCOUNT ACCOUNT                           
INPI 14;TAB;                      // WRITE-OFF ACCOUNT                          
INPI 6;TAB;                       // CD/WRITE-OFF CLI/PRO                       
INPI 6;TAB;                       // WRITE-OFF REFERENCE                        
INPI 8;TAB;                       // WRITE-OFF DATE                             
INPI 30;TAB;                      // WRITE-OFF NARRATIVE                        
INPI 12;TAB;                      // GST WRITE-OFF ACCOUNT                      
INPI 12;TAB;                      // PST WRITE-OFF ACCOUNT                      
INPI 8;TAB;                       // OFFSET DATE                                
INPI 2;TAB;                       // ANALYSIS OFFICE                            
INPI 3;TAB;                       // DEPARTMENT                                 
INPI 7;TAB;                       // PERSON                                     
INPI 17;TAB;                      // PERIOD                                     
INPI 13;TAB;                      // BILL NUMBER                                
INPI 12;                          // BILLING SOURCE                             
ENTER;TSTOK;BNE X4;                                                             
RESO;WRITE;                                                                     
*                                                                               
*----------------------------------------------------------------------         
* CHECK ACTION AND PROCESS ITEMS                                                
*----------------------------------------------------------------------         
.R1;READ;BNE X1;                                                                
NTHUNP 5;                                                                       
CMIS "Y";BE A1;CMIS "y";BE A1;    // APPLY DOLLARS                              
CMIS "O";BE A1;CMIS "o";BE A1;    // OFFSETS                                    
CMIS "W";BE A1;CMIS "w";BE A1;    // WRITE-OFFS                                 
CMIS "T";BE A1;CMIS "t";BE A1;    // TRANSFERS                                  
CMIS "S";BE A3;CMIS "s";BE A3;    // UNAPPLIED CASH                             
BR E1;                                                                          
*------------------------------------                                           
* APPLY DOLLARS, OFFSETS, WRITE-OFFS                                            
*------------------------------------                                           
.A1;                                                                            
LOCWRK PS;CMWS S;BNE A2;          // PREVIOUS SCREEN WAS SPECIAL SCREEN         
PF02;TSTOK;EXITNE;                // GO BACK TO MARK SCREEN                     
LOCWRK PS;MVSW 0;                 // RESET PREVIOUS SCREEN INDICATOR            
*                                                                               
.A2;                                                                            
SETIP 1;NTHUNP 5;                                                               
CMIS "      ";BE E3;              // DATE FIELD REQUIRED                        
INPS "DA=";INPI 8;                // FILTER ON BILL DATE                        
CMIS "      ";BE E2;              // BILL FIELD REQUIRED                        
INPS ",BI=";INPI 6;               // FILTER ON BILL NUMBER                      
ENTER;TSTOK;BNE E3;               // ERROR                                      
NTHUNP 6;CMTS "|||||||||";BE E2;  // DO WE HAVE SOMETHING?                      
NTHUNP 6;TSTFX 00;BE E2;          // DO WE HAVE SOMETHING?                      
SETIP 0;INPI 1;                   // ACTION                                     
SETIP 15;INPI 12;                 // AMOUNT                                     
ENTER;TSTOK;BNE E4;               // UNABLE TO APPLY CASH                       
RESO;WRITE;BR C1;                                                               
*----------------------------------------------------------------------         
* PROCESS SPECIAL SCREEN USED FOR UNAPPLIED CASH                                
*----------------------------------------------------------------------         
.A3;                                                                            
NTHUNP 2;CMTS "SPECIAL";BE A4;    // IS THIS SPECIAL SCREEN ?                   
PF02;TSTOK;EXITNE;                // GO BACK TO MARK SCREEN                     
.A4;                                                                            
LOCWRK PS;MVSW S;                 // SET SPECIAL SCREEN INDICATOR               
.A5;                                                                            
SETIP 27;                                                                       
NTHUNP 6;INPI 12;TAB;             // SOURCE                                     
INPI 6;TAB;                       // REFERENCE- BILL #                          
INPI 8;TAB;                       // DATE                                       
INPI 2;TAB;                       // OFFICE                                     
INPI 12;                          // AMOUNT                                     
ENTER;TSTOK;BNE E6;               // UNABLE TO APPLY CASH                       
RESO;WRITE;BR R1;                                                               
.E6;                                                                            
LOCWRK WE;MVSW E;                                                               
SAVERR;RESO;WRITE;                                                              
BR R1;                                                                          
*----------------------------------------------------------------------         
* ERRORS!                                                                       
*----------------------------------------------------------------------         
.E1;NTHUNP 5;SETIP 0;INPI 1;SUBIP 1;                                            
OUTERR "EA#9999 INVALID ITEM ACTION";BR E5;                                     
.E2;NTHUNP 5;SETIP 9;INPI 1;SUBIP 1;                                            
OUTERR "EA#9999 BILL NOT FOUND";BR E5;                                          
.E3;NTHUNP 5;SETIP 1;INPI 1;SUBIP 1;                                            
OUTERR "EA#9999 INVALID DATE";BR E5;                                            
.E4;NTHUNP 5;SETIP 15;INPI 1;SUBIP 1;                                           
OUTERR "EA#9999 INVALID AMOUNT";BR E5;                                          
.E5;WRITE;LOCWRK WE;MVSW E;                                                     
.C1;                                                                            
NTHUNP 5;INPS "//";ENTER;          // SCREEN CLEAR                              
BR R1;                                                                          
*----------------------------------------------------------------------         
* EXIT                                                                          
*----------------------------------------------------------------------         
.X1;                                                                            
NTHUNP 5;INPS "//";ENTER;          // SCREEN CLEAR                              
LOCWRK WE;CMWS E;BNE X2;                                                        
PF03;                                                                           
LOCTXT "Enter 'Y' to confirm";NXTUNP;                                           
INPS Y;ENTER;BR EX;                                                             
.X2;                                                                            
NTHUNP 4;INPS "UPD";               // REPORT ID OF 'UPD' FOR SCRIPT             
PF06;TSTOK;BE X3;                                                               
READ;WRITE;                                                                     
READ;RESO;WRITE;                                                                
LOCWRK WE;MVSW E;BR X1;            // ERROR                                     
.X3;                                                                            
LOCTXT "Enter 'Y' to confirm";NXTUNP;                                           
INPS Y;ENTER;BR EX;                                                             
.X4;                                                                            
SAVERR;RESO;WRITE;                                                              
.EX;                                                                            
END;                                                                            
