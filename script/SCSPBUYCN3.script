*          DATA SET SCSPBUYCN3 AT LEVEL 004 AS OF 05/30/02                      
USRI 8;SYSS SPOT;PRGS BUY;PWDI 8;$CT;                                           
* USRI 8;SYSS SPOT;PRGS BUY;PWDI 8;$CT;                                         
TSTOK;EXITNE;                                                                   
* READ A RECORD                                                                 
.RD;                                                                            
READ;EXITNE;                                                                    
CMIS NBUY;BNE RD;ADDIP 4;                                                       
FU;TAB;INPS //;ENTER;           // HAVE TO CLEAR INPUT FIELDS                   
FU;TAB;INPS N;TAB;INPS "SPBUYCN2";TAB;                                          
INPI 3;TAB;                     // CLIENT                                       
INPS POL;TAB;                   // PRODUCT                                      
INPI 3;TAB;                     // ESTIMATE                                     
INPI 4;TAB;                     // STATION                                      
*** NEW CODE FOR BUYID ***                                                      
SETIP 84;CMIS " ";BE N0;        // POINT TO BUYID AND TEST PRESENT              
INPS "ID=";INPI 12;BACKUP;      // BUYID                                        
.N0;                                                                            
SETIP 14;                       // POSITION POINTER TO BUYLINE                  
*** END CODE FOR BUYID ***                                                      
ENTER;TSTOK;BNE ER;             // IF ERROR, READ NEXT RECORD                   
ADDIP 3;                        // SKIP OVER BUYLINE NUMBER                     
NTHUNP 9;INPS "B,";                                                             
INPI 5;BACKUP;                  // START DATE                                   
INPS "-";                                                                       
INPI 2;BACKUP;                  // NUMBER OF WEEKS                              
INPS "W,";                                                                      
INPI 7;BACKUP;INPS ",";         // DAYS                                         
INPI 2;BACKUP;INPS ",";         // NUMBER PER WEEK                              
INPI 11;BACKUP;INPS ",";        // START-END TIME                               
INPI 1;BACKUP;INPS ",";         // DAYPART                                      
INPI 3;BACKUP;INPS ",";         // SPOT LENGTH                                  
*** NEW CODE FOR ADJACENCY CODE  ***                                            
INPI 17;BACKUP;INPS ",";        // PROGRAM                                      
ADDIP 17;CMIS " ";BE N2;        // POINT TO ADJ CODE                            
INPI 2;BACKUP;INPS ",";SUBIP 2;  // INPUT ADJ CODE THEN BACKUP                  
.N2;                                                                            
SUBIP 17;                                                                       
*** END CODE FOR ADJACENCY CODE  ***                                            
INPI 10;BACKUP;                 // COST PER SPOT                                
CMIS A;BL N4;                   // IF INPUT < A, NO MASTER PRODS                
CMIS POL;BE N4;                 // IF INPUT = POL, NO MASTER PRODS              
INPS ",";INPS "M=";                                                             
INPI 7;BR N6;                   // MASTER PRODUCT 1 & 2                         
.N4;                                                                            
ADDIP 7;                        // SKIP PAST MASTER PRODUCT 1 & 2               
.N6;                                                                            
*** NEW CODE TO SKIP NEW FIELDS ***                                             
ADDIP 14;                       // SKIP PAST ADJ CODE AND BUY ID                
*** END CODE TO SKIP NEW FIELDS ***                                             
ENTER;TSTOK;BNE ER;                                                             
NTHUNP 9;NEXT;FLDOFF 76;        // GET BUY NUM FROM SCREEN                      
SETWP 0;OUTW 3;                 // WRITE INTO WORK AREA                         
NTHUNP 9;                                                                       
SETWP 0;INPW 3;INPS "S";        // WRITE IT BACK ONTO SCREEN                    
ENTER;TSTOK;BNE ER;                                                             
NTHUNP 9;INPS S;                // ENTER 'S' INTO FIELD                         
NTHUNP 13;                      // ENTER WEEKLY SPOTS                           
.I4;                                                                            
CMIS /Z;BE I8;                  // IF INPUT FIELD IS /Z, GET OUT                
SAVFP;TAB;TAB;BNE I8;           // MUST TAB TWICE AND CHECK IF GO               
RESFP;CMIS "ºº";BE I5;          // OFF END OF SCREEN BECAUSE OF EXTRA           
INPI 2;BR I6;                   // UNPROTECTED FLD AT END OF SCREEN             
.I5;                                                                            
INPS "0º";ADDIP 2;              // IF BLANK SPOT, ENTER 0 ON SCREEN             
.I6;                                                                            
TAB;BR I4;                                                                      
.I8;                                                                            
ENTER;TSTOK;BNE ER;                                                             
FSTPRO;RESO;WRITE;BR RD;                                                        
.ER;                                                                            
SAVERR;RESO;WRITE;BR RD;                                                        
.AB;                                                                            
ABEND;                                                                          
