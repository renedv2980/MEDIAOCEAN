*          DATA SET SCACINP62  AT LEVEL 005 AS OF 05/29/02                      
********************************************************************            
* ACC / INPUT, TYPE 62                                             *            
********************************************************************            
#SCRIPT ACINP62                                                                 
SETWP 0;LBLW W1;ADDWP 1;LBLW W2;       // SET UP WORKING STORAGE                
USRI 8;SYSS ACC;PRGS INP;PWDI 8;$CT;                                            
TSTOK;EXITNE;                                                                   
FU;TAB;INPS BATCH;TAB;INPS OPEN;ENTER;                                          
TSTOK;EXITNE;                                                                   
* READ THE BATCH HEADER DATA RECORD                                             
READ;EXITNE;                                                                    
* BATCH REF     BATCH NAME            BATCH TYPE                                
LOCNUM 3;INPI 4;LOCNUM 5;INPI 15;LOCNUM 7;INPI 3;                               
*   MOA        EFF DT                                                           
LOCNUM 8;INPI 6;LOCNUM 12;INPI 8;                                               
*   ITM CNT      BATCH TOT                                                      
LOCNUM 20;INPS 250;LOCNUM 24;INPS 999999;                                       
*   COMMENTS                                                                    
LOCNUM 32;INPI 50;                                                              
*----------------------------        HANDLE ENDING STATUS                       
LOCWRK W1;                                                                      
CMIS " ";BE S1;CMIS C;BE S1;CMIS c;BNE S2;                                      
.S1;MVSW C;BR S7;                                                               
.S2;                                                                            
CMIS S;BE S3;CMIS s;BNE S4;                                                     
.S3;MVSW S;BR S7;                                                               
.S4;                                                                            
CMIS U;BE S5;CMIS u;BNE S6;                                                     
.S5;MVSW U;BR S7;                                                               
.S6;OUTERR "EA#9999 INVALID ENDING STATUS FOR BATCH";                           
WRITE;EXIT;                                                                     
.S7;                                                                            
*----------------------------                                                   
ENTER;TSTOK;BE L0;                                                              
* NOTE THAT NEXT READ FORCES PREVIOUS WRITE !!! (MH JAN/95)                     
SAVERR;RESO;WRITE;READ;EXIT;                                                    
.L0;                                                                            
* NEED TO WRITE A RESULT CODE TO BATCH HEADER RECORD !                          
FSTPRO;RESO;WRITE;                                                              
*-------------------------------------------------------------------            
* INPUT SCREEN SHOULD BE IN PLACE NOW -- POSN TO CLIENT/PROD/JOB                
*-------------------------------------------------------------------            
.L1;                                                                            
READ;BNE L9;NTHUNP 7;INPS //;ENTER;                                             
LOCWRK W2;MVSW 0;                                                               
.L2;                                                                            
NTHUNP 7;                                                                       
* CLI PRODUCT JOB(LEN 12) SPACES MEANS THIS IS A TAX ONLY ITEM                  
CMIS "            ";BE L6;                                                      
*CLI       PRODUCT    JOB        DATE       REFNC      COMM                     
INPI 3;TAB;INPI 3;TAB;INPI 6;TAB;INPI 8;TAB;INPI 6;TAB;INPI 1;                  
*CATEGORY                                                                       
TAB;INPI 2;TAB;TAB;TAB;            //SKIP NARRATIVE FOR NOW                     
* UNITS    LVL        WC         TX         UN PR       INC ACCT                
INPI 6;TAB;INPI 1;TAB;INPI 2;TAB;INPI 2;TAB;INPI 10;TAB;INPI 14;TAB;            
NTHUNP 14;ADDIP 50;                                                             
INPI 49;TAB;INPI 49;              //NOW FILL IN NARRATIVE                       
SUBIP 148;                                                                      
ENTER;TSTOK;BNE X1;                                                             
NTHUNP 19;CMTS "Y";BE T1;        // DO WE NEED TAX PART?                        
NTHUNP 19;CMTS "*Y";BE T1;       // DO WE NEED TAX PART?                        
LOCNUM 83;INPS Y;                // NO TAX, CONTINUE                            
ENTER;TSTOK;BNE X1;BR U9;        // SKIP TAX PART                               
.L6;                             // TAX ONLY ITEM                               
LOCWRK W2;MVSW 1;                // SET SWITCH FOR 1 PASS                       
ADDIP 64;                        // RESET INPUT PTR TO TAX DR ACCT              
PF09;TSTOK;BNE X1;BR T4;         // PF09 TO TAX SCREEN                          
*-------------------------------------------------------------------            
* TAX SCREEN                                                                    
*-------------------------------------------------------------------            
.T1;                                                                            
LOCWRK W2;CMWS 0;BNE T2;                                                        
LOCNUM 83;INPS S;BR T3;          // SCRIPT BYPASS FOR ERRORS                    
.T2;                                                                            
LOCNUM 83;INPS Y;                // 2ND PASS, OK                                
.T3;                                                                            
ENTER;TSTOK;BNE X1;                                                             
.T4;                                                                            
NTHUNP 7;                                                                       
* DB/ACC                                                                        
CMIS "              ";BNE T5;ADDIP 14;TAB;BR T6;                                
.T5;                                                                            
INPI 14;TAB;                                                                    
.T6;                                                                            
* DOC NUM                                                                       
CMIS "      ";BNE T7;ADDIP 6;TAB;BR T8;                                         
.T7;                                                                            
INPI 6;TAB;                                                                     
.T8;                                                                            
* DATE                                                                          
CMIS "        ";BNE T9;ADDIP 8;TAB;BR U0;                                       
.T9;                                                                            
INPI 8;TAB;                                                                     
.U0;                                                                            
* LOCALITY                                                                      
NTHUNP 10;                                                                      
TSTS "ººººººººº";BNH U2;                                                        
CMIS "ººººººººº";BNE U2;ADDIP 9;TAB;BR U3;                                      
.U2;                                                                            
INPI 9;TAB;                                                                     
.U3;                                                                            
* BASIS                                                                         
CMIS "           ";BNE U4;ADDIP 11;TAB;BR U5;                                   
.U4;                                                                            
INPI 11;TAB;                                                                    
.U5;                                                                            
* WC                                                                            
CMIS "  ";BNE U6;ADDIP 2;BR U7;                                                 
.U6;                                                                            
INPI 2;                                                                         
.U7;                                                                            
* NARR                                                                          
LOCNUM 59;INPI 49;TAB;INPI 49;                                                  
ENTER;TSTOK;BE U8;LOCCUR;                                                       
SAVERR;RESO;WRITE;PF11;BR L1;   // SAVE ERROR, RETURN TO MAIN SCRN              
.U8;                                                                            
PF11;                                                                           
LOCWRK W2;CMWS 1;BE U9;         // DONE WITH 2 PASS                             
LOCWRK W2;MVSW 1;                                                               
SETIP 0;BR L2;                // RESET INPUT PTR AND REENTER                    
.U9;                                                                            
FSTPRO;RESO;WRITE;BR L1;                                                        
*-------------------------------------------------------------------            
* ERRORS!                                                                       
*-------------------------------------------------------------------            
.X1;                                                                            
SAVERR;RESO;WRITE;                                                              
BR L1;                                                                          
*-------------------------------------------------------------------            
* THIS ENDS THE BATCH BASED ON THE ENDING STATUS                                
*-------------------------------------------------------------------            
.L9;                                                                            
LOCWRK W1;CMWS S;BNE R1;PF02;EXIT;         // SAVE                              
.R1;LOCWRK W1;CMWS U;BNE R2;PF06;BR R3;    // UPDATE                            
.R2;PF05;                                  // CLOSE                             
.R3;TSTOK;EXITNE;                                                               
NTHUNP 7;INPS &&;TAB;INPS &&;ENTER;        // TELL BATCH TO SET                 
TSTOK;EXITNE;                              // CONTROL TOTALS ITSELF             
END;                                                                            
