        /* DATA SET ACSAPICEVC AT LEVEL 022 AS OF 02/10/21    */                
       ++INCLUDE IEBUPDTHDR                                                     
                                                                                
**** LOAD=Y MEANS LOAD MODE, OTHERWISE UPDATE MODE.                             
* SET DEFAULT TO LOAD=N. CAN BE OVERRIDDEN IN THE JCL TO LOAD=Y.                
*&&      SET   LOAD=N                                                           
                                                                                
**** TEST=Y IS FOR DEBUGGING OR REGRESSION TESTING ONLY.                        
* SET DEFAULT TO TEST=N. CAN BE OVERRIDDEN IN THE JCL TO TEST=Y.                
*&&      SET   TEST=N                                                           
                                                                                
**** SUPP=N IS FOR DEBUGGING ONLY. USE THIS WHEN NO SUPPORT DATA IS             
****     AVAILABLE, SO THAT ACC TRANSACTIONS WILL BE PROCESSED THAT             
****     WOULD OTHERWISE ONLY GO TO THE EXCEPTION FILE.                         
* SET DEFAULT TO SUPP=Y. CAN BE OVERRIDDEN IN THE JCL TO SUPP=N.                
*&&      SET   SUPP=Y                                                           
                                                                                
***********************************************************************         
                                                                                
       SAP MEDIA VENDOR CLEARANCE EXPORT: XML FILE GENERATION                   
                                                                                
IMPORTANT NOTE: this ICETOOL application sets RC=4 if there are no data         
records in the generated XML file. We do this by using the NULLOFL              
option. The JCL downstream from this ICETOOL step tests for RC=4, and           
does not transmit the XML file if this step completes with a non-zero           
return code. Therefore, if any COUNT operators are added to this                
program which may set the return code to a non-zero value, then the             
JCL may need to be changed!                                                     
                                                                                
***********************************************************************         
***********************************************************************         
MNAS 017 Feb15/19 : SPEC-12263 Multi Currency                                   
JSHA 017          : test agencies 0B, 02, 01 DDSBO, DDS2O, DDS10                
JSHA 017 Aug16/19 : SPEC-38053 - Fix for Multi Currency                         
JSHA 017 Mar26/20 : SPEC-44647 - Fix for SRTITEMS Issue in FX01CNTL             
JSHA 017 Mar28/20 : ITMF-45199 - Further fix to SRTITEMS in SRTICNTL            
                                 Remove D_AXG$NETW from SORT FIELDS             
JSHA 018 Aug21/19 : SPEC-33177 - SAP Brand ID                                   
JSHA 020 Nov19/20 : SPEC-51798 - FIX for Print ALL Products Brand Code          
RKEJ 021 Jul21/20 : SPEC-34656 - F/X Postings save in Exceptions file           
JSHA 022 Jan29/21 : SPEC-53480 - FIX for SPEC-51798 - I had an AND              
                    instead of an OR on the 2nd line of the EXCEP file          
***********************************************************************         
./ ADD NAME=SYMNAMES   ** SYMBOLICS USED IN CONTROL CARDS                       
*                                                                               
PIPE,C'|'                          DELIMITER                                    
TAB,X'05'                          DELIMITER                                    
*                                                                               
F1_ONLY,C'1'                       JOIN RESULT INDICATORS (AS PER IBM)          
F2_ONLY,C'2'                                                                    
BOTH_F1_AND_F2,C'B'                                                             
*                                                                               
* Parsed record variables (for the ACC extract dataset)                         
*                                                                               
P_AXG$ACCD,%107                    ACCOUNT CODE                                 
P_AXG$TRDT,%110                    TRANSACTION DATE                             
P_AXG$TRRF,%111                    REFNUM                                       
P_AXG$SYS,%113                     SYSTEM CODE                                  
P_AXG$MED,%114                     MEDIA                                        
P_AXG$PRD,%116                     CLIENT/PRODUCT                               
P_AXG$EST,%117                     ESTIMATE                                     
P_AXG$MOA,%121                     MONTH OF ACTIVITY                            
P_AXG$ACDT,%122                    ACTIVITY DATE                                
P_AXG$MOS,%124                     MONTH OF SERVICE                             
P_AXG$DUDT,%133                    DUE DATE                                     
P_AXG$INV,%134                     INVOICE NUMBER (LONG)                        
P_AXG$MSYS,%147                    SAP MEDIA SYSTEM                             
P_AXG$VCH#,%148                    SAP VOUCHER NUMBER                           
P_AXG$CURR,%149                    SAP CURRENCY (USD/CND)                       
P_AXG$XCHR,%150                    SAP EXCHANGE RATE                            
P_AXG$STYP,%151                    SAP TRANSACATION TYPE                        
P_AXG$NETW,%152                    SAP NETWORK                                  
P_AXG$NETA,%154                    SAP NET AMOUNT                               
P_AXG$TAXA_HST,%155                SAP TAX AMOUNT (HST)                         
P_AXG$TAXC,%156                    SAP TAX CODE                                 
P_AXG$KEY,%157                     SAP MEDIAOCEAN A/R KEY                       
P_AXG$SINV,%158                    INVOICE NUMBER (SHORT)                       
P_AXG$PAYE,%159                    SAP PAYEE                                    
P_AXG$MEDT,%160                    SAP MEDIA TYPE                               
P_AXG$SMED,%161                    SAP SUBMEDIA                                 
P_AXG$MATL,%162                    SAP MATERIAL                                 
P_AXG$INS#,%163                    SAP INSERTION NUMBER                         
P_AXG$TAXM_HST,%164                SAP TAX MATERIAL CODE (HST)                  
P_AXG$VORR,%165                    SAP VENDOR OR REP (V/R)                      
P_AXG$TAXA_GST,%166                SAP TAX AMOUNT (GST)                         
P_AXG$TAXM_GST,%167                SAP TAX MATERIAL CODE (GST)                  
P_AXG$TAXA_QST,%168                SAP TAX AMOUNT (QST)                         
P_AXG$TAXM_QST,%169                SAP TAX MATERIAL CODE (QST)                  
*                                                                               
* Open AP file symbols                                                          
*                                                                               
OPEN_ACC2,1,12,CH                                                               
OPEN_SMC2,*,2,CH                                                                
OPEN_OFC2,*,2,CH                                                                
OPEN_BDTE2,*,6,CH                                                               
OPEN_REF2,*,6,CH                                                                
OPEN_SEQ2,*,3,ZD                                                                
OPEN_BAL2,*,14,CH                                                               
*                                                                               
*                                                                               
./ ADD NAME=TOOLIN     ** CONTROL CARDS FOR ICETOOL                             
*                                                                               
* Parse the ACC extract file, keeping only Spot and Print transactions.         
* Produce multiple output files.                                                
COPY FROM(XTRACTIN) USING(PARS)                                                 
*                                                                               
*&&LOAD                                                                         
* Join the open accounts payable file with the ACC extract file.                
COPY JKFROM USING(JONA)                                                         
*                                                                               
* Produce a report showing the missing accounts.                                
DISPLAY FROM(OPNAPERR) LIST(OPNAPRPT) -                                         
      TITLE('Open AP Missing Accounts') -                                       
      DATE TIME PAGE -                                                          
      BLANK -                                                                   
      HEADER('Acct','Number') ON(REFB_OPEN_ACC2)                                
*                                                                               
* There should be no open accounts payable without matching                     
* transactions on the ACC extract file.                                         
****COUNT FROM(OPNAPERR) NOTEMPTY RC8                                           
*&&                                                                             
*                                                                               
*&!LOAD                                                                         
COPY FROM(PARSFIL0) TO(PARSFIL1)                                                
*&!                                                                             
*                                                                               
* Sort the support file.                                                        
SELECT FROM(SUPPORT) TO(SUPPSORT) DISCARD(SUPPDUPS) ON(SAPRKEY) FIRST -         
         USING(SEL1)                                                            
*                                                                               
* Confirm that there are no duplicate keys in the support file.                 
COUNT FROM(SUPPDUPS) NOTEMPTY RC8                                               
*                                                                               
* Join the ACC extract and support files on the MO client code.                 
COPY JKFROM USING(JONC)                                                         
*                                                                               
* Join the ACC extract and support files on the MO office code.                 
COPY JKFROM USING(JONO)                                                         
*                                                                               
* Join the ACC extract and support files on the MO product code.                
COPY JKFROM USING(JONB)                                                         
*                                                                               
* Join the ACC extract and support files on the Payee (i.e., Vendor).           
COPY JKFROM USING(JONP)                                                         
*                                                                               
* Discard all transactions with errors, producing an FB dataset of              
* transactions to be joined with vendor data. This is the "detail               
* work file".                                                                   
COPY FROM(ALLRECS) TO(GOODTRNS) USING(COPG)                                     
*                                                                               
* Copy GOODTRNS to another dataset, so we can join it with itself.              
COPY FROM(GOODTRNS) TO(TEMP1)                                                   
*                                                                               
* Seed the invoice gross $ into the detail work file.                           
COPY JKFROM TO(JOINVEND) USING(JONH)                                            
*                                                                               
* Generate a tab-delimited ACC transaction exception file for import            
* into Excel.                                                                   
COPY FROM(EXCEPTN) TO(EXCEPTAB) USING(COPH)                                     
*                                                                               
DISPLAY FROM(EXCEPTN) LIST(XCEPTRPT) -                                          
      TITLE('SAP Vendor Clearance Export Exception Report') -                   
      DATE TIME PAGE -                                                          
      BLANK -                                                                   
      BTITLE('Canadian agency') BREAK(A_RESOURCES_AGY) -                        
      HEADER('Acct#') ON(AXG$ACCD) -                                            
      HEADER('Ref#') ON(AXG$TRRF) -                                             
      HEADER('System') ON(AXG$SYS) -                                            
      HEADER('Media') ON(AXG$MED) -                                             
      HEADER('Client') ON(AXG$PRD_CLT) -                                        
      HEADER('Product') ON(AXG$PRD_PRD) -                                       
      HEADER('Est') ON(AXG$EST_EST) -                                           
      HEADER('Media','Office') ON(A_SUP_SAPROFFC) -                             
      HEADER('Activity','Date') ON(AXG$ACDT) -                                  
      HEADER('Month of','Service') ON(AXG$MOS) -                                
      HEADER('Invoice','Number') ON(AXG$INV) -                                  
      HEADER('Payee') ON(AXG$PAYE) -                                            
      HEADER('Payee','Type') ON(AXG$VORR) -                                     
      HEADER('Error','Message') ON(A_ERROR_TEXT)                                
*                                                                               
* Generate ACC exception file to be concatenated the following night.           
COPY JKFROM TO(TEMP4) USING(COPE)                                               
*                                                                               
* Parse Exception (TEMP4) file and F/X (ONLYFX2) file and reformat them         
COPY FROM(TEMP4) TO(TEMP5) USING(COP2)                                          
COPY FROM(ONLYFX2) TO(TEMP6) USING(COP2)                                        
*                                                                               
* Join Reformatted Exception file (TEMP5) to Reformatted F/X (TEMP6)            
* file to get the matching F/X postings.                                        
COPY JKFROM TO(TEMP7) USING(COP3)                                               
*                                                                               
* Concatenate the F/X matched postings to the original Exception file.          
COPY FROM(TEMP4) TO(TEMP7)                                                      
*                                                                               
* Copy the Final Exception file.                                                
COPY FROM(TEMP7) TO(EXCEPOUT)                                                   
*                                                                               
* Produce tax addendum records.                                                 
SORT FROM(JOINVEND) TO(SPECIALS) USING(SRTV)                                    
*                                                                               
* Produce variance addendum records.                                            
COPY JKFROM TO(SPECIALS) USING(COP1)                                            
*                                                                               
* Concatenate the addendum records to the detail work file.                     
COPY FROM(SPECIALS) TO(JOINVEND)                                                
*                                                                               
* Sort the detail work file by invoice number.                                  
SORT FROM(JOINVEND) TO(SRTITEMS) USING(SRTI)                                    
*                                                                               
* Calculate the exchange rate if necessary.                                     
COPY JKFROM TO(TEMP2) USING(FX01)                                               
*                                                                               
* Copy TEMP2 to another dataset, so we can join it with itself.                 
COPY FROM(TEMP2) TO(TEMP3)                                                      
*                                                                               
* Seed the Net_Amount_Local $ into the detail work file.                        
COPY JKFROM TO(FINAL) USING(JONK)                                               
*                                                                               
* Copy FINAL to a tab-delimited file we can import into Excel (for              
*  debugging).                                                                  
COPY FROM(FINAL) TO(JOINDEB) USING(JDEB)                                        
*                                                                               
* Generate an XML output file. This file is syntactically correct, but          
* contains the more verbose syntax for empty fields.                            
COPY FROM(FINAL) USING(COPX)                                                    
*                                                                               
* Prettify the XML output file by shortening tags with empty fields.            
COPY FROM(TEMPXML) TO(XMLOUT) USING(CPTX)                                       
                                                                                
                                                                                
./ ADD NAME=SEL1CNTL                                                            
*                                                                               
* Change all "R" (Rep) support records to "V" (Vendor) support records          
* before checking for duplicates.                                               
*                                                                               
 INREC IFTHEN=(WHEN=(SAPKTYPE,EQ,SAPKTYPE_REP),                                 
                      OVERLAY=(SAPKTYPE:SAPKTYPE_VNDR))                         
                                                                                
                                                                                
./ ADD NAME=PARSCNTL                                                            
*                                                                               
 INCLUDE COND=(5,5,CH,EQ,C'05061')  Note: RECTYPE is always in column 5         
*                                                                               
 INREC PARSE=(%=(ENDBEFR=PIPE,REPEAT=7),                                        
           P_AXG$ACCD=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$ACCD), for debugging         
           %=(ENDBEFR=PIPE,REPEAT=2),                                           
           P_AXG$TRDT=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$TRDT),                       
           P_AXG$TRRF=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$TRRF), for debugging         
           %=(ENDBEFR=PIPE),                                                    
           P_AXG$SYS=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$SYS),                         
           P_AXG$MED=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$MED),                         
           %=(ENDBEFR=PIPE),                                                    
           P_AXG$PRD=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$PRD),  CLIENT/PRODUCT         
           P_AXG$EST=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$EST_EST),                     
           %=(ENDBEFR=PIPE,REPEAT=3),                                           
           P_AXG$MOA=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$MOA),                         
           P_AXG$ACDT=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$ACDT),                       
           %=(ENDBEFR=PIPE),                                                    
           P_AXG$MOS=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$MOS),                         
           %=(ENDBEFR=PIPE,REPEAT=8),                                           
           P_AXG$DUDT=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$DUDT),                       
           P_AXG$INV=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$INV),                         
           %=(ENDBEFR=PIPE,REPEAT=12),                                          
           P_AXG$MSYS=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$MSYS),                       
           P_AXG$VCH#=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$VCH#),                       
           P_AXG$CURR=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$CURR),                       
           P_AXG$XCHR=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$XCHR),                       
           P_AXG$STYP=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$STYP),                       
           P_AXG$NETW=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$NETW),                       
           P_AXG$NETA=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$NETA),                       
           P_AXG$TAXA_HST=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$TAXA_HST),               
           P_AXG$TAXC=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$TAXC),                       
           P_AXG$KEY=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$KEY),                         
           P_AXG$SINV=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$SINV),                       
           P_AXG$PAYE=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$PAYE),                       
           P_AXG$MEDT=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$MEDT),                       
           P_AXG$SMED=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$SMED),                       
           P_AXG$MATL=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$MATL),                       
           P_AXG$INS#=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$INS#),                       
           P_AXG$TAXM_HST=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$TAXM_HST),               
           P_AXG$VORR=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$VORR),                       
           P_AXG$TAXA_GST=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$TAXA_GST),               
           P_AXG$TAXM_GST=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$TAXM_GST),               
           P_AXG$TAXA_QST=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$TAXA_QST),               
           P_AXG$TAXM_QST=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$TAXM_QST)),              
    BUILD=(AXG$LEN,                                                             
           AXG$ACCD:P_AXG$ACCD,                                                 
           AXG$TRDT:P_AXG$TRDT,                                                 
           AXG$TRRF:P_AXG$TRRF,                                                 
           AXG$SYS:P_AXG$SYS,                                                   
           AXG$MED:P_AXG$MED,                                                   
           AXG$PRD:P_AXG$PRD,                                                   
           AXG$EST_EST:P_AXG$EST,                                               
           AXG$MOA:P_AXG$MOA,                                                   
           AXG$ACDT:P_AXG$ACDT,                                                 
           AXG$MOS:P_AXG$MOS,                                                   
           AXG$DUDT:P_AXG$DUDT,                                                 
           AXG$INV:P_AXG$INV,                                                   
           AXG$MSYS:P_AXG$MSYS,                                                 
           AXG$VCH#:P_AXG$VCH#,                                                 
           AXG$CURR:P_AXG$CURR,                                                 
           AXG$XCHR:P_AXG$XCHR,                                                 
           AXG$STYP:P_AXG$STYP,                                                 
           AXG$NETW:P_AXG$NETW,                                                 
           AXG$NETA:P_AXG$NETA,SFF,TO=ZD,                                       
           AXG$TAXA_HST:P_AXG$TAXA_HST,SFF,TO=ZD,                               
           AXG$TAXC:P_AXG$TAXC,                                                 
           AXG$KEY:P_AXG$KEY,                                                   
           AXG$SINV:P_AXG$SINV,                                                 
           AXG$PAYE:P_AXG$PAYE,                                                 
           AXG$MEDT:P_AXG$MEDT,                                                 
           AXG$SMED:P_AXG$SMED,                                                 
           AXG$MATL:P_AXG$MATL,                                                 
           AXG$INS#:P_AXG$INS#,                                                 
           AXG$TAXM_HST:P_AXG$TAXM_HST,                                         
           AXG$VORR:P_AXG$VORR,                                                 
           AXG$TAXA_GST:P_AXG$TAXA_GST,SFF,TO=ZD,                               
           AXG$TAXM_GST:P_AXG$TAXM_GST,                                         
           AXG$TAXA_QST:P_AXG$TAXA_QST,SFF,TO=ZD,                               
           AXG$TAXM_QST:P_AXG$TAXM_QST,                                         
           A_ACC_SEQNUM:SEQNUM,LEN_A_ACC_SEQNUM,PD,                             
           A_ORIGINAL_REC_VL:5)   *** Original record starts in col. 5          
*                                                                               
 OUTREC OVERLAY=(A_RESOURCES_AGY:AXG$MSYS_AGY,                                  
                 CHANGE=(LEN_D_RESOURCES_AGY,   ** for Re:Sources               
                         C'TB',C'ZO',                                           
                         C'O0',C'SM',                                           
                         C'SO',C'SS',                                           
                         C'VA',C'ZO',                                           
                         C'VI',C'SM',                                           
                         C'VH',C'SS',                                           
                         C'YE',C'ZO',                                           
                         C'YF',C'SS',                                           
                         C'*B',C'*B',                                           
                         C'OB',C'OB',                                           
                         C'*2',C'*2',                                           
                         C'O2',C'O2',                                           
                         C'O1',C'O1',                                           
                         C'T1',C'T1',                                           
                         C'Y1',C'SM'))                                          
*                                                                               
* Include only payables for Spot and Print. Split the output records            
* into multiple output files:                                                   
*  1. Only F/X transactions                                                     
*  2. Only VAR transactions                                                     
*  3. All other transactions                                                    
*                                                                               
 OUTFIL FNAMES=ONLYFX2,              ** F/X transactions only                   
   INCLUDE=((AXG$SYS,EQ,SAPKSYS_SPOT,OR,AXG$SYS,EQ,SAPKSYS_PRNT),AND,           
             AXG$UNIT_LEDGER,NE,C'SR',AND,                                      
             AXG$STYP,EQ,C'F/X'),                                               
   BUILD=(1,4,A_ORIGINAL_REC_VL)                                                
                                                                                
 OUTFIL FNAMES=ONLYFX,               ** F/X transactions only                   
   INCLUDE=((AXG$SYS,EQ,SAPKSYS_SPOT,OR,AXG$SYS,EQ,SAPKSYS_PRNT),AND,           
             AXG$UNIT_LEDGER,NE,C'SR',AND,                                      
             AXG$STYP,EQ,C'F/X'),                                               
   VTOF,                                                                        
   BUILD=(D_AXG$SYS:AXG$SYS,         ** only save what we need later on         
          D_AXG$MED:AXG$MED,                                                    
          D_AXG$CLT:AXG$PRD_CLT,                                                
          D_AXG$PRD:AXG$PRD_PRD,                                                
          D_AXG$EST:AXG$EST_EST,                                                
          D_AXG$INV:AXG$INV,                                                    
          D_AXG$CURR:AXG$CURR,                                                  
          D_AXG$NETW:AXG$NETW,                                                  
          D_AXG$NETA:AXG$NETA)                                                  
                                                                                
 OUTFIL FNAMES=ONLYVAR,              ** VAR transactions only                   
   INCLUDE=(AXG$SYS,EQ,SAPKSYS_SPOT,AND,                                        
            AXG$UNIT_LEDGER,NE,C'SR',AND,                                       
            AXG$STYP,EQ,C'VAR'),                                                
   VTOF,                                                                        
   BUILD=(D_AXG$SYS:AXG$SYS,         ** only save what we need later on         
          D_AXG$MED:AXG$MED,                                                    
          D_AXG$CLT:AXG$PRD_CLT,                                                
          D_AXG$PRD:AXG$PRD_PRD,                                                
          D_AXG$EST:AXG$EST_EST,                                                
          D_AXG$INV:AXG$INV,                                                    
          D_AXG$MOS:AXG$MOS,                                                    
          D_AXG$NETW:AXG$NETW,                                                  
          D_AXG$NETA:AXG$NETA,                                                  
          D_AXG$MATL:AXG$MATL)                                                  
                                                                                
 OUTFIL FNAMES=PARSFIL0,             ** "real" transactions only                
   VTOF,                                                                        
   INCLUDE=((AXG$SYS,EQ,SAPKSYS_SPOT,OR,AXG$SYS,EQ,SAPKSYS_PRNT),AND,           
             AXG$UNIT_LEDGER,NE,C'SR',AND,                                      
             AXG$STYP,NE,C'F/X',AND,                                            
             AXG$STYP,NE,C'VAR'),                                               
** Start building in column 5, to account for the absence of the RDW !          
   BUILD=(5:AXG$RECORD,              *** start in column 5                      
          A_RESOURCES_AGY:A_RESOURCES_AGY,                                      
          A_ACC_SEQNUM:A_ACC_SEQNUM)                                            
                                                                                
 OUTFIL FNAMES=ACCSAVE,                                                         
   BUILD=(1,4,                                                                  
          O_ACC_SEQNUM:A_ACC_SEQNUM,                                            
          O_ORIGINAL_REC_VL:A_ORIGINAL_REC_VL)                                  
                                                                                
                                                                                
./ ADD NAME=JONACNTL                                                            
*                                                                               
* Join the open accounts payable file with the ACC extract file, and            
* discard any transactions which are not present in the open accounts           
* payable file.                                                                 
*                                                                               
 JOINKEYS F1=OPENAP,                                                            
             TASKID=T2,          <<<< SEE T2F1CNTL                              
             FIELDS=(OPEN_ACC2,A)            ** account# (w/out UL)             
 JOINKEYS F2=PARSFIL0,                                                          
             FIELDS=(AXG$ACCD_ACCT,A)                                           
 JOIN UNPAIRED,F1    *** keep matched accts and unmatched open AP accts         
 REFORMAT FIELDS=(?,                  ** REFB_JOIN_RESULT_INDICATOR             
                  F1:OPEN_ACC2,       ** REFB_OPEN_ACC2                         
                  F2:AXG$RECORD,      ** REFB_AXG$RECORD                        
                     A_RESOURCES_AGY, ** REFB_A_RESOURCES_AGY                   
                     A_ACC_SEQNUM)                                              
*                                                                               
 OUTFIL FNAMES=OPNAPERR,             ** unmatched F1 records go here            
   INCLUDE=(REFB_JOIN_RESULT_INDICATOR,EQ,F1_ONLY),                             
   BUILD=(REFB_JOIN_RESULT_INDICATOR,                                           
          REFB_OPEN_ACC2)            ** this is what we really need             
 OUTFIL FNAMES=PARSFIL1,             ** parsed ACC transaction recs.            
   SAVE,                             ** (anything not saved already)            
** Start building in column 5, to account for the absence of the RDW !          
   BUILD=(5:REFB_AXG$RECORD,         ** entire parsed ACC trans. record         
          A_RESOURCES_AGY:REFB_A_RESOURCES_AGY,                                 
          A_ACC_SEQNUM:REFB_A_ACC_SEQNUM)                                       
                                                                                
                                                                                
./ ADD NAME=T2F1CNTL                                                            
*                                                                               
* This collapses F1 *after* the sort (where the sort key is defined             
* by the F1 JOINKEYS fields above), retaining only the first duplicate.         
*                                                                               
 SUM FIELDS=(NONE)                                                              
                                                                                
                                                                                
./ ADD NAME=JONCCNTL                                                            
*                                                                               
* Join the ACC extract and support files on the MO client code, and             
* extract the SAP client code and MO media office code from the                 
* support record.                                                               
*                                                                               
 JOINKEYS F1=SUPPSORT,                                                          
             FIELDS=(SAPKSYS,A,                                                 
                     SAPKMED,A,                                                 
                     SAPKCLT,A),                                                
             SORTED,                                                            
             INCLUDE=(SAPKTYPE,EQ,SAPKTYPE_CLT)                                 
 JOINKEYS F2=PARSFIL1,                                                          
             FIELDS=(AXG$SYS,A,                                                 
                     AXG$MED,A,                                                 
                     AXG$PRD_CLT,A)                                             
 JOIN UNPAIRED,F2    *** keep *both* matched and unmatched transactions         
 REFORMAT FIELDS=(F2:AXG$RECORD,          ** all parsed ACC data                
                  ?,                      ** JOIN result indicator              
                  F1:SAPRCUST,            ** from support file                  
                     SAPROFFC,            ** from support file                  
                  F2:A_RESOURCES_AGY,                                           
                     A_ACC_SEQNUM),       ** from ACC transaction file          
                  FILL=X'00'                                                    
*                                                                               
* Build our work record from the reformatted fields.                            
 INREC BUILD=(AXG$TYPE:REFC_AXG$RECORD,                                         
              A_JOIN_RESULT:REFC_JOIN_RESULT_INDICATOR,                         
              A_SUP_SAPRCUST:REFC_A_SUP_SAPRCUST,                               
              A_SUP_SAPROFFC:REFC_A_SUP_SAPROFFC,                               
              A_RESOURCES_AGY:REFC_A_RESOURCES_AGY,                             
              A_ACC_SEQNUM:REFC_A_ACC_SEQNUM)                                   
*                                                                               
 OUTFIL FNAMES=PARSFIL2            *** all records go here                      
*                                                                               
 OUTFIL FNAMES=EXCEPTN,            *** only problematic records go here         
   INCLUDE=(A_JOIN_RESULT,EQ,F2_ONLY,OR,           *** missing client           
           (A_JOIN_RESULT,EQ,BOTH_F1_AND_F2,AND,   *** missing data             
            (A_SUP_SAPRCUST,EQ,X'00',OR,                                        
             A_SUP_SAPROFFC,EQ,X'00'))),                                        
   OVERLAY=(A_ERROR_TEXT:C'MISSING SAP CLIENT DATA ')                           
                                                                                
                                                                                
./ ADD NAME=JONOCNTL                                                            
*                                                                               
* Join the ACC extract and support files on the media office key, and           
* extract the SAP media office data.                                            
*                                                                               
 JOINKEYS F1=SUPPSORT,                                                          
             FIELDS=(SAPKSYS,A,                                                 
                     SAPKMDOF,A),                                               
             SORTED,                                                            
             INCLUDE=(SAPKTYPE,EQ,SAPKTYPE_MOFF)                                
 JOINKEYS F2=PARSFIL2,                                                          
             FIELDS=(AXG$SYS,A,                                                 
                     A_SUP_SAPROFFC,A)                                          
 JOIN UNPAIRED,F2    *** keep *both* matched and unmatched transactions         
 REFORMAT FIELDS=(F2:AXG$RECORD,          ** all parsed ACC data                
                  ?,                      ** JOIN result indicator              
                  F2:A_SUP_SAPRCUST,      ** (extracted earlier)                
                     A_SUP_SAPROFFC,      ** (extracted earlier)                
                  F1:SAPRCOMP,            ** from support file                  
                     SAPRSORG,            ** from support file                  
                     SAPRPCTR,            ** from support file                  
                  F2:A_RESOURCES_AGY,                                           
                     A_ACC_SEQNUM),       ** from ACC transaction file          
                  FILL=X'00'                                                    
*                                                                               
* Build our work record from the reformatted fields.                            
 INREC BUILD=(AXG$TYPE:REFO_AXG$RECORD,                                         
              A_JOIN_RESULT:REFO_JOIN_RESULT_INDICATOR,                         
              A_SUP_SAPRCUST:REFO_A_SUP_SAPRCUST,                               
              A_SUP_SAPROFFC:REFO_A_SUP_SAPROFFC,                               
              A_SUP_SAPRCOMP:REFO_SAPRCOMP,                                     
              A_SUP_SAPRSORG:REFO_SAPRSORG,                                     
              A_SUP_SAPRPCTR:REFO_SAPRPCTR,                                     
              A_RESOURCES_AGY:REFO_A_RESOURCES_AGY,                             
              A_ACC_SEQNUM:REFO_A_ACC_SEQNUM)                                   
*                                                                               
 OUTFIL FNAMES=PARSFIL3            *** all records go here                      
*                                                                               
 OUTFIL FNAMES=EXCEPTN,            *** only problematic records go here         
   INCLUDE=(A_JOIN_RESULT,EQ,F2_ONLY,OR,           *** missing office           
           (A_JOIN_RESULT,EQ,BOTH_F1_AND_F2,AND,   *** missing data             
            (A_SUP_SAPRCOMP,EQ,X'00',OR,                                        
             A_SUP_SAPRSORG,EQ,X'00',OR,                                        
             A_SUP_SAPRPCTR,EQ,X'00'))),                                        
   OVERLAY=(A_ERROR_TEXT:C'MISSING SAP MEDIA OFFICE')                           
                                                                                
                                                                                
./ ADD NAME=JONBCNTL                                                            
*                                                                               
* Join the ACC extract and support files on the MO product code, and            
* extract the SAP product code from the Support Record                          
*                                                                               
 JOINKEYS F1=SUPPSORT,                                                          
             FIELDS=(SAPKSYS,A,                                                 
                     SAPKMED,A,                                                 
                     SAPKBRAN,A),                                               
             SORTED,                                                            
             INCLUDE=(SAPKTYPE,EQ,SAPKTYPE_PRD)                                 
 JOINKEYS F2=PARSFIL3,                                                          
             FIELDS=(AXG$SYS,A,                                                 
                     AXG$MED,A,                                                 
                     AXG$PRD,A)                                                 
 JOIN UNPAIRED,F2    *** keep *both* matched and unmatched transactions         
 REFORMAT FIELDS=(F2:AXG$RECORD,          ** all parsed ACC data                
                  ?,                      ** JOIN result indicator              
                  F2:A_SUP_SAPRCUST,      ** (extracted earlier)                
                     A_SUP_SAPROFFC,      ** (extracted earlier)                
                     A_SUP_SAPRCOMP,      ** (extracted earlier)                
                     A_SUP_SAPRSORG,      ** (extracted earlier)                
                     A_SUP_SAPRPCTR,      ** (extracted earlier)                
                  F1:SAPRBRAN,            ** from support file                  
                  F2:A_RESOURCES_AGY,                                           
                     A_ACC_SEQNUM),       ** from ACC transaction file          
                  FILL=X'00'                                                    
*                                                                               
* Build our work record from the reformatted fields.                            
 INREC BUILD=(AXG$TYPE:REFD_AXG$RECORD,                                         
              A_JOIN_RESULT:REFD_JOIN_RESULT_INDICATOR,                         
              A_SUP_SAPRCUST:REFD_A_SUP_SAPRCUST,                               
              A_SUP_SAPROFFC:REFD_A_SUP_SAPROFFC,                               
              A_SUP_SAPRCOMP:REFD_SAPRCOMP,                                     
              A_SUP_SAPRSORG:REFD_SAPRSORG,                                     
              A_SUP_SAPRPCTR:REFD_SAPRPCTR,                                     
              A_SUP_SAPRBRAN:REFD_A_SUP_SAPRBRAN,                               
              A_RESOURCES_AGY:REFD_A_RESOURCES_AGY,                             
              A_ACC_SEQNUM:REFD_A_ACC_SEQNUM)                                   
*                                                                               
 OUTFIL FNAMES=PARSFIL4,           *** all records go here                      
       IFTHEN=(WHEN=(A_SUP_SAPRBRAN,EQ,X'00',AND,                               
                    AXG$PRD_PRD,EQ,C'ALL'),                                     
       OVERLAY=(A_SUP_SAPRBRAN:C'3999999999'))                                  
*                                                                               
 OUTFIL FNAMES=EXCEPTN,            *** only problematic records go here         
   INCLUDE=(A_JOIN_RESULT,EQ,F2_ONLY,AND,          *** missing product          
           (AXG$PRD_PRD,NE,C'ALL',OR,                                           
           (A_JOIN_RESULT,EQ,BOTH_F1_AND_F2,AND,   *** missing data             
            (A_SUP_SAPRBRAN,EQ,X'00')))),                                       
   OVERLAY=(A_ERROR_TEXT:C'MISSING SAP PRODUCT DATA')                           
                                                                                
*                                                                               
./ ADD NAME=JONPCNTL                                                            
*                                                                               
* Join the ACC extract and support files on the Payee, and extract the          
* SAP vendor code.                                                              
*                                                                               
 JOINKEYS F1=SUPPSORT,                                                          
             FIELDS=(SAPKSYS,A,                                                 
                     SAPKMED,A,                                                 
                     SAPKDATA,A),                                               
             SORTED,                                                            
             INCLUDE=(SAPKTYPE,EQ,SAPKTYPE_VNDR)                                
 JOINKEYS F2=PARSFIL4,                                                          
             FIELDS=(AXG$SYS,A,                                                 
                     AXG$MED,A,                                                 
                     AXG$PAYE,A)                                                
 JOIN UNPAIRED,F2    *** keep *both* matched and unmatched transactions         
 REFORMAT FIELDS=(F2:AXG$RECORD,          ** all parsed ACC data                
                  ?,                      ** JOIN result indicator              
                  F2:A_SUP_SAPRCUST,      ** (extracted earlier)                
                     A_SUP_SAPROFFC,      ** (extracted earlier)                
                     A_SUP_SAPRCOMP,      ** (extracted earlier)                
                     A_SUP_SAPRSORG,      ** (extracted earlier)                
                     A_SUP_SAPRPCTR,      ** (extracted earlier)                
                     A_SUP_SAPRBRAN,      ** (extracted earlier)                
                  F1:SAPRVEND,            ** from support file                  
                  F2:A_RESOURCES_AGY,                                           
                     A_ACC_SEQNUM),       ** from ACC transaction file          
                  FILL=X'00'                                                    
*                                                                               
* Build our work record from the reformatted fields.                            
 INREC BUILD=(AXG$LEN,                                                          
              AXG$TYPE:REFP_AXG$RECORD,                                         
              A_JOIN_RESULT:REFP_JOIN_RESULT_INDICATOR,                         
              A_SUP_SAPRCUST:REFP_A_SUP_SAPRCUST,                               
              A_SUP_SAPROFFC:REFP_A_SUP_SAPROFFC,                               
              A_SUP_SAPRCOMP:REFP_SAPRCOMP,                                     
              A_SUP_SAPRSORG:REFP_SAPRSORG,                                     
              A_SUP_SAPRPCTR:REFP_SAPRPCTR,                                     
              A_SUP_SAPRBRAN:REFP_A_SUP_SAPRBRAN,                               
              A_SUP_SAPRVEND:REFP_SAPRVEND,                                     
              A_RESOURCES_AGY:REFP_A_RESOURCES_AGY,                             
              A_ACC_SEQNUM:REFP_A_ACC_SEQNUM)                                   
*                                                                               
 OUTFIL FNAMES=ALLRECS             *** all transaction records go here          
*                                                                               
 OUTFIL FNAMES=EXCEPTN,            *** only problematic records go here         
   INCLUDE=(A_JOIN_RESULT,EQ,F2_ONLY,OR,           *** missing vendor           
           (A_JOIN_RESULT,EQ,BOTH_F1_AND_F2,AND,   *** missing data             
            A_SUP_SAPRVEND,EQ,X'00')),                                          
   OVERLAY=(A_ERROR_TEXT:C'MISSING SAP VENDOR DATA ')                           
                                                                                
                                                                                
./ ADD NAME=COPGCNTL                                                            
*                                                                               
* Discard all records which were kept around only so that they could            
* be written to the exception file.                                             
*                                                                               
*&&SUPP                                                                         
 OMIT COND=(A_SUP_SAPRCUST,EQ,X'00',OR,                                         
            A_SUP_SAPRCOMP,EQ,X'00',OR,                                         
            A_SUP_SAPRSORG,EQ,X'00',OR,                                         
            A_SUP_SAPRPCTR,EQ,X'00',OR,                                         
            A_SUP_SAPRBRAN,EQ,X'00',OR,                                         
            A_SUP_SAPRVEND,EQ,X'00')                                            
*&&                                                                             
*                                                                               
 OUTFIL VTOF,BUILD=(D_AXG$SYS:AXG$SYS,                                          
                    D_AXG$MED:AXG$MED,                                          
                    D_AXG$CLT:AXG$PRD_CLT,                                      
                    D_AXG$PRD:AXG$PRD_PRD,                                      
                    D_AXG$EST:AXG$EST_EST,                                      
                    D_AXG$INV:AXG$INV,                                          
                    D_SAP_VENDOR:A_SUP_SAPRVEND,                                
                    D_AXG$ACCD:AXG$ACCD,                                        
                    D_AXG$TRDT:AXG$TRDT,Y2T,DTNS=(DM4),                         
                    D_AXG$TRRF:AXG$TRRF,                                        
                    D_AXG$MOA_CENT:C'20',   *** HARD-CODED CENTURY ***          
                    D_AXG$MOA:AXG$MOA,                                          
                    D_AXG$ACDT:AXG$ACDT,Y2T,DTNS=(DM4),                         
                    D_AXG$SINV:AXG$SINV,                                        
                    D_AXG$MOS_CENT:C'20',   *** HARD-CODED CENTURY ***          
                    D_AXG$MOS:AXG$MOS,                                          
                    D_AXG$DUDT:AXG$DUDT,Y2T,DTNS=(DM4),                         
                    D_AXG$MSYS:AXG$MSYS,                                        
                    D_RESOURCES_AGY:A_RESOURCES_AGY,                            
*&!TEST                                                                         
                    D_AXG$VCH#:AXG$VCH#,                                        
*&!                                                                             
*&&TEST                                                                         
                    D_AXG$VCH#:14C'*',      *** FOR REGRESSION TESTING          
*&&                                                                             
                    D_AXG$CURR:AXG$CURR,                                        
                    D_AXG$XCHR:AXG$XCHR,                                        
                    D_AXG$STYP:AXG$STYP,                                        
                    D_AXG$NETW:AXG$NETW,                                        
                    D_AXG$NETA:AXG$NETA,                                        
                    D_AXG$TAXA_HST:AXG$TAXA_HST,                                
                    D_AXG$TAXC:AXG$TAXC,                                        
                    D_AXG$MEDT:AXG$MEDT,                                        
                    D_AXG$SMED:AXG$SMED,                                        
                    D_AXG$MATL:AXG$MATL,                                        
                    D_AXG$INS#:AXG$INS#,                                        
                    D_AXG$TAXM_HST:AXG$TAXM_HST,                                
                    D_AXG$KEY:AXG$KEY,                                          
                    D_AXG$TAXA_GST:AXG$TAXA_GST,                                
                    D_AXG$TAXM_GST:AXG$TAXM_GST,                                
                    D_AXG$TAXA_QST:AXG$TAXA_QST,                                
                    D_AXG$TAXM_QST:AXG$TAXM_QST,                                
                    D_SUP_SAPRCUST:A_SUP_SAPRCUST,                              
                    D_SUP_SAPRCOMP:A_SUP_SAPRCOMP,                              
                    D_SUP_SAPRSORG:A_SUP_SAPRSORG,                              
                    D_SUP_SAPRPCTR:A_SUP_SAPRPCTR,                              
                    D_SUP_SAPRBRAN:A_SUP_SAPRBRAN)                              
                                                                                
                                                                                
./ ADD NAME=JONHCNTL                                                            
*                                                                               
* Produce gross $ totals across months-of-service for each invoice.             
*                                                                               
 JOINKEYS F1=GOODTRNS,                                                          
             FIELDS=(D_AXG$SYS,A,                                               
                     D_AXG$MED,A,                                               
                     D_AXG$CLT,A,                                               
                     D_AXG$PRD,A,                                               
                     D_AXG$EST,A,                                               
                     D_AXG$INV,A,                                               
                     D_SAP_VENDOR,A)                                            
 JOINKEYS F2=TEMP1,                                                             
             TASKID=T1,          <<<< SEE T1F2CNTL                              
             FIELDS=(D_AXG$SYS,A,                                               
                     D_AXG$MED,A,                                               
                     D_AXG$CLT,A,                                               
                     D_AXG$PRD,A,                                               
                     D_AXG$EST,A,                                               
                     D_AXG$INV,A,                                               
                     D_SAP_VENDOR,A)                                            
 REFORMAT FIELDS=(F2:D_AXG$NETA,    <===== computed gross (from totals)         
                  F1:D_JOINED_DATA)                                             
*                                                                               
* Build our work record from the reformatted fields.                            
 INREC BUILD=(D_GROSS$:REFH_D_GROSS$,                                           
              D_JOINED_DATA:REFH_D_JOINED_DATA)                                 
                                                                                
                                                                                
./ ADD NAME=T1F2CNTL                                                            
*                                                                               
* This collapses F2 *after* the sort (where the sort key is defined             
* by the F2 JOINKEYS fields above), retaining only the first duplicate.         
* The gross $ (i.e., the SUM) winds up in the totalled record.                  
*                                                                               
 SUM FIELDS=(D_AXG$NETA)                                                        
                                                                                
                                                                                
./ ADD NAME=COPECNTL                                                            
*                                                                               
* Generate the exception file. This done by formatting the output               
* records to contain nothing but the original pipe-delimited records.           
* We must also throw away duplicates, because a given ACC transaction           
* record may have generated more than one exception.                            
*                                                                               
 JOINKEYS F1=EXCEPTN,                                                           
             TASKID=TE,          <<<< SEE TEF1CNTL                              
             FIELDS=(A_ACC_SEQNUM,A)                                            
 JOINKEYS F2=ACCSAVE,                                                           
             SORTED,                                                            
             FIELDS=(O_ACC_SEQNUM,A)                                            
 REFORMAT FIELDS=(F2:1,4,                                                       
                     O_ORIGINAL_REC_VL) ** Original pipe-delimited rec.         
                                                                                
                                                                                
./ ADD NAME=TEF1CNTL                                                            
*                                                                               
* Throw away duplicate exception records.                                       
*                                                                               
 SUM FIELDS=(NONE)                                                              
                                                                                
                                                                                
                                                                                
                                                                                
                                                                                
./ ADD NAME=COP2CNTL                                                            
*                                                                               
* Parse the Exception file and F/X only file to reformat them.                  
*                                                                               
 INREC PARSE=(%=(ENDBEFR=PIPE,REPEAT=13),                                       
           P_AXG$SYS=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$SYS),                         
           P_AXG$MED=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$MED),                         
           %=(ENDBEFR=PIPE),                                                    
           P_AXG$PRD=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$PRD),  CLIENT/PRODUCT         
           P_AXG$EST=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$EST_EST),                     
           %=(ENDBEFR=PIPE,REPEAT=16),                                          
           P_AXG$INV=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$INV),                         
           %=(ENDBEFR=PIPE,REPEAT=17),                                          
           P_AXG$NETW=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$NETW)),                      
    BUILD=(Z_AXG$LEN,                                                           
           Z_AXG$SYS:P_AXG$SYS,                                                 
           Z_AXG$MED:P_AXG$MED,                                                 
           Z_AXG$PRD:P_AXG$PRD,                                                 
           Z_AXG$EST:P_AXG$EST,                                                 
           Z_AXG$INV:P_AXG$INV,                                                 
           Z_AXG$NETW:P_AXG$NETW,                                               
           Z_ORIGINAL_REC_VL:5)   *** Original record starts in col. 5          
                                                                                
                                                                                
                                                                                
./ ADD NAME=COP3CNTL                                                            
*                                                                               
* Join the reformatted Exception file (TEMP5) with the reformatted F/X          
* postings (TEMP6) to get the matched F/X postings that has its main            
* postings kicked out in the exception file.                                    
*                                                                               
 JOINKEYS F1=TEMP5,                                                             
             FIELDS=(Z_AXG$SYS,A,                                               
                     Z_AXG$MED,A,                                               
                     Z_AXG$PRD,A,                                               
                     Z_AXG$EST,A,                                               
                     Z_AXG$INV,A,                                               
                     Z_AXG$NETW,A)                                              
 JOINKEYS F2=TEMP6,                                                             
             FIELDS=(Z_AXG$SYS,A,                                               
                     Z_AXG$MED,A,                                               
                     Z_AXG$PRD,A,                                               
                     Z_AXG$EST,A,                                               
                     Z_AXG$INV,A,                                               
                     Z_AXG$NETW,A)                                              
                                                                                
 REFORMAT FIELDS=(F2:1,4,Z_ORIGINAL_REC_VL)                                     
                                                                                
                                                                                
                                                                                
./ ADD NAME=SRTVCNTL                                                            
*                                                                               
* Produce a file of tax addendum records per invoice.                           
*                                                                               
* Ignore transactions without any kind of tax at all.                           
 INCLUDE COND=(D_AXG$TAXA_HST,NE,0,OR,                                          
               D_AXG$TAXA_GST,NE,0,OR,                                          
               D_AXG$TAXA_QST,NE,0)                                             
*                                                                               
 SORT FIELDS=(D_AXG$MSYS_AGY,A,                                                 
              D_AXG$SYS,A,                                                      
              D_AXG$MED,A,                                                      
              D_AXG$CLT,A,                                                      
              D_AXG$PRD,A,                                                      
              D_AXG$EST,A,                                                      
              D_AXG$INV,A,                                                      
              D_SAP_VENDOR,A,                                                   
              D_AXG$MOS,A),                                                     
      EQUALS                               *** makes debugging easier!          
*                                                                               
* Add up all types of taxes across all ACC transactions for a given             
* invoice/MOS.                                                                  
 SUM FIELDS=(D_AXG$TAXA_HST,                                                    
             D_AXG$TAXA_GST,                                                    
             D_AXG$TAXA_QST)                                                    
*                                                                               
* Force addendum records to sort after the true detail line items.              
 OUTFIL BUILD=(D_SORT_SEQUENCE:C'1',                                            
               D_JOINED_DATA:D_JOINED_DATA,/,                                   
               D_SORT_SEQUENCE:C'2',                                            
               D_JOINED_DATA:D_JOINED_DATA,/,                                   
               D_SORT_SEQUENCE:C'3',                                            
               D_JOINED_DATA:D_JOINED_DATA)                                     
                                                                                
                                                                                
./ ADD NAME=COP1CNTL                                                            
*                                                                               
* Filter out any VAR records which do not have matching transactions.           
* Produce a file of variance addendum records per invoice.                      
*                                                                               
 JOINKEYS F1=ONLYVAR,                                                           
             TASKID=T4,          <<<< SEE T4F1CNTL                              
             FIELDS=(D_AXG$SYS,A,                                               
                     D_AXG$MED,A,                                               
                     D_AXG$CLT,A,                                               
                     D_AXG$PRD,A,                                               
                     D_AXG$EST,A,                                               
                     D_AXG$INV,A,                                               
                     D_AXG$NETW,A,       Payee on VAR                           
                     D_AXG$MOS,A)                                               
 JOINKEYS F2=JOINVEND,                                                          
             TASKID=T4,          <<<< SEE T4F2CNTL                              
             FIELDS=(D_AXG$SYS,A,                                               
                     D_AXG$MED,A,                                               
                     D_AXG$CLT,A,                                               
                     D_AXG$PRD,A,                                               
                     D_AXG$EST,A,                                               
                     D_AXG$INV,A,                                               
                     D_AXG$ULA,A,       Field is really AXG#ACCD                
                     D_AXG$MOS,A)                                               
*                                                                               
 REFORMAT FIELDS=(F2:D_JOINVEND_REC,                                            
                  F1:D_AXG$NETA,                                                
                     D_AXG$MATL)                                                
*                                                                               
* Build our work record from the reformatted fields.                            
 INREC OVERLAY=(D_AXG$NETA:REFY_D_AXG$NETA,                                     
                D_AXG$TAXM_HST:REFY_D_AXG$MATL)                                 
*                                                                               
* Force addendum records to sort after the true detail line items.              
 OUTFIL BUILD=(D_SORT_SEQUENCE:C'4',                                            
               D_JOINED_DATA:D_JOINED_DATA,                                     
               /,                                                               
               D_SORT_SEQUENCE:C'5',                                            
               D_JOINED_DATA:D_JOINED_DATA)                                     
                                                                                
                                                                                
./ ADD NAME=T4F1CNTL                                                            
*                                                                               
* This collapses F1 *after* the sort (where the sort key is defined             
* by the F1 JOINKEYS fields above), retaining only the first duplicate.         
* The total variance ends up in the summed records.                             
*                                                                               
 SUM FIELDS=(D_AXG$NETA)                                                        
                                                                                
                                                                                
./ ADD NAME=T4F2CNTL                                                            
*                                                                               
* This collapses F2 *after* the sort (where the sort key is defined             
* by the F2 JOINKEYS fields above), retaining only the first duplicate.         
*                                                                               
 SUM FIELDS=(NONE)                                                              
                                                                                
                                                                                
./ ADD NAME=SRTICNTL                                                            
*                                                                               
* Sort the detail records into the sequence that we want them to appear         
* in the XML file, and fill in all other available (and relevant) data          
* fields. Discard any record with a zero amount in its relevant amount          
* field. Assign the Item_Number value to each remaining detail line.            
* The Item_Number is restarted at value 1 at the start of each Header           
* (System, Media, Client, Product, Estimate, Invoice#, Vendor).                 
*                                                                               
 INREC IFTHEN=(WHEN=INIT,OVERLAY=(D_AXG$STYP:C'INV')),                          
       IFTHEN=(WHEN=(D_GROSS$,LT,0),OVERLAY=(D_AXG$STYP:C'CRM'))                
*                                                                               
 SORT FIELDS=(D_AXG$MSYS_AGY,A,                                                 
              D_AXG$SYS,A,                                                      
              D_AXG$MED,A,                                                      
              D_AXG$CLT,A,                                                      
              D_AXG$PRD,A,                                                      
              D_AXG$EST,A,                                                      
              D_AXG$INV,A,                                                      
              D_SAP_VENDOR,A,                                                   
              D_AXG$MOS,A,                                                      
              D_SORT_SEQUENCE,A),                                               
      EQUALS                               *** makes debugging easier!          
*                                                                               
 OUTREC IFTHEN=(WHEN=INIT,                 *** initialization                   
         OVERLAY=(X_AXG$CLT:D_AXG$CLT,          *** FROM AXG$PRD                
                  X_AXG$PRD:D_AXG$PRD,          *** FROM AXG$PRD                
                  X_MATERIAL:D_AXG$MATL)),                                      
*                                          tax addendum records                 
        IFTHEN=(WHEN=(D_SORT_SEQUENCE,EQ,C'1'),                                 
         OVERLAY=(X_NET_AMOUNT:D_AXG$TAXA_HST,                                  
                   EDIT=(SIIIIIIIIIIIT.TT),SIGNS=(,-),                          
                  D_AXG$INS#:C'TAX',                                            
                  X_MATERIAL:D_AXG$TAXM_HST),HIT=NEXT),                         
                                                                                
*                                                                               
        IFTHEN=(WHEN=(D_SORT_SEQUENCE,EQ,C'2'),                                 
         OVERLAY=(X_NET_AMOUNT:D_AXG$TAXA_GST,                                  
                   EDIT=(SIIIIIIIIIIIT.TT),SIGNS=(,-),                          
                  D_AXG$INS#:C'TAX',                                            
                  X_MATERIAL:D_AXG$TAXM_GST),HIT=NEXT),                         
                                                                                
*                                                                               
        IFTHEN=(WHEN=(D_SORT_SEQUENCE,EQ,C'3'),                                 
         OVERLAY=(X_NET_AMOUNT:D_AXG$TAXA_QST,                                  
                   EDIT=(SIIIIIIIIIIIT.TT),SIGNS=(,-),                          
                  D_AXG$INS#:C'TAX',                                            
                  X_MATERIAL:D_AXG$TAXM_QST),HIT=NEXT),                         
                                                                                
*                                          variance addendum records            
        IFTHEN=(WHEN=(D_SORT_SEQUENCE,EQ,C'4'),                                 
         OVERLAY=(X_NET_AMOUNT:D_AXG$NETA,                                      
                   EDIT=(SIIIIIIIIIIIT.TT),SIGNS=(,-),                          
                  D_AXG$INS#:C'VAR',                                            
                  X_MATERIAL:D_AXG$TAXM_HST),HIT=NEXT),                         
*                                                                               
        IFTHEN=(WHEN=(D_SORT_SEQUENCE,EQ,C'5'),                                 
         OVERLAY=(X_NET_AMOUNT:D_AXG$NETA,MUL,-1,                               
                   EDIT=(SIIIIIIIIIIIT.TT),SIGNS=(,-),                          
                  D_AXG$INS#:C'VAR',                                            
                  X_MATERIAL:D_AXG$MATL),HIT=NEXT),                             
*                                                                               
        IFTHEN=(WHEN=ANY,              *** all variance, tax records            
         OVERLAY=(D_AXG$NETW:C'999999999999')),                                 
*                                                                               
        IFTHEN=(WHEN=NONE,             *** all *other* records                  
         OVERLAY=(X_NET_AMOUNT:D_AXG$NETA,SUB,                                  
                               D_AXG$TAXA_HST,SUB,                              
                               D_AXG$TAXA_GST,SUB,                              
                               D_AXG$TAXA_QST,                                  
                               EDIT=(SIIIIIIIIIIIT.TT),SIGNS=(,-)))             
*                                                                               
 OUTFIL OMIT=((D_AXG$INS#,EQ,C'VAR',AND,D_AXG$NETA,EQ,0),OR,                    
              (D_AXG$INS#,EQ,C'TAX',AND,D_AXG$TAXA_HST,EQ,0,AND,                
               X_MATERIAL,EQ,D_AXG$TAXM_HST),OR,                                
              (D_AXG$INS#,EQ,C'TAX',AND,D_AXG$TAXA_GST,EQ,0,AND,                
               X_MATERIAL,EQ,D_AXG$TAXM_GST),OR,                                
              (D_AXG$INS#,EQ,C'TAX',AND,D_AXG$TAXA_QST,EQ,0,AND,                
               X_MATERIAL,EQ,D_AXG$TAXM_QST),OR,                                
              (D_AXG$INS#,NE,C'TAX',AND,                                        
               D_AXG$INS#,NE,C'VAR',AND,                                        
               D_AXG$NETA,EQ,0)),                                               
        OVERLAY=(D_ITEM_NUMBER:SEQNUM,LEN_D_ITEM_NUMBER,ZD,                     
                   RESTART=(D_ITEM_#_KEY))                                      
                                                                                
                                                                                
./ ADD NAME=FX01CNTL                                                            
*                                                                               
* Merge the F/X totals into each line item, and populate                        
* the details with the required fields related to the F/X transactions.         
* Calculate the exchange rate and the Net_Amount_Local based on                 
* matching F/X transactions.                                                    
*                                                                               
 JOINKEYS F1=ONLYFX,                                                            
             TASKID=TF,          <<<< SEE TFF1CNTL                              
             FIELDS=(D_AXG$SYS,A,                                               
                     D_AXG$MED,A,                                               
                     D_AXG$CLT,A,                                               
                     D_AXG$PRD,A,                                               
                     D_AXG$EST,A,                                               
                     D_AXG$INV,A)                                               
 JOINKEYS F2=SRTITEMS,                                                          
             SORTED,                                                            
             FIELDS=(D_AXG$SYS,A,                                               
                     D_AXG$MED,A,                                               
                     D_AXG$CLT,A,                                               
                     D_AXG$PRD,A,                                               
                     D_AXG$EST,A,                                               
                     D_AXG$INV,A)                                               
 JOIN UNPAIRED,F2                         ** discard unmatched F/X recs         
*                                         ** keep unpaired from F2              
 REFORMAT FIELDS=(?,                      ** 1(file1) 2(file2) B(both)          
                  F1:D_AXG$NETA,          ** F/X record total $                 
                  F1:D_AXG$CURR,          ** F/X currency indicator             
                  F2:D_GROSS$,            ** non-F/X record total $             
                     D_ITEM_NUMBER,                                             
                     D_JOINED_DATA,                                             
                     X_FIELDS)                                                  
*                                                                               
* Build our work record from the reformatted fields.                            
 INREC BUILD=(D_JOIN_RESULT:REFF_JOIN_RESULT_INDICATOR,                         
              D_GROSS$:REFF_NON_FX_AXG$NETA,         ** non-F/X total           
              D_ITEM_NUMBER:REFF_D_ITEM_NUMBER,                                 
              D_FX_SUM:REFF_FX_AXG$NETA,             ** F/X total               
              D_NON_FX_SUM:REFF_NON_FX_AXG$NETA,     ** non-F/X total           
              D_CURR:REFF_FX_AXG$CURR,               ** F/X currency            
              D_JOINED_DATA:REFF_D_JOINED_DATA,                                 
              X_FIELDS:REFF_X_FIELDS)                                           
*                                                                               
 OUTREC IFTHEN=(WHEN=INIT,                 *** initialization                   
         OVERLAY=(D_GROSS$:D_GROSS$,EDIT=(SIIIIIIIIIT.TT),SIGNS=(,-),           
                  D_NET_AMT_LCL:+0,LENGTH=LEN_D_NET_AMT_LCL,TO=ZD)),            
        IFTHEN=(WHEN=(D_JOIN_RESULT,EQ,BOTH_F1_AND_F2,AND,                      
                      D_NON_FX_SUM,NE,0),                                       
*MN      OVERLAY=(D_AXG$CURR:C'USD',                                            
         OVERLAY=(D_AXG$CURR:D_CURR,                                            
                  D_AXG$XCHR:((D_NON_FX_SUM,MUL,+10000000),DIV,                 
                              (D_NON_FX_SUM,ADD,D_FX_SUM)),TO=ZD,               
                  D_NET_AMT_LCL:((D_AXG$NETA,MUL,+10000000),DIV,                
                                  D_AXG$XCHR),TO=ZD))                           
                                                                                
                                                                                
./ ADD NAME=TFF1CNTL                                                            
*                                                                               
* This collapses F1 *after* the sort (where the sort key is defined             
* by the JOINKEYS fields above), retaining only the first duplicate,            
* and the total dollars summed in the output records.                           
*                                                                               
 SUM FIELDS=(D_AXG$NETA)                                                        
                                                                                
                                                                                
./ ADD NAME=JONKCNTL                                                            
*                                                                               
* Calculate the Gross_Amount_Local for each invoice.                            
*                                                                               
 JOINKEYS F1=TEMP2,                                                             
             FIELDS=(D_AXG$SYS,A,                                               
                     D_AXG$MED,A,                                               
                     D_AXG$CLT,A,                                               
                     D_AXG$PRD,A,                                               
                     D_AXG$EST,A,                                               
                     D_AXG$INV,A,                                               
                     D_SAP_VENDOR,A)                                            
 JOINKEYS F2=TEMP3,                                                             
             TASKID=T3,          <<<< SEE T3F2CNTL                              
             FIELDS=(D_AXG$SYS,A,                                               
                     D_AXG$MED,A,                                               
                     D_AXG$CLT,A,                                               
                     D_AXG$PRD,A,                                               
                     D_AXG$EST,A,                                               
                     D_AXG$INV,A,                                               
                     D_SAP_VENDOR,A)                                            
 REFORMAT FIELDS=(F2:D_NET_AMT_LCL, <===== computed total                       
                  F1:D_NET_AMT_LCL,                                             
                     D_GROSS$,                                                  
                     D_ITEM_NUMBER,                                             
                     D_JOINED_DATA,                                             
                     X_FIELDS)                                                  
*                                                                               
* Build our work record from the reformatted fields.                            
 INREC BUILD=(D_GROSS$:REFK_D_GROSS$,                                           
              D_ITEM_NUMBER:REFK_D_ITEM_NUMBER,                                 
              D_GROSS_AMT_LCL:REFK_D_GROSS_AMT_LCL,                             
              D_NET_AMT_LCL:REFK_D_NET_AMT_LCL,                                 
              D_JOINED_DATA:REFK_D_JOINED_DATA,                                 
              X_FIELDS:REFK_X_FIELDS)                                           
*                                                                               
 OUTREC IFTHEN=(WHEN=(D_AXG$XCHR,NE,0),     *** test exchange rate              
         OVERLAY=(X_NET_AMT_LCL:D_NET_AMT_LCL,                                  
                    EDIT=(SIIIIIIIIIIIT.TT),SIGNS=(,-),                         
                  D_GROSS_AMT_LCL:D_GROSS_AMT_LCL,                              
                    EDIT=(SIIIIIIIIIIT.TT),SIGNS=(,-),                          
                  D_AXG$XCHR:D_AXG$XCHR,DIV,+100,                               
                    EDIT=(SIIIIIIIT.TTTTT),SIGNS=(,-))),                        
        IFTHEN=(WHEN=(D_AXG$XCHR,EQ,0),                                         
         OVERLAY=(D_GROSS_AMT_LCL:15X))                                         
                                                                                
                                                                                
./ ADD NAME=T3F2CNTL                                                            
*                                                                               
* This collapses F2 *after* the sort (where the sort key is defined             
* by the F2 JOINKEYS fields above), retaining only the first duplicate.         
* The net $ (i.e., the SUM) winds up in the totalled record.                    
*                                                                               
 SUM FIELDS=(D_NET_AMT_LCL)                                                     
                                                                                
                                                                                
./ ADD NAME=JDEBCNTL                                                            
*                                                                               
* Produce a tab-delimited file for debugging (this can easily be                
* imported into Excel).                                                         
*                                                                               
 OUTREC IFTHEN=(WHEN=GROUP,KEYBEGIN=(D_ITEM_#_KEY),                             
                PUSH=(D_GROSS$:D_GROSS$))                                       
                                                                                
 OUTFIL REMOVECC,                                                               
        HEADER1=('SEP=',TAB,/,         *** SEP= only works in Excel!!!          
                 'AGENCY',TAB,                                                  
                 'SYSTEM',TAB,                                                  
                 'MEDIA',TAB,                                                   
                 'CLT',TAB,                                                     
                 'PRD',TAB,                                                     
                 'EST',TAB,                                                     
                 'INV# (LONG)',TAB,                                             
                 'MOS',TAB,                                                     
                 'MOA',TAB,                                                     
                 'GROSS',TAB,                                                   
                 'GROSS LOCAL',TAB,                                             
                 'MATERIAL',TAB,                                                
                 'NET',TAB,                                                     
                 'NET LOCAL',TAB,                                               
                 'TAX CODE',TAB,                                                
                 'ITEM#',TAB,                                                   
                 'SAP VENDOR',TAB,                                              
                 'ACCT CODE',TAB,                                               
                 'INVOICE DATE',TAB,                                            
                 'REFNUM',TAB,                                                  
                 'ACTIV DATE',TAB,                                              
                 'INV#',TAB,                                                    
                 'DUE DATE',TAB,                                                
                 'SAP MEDIA',TAB,                                               
                 'SAP VOUCHER',TAB,                                             
                 'SAP CURRENCY',TAB,                                            
                 'SAP EXCHANGE RATE',TAB,                                       
                 'SAP TRANSACTION TYPE',TAB,                                    
                 'SAP MEDIA TYPE',TAB,                                          
                 'SAP SUBMEDIA',TAB,                                            
                 'SAP MATERIAL',TAB,                                            
                 'SAP INSERTION#',TAB,                                          
                 'SAP HST TAX MATERIAL',TAB,                                    
                 'SAP GST TAX MATERIAL',TAB,                                    
                 'SAP QST TAX MATERIAL',TAB,                                    
                 'SAP NETWORK',TAB,                                             
                 'ITEM TEXT',TAB,                                               
                 'SAP CUST',TAB,                                                
                 'SAP COMPANY',TAB,                                             
                 'SAP SALES',TAB,                                               
                 'SAP PROFIT',TAB,                                              
                 'SAP BRAND ID',TAB),                                           
       BUILD=(D_RESOURCES_AGY,TAB,                                              
              D_AXG$SYS,TAB,                                                    
              D_AXG$MED,TAB,                                                    
              X_AXG$CLT,TAB,                                                    
              X_AXG$PRD,TAB,                                                    
              D_AXG$EST,TAB,                                                    
              D_AXG$INV,TAB,                                                    
              D_AXG$MOS_CYM,TAB,                                                
              D_AXG$MOA_CYM,TAB,                                                
              D_GROSS$,TAB,                                                     
              D_GROSS_AMT_LCL,TAB,                                              
              X_MATERIAL,TAB,                                                   
              X_NET_AMOUNT,TAB,                                                 
              X_NET_AMT_LCL,TAB,                                                
              D_AXG$TAXC,TAB,                                                   
              D_ITEM_NUMBER,TAB,                                                
              D_SAP_VENDOR,TAB,                                                 
              D_AXG$ACCD,TAB,                                                   
              D_AXG$TRDT,TAB,                                                   
              D_AXG$TRRF,TAB,                                                   
              D_AXG$ACDT,TAB,                                                   
              D_AXG$SINV,TAB,                                                   
              D_AXG$DUDT,TAB,                                                   
              C'MEDIAOCEAN_CA_',D_RESOURCES_AGY,TAB,                            
              D_AXG$VCH#,TAB,                                                   
              D_AXG$CURR,TAB,                                                   
              D_AXG$XCHR,TAB,                                                   
              D_AXG$STYP,TAB,                                                   
              D_AXG$MEDT,TAB,                                                   
              D_AXG$SMED,TAB,                                                   
              D_AXG$MATL,TAB,                                                   
              D_AXG$INS#,TAB,                                                   
              D_AXG$TAXM_HST,TAB,                                               
              D_AXG$TAXM_GST,TAB,                                               
              D_AXG$TAXM_QST,TAB,                                               
              D_AXG$NETW,TAB,                                                   
              D_AXG$KEY,TAB,                                                    
              D_SUP_SAPRCUST,TAB,                                               
              D_SUP_SAPRCOMP,TAB,                                               
              D_SUP_SAPRSORG,TAB,                                               
              D_SUP_SAPRPCTR,TAB,                                               
              D_SUP_SAPRBRAN,TAB)                                               
                                                                                
                                                                                
                                                                                
./ ADD NAME=COPHCNTL                                                            
*                                                                               
* Produce a tab-delimited file which can be imported into Excel.                
*                                                                               
 OUTFIL REMOVECC,                                                               
        VTOF,                                                                   
        HEADER1=('Canadian agency',TAB,                                         
                 'Acct#',TAB,                                                   
                 'Ref#',TAB,                                                    
                 'System',TAB,                                                  
                 'Media',TAB,                                                   
                 'Client',TAB,                                                  
                 'Product',TAB,                                                 
                 'Est',TAB,                                                     
                 'Media Office',TAB,                                            
                 'Activity Date',TAB,                                           
                 'Month of Service',TAB,                                        
                 'Invoice Number',TAB,                                          
                 'Payee',TAB,                                                   
                 'Payee Type',TAB,                                              
                 'Error Message',TAB),                                          
       BUILD=(A_RESOURCES_AGY,TAB,                                              
              AXG$ACCD,TAB,                                                     
              AXG$TRRF,TAB,                                                     
              AXG$SYS,TAB,                                                      
              AXG$MED,TAB,                                                      
              AXG$PRD_CLT,TAB,                                                  
              AXG$PRD_PRD,TAB,                                                  
              AXG$EST_EST,TAB,                                                  
              A_SUP_SAPROFFC,TAB,                                               
              AXG$ACDT,TAB,                                                     
              AXG$MOS,TAB,                                                      
              AXG$INV,TAB,                                                      
              AXG$PAYE,TAB,                                                     
              AXG$VORR,TAB,                                                     
              A_ERROR_TEXT,TAB,                                                 
              200:X)         *** record must be long enough for header          
                                                                                
                                                                                
                                                                                
./ ADD NAME=COPXCNTL                                                            
*                                                                               
* Generate the almost-final XML dataset.                                        
*                                                                               
* This OUTREC is used to format the Invoice Header field lines in XML           
* format. This is necessary because the formatting features are not             
* available under HEADER3.                                                      
*                                                                               
 OUTREC OVERLAY=(X_MEDIA_SYSTEM:D_RESOURCES_AGY,JFY=(SHIFT=LEFT,                
      LEAD=C'<Media_System>MEDIAOCEAN_CA_',                                     
      TRAIL=C'</Media_System>',LENGTH=80),                                      
     X_VOUCHER_NO:D_AXG$VCH#,JFY=(SHIFT=LEFT,                                   
      LEAD=C'<Voucher_No>',TRAIL=C'</Voucher_No>',LENGTH=80),                   
     X_SAP_VENDOR:D_SAP_VENDOR,JFY=(SHIFT=LEFT,                                 
      LEAD=C'<Vendor>',TRAIL=C'</Vendor>',LENGTH=80),                           
     X_INVOICE_NO:D_AXG$INV,JFY=(SHIFT=LEFT,                                    
      LEAD=C'<Invoice_No>',TRAIL=C'</Invoice_No>',LENGTH=80),                   
     X_COMPANY_CODE:D_SUP_SAPRCOMP,JFY=(SHIFT=LEFT,                             
      LEAD=C'<Company_Code>',TRAIL=C'</Company_Code>',LENGTH=80),               
     X_GROSS_AMOUNT:D_GROSS$,JFY=(SHIFT=LEFT,                                   
      LEAD=C'<Gross_Amount>',TRAIL=C'</Gross_Amount>',LENGTH=80),               
     X_GROSS_AMT_LCL:D_GROSS_AMT_LCL,JFY=(SHIFT=LEFT,                           
      LEAD=C'<Gross_Amount_Local>',                                             
      TRAIL=C'</Gross_Amount_Local>',LENGTH=80),                                
     X_CURRENCY:D_AXG$CURR,JFY=(SHIFT=LEFT,                                     
      LEAD=C'<Currency>',TRAIL=C'</Currency>',LENGTH=80),                       
     X_EXCHANGE_RATE:D_AXG$XCHR,JFY=(SHIFT=LEFT,                                
      LEAD=C'<Exchange_Rate>',TRAIL=C'</Exchange_Rate>',LENGTH=80),             
     X_INVOICE_DATE:D_AXG$TRDT,JFY=(SHIFT=LEFT,                                 
      LEAD=C'<Invoice_Date>',TRAIL=C'</Invoice_Date>',LENGTH=80),               
     X_DUE_DATE:D_AXG$DUDT,JFY=(SHIFT=LEFT,                                     
      LEAD=C'<Due_Date>',TRAIL=C'</Due_Date>',LENGTH=80),                       
     X_TRANS_TYPE:D_AXG$STYP,JFY=(SHIFT=LEFT,                                   
      LEAD=C'<Trans_Type>',TRAIL=C'</Trans_Type>',LENGTH=80),                   
     X_NETWORK:D_AXG$NETW,JFY=(SHIFT=LEFT,                                      
      LEAD=C'<Network>',TRAIL=C'</Network>',LENGTH=80),                         
     X_POSTING_DATE:D_AXG$ACDT,JFY=(SHIFT=LEFT,                                 
      LEAD=C'<Posting_Date>',TRAIL=C'</Posting_Date>',LENGTH=80))               
*                                                                               
 OUTFIL FNAMES=TEMPXML,                                                         
        REMOVECC,                                                               
        NULLOFL=RC4,          *** SET RC=4 IF THERE ARE NO DATA RECORDS         
 BUILD=(05:C'<LineItems>',/,                                                    
     07:D_ITEM_NUMBER,JFY=(SHIFT=LEFT,                                          
        LEAD=C'<Item_No>',TRAIL=C'</Item_No>',LENGTH=74),/,                     
     07:C'<Gl_Account/>'/,        *** REQUIRED BY XSD SCHEMA                    
     07:X_NET_AMOUNT,JFY=(SHIFT=LEFT,                                           
        LEAD=C'<Net_Amount>',TRAIL=C'</Net_Amount>',LENGTH=74),/,               
     07:X_NET_AMT_LCL,JFY=(SHIFT=LEFT,                                          
        LEAD=C'<Net_Amount_Local>',                                             
        TRAIL=C'</Net_Amount_Local>',LENGTH=74),/,                              
     07:D_AXG$TAXC,JFY=(SHIFT=LEFT,                                             
        LEAD=C'<Tax_Code>',TRAIL=C'</Tax_Code>',LENGTH=74),/,                   
     07:D_SUP_SAPRCUST,JFY=(SHIFT=LEFT,                                         
        LEAD=C'<Customer>',TRAIL=C'</Customer>',LENGTH=74),/,                   
     07:D_SUP_SAPRSORG,JFY=(SHIFT=LEFT,                                         
        LEAD=C'<Sales_Org>',TRAIL=C'</Sales_Org>',LENGTH=74),/,                 
     07:X_MATERIAL,JFY=(SHIFT=LEFT,                                             
        LEAD=C'<Material>',TRAIL=C'</Material>',LENGTH=74),/,                   
     07:C'<Bill_Ind>B</Bill_Ind>'/,                                             
     07:D_AXG$MOS_CYM,JFY=(SHIFT=LEFT,                                          
        LEAD=C'<Mos>',TRAIL=C'</Mos>',LENGTH=74),/,                             
     07:D_AXG$MEDT,JFY=(SHIFT=LEFT,                                             
        LEAD=C'<Media_Type>',TRAIL=C'</Media_Type>',LENGTH=74),/,               
     07:D_AXG$SMED,JFY=(SHIFT=LEFT,                                             
        LEAD=C'<Submedia>',TRAIL=C'</Submedia>',LENGTH=74),/,                   
     07:D_AXG$KEY,JFY=(SHIFT=LEFT,                                              
        LEAD=C'<Item_Text>',TRAIL=C'</Item_Text>',LENGTH=74),/,                 
     07:X_AXG$CLT_PRD,JFY=(SHIFT=LEFT,                                          
        LEAD=C'<Product>',TRAIL=C'</Product>',LENGTH=74),/,                     
     07:D_SUP_SAPRBRAN,JFY=(SHIFT=LEFT,                                         
        LEAD=C'<Brand>',TRAIL=C'</Brand>',LENGTH=74),/,                         
     07:D_AXG$EST,JFY=(SHIFT=LEFT,                                              
        LEAD=C'<Estimate>',TRAIL=C'</Estimate>',LENGTH=74),/,                   
     07:C'<Moa/>'/,                *** As per SAP                               
     07:D_SUP_SAPRPCTR,JFY=(SHIFT=LEFT,                                         
        LEAD=C'<Profit_Center>',TRAIL=C'</Profit_Center>',LENGTH=74),/,         
     07:D_AXG$INS#,JFY=(SHIFT=LEFT,                                             
        LEAD=C'<Insertion_Number>',                                             
        TRAIL=C'</Insertion_Number>',LENGTH=74),/,                              
     05:C'</LineItems>',80:X),                                                  
 HEADER1=('<?xml version="1.0" encoding="UTF-8" ?>',/,                          
          '<n1:MediaVendorClearance',/,                                         
          'xmlns:n1="urn:publicis.com:0054:PUBLICIS_I_PUR_MEDIA1.0">'),         
 SECTIONS=(D_AXG$SYS,                                                           
           D_AXG$MED,                                                           
           D_AXG$CLT,                                                           
           D_AXG$PRD,                                                           
           D_AXG$EST,                                                           
           D_AXG$INV,                                                           
           D_SAP_VENDOR,                                                        
       HEADER3=(03:'<Header>',/,                                                
                05:X_MEDIA_SYSTEM,/,                                            
                05:X_VOUCHER_NO,/,                                              
                05:X_SAP_VENDOR,/,                                              
                05:X_INVOICE_NO,/,                                              
                05:X_COMPANY_CODE,/,                                            
                05:X_GROSS_AMOUNT,/,                                            
                05:X_GROSS_AMT_LCL,/,                                           
                05:X_CURRENCY,/,                                                
                05:X_EXCHANGE_RATE,/,                                           
                05:X_INVOICE_DATE,/,                                            
                05:X_DUE_DATE,/,                                                
                05:X_TRANS_TYPE,/,                                              
                05:X_NETWORK,/,                                                 
                05:X_POSTING_DATE),                                             
       TRAILER3=(03:'</Header>')),                                              
 TRAILER1=('</n1:MediaVendorClearance>')                                        
                                                                                
                                                                                
./ ADD NAME=CPTXCNTL                                                            
*                                                                               
* Generate the final XML dataset. Simplify all XML tags that contain            
* empty fields.                                                                 
*                                                                               
 OUTFIL FINDREP=(INOUT=(C'<Vendor></Vendor>',C'<Vendor/>',                      
      C'<Media_System></Media_System>',C'<Media_System/>',                      
      C'<Voucher_No></Voucher_No>',C'<Voucher_No/>',                            
      C'<Customer></Customer>',C'<Customer/>',                                  
      C'<Invoice_No></Invoice_No>',C'<Invoice_No/>',                            
      C'<Company_Code></Company_Code>',C'<Company_Code/>',                      
      C'<Gross_Amount></Gross_Amount>',C'<Gross_Amount/>',                      
      C'<Gross_Amount_Local></Gross_Amount_Local>',                             
         C'<Gross_Amount_Local/>',                                              
      C'<Net_Amount_Local></Net_Amount_Local>',C'<Net_Amount_Local/>',          
      C'<Currency></Currency>',C'<Currency/>',                                  
      C'<Exchange_Rate></Exchange_Rate>',C'<Exchange_Rate/>',                   
      C'<Invoice_Date></Invoice_Date>',C'<Invoice_Date/>',                      
      C'<Due_Date></Due_Date>',C'<Due_Date/>',                                  
      C'<Trans_Type></Trans_Type>',C'<Trans_Type/>',                            
      C'<Posting_Date></Posting_Date>',C'<Posting_Date/>',                      
      C'<Item_No></Item_No>',C'<Item_No/>',                                     
      C'<Net_Amount></Net_Amount>',C'<Net_Amount/>',                            
      C'<Tax_Code></Tax_Code>',C'<Tax_Code/>',                                  
      C'<Sales_Org></Sales_Org>',C'<Sales_Org/>',                               
      C'<Material></Material>',C'<Material/>',                                  
      C'<Mos></Mos>',C'<Mos/>',                                                 
      C'<Media_Type></Media_Type>',C'<Media_Type/>',                            
      C'<Submedia></Submedia>',C'<Submedia/>',                                  
      C'<Item_Text></Item_Text>',C'<Item_Text/>',                               
      C'<Network></Network>',C'<Network/>',                                     
      C'<Estimate></Estimate>',C'<Estimate/>',                                  
      C'<Profit_Center></Profit_Center>',C'<Profit_Center/>',                   
      C'<Insertion_Number></Insertion_Number>',C'<Insertion_Number/>',          
      C'<Product></Product>',C'<Product/>',                                     
      C'<Brand></Brand>',C'<Brand/>'))                                          
                                                                                
                                                                                
