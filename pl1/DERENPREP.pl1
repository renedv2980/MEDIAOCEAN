        /* DATA SET DERENPREP  AT LEVEL 006 AS OF 05/14/15    */                
       ++INCLUDE IEBUPDTHDR                                                     
***********************************************************************         
                                                                                
THIS MEMBER CONTAINS ICETOOL/DFSORT CONTROL CARDS. IT IS A PREPROCESSOR         
FOR THE RENTRAK CONVERSION.                                                     
                                                                                
***********************************************************************         
./ ADD NAME=SYMNAMES   ** SYMBOLICS USED IN CONTROL CARDS                       
*                                                                               
*                                                                               
RDW,1,4,BI                                                                      
RECORD_TYPE,*,2,CH           ALWAYS THE FIRST FIELD IN EACH RECORD              
FILE_INFO_RECORD,C'00'                                                          
UNIVERSE_RECORD,C'01'                                                           
NETWORK_INFO_RECORD,C'02'                                                       
AVG_AUD_RECORD,C'03'                                                            
RATINGS_RECORD,C'04'                                                            
*                                                                               
*                                                                               
R00_NETWORK_TYPE,%01                                                            
R00_PLAYBACK,%02                                                                
* THE PARSE VARIABLES FOR THIS GROUP OF 11 DEMOS **MUST** BE ASSIGNED           
* SEQUENTIAL NUMBERS, BECAUSE THEY ARE PARSED TOGETHER AS A UNIT.               
R01_UHH,%103                                                                    
R01_UHH1834,%104                                                                
R01_UHH1844,%105                                                                
R01_UHH2554,%106                                                                
R01_UHH1824,%107                                                                
R01_UHH2534,%108                                                                
R01_UHH3544,%109                                                                
R01_UHH4554,%110                                                                
R01_UHH5564,%111                                                                
R01_UHH65O,%112                                                                 
R01_UHH3564,%113                                                                
*                                                                               
R02_NETWORK_CODE,%14                                                            
R02_NETWORK_SHORT_NAME,%15                                                      
R02_NETWORK_LONG_NAME,%16                                                       
R03_NETWORK_CODE,%17                                                            
R03_AIRDATE_YYYY,%18                                                            
R03_AIRDATE_MM,%19                                                              
R03_AIRDATE_DD,%20                                                              
R03_AIRTIME_HH,%21                                                              
R03_AIRTIME_MM,%22                                                              
R03_PROGRAM_CODE,%23                                                            
R03_PROGRAM_NAME,%24                                                            
* THE PARSE VARIABLES FOR THIS GROUP OF 11 DEMOS **MUST** BE ASSIGNED           
* SEQUENTIAL NUMBERS, BECAUSE THEY ARE PARSED TOGETHER AS A UNIT.               
R03_AHH,%325                                                                    
R03_AHH1834,%326                                                                
R03_AHH1844,%327                                                                
R03_AHH2554,%328                                                                
R03_AHH1824,%329                                                                
R03_AHH2534,%330                                                                
R03_AHH3544,%331                                                                
R03_AHH4554,%332                                                                
R03_AHH5564,%333                                                                
R03_AHH65O,%334                                                                 
R03_AHH3564,%335                                                                
*                                                                               
R04_NETWORK_CODE,%36                                                            
R04_AIRDATE_YYYY,%37                                                            
R04_AIRDATE_MM,%38                                                              
R04_AIRDATE_DD,%39                                                              
R04_AIRTIME_HH,%40                                                              
R04_AIRTIME_MM,%41                                                              
* THE PARSE VARIABLES FOR THIS GROUP OF 11 DEMOS **MUST** BE ASSIGNED           
* SEQUENTIAL NUMBERS, BECAUSE THEY ARE PARSED TOGETHER AS A UNIT.               
R04_RHH,%442                                                                    
R04_RHH1834,%443                                                                
R04_RHH1844,%444                                                                
R04_RHH2554,%445                                                                
R04_RHH1824,%446                                                                
R04_RHH2534,%447                                                                
R04_RHH3544,%448                                                                
R04_RHH4554,%449                                                                
R04_RHH5564,%450                                                                
R04_RHH65O,%451                                                                 
R04_RHH3564,%452                                                                
R00_PERIOD_START_DATE,%53                                                       
R00_PERIOD_END_DATE,%54                                                         
*                                                                               
*                                                                               
*                                                                               
./ ADD NAME=TOOLIN     ** CONTROL CARDS FOR ICETOOL                             
*                                                                               
* PARSE THE RENTRAK INPUT FILE. FORCE THE OUTPUT RECORDS TO BE LONG             
* ENOUGH TO CONTAIN ALL FIELDS (SO THE SORT DOES NOT FAIL).                     
*                                                                               
COPY FROM(FILEIN) TO(TEMP1) USING(FIIN)                                         
*                                                                               
* SORT AND PRODUCE TWO OUTPUT FILES:                                            
*   1. A CSV FILE TO BE PROCESSED BY THE SUBSEQUENT ICETOOL OPERATORS.          
*   2. A FILE CONTAINING AN AUTONOTE E-MAIL NOTIFICATION.                       
*                                                                               
SORT FROM(TEMP1) USING(SRT1)                                                    
*                                                                               
* PRODUCE A CONVERSION INPUT FILE FROM THE CSV FILE.                            
*                                                                               
SORT FROM(PARSED) TO(FILEOUT) USING(FINL)                                       
*                                                                               
*                                                                               
./ ADD NAME=FIINCNTL                                                            
*                                                                               
 INREC IFTHEN=(WHEN=(RECORD_TYPE,EQ,FILE_INFO_RECORD),                          
    PARSE=(%=(ENDBEFR=C'|'),         "00" RECORD INDICATOR                      
           %=(ENDBEFR=C'|'),         "RENTRAK"                                  
           %=(ENDBEFR=C'|'),         VERSION                                    
           R00_NETWORK_TYPE=(ENDBEFR=C'|',FIXLEN=1),                            
           %=(ENDBEFR=C'|'),         FILE FREQUENCY (W)                         
           R00_PERIOD_START_DATE=(ENDBEFR=C'|',FIXLEN=10),                      
           %=(ENDBEFR=C'|'),         PERIOD START TIME                          
           R00_PERIOD_END_DATE=(ENDBEFR=C'|',FIXLEN=10),                        
           %=(ENDBEFR=C'|'),         PERIOD END TIME                            
           R00_PLAYBACK=(ENDBEFR=C'|',FIXLEN=4)),                               
    BUILD=(W_RDW,                                                               
           W_RECORD_TYPE:RECORD_TYPE,                                           
           W_NETWORK_TYPE:R00_NETWORK_TYPE,                                     
           W_PLAYBACK:R00_PLAYBACK,                                             
           W_PERIOD_START:R00_PERIOD_START_DATE,                                
           W_PERIOD_END:R00_PERIOD_END_DATE)),                                  
*                                                                               
   IFTHEN=(WHEN=(RECORD_TYPE,EQ,UNIVERSE_RECORD),                               
    PARSE=(%=(ENDBEFR=C'|'),         "01" RECORD INDICATOR                      
           R01_UHH=(ENDBEFR=C'|',FIXLEN=10,REPEAT=11)),                         
    BUILD=(W_RDW,                                                               
           W_RECORD_TYPE:RECORD_TYPE,                                           
           W_UHH:R01_UHH,UFF,M11,                                               
           W_UHH1834:R01_UHH1834,UFF,M11,                                       
           W_UHH1844:R01_UHH1844,UFF,M11,                                       
           W_UHH2554:R01_UHH2554,UFF,M11,                                       
           W_UHH1824:R01_UHH1824,UFF,M11,                                       
           W_UHH2534:R01_UHH2534,UFF,M11,                                       
           W_UHH3544:R01_UHH3544,UFF,M11,                                       
           W_UHH4554:R01_UHH4554,UFF,M11,                                       
           W_UHH5564:R01_UHH5564,UFF,M11,                                       
           W_UHH65O:R01_UHH65O,UFF,M11,                                         
           W_UHH3564:R01_UHH3564,UFF,M11)),                                     
*                                                                               
   IFTHEN=(WHEN=(RECORD_TYPE,EQ,NETWORK_INFO_RECORD),                           
    PARSE=(%=(ENDBEFR=C'|'),         "02" RECORD INDICATOR                      
           R02_NETWORK_CODE=(ENDBEFR=C'|',FIXLEN=4),                            
           R02_NETWORK_SHORT_NAME=(ENDBEFR=C'|',FIXLEN=8),                      
           R02_NETWORK_LONG_NAME=(ENDBEFR=C'|',FIXLEN=30)),                     
    BUILD=(W_RDW,                                                               
           W_RECORD_TYPE:RECORD_TYPE,                                           
           W_NETWORK_CODE:R02_NETWORK_CODE,UFF,M11,                             
           W_NETWORK_SHORT_NAME:R02_NETWORK_SHORT_NAME,                         
           W_NETWORK_LONG_NAME:R02_NETWORK_LONG_NAME)),                         
*                                                                               
   IFTHEN=(WHEN=(RECORD_TYPE,EQ,AVG_AUD_RECORD),                                
    PARSE=(%=(ENDBEFR=C'|'),         "03" RECORD INDICATOR                      
           R03_NETWORK_CODE=(ENDBEFR=C'|',FIXLEN=4),                            
           R03_AIRDATE_MM=(ENDBEFR=C'/',FIXLEN=2),                              
           R03_AIRDATE_DD=(ENDBEFR=C'/',FIXLEN=2),                              
           R03_AIRDATE_YYYY=(ENDBEFR=C'|',FIXLEN=4),                            
           R03_AIRTIME_HH=(ENDBEFR=C':',FIXLEN=2),                              
           R03_AIRTIME_MM=(ENDBEFR=C'|',FIXLEN=2),                              
           %=(ENDBEFR=C'|',FIXLEN=1),            *** OFF-AIR FLAG               
           R03_PROGRAM_CODE=(ENDBEFR=C'|',FIXLEN=10),                           
           R03_PROGRAM_NAME=(ENDBEFR=C'|',FIXLEN=25),                           
           R03_AHH=(ENDBEFR=C'|',FIXLEN=10,REPEAT=11)),                         
    BUILD=(W_RDW,                                                               
           W_RECORD_TYPE:RECORD_TYPE,                                           
           W_NETWORK_CODE:R03_NETWORK_CODE,UFF,M11,                             
           W_AIRDATE_MM:R03_AIRDATE_MM,                                         
           C'/',                                                                
           W_AIRDATE_DD:R03_AIRDATE_DD,                                         
           C'/',                                                                
           W_AIRDATE_YYYY:R03_AIRDATE_YYYY,                                     
           W_AIRTIME_HH:R03_AIRTIME_HH,                                         
           W_AIRTIME_MM:R03_AIRTIME_MM,                                         
           W_PROGRAM_CODE:R03_PROGRAM_CODE,UFF,M11,                             
           W_PROGRAM_NAME:R03_PROGRAM_NAME,                                     
           W_AHH:R03_AHH,UFF,M11,                                               
           W_AHH1834:R03_AHH1834,UFF,M11,                                       
           W_AHH1844:R03_AHH1844,UFF,M11,                                       
           W_AHH2554:R03_AHH2554,UFF,M11,                                       
           W_AHH1824:R03_AHH1824,UFF,M11,                                       
           W_AHH2534:R03_AHH2534,UFF,M11,                                       
           W_AHH3544:R03_AHH3544,UFF,M11,                                       
           W_AHH4554:R03_AHH4554,UFF,M11,                                       
           W_AHH5564:R03_AHH5564,UFF,M11,                                       
           W_AHH65O:R03_AHH65O,UFF,M11,                                         
           W_AHH3564:R03_AHH3564,UFF,M11)),                                     
*                                                                               
   IFTHEN=(WHEN=(RECORD_TYPE,EQ,RATINGS_RECORD),                                
    PARSE=(%=(ENDBEFR=C'|'),         "04" RECORD INDICATOR                      
           R04_NETWORK_CODE=(ENDBEFR=C'|',FIXLEN=4),                            
           R04_AIRDATE_MM=(ENDBEFR=C'/',FIXLEN=2),                              
           R04_AIRDATE_DD=(ENDBEFR=C'/',FIXLEN=2),                              
           R04_AIRDATE_YYYY=(ENDBEFR=C'|',FIXLEN=4),                            
           R04_AIRTIME_HH=(ENDBEFR=C':',FIXLEN=2),                              
           R04_AIRTIME_MM=(ENDBEFR=C'|',FIXLEN=2),                              
           %=(ENDBEFR=C'|',FIXLEN=1),            *** OFF-AIR FLAG               
           R04_RHH=(ENDBEFR=C'|',FIXLEN=7,REPEAT=11)),                          
    BUILD=(W_RDW,                                                               
           W_RECORD_TYPE:RECORD_TYPE,                                           
           W_NETWORK_CODE:R04_NETWORK_CODE,UFF,M11,                             
           W_AIRDATE_MM:R04_AIRDATE_MM,                                         
           C'/',                                                                
           W_AIRDATE_DD:R04_AIRDATE_DD,                                         
           C'/',                                                                
           W_AIRDATE_YYYY:R04_AIRDATE_YYYY,                                     
           W_AIRTIME_HH:R04_AIRTIME_HH,                                         
           W_AIRTIME_MM:R04_AIRTIME_MM,                                         
           W_RHH:R04_RHH,UFF,M11,                                               
           W_RHH1834:R04_RHH1834,UFF,M11,                                       
           W_RHH1844:R04_RHH1844,UFF,M11,                                       
           W_RHH2554:R04_RHH2554,UFF,M11,                                       
           W_RHH1824:R04_RHH1824,UFF,M11,                                       
           W_RHH2534:R04_RHH2534,UFF,M11,                                       
           W_RHH3544:R04_RHH3544,UFF,M11,                                       
           W_RHH4554:R04_RHH4554,UFF,M11,                                       
           W_RHH5564:R04_RHH5564,UFF,M11,                                       
           W_RHH65O:R04_RHH65O,UFF,M11,                                         
           W_RHH3564:R04_RHH3564,UFF,M11))                                      
*                                                                               
 OUTREC OVERLAY=(W_RECORD_END:C' ')  ** PREVENT "TOO SHORT" RECORDS             
*                                                                               
./ ADD NAME=SRT1CNTL                                                            
*                                                                               
 SORT FIELDS=(W_NETWORK_CODE,A,                                                 
              W_AIRDATE_YYYY,A,                                                 
              W_AIRDATE_MM,A,                                                   
              W_AIRDATE_DD,A,                                                   
              W_AIRTIME_HH,A,                                                   
              W_AIRTIME_MM,A,                                                   
              RECORD_TYPE,A)                                                    
*                                                                               
 OUTREC IFTHEN=(WHEN=GROUP,BEGIN=(RECORD_TYPE,EQ,FILE_INFO_RECORD),             
                 PUSH=(W_NETWORK_TYPE:W_NETWORK_TYPE,                           
                       W_PLAYBACK:W_PLAYBACK,                                   
                       W_PERIOD_START:W_PERIOD_START,                           
                       W_PERIOD_END:W_PERIOD_END)),                             
        IFTHEN=(WHEN=GROUP,BEGIN=(RECORD_TYPE,EQ,UNIVERSE_RECORD),              
                 PUSH=(W_UHH:W_UHH,                                             
                       W_UHH1834:W_UHH1834,                                     
                       W_UHH1844:W_UHH1844,                                     
                       W_UHH2554:W_UHH2554,                                     
                       W_UHH1824:W_UHH1824,                                     
                       W_UHH2534:W_UHH2534,                                     
                       W_UHH3544:W_UHH3544,                                     
                       W_UHH4554:W_UHH4554,                                     
                       W_UHH5564:W_UHH5564,                                     
                       W_UHH65O:W_UHH65O,                                       
                       W_UHH3564:W_UHH3564)),                                   
        IFTHEN=(WHEN=GROUP,BEGIN=(RECORD_TYPE,EQ,NETWORK_INFO_RECORD),          
                 PUSH=(W_NETWORK_SHORT_NAME:W_NETWORK_SHORT_NAME,               
                       W_NETWORK_LONG_NAME:W_NETWORK_LONG_NAME)),               
        IFTHEN=(WHEN=GROUP,BEGIN=(RECORD_TYPE,EQ,AVG_AUD_RECORD),               
                 PUSH=(W_PROGRAM_CODE:W_PROGRAM_CODE,                           
                       W_PROGRAM_NAME:W_PROGRAM_NAME,                           
                       W_AHH:W_AHH,                                             
                       W_AHH1834:W_AHH1834,                                     
                       W_AHH1844:W_AHH1844,                                     
                       W_AHH2554:W_AHH2554,                                     
                       W_AHH1824:W_AHH1824,                                     
                       W_AHH2534:W_AHH2534,                                     
                       W_AHH3544:W_AHH3544,                                     
                       W_AHH4554:W_AHH4554,                                     
                       W_AHH5564:W_AHH5564,                                     
                       W_AHH65O:W_AHH65O,                                       
                       W_AHH3564:W_AHH3564))                                    
*                                                                               
* PRODUCE A FILE FOR AN AUTONOTE NOTIFICATION EMAIL. IF AN E-MAIL IS            
* UNWANTED (E.G., DURING TESTING), THEN DUMMY THE AUTONOTE DD STATEMENT         
* IN THE JCL.                                                                   
*                                                                               
 OUTFIL FNAMES=AUTONOTE,                                                        
        ENDREC=1,                                                               
        BUILD=(1,4,                   *** RDW                                   
               C'AUTONOTE*',                                                    
               C'NETLOADS@MEDIAOCEAN.COM:',                                     
               C'RENTRAK CABLE ',                                               
               W_PERIOD_START,                                                  
               C'-',                                                            
               W_PERIOD_END,                                                    
               C' ',                                                            
               W_PLAYBACK)                                                      
*                                                                               
* PRODUCE A CSV FILE CONTAINING ALL RELEVANT DATA.                              
*                                                                               
* THE HEADER1 COMES IN HANDY IF THE CSV FILE IS TO BE VIEWED IN EXCEL.          
*                                                                               
 OUTFIL FNAMES=PARSED,REMOVECC,                                                 
        HEADER1=('" ",',               *** RECORD TYPE                          
                 '" ",',               *** NETWORK TYPE                         
                 '"PLAYBACK",',                                                 
                 '"PERIOD START",',                                             
                 '"PERIOD END",',                                               
                 '"NET. CODE",',                                                
                 '"NET. SHORT",',                                               
                 '"NET. LONG",',                                                
                 '"AIR DATE",',                                                 
                 '"AIR TIME",',                                                 
                 '"PROG. CODE",',                                               
                 '"PROG. NAME",',                                               
                 '"AA HH",',                                                    
                 '"AA 18-34",',                                                 
                 '"AA 18-44",',                                                 
                 '"AA 25-54",',                                                 
                 '"AA 18-24",',                                                 
                 '"AA 25-34",',                                                 
                 '"AA 35-44",',                                                 
                 '"AA 45-54",',                                                 
                 '"AA 55-64",',                                                 
                 '"AA 65+",',                                                   
                 '"AA 35-64",',                                                 
                 '"UNIV HH",',                                                  
                 '"UNIV 18-34",',                                               
                 '"UNIV 18-44",',                                               
                 '"UNIV 25-54",',                                               
                 '"UNIV 18-24",',                                               
                 '"UNIV 25-34",',                                               
                 '"UNIV 35-44",',                                               
                 '"UNIV 45-54",',                                               
                 '"UNIV 55-64",',                                               
                 '"UNIV 65+",',                                                 
                 '"UNIV 35-64",',                                               
                 '"RTG HH",',                                                   
                 '"RTG 18-34",',                                                
                 '"RTG 18-44",',                                                
                 '"RTG 25-54",',                                                
                 '"RTG 18-24",',                                                
                 '"RTG 25-34",',                                                
                 '"RTG 35-44",',                                                
                 '"RTG 45-54",',                                                
                 '"RTG 55-64",',                                                
                 '"RTG 65+",',                                                  
                 '"RTG 35-64"'),                                                
        INCLUDE=(RECORD_TYPE,EQ,RATINGS_RECORD),                                
        OVERLAY=(DELIMIT_001:C',"',              *** CSV DELIMITERS             
                 DELIMIT_002:C'","',                                            
                 DELIMIT_043:C'","',                                            
                 DELIMIT_044:C'","',                                            
                 DELIMIT_003:C'","',                                            
                 DELIMIT_004:C'","',                                            
                 DELIMIT_005:C'","',                                            
                 DELIMIT_006:C'","',                                            
                 DELIMIT_007:C'","',                                            
                 DELIMIT_008:C'","',                                            
                 DELIMIT_009:C'","',                                            
                 DELIMIT_010:C'","',                                            
                 DELIMIT_011:C'","',                                            
                 DELIMIT_012:C'","',                                            
                 DELIMIT_013:C'","',                                            
                 DELIMIT_014:C'","',                                            
                 DELIMIT_015:C'","',                                            
                 DELIMIT_016:C'","',                                            
                 DELIMIT_017:C'","',                                            
                 DELIMIT_018:C'","',                                            
                 DELIMIT_019:C'","',                                            
                 DELIMIT_020:C'","',                                            
                 DELIMIT_021:C'","',                                            
                 DELIMIT_022:C'","',                                            
                 DELIMIT_023:C'","',                                            
                 DELIMIT_024:C'","',                                            
                 DELIMIT_025:C'","',                                            
                 DELIMIT_026:C'","',                                            
                 DELIMIT_027:C'","',                                            
                 DELIMIT_028:C'","',                                            
                 DELIMIT_029:C'","',                                            
                 DELIMIT_030:C'","',                                            
                 DELIMIT_031:C'","',                                            
                 DELIMIT_032:C'","',                                            
                 DELIMIT_033:C'","',                                            
                 DELIMIT_034:C'","',                                            
                 DELIMIT_035:C'","',                                            
                 DELIMIT_036:C'","',                                            
                 DELIMIT_037:C'","',                                            
                 DELIMIT_038:C'","',                                            
                 DELIMIT_039:C'","',                                            
                 DELIMIT_040:C'","',                                            
                 DELIMIT_041:C'","',                                            
                 DELIMIT_042:C'","',                                            
                 END_QUOTE:C'"')                                                
*                                                                               
./ ADD NAME=FINLCNTL                                                            
*                                                                               
 INCLUDE COND=(RECORD_TYPE,EQ,RATINGS_RECORD)                                   
*                                                                               
* CONVERT RENTRAK PLAYBACK CODES TO OUR INTERNAL CODES                          
*                                                                               
 INREC OVERLAY=(W_PLAYBACK_CODE:W_PLAYBACK,                                     
                CHANGE=(1,        ** LENGTH OF SUBSTITUTED LOOKUP VALUE         
                  C'LIVE',PLB_LO,                                               
                  C'LSD ',PLB_LS,                                               
                  C'L01 ',PLB_L1,                                               
                  C'L02 ',PLB_L2,                                               
                  C'L03 ',PLB_L3,                                               
                  C'L07 ',PLB_L7))                                              
*                                                                               
 SORT FIELDS=(W_NETWORK_SHORT_NAME,A,                                           
              W_AIRDATE_YYYY,A,                                                 
              W_AIRDATE_MM,A,                                                   
              W_AIRDATE_DD,A,                                                   
              W_AIRTIME_HH,A,                                                   
              W_AIRTIME_MM,A)                                                   
*                                                                               
* BUILD THE CONVERSION INPUT RECORDS                                            
*                                                                               
 OUTREC BUILD=(R_RDW,                                                           
               R_NETWORK_TYPE:W_NETWORK_TYPE,                                   
               R_PLAYBACK:W_PLAYBACK_CODE,                                      
               R_NETWORK_CODE:W_NETWORK_CODE,                                   
               R_NETWORK_SHORT_NAME:W_NETWORK_SHORT_NAME,                       
               R_NETWORK_LONG_NAME:W_NETWORK_LONG_NAME,                         
               R_AIRDATE:W_AIRDATE,                                             
               R_AIRTIME:W_AIRTIME,                                             
               R_PROGRAM_CODE:W_PROGRAM_CODE,                                   
               R_PROGRAM_NAME:W_PROGRAM_NAME,                                   
               R_AHH:W_AHH,TO=BI,LENGTH=4,                                      
               R_AHH1834:W_AHH1834,TO=BI,LENGTH=4,                              
               R_AHH1844:W_AHH1844,TO=BI,LENGTH=4,                              
               R_AHH2554:W_AHH2554,TO=BI,LENGTH=4,                              
               R_AHH1824:W_AHH1824,TO=BI,LENGTH=4,                              
               R_AHH2534:W_AHH2534,TO=BI,LENGTH=4,                              
               R_AHH3544:W_AHH3544,TO=BI,LENGTH=4,                              
               R_AHH4554:W_AHH4554,TO=BI,LENGTH=4,                              
               R_AHH5564:W_AHH5564,TO=BI,LENGTH=4,                              
               R_AHH65O:W_AHH65O,TO=BI,LENGTH=4,                                
               R_AHH3564:W_AHH3564,TO=BI,LENGTH=4,                              
               R_UHH:W_UHH,TO=BI,LENGTH=4,                                      
               R_UHH1834:W_UHH1834,TO=BI,LENGTH=4,                              
               R_UHH1844:W_UHH1844,TO=BI,LENGTH=4,                              
               R_UHH2554:W_UHH2554,TO=BI,LENGTH=4,                              
               R_UHH1824:W_UHH1824,TO=BI,LENGTH=4,                              
               R_UHH2534:W_UHH2534,TO=BI,LENGTH=4,                              
               R_UHH3544:W_UHH3544,TO=BI,LENGTH=4,                              
               R_UHH4554:W_UHH4554,TO=BI,LENGTH=4,                              
               R_UHH5564:W_UHH5564,TO=BI,LENGTH=4,                              
               R_UHH65O:W_UHH65O,TO=BI,LENGTH=4,                                
               R_UHH3564:W_UHH3564,TO=BI,LENGTH=4,                              
               R_RHH:W_RHH,TO=BI,LENGTH=4,                                      
               R_RHH1834:W_RHH1834,TO=BI,LENGTH=4,                              
               R_RHH1844:W_RHH1844,TO=BI,LENGTH=4,                              
               R_RHH2554:W_RHH2554,TO=BI,LENGTH=4,                              
               R_RHH1824:W_RHH1824,TO=BI,LENGTH=4,                              
               R_RHH2534:W_RHH2534,TO=BI,LENGTH=4,                              
               R_RHH3544:W_RHH3544,TO=BI,LENGTH=4,                              
               R_RHH4554:W_RHH4554,TO=BI,LENGTH=4,                              
               R_RHH5564:W_RHH5564,TO=BI,LENGTH=4,                              
               R_RHH65O:W_RHH65O,TO=BI,LENGTH=4,                                
               R_RHH3564:W_RHH3564,TO=BI,LENGTH=4)                              
*                                                                               
