        /* DATA SET DEMRGICED  AT LEVEL 002 AS OF 07/02/13    */                
       ++INCLUDE IEBUPDTHDR                                                     
***********************************************************************         
                                                                                
THIS MEMBER CONTAINS ICETOOL/DFSORT CONTROL CARDS. IT DRIVES A                  
GENERALIZED DEMO RECORD MINOR KEY MERGE FACILITY. THIS VERSION SUPPORTS         
A USER EXIT TO PERFORM AN ELEMENT-LEVEL MERGE.                                  
                                                                                
ON INPUT, DDNAME INFILE IS THE OUTPUT OF A DEMO CONVERSION PROGRAM.             
                                                                                
***********************************************************************         
./ ADD NAME=SYMNAMES   ** SYMBOLICS USED IN CONTROL CARDS                       
*                                                                               
CONTROL_BREAK_FULL_KEY,C'*END OF MAJOR/MINOR KEY*'                              
CONVERSION_RECORD,C'C'                                                          
CONTROL_BREAK,C' '                                                              
*                                                                               
RDW,1,4,BI                      RDW                                             
 POSITION,RDW                                                                   
VARIABLE_RECLEN,*,2,BI                                                          
 SKIP,2                                                                         
RECORD,5                        REMAINDER OF RECORD                             
 POSITION,RECORD                                                                
MAJOR_KEY,*,18,BI                                                               
MINOR_KEY,*,2,BI                                                                
 POSITION,MAJOR_KEY                                                             
FULL_KEY,*,20,BI                                                                
*                                                                               
* SORT WORK RECORD DEFINITION                                                   
*                                                                               
W_RDW,1,4,BI                    RDW                                             
W_FLAG,*,1,CH                   FLAG                                            
W_FULL_KEY,*,20,BI              MAJOR/MINOR KEY                                 
 POSITION,W_FULL_KEY                                                            
W_RECORD,*                      COMPLETE INPUT RECORD                           
*                                                                               
*                                                                               
./ ADD NAME=TOOLIN     ** CONTROL CARDS FOR ICETOOL                             
*                                                                               
* SEE THE COMMENTS BEFORE EACH CNTL CONTROL CARD BLOCK FOR THE ICETOOL          
* OPERATORS IN THIS SEQUENCE.                                                   
*                                                                               
SORT FROM(INFILE) TO(MAJRKEYS) USING(SRT1)                                      
COPY FROM(MAJRKEYS) TO(UNMERGED) USING(COP5)                                    
COPY FROM(INFILE) TO(UNMERGED) USING(COP2)                                      
SORT FROM(UNMERGED) USING(SRT2)                                                 
COPY FROM(MERGEOUT) TO(FINALOUT) USING(COP3)                                    
*                                                                               
*                                                                               
./ ADD NAME=SRT1CNTL   ** CONTROL CARDS FOR DFSORT                              
*                                                                               
* SORT THE INPUT FILE ON MAJOR KEY, ELIMINATING ALL DUPLICATES.                 
* THE RESULTING FILE DRIVES THE READ OF THE EXISTING DEMO FILE BY               
* THE EXIT THAT FOLLOWS. PASSIVES ARE IDENTIFIED AS THOSE RECORDS WITH          
* A LENGTH OF PRECISELY 27 BYTES, AND THESE ARE OMITTED FROM THE SORT.          
*                                                                               
 OMIT COND=(VARIABLE_RECLEN,EQ,X'001B')                                         
 SORT FIELDS=(MAJOR_KEY,A)                                                      
 SUM FIELDS=(NONE)                                                              
*                                                                               
*                                                                               
./ ADD NAME=COP5CNTL   ** CONTROL CARDS FOR DFSORT                              
*                                                                               
* READ THE DEMO FILE FOR ALL MINOR KEYS REFERENCED BY THE MAJOR KEYS            
* FOUND IN THE CONVERSION OUTPUT, AND PRODUCE A FILE OF THESE RECORDS.          
*                                                                               
 MODS E35=(DEMRGU5,40000)                                                       
*                                                                               
*                                                                               
./ ADD NAME=COP2CNTL   ** CONTROL CARDS FOR DFSORT                              
*                                                                               
* PREFIX THE CONVERSION OUTPUT RECORDS WITH A FLAG CHARACTER.                   
*                                                                               
 INREC BUILD=(W_RDW,                                                            
              W_FLAG:CONVERSION_RECORD,                                         
              W_RECORD:RECORD)                                                  
*                                                                               
*                                                                               
./ ADD NAME=SRT2CNTL   ** CONTROL CARDS FOR DFSORT                              
*                                                                               
* SORT BOTH THE NEW AND ORIGINAL RECORDS TOGETHER BY MAJOR/MINOR KEY,           
* AND INSERT CONTROL RECORDS FOR SEAN INDICATING WHERE THE END OF EACH          
* MAJOR/MINOR KEY.                                                              
* AFTER THE SORT, THE EXIT WILL DO THE ELEMENT MERGE.                           
*                                                                               
 SORT FIELDS=(W_FULL_KEY,A)                                                     
 OPTION EQUALS                                                                  
 OUTFIL FNAMES=MERGEOUT,REMOVECC,                                               
  SECTIONS=(W_FULL_KEY,                                                         
            TRAILER3=(CONTROL_BREAK,W_FULL_KEY,CONTROL_BREAK_FULL_KEY))         
*                                                                               
*                                                                               
./ ADD NAME=COP3CNTL   ** CONTROL CARDS FOR DFSORT                              
*                                                                               
* SORT BOTH THE NEW AND ORIGINAL RECORDS TOGETHER BY MAJOR/MINOR KEY,           
* AND INSERT CONTROL RECORDS FOR SEAN INDICATING WHERE THE END OF EACH          
* MAJOR/MINOR KEY.                                                              
* AFTER THE SORT, THE EXIT WILL DO THE ELEMENT MERGE.                           
*                                                                               
  MODS E35=(DEMRGU6,50000)                                                      
