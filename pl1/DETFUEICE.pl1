        /* DATA SET DETFUEICE  AT LEVEL 016 AS OF 09/13/17    */                
       ++INCLUDE IEBUPDTHDR                                                     
***********************************************************************         
                                                                                
THIS MEMBER CONTAINS ICETOOL/DFSORT CONTROL CARDS. IT IS A PREPROCESSOR         
FOR THE FUSION NCC CARRIAGE UNIVERSE ESTIMATE AND AD-INSERTABLE                 
UNIVERSE ESTIMATE FILES.                                                        
                                                                                
ON INPUT, DDNAME FILESIN IS A CONCATENATED DATASET WHICH MAY CONTAIN            
BOTH CARRIAGE UE AND AD-INSERTABLE UE DATA. THE SEQUENCE OF THE                 
CONCATENATION IS IRRELEVANT. MULTIPLE BOOKS AND/OR MARKETS ARE                  
PERMITTED.                                                                      
                                                                                
ON OUTPUT, TWO DATASETS ARE PRODUCED: DDNAMES UEOUT AND MSOOUT. BOTH            
ARE READ BY DETFUE (THE CONVERSION PROGRAM).                                    
                                                                                
***********************************************************************         
./ ADD NAME=SYMNAMES   ** SYMBOLICS USED IN CONTROL CARDS                       
*                                                                               
V_RDW,1,4,BI                                                                    
V_RECORD_TYPE,*,3,CH                                                            
V_REMAINDER,=                                                                   
*                                                                               
FIXED_LRECL,200              SEE JCL: LENGTH OF VB RECORD (WITHOUT RDW)         
RECORD_TYPE,1,3,CH           ALWAYS THE FIRST FIELD IN EACH CSV RECORD          
CARRIAGE_UE_RECORD,C'"C"'        DENOTES CARRIAGE UE RECORD                     
AD_INSERTABLE_UE_RECORD,C'"A"'   DENOTES AIUE RECORD                            
*                                                                               
SYSTEM_UE,C'SYS '                DENOTES THE SYSTEM UNIVERSE ESTIMATE           
UNKNOWN_NETWORK_ID_CODE,0        DENOTES UNKNOWN NETWORK ID, EXCEPT IN          
*                                 CONJUNCTION WITH A SYSTEM UE RECORD           
*                                                                               
*                                                                               
* CARRIAGE UNIVERSE ESTIMATE FILE PARSED FIELDS                                 
*                                                                               
C_YEAR,%01                                                                      
C_MONTH,%02                                                                     
C_DMA_NUMBER,%03                                                                
C_NETWORK_ID,%04                                                                
C_CARRIAGE_UE,%05                                                               
C_AMS_NETWORK_CODE,%06                                                          
C_DMA_NAME,%07                                                                  
*                                                                               
* AD-INSERTABLE UNIVERSE ESTIMATE FILE PARSED FIELDS                            
*                                                                               
A_YEAR,%20                                                                      
A_MONTH,%21                                                                     
A_DMA_NUMBER,%22                                                                
A_NETWORK_ID,%23                                                                
A_SYSCODE_NUMBER,%24                                                            
A_AI_UE,%25                                                                     
A_AMS_NETWORK_CODE,%26                                                          
A_SYSCODE_NAME,%27                                                              
A_MSO_NAME,%28                                                                  
A_SOURCE,%29                                                                    
A_SAMPLE_TYPE,%30                                                               
*                                                                               
* FIELDS IN MERGED INTERMEDIATE WORK FILE                                       
*                                                                               
W_YEAR,1,4,CH                                                                   
W_MONTH,*,2,CH                                                                  
W_DMA_NUMBER,*,3,ZD                                                             
W_NETWORK_ID,*,5,ZD                                                             
W_SYSCODE_NUMBER,*,4,ZD                                                         
W_AI_UE,*,10,ZD                                                                 
W_CARRIAGE_UE,*,10,ZD                                                           
W_AMS_NETWORK_CODE,*,4,CH                                                       
W_DMA_NAME,*,20,CH                                                              
W_MSO_NAME,*,40,CH                                                              
W_SYSCODE_NAME,*,40,CH                                                          
W_SOURCE,*,10,CH                                                                
W_SAMPLE_TYPE,*,1,CH                                                            
W_RECORD_TYPE,*,3,CH                                                            
*                                                                               
* NOTE: SYMBOLS IN THE UE AND MSO OUTPUT DATASETS ARE DEFINED IN THE            
*       DSECTS IN PAN MEMBER DETFUERECD. THEY ARE TRANSFORMED                   
*       PROGRAMMATICALLY (AND DYNAMICALLY) INTO SYMBOLS.                        
*                                                                               
*                                                                               
./ ADD NAME=TOOLIN     ** CONTROL CARDS FOR ICETOOL                             
*                                                                               
* SORT THE CSV CARRIAGE UE AND AIUE FILES TOGETHER. THROW AWAY THE              
* NON-DATA RECORDS, AND FORCE THE CARRIAGE RECORDS AHEAD OF THE                 
* AIUE RECORDS.                                                                 
*                                                                               
SORT FROM(FILESIN) TO(INITSORT) USING(INIT)                                     
*                                                                               
* CREATE A PARSED FILE FROM THE SORTED CSV FILE.                                
*                                                                               
COPY FROM(INITSORT) USING(FIIN)                                                 
*                                                                               
* SEE IF THERE ARE DUPLICATE CARRIAGE RECORDS FOR THE SAME BOOK, DMA,           
* AND STATION (THATS A NO-NO). IF SO, WRITE THEM TO A DATASET.                  
*                                                                               
SELECT FROM(PARSED) TO(DUPSONLY) ALLDUPS USING(SLCT) -                          
   ON(W_YEAR) -                                                                 
   ON(W_MONTH) -                                                                
   ON(W_DMA_NUMBER) -                                                           
   ON(W_NETWORK_ID)                                                             
*                                                                               
* TELL ICETOOL TO KEEP GOING EVEN IF THE COUNT COMPLETES WITH RC8.              
MODE CONTINUE                                                                   
*                                                                               
* IF ANY DUPS WERE FOUND IN THE SELECT JUST ABOVE, SET THE RETURN               
* CODE TO 8. THIS WILL BE TESTED IN THE JCL. PRINT A REPORT SHOWING             
* THE DUPLICATE RECORDS.                                                        
*                                                                               
COUNT FROM(DUPSONLY) RC8 NOTEMPTY                                               
*                                                                               
DISPLAY FROM(DUPSONLY) LIST(SHOWERRS) BLANK -                                   
   TFIRST -                                                                     
   LINES(999) -                                                                 
   TITLE('DUPLICATE CARRIAGE UE RECORDS REPORT:') -                             
   DATE TIME -                                                                  
   HEADER('RECORD #') ON(NUM) -                                                 
   HEADER('YEAR') ON(W_YEAR) -                                                  
   HEADER('MONTH') ON(W_MONTH) -                                                
   HEADER('DMA #') ON(W_DMA_NUMBER) -                                           
   HEADER('NETWORK ID') ON(W_NETWORK_ID) -                                      
   HEADER('CARRIAGE UE') ON(W_CARRIAGE_UE)                                      
*                                                                               
* CHECK FOR AIUE RECORDS WITHOUT ASSOCIATED CARRIAGE UE RECORDS.                
* IF THERE ARE ANY, WRITE THEM TO A DATASET.                                    
*                                                                               
COPY JKFROM TO(MISSCARR) USING(JKT1)                                            
*                                                                               
* IF ANY RECORDS WERE FOUND IN THE OUTER JOIN ABOVE, SET THE RETURN             
* CODE TO 8. THIS WILL BE TESTED IN THE JCL. PRINT A REPORT SHOWING             
* THE RECORDS.                                                                  
*                                                                               
COUNT FROM(MISSCARR) RC8 NOTEMPTY                                               
*                                                                               
DISPLAY FROM(MISSCARR) LIST(SHOWERRS) BLANK -                                   
   TFIRST -                                                                     
   LINES(999) -                                                                 
   TITLE('AIUE RECORDS WITH NO CARRIAGE VALUES REPORT:') -                      
   DATE TIME -                                                                  
   HEADER('SYSCODE #') ON(W_SYSCODE_NUMBER) -                                   
   HEADER('DMA #') ON(W_DMA_NUMBER) -                                           
   HEADER('NETWORK ID') ON(W_NETWORK_ID) -                                      
   HEADER('NETWORK CODE') ON(W_AMS_NETWORK_CODE) -                              
   HEADER('AIUE') ON(W_AI_UE) -                                                 
   HEADER('YEAR') ON(W_YEAR) -                                                  
   HEADER('MONTH') ON(W_MONTH)                                                  
*                                                                               
* LOOK FOR CARRIAGE RECORDS WITH INVALID DATA FIELDS, AND REPORT THEM.          
*                                                                               
COPY FROM(PARSED) TO(TEMP1) USING(COP1)                                         
*                                                                               
COUNT FROM(TEMP1) RC8 NOTEMPTY                                                  
*                                                                               
DISPLAY FROM(TEMP1) LIST(SHOWERRS) BLANK -                                      
   TFIRST -                                                                     
   LINES(999) -                                                                 
   TITLE('CARRIAGE UE RECORDS WITH INVALID VALUES:') -                          
   DATE TIME -                                                                  
   HEADER('DMA #') ON(W_DMA_NUMBER) -                                           
   HEADER('DMA NAME') ON(W_DMA_NAME) -                                          
   HEADER('NETWORK ID') ON(W_NETWORK_ID) -                                      
   HEADER('NETWORK CODE') ON(W_AMS_NETWORK_CODE) -                              
   HEADER('CARRIAGE UE') ON(W_CARRIAGE_UE) -                                    
   HEADER('YEAR') ON(W_YEAR) -                                                  
   HEADER('MONTH') ON(W_MONTH)                                                  
*                                                                               
* LOOK FOR AIUE RECORDS WITH INVALID DATA FIELDS, AND REPORT THEM.              
*                                                                               
COPY FROM(PARSED) TO(TEMP2) USING(COP2)                                         
*                                                                               
COUNT FROM(TEMP2) RC8 NOTEMPTY                                                  
*                                                                               
DISPLAY FROM(TEMP2) LIST(SHOWERRS) BLANK -                                      
   TFIRST -                                                                     
   LINES(999) -                                                                 
   TITLE('AIUE RECORDS WITH INVALID VALUES:') -                                 
   DATE TIME -                                                                  
   HEADER('SYSCODE #') ON(W_SYSCODE_NUMBER) -                                   
   HEADER('DMA #') ON(W_DMA_NUMBER) -                                           
   HEADER('NETWORK ID') ON(W_NETWORK_ID) -                                      
   HEADER('NETWORK CODE') ON(W_AMS_NETWORK_CODE) -                              
   HEADER('AIUE') ON(W_AI_UE) -                                                 
   HEADER('YEAR') ON(W_YEAR) -                                                  
   HEADER('MONTH') ON(W_MONTH)                                                  
*                                                                               
* RESET THE ICETOOL MODE TO THE DEFAULT (STOP).                                 
MODE STOP                                                                       
*                                                                               
* REMOVE RECORDS WITHOUT A NIELSEN STATION NUMBER, UNLESS THEY ARE              
* FOR THE "SYS" NETWORK (THE SYSTEM UE).                                        
* THEN SORT ALL REMAINING RECORDS, MERGING THE CARRIAGE UE VALUES INTO          
* THEIR CORRESPONDING AD-INSERTABLE RECORDS.                                    
*                                                                               
SPLICE FROM(PARSED) TO(MERGED) USING(STRP) WITHALL KEEPNODUPS -                 
   ON(W_YEAR) -                                                                 
   ON(W_MONTH) -                                                                
   ON(W_DMA_NUMBER) -                                                           
   ON(W_NETWORK_ID) -                                                           
   WITH(W_SYSCODE_NUMBER) -                                                     
   WITH(W_AI_UE) -                                                              
   WITH(W_MSO_NAME) -                                                           
   WITH(W_SYSCODE_NAME) -                                                       
   WITH(W_SOURCE) -                                                             
   WITH(W_SAMPLE_TYPE)                                                          
*                                                                               
* CREATE TWO OUTPUT FILES AS INPUT FOR THE CONVERSION:                          
*  1. UEOUT: THE UE FILE. SORTED IN KEY SEQUENCE.                               
*  2. MSOOUT: THE MSO NAME FILE, SORTED BY SYSCODE (WITH DUPLICATES             
*      IN THE INTERMEDIATE FILE REMOVED).                                       
*                                                                               
SORT FROM(MERGED) USING(UEOU)                                                   
*                                                                               
* SEE IF THERE ARE DUPLICATE AIUE RECORDS GENERATED FOR THE SAME                
* BOOK, MARKET, SYSCODE, AND STATION. IF SO, WRITE THEM TO A DATASET.           
*                                                                               
SELECT FROM(UEOUT) TO(AIUEDUPS) ALLDUPS USING(NSRT) -                           
   ON(UE_YEAR) -                                                                
   ON(UE_MONTH) -                                                               
   ON(UE_DMA#) -                                                                
   ON(UE_SYSCODE) -                                                             
   ON(UE_NETWORK_ID)                                                            
*                                                                               
* TELL ICETOOL TO KEEP GOING EVEN IF THE COUNT COMPLETES WITH RC8.              
MODE CONTINUE                                                                   
*                                                                               
* IF ANY DUPS WERE FOUND IN THE SELECT JUST ABOVE, SET THE RETURN               
* CODE TO 8. THIS WILL BE TESTED IN THE JCL. PRINT A REPORT SHOWING             
* THE DUPLICATE RECORDS.                                                        
*                                                                               
COUNT FROM(AIUEDUPS) RC8 NOTEMPTY                                               
*                                                                               
DISPLAY FROM(AIUEDUPS) LIST(SHOWERRS) BLANK -                                   
   TFIRST -                                                                     
   LINES(999) -                                                                 
   TITLE('DUPLICATE AD-INSERTABLE UE RECORDS REPORT:') -                        
   DATE TIME -                                                                  
   HEADER('YEAR') ON(UE_YEAR) -                                                 
   HEADER('MONTH') ON(UE_MONTH) -                                               
   HEADER('DMA #') ON(UE_DMA#) -                                                
   HEADER('SYSCODE') ON(UE_SYSCODE) -                                           
   HEADER('NETWORK_ID') ON(UE_NETWORK_ID) -                                     
   HEADER('SAMPLE TYPE') ON(UE_SAMPLE_TYPE) -                                   
   HEADER('AIUE') ON(UE_AI_UE)                                                  
*                                                                               
* RESET THE ICETOOL MODE TO THE DEFAULT (STOP).                                 
MODE STOP                                                                       
*                                                                               
SORT FROM(MERGED) USING(MSOU)                                                   
*                                                                               
*                                                                               
./ ADD NAME=INITCNTL      ** CONTROL CARDS FOR USING(INIT)                      
*                                                                               
* THE INPUT FILES CONTAIN VARIABLE-LENGTH RECORDS. THE FIRST 3 BYTES            
* OF EACH RECORD (AFTER THE RDW) MUST CONTAIN THE RECORD IDENTIFIER.            
* (OPTION VLSHRT IS SET SO THAT DFSORT DOES NOT ABEND IF A RECORD               
* SHORTER THAN 3 BYTES IS ENCOUNTERED. WE HAVE SEEN THAT HAPPEN WHEN            
* NCC INCLUDES A RECORD AT THE END OF THE FILE CONSISTING OF A SINGLE           
* BLANK.)                                                                       
* AFTER SORTING, WE CONVERT THE FILE TO FIXED-LENGTH RECORD FORMAT.             
*                                                                               
 OPTION VLSHRT                                                                  
 INCLUDE COND=(V_RECORD_TYPE,EQ,AD_INSERTABLE_UE_RECORD,OR,                     
               V_RECORD_TYPE,EQ,CARRIAGE_UE_RECORD)                             
 SORT FIELDS=(V_RECORD_TYPE,D)  SORT "C" RECORDS AHEAD OF "A" RECORDS           
 OUTFIL VTOF,BUILD=(V_REMAINDER,FIXED_LRECL)                                    
*                                                                               
*                                                                               
./ ADD NAME=SLCTCNTL      ** CONTROL CARDS FOR USING(SLCT)                      
*                                                                               
 INCLUDE COND=(W_RECORD_TYPE,EQ,CARRIAGE_UE_RECORD,AND,                         
              (W_NETWORK_ID,NE,UNKNOWN_NETWORK_ID_CODE,OR,                      
               W_AMS_NETWORK_CODE,EQ,SYSTEM_UE))                                
*                                                                               
*                                                                               
./ ADD NAME=FIINCNTL      ** CONTROL CARDS FOR USING(FIIN)                      
*                                                                               
 INREC IFTHEN=(WHEN=(RECORD_TYPE,EQ,CARRIAGE_UE_RECORD),                        
    PARSE=(%=(ENDBEFR=C'",'),                  "C" RECORD INDICATOR             
           C_DMA_NUMBER=(ENDBEFR=C',"',FIXLEN=3),                               
           C_DMA_NAME=(ENDBEFR=C'",',FIXLEN=20),                                
           C_NETWORK_ID=(ENDBEFR=C',"',FIXLEN=5),                               
           C_AMS_NETWORK_CODE=(ENDBEFR=C'",',FIXLEN=4),                         
           C_CARRIAGE_UE=(ENDBEFR=C',',FIXLEN=10),                              
           C_YEAR=(ENDBEFR=C',',FIXLEN=4),                                      
           C_MONTH=(FIXLEN=2)),                                                 
    BUILD=(W_YEAR:C_YEAR,UFF,M11,                                               
           W_MONTH:C_MONTH,UFF,M11,                                             
           W_DMA_NUMBER:C_DMA_NUMBER,UFF,M11,                                   
           W_NETWORK_ID:C_NETWORK_ID,UFF,M11,                                   
           W_CARRIAGE_UE:C_CARRIAGE_UE,UFF,M11,                                 
           W_AMS_NETWORK_CODE:C_AMS_NETWORK_CODE,                               
           W_DMA_NAME:C_DMA_NAME,                                               
           W_RECORD_TYPE:CARRIAGE_UE_RECORD)),                                  
   IFTHEN=(WHEN=(RECORD_TYPE,EQ,AD_INSERTABLE_UE_RECORD),                       
    PARSE=(%=(ENDBEFR=C'","'),               "A" RECORD INDICATOR               
           A_SYSCODE_NUMBER=(ENDBEFR=C'","',FIXLEN=4),                          
           A_SYSCODE_NAME=(ENDBEFR=C'",',FIXLEN=40),                            
           A_DMA_NUMBER=(ENDBEFR=C',"',FIXLEN=3),                               
           A_MSO_NAME=(ENDBEFR=C'",',FIXLEN=40),                                
           A_NETWORK_ID=(ENDBEFR=C',"',FIXLEN=5),                               
           A_AMS_NETWORK_CODE=(ENDBEFR=C'",',FIXLEN=4),                         
           A_AI_UE=(ENDBEFR=C',',FIXLEN=10),                                    
           A_YEAR=(ENDBEFR=C',',FIXLEN=4),                                      
           A_MONTH=(ENDBEFR=C',"',FIXLEN=2),                                    
           A_SOURCE=(ENDBEFR=C'","',FIXLEN=10),                                 
           A_SAMPLE_TYPE=(ENDBEFR=C'"',FIXLEN=1)),                              
    BUILD=(W_YEAR:A_YEAR,UFF,M11,                                               
           W_MONTH:A_MONTH,UFF,M11,                                             
           W_DMA_NUMBER:A_DMA_NUMBER,UFF,M11,                                   
           W_NETWORK_ID:A_NETWORK_ID,UFF,M11,                                   
           W_SYSCODE_NUMBER:A_SYSCODE_NUMBER,UFF,EDIT=(TTTT),                   
           W_AI_UE:A_AI_UE,UFF,M11,                                             
           W_AMS_NETWORK_CODE:A_AMS_NETWORK_CODE,                               
           W_MSO_NAME:A_MSO_NAME,                                               
           W_SYSCODE_NAME:A_SYSCODE_NAME,                                       
           W_SOURCE:A_SOURCE,                                                   
           W_SAMPLE_TYPE:A_SAMPLE_TYPE,                                         
           W_RECORD_TYPE:AD_INSERTABLE_UE_RECORD))                              
 OUTFIL FNAMES=PARSED                                                           
 OUTFIL FNAMES=TEMPA,                                                           
  INCLUDE=(W_RECORD_TYPE,EQ,AD_INSERTABLE_UE_RECORD,AND,                        
          (W_NETWORK_ID,NE,UNKNOWN_NETWORK_ID_CODE,OR,                          
           W_AMS_NETWORK_CODE,EQ,SYSTEM_UE))                                    
 OUTFIL FNAMES=TEMPC,                                                           
  INCLUDE=(W_RECORD_TYPE,EQ,CARRIAGE_UE_RECORD,AND,                             
          (W_NETWORK_ID,NE,UNKNOWN_NETWORK_ID_CODE,OR,                          
           W_AMS_NETWORK_CODE,EQ,SYSTEM_UE))                                    
*                                                                               
*                                                                               
./ ADD NAME=JKT1CNTL                                                            
*                                                                               
  JOINKEYS F1=TEMPA,FIELDS=(W_YEAR,A,                                           
                            W_MONTH,A,                                          
                            W_DMA_NUMBER,A,                                     
                            W_NETWORK_ID,A)                                     
  JOINKEYS F2=TEMPC,FIELDS=(W_YEAR,A,                                           
                            W_MONTH,A,                                          
                            W_DMA_NUMBER,A,                                     
                            W_NETWORK_ID,A)                                     
  JOIN UNPAIRED,F1,ONLY                                                         
*                                                                               
*                                                                               
./ ADD NAME=STRPCNTL      ** CONTROL CARDS FOR USING(STRP)                      
*                                                                               
 OMIT COND=(W_NETWORK_ID,EQ,UNKNOWN_NETWORK_ID_CODE,AND,                        
            W_AMS_NETWORK_CODE,NE,SYSTEM_UE)                                    
*                                                                               
*                                                                               
./ ADD NAME=UEOUCNTL      ** CONTROL CARDS FOR USING(UEOU)                      
*                                                                               
 INREC BUILD=(UE_YEAR:W_YEAR,UFF,M11,                                           
           UE_MONTH:W_MONTH,UFF,M11,                                            
           UE_DMA#:W_DMA_NUMBER,UFF,M11,                                        
           UE_SYSCODE:W_SYSCODE_NUMBER,UFF,M11,                                 
           UE_SOURCE:W_SOURCE,                                                  
           UE_SAMPLE_TYPE:W_SAMPLE_TYPE,                                        
           UE_NETWORK_ID:W_NETWORK_ID,UFF,M11,                                  
           UE_AI_UE:W_AI_UE,UFF,M11,                                            
           UE_CARRIAGE_UE:W_CARRIAGE_UE,UFF,M11)                                
 SORT FIELDS=(UE_YEAR,A,                                                        
              UE_MONTH,A,                                                       
              UE_DMA#,A,                                                        
              UE_SYSCODE,A,                                                     
              UE_NETWORK_ID,A)                                                  
*                                                                               
* RECORDS WITH SYSCODE 0000 INDICATE THAT A CARRIAGE RECORD HAD NO              
* ASSOCIATED AIUE RECORDS. WE JUST SKIP THOSE.                                  
*                                                                               
 OUTFIL FNAMES=UEOUT,OMIT=(UE_SYSCODE,EQ,C'0000')                               
*                                                                               
*                                                                               
./ ADD NAME=MSOUCNTL      ** CONTROL CARDS FOR USING(MSOU)                      
*                                                                               
 INREC BUILD=(MSO_YEAR:W_YEAR,UFF,M11,                                          
           MSO_MONTH:W_MONTH,UFF,M11,                                           
           MSO_SYSCODE:W_SYSCODE_NUMBER,UFF,M11,                                
           MSO_DMA#:W_DMA_NUMBER,UFF,M11,                                       
           MSO_DMA_NAME:W_DMA_NAME,                                             
           MSO_MSO_NAME:W_MSO_NAME,                                             
           MSO_SYSCODE_NAME:W_SYSCODE_NAME,                                     
           MSO_SOURCE:W_SOURCE,                                                 
           MSO_SAMPLE_TYPE:W_SAMPLE_TYPE)                                       
 SORT FIELDS=(MSO_YEAR,A,                                                       
              MSO_MONTH,A,                                                      
              MSO_SYSCODE,A)                                                    
 SUM FIELDS=(NONE)                                                              
*                                                                               
* RECORDS WITH SYSCODE 0000 INDICATE THAT A CARRIAGE RECORD HAD NO              
* ASSOCIATED AIUE RECORDS. WE JUST SKIP THOSE.                                  
*                                                                               
 OUTFIL FNAMES=MSOOUT,OMIT=(MSO_SYSCODE,EQ,C'0000')                             
*                                                                               
*                                                                               
./ ADD NAME=COP1CNTL                                                            
*                                                                               
 INCLUDE COND=(W_RECORD_TYPE,EQ,CARRIAGE_UE_RECORD,AND,                         
               W_NETWORK_ID,NE,UNKNOWN_NETWORK_ID_CODE,AND,                     
               (W_DMA_NUMBER,NE,NUM,OR,                                         
                W_NETWORK_ID,NE,NUM,OR,                                         
                W_CARRIAGE_UE,EQ,0,OR,                                          
                W_CARRIAGE_UE,GT,99999999,OR,                                   
                W_CARRIAGE_UE,NE,NUM))                                          
*                                                                               
*                                                                               
./ ADD NAME=COP2CNTL                                                            
*                                                                               
 INCLUDE COND=(W_RECORD_TYPE,EQ,AD_INSERTABLE_UE_RECORD,AND,                    
               (W_DMA_NUMBER,NE,NUM,OR,                                         
                W_NETWORK_ID,NE,NUM,OR,                                         
                W_SYSCODE_NUMBER,NE,NUM,OR,                                     
                W_AI_UE,EQ,0,OR,                                                
                W_AI_UE,GT,99999999,OR,                                         
                W_AI_UE,NE,NUM,OR,                                              
                W_SAMPLE_TYPE,LE,C' '))                                         
*                                                                               
*                                                                               
./ ADD NAME=NSRTCNTL      ** CONTROL CARDS FOR USING(NSRT)                      
*                                                                               
 OPTION COPY                                                                    
*                                                                               
*                                                                               
