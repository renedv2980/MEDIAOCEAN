        /* DATA SET AXSORTG    AT LEVEL 002 AS OF 10/01/13    */                
       ++INCLUDE IEBUPDTHDR                                                     
***********************************************************************         
                                                                                
GROUP M RECONCILIATION REPORT                                                   
                                                                                
***********************************************************************         
./ ADD NAME=SYMNAMES   ** SYMBOLICS USED IN CONTROL CARDS                       
*                                                                               
PIPE,C'|'                 DELIMITER                                             
*                                                                               
* PARSED RECORD VARIABLES                                                       
*                                                                               
P_RECORD_TYPE,%01                                                               
P_RECORD_DATE,%02                                                               
P_RECORD_TIME,%03                                                               
P_GLOBAL_INPUT_NUMBER,%04                                                       
P_TRANSACTION_TYPE,%05                                                          
P_UNIT,%06                                                                      
P_LEDGER,%07                                                                    
P_DEBIT_AMOUNT,%08                                                              
P_CREDIT_AMOUNT,%09                                                             
P_ACTIVITY_DATE,%10                                                             
P_OFFICE_CODE,%11                                                               
*                                                                               
* INTERMEDIATE WORK RECORD                                                      
*                                                                               
W_RDW,1,4,BI                                                                    
W_RECORD_TYPE,*,5,CH                                                            
W_RECORD_DATE,*,8,CH                                                            
W_RECORD_TIME,*,8,CH                                                            
W_ACTIVITY_DATE,*,6,CH                                                          
W_OFFICE_CODE,*,2,CH                                                            
W_GLOBAL_INPUT_NUMBER,*,16,CH                                                   
W_TRANSACTION_TYPE,*,3,CH                                                       
W_UNIT_LEDGER,*,2,CH                                                            
 W_UNIT,=,1,CH                                                                  
 W_LEDGER,*,1,CH                                                                
W_DEBIT_AMOUNT,*,16,ZD                                                          
W_CREDIT_AMOUNT,*,16,ZD                                                         
*                                                                               
* OUTPUT FILE FORMAT                                                            
* SKIP ONE COLUMN BETWEEN OUTPUT FIELDS FOR PIPE DELIMITER                      
*                                                                               
O_RDW,1,4,BI                                                                    
O_RECORD_TYPE,*,5,CH                                                            
 SKIP,1                                                                         
O_RECORD_DATE,*,8,CH                                                            
 SKIP,1                                                                         
O_RECORD_TIME,*,8,CH                                                            
 SKIP,1                                                                         
O_ACTIVITY_DATE,*,6,CH                                                          
 SKIP,1                                                                         
O_OFFICE_CODE,*,2,CH                                                            
 SKIP,1                                                                         
O_GLOBAL_INPUT_NUMBER,*,16,CH                                                   
 SKIP,1                                                                         
O_TRANSACTION_TYPE,*,3,CH                                                       
 SKIP,1                                                                         
O_UNIT_LEDGER,*,2,CH                                                            
 O_UNIT,=,1,CH                                                                  
 O_LEDGER,*,1,CH                                                                
 SKIP,1                                                                         
O_DEBIT_AMOUNT,*,16,CH                                                          
 SKIP,1                                                                         
O_CREDIT_AMOUNT,*,16,CH                                                         
 SKIP,1                                                                         
*                                                                               
./ ADD NAME=TOOLIN     ** CONTROL CARDS FOR ICETOOL                             
*                                                                               
* READ THE INPUT FILE, AND GENERATE THE OUTPUT DATA FILE.                       
*                                                                               
COPY FROM(FILEIN) USING(COP1)                                                   
SORT FROM(FSORT) TO(TEMP1) USING(SRT1)                                          
COPY FROM(TEMP1) TO(FILEOUT) USING(COP2)                                        
*                                                                               
* PRODUCE A SYSPRINT REPORT (FOR INFORMATIONAL PURPOSES ONLY).                  
*                                                                               
DISPLAY FROM(TEMP1) LIST(LISTRECS) -                                            
   TITLE('RECONCILIATION RECORD LISTING') -                                     
   DATE TIME PAGE -                                                             
   BLANK -                                                                      
   HEADER('REC.','TYPE') ON(W_RECORD_TYPE) -                                    
   HEADER('REC.','DATE') ON(W_RECORD_DATE) -                                    
   HEADER('REC.','TIME') ON(W_RECORD_TIME) -                                    
   HEADER('OFC','CD.') ON(W_OFFICE_CODE) -                                      
   HEADER('GIN') ON(W_GLOBAL_INPUT_NUMBER) -                                    
   HEADER('TRANS','TYP') ON(W_TRANSACTION_TYPE) -                               
   HEADER('UN','LG') ON(W_UNIT_LEDGER) -                                        
   HEADER('TOTAL','DEBIT $') ON(W_DEBIT_AMOUNT,U20,C1) -                        
   HEADER('TOTAL','CREDIT $') ON(W_CREDIT_AMOUNT,U20,C1) -                      
   BTITLE('ACTIVITY DATE:') -                                                   
   BREAK(W_ACTIVITY_DATE) -                                                     
   BTOTAL('DAILY TOTALS:') -                                                    
   TOTAL('MONTHLY TOTALS:')                                                     
*                                                                               
*                                                                               
./ ADD NAME=COP1CNTL                                                            
*                                                                               
* SPLIT THE INPUT FILE INTO TWO FILES: ONE WITH RECORDS TO BE SORTED,           
* THE OTHER WITH RECORDS TO BE PASSED UNTOUCHED TO THE OUTPUT FILE.             
*                                                                               
* NOTE: THE RECORD ID IS ALWAYS THE FIRST FIELD (IN COLUMN 5, BECAUSE           
* THESE ARE VB RECORDS.)                                                        
*                                                                               
 OUTFIL FNAMES=FSORT,INCLUDE=(5,5,CH,EQ,C'05061')                               
 OUTFIL FNAMES=FILEOUT,INCLUDE=(5,5,CH,EQ,C'05072') ** FILEOUT IS *MOD*         
*                                                                               
*                                                                               
./ ADD NAME=SRT1CNTL                                                            
*                                                                               
* PARSE THE "FSORT" FILE, BUILD WORK RECORDS, SORT THE FILE, AND                
* COLLAPSE THE FILE INTO TOTAL RECORDS.                                         
*                                                                               
 INREC PARSE=(P_RECORD_TYPE=(ENDBEFR=PIPE,FIXLEN=5),  "05061"                   
              %=(ENDBEFR=PIPE),                       "A"                       
              P_RECORD_DATE=(ENDBEFR=PIPE,FIXLEN=8),                            
              P_RECORD_TIME=(ENDBEFR=PIPE,FIXLEN=8),                            
              %=(ENDBEFR=PIPE),             SEQUENCE NUMBER                     
              P_GLOBAL_INPUT_NUMBER=(ENDBEFR=PIPE,FIXLEN=16),                   
              P_TRANSACTION_TYPE=(ENDBEFR=PIPE,FIXLEN=3),                       
              P_UNIT=(FIXLEN=1),            UNIT AND LEDGER COME FROM           
              P_LEDGER=(ENDBEFR=PIPE,FIXLEN=1),  ..."ACCOUNT" FIELD!            
              %=(ENDBEFR=PIPE),             CONTRA ACCOUNT                      
              P_OFFICE_CODE=(ENDBEFR=PIPE,FIXLEN=2),                            
              %=(ENDBEFR=PIPE),             TRANSACTION DATE                    
              %=(ENDBEFR=PIPE),             REFERENCE NUMBER                    
              %=(ENDBEFR=PIPE),             SUBREF                              
              %=(ENDBEFR=PIPE),             SYSTEM                              
              %=(ENDBEFR=PIPE),             MEDIA                               
              %=(ENDBEFR=PIPE),             CLIENT                              
              %=(ENDBEFR=PIPE),             CLT/PRD                             
              %=(ENDBEFR=PIPE),             ESTIMATE                            
              %=(ENDBEFR=PIPE),             JOB                                 
              P_DEBIT_AMOUNT=(ENDBEFR=PIPE,FIXLEN=16),                          
              P_CREDIT_AMOUNT=(ENDBEFR=PIPE,FIXLEN=16),                         
              %=(ENDBEFR=PIPE),             MONTH OF ACTIVITY                   
              P_ACTIVITY_DATE=(ENDBEFR=PIPE,FIXLEN=6),                          
              %=(ENDBEFR=PIPE)),            REMAINDER (IGNORE)                  
    BUILD=(W_RDW,                                                               
           W_RECORD_TYPE:P_RECORD_TYPE,                                         
           W_RECORD_DATE:P_RECORD_DATE,                                         
           W_RECORD_TIME:P_RECORD_TIME,                                         
           W_ACTIVITY_DATE:P_ACTIVITY_DATE,                                     
           W_OFFICE_CODE:P_OFFICE_CODE,                                         
           W_GLOBAL_INPUT_NUMBER:P_GLOBAL_INPUT_NUMBER,                         
           W_TRANSACTION_TYPE:P_TRANSACTION_TYPE,                               
           W_UNIT:P_UNIT,                                                       
           W_LEDGER:P_LEDGER,                                                   
           W_DEBIT_AMOUNT:P_DEBIT_AMOUNT,SFF,TO=ZD,                             
           W_CREDIT_AMOUNT:P_CREDIT_AMOUNT,SFF,TO=ZD)                           
                                                                                
 SORT FIELDS=(W_ACTIVITY_DATE,A,                                                
              W_OFFICE_CODE,A,                                                  
              W_GLOBAL_INPUT_NUMBER,A,                                          
              W_TRANSACTION_TYPE,A,                                             
              W_UNIT,A,                                                         
              W_LEDGER,A)                                                       
                                                                                
 SUM FIELDS=(W_DEBIT_AMOUNT,                                                    
             W_CREDIT_AMOUNT)                                                   
*                                                                               
*                                                                               
./ ADD NAME=COP2CNTL                                                            
*                                                                               
* BUILD THE OUTPUT FILE.                                                        
*                                                                               
 OUTREC BUILD=(O_RDW,                                                           
               O_RECORD_TYPE:W_RECORD_TYPE,                                     
               PIPE,                                                            
               O_RECORD_DATE:W_RECORD_DATE,                                     
               PIPE,                                                            
               O_RECORD_TIME:W_RECORD_TIME,                                     
               PIPE,                                                            
               O_ACTIVITY_DATE:W_ACTIVITY_DATE,                                 
               PIPE,                                                            
               O_OFFICE_CODE:W_OFFICE_CODE,                                     
               PIPE,                                                            
               O_GLOBAL_INPUT_NUMBER:W_GLOBAL_INPUT_NUMBER,                     
               PIPE,                                                            
               O_TRANSACTION_TYPE:W_TRANSACTION_TYPE,                           
               PIPE,                                                            
               O_UNIT:W_UNIT,                                                   
               O_LEDGER:W_LEDGER,                                               
               PIPE,                                                            
               O_DEBIT_AMOUNT:W_DEBIT_AMOUNT,                                   
                  EDIT=(SIIIIIIIIIIIT.TT),SIGNS=(,-),                           
               PIPE,                                                            
               O_CREDIT_AMOUNT:W_CREDIT_AMOUNT,                                 
                  EDIT=(SIIIIIIIIIIIT.TT),SIGNS=(,-),                           
               PIPE)                                                            
*                                                                               
