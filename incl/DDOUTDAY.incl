*          DATA SET DDOUTDAY   AT LEVEL 009 AS OF 05/01/02                      
         TITLE 'CONVERT 2-BYTE DAY FORMAT INTO LITERAL STRING'                  
OUTDAY   CSECT                                                                  
*        R2    PARAMETER 1       A(ONE-BYTE DAYS OF WEEK)X'40' - MON  *         
*                                                        X'20' - TUE  *         
*                                                        ...     ...  *         
*                                                        X'01' - SUN  *         
*        R3    PARAMETER 2       A(ONE-BYTE START-END DAYS)           *         
*                                  BITS 7-4 START      X'01' - MON    *         
*                                  BITS 3-0 END        X'02'- TUE     *         
*                                                      ...    ...     *         
*                                                      X'07'- SUN     *         
*        R4    PARAMTER 3        A(ELEVEN BYTE OUTPUT AREA)           *         
*                                  BYTE 0-2 ADDRESS                   *         
*                                  BYTE 3   X'00' USE COMMAS          *         
*                                           X'01' USE SLASHES         *         
         NMOD1 20,OUTDAY                                                        
         USING ODWORK,RC                                                        
         EJECT                                                                  
         LM    R2,R4,0(R1)                                                      
         LR    R8,R1                                                            
         SR    RE,RE               BREAK DOWN S-E DAYS                          
         IC    RE,0(R3)                                                         
         SRL   RE,4                                                             
         STC   RE,STRDAY                                                        
         MVC   ENDDAY,0(R3)                                                     
         NI    ENDDAY,X'0F'                                                     
         MVI   0(R4),C' '          CLEAR OUTPUT ARCH                            
         MVC   1(10,R4),0(R4)                                                   
         MVC   LIST(35),DAYS       SET UP DAY LIST                              
         MVC   LIST+35(35),DAYS                                                 
         MVI   OP,C','                                                          
         CLI   8(R1),0                                                          
         BE    *+8                                                              
         MVI   OP,C'/'                                                          
*        TEST WHETHER START-END DATES IN DAY BYTE                               
         LA    R5,1                                                             
         LA    R6,7                                                             
         XC    HALF,HALF                                                        
         MVC   HALF+1(1),STRDAY                                                 
         SH    R6,HALF                                                          
         BZ    OD08                                                             
         SLL   R5,1                                                             
         BCT   R6,*-4                                                           
*                                                                               
OD08     STC   R5,BYTE                                                          
         NC    BYTE,0(R2)                                                       
         BZ    ODERR                                                            
OD10     CLC   STRDAY,ENDDAY       TEST ONE DAY ONLY                            
         BE    OD30                                                             
         LA    R5,1                                                             
         LA    R6,7                                                             
         MVC   HALF+1(1),ENDDAY                                                 
         SH    R6,HALF                                                          
         BZ    OD14                                                             
         SLL   R5,1                                                             
         BCT   R6,*-4                                                           
*                                                                               
OD14     STC   R5,BYTE                                                          
         NC    BYTE,0(R2)                                                       
         BZ    ODERR                                                            
OD20     LA    R5,PRELIST                                                       
         XC    HALF,HALF                                                        
         MVC   HALF+1(1),STRDAY                                                 
         LA    R6,5                                                             
         MH    R6,HALF                                                          
         AR    R5,R6                                                            
         MVI   35(R5),0            SET END OF LIST                              
         LA    R7,0(R4)            OUTPUT FIELD                                 
         MVC   THISDAY,2(R5)                                                    
OD22     MVC   PRTCHAR,THISDAY                                                  
         BAS   RE,PRNTIT                                                        
*                                  TEST IF NEXT DAY INCLUDED                    
         MVC   BYTE,0(R2)                                                       
         NC    BYTE,5(R5)                                                       
         BZ    OD28                                                             
         MVI   0(R7),C'-'          PRINT DASH                                   
         LA    R7,1(R7)                                                         
*                                                                               
OD24     LA    R5,5(R5)            FIND LAST DAY                                
         MVC   LASTDAY,2(R5)                                                    
         CLC   1(1,R5),ENDDAY                                                   
         BE    OD26                                                             
         MVC   BYTE,0(R2)                                                       
         NC    BYTE,5(R5)                                                       
         BNZ   OD24                                                             
*                                                                               
OD26     MVC   PRTCHAR,LASTDAY                                                  
         BAS   RE,PRNTIT                                                        
*                                                                               
OD28     CLC   ENDDAY,1(R5)                                                     
         BE    ODDONE                                                           
         MVC   0(1,R7),OP                                                       
         LA    R7,1(R7)                                                         
         LA    R5,5(R5)                                                         
         MVC   BYTE,0(R2)                                                       
         NC    BYTE,0(R5)                                                       
         BZ    *-16                                                             
         MVC   THISDAY,2(R5)                                                    
         B     OD22                                                             
         SPACE 2                                                                
OD30     LA    R5,PRELIST          ONLY ONE DAY, PUT OUT WHOLE WORD             
         LA    R6,5                                                             
         XC    HALF,HALF                                                        
         MVC   HALF+1(1),STRDAY                                                 
         MH    R6,HALF                                                          
         AR    R5,R6                                                            
         MVC   0(3,R4),2(R5)                                                    
         MVI   0(R8),3                                                          
         B     ODEXT                                                            
         EJECT                                                                  
PRNTIT   SR    R1,R1                                                            
         CLI   PRTCHAR,C'S'        SAT OR SUN                                   
         BNE   *+8                                                              
         LA    R1,1                TUE OR THU                                   
         CLI   PRTCHAR,C'T'                                                     
         BNE   *+8                                                              
         LA    R1,1                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R7),PRTCHAR                                                  
         LA    R7,1(R7,R1)         UPDATE OUTPUT POINTER                        
         BR    RE                                                               
         SPACE 3                                                                
ODERR    MVI   0(R8),0                                                          
         B     ODEXT                                                            
         SPACE 2                                                                
ODDONE   SR    R7,R4               OUTPUT LENGTH                                
         STC   R7,0(R8)                                                         
*                                                                               
ODEXT    XMOD1 1                                                                
DAYS     DS    0C                                                               
         DC    X'4001',CL3'MON'                                                 
         DC    X'2002',CL3'TUE'                                                 
         DC    X'1003',CL3'WED'                                                 
         DC    X'0804',CL3'THU'                                                 
         DC    X'0405',CL3'FRI'                                                 
         DC    X'0206',CL3'SAT'                                                 
         DC    X'0107',CL3'SUN'                                                 
         DC    X'4001',CL3'MON'                                                 
         DC    X'2002',CL3'TUE'                                                 
         DC    X'1003',CL3'WED'                                                 
         DC    X'0804',CL3'THU'                                                 
         DC    X'0405',CL3'FRI'                                                 
         DC    X'0206',CL3'SAT'                                                 
         DC    X'0107',CL3'SUN'                                                 
         SPACE 3                                                                
ODWORK   DSECT                                                                  
HALF     DS    H                                                                
STRDAY   DS    CL1                                                              
ENDDAY   DS    CL1                                                              
PRELIST  DS    5C                                                               
LIST     DS    CL70                                                             
OP       DS    CL1                                                              
BYTE     DS    CL1                                                              
PRTCHAR  DS    CL3                                                              
THISDAY  DS    CL3                                                              
LASTDAY  DS    CL3                                                              
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'009DDOUTDAY  05/01/02'                                      
         END                                                                    
