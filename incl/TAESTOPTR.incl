*          DATA SET TAESTOPTR  AT LEVEL 025 AS OF 01/16/14                      
*        OUTPUT OPTION ROUTINES FOR ESTIMATING                                  
*        USED BY TAGEN2C AND TAESTPRT                                           
         SPACE 1                                                                
         USING OPTD,R1             R1=A(OPTION TABLE ENTRY)                     
*                                  R3=A(UNSCAN BLOCK ENTRY)                     
         USING TAESSBCD,R4         R4=A(SUB-ELEMENT)                            
OUTLHS1  DS    0H                                                               
         XR    RF,RF               DISPLAY 1 CHAR ON LHS                        
         B     OUTLHS                                                           
         SPACE 2                                                                
OUTLHS2  DS    0H                                                               
         LA    RF,1                DISPLAY 2 CHARS ON LHS                       
         B     OUTLHS                                                           
         SPACE 2                                                                
OUTLHS3  DS    0H                                                               
         LA    RF,2                DISPLAY 3 CHARS ON LHS                       
         B     OUTLHS                                                           
         SPACE 2                                                                
OUTLHS4  DS    0H                                                               
         LA    RF,3                DISPLAY 4 CHARS ON LHS                       
         B     OUTLHS                                                           
         SPACE 2                                                                
OUTLHS   DS    0H                  RF=N'CHARS-1 TO DISPLAY ON LHS               
         EX    RF,*+8                                                           
         B     UXIT                                                             
         MVC   0(0,R3),OPTLHS                                                   
         SPACE 2                                                                
OUTGRTNO DS    0H                  GUARANTEE NUMBER                             
         MVI   0(R3),C'#'          PRECEED WITH POUND SIGN                      
         LA    RF,1(R3)                                                         
         BAS   RE,OUTBINAL                                                      
         B     UXIT                                                             
         SPACE 2                                                                
OUTSSN   DS    0H                  SOCIAL SECURITY NUMBER                       
         ICM   R1,15,TAESSBDT                                                   
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  0(9,R3),DUB                                                      
         TM    TGSYSTAT,TASYSPID   USING PID?                                   
         BZ    UXIT                                                             
         MVC   MYWORK(9),0(R3)                                                  
         XC    0(9,R3),0(R3)                                                    
         MVC   WORK(2),HALF         SAVE HALF                                   
         GOTO1 SSNPACK,DMCB,MYWORK,0(R3)                                        
         MVC   HALF,WORK            RESTORE HALF                                
         B     UXIT                                                             
         SPACE 2                                                                
OUTPERF  DS    0H                  P(ERFORMERS) OR A(DDITIONAL PERFS)           
         OI    STATUS2,DOPPERF     SET DISPLAYED THIS OPTION                    
         B     OUTLHS1                                                          
         EJECT                                                                  
*              OPTION OUTPUT ROUTINES, CONT'D                                   
         SPACE 1                                                                
         USING OPTD,R1             R1=A(OPTION TABLE ENTRY)                     
*                                  R3=A(UNSCAN BLOCK ENTRY)                     
         USING TAESSBCD,R4         R4=A(SUB-ELEMENT)                            
         SPACE 1                                                                
OUTAMTL  DS    0H                                                               
         LR    RF,R3               DISPLAY AMOUNT ON LHS                        
         BAS   RE,OUTAMT                                                        
         B     UXIT                                                             
         SPACE 1                                                                
OUTAMT2  DS    0H                                                               
         LA    RF,LHSLNQ(R3)       DISPLAY AMOUNT ON RHS                        
         BAS   RE,OUTAMT                                                        
         B     OUTLHS2             DISPLAY 2 CHARS OF LHS                       
         SPACE 1                                                                
OUTAMT3  DS    0H                                                               
         LA    RF,LHSLNQ(R3)       DISPLAY AMOUNT ON RHS                        
         BAS   RE,OUTAMT                                                        
         B     OUTLHS3             DISPLAY 3 CHARS OF LHS                       
         SPACE 1                                                                
OUTAMT4  DS    0H                                                               
         LA    RF,LHSLNQ(R3)       DISPLAY AMOUNT ON RHS                        
         BAS   RE,OUTAMT                                                        
         B     OUTLHS4             DISPLAY 4 CHARS OF LHS                       
         SPACE 1                                                                
OUTAMT   DS    0H                  AMOUNTS - RF=A(OUTPUT AREA)                  
         EDIT  (4,TAESSBDT),(10,(RF)),2,ALIGN=LEFT                              
         AR    RF,R0                                                            
         SH    RF,=H'3'                                                         
         CLC   0(3,RF),=C'.00'     DON'T DISPLAY PENNIES IF NONE                
         BNE   *+10                                                             
         MVC   0(3,RF),SPACES                                                   
         OC    TAESSBDT(4),TAESSBDT  INSURE WE DISPLAY ZERO                     
         BNZ   *+8                                                              
         MVI   0(RF),C'0'                                                       
         BR    RE                                                               
         SPACE 1                                                                
OUTAMTRF NTR1                      AMOUNTS -RF=A(OUTPUT AREA)                   
         BAS   RE,OUTAMT                                                        
         CLI   0(RF),C'0'                                                       
         BNE   *+8                                                              
         LA    RF,1(RF)                                                         
         CLI   0(RF),C'.'                                                       
         BNE   *+8                                                              
         LA    RF,3(RF)                                                         
         XIT1  REGS=(RF)           XITS WITH RF AT END OF AMT                   
         EJECT                                                                  
*              OPTION OUTPUT ROUTINES, CONT'D                                   
         SPACE 1                                                                
         USING OPTD,R1             R1=A(OPTION TABLE ENTRY)                     
*                                  R3=A(UNSCAN BLOCK ENTRY)                     
         USING TAESSBCD,R4         R4=A(SUB-ELEMENT)                            
         SPACE 1                                                                
OUTRHS   DS    0H                  DISPLAY RHS                                  
         ZIC   RF,OPTSUBLN                                                      
         BCTR  RF,0                                                             
         EX    RF,*+6                                                           
         BR    RE                                                               
         MVC   LHSLNQ(0,R3),TAESSBDT                                            
         SPACE 1                                                                
OUTRHS1  DS    0H                  DISPLAY RHS AND 1 CHAR OF LHS                
         BAS   RE,OUTRHS                                                        
         B     OUTLHS1                                                          
         SPACE 1                                                                
OUTRHS2  DS    0H                  DISPLAY RHS AND 2 CHARS OF LHS               
         BAS   RE,OUTRHS                                                        
         B     OUTLHS2                                                          
         SPACE 1                                                                
OUTRHS3  DS    0H                  DISPLAY RHS AND 3 CHARS OF LHS               
         BAS   RE,OUTRHS                                                        
         B     OUTLHS3                                                          
         SPACE 1                                                                
OUTPCT   DS    0H                  PERCENTAGES ON LHS                           
         LR    RF,R3                                                            
         BAS   RE,OUTPCTAL                                                      
         B     UXIT                                                             
         SPACE 1                                                                
OUTPCTR  DS    0H                                                               
         LA    RF,LHSLNQ(R3)       PERCENTAGES ON RHS                           
         BAS   RE,OUTPCTAL                                                      
         B     OUTLHS2             DISPLAY 2 CHARS OF LHS                       
         SPACE 1                                                                
OUTPCTAL NTR1                      ALL PERCENTAGES                              
         BAS   RE,OUTBINAL                                                      
         AR    RF,R0                                                            
         MVI   0(RF),C'%'          TRAIL WITH PERCENT SIGN                      
         B     UXIT                                                             
         SPACE 1                                                                
OUTWPCTR DS    0H                                                               
         LA    RF,LHSLNQ(R3)       WHOLE PERCENTAGES ON RHS                     
         BAS   RE,OUTWPCT                                                       
         B     OUTLHS3             DISPLAY 3 CHARS OF LHS                       
         SPACE 1                                                                
OUTWPCT  NTR1                                                                   
         BAS   RE,OUTAMTRF                                                      
         MVI   0(RF),C'%'          TRAIL WITH PERCENT SIGN                      
         B     UXIT                                                             
         EJECT                                                                  
*              OPTION OUTPUT ROUTINES, CONT'D                                   
         SPACE 1                                                                
         USING OPTD,R1             R1=A(OPTION TABLE ENTRY)                     
*                                  R3=A(UNSCAN BLOCK ENTRY)                     
         USING TAESSBCD,R4         R4=A(SUB-ELEMENT)                            
         SPACE 1                                                                
OUTEXCH  DS    0H                  EXCHANGE RATE                                
         LA    RE,LHSLNQ(R3)                                                    
         EDIT  (4,TAESSBDT),(10,0(RE)),4,ALIGN=LEFT                             
         B     OUTLHS2             DISPLAY LHS                                  
         SPACE 2                                                                
OUTPRBR  DS    0H                  WHERE PRODUCED OR BROADCAST                  
         MVC   LHSLNQ(2,R3),=C'US'                                              
         CLI   TAESSBDT,C'C'                                                    
         BNE   *+10                                                             
         MVC   LHSLNQ(3,R3),=C'CAN'                                             
         B     OUTLHS2                                                          
         SPACE 2                                                                
OUTBINR  DS    0H                  BINARY NUMBER ON RHS                         
         ZIC   RE,TAESSBDT                                                      
         CVD   RE,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  LHSLNQ(2,R3),DUB+6(2)                                            
         B     OUTLHS2             DISPLAY 2 CHARS OF LHS                       
         SPACE 2                                                                
OUTBIN   DS    0H                  BINARY AMOUNT - NO DEC. PLACES (LHS)         
         LR    RF,R3                                                            
         BAS   RE,OUTBINAL                                                      
         B     UXIT                                                             
         SPACE 2                                                                
OUTBINAL DS    0H                  BINARY AMOUNT - NO DEC. PLACES               
         EDIT  (2,TAESSBDT),(10,(RF)),ALIGN=LEFT,ZERO=NOBLANK                   
         BR    RE                                                               
         SPACE 2                                                                
OUTBIN1  DS    0H                  BINARY AMOUNT - NO DEC. PLACES (LHS)         
         EDIT  (1,TAESSBDT),(3,(R3)),ALIGN=LEFT,ZERO=NOBLANK                    
         B     UXIT                                                             
         SPACE 2                                                                
OUTASOF  DS    0H                  DISPLAY AS OF DATE ON LHS                    
         MVI   0(R3),C'>'          MOVE OUT 'GREATER-THAN' SIGN                 
         LA    RF,1(R3)                                                         
         BAS   RE,OUTDATE                                                       
         B     UXIT                                                             
         EJECT                                                                  
*              OPTION OUTPUT ROUTINES, CONT'D                                   
         SPACE 1                                                                
         USING OPTD,R1             R1=A(OPTION TABLE ENTRY)                     
*                                  R3=A(UNSCAN BLOCK ENTRY)                     
         USING TAESSBCD,R4         R4=A(SUB-ELEMENT)                            
         SPACE 1                                                                
OUTDATER DS    0H                  DISPLAY DATE ON RHS                          
         LA    RF,LHSLNQ(R3)                                                    
         BAS   RE,OUTDATE                                                       
         B     OUTLHS3             DISPLAY LHS                                  
         SPACE 2                                                                
OUTEXP   DS    0H                  EXPIRY DATE                                  
         CLI   TAESSBDT,X'FF'                                                   
         BNE   OUTDATER                                                         
         MVI   LHSLNQ(R3),C'Y'                                                  
         B     OUTLHS3             DISPLAY LHS                                  
         SPACE 2                                                                
OUTDATEL DS    0H                  DISPLAY DATE ON LHS                          
         LR    RF,R3                                                            
         BAS   RE,OUTDATE                                                       
         B     UXIT                                                             
         SPACE 2                                                                
OUTDATE  NTR1  ,                   RF=A(OUTPUT AREA)                            
         LR    R2,RF                                                            
         GOTO1 DATCON,DMCB,(1,TAESSBDT),(10,DUB)  O/P FORMAT MM/DD/YY           
         MVC   0(2,R2),DUB+1       ASSUME NEED ONLY M/                          
         CLI   DUB,C'0'            SUPPRESS LEADING 0 IN MONTH                  
         BE    *+14                                                             
         MVC   0(3,R2),DUB         NEED MM/                                     
         LA    R2,1(R2)                                                         
         MVC   2(4,R2),DUB+4       ASSUME NEED ONLY ...D/YY                     
         CLI   DUB+3,C'0'          SUPPRESS LEADING 0 IN DAY                    
         BE    *+14                                                             
         MVC   2(5,R2),DUB+3       NEED ...DD/YY                                
         LA    R2,1(R2)                                                         
         CLC   TAESSBDT(1),TGTODAY1  IF YEAR IS SAME AS TODAY                   
         BNE   OUTD5                                                            
         LA    RF,3(R2)            SET RF=A(NEXT SLOT) RIGHT AFTER DAY          
         MVC   0(3,RF),SPACES      GET RID OF /YY                               
         B     *+8                                                              
OUTD5    LA    RF,6(R2)            RETURN A(NEXT SLOT) IN RF                    
         B     UXITRF                                                           
         SPACE 2                                                                
OUTCYC   DS    0H                  DISPLAY CYCLE DATES                          
         LR    RF,R3                                                            
         BAS   RE,OUTDATE          DISPLAY 1ST DATE (RETURNS RF=A(NXT))         
         MVI   0(RF),C'-'          INSERT HYPHEN                                
         LA    RF,1(RF)                                                         
         LA    R4,3(R4)            BUMP TO SECOND DATE                          
         BAS   RE,OUTDATE          DISPLAY IT                                   
         B     UXIT                                                             
         EJECT                                                                  
*              OPTION OUTPUT ROUTINES, CONT'D                                   
         SPACE 1                                                                
         USING OPTD,R1             R1=A(OPTION TABLE ENTRY)                     
*                                  R3=A(UNSCAN BLOCK ENTRY)                     
         USING TAESSBCD,R4         R4=A(SUB-ELEMENT)                            
         SPACE 1                                                                
OUTUSE   DS    0H                            USE TYPE                           
         GOTO1 USEVAL,DMCB,(X'80',TAESSBDT),TAESSBDT+1  CVT TO CHAR.            
         MVC   0(L'TGUSCDE,R3),TGUSCDE       AND MOVE TO DISPLAY AREA           
         MVC   L'TGUSCDE(L'TGUSTYCD,R3),TGUSTYCD                                
         B     UXIT                                                             
         SPACE 2                                                                
OUTUNITS DS    0H                            UNITS                              
         CLI   TGUSEQU,URTK                  RTK: 0=ENTIRE, 1=PARTIAL           
         BNE   OUTBIN                                                           
         OC    TAESSBDT(2),TAESSBDT                                             
         BNZ   OUTUNIT5                                                         
         MVC   0(4,R3),=C'ENTR'                                                 
         B     UXIT                                                             
OUTUNIT5 CLC   =X'0001',TAESSBDT                                                
         BNE   UXIT                                                             
         MVC   0(4,R3),=C'PART'                                                 
         B     UXIT                                                             
         SPACE 2                                                                
OUTMAJ   DS    0H                            MAJORS                             
         CLI   TGUSEQU,URTK                  RTK: >60 DAYS                      
         BE    OUTMAJ5                                                          
         GOTO1 MAJVAL,DMCB,(X'80',TAESSBDT)  CONVERT EQUATE TO CHAR.            
         CLI   TAESSBDT,NY+LA+CHI            IF ALL MAJORS                      
         BNE   *+14                                                             
         MVC   0(3,R3),=C'ALL'               DISPLAY "ALL" INSTEAD              
         B     *+10                                                             
         MVC   0(L'TGMACHAR,R3),TGMACHAR     ELSE MOVE TO DISPLAY AREA          
         B     UXIT                                                             
*                                                                               
OUTMAJ5  TM    TAESSBDT,X'01'      RTK: >60 DAYS                                
         BZ    UXIT                                                             
         MVC   0(3,R3),=C'>60'                                                  
         B     UXIT                                                             
         SPACE 2                                                                
OUTYEAR  DS    0H                            CONTRACT YEAR                      
         LR    R2,R1                                                            
         GOTO1 YRVAL,DMCB,(X'80',TAESSBDT)   CONVERT EQUATE TO CHAR.            
         MVC   LHSLNQ(L'TGYRCDE,R3),TGYRCDE  MOVE TO DISPLAY AREA               
         LR    R1,R2                                                            
         B     OUTLHS2                       ALSO DISPLAY LHS                   
         SPACE 2                                                                
OUTUSES2 DS    0H                  SPECIFIC USE NUMBERS                         
         LR    RF,R3                                                            
         BAS   RE,OUTBINAL         DISPLAY START                                
         AR    RF,R0                                                            
         MVI   0(RF),C'-'          IMMEDIATELY FOLLOWED BY HYPHEN               
         CLC   TAESSBDT(2),TAESSBDT+2                                           
         BE    UXIT                GET OUT IF START USE = END USE               
         LA    RF,1(RF)                                                         
         LA    R4,2(R4)            BUMP TO END                                  
         BAS   RE,OUTBINAL         AND DISPLAY IT                               
         B     UXIT                                                             
         SPACE 2                                                                
OUTCOMNM DS    0H                  HYPO COMMERCIAL NAME                         
         ZIC   RE,TAESSBDT                                                      
OUTCOMN2 BCTR  RE,0                ENTRY FROM TAESTPRT                          
         CHI   RE,34                                                            
         BNH   *+8                                                              
         LHI   RE,34                                                            
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     UXIT                                                             
         MVC   HCOMNAME(0),TAESSBDT+1                                           
         DROP  R4                                                               
         EJECT                                                                  
*              OPTION OUTPUT ROUTINES, CONT'D                                   
         SPACE 1                                                                
         USING OPTD,R1             R1=A(OPTION TABLE ENTRY)                     
*                                  R3=A(UNSCAN BLOCK ENTRY)                     
         USING TAESSBCD,R4         R4=A(SUB-ELEMENT)                            
         SPACE 1                                                                
OUTOVER2 DS    0H                  OVERSCALE 2                                  
         MVI   LHSLNQ(R3),C'/'                                                  
         LA    RF,LHSLNQ+1(R3)                                                  
         BAS   RE,OUTAMT           DISPLAY AS AMOUNT W/O PCT SIGN               
         B     OUTLHS2                                                          
         SPACE 2                                                                
OUTOV12  DS    0H                  OVERSCALE 1 & 2                              
         LA    RF,LHSLNQ(R3)                                                    
         BAS   RE,OUTAMTRF         DISPLAY OV1 AS AMT W/O PCT SIGN              
         MVI   0(RF),C'/'                                                       
         LA    RF,1(RF)                                                         
         LA    R4,4(R4)            BUMP PAST OV1 FOR OV2 RATE                   
         BAS   RE,OUTAMT           DISPLAY OV2 AS AMOUNT W/O PCT SIGN           
         B     OUTLHS2                                                          
         EJECT                                                                  
*              OPTION OUTPUT ROUTINES, CONT'D                                   
         SPACE 1                                                                
         USING OPTD,R1             R1=A(OPTION TABLE ENTRY)                     
*                                  R3=A(UNSCAN BLOCK ENTRY)                     
         USING TAESSBCD,R4         R4=A(SUB-ELEMENT)                            
         SPACE 1                                                                
OUTOVAS  DS    0H                  OVERSCALE AS OF DATE                         
         MVI   0(R3),C'>'                                                       
         LA    RF,1(R3)                                                         
         BAS   RE,OUTDATE                                                       
         LA    RF,LHSLNQ(R3)                                                    
         LA    R4,3(R4)            BUMP PAST DATE FOR RATE                      
         BAS   RE,OUTAMT           DISPLAY AS AMOUNT W/O PCT SIGN               
         B     UXIT                                                             
         SPACE 2                                                                
OUTOV2AS DS    0H                  OVERSCALE 2 AS OF DATE                       
         MVI   0(R3),C'>'                                                       
         LA    RF,1(R3)                                                         
         BAS   RE,OUTDATE                                                       
         LA    RF,LHSLNQ(R3)                                                    
         LA    R4,3(R4)            BUMP PAST DATE FOR RATE                      
         MVI   0(RF),C'/'                                                       
         LA    RF,1(RF)                                                         
         BAS   RE,OUTAMT           DISPLAY AS AMOUNT W/O PCT SIGN               
         B     UXIT                                                             
         SPACE 2                                                                
OUTOV12A DS    0H                  OVERSCALE 1 & 2 AS OF DATE                   
         MVI   0(R3),C'>'                                                       
         LA    RF,1(R3)                                                         
         BAS   RE,OUTDATE                                                       
         LA    RF,LHSLNQ(R3)                                                    
         LA    R4,3(R4)            BUMP PAST DATE FOR RATE                      
         BAS   RE,OUTAMTRF         DISPLAY AS AMOUNT W/O PCT SIGN               
         MVI   0(RF),C'/'                                                       
         LA    RF,1(RF)                                                         
         LA    R4,4(R4)            BUMP PAST OV1 FOR OV2 RATE                   
         BAS   RE,OUTAMT           DISPLAY AS AMOUNT W/O PCT SIGN               
         B     UXIT                                                             
         SPACE 2                                                                
OUTYRAS  DS    0H                  CONTRACT YEAR AS OF DATE                     
         MVI   0(R3),C'>'                                                       
         LA    RF,1(R3)                                                         
         BAS   RE,OUTDATE                                                       
         GOTO1 YRVAL,DMCB,(X'80',TAESSBDT+3)   CONVERT EQUATE TO CHAR.          
         MVI   LHSLNQ(R3),C'Y'                 PREFIX YEAR WITH 'Y'             
         MVC   LHSLNQ+1(L'TGYRCDE,R3),TGYRCDE  FOLLOW WITH YEAR                 
         B     UXIT                                                             
         EJECT                                                                  
*              OPTION OUTPUT ROUTINES, CONT'D                                   
         SPACE 1                                                                
         USING OPTD,R1             R1=A(OPTION TABLE ENTRY)                     
*                                  R3=A(UNSCAN BLOCK ENTRY)                     
         USING TAESSBCD,R4         R4=A(SUB-ELEMENT)                            
         SPACE 1                                                                
OUTHLDAS DS    0H                  HLD AMT AS OF DATE                           
         MVI   0(R3),C'>'                                                       
         LA    RF,1(R3)                                                         
         BAS   RE,OUTDATE                                                       
         MVI   LHSLNQ(R3),C'H'     PREFIX AMT WITH 'H'                          
         LA    RF,LHSLNQ+1(R3)                                                  
         LA    R4,3(R4)            BUMP TO AMT                                  
         BAS   RE,OUTAMT                                                        
         B     UXIT                                                             
         SPACE 1                                                                
OUTHLDRA DS    0H                  HLD RATE AS OF DATE                          
         MVI   0(R3),C'>'                                                       
         LA    RF,1(R3)                                                         
         BAS   RE,OUTDATE                                                       
         MVI   LHSLNQ(R3),C'H'     PREFIX AMT WITH 'H'                          
         LA    RF,LHSLNQ+1(R3)                                                  
         LA    R4,3(R4)            BUMP TO PERCENT                              
         BAS   RE,OUTWPCT                                                       
         B     UXIT                                                             
         EJECT                                                                  
*              OPTION OUTPUT ROUTINES, CONT'D                                   
         SPACE 1                                                                
         USING OPTD,R1             R1=A(OPTION TABLE ENTRY)                     
*                                  R3=A(UNSCAN BLOCK ENTRY)                     
         USING TAESSBCD,R4         R4=A(SUB-ELEMENT)                            
         SPACE 1                                                                
OUTATYPE DS    0H                            COMMERCIAL ACTRA TYPE              
         LR    R2,R1                                                            
         GOTO1 CCTYPVAL,DMCB,(X'80',TAESSBDT) CONVERT EQUATE TO CHAR.           
         MVC   LHSLNQ(L'TGCCTCDE,R3),TGCCTCDE MOVE TO DISPLAY AREA              
         LR    R1,R2                                                            
         B     OUTLHS3                       ALSO DISPLAY LHS                   
         SPACE 2                                                                
OUTCSF   DS    0H                            CONTRACT SERVICE FEE               
         CLI   TAESSBDT,C'Y'                                                    
         BE    *+12                                                             
         CLI   TAESSBDT,C'N'                                                    
         BNE   *+14                                                             
         MVC   LHSLNQ(1,R3),TAESSBDT         DISP Y/N AND LHS FOR 3             
         B     OUTLHS3                                                          
         B     OUTAMT3                       ELSE, DISP AMT & LHS FOR 3         
         EJECT                                                                  
