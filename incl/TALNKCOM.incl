*          DATA SET TALNKCOM   AT LEVEL 001 AS OF 03/08/11                      
***********************************************************************         
*        VALIDATE PASSED MUSIC CODES                                  *         
*        ON ENTRY ... P1=A(AGENCY CODE)                               *         
*                     R3=A(IOKEY)                                     *         
***********************************************************************         
                                                                                
VALMUS   NTR1  BASE=*,LABEL=*                                                   
         MVC   DUB1,0(R1)                                                       
                                                                                
         LA    R2,RQCOMU1          R2=A(FIRST MUSIC CODE FIELD)                 
         LHI   R0,4                R0=# OF MUSIC CODE FIELDS                    
                                                                                
         USING ERRENTD,R4                                                       
         LA    R4,ERCOM1NF         R4=A(FIRST ERROR ENTRY)                      
                                                                                
         USING TLMUD,R3                                                         
VM10     OC    0(L'RQCOMU1,R2),0(R2)                                            
         JZ    VM20                                                             
         XC    TLMUKEY,TLMUKEY     IF CURRENT FIELD IS POPULATED                
         MVI   TLMUCD,TLMUCDQ      READ FOR MUSIC KEY                           
         MVC   TLMUAGY,DUB1        AND ENSURE IT EXISTS                         
         MVC   TLMUMUS,0(R2)                                                    
         GOTOR (#IOEXEC,AIOEXEC),'IORD+IODIR+IO3'                               
         JE    VM20                                                             
         GOTOR (#ADDERR,AADDERR),DMCB,(R4)                                      
         DROP  R3                                                               
                                                                                
VM20     LA    R2,L'RQCOMU1(R2)    BUMP TO THE NEXT MUSIC FIELD                 
         ZIC   RE,EELEN            AND ERROR MESSAGE                            
         AR    R4,RE               AND GO VALIDATE                              
         BCT   R0,VM10                                                          
         J     XIT                                                              
         DROP  R4                                                               
                                                                                
***********************************************************************         
*        ERROR ENTRIES FOR VALMUS                                     *         
*        (LOOK AT ERRENTD FOR DSECT)                                  *         
***********************************************************************         
                                                                                
ERCOM1NF DC    AL1(ECOM1NFX-*),AL2(106),AL1(ERRCATY1),AL1(D#COMU1)              
         DC    C'Music Code not on file'                                        
ECOM1NFX EQU   *                                                                
                                                                                
ERCOM2NF DC    AL1(ECOM2NFX-*),AL2(108),AL1(ERRCATY1),AL1(D#COMU2)              
         DC    C'Music Code not on file'                                        
ECOM2NFX EQU   *                                                                
                                                                                
ERCOM3NF DC    AL1(ECOM3NFX-*),AL2(110),AL1(ERRCATY1),AL1(D#COMU3)              
         DC    C'Music Code not on file'                                        
ECOM3NFX EQU   *                                                                
                                                                                
ERCOM4NF DC    AL1(ECOM4NFX-*),AL2(112),AL1(ERRCATY1),AL1(D#COMU4)              
         DC    C'Music Code not on file'                                        
ECOM4NFX EQU   *                                                                
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        ROUTINE COPIES STUDIO ELEMENT INTO REQUEST MAP               *         
*        ON ENTRY ... R4 = A(COMMERCIAL STUDIO ELEMENT)               *         
***********************************************************************         
                                                                                
         USING TACSD,R4                                                         
CPYTACS  NTR1  BASE=*,LABEL=*                                                   
         MVC   ELEM,SPACES         COPY STUDIO ELEMENT INTO ELEM                
         ZIC   RE,TACSLEN                                                       
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         J     *+10                                                             
         MVC   ELEM(0),TACSD                                                    
         MVI   0(R4),X'FF'                                                      
         DROP  R4                                                               
                                                                                
         USING TACSD,R4                                                         
         LA    R4,ELEM                                                          
         CLI   TACSTYPE,TACSTYPF                                                
         JNE   CCS10                                                            
         GOTOR (#CHKFLD,ACHKFLD),DMCB,(L'RQCOFDT,RQCOFDT),TACSDATE,    +        
               ERCOFDT,(0,RQCOMED),CCSR                                         
         CLI   TACSTYPE,TACSTYPF                                                
         JNE   CCS10                                                            
         GOTOR (#CHKFLD,ACHKFLD),DMCB,(L'RQCOFDT,RQCOFDT),TACSDATE,    +        
               ERCOFDT,(0,RQCOMED),CCSR                                         
         GOTOR (#CHKFLD,ACHKFLD),DMCB,(L'RQCOFSU,RQCOFSU),TACSSTUD,    +        
               ERCOFSU,(L'RQCOFDT,RQCOFDT)                                      
         GOTOR (#CHKFLD,ACHKFLD),DMCB,(L'RQCOFCY,RQCOFCY),TACSCITY,    +        
               ERCOFCY,(L'RQCOFDT,RQCOFDT)                                      
         GOTOR (#CHKFLD,ACHKFLD),DMCB,(L'RQCOFST,RQCOFST),TACSSTAT,    +        
               ERCOFST,(L'RQCOFDT,RQCOFDT)                                      
         OI    CPYSTAT2,CPYTACSF                                                
                                                                                
         CLC   TACSDATE,RQCOFDT    IF FILM DATE IS CHANGING                     
         JE    XIT                                                              
         BRAS  RE,FDTCHG           DO SPECIAL PROCESSING                        
         J     XIT                                                              
                                                                                
CCS10    CLI   TACSTYPE,TACSTYPM                                                
         JNE   CCS20                                                            
         GOTOR (#CHKFLD,ACHKFLD),DMCB,(L'RQCOMDT,RQCOMDT),TACSDATE,    +        
               ERCOMDT,0                                                        
         GOTOR (#CHKFLD,ACHKFLD),DMCB,(L'RQCOMSU,RQCOMSU),TACSSTUD,    +        
               ERCOMSU,(L'RQCOMDT,RQCOMDT)                                      
         GOTOR (#CHKFLD,ACHKFLD),DMCB,(L'RQCOMCY,RQCOMCY),TACSCITY,    +        
               ERCOMCY,(L'RQCOMDT,RQCOMDT)                                      
         GOTOR (#CHKFLD,ACHKFLD),DMCB,(L'RQCOMST,RQCOMST),TACSSTAT,    +        
               ERCOMST,(L'RQCOMDT,RQCOMDT)                                      
         OI    CPYSTAT2,CPYTACSM                                                
         J     XIT                                                              
                                                                                
CCS20    CLI   TACSTYPE,TACSTYPR                                                
         JNE   XIT                                                              
         GOTOR (#CHKFLD,ACHKFLD),DMCB,(L'RQCORDT,RQCORDT),TACSDATE,    +        
               ERCORDT,0                                                        
         GOTOR (#CHKFLD,ACHKFLD),DMCB,(L'RQCORSU,RQCORSU),TACSSTUD,    +        
               ERCORSU,(L'RQCORDT,RQCORDT)                                      
         GOTOR (#CHKFLD,ACHKFLD),DMCB,(L'RQCORCY,RQCORCY),TACSCITY,    +        
               ERCORCY,(L'RQCORDT,RQCORDT)                                      
         GOTOR (#CHKFLD,ACHKFLD),DMCB,(L'RQCORST,RQCORST),TACSSTAT,    +        
               ERCORST,(L'RQCORDT,RQCORDT)                                      
         OI    CPYSTAT2,CPYTACSR                                                
         J     XIT                                                              
         DROP  R4                                                               
                                                                                
***********************************************************************         
*        IF STUDIO ELEMENT DID NOT EXIST ON RECORD PREVIOUSLY AND     *         
*        STUDIO INFORMATION IS BEING ADDED NOW, ROUTINE PROCESSES IT  *         
***********************************************************************         
                                                                                
NEWTACS  NTR1  BASE=*,LABEL=*                                                   
         TM    CPYSTAT2,CPYTACSF   IF FILM STUDIO ELEMENT DID NOT               
         JO    NCS10               EXIST ON RECORD PREVIOUSLY                   
         OC    RQCOFDT,RQCOFDT     AND IS BEING ADDED NOW                       
         JZ    NCS10               RETURN PROMPT FOR VERIFICATION               
         GOTOR (#ADDERR,AADDERR),DMCB,('ESREVIW',ERCOFDT)                       
         GOTOR (#ADDERR,AADDERR),DMCB,ERCOFSU                                   
         GOTOR (#ADDERR,AADDERR),DMCB,ERCOFCY                                   
         BRAS  RE,FDTCHG           AND SET TO DO SPECIAL PROCESSING             
         OC    RQCOFST,RQCOFST                                                  
         JZ    NCS10                                                            
         GOTOR (#ADDERR,AADDERR),DMCB,ERCOFST                                   
                                                                                
NCS10    TM    CPYSTAT2,CPYTACSM   IF MUSIC STUDIO ELEMENT DID NOT              
         JO    NCS20               EXIST ON RECORD PREVIOUSLY                   
         OC    RQCOMDT,RQCOMDT     AND IS BEING ADDED NOW                       
         JZ    NCS20               RETURN PROMPT FOR VERIFICATION               
         GOTOR (#ADDERR,AADDERR),DMCB,('ESREVIW',ERCOMDT)                       
         GOTOR (#ADDERR,AADDERR),DMCB,ERCOMSU                                   
         GOTOR (#ADDERR,AADDERR),DMCB,ERCOMCY                                   
         OC    RQCOMST,RQCOMST                                                  
         JZ    NCS20                                                            
         GOTOR (#ADDERR,AADDERR),DMCB,ERCOMST                                   
                                                                                
NCS20    TM    CPYSTAT2,CPYTACSR   IF RECORD STUDIO ELEMENT DID NOT             
         JO    XIT                 EXIST ON RECORD PREVIOUSLY                   
         OC    RQCORDT,RQCORDT     AND IS BEING ADDED NOW                       
         JZ    XIT                 RETURN PROMPT FOR VERIFICATION               
         GOTOR (#ADDERR,AADDERR),DMCB,('ESREVIW',ERCORDT)                       
         GOTOR (#ADDERR,AADDERR),DMCB,ERCORSU                                   
         GOTOR (#ADDERR,AADDERR),DMCB,ERCORCY                                   
         OC    RQCORST,RQCORST                                                  
         JZ    XIT                                                              
         GOTOR (#ADDERR,AADDERR),DMCB,ERCORST                                   
         J     XIT                                                              
                                                                                
***********************************************************************         
*        ERROR ENTRIES FOR CPYTACS AND NEWTACS                        *         
*        (LOOK AT ERRENTD FOR DSECT)                                  *         
***********************************************************************         
                                                                                
ERCOFDT  DC    AL1(ECOFDTX-*),AL2(31),AL1(ERRCATY2),AL1(D#COFDT)                
         DC    C'Review update to Film Date'                                    
ECOFDTX  EQU   *                                                                
                                                                                
ERCOFSU  DC    AL1(ECOFSUX-*),AL2(32),AL1(ERRCATY2),AL1(D#COFSU)                
         DC    C'Review update to Film Studio'                                  
ECOFSUX  EQU   *                                                                
                                                                                
ERCOFCY  DC    AL1(ECOFCYX-*),AL2(33),AL1(ERRCATY2),AL1(D#COFCY)                
         DC    C'Review update to Film City'                                    
ECOFCYX  EQU   *                                                                
                                                                                
ERCOFST  DC    AL1(ECOFSTX-*),AL2(133),AL1(ERRCATY2),AL1(D#COFST)               
         DC    C'Review update to Film State'                                   
ECOFSTX  EQU   *                                                                
                                                                                
ERCOMDT  DC    AL1(ECOMDTX-*),AL2(37),AL1(ERRCATY2),AL1(D#COMDT)                
         DC    C'Review update to Music Date'                                   
ECOMDTX  EQU   *                                                                
                                                                                
ERCOMSU  DC    AL1(ECOMSUX-*),AL2(38),AL1(ERRCATY2),AL1(D#COMSU)                
         DC    C'Review update to Music Studio'                                 
ECOMSUX  EQU   *                                                                
                                                                                
ERCOMCY  DC    AL1(ECOMCYX-*),AL2(39),AL1(ERRCATY2),AL1(D#COMCY)                
         DC    C'Review update to Music City'                                   
ECOMCYX  EQU   *                                                                
                                                                                
ERCOMST  DC    AL1(ECOMSTX-*),AL2(135),AL1(ERRCATY2),AL1(D#COMST)               
         DC    C'Review update to Music State'                                  
ECOMSTX  EQU   *                                                                
                                                                                
ERCORDT  DC    AL1(ECORDTX-*),AL2(34),AL1(ERRCATY2),AL1(D#CORDT)                
         DC    C'Review update to Recording Date'                               
ECORDTX  EQU   *                                                                
                                                                                
ERCORSU  DC    AL1(ECORSUX-*),AL2(35),AL1(ERRCATY2),AL1(D#CORSU)                
         DC    C'Review update to Recording Studio'                             
ECORSUX  EQU   *                                                                
                                                                                
ERCORCY  DC    AL1(ECORCYX-*),AL2(36),AL1(ERRCATY2),AL1(D#CORCY)                
         DC    C'Review update to Recording City'                               
ECORCYX  EQU   *                                                                
                                                                                
ERCORST  DC    AL1(ECORSTX-*),AL2(134),AL1(ERRCATY2),AL1(D#CORST)               
         DC    C'Review update to Recording State'                              
ECORSTX  EQU   *                                                                
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
CCSR     DC    AL1(TACOMEDR),X'FF'                                              
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        ROUTINE COPIES COMMERCIAL NAME ELEMENT INTO REQUEST MAP      *         
*        ON ENTRY ... R2 = A(ELEM)                                              
*                     R4 = A(FREE FORM NAME ELEMENT)                  *         
***********************************************************************         
                                                                                
         USING TANAD,R4                                                         
CPYTANA  NTR1  BASE=*,LABEL=*                                                   
         XC    ELEM,ELEM                                                        
         ZIC   RF,TANALEN          COPY EXISTING NAME ELEMENT                   
         BCTR  RF,0                INTO ELEM                                    
         EX    RF,*+8                                                           
         J     *+10                                                             
         MVC   ELEM(0),0(R4)                                                    
                                                                                
         MVI   0(R4),X'FF'         SET TO DELETE ELEMENT                        
         DROP  R4                                                               
                                                                                
         USING TANAD,R2                                                         
         OC    TANANAME(L'RQCOTIT),SPACES                                       
         GOTOR (#CHKFLD,ACHKFLD),DMCB,(L'RQCOTIT,RQCOTIT),TANANAME,    +        
               ERCOTIT,0                                                        
                                                                                
         CLI   RQCOMOD,RQCOEXE     IF MODE IS EXECUTE                           
         JNE   XIT                                                              
         CLC   TANANAME,RQCOTIT    AND TITLE IS CHANGING                        
         JE    XIT                                                              
         BRAS  RE,FDTCHG           SET TO DO SPECIAL PROCESSINGG                
         J     XIT                                                              
         DROP  R2                                                               
                                                                                
***********************************************************************         
*        ERROR ENTRIES FOR CPYTANA                                    *         
*        (LOOK AT ERRENTD FOR DSECT)                                  *         
***********************************************************************         
                                                                                
ERCOTIT  DC    AL1(ECOTITX-*),AL2(14),AL1(ERRCATY2),AL1(D#COTIT)                
         DC    C'Review update to Title'                                        
ECOTITX  EQU   *                                                                
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        ROUTINE COPIES COMMENT ELEMENT INTO REQUEST MAP              *         
*        ON ENTRY ... R2 = A(ELEM)                                              
*                     R4 = A(VARIABLE LENGTH COMMENT ELEMENT)         *         
***********************************************************************         
                                                                                
         USING TACMD,R4                                                         
CPYTACM  NTR1  BASE=*,LABEL=*                                                   
         CLI   TACMTYPE,TACMTYPG   IF TYPE IS GENERAL COMMENT                   
         JNE   XIT                                                              
                                                                                
         XC    ELEM,ELEM                                                        
         ZIC   RF,TACMLEN          COPY EXISTING COMMENT ELEMENT                
         BCTR  RF,0                INTO ELEM                                    
         EX    RF,*+8                                                           
         J     *+10                                                             
         MVC   ELEM(0),0(R4)                                                    
                                                                                
         OI    CPYSTAT1,CPYTACMG   SET PROCESSED STATUS                         
         MVI   0(R4),X'FF'         AND SET TO DELETE ELEMENT                    
         DROP  R4                                                               
                                                                                
         USING TACMD,R2                                                         
         OC    TACMCOMM(L'RQCOCMT),SPACES                                       
         GOTOR (#CHKFLD,ACHKFLD),DMCB,(L'RQCOCMT,RQCOCMT),TACMCOMM,    +        
               ERCOCMT,0                                                        
         J     XIT                                                              
         DROP  R2                                                               
                                                                                
***********************************************************************         
*        IF COMMENT ELEMENT DID NOT EXIST ON RECORD PREVIOUSLY        *         
*        AND COMMENT IS BEING ADDED NOW, ROUTINE PROCESSES IT         *         
***********************************************************************         
                                                                                
NEWTACM  NTR1  BASE=*,LABEL=*                                                   
         TM    CPYSTAT1,CPYTACMG   IF GENERAL COMMENT ELEMENT DID NOT           
         JO    XIT                 EXIST ON RECORD PREVIOUSLY                   
         OC    RQCOCMT,RQCOCMT     AND IS BEING ADDED NOW                       
         JZ    XIT                 RETURN PROMPT FOR VERIFICATION               
         GOTOR (#ADDERR,AADDERR),DMCB,('ESREVIW',ERCOCMT)                       
         J     XIT                                                              
                                                                                
***********************************************************************         
*        ERROR ENTRIES FOR CPYTACM AND NEWTACM                        *         
*        (LOOK AT ERRENTD FOR DSECT)                                  *         
***********************************************************************         
                                                                                
ERCOCMT  DC    AL1(ECOCMTX-*),AL2(73),AL1(ERRCATY2),AL1(D#COCMT)                
         DC    C'Review update to Comment'                                      
ECOCMTX  EQU   *                                                                
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        ROUTINE COPIES PUBLISHED MUSIC ELEMENT INTO REQUEST MAP      *         
*        ON ENTRY ... R4 = A(COMMERCIAL PUBLISHED MUSIC ELEMENT)      *         
***********************************************************************         
                                                                                
         USING TACPD,R4                                                         
CPYTACP  NTR1  BASE=*,LABEL=*                                                   
         GOTOR (#CHKFLD,ACHKFLD),DMCB,(L'RQCOMU1,RQCOMU1),TACPMUS,     +        
               ERCOMU1,0                                                        
         OI    CPYSTAT1,CPYTACP1   PROCESS PUBLISHED MUSIC CODE 1               
         MVI   0(R4),X'FF'                                                      
                                                                                
         ZIC   RF,1(R4)                                                         
         AR    R4,RF                                                            
         CLI   0(R4),TACPELQ       PROCESS PUBLISHED MUSIC CODE 2               
         JNE   XIT                                                              
         GOTOR (#CHKFLD,ACHKFLD),DMCB,(L'RQCOMU2,RQCOMU2),TACPMUS,     +        
               ERCOMU2,0                                                        
         OI    CPYSTAT2,CPYTACP2                                                
         MVI   0(R4),X'FF'                                                      
                                                                                
         ZIC   RF,1(R4)                                                         
         AR    R4,RF                                                            
         CLI   0(R4),TACPELQ       PROCESS PUBLISHED MUSIC CODE 3               
         JNE   XIT                                                              
         GOTOR (#CHKFLD,ACHKFLD),DMCB,(L'RQCOMU3,RQCOMU3),TACPMUS,     +        
               ERCOMU3,0                                                        
         OI    CPYSTAT2,CPYTACP3                                                
         MVI   0(R4),X'FF'                                                      
                                                                                
         ZIC   RF,1(R4)                                                         
         AR    R4,RF                                                            
         CLI   0(R4),TACPELQ       PROCESS PUBLISHED MUSIC CODE 4               
         JNE   XIT                                                              
         GOTOR (#CHKFLD,ACHKFLD),DMCB,(L'RQCOMU4,RQCOMU4),TACPMUS,     +        
               ERCOMU4,0                                                        
         OI    CPYSTAT2,CPYTACP4                                                
         MVI   0(R4),X'FF'                                                      
         J     XIT                                                              
         DROP  R4                                                               
                                                                                
***********************************************************************         
*        IF PUBLISHED MUSIC ELEMENTS DID NOT EXIST ON RECORD          *         
*        PREVIOUSLY AND PUBLISHED MUSIC CODES ARE BEING ADDED NOW,    *         
*        ROUTINE PROCESSES THEM                                       *         
***********************************************************************         
                                                                                
NEWTACP  NTR1  BASE=*,LABEL=*                                                   
         TM    CPYSTAT1,CPYTACP1   IF PUBLISHED MUSIC 1 ELEMENT DID             
         JO    NCP10               NOT EXIST ON RECORD PREVIOUSLY               
         OC    RQCOMU1,RQCOMU1     AND IS BEING ADDED NOW                       
         JZ    XIT                 RETURN PROMPT FOR VERIFICATION               
         GOTOR (#ADDERR,AADDERR),DMCB,('ESREVIW',ERCOMU1)                       
                                                                                
NCP10    TM    CPYSTAT2,CPYTACP2   IF PUBLISHED MUSIC 2 ELEMENT DID             
         JO    NCP20               NOT EXIST ON RECORD PREVIOUSLY               
         OC    RQCOMU2,RQCOMU2     AND IS BEING ADDED NOW                       
         JZ    XIT                 RETURN PROMPT FOR VERIFICATION               
         GOTOR (#ADDERR,AADDERR),DMCB,('ESREVIW',ERCOMU2)                       
                                                                                
NCP20    TM    CPYSTAT2,CPYTACP3   IF PUBLISHED MUSIC 3 ELEMENT DID             
         JO    NCP30               NOT EXIST ON RECORD PREVIOUSLY               
         OC    RQCOMU3,RQCOMU3     AND IS BEING ADDED NOW                       
         JZ    XIT                 RETURN PROMPT FOR VERIFICATION               
         GOTOR (#ADDERR,AADDERR),DMCB,('ESREVIW',ERCOMU3)                       
                                                                                
NCP30    TM    CPYSTAT2,CPYTACP4   IF PUBLISHED MUSIC 4 ELEMENT DID             
         JO    XIT                 NOT EXIST ON RECORD PREVIOUSLY               
         OC    RQCOMU4,RQCOMU4     AND IS BEING ADDED NOW                       
         JZ    XIT                 RETURN PROMPT FOR VERIFICATION               
         GOTOR (#ADDERR,AADDERR),DMCB,('ESREVIW',ERCOMU4)                       
         J     XIT                                                              
                                                                                
***********************************************************************         
*        ERROR ENTRIES FOR CPYTACP AND NEWTACP                        *         
*        (LOOK AT ERRENTD FOR DSECT)                                  *         
***********************************************************************         
                                                                                
ERCOMU1  DC    AL1(ECOMU1X-*),AL2(107),AL1(ERRCATY2),AL1(D#COMU1)               
         DC    C'Review update to Music Code'                                   
ECOMU1X  EQU   *                                                                
                                                                                
ERCOMU2  DC    AL1(ECOMU2X-*),AL2(109),AL1(ERRCATY2),AL1(D#COMU2)               
         DC    C'Review update to Music Code'                                   
ECOMU2X  EQU   *                                                                
                                                                                
ERCOMU3  DC    AL1(ECOMU3X-*),AL2(111),AL1(ERRCATY2),AL1(D#COMU3)               
         DC    C'Review update to Music Code'                                   
ECOMU3X  EQU   *                                                                
                                                                                
ERCOMU4  DC    AL1(ECOMU4X-*),AL2(113),AL1(ERRCATY2),AL1(D#COMU4)               
         DC    C'Review update to Music Code'                                   
ECOMU4X  EQU   *                                                                
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        ADD VARIABLE LENGTH COMMENT ELEMENT                          *         
*        ON ENTRY ... R2=A(ELEM)                                      *         
*                     R3=A(COMMERCIAL RECORD)                         *         
***********************************************************************         
                                                                                
         USING TACMD,R2                                                         
ADDTACM  NTR1  BASE=*,LABEL=*                                                   
         OC    RQCOCMT,RQCOCMT     IF COMMENT IS PRESENT                        
         JZ    XIT                                                              
         XC    ELEM,ELEM           INITIALIZE COMMENT                           
         MVI   TACMEL,TACMELQ                                                   
         MVI   TACMLEN,TACMLNQ                                                  
         MVI   TACMTYPE,TACMTYPG   PUT COMMENT INTO ELEMENT                     
         MVC   TACMCOMM(L'RQCOCMT),RQCOCMT                                      
         GOTOR (#SETELEN,ASETELEN)                                              
         GOTO1 VHELLO,DMCB,(C'P',=C'TALFIL'),(R4),(R2),0                        
         J     XIT                                                              
         DROP  R2                                                               
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        ADD COMMERCIAL MUSIC ELEMENT                                 *         
*        ON ENTRY ... R2=A(ELEM)                                      *         
*                     R3=A(COMMERCIAL RECORD)                         *         
***********************************************************************         
                                                                                
ADDTACP  NTR1  BASE=*,LABEL=*                                                   
         LA    R6,RQCOMU1          R6=A(FIRST MUSIC CODE FIELD)                 
         LHI   R0,4                R0=MUSIC CODE FIELD COUNTER                  
                                                                                
         USING TACPD,R2                                                         
ACP10    OC    0(L'RQCOMU1,R6),0(R6)                                            
         JZ    ACP20               IF MUSIC CODE IS PRESENT                     
         XC    ELEM,ELEM           INITIALIZE ELEMENT                           
         MVI   TACPEL,TACPELQ                                                   
         MVI   TACPLEN,TACPLNQ                                                  
         MVC   TACPMUS,0(R6)       PUT CODE INTO ELEMENT                        
         GOTO1 VHELLO,DMCB,(C'P',=C'TALFIL'),(R4),(R2),0                        
         DROP  R2                                                               
                                                                                
ACP20    LA    R6,L'RQCOMU1(R6)                                                 
         BCT   R0,ACP10                                                         
         J     XIT                                                              
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        ADD COMMERCIAL STUDIO ELEMENT                                *         
*        ON ENTRY ... R2=A(ELEM)                                      *         
*                     R4=A(COMMERCIAL RECORD)                         *         
***********************************************************************         
                                                                                
         USING TACSD,R2                                                         
ADDTACS  NTR1  BASE=*,LABEL=*                                                   
         OC    RQCOFDT,RQCOFDT     IF FILMING INFO IS PRESENT                   
         JZ    ACS10                                                            
         XC    ELEM,ELEM           INITIALIZE STUDIO ELEMENT                    
         MVI   TACSEL,TACSELQ                                                   
         MVI   TACSLEN,TACSLNQ                                                  
         MVI   TACSTYPE,TACSTYPF   PUT TYPE                                     
         MVC   TACSDATE,RQCOFDT    DATE                                         
         MVC   TACSSTUD,RQCOFSU    STUDIO                                       
         MVC   TACSCITY,RQCOFCY    CITY                                         
         MVC   TACSSTAT,RQCOFST    AND STATE INTO ELEMENT                       
         GOTO1 VHELLO,DMCB,(C'P',=C'TALFIL'),(R4),(R2),0                        
                                                                                
ACS10    OC    RQCORDT,RQCORDT     IF RECORDING INFO IS PRESENT                 
         JZ    ACS20                                                            
         XC    ELEM,ELEM           INITIALIZE STUDIO ELEMENT                    
         MVI   TACSEL,TACSELQ                                                   
         MVI   TACSLEN,TACSLNQ                                                  
         MVI   TACSTYPE,TACSTYPR   PUT TYPE                                     
         MVC   TACSDATE,RQCORDT    DATE                                         
         MVC   TACSSTUD,RQCORSU    STUDIO                                       
         MVC   TACSCITY,RQCORCY    CITY                                         
         MVC   TACSSTAT,RQCORST    AND STATE INTO ELEMENT                       
         GOTO1 VHELLO,DMCB,(C'P',=C'TALFIL'),(R4),(R2),0                        
                                                                                
ACS20    OC    RQCOMDT,RQCOMDT     IF MUSIC INFO IS PRESENT                     
         JZ    XIT                                                              
         XC    ELEM,ELEM           INITIALIZE STUDIO ELEMENT                    
         MVI   TACSEL,TACSELQ                                                   
         MVI   TACSLEN,TACSLNQ                                                  
         MVI   TACSTYPE,TACSTYPM   PUT TYPE                                     
         MVC   TACSDATE,RQCOMDT    DATE                                         
         MVC   TACSSTUD,RQCOMSU    STUDIO                                       
         MVC   TACSCITY,RQCOMCY    CITY                                         
         MVC   TACSSTAT,RQCOMST    AND STATE INTO ELEMENT                       
         GOTO1 VHELLO,DMCB,(C'P',=C'TALFIL'),(R4),(R2),0                        
         J     XIT                                                              
         DROP  R2                                                               
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        ADD NAME ELEMENT                                             *         
*        ON ENTRY ... R2=A(ELEM)                                      *         
*                     R4=A(COMMERCIAL RECORD)                         *         
***********************************************************************         
                                                                                
         USING TANAD,R2                                                         
ADDTANA  NTR1  BASE=*,LABEL=*                                                   
         OC    RQCOTIT,RQCOTIT     IF TITLE IS PRESENT                          
         JZ    XIT                                                              
         XC    ELEM,ELEM           INITIALIZE ELEMENT                           
         MVI   TANAEL,TANAELQ                                                   
         MVC   TANANAME,RQCOTIT    PUT TITLE INTO ELEMENT                       
         GOTOR (#SETELEN,ASETELEN) AND ADD IT                                   
         GOTO1 VHELLO,DMCB,(C'P',=C'TALFIL'),(R4),(R2),0                        
         J     XIT                                                              
         DROP  R2                                                               
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
