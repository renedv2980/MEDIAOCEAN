*          DATA SET WHTEST1    AT LEVEL 053 AS OF 02/26/99                      
***********************************                                             
****** FROM SPNWS00A                                                            
***********************************                                             
         MVI   NBRKTYP,NBRKTYPQ                                                 
         MVI   NBRKSTY,NBRKSTYQ                                                 
         MVC   NBRKAGMD,BAGYMD                                                  
         MVC   NBRKBYR,BBYR                                                     
         MVC   NBRKSEQ,BCMSEQ                                                   
         MVC   NBRKSTA,BUYMSTA+2                                                
         MVC   NBRKKBUY,BUYKBUY-BUYKEY+APRECKEY                                 
         MVI   NBRLEN+1,NBRFSTEL-NBRKEY                                         
         MVC   20(2,R2),QAGY       AGENCY POWER CODE                            
*                                                                               
         LA    R4,APELEM                                                        
         XC    APELEM,APELEM                                                    
         USING NBRSELD,R4                                                       
         MVI   NBRSEL,NBRSELQ                                                   
         MVI   NBRSLN,NBRSLNQ                                                   
         MVC   NBRSBYLN,BUYKBUY                                                 
*   NO PACKAGE/ORBIT NUMBER                                                     
         MVC   NBRSDAYS,BDDAY                                                   
         GOTO1 VMSUNPK,RPARM,(X'80',BUYMSTA),ACWORK,NBRSSTA                     
*                                                                               
         CLI   NBRSSTA+4,C' '                                                   
         BNE   *+16                                                             
         CLI   QMED,C'T'                                                        
         BNE   *+8                                                              
         MVI   NBRSSTA+4,C'T'                                                   
*                                                                               
         MVC   NBRSTIMS(L'TBUYTIMS*2),BDTIMST    TIMES                          
         MVC   NBRSDYPT,BDDAYPT    DAYPART                                      
         MVC   NBRSSLN,BDSEC       SPOT LENGTH                                  
         ZICM  RF,BDCOST,3                                                      
         TM    BDCIND2,X'20'       CANADIAN AGENCY BUY?                         
         BNZ   SNBR10              YES                                          
         TM    BDCIND2,X'10'       COST IN US DOLLARS                           
         BZ    SNBR10                                                           
         MHI   RF,100                                                           
SNBR10   STCM  RF,15,NBRSCST1                                                   
         MVC   NBRSPROG,BDPROGRM   PROGRAM                                      
         MVC   NBRSADJC,BDPROGT    ADJACENCY CODE                               
         GOTO1 AADDELS,NBRRECD                                                  
***********************************                                             
* NOW FOR THE DEMOS                                                             
***********************************                                             
         XC    ACFULL,ACFULL                                                    
         LA    R1,BDELEM                                                        
         SR    R0,R0                                                            
SNBR17   CLI   0(R1),0                                                          
         BE    SNBR20                                                           
         CLI   0(R1),X'02'         ORIGINAL DEMO ELEMENT                        
         BE    *+14                                                             
         IC    R0,1(R1)                                                         
         AR    R1,R0                                                            
         B     SNBR17                                                           
*                                                                               
         XC    ACWORK,ACWORK                                                    
         LA    RE,ESTDEMS+3                                                     
         LA    R0,ESTDEMS+L'ESTDEMS                                             
         LA    RF,ACWORK+3                                                      
         MVC   ACWORK(3),ESTDEMS                                                
*                                                                               
SNBR17A  MVC   0(3,RF),0(RE)       NO                                           
         LA    RF,3(RF)            BUMP TO NEXT ENTRY IN OUR LIST               
         LA    RE,3(RE)            BUMP TO NEXT ENTRY IN EST LIST               
         OC    0(3,RE),0(RE)                                                    
         BZ    SNBR17X                                                          
         CR    RE,R0                                                            
         BL    SNBR17A                                                          
*                                                                               
SNBR17X  DS    0H                                                               
         LA    R4,APELEM                                                        
         XC    APELEM,APELEM                                                    
         USING NBRDMELD,R4                                                      
         MVI   NBRDMEL,NBRDMELQ                                                 
         LA    R5,NBRDMDMO                                                      
*                                                                               
         LA    R6,ACWORK           OUR DEMO LIST                                
         LR    R0,R1               SAVE A(DEMO ELEMENT)                         
SNBR18   LR    R1,R0               R1 = A(DEMO ELEMENT)                         
         LA    RE,8                RE = L'DEMO                                  
         ZIC   RF,1(R1)            RF = A(LAST BYTE IN DEMO ELEMENT)            
         AR    RF,R1                                                            
         BCTR  RF,0                                                             
         LA    R1,NDEMNO-NDELEM(R1)  R1 = A(1ST DEMO ENTRY)                     
         MVC   0(3,R5),0(R6)       COPY DEMO CATEGORY                           
*                                                                               
SNBR19   CLC   0(3,R1),0(R6)                                                    
         BNE   *+10                                                             
         MVC   3(5,R5),3(R1)       COPY THE RATING                              
         BXLE  R1,RE,SNBR19                                                     
*                                                                               
         LA    R5,L'NBRDMDMO(R5)                                                
         LA    R6,3(R6)                                                         
         LA    RE,NBRDMDMO+8*L'NBRDMDMO                                         
         CR    R5,RE               PAST OUR 8 DEMOS WE CAN STORE?               
         BNL   *+14                YES                                          
         OC    0(3,R6),0(R6)                                                    
         BNZ   SNBR18                                                           
*                                                                               
         LR    R1,R5                                                            
         SR    R1,R4                                                            
         STC   R1,NBRDMLEN                                                      
***      BCTR  R1,0                SAVE DEMO ELEMENT                            
***      EX    R1,*+8                                                           
***      B     *+10                                                             
***      MVC   LDMOEL(0),APELEM                                                 
         GOTO1 AADDELS,NBRRECD                                                  
***********************************                                             
* NOW FOR THE SPOTS                                                             
***********************************                                             
SNBR20   DS    0H                                                               
         LA    R6,BDELEM                                                        
         USING REGELEM,R6                                                       
         LA    R4,APELEM                                                        
         XC    APELEM,APELEM                                                    
         USING NBRSPELD,R4                                                      
         MVI   NBRSPEL,NBRSPELQ                                                 
******   MVC   NBRSPDAT,CMPDATSP   <=== NO NEED FOR THIS YET                    
*                                                                               
SNBR30   CLI   0(R6),0             END OF RECORD?                               
         BE    SNBR60              THAT'S IT!                                   
*                                                                               
SNBR32   CLI   0(R6),X'06'         NON-POL SPOT ELEMENT?                        
         BL    SNBR50                                                           
         CLI   0(R6),X'07'                                                      
         BNH   SNBR40                                                           
*                                                                               
         CLI   0(R6),X'0B'         POL SPOT ELEMENT?                            
         BL    SNBR50                                                           
         CLI   0(R6),X'0C'                                                      
         BH    SNBR50                                                           
*                                                                               
         CLC   RPPRD,BPRD          MATCHES ON THE CAMPAIGN PRODUCT?             
         BE    SNBR34              YES                                          
         CLI   BPRD,X'FF'          POL IN THE CAMPAIGN DEFINITION?              
         BE    SNBR34              YES, WHEN BUILDING, TAKE ALL OF THEM         
*                                                                               
SNBR33   CLI   CMPPRD2,0           NO, MAKE SURE NOT PR2-PR1                    
         BE    SNBR50                  CAN'T BE                                 
         CLC   RPPRD,CMPPRD2                                                    
         BNE   SNBR50                  CAN'T BE                                 
         CLI   RLEN,RPSTAT2+L'RPSTAT2-REGELEM   PIGGYBACK IN ELEMENT?           
         BNH   SNBR50                           NO                              
         CLC   RPPRD+L'RPALLOC,BPRD                                             
         BNE   SNBR50                                                           
         B     SNBR40                                                           
*                                                                               
SNBR34   CLI   CMPPRD2,0           ANY CAMPAIGN PIGGYBACK PRODUCT?              
         BNE   SNBR34A             YES, MAKE SURE THERE IS A PB IN ELEM         
         CLI   RLEN,RPSTAT2+L'RPSTAT2-REGELEM   PIGGYBACK IN ELEMENT?           
         BH    SNBR50                           YES, SHOULDN'T BE               
         B     SNBR40                                                           
*                                                                               
SNBR34A  CLI   RLEN,RPSTAT2+L'RPSTAT2-REGELEM   PIGGYBACK IN ELEMENT?           
         BNH   SNBR50                           NO                              
         CLC   RPPRD+L'RPALLOC,CMPPRD2          MATCH CAMP PIGGYBACK?           
         BNE   SNBR50                           NOPE                            
*                                                                               
SNBR40   LA    R1,CMPDATSP         LIST OF FLIGHT DATES                         
SNBR42   OC    0(2,R1),0(R1)                                                    
         BZ    SNBR50              NO MATCH IN FLIGHT DATES, NEXT SPOT          
*                                                                               
         CLC   RDATE,0(R1)         SPOT DATE IS IN THIS FLIGHT WEEK?            
         BL    *+14                                                             
         CLC   RDATE,2(R1)                                                      
         BNH   SNBR44                                                           
         LA    R1,4(R1)            CHECK NEXT FLIGHT WEEK                       
         B     SNBR42                                                           
*                                                                               
SNBR44   LR    RE,R1               RE = NTH DISPLACED WEEK IN SCHEDULE          
         LA    R0,CMPDATSP                                                      
         SR    RE,R0                                                            
         SRL   RE,2                DIVIDE BY 4                                  
         LA    RF,NBRSPSPW(RE)                                                  
*                                                                               
         LA    R0,1                                                             
         CLI   RCODE,X'0B'         POOL SPOT?                                   
         BNL   *+8                                                              
         IC    R0,RNUM                                                          
         TM    RSTATUS,X'80'       MINUS SPOT?                                  
         BZ    *+6                                                              
         LNR   R0,R0               YES, NEGATE TO SUBTRACT                      
*                                                                               
SNBR48   ZIC   RE,0(RF)            BUMP UP THE SPOT COUNT                       
         AR    RE,R0                                                            
         CHI   RE,256                                                           
         BL    *+6                                                              
         DC    H'0'                DIE, MORE THAN 255 SPOTS IN WEEK             
         CHI   RE,0                                                             
         BNL   *+6                                                              
         XR    RE,RE               CAN'T HANDLE NEGATIVE SPOTS YET              
         STC   RE,0(RF)                                                         
*                                                                               
SNBR49   CLI   RCODE,X'0B'                                                      
         BL    SNBR50                                                           
         TM    RSTATUS,X'20'       COST OVERRIDE?                               
         BZ    SNBR50                                                           
         BAS   RE,ADDOVRDE         ADD COST OVERRIDE TO NBR RECORD              
*                                                                               
SNBR50   XR    R0,R0               NEXT SPOT ELEMENT                            
         IC    R0,1(R6)                                                         
         AR    R6,R0                                                            
         B     SNBR30                                                           
*                                                                               
SNBR60   XR    RE,RE                                                            
         IC    RE,CMPNWKS                                                       
         LA    RE,NBRSPSPW-NBRSPELD(RE)                                         
         STC   RE,NBRSPLEN                                                      
***      BCTR  RE,0                SAVE SPOT/WEEK ELEMENT                       
***      EX    RE,*+8                                                           
***      B     *+10                                                             
***      MVC   LSPWEL(0),APELEM                                                 
         GOTO1 AADDELS,NBRRECD                                                  
         DROP  R6                                                               
***********************************                                             
* NOW FOR THE COMMENTS                                                          
***********************************                                             
         LA    R6,BDELEM                                                        
SNBR70   CLI   0(R6),0             END OF RECORD?                               
         BE    SNBR80              THAT'S IT!                                   
*                                                                               
SNBR72   CLI   0(R6),X'66'         COMMENT ELEMENT?                             
         BNE   SNBR79                                                           
*                                                                               
         XC    APELEM,APELEM       COPY THE BUY COMMENTS                        
         ZIC   R1,1(R6)                                                         
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   APELEM(0),0(R6)                                                  
         MVI   APELEM,NBRCMELQ                                                  
*                                                                               
         GOTO1 AADDELS,NBRRECD                                                  
*                                                                               
SNBR79   ZIC   R0,1(R6)                                                         
         AR    R6,R0                                                            
         B     SNBR70                                                           
***********************************                                             
* NOW FOR THE UPGRADE                                                           
***********************************                                             
SNBR80   LA    R6,BDELEM                                                        
SNBR80A  CLI   0(R6),0             END OF RECORD?                               
         BE    SNBR90              THAT'S IT!                                   
*                                                                               
SNBR82   CLI   0(R6),X'62'         UPGRADE ELEMENT?                             
         BNE   SNBR89                                                           
*                                                                               
         USING UPELEM,R6                                                        
         LA    R1,NBRFSTEL                                                      
         USING NBRSELD,R1                                                       
         MVC   NBRSBOOK,UPFBK                                                   
*                                                                               
         MVI   NBRSUPUT,0                                                       
         CLI   UP2YRP,C'N'                                                      
         BNE   *+8                                                              
         MVI   NBRSUPUT,1                                                       
         CLI   UP2YRP,C'Y'                                                      
         BNE   *+8                                                              
         MVI   NBRSUPUT,2                                                       
*                                                                               
         MVI   NBRSUSHR,0                                                       
         CLI   UP2YRS,C'N'                                                      
         BNE   *+8                                                              
         MVI   NBRSUSHR,1                                                       
         CLI   UP2YRS,C'Y'                                                      
         BNE   *+8                                                              
         MVI   NBRSUSHR,2                                                       
         DROP  R1                                                               
*                                                                               
         OC    UPSTA(L'UPSTA+L'UPDAYTIM),UPSTA   ANY OVERRIDE D/T, STA?         
         BZ    SNBR84                            NONE                           
         LA    R4,APELEM                                                        
         USING NBRODELD,R4                                                      
         XC    APELEM,APELEM       MAKE AN OVERRIDE ELEMENT                     
         MVI   NBRODEL,NBRODELQ                                                 
         MVI   NBRODLEN,NBRODLNQ                                                
         MVC   NBRODODY(L'UPDAYTIM),UPDAYTIM                                    
****     MVC   NBRODSTA,UPSTA                                                   
         GOTO1 AADDELS,NBRRECD                                                  
*                                                                               
SNBR84   LA    R4,APELEM                                                        
         USING NBRUPELD,R4                                                      
         XC    APELEM,APELEM       MAKE AN UPGRADE ELEMENT                      
         MVI   NBRUPEL,NBRUPELQ                                                 
         MVI   NBRUPLEN,NBRUPLNQ                                                
         MVC   NBRUPFIL,UPFILE                                                  
         MVC   NBRUPEXP,UPTYPE                                                  
         MVC   NBRUPOBK,UPFBK                                                   
**********************************                                              
***  SHOULD BE SAFE TO USE APPARM HERE NOW                                      
**********************************                                              
         GOTO1 AFMTUPG,APPARM,UPELEM,APELEM,DBLOCK                              
         MVC   NBRUPBKL,UPFBKLST           BOOKLIST                             
*                                                                               
         GOTO1 AADDELS,NBRRECD                                                  
         B     SNBR90                                                           
*                                                                               
SNBR89   ZIC   R0,1(R6)                                                         
         AR    R6,R0                                                            
         B     SNBR80A                                                          
         DROP  R6                                                               
*                                                                               
SNBR90   DS    0H                                                               
*                                                                               
SNBRX    XIT1                                                                   
         DROP  R2,R3,R4,R8                                                      
         EJECT                                                                  
***********************************************************************         
* ADD COST OVERRIDE ELEMENT TO BUY REVISION RECORD                              
*                                                                               
* ON ENTRY: (R6)                   A(POL SPOT ELEMENT)                          
*           (R3)                   A(BUY REVISION RECORD)                       
*           (R2)                   A(BUY RECORD)                                
*                                                                               
* WARNING!  ACWORK GETS CLOBBERED                                               
***********************************************************************         
ADDOVRDE NTR1                                                                   
*  SAVE SPOTS PER WEEK ELEMENT IN ACWORK SO WE CAN RESTORE LATER                
         MVC   ACWORK(NMAXWKS+NBRSPSPW-NBRSPELD),APELEM                         
         USING REGELEM,R6                                                       
         LA    R1,APELEM                                                        
         USING NBRCOELD,R1                                                      
         XC    APELEM,APELEM                                                    
         MVI   NBRCOEL,NBRCOELQ                                                 
         MVI   NBRCOLEN,NBRCOLNQ                                                
         MVC   NBRCODAT,RDATE                                                   
         XR    R0,R0                                                            
         ICM   R0,7,RPCOST                                                      
         LR    RF,R2                                                            
         AHI   RF,BDCIND2-BUYKEY                                                
         TM    0(RF),X'20'         CANADIAN AGENCY?                             
         BNZ   AOVR10                                                           
         TM    0(RF),X'10'         COST IN US DOLLARS?                          
         BZ    AOVR10                                                           
         MHI   R0,100                                                           
AOVR10   STCM  R0,15,NBRCOCST                                                   
         GOTO1 AADDELS,(R3)                                                     
*  RESTORE SPOTS PER WEEK ELEMENT FROM ACWORK SO WE CAN CONTINUE                
         MVC   APELEM(NMAXWKS+NBRSPSPW-NBRSPELD),ACWORK                         
AOVRX    B     SNBRX                                                            
         DROP  R1,R6                                                            
         LTORG                                                                  
