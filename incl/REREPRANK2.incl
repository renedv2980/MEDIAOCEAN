*          DATA SET REREPRANK2 AT LEVEL 009 AS OF 05/01/02                      
*              ROUTINE TO ADJUST MONTH TABLE FOR YTD FIGURES                    
         SPACE 3                                                                
ADJMONTH NTR1                                                                   
         LA    R4,1                                                             
         CLC   QSTART+2(2),=C'01'                                               
         BE    XIT                                                              
         L     R2,MONTABLE                                                      
         LA    R3,24                                                            
         SPACE 2                                                                
ADJMON2  CLI   0(R2),0             FIND THE FIRST EMPTY LINE                    
         BE    ADJMON4                                                          
         LA    R2,40(R2)                                                        
         BCT   R3,ADJMON2                                                       
         SPACE 2                                                                
ADJMON4  CVD   R4,DUB              NOW PUT IN YTD BUCKETS                       
         UNPK  WORK(2),DUB                                                      
         OI    WORK+1,X'F0'                                                     
         CLC   QSTART+2(2),WORK                                                 
         BE    XIT                                                              
         MVC   0(2,R2),QSTART                                                   
         MVC   2(2,R2),WORK                                                     
         MVC   4(2,R2),=C'01'                                                   
         OI    6(R2),X'80'                                                      
         LA    R4,1(R4)                                                         
         LA    R2,40(R2)                                                        
         BCT   R3,ADJMON4                                                       
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO PUT THE VALUES FROM MONTH TABLE TO POST LINE          
         SPACE 3                                                                
POSTVALS NTR1                                                                   
         XC    WORK+28(32),WORK+28                                              
         L     R2,MONTABLE                                                      
         LA    R3,24                                                            
         SPACE 2                                                                
POSTVAL2 CLI   0(R2),0                                                          
         BE    XIT                                                              
         LA    R4,WORK+36                                                       
         BAS   RE,POSTVAL6                                                      
         TM    6(R2),X'80'                                                      
         BO    POSTVAL4                                                         
         LA    R4,WORK+28                                                       
         BAS   RE,POSTVAL6                                                      
         SPACE 2                                                                
POSTVAL4 LA    R2,40(R2)                                                        
         BCT   R3,POSTVAL2                                                      
         B     XIT                                                              
*                                  CURRENT BUCKET                               
POSTVAL6 DS    0H                                                               
         L     R5,12(R2)           GET CURRENT INVOICE AMOUNT                   
         TM    6(R2),X'01'         TEST FOR ANY CURRENT INVOICE DATA            
         BO    *+8                 IF NONE, THEN                                
         L     R5,8(R2)            GET CURRENT ESTIMATE AMOUNT                  
         A     R5,0(R4)                                                         
         ST    R5,0(R4)                                                         
         ST    R5,16(R4)                                                        
         SPACE 2                                                                
*                                  PRIOR BUCKET                                 
         CLI   QOPTION3,C'A'       TEST IF PRIOR ACTUAL WANTED                  
         BNE   POSTVAL8                                                         
         L     R5,36(R2)           GET PRIOR INVOICE AMOUNT                     
         TM    6(R2),X'02'         TEST FOR ANY PRIOR INVOICE AMOUNT            
         BO    *+8                 IF NONE, THEN                                
         L     R5,32(R2)           GET PRIOR ESTIMATE AMOUNT                    
         A     R5,4(R4)                                                         
         ST    R5,4(R4)                                                         
         ST    R5,20(R4)           TOTAL PRIOR                                  
         BR    RE                                                               
         SPACE 1                                                                
*                                  USE AS AT PRIOR                              
POSTVAL8 L     R5,24(R2)           PRIOR AS AT INVOICE AMT                      
         TM    6(R2),X'04'         TEST FOR ANY PRIOR AS AT INV AMT             
         BO    *+8                 IF NONE,THEN                                 
         L     R5,20(R2)           GET PRIOR AS AT ESTIMATE                     
         A     R5,4(R4)                                                         
         ST    R5,4(R4)                                                         
         ST    R5,20(R4)                                                        
         BR    RE                                                               
         EJECT                                                                  
