*          DATA SET T20E03     AT LEVEL 006 AS OF 10/04/83                      
         TITLE 'LIST ACTIVE STATIONS'                                           
T20E03   CSECT                                                                  
         NMOD1 0,T20E03                                                         
         USING GENOLD,RC                                                        
         USING T20EFFD,RA                                                       
         USING FLDHDRD,R2                                                       
         L     RC,0(R1)                                                         
         XC    DATALIN,DATALIN                                                  
         LA    R2,DEMLAST                                                       
         SR    RE,RE                                                            
         IC    RE,0(R2)            INSERT FIELD LENGTH                          
         AR    R2,RE               SET NEXT FIELD ADDRESS                       
         MVC   FLDDATA+8(16),=C'STATIONS ON BOOK'                               
         FOUT  (R2)                                                             
         SR    RE,RE                                                            
         IC    RE,0(R2)            INSERT FIELD LENGTH                          
         AR    R2,RE               SET NEXT FIELD ADDRESS                       
         SR    RE,RE                                                            
         IC    RE,0(R2)            INSERT FIELD LENGTH                          
         AR    R2,RE               SET NEXT FIELD ADDRESS                       
*                                                                               
*        GET STATIONS                                                           
*                                                                               
         LA    RE,WORKEND          CLEAR WORK AREA                              
         LA    RF,1200                                                          
         XCEF                                                                   
         LA    R5,WORKEND          SET WORK ADDRESS                             
         LA    R6,18               SET NUMBER OF LINES AVAILABLE                
         LA    R7,0                SET TABLE DISPLACEMENT                       
         LA    R8,13               SET NUMBER OF COLUMNS                        
*                                                                               
*        SET FIRST KEY                                                          
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(2),=C'RT'                                                    
         MVI   KEY+2,C'A'          SET RATING SERVICE                           
         CLI   CSOURCE,1                                                        
         BE    *+8                                                              
         MVI   KEY+2,C'N'                                                       
         MVC   KEY+5(5),STAT       SET START STATION                            
         SR    RF,RF               SET REQUESTED BOOK                           
         MVC   HALF(1),B1                                                       
         OC    HALF(1),B2                                                       
         MVC   KEY+3(1),HALF                                                    
*                                                                               
*        READ DEMO FILE DIRECTORY AND BUILD A LIST OF STATIONS                  
*                                                                               
DEMREAD  MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,=C'DMRDHI',=C'DEMDIR',KEYSAVE,KEY,        X        
               (TERMNAL,0)                                                      
         TM    8(R1),X'FF'                                                      
         BNZ   FOUTSTA             EOF - SEND STATIONS                          
         CLC   KEY(4),KEYSAVE                                                   
         BNE   FOUTSTA             END BOOK - SEND STATIONS                     
         MVC   0(4,R5),KEY+5       MOVE STATION TO TABLE                        
         LA    R5,80(R5)           BUMP TO NEXT LINE                            
         MVC   STAT,KEY+5          SAVE LAST                                    
         MVI   KEY+9,X'FF'         NEXT STATION                                 
         BCT   R6,DEMREAD                                                       
         LA    R7,6(R7)            SET NEW COLUMN                               
         LA    R5,WORKEND                                                       
         AR    R5,R7                                                            
         LA    R6,18                                                            
         BCT   R8,DEMREAD          UP TO 10 COLUMNS ON SCREEN                   
         EJECT                                                                  
*                                                                               
*        STATION TABLE HAS BEEN BUILT - NOW SEND THEM                           
*                                                                               
FOUTSTA  LA    R5,WORKEND          SET LINE AREA                                
         LA    R6,18               SET LOOP CONTROL                             
FOUTSTA1 MVC   FLDDATA(79),0(R5)                                                
         FOUT  (R2)                                                             
         SR    RE,RE                                                            
         IC    RE,0(R2)            INSERT FIELD LENGTH                          
         AR    R2,RE               SET NEXT FIELD ADDRESS                       
         LA    R5,80(R5)                                                        
         BCT   R6,FOUTSTA1                                                      
*                                                                               
EXIT1    LA    R2,DEMSTAH                                                       
         MVC   FLDDATA(5),STAT     SET NEXT START STATION                       
         FOUT  (R2)                                                             
         OI    FLDOIND,1                                                        
         LA    R2,DEMPAGH                                                       
         OI    FLDOIND,X'40'       POSITION CURSOR                              
         B     EXXMOD                                                           
         EJECT                                                                  
       ++INCLUDE GENEROL                                                        
         LTORG                                                                  
       ++INCLUDE GENOLD                                                         
       ++INCLUDE T20EWORK                                                       
