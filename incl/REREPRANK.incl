*          DATA SET REREPRANK  AT LEVEL 032 AS OF 05/01/02                      
*              TEST MODE SETTINGS                                               
         SPACE 3                                                                
TESTMODE CLI   MODE,REQFRST                                                     
         BNE   MD2                                                              
         MVI   RCSUBPRG,0                                                       
         BAS   RE,CLEARTAB                                                      
         BAS   RE,ADJMONTH                                                      
         MVC   PAGE,=H'1'                                                       
         MVI   FORCEHED,C'Y'                                                    
         B     MDEXT                                                            
         SPACE 2                                                                
MD2      CLI   MODE,OFFLAST                                                     
         BNE   MD4                                                              
         CLI   QSEQ,C'S'                                                        
         BE    MDEXT                                                            
         B     MD6                                                              
         SPACE 2                                                                
MD4      CLI   MODE,STALAST                                                     
         BNE   MD8                                                              
         CLI   QSEQ,C'S'                                                        
         BNE   MDEXT                                                            
         SPACE 2                                                                
MD6      LA    R1,ACCYDCUR-ACCUMD                                               
         BAS   RE,ANYDATA          IF NO DATA -- PRINT NOTHING                  
         BZ    MDEXT                                                            
         BAS   RE,RANKDETS                                                      
         BAS   RE,PRNTDETS                                                      
         B     MDEXT                                                            
         SPACE 2                                                                
MD8      CLI   MODE,REQLAST                                                     
         BNE   MDEXT                                                            
         CLI   QSEQ,C'S'           TEST FOR STATION ORDER                       
         BNE   MD9                                                              
         CLC   QSTATION,SPACES     TEST FOR 1 STATION REQUEST                   
         BNE   MDEXT               DON'T PRINT TOTALS                           
         B     MD10                                                             
*                                                                               
*                                  WE HAVE OFFICE ORDER                         
MD9      CLC   QOFFICE,SPACES      TEST FOR OFFICE REQUEST                      
         BNE   MDEXT                                                            
MD10     LA    R1,ACCYTCUR-ACCUMD                                               
         BAS   RE,ANYDATA                                                       
         BZ    MDEXT               PRINT NOTHING IF NO INFO AT ALL              
         BAS   RE,RANKTOTS                                                      
         BAS   RE,PRNTTOTS                                                      
         SPACE 1                                                                
         MVI   RCSUBPRG,2                                                       
         GOTO1 PRINT,P1,SPACES-1,=C'BC09'                                       
         MVI   FORCEMID,C'Y'                                                    
         GOTO1 REPORT                                                           
         SPACE 2                                                                
MDEXT    XMOD1 1                                                                
         EJECT                                                                  
       ++INCLUDE REREPRANK2                                                     
*              ROUTINE TO CLEAR TABLE                                           
         SPACE 3                                                                
CLEARTAB NTR1                                                                   
         L     R2,=A(ANTABLE)                                                   
         AR    R2,RB                                                            
         L     R3,0(R2)                                                         
         LA    R2,4(R2)                                                         
         SPACE 2                                                                
CLEARTB2 MVI   0(R2),X'FF'                                                      
         XC    1(59,R2),1(R2)                                                   
         LA    R2,60(R2)                                                        
         BCT   R3,CLEARTB2                                                      
         XC    0(120,R2),0(R2)                                                  
         MVC   2(26,R2),=CL26'      OTHER AGENCIES'                             
         CLI   QPROG,C'6' '                                                     
         BNE   *+10                                                             
         MVC   2(26,R2),=CL26'      OTHER ADVERTISERS'                          
         MVC   62(26,R2),=C'      ** TOTALS **'                                 
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO SEE IF THIS CAN BE POSTED (WITHOUT NAME)              
         SPACE 3                                                                
CANPOST  NTR1                                                                   
         L     R2,=A(ANTABLE)                                                   
         AR    R2,RB                                                            
         L     R3,0(R2)                                                         
         LA    R2,4(R2)                                                         
         SPACE 2                                                                
CANPOST2 CLC   2(6,R2),WORK+2                                                   
         BE    CANYES                                                           
         CLI   0(R2),X'FF'                                                      
         BE    CANNO                                                            
         LA    R2,60(R2)                                                        
         BCT   R3,CANPOST2                                                      
         SPACE 2                                                                
CANYES   SR    R2,R2                                                            
         LTR   R2,R2                                                            
         B     XIT                                                              
         SPACE 2                                                                
CANNO    LA    R2,1                                                             
         LTR   R2,R2                                                            
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO POST A LINE                                           
*              ANY NEW ENTRIES WILL NOW HAVE NAME AS WELL                       
         SPACE 2                                                                
POSTLINE NTR1                                                                   
         L     R2,=A(ANTABLE)                                                   
         AR    R2,RB                                                            
         L     R3,0(R2)                                                         
         LA    R2,4(R2)                                                         
         USING ACCUMD,R2                                                        
         SPACE 2                                                                
POSTL2   CLC   ACCCODE,WORK+2      CODE IS ALREADY THERE                        
         BNE   POSTL4                                                           
         BAS   RE,POSTADD          ADD IN                                       
         B     POSTL8                                                           
         SPACE 2                                                                
POSTL4   CLI   ACCRANK,X'FF'       THIS IS A NEW ONE                            
         BNE   POSTL6                                                           
         XC    ACCLINE,ACCLINE     CLEAR                                        
         MVC   ACCCODE(26),WORK+2  PUT IN DESCRIPTION                           
         BAS   RE,POSTADD          AND ADD                                      
         B     POSTL8                                                           
         SPACE 2                                                                
POSTL6   LA    R2,60(R2)                                                        
         BCT   R3,POSTL2                                                        
         BAS   RE,POSTADD          RUN OUT OF LINES SO ADD TO OTHERS            
         B     POSTL10                                                          
         SPACE 2                                                                
POSTL8   LA    R2,60(R2)                                                        
         BCT   R3,POSTL8                                                        
         SPACE 2                                                                
POSTL10  LA    R2,60(R2)           ADD THEM ALL TO TOTALS LINE                  
         BAS   RE,POSTADD                                                       
         B     XIT                                                              
         SPACE 2                                                                
POSTADD  LA    R4,WORK+28                                                       
         LA    R5,ACCPDCUR                                                      
         LA    R6,8                                                             
         SPACE 2                                                                
POSTADD2 L     R7,0(R4)                                                         
         A     R7,0(R5)                                                         
         ST    R7,0(R5)                                                         
         LA    R4,4(R4)                                                         
         LA    R5,4(R5)                                                         
         BCT   R6,POSTADD2                                                      
         BR    RE                                                               
         EJECT                                                                  
*              ROUTINES TO RANK TABLE                                           
         SPACE 3                                                                
RANKDETS NTR1                                                                   
         LA    R4,36               DISPLACE TO CURRENT YTD (DETAIL)             
         B     RANKALL                                                          
         SPACE 2                                                                
RANKTOTS NTR1                                                                   
         LA    R4,52               DISPLACE TO CURRENT YTD (TOTAL)              
         SPACE 2                                                                
RANKALL  L     R2,=A(ANTABLE)                                                   
         AR    R2,RB                                                            
         L     R3,0(R2)                                                         
         LA    R2,4(R2)                                                         
         SPACE 1                                                                
* MOVE ALL APPLICABLE DETAIL LINES TO TOP OF ANTABLE                            
         LR    R6,R2               R2=PNTR1, R6=PNTR2                           
         SR    R1,R1                                                            
RANK10   LA    R7,0(R4,R6)                                                      
         OC    0(8,R7),0(R7)       TEST ANY YTD DATA                            
         BZ    RANK14              NO-POINT TO NEXT                             
         CR    R2,R6               YES-ARE R2,R6 AT SAME ENTRY                  
         BE    RANK12              --YES-POINT BOTH TO NEXT                     
         XC    0(60,R2),0(R6)      --NO-SWAP FIELDS                             
         XC    0(60,R6),0(R2)                                                   
         XC    0(60,R2),0(R6)                                                   
RANK12   LA    R2,60(R2)                                                        
         LA    R1,1(R1)            UPDATE COUNTER, GOOD ENTRY FOUND             
RANK14   LA    R6,60(R6)                                                        
         CLI   0(R6),X'FF'         TEST FOR LAST ENTRY                          
         BE    *+8                                                              
         BCT   R3,RANK10                                                        
         SPACE 2                                                                
*                        CHANGE HIGH ORDER BIT FOR XSORT                        
         L     R2,=A(ANTABLE)                                                   
         AR    R2,RB                                                            
         LA    R2,4(R2)                                                         
         LR    R6,R2               TABLE                                        
         LR    R5,R1               NUMBER OF ENTRIES                            
         LA    R7,0(R4,R6)                                                      
*                                                                               
         XI    0(R7),X'80'                                                      
         LA    R7,60(R7)                                                        
         BCT   R5,*-8                                                           
         SPACE 1                                                                
         LR    R3,R1                                                            
         GOTO1 XSORT,DMCB,(1,(R2)),(R3),60,8,(R4)                               
         SPACE 1                                                                
*                        RESET HIGH ORDER BIT                                   
         LR    R5,R3                                                            
         LR    R6,R2                                                            
         LA    R7,0(R4,R6)                                                      
*                                                                               
         XI    0(R7),X'80'                                                      
         LA    R7,60(R7)                                                        
         BCT   R5,*-8                                                           
         LA    R6,1                                                             
         ST    R3,DETNUM           SAVE ACTUAL NO. OF ENTRIES FOR PRNT          
         MVC   LASTVALS,=X'FFFFFFFF'                                            
         LA    R5,1                                                             
         SPACE 2                                                                
RANK2    LA    R1,0(R2,R4)         NOW PUT IN RANK NUMBERS                      
         CLC   0(4,R1),LASTVALS                                                 
         BNE   RANK4                                                            
         STH   R5,0(R2)                                                         
         OI    0(R2),X'80'         SIGNIFIES EQUALITY                           
         B     RANK6                                                            
         SPACE 2                                                                
RANK4    MVC   LASTVALS,0(R1)      LOWER  THAN PREVIOUS                         
         LR    R5,R6               SET PRESENT VALUES                           
         STH   R5,0(R2)                                                         
         SPACE 2                                                                
RANK6    LA    R6,1(R6)                                                         
         LA    R2,60(R2)                                                        
         CLI   0(R2),X'FF'                                                      
         BE    XIT                                                              
         BCT   R3,RANK2                                                         
         B     XIT                                                              
         SPACE 2                                                                
LASTVALS DS    F                                                                
         EJECT                                                                  
*              ROUTINE TO PRINT TABLE                                           
         SPACE 3                                                                
PRNTDETS NTR1                                                                   
         LA    R4,28               DISPLACE TO DETAIL  VALUES                   
         B     PRNTALL                                                          
         SPACE 2                                                                
PRNTTOTS NTR1                                                                   
         LA    R4,44               DISPLACE TO TOTAL VALUES                     
         SPACE 2                                                                
PRNTALL  EQU   *                                                                
         L     R2,=A(ANTABLE)                                                   
         AR    R2,RB                                                            
         L     R3,DETNUM                                                        
         LA    R2,4(R2)                                                         
         MVI   FORCEHED,C'Y'                                                    
         SPACE 2                                                                
PRNT2    CLI   0(R2),X'FF'                                                      
         BE    PRNT12                                                           
         LA    R5,0(R2,R4)                                                      
         OC    0(16,R5),0(R5)                                                   
         BZ    PRNT12                                                           
         OC    0(2,R2),0(R2)       RANK NUMBER                                  
         BZ    PRNT6                                                            
         TM    0(R2),X'80'                                                      
         BZ    PRNT4                                                            
         NI    0(R2),X'7F'                                                      
         MVI   P+4,C'='                                                         
         SPACE 2                                                                
PRNT4    EDIT  (2,(R2)),(4,P)                                                   
         SPACE 2                                                                
PRNT6    MVC   P+6(6),2(R2)        CODE                                         
         CLI   QPROG,C'6'                                                       
         BE    PRNT8                                                            
         MVC   P+13(20),8(R2)      AGENCY NAME OR ...                           
         B     PRNT10                                                           
         SPACE 2                                                                
PRNT8    MVC   P+11(20),8(R2)      ADVERTISER NAME                              
         SPACE 2                                                                
PRNT10   LA    R6,P+33             DETAIL LINE                                  
         BAS   RE,FORMAT                                                        
         LA    R5,8(R5)                                                         
         LA    R6,P+73                                                          
         BAS   RE,FORMAT                                                        
         MVI   SPACING,2                                                        
         GOTO1 REPORT                                                           
         SPACE 2                                                                
PRNT12   LA    R2,60(R2)                                                        
         BCT   R3,PRNT2                                                         
         OC    DETNUM,DETNUM                                                    
         BZ    PRNT13                                                           
         L     R2,=A(OTHLINES)                                                  
         AR    R2,RB                                                            
         XC    DETNUM,DETNUM                                                    
         LA    R3,1                                                             
         B     PRNT2                                                            
PRNT13   EQU   *                                                                
         MVI   SPACING,2                                                        
         GOTO1 REPORT                                                           
         MVC   P+11(12),=C'** TOTALS **'                                        
         CLI   QPROG,C'6'                                                       
         BE    PRNT14                                                           
         MVC   P+11(2),SPACES                                                   
         MVC   P+13(12),=C'** TOTALS **'                                        
         SPACE 2                                                                
PRNT14   LA    R6,P+33             TOTALS LINE                                  
         LA    R5,0(R2,R4)                                                      
         BAS   RE,FORMAT                                                        
         LA    R5,8(R5)                                                         
         LA    R6,P+73                                                          
         BAS   RE,FORMAT                                                        
         GOTO1 REPORT                                                           
         B     XIT                                                              
         EJECT                                                                  
*              FORMAT THE NUMBERS                                               
         SPACE 3                                                                
FORMAT   NTR1                                                                   
         MVC   WORK,SPACES         FIRST REFRESH HEAD7                          
         CH    R4,=H'44'                                                        
         BE    FORMAT4                                                          
         CLI   QSEQ,C'S'                                                        
         BE    FORMAT2                                                          
         MVC   WORK(6),=C'OFFICE'                                               
         MVC   WORK+7(2),ROFFKOFF                                               
         MVC   WORK+10(20),ROFFNAME                                             
         B     FORMAT10                                                         
         SPACE 2                                                                
FORMAT2  MVC   WORK+4(7),=C'STATION'                                            
         MVC   WORK+12(4),RSTAKSTA                                              
         MVC   WORK+16(3),=C'-TV'                                               
         CLI   RCONKSTA+4,C' '                                                  
         BE    FORMAT10                                                         
         MVC   WORK+17(1),RSTAKSTA+4                                            
         MVI   WORK+18,C'M'                                                     
         B     FORMAT10                                                         
         SPACE 2                                                                
FORMAT4  CLI   QSEQ,C'S'                                                        
         BE    FORMAT6                                                          
         MVC   WORK(21),=C'ALL REQUESTED OFFICES'                               
         B     FORMAT10                                                         
         SPACE 2                                                                
FORMAT6  MVC   WORK(22),=C'ALL REQUESTED STATIONS'                              
         SPACE 2                                                                
FORMAT10 MVC   HEAD7+45(30),WORK                                                
         LM    R2,R3,0(R5)                                                      
         XC    0(8,R5),0(R5)                                                    
         LR    R4,R2                                                            
         SR    R4,R3                                                            
         STM   R2,R4,DMCB                                                       
         LA    R7,3                                                             
         LA    R5,DMCB                                                          
         SPACE 2                                                                
FORMAT12 MVI   0(R6),C' '          (CLEAR PREVIOUS SIGN)                        
         OC    0(4,R5),0(R5)                                                    
         BE    FORMAT14                                                         
         EDIT  (4,(R5)),(9,(R6)),MINUS=YES                                      
         SPACE 2                                                                
FORMAT14 LA    R5,4(R5)                                                         
         LA    R6,9(R6)                                                         
         BCT   R7,FORMAT12                                                      
         LTR   R4,R4               TEST FOR 0 CHANGE                            
         BZ    XIT                                                              
         BCTR  R6,R0                                                            
         CLI   0(R6),C'-'                                                       
         BE    *+8                                                              
         MVI   0(R6),C'+'                                                       
         LA    R6,1(R6)                                                         
         LTR   R3,R3                                                            
         BZ    FORMAT16                                                         
         XR    R2,R3               TEST FOR DIFFERENT SIGNS                     
         LTR   R2,R2                                                            
         BM    XIT                                                              
         SRDA  R4,32                                                            
         M     R4,=F'10000'                                                     
         DR    R4,R3                                                            
         MVC   4(4,R6),=C'HIGH'                                                 
         C     R5,=F'99999'                                                     
         BH    XIT                                                              
         EDIT  (R5),(7,2(R6)),2,MINUS=YES                                       
         CLI   8(R6),C' '                                                       
         BNE   XIT                                                              
         MVI   8(R6),C'+'                                                       
         B     XIT                                                              
         SPACE 1                                                                
FORMAT16 LTR   R2,R2               TEST NO PRIOR DATA, BUT CURRENT              
         BZ    XIT                                                              
         MVC   4(4,R6),=C'HIGH'                                                 
         SPACE 2                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
ANYDATA  NTR1                                                                   
         L     R2,=A(ANTABLE)                                                   
         AR    R2,RB                                                            
         L     R3,0(R2)                                                         
         LA    R2,4(R2)                                                         
         USING ACCUMD,R2                                                        
*                                                                               
ANYDATA2 CLI   0(R2),X'FF'                                                      
         BE    ANYDATA3                                                         
         LA    R4,0(R1,R2)                                                      
         OC    0(8,R4),0(R4)                                                    
         BNZ   ANYDATA4                                                         
         LA    R2,60(R2)                                                        
         BCT   R3,ANYDATA2                                                      
ANYDATA3 SR    R3,R3                                                            
ANYDATA4 LTR   R3,R3                                                            
         B     XIT                                                              
         SPACE 2                                                                
DETNUM   DS    F                                                                
         EJECT                                                                  
         LTORG                                                                  
ANTABLE  CSECT                                                                  
         DC    F'2000'                                                          
DETLINES DS    120000C                                                          
OTHLINES DS    120C                                                             
         EJECT                                                                  
*              ACCUMULATOR DESIGN                                               
         SPACE 3                                                                
ACCUMD   DSECT                                                                  
ACCLINE  DS    0CL60                                                            
ACCRANK  DS    CL2                                                              
ACCCODE  DS    CL6                 AGENCY OR ADVERTISER CODE                    
ACCNAME  DS    CL20                AGENCY OR ADVERTISER NAME                    
ACCPDCUR DS    CL4                 DETAIL  PERIOD  CURRENT                      
ACCPDPRI DS    CL4                                 PRIOR                        
ACCYDCUR DS    CL4                         YTD     CURRENT                      
ACCYDPRI DS    CL4                                 PRIOR                        
ACCPTCUR DS    CL4                 TOTAL   PERIOD  CURRENT                      
ACCPTPRI DS    CL4                                 PRIOR                        
ACCYTCUR DS    CL4                         YTD     CURRENT                      
ACCYTPRI DS    CL4                                 PRIOR                        
         SPACE 2                                                                
*                                  LINES 1-N ARE FOR ANAYSIS                    
*                                  LINE  N+1 IS  FOR OTHERS                     
*                                  LINE  N+2 IS  FOR TOTALS                     
*                                  VALUE OF N IS DEFINED IN FIRST               
*                                  FOUR BYTES OF ANTABLE                        
         EJECT                                                                  
