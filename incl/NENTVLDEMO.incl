*          DATA SET NENTVLDEMO AT LEVEL 165 AS OF 02/27/20                      
***********************************************************************         
*  TITLE: SUBR                                                        *         
*                                                                     *         
*  INCLUDED FROM: NENETVALUE                                          *         
*  SUPPORTS MINUTE BY MINUTE DATA                                               
*  CONTAINS PLD CODE - NOTE HELLO CALLS                                         
*                                                                     *         
*  COMMENTS: SEPARATES DEMO FUNCTIONS FROM NETVALUE                   *         
*            ROUTINES CALLED BY:                                      *         
*        GOTO1 SUBR,PLIST,('EQU',(RC)) {,P1}                          *         
*                                                                     *         
*---------------------------------------------------+4----------------*         
* MOD LOG:                                                            *         
*                                                                     *         
* 28JUN99  (NS ) --- ESTOUT ROUTINE MOVE BOOK FROM BUY                          
* 05APR99  (PXZ) --- CABLE PRECISION ONLY FOR CABLE                             
* 07OCT91  (EFJ) --- ADD CODE TO ADD ELEMS TO UNIT REC FOR CABLE      *         
*                    STATIONS (SETCABLE)                              *         
*                --- ADD IN NMOD STORAGE (OFF R8)                     *         
*                --- CHANGE WHERE SETATPDM LOOKS FOR MIRROR TYPE CODE *         
*                                                                     *         
*                                                                     *         
***********************************************************************         
SUBR     RSECT                                                                  
         DS    8000C               1F40 HEX                                     
         ORG   *-8000                                                           
         NMOD1 SUBRWRKX-SUBRWORK,**SUBR**,R9,CLEAR=YES                          
         LR    R8,RC                                                            
         USING SUBRWORK,R8                                                      
         L     RC,0(R1)            RE-ESTABLISH WORKING STORAGE                 
         USING CONVWORK,RC                                                      
*                                                                               
         L     RE,0(R1)                                                         
         SRL   RE,24               GET THE ROUTINE NUMBER                       
         SLL   RE,2                                                             
         LA    RF,*+2(RE)                                                       
         BR    RF                                                               
*                                                                               
INITDBE  EQU   (INITDB#-*)/4+1                                                  
MANOUTSE EQU   (MANOUTS#-*)/4+1                                                 
ACTOUTSE EQU   (ACTOUTS#-*)/4+1                                                 
ESTOUTSE EQU   (ESTOUTS#-*)/4+1                                                 
STATPDME EQU   (STATPDM#-*)/4+1                                                 
STPRDEME EQU   (STPRDEM#-*)/4+1                                                 
STTPDEME EQU   (STTPDEM#-*)/4+1                                                 
CONOUTE  EQU   (CONOUT#-*)/4+1                                                  
ESTOACTE EQU   (ESTOACT#-*)/4+1                                                 
PRGTOESE EQU   (PRGTOES#-*)/4+1                                                 
OVRHOMEE EQU   (OVRHOME#-*)/4+1                                                 
USEFORME EQU   (USEFORM#-*)/4+1                                                 
GETDEMOE EQU   (GETDEMO#-*)/4+1                                                 
SETATPDE EQU   (SETATPD#-*)/4+1                                                 
BTUREADE EQU   (BTUREAD#-*)/4+1                                                 
*                                                                               
INITDB#  B     INITDB      01      INITIALIZE DBLOCK                            
MANOUTS# B     MANOUTS     02      ACTOUTS, BUT FOR MAN OVERRIDES               
ACTOUTS# B     ACTOUTS     03      ESTOUTS, BUT FILE=NTI                        
ESTOUTS# B     ESTOUTS     04      GET REQUESTED DEMOS FROM UNIT REC            
STATPDM# B     SETATPDM    05      MAKE DEMAND 1/2 HR TIME PER DEMOS            
STPRDEM# B     SETPRDEM    06      MAKE DEMAND RETURN PGM DEMOS                 
STTPDEM# B     SETTPDEM    07      MAKE DEMAND RETURN TIME PER DEMOS            
CONOUT#  B     CONOUT      08      VPH STUFF                                    
ESTOACT# B     ESTTOACT    09      CREATE DUMMY REC WITH ACT DEMO ELEMS         
PRGTOES# B     PRGTOEST    10      CREATE DUMMY REC WITH EST DEMO ELEMS         
OVRHOME# B     OVERHOME    11      USED WHEN DEMOS ARE FM DIFF PLACES           
USEFORM# B     USEFORM     12      ADJUST DEMOS IN DUMMY REC                    
GETDEMO# B     GETDEMOS    13      GET DEMOS FM DEMAND INTO DUMMYREC            
SETATPD# B     SETATPD2    14      SET AFFID TIME FOR CABLE PROG POST           
BTUREAD# B     BTUREAD     15      READ FOR USER FILE                           
*                                                                               
SUBRXIT  XIT1                                                                   
*                                                                               
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.7.2                                                    *         
*  TITLE: INITDB - INITIALIZE DBLOCK FOR ALL USES OF DEMAND.          *         
* ADDITIONAL FIELDS IN DBLOCK ARE SET BY THE CALLING ROUTINE.         *         
*                                                                     *         
*  CALLED FROM: ALL ROUTINES READING THE DEMO FILE                    *         
*                                                                     *         
*  GLOBAL OUTPUTS: DBLOCK                                             *         
***********************************************************************         
INITDB   DS    0H                                                               
         BRAS  RE,INITDB1                                                       
*&&DO                                                                           
         TM    NBUNST4,X'10'       =PCPR UNIT?                                  
         BNO   *+12                                                             
         MVI   NBPOSTYP,C'S'       YES-SET FOR SYNDICATION                      
         MVI   NBUSER2+4,C'Y'         SET TO Y                                  
*                                                                               
         XC    DBLOCK,DBLOCK                                                    
         MVC   DBFILE,=C'NTI'                                                   
         L     R1,AWORKREC                                                      
         ST    R1,DBAREC                                                        
         MVC   DBCOMFCS,NBACOM                                                  
         MVI   DBSELSRC,C'N'                                                    
         MVI   DBSELMED,C'N'       INITIALIZE FOR NETWORK                       
*                                                                               
         CLI   NBSURVEY,C'H'                                                    
         BNE   INITDB2                                                          
         CLI   NBUSER1+3,C'H'      ONLY NHTI WKLY                               
         BE    *+12                                                             
         CLI   NBUSER1+3,C'B'      NHTI AND NAD WKLY                            
         BNE   *+8                                                              
         MVI   DBSELMED,C'W'                                                    
*                                                                               
INITDB2  MVC   DBSELSTA(4),NBACTNET                                             
         CLC   NBAUTH,=XL2'4040'                                                
         BNH   *+10                                                             
         MVC   DBAUTH(2),NBAUTH    MOVE AUTH CODE FOR DEMO SECURITY             
* OUT->  CLI   NBPOSTYP,C'N'                                                    
* OUT->  BE    *+18                                                             
         CLI   NBNTISTA,X'40'                                                   
         BNH   *+10                                                             
         MVC   DBSELSTA(4),NBNTISTA  IF SYNDICATOR USE NTI STATION              
         MVI   DBSELSTA+4,C'T'                                                  
         CLI   NBSURVEY,X'40'                                                   
         BNH   *+18                                                             
         CLI   NBSURVEY,C'N'                                                    
         BE    *+10                                                             
         MVC   DBSELSTA+4(1),NBSURVEY                                           
*                                                                               
         CLC   =C'SIN',DBSELSTA    IF SIN                                       
         BE    *+10                                                             
         CLC   =C'ITN',DBSELSTA    IF ITN                                       
         BNE   *+8                                                              
         MVI   DBSELSTA+4,C'S'     IT'S SYNDICATION                             
*                                                                               
         MVC   DBSELBK,SAVEBOOK                                                 
         MVC   DBSELAGY,NBSELAGY                                                
         CLC   DBSELBK,=X'5D25'    SWITCH TO QH DATA                            
         BL    CABQHX                                                           
         CLC   DBSELBK,=X'5D31'    BLACK WEEK                                   
         BE    CABQHX                                                           
         CLC   DBSELBK,=X'5D32'    BLACK WEEK                                   
         BE    CABQHX                                                           
         CLC   DBSELBK,=X'5D33'    BLACK WEEK                                   
         BE    CABQHX                                                           
         CLI   DBSELSTA+4,C'C'                                                  
         BNE   CABQHX                                                           
         MVI   DBSELSTA+4,C'Q'                                                  
*                                                                               
CABQHX   DS    0H                                                               
*                                                                               
         CLC   DBSELSTA,=C'PREVC'                                               
         BNE   *+8                                                              
         MVI   DBSELDUR,X'FF'      PICK UP ALL DUR INCL < 15MIN DURS            
** PXZ   CLI   NBPOSTYP,C'C        IF POSTING CABLE                             
         CLI   NBSURVEY,C'C        IF POSTING CABLE                             
         BNE   ENDSETDR                                                         
***      CLC   =C'FXNC',DBSELSTA   WHETHER IT'S C OR Q OR WHATEVER              
***      BNE   ENDSETDR                                                         
         OC    NBAFFTIM,NBAFFTIM                                                
         BNZ   SETDUR                                                           
         OC    NBNTI,NBNTI                                                      
         BZ    ENDSETDR                                                         
SETDUR   MVI   DBSELDUR,X'FF'      PICK UP ALL DUR INCL < 15MIN DURS            
ENDSETDR DS    0H                                                               
*                                                                               
         CLI   NBPOSTDT,C'C'       TEST FOR CONFORMED                           
         BNE   *+8                                                              
         MVI   DBBTYPE,C'C'                                                     
         CLI   NBPOSTDT,C'I'       TEST FOR INTEGRATED                          
         BNE   *+8                                                              
         MVI   DBBTYPE,C'I'                                                     
         CLI   NBPOSTDT,C'A'       TEST FOR ASCRIBED                            
         BNE   *+8                                                              
         MVI   DBBTYPE,C'A'                                                     
         CLI   NBPOSTDT,C'Z'       TEST FOR X-RATED                             
         BNE   *+8                                                              
         MVI   DBBTYPE,C'Z'                                                     
         CLI   NBPOSTDT,C'E'                                                    
         BNE   *+8                                                              
         MVI   DBBTYPE,C'E'                                                     
** PXZ   CLI   NBPOSTYP,C'H'                                                    
         CLI   NBSURVEY,C'H'                                                    
         BNE   *+8                                                              
         MVI   DBBTYPE,0                                                        
*                                                                               
         TM    NBINDS4,NBI4BRK     BREAKOUT?                                    
         BNO   *+8                                                              
         MVI   DBSTYPE,C'B'                                                     
*                                  FIX AGENCY SCREW-UPS                         
         L     RE,=A(AGSTAEQU)                                                  
         A     RE,RELO                                                          
AGSTEQ   CLC   DBSELSTA(4),0(RE)                                                
         BL    AGSTEQX         <---WON'T BE THERE                               
         BH    *+14            <---TRY NEXT                                     
         MVC   DBSELSTA(4),4(RE)   MOVE NEW STATION TO DBLOCK                   
         B     AGSTEQX                                                          
         LA    RE,L'AGSTAEQU(RE)   BUMP TO NEXT STA EQU                         
         B     AGSTEQ                                                           
*                                                                               
AGSTEQX  BAS   RE,SETDMPRE         SET DEMO PRECISSION                          
*&&                                                                             
*                                                                               
         BAS   RE,SETDMPRE         SET DEMO PRECISSION                          
         B     SUBRXIT                                                          
         EJECT                                                                  
***********************************************************************         
*  TITLE: MANOUTS                                                     *         
*                                                                     *         
*  CALLED FROM : DOUSEACT                                             *         
*                                                                     *         
*  COMMENTS: SAME AS ACTOUTS, EXCEPT USED FOR MANUAL OVERRIDES        *         
*            GETS ACTUAL DEMOS FROM THE UNIT RECORD                   *         
***********************************************************************         
MANOUTS  DS    0H                                                               
         XC    DBLOCK,DBLOCK       GET GARBAGE OUT OF DBLOCK                    
         MVC   DBFILE,=C'NTI'      TO WANT ACTUAL   DEMOS                       
         CLI   NBSTATYP,C'V'       CHECK MEDIA TYPE 'V'=VIDEO EX HULU           
         BNE   *+10                                                             
         MVC   DBFILE,=C'CAB'      SPECIAL PRECISION FOR HULU                   
         MVC   DBCOMFCS,NBACOM     A(COMFACS)                                   
         ST    R7,DBAREC           A(UNIT RECORD)                               
         MVC   DBAQUART,ELEM1      A(1ST ELEM ON UNIT REC)                      
         MVI   DBSELMED,C'N'                                                    
         MVI   DBSELSRC,C'N'                                                    
         MVC   DBSELBK,SAVEBOOK                                                 
         GOTO1 =V(NETWEEK),DMCB,EBCDDAT,NBGETDAY,NBADDAY,RR=RELO                
         MVC   DBSELBK(1),4(R1)        NTI YEAR                                 
*951110  MVC   DBSELBK+1(1),0(R1)      NTI WEEK IS HUT WEEK                     
         MVC   DBSELBK+1(1),8(R1)      NTI WEEK IS RTG WEEK                     
*                                                                               
         MVI   BYTE,NUOVDEQ        ACTUALS                                      
         MVC   SVBYTE,BYTE                                                      
         BAS   RE,SETDMPRE         SET DEMO PRECISSION                          
         BAS   RE,SETDMSET         WIPE OUT VPH ELEMENT IN DBAREC               
*                                                                               
         BRAS  RE,SETEXPD              EXPAND DEMOS?                            
         BRAS  RE,DODEMOUT                                                      
         BRAS  RE,RESEXPD               RESET EXPD DEMOS?                       
         BAS   RE,SETDMPST         RESET DBAREC                                 
         MVC   DBSELBK,SAVEBOOK                                                 
*                                                                               
XITMANO  B     SUBRXIT                                                          
*                                                                               
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.8.1                                                    *         
*  TITLE: ACTOUTS                                                     *         
*                                                                     *         
*  CALLED FROM : DOUSEACT                                             *         
*                                                                     *         
*  COMMENTS: SHOULD BE SAME AS ESTOUTS, EXCEPT FILE=NTI.              *         
*            GETS ACTUAL DEMOS FROM THE UNIT RECORD                   *         
*                                                                     *         
*  IF UNIVERSES ARE CABLE, THEN CHANGES ALL OCCURENCES OF HOMES       *         
*    TO CABLE-HOMES.                                                  *         
***********************************************************************         
ACTOUTS  DS    0H                                                               
         CLI   HAVBTU,C'O'         CHECK FOR OPI FILE NEEDED                    
         BE    *+12                                                             
         CLI   HAVBTU,C'Y'         CHECK FOR USER FILE NEEDED                   
         BNE   ACTOUT1                                                          
*                                                                               
         GOTO1 ASUBR,DMCB,('INITDBE',(RC))                                      
*                                                                               
         CLI   HAVBTU,C'O'                                                      
         BE    ACTOOPI                                                          
*                                                                               
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNTFIL'),(X'60',NBAIO),(1,=C'K')           
         CLI   DMCB+12,0           IF FOUND EXIT                                
         BNE   ACTOUT1                                                          
         L     RE,12(R1)                                                        
         MVC   DBSELBK,3(RE)        BOOK OVERRIDE FROM BUY RECORD (TVQ)         
         B     *+10                                                             
ACTOOPI  MVC   DBSELBK,SAVEBOOK     OPI                                         
*                                                                               
         MVI   BTUTYP,0                                                         
         XC    BTUEXT(12),BTUEXT                                                
         XC    DBEXTEND,DBEXTEND                                                
*        GOTO1 ASUBR,FULL,('BTUREADE',(RC))                                     
         BRAS  RE,BTUREADX                                                      
         CLI   GOTUTYPE,1                                                       
         BNE   ACTOUT1                                                          
         MVC   BTUEXT,=C'UFIL'                                                  
         MVC   BTUEX2,BTUDB+4      SET DBAREC                                   
         XC    DBLOCK,DBLOCK                                                    
         LA    RE,BTUEXT                                                        
         ST    RE,DBEXTEND                                                      
         B     ACTOUT1A                                                         
*                                                                               
ACTOUT1  XC    DBLOCK,DBLOCK       GET GARBAGE OUT OF DBLOCK                    
ACTOUT1A MVC   DBFILE,=C'NTI'      TO WANT ACTUAL   DEMOS                       
         MVC   DBCOMFCS,NBACOM     A(COMFACS)                                   
         ST    R7,DBAREC           A(UNIT RECORD)                               
         MVC   DBAQUART,ELEM1      A(1ST ELEM ON UNIT REC)                      
*                                                                               
         B     ACOUT12             BYPASS CABLE UNIVERSE LOGIC                  
*                                                                               
         TM    NBUNST2,X'08'        IF USING CABLE DEMOS,                       
         BZ    ACOUT12              CHANGE HOMES TO CABLE                       
*        LA    R4,65                65 DEMOS IN LIST (WHEN 20 DEMOS)            
         LA    R4,80                80 DEMOS IN LIST  (NOW 25 DEMOS)            
         LA    R3,DIALIST                                                       
ACOUT2   CLI   0(R3),X'FF'          IF END OF LIST                              
         BE    ACOUT12                                                          
         CLI   2(R3),1              IF DEMO IS HOMES                            
         BNE   ACOUT4                                                           
         MVI   2(R3),16             SET IT TO CABLE                             
ACOUT4   LA    R3,3(R3)                                                         
         BCT   R4,ACOUT2            NEXT DEMO                                   
*                                                                               
*        LA    R4,21                21 DEMOS IN UNIV LIST                       
         LA    R4,26                26 DEMOS IN UNIV LIST                       
         LA    R3,DIULIST                                                       
ACOUT8   CLI   0(R3),X'FF'          IF END OF LIST                              
         BE    ACOUT12                                                          
         CLI   2(R3),1              IF DEMO IS HOMES                            
         BNE   ACOUT10                                                          
         MVI   2(R3),16             SET IT TO CABLE                             
ACOUT10  LA    R3,3(R3)                                                         
         BCT   R4,ACOUT8            NEXT DEMO                                   
*                                                                               
ACOUT12  MVI   BYTE,NUOVDEQ        ACTUALS                                      
         MVC   SVBYTE,BYTE                                                      
         BAS   RE,SETDMPRE         SET DEMO PRECISSION                          
         CLI   RESULT,C'L'         IF DEMOS FROM PROGRAM REC                    
         BE    *+8                 DON'T SET TO C'Y'                            
         BRAS  RE,SETEXPD          SET EXPDEM PRECISION                         
         MVC   DBFILE,=C'NTI'                                                   
         MVI   DBFUNCT,0                                                        
         BRAS  RE,DODEMOUT                                                      
         BRAS  RE,RESEXPD                                                       
*                                                                               
XITACTO  B     SUBRXIT                                                          
*                                                                               
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.3.1                                                    *         
*  TITLE: ESTOUTS                                                     *         
*                                                                     *         
*  COMMENTS: GETS THE REQUESTED DEMOS FROM THE UNIT RECORD AND STICKS *         
*          THE RETURNED VALUES IN DOALIST.                            *         
*                                                                     *         
*  CALLED FROM: STDOEST                                               *         
*                 DOESTACT                                            *         
*                 DOPRGREC                                            *         
*                                                                     *         
*  CALLS TO: DEMOUT - RETURNS THE REQUESTED DEMO VALUES               *         
*                                                                     *         
*  INPUTS: NDDEMOS - LIST OF THE DEMOS REQUESTED (WITHOUT PREFIX)     *         
*          NDNDEMOS - THE NUMBR OF SUCH REQUSTS                       *         
*          NBAIO - THE ADDDRESS OF THE UNIT RECORD                    *         
*          ELEM1 - THE LOCATION OF THE FIRST ELEMENT IN UNITREC       *         
*                                                                     *         
*  GLOBAL OUTPUTS: DOALIST - THE VALUES FOR THE REQUESTED DEMOS.      *         
*                      INCLUDES HOME DATA, VPHS GRPS, AND IMPS        *         
*                  DOULIST - UNIVERSES                                *         
*                                                                     *         
* LOCAL OUTPUTS:  DBLOCK - FILLS IN VALUES NEEDED BY DEMOUT           *         
***********************************************************************         
ESTOUTS  DS    0H                                                               
         CLI   HAVBTU,C'O'         CHECK FOR OPI  FILE NEEDED                   
         BE    *+12                                                             
         CLI   HAVBTU,C'Y'         CHECK FOR TVQ  FILE NEEDED                   
         BNE   ESTOUT5                                                          
*                                                                               
         GOTO1 ASUBR,DMCB,('INITDBE',(RC))                                      
*                                                                               
*        MVC   DBSELBK,SAVEBOOK                                                 
*        MVC   DBSELBK,=X'6309'    <<<FORCE FOR NOW DEFAULT BOOK                
         MVI   BTUTYP,C'E'                                                      
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNTFIL'),(X'60',NBAIO),(1,=C'J')           
         CLI   DMCB+12,0           IF FOUND EXIT                                
         BNE   ESTOUT1                                                          
         L     RE,12(R1)                                                        
         MVC   DBSELBK,3(RE)        BOOK OVERRIDE FROM BUY RECORD               
         MVI   BTUTYP,0                                                         
*                                                                               
ESTOUT1  XC    BTUEXT(12),BTUEXT                                                
         XC    DBEXTEND,DBEXTEND                                                
*        GOTO1 ASUBR,FULL,('BTUREADE',(RC))                                     
         BRAS  RE,BTUREADX                                                      
         CLI   GOTUTYPE,1                                                       
         BNE   ESTOUT5                                                          
         MVC   BTUEXT,=C'UFIL'                                                  
         MVC   BTUEX2,BTUDB+4      SET DBAREC                                   
         XC    DBLOCK,DBLOCK                                                    
         LA    RE,BTUEXT                                                        
         ST    RE,DBEXTEND                                                      
         LA    RF,DOALEN                                                        
         XCEF  DOALIST,(RF)        CLEAR DOALIST FOR ESTIMATED DEMOS            
         B     ESTOUT5A                                                         
*                                                                               
ESTOUT5  LA    RF,DOALEN                                                        
         XCEF  DOALIST,(RF)        CLEAR DOALIST FOR ESTIMATED DEMOS            
*                                                                               
         XC    DBLOCK,DBLOCK       GET GARBAGE OUT OF DBLOCK                    
ESTOUT5A MVC   DBFILE,=C'EVN'      TO WANT ESTIMATE DEMOS                       
         MVC   DBCOMFCS,NBACOM     A(COMFACS)                                   
         ST    R7,DBAREC           A(UNIT RECORD)                               
         MVC   DBAQUART,ELEM1      A(1ST ELEM ON UNIT REC)                      
*                                                                               
         TM    NBINDS9,NBI9CP1+NBI9CP2  COMSCORE?                               
         JZ    ESTOUT7                                                          
         GOTOR =A(XTROUTIN),DMCB,(0,(RC)),RR=RELO  GET PROGRAM RECORD           
*                                                                               
ESTOUT7  MVI   BYTE,NUOVDDQ        ESTIMATED                                    
         MVC   SVBYTE,BYTE                                                      
         BAS   RE,SETDMPRE         SET DEMO PRECISSION                          
*                                                                               
* SETDMPRE WILL SET DBFILE AND DBFUNCT TO GET RLD IF                            
* FLAGS SET FOR RLD SO NEED TO RESET HERE TO GET ESTIMATED                      
*                                                                               
ESTOUT10 BRAS  RE,DODEMOUT                                                      
*                                                                               
XITESTO  B     SUBRXIT                                                          
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.4.1                                                    *         
*  TITLE: SETATPDM - SET DBLOCK SO THAT DEMAND WILL RETURN HALF-HOUR  *         
*                    (AFFADAVIT) TIME PERIOD DEMOS                    *         
*                                                                     *         
*  CALLED FROM: STASSRES -ASSIGN RESULT STATE                         *         
*                                                                     *         
*  GLOBAL OUTPUTS: DBLOCK                                             *         
***********************************************************************         
SETATPDM DS    0H                                                               
         MVI   MIRDIV,1            INIT DIVIDOR                                 
         GOTO1 ASUBR,DMCB,('INITDBE',(RC))   SET UP DBLOCK FOR DEMAND           
         MVI   DBFUNCT,DBGETDEM                                                 
         MVC   DBSELDAY,NBDAY                                                   
SETATPD2 XC    DBSELTIM(4),DBSELTIM          RESET START/END TIME               
         MVC   DBSELTIM(2),NBAFFTIM                                             
         MVC   DBSELTIM+2(2),NBAFFTIM                                           
*                                                                               
         BRAS  RE,GET18            IS THERE AFFID TIME OVERRIDE?                
         OC    WORK(2),WORK                                                     
         BZ    NOAFFOVD                                                         
         MVC   DBSELTIM(2),WORK                                                 
         MVC   DBSELTIM+2(2),WORK                                               
NOAFFOVD DS    0H                                                               
*                                                                               
         OC    NBSDAFDT,NBSDAFDT                                                
         BZ    DAF10                                                            
         GOTO1 NBGETDAY,DMCB,EBCDDAT,FULL    GET AFFID DAY                      
*                                                                               
         OC    NBDAYPAK,NBDAYPAK                                                
         BNZ   SETATPD5                                                         
         XC    DMCB,DMCB                                                        
         MVC   DMCB+4(4),=X'D9000A03'        GET DAYPAK                         
         GOTO1 NBCALLOV,DMCB                                                    
         L     RF,DMCB                                                          
         ST    RF,NBDAYPAK                                                      
SETATPD5 GOTO1 NBDAYPAK,DMCB,(3,FULL),WORK,WORK+10                              
         CLI   WORK,0                                                           
         BNE   *+6                                                              
         DC    H'0'                                                             
         MVC   DBSELDAY,WORK                                                    
DAF10    DS    0H                                                               
         CLI   NBUSER2+8,C'N'      CHECK FOR BREAKOUT AVG.                      
         BNE   DAF11               NO                                           
         MVC   DBSELTIM+2(2),NBAFFTIM                                           
*                                                                               
         BRAS  RE,GET18            GAP AFFID TIME OVERRIDE                      
         OC    WORK(2),WORK                                                     
         BZ    DAF11                                                            
         MVC   DBSELTIM+2(2),WORK      AFFID OVERRIDE                           
*                                                                               
* BUILD DBXTLD & CHAIN                                                          
*                                                                               
DAF11    DS    0H                                                               
         BRAS  RE,SETMIRHR         SET MIRROR HOURS                             
         OC    HALF,HALF           SAVE IN HALF FOR HOURFIX BELOW               
         BZ    XITATPD                                                          
*                                                                               
*                                                                               
         LA    RF,DBXTLWRK                                                      
         USING DBXTLD,RF                                                        
         MVC   DBXTLID,=C'DYTM'                                                 
         XC    DBXTLNXT,DBXTLNXT                                                
         MVI   DBXTLIDX,0                                                       
         MVC   DBXTLIST+0(1),DBSELDAY                                           
         MVC   DBXTLIST+1(2),DBSELTIM                                           
         MVC   DBXTLIST+3(2),DBSELTIM+2                                         
         MVC   DBXTLIST+5(1),DBSELDAY                                           
*                                                                               
         ZICM  R1,DBSELTIM,2                                                    
         BRAS  RE,HOURFIX                                                       
         STCM  R1,3,DBXTLIST+6                                                  
*                                                                               
         ZICM  R1,DBSELTIM+2,2                                                  
         BRAS  RE,HOURFIX                                                       
         STCM  R1,3,DBXTLIST+8                                                  
*                                                                               
DAF80    XC    DBXTLIST+10(5),DBXTLIST+10                                       
*                                                                               
         CLI   NBMIRTYP,C'M'       DUAL MIRROR M ?                              
         BNE   DAF85                                                            
         MVC   HALF,=H'500'        SET NEXT MIRROR TIME                         
         B     DAF90                                                            
DAF85    CLI   NBMIRTYP,C'N'       DUAL MIRROR N ?                              
         BNE   DAF85A                                                           
         MVC   HALF,=H'400'        SET NEXT MIRROR TIME                         
         B     DAF90                                                            
*                                                                               
DAF85A   CLI   NBMIRTYP,C'P'       DUAL MIRROR P ?                              
         BNE   DAF100                                                           
         MVC   HALF,=H'600'        SET NEXT MIRROR TIME                         
         B     DAF90                                                            
*                                                                               
DAF90    MVI   DBXTLIDX,1          YES-SET INDEX                                
         MVC   DBXTLIST+10(1),DBSELDAY                                          
         ZICM  R1,DBSELTIM,2                                                    
         BRAS  RE,HOURFIX                                                       
         STCM  R1,3,DBXTLIST+11                                                 
         ZICM  R1,DBSELTIM+2,2                                                  
         BRAS  RE,HOURFIX                                                       
         STCM  R1,3,DBXTLIST+13                                                 
*                                                                               
DAF100   EQU   *                                                                
         DROP  RF                                                               
*                                                                               
* SET DBEXTEND LINKED LIST CHAIN                                                
         OC    DBEXTEND,DBEXTEND                                                
         BNZ   *+12                                                             
         ST    RF,DBEXTEND                                                      
         B     XITATPD                                                          
*                                                                               
         L     RE,DBEXTEND                                                      
*                                                                               
         OC    4(4,RE),4(RE)       NEXT EXTENSION?                              
         BZ    *+12                NO MORE - LAST                               
         L     RE,4(RE)                                                         
         B     *-14                                                             
*                                                                               
         CR    RE,RF               PROTECT AGAINST POINTING TO ITSELF           
         BE    *+8                 HAPPENS IF DOUBLE ENTRY TO THIS RTN          
         ST    RF,4(RE)                                                         
*                                                                               
XITATPD  DS    0H                                                               
         B     SUBRXIT                                                          
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.4.2                                                    *         
*  TITLE: SETPRDEM - SET DBLOCK SO THAT DEMAND WILL RETURN PROGRAM    *         
*                    DEMOS                                            *         
*                                                                     *         
*  CALLED FROM: STASSRES - ASSIGN RESULT STATE                        *         
*               STCKVPEX - CHECK IF VPHS EXIST STATE                  *         
*                                                                     *         
*  GLOBAL OUTPUTS: DBLOCK                                             *         
***********************************************************************         
SETPRDEM DS    0H                                                               
         GOTO1 ASUBR,DMCB,('INITDBE',(RC))  SET UP DBLOCK FOR DEMAND            
         MVI   DBFUNCT,DBGETNTI                                                 
         CLI   NBSURVEY,C'S'       IF SYNDICATION                               
         BE    SETPRD00            IGNORE VENDING CHECK                         
         TM    NBINDS3,NBI3AIRV    IF VENDING STATION TIME PERIOD ONLY          
         BO    *+10                                                             
SETPRD00 MVC   DBSELPRG,NBNTI      PROGRAM NUMBER                               
         MVC   DBSELDAY,NBDAY      (EXACT DAY)                                  
         MVC   DBSELTIM,NBTIME     (EXACT TIME)                                 
*                                                                               
         BRAS  RE,ITNDAY FOR ITN USE AFFID DATE                                 
*                                                                               
         CLI   NBSURVEY,C'C'       CABLE                                        
         BNE   SETPRDM2                                                         
         MVI   DBSELSTA+4,C'C'     SET UP 5TH CHARACTER                         
         MVC   DBSELPTT,SVPSTAFD   AND DETAIL LEVEL                             
         CLI   DBSELPTT,C'D'       DAYLY IS REALLY EPISODE                      
         BNE   *+8                                                              
         MVI   DBSELPTT,C'E'                                                    
         CLI   NBMIRTYP,C' '       CHECK FOR MIRRORS                            
         BH    DAF11                                                            
*                                                                               
SETPRDM2 CLI   NBSURVEY,C'H'       HISPANIC IS SPECIAL                          
         BNE   XITPRDM                                                          
         TM    NBINDS3,NBI3AIRV    IF VENDING STATION TIME PERIOD ONLY          
         BO    SETPRDM3                                                         
         OC    NBNTI,NBNTI         OK IF WE HAVE PROGRAM NUMBER                 
         BNZ   XITPRDM                                                          
SETPRDM3 MVC   DBSELDAY,NBSDROT    OTHERWISE USE ROTATION                       
*                                                                               
XITPRDM  B     SUBRXIT                                                          
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.4.3                                                    *         
*  TITLE: SETTPDEM - SET DBLOCK SO DEMAND WILL RETURN TIME *                    
*                    PERIOD DEMOS                                     *         
*                                                                     *         
*  CALLED FROM: STASSRES                                              *         
*               STCKEX                                                *         
*               STCKBW                                                *         
*               STCKVPEX                                              *         
*                                                                     *         
*  GLOBAL OUTPUTS: DBLOCK                                             *         
***********************************************************************         
SETTPDEM DS    0H                                                               
         GOTO1 ASUBR,DMCB,('INITDBE',(RC))   SET UP DBLOCK FOR DEMAND           
         MVI   DBFUNCT,DBGETDEM                                                 
         MVC   DBSELDAY,NBDAY                                                   
         MVC   DBSELTIM,NBTIME                                                  
         CLI   NBMIRTYP,C' '       ANY MIRRORS ACTIVE                           
         BH    DAF11               YES PROCESS THEM                             
*                                                                               
XITTPDM  B     SUBRXIT                                                          
         EJECT                                                                  
*                                                                               
CONOUT   DS    0H                                                               
         BRAS  RE,XCONOUT                                                       
CONOUTX  B     SUBRXIT                                                          
*                                                                               
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.8.D.2                                                  *         
*  TITLE: ESTTOACT - CREATE A DUMMY RECORD WHICH CONTAINS ACTUAL      *         
*           DEMO ELEMENTS. THE VALUES OF THESE ELEMENTS ARE IDENTICAL *         
*           TO THOSE IN THE ESTIMATE ELEMENTS OF THE UNIT RECORD.     *         
*                                                                     *         
*  CALLS TO: DEMAINT - CONVERTS ESTIMATED ELEMENTS TO ACTUAL          *         
*                                                                     *         
*  CALLED FROM: DOPRGREC CASE OF STPUTEM                              *         
*               DOLATEST CASE OF STPUTEM                              *         
*                                                                     *         
*  AT ENTRY     P2=0 CONVERT FROM NBAIO TO DUMMYREC                   *         
*               P2^0 CONVERT DUMMYREC IN PLACE                        *         
*                                                                     *         
*  GLOBAL OUTPUTS: DUMMYREC - CONTAINS THE ACTUAL ELEMENTS            *         
*                                                                     *         
*                                                                     *         
***********************************************************************         
ESTTOACT DS    0H                                                               
         XC    DBLOCK,DBLOCK       INITIALIZE DBLOCK TO DESCRIBE                
         MVC   DBFILE,=C'EVN'      UNIT RECORD (SOURCE OF EST. DEMOS)           
         OC    4(4,R1),4(R1)       TEST FOR CONVERSION TYPE  (P2=0?)            
         BNZ   ESTTOAC1            ITS DUMMYREC IN PLACE                        
         MVC   DBAREC,NBAIO                                                     
         MVC   DBAQUART,ELEM1                                                   
         BAS   RE,INITDUM          SET UP DUMMY RECORD                          
         B     ESTTOAC2                                                         
         SPACE 1                                                                
ESTTOAC1 LA    RE,DUMMYREC                                                      
         ST    RE,DBAREC                                                        
         LA    RE,23(RE)           POINT TO FIRST ELEMENT                       
         ST    RE,DBAQUART                                                      
         SPACE 1                                                                
ESTTOAC2 MVC   DBCOMFCS,NBACOM                                                  
         XC    OUTFORM,OUTFORM     BUILD NTI OUTPUT FORMAT BLOCK                
         MVC   OUTFORM(7),NTIVALS                                               
         GOTO1 =V(NETWEEK),DMCB,EBCDDAT,NBGETDAY,NBADDAY,RR=RELO                
         MVC   OUTFORM+7(1),4(R1)  NTI YEAR                                     
         MVC   OUTFORM+8(1),0(R1)  NTI WEEK IS HUT WEEK                         
*                                  GET ESTIMATED DEMOS IN NTI FORMAT            
         L     R5,AWORKREC                                                      
         GOTO1 NBDEMAIN,DMCB,=C'GET',DBLOCK,(R5),OUTFORM                        
         CLI   DBERROR,0           TEST FOR ERROR                               
         BE    *+6                 NO-VALUES ARE NOW IN WORKREC                 
         DC    H'0'                                                             
*                                                                               
         MVC   DBFILE,=C'NTI'      CREATE ACTUAL ELEMENTS FROM                  
         LA    R1,DUMMYREC         ESTIMATED VALUES AND PUT TO                  
         ST    R1,DBAREC           DUMMYREC                                     
         LA    R1,23(R1)                                                        
         ST    R1,DBAQUART                                                      
         GOTO1 NBDEMAIN,DMCB,=C'REP',DBLOCK,(R5),OUTFORM                        
         CLI   DBERROR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         TM    NBUNST2,X'08'       IF NOT CABLE DELETE 49 ELEM                  
         BO    ESTOACTX                                                         
** PXZ   CLI   NBPOSTYP,C'H'                                                    
         CLI   NBSURVEY,C'H'                                                    
         BE    ESTOACTX                                                         
*        GOTO1 NBHELLO,DMCB,(C'D',=C'PAVFIL'),(X'49',DUMMYREC),0                
*        L     RF,=V(HELLO)                                                     
*        A     RF,RELO                                                          
***      L     RF,NBHELLO                                                       
***      LA    RE,3000             OVERRIDE PVFIL REC LENGTH                    
***      LA    R1,DMCB                                                          
***      ST    RE,16(R1)                                                        
***      GOTO1 (RF),DMCB,(C'd',=C'PAVFIL'),(X'49',DUMMYREC),0,0                 
         BAS   RE,DELET49                                                       
         CLI   DMCB+12,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
ESTOACTX B     SUBRXIT                                                          
*                                                                               
DELET49  NTR1                                                                   
         L     RF,NBHELLO                                                       
         LA    RE,3000             OVERRIDE PVFIL REC LENGTH                    
         LA    R1,DMCB                                                          
         ST    RE,16(R1)                                                        
         GOTO1 (RF),DMCB,(C'd',=C'PAVFIL'),(X'49',DUMMYREC),0,0                 
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.8.L.3                                                  *         
*  TITLE: PRGTOEST - CREATE A DUMMY RECORD WHICH CONTAINS ESTIMATE    *         
*           DEMO ELEMENTS. THE VALUES OF THESE ELEMENTS COMBINE THE   *         
*           PROGRAM RECORD VPHS AND RATING WITH THE UNIT RECORD'S     *         
*           HOMES DATA AND UNIVERSE ELEMENTS                          *         
*                                                                     *         
*  CALLS TO: DEMOMATH - PUTS UNIT RECORDS ESTIMATED DEMOS ON DUMMYREC *         
*            HELLO    - REPLACES ESTIMATED VPH ELEMENT AND ADDS       *         
*                     - OVERRIDE ELEMENTS FOR RATING/SHARE            *         
*            DEMOUT   - LOOK UP HUT FOR CALCULATING RATING/SHARE      *         
*            DEMOMATH - RECALCULATE DUMMY RECORD TO MERGE RATING/SHARE*         
*            HELLO    - DELETE OVERRIDE ELEMENTS FROM DUMMYREC        *         
*                                                                     *         
*  CALLED FROM: DOPRGREC CASE OF STPUTEM, DOLATEST CASE OF STPUTEM    *         
*  INPUTS: WORKREC - PROGRAM RECORD                                   *         
*  GLOBAL OUTPUTS: DUMMYREC - CONTAINS THE ESTIMATED ELEMENTS         *         
***********************************************************************         
PRGTOEST DS    0H                                                               
         BAS   RE,INITDUM          RESET THE DUMMY RECORD                       
*                                                                               
         XC    DBLOCK,DBLOCK       SET DBLOCK TO DESCRIBE EVN                   
         MVC   DBFILE,=C'EVN'      FILE ON THE UNIT RECORD                      
         MVC   DBAREC,NBAIO                                                     
         MVC   DBAQUART,ELEM1                                                   
         MVC   DBCOMFCS,NBACOM                                                  
         BAS   RE,INITMATH         INITIALIZE MATHFACS BLOCK                    
         GOTO1 NBDEMMTH,DMCB,=C'CON',NBAIO,DUMMYREC,MATHFACS                    
*                                                                               
         DS    0H                                                               
PRGTOES2 L     R5,AWORKREC                                                      
         GOTO1 NBHELLO,DMCB,(C'G',=C'SPTFILE'),(X'92',(R5)),0                   
         CLI   DMCB+12,0           TEST FOR PROGRAM ELEMENT FOUND               
         BE    *+6                 YES                                          
         DC    H'0'                                                             
         L     R3,12(R1)                                                        
         USING NPGELEM,R3                                                       
         LA    R2,WORK             POINT TO AREA FOR VPH ELEM                   
         USING NUEVD,R2                                                         
         MVI   NUEVD,X'33'         BUILD ESTIMATED VPH ELEMENT                  
         MVI   NUEVLEN,37          OLD VPH ELEMENT LENGTH                       
         MVI   NUEVELEN,1                                                       
         MVC   NUEVPHS,NPGVPHS     OLD VPH'S                                    
*                                                                               
         ZIC   R1,1(R3)            GET PROG ELEMENT LENGTH (ADDR IN R3)         
         AR    R1,R3                                                            
         CLI   0(R1),X'93'         TEST FOR NEW PROGRAM ELEMENT FOUND           
         BNE   PRGTOES4            NO                                           
         USING NPG2ELEM,R1                                                      
         MVI   NUEVLEN,119         ENLARGED VPH ELEMENT LENGTH                  
         MVI   NUEVELEN,X'42'      ENLARGED VPH CELL LENGTH                     
         MVC   NUEVPHS,NPG2VPHS    ENLARGED NUMBER OF VPH'S                     
         DROP  R1                                                               
         DROP  R2                                                               
*                                                                               
PRGTOES4 DS    0H                                                               
**PRGTOES4 GOTO1 NBHELLO,(R1),(C'D',=C'PAVFIL'),(X'33',DUMMYREC),0              
**         GOTO1 (RF),(R1),(C'P',=C'PAVFIL'),DUMMYREC,WORK                      
**       L     RF,=V(HELLO)                                                     
**       A     RF,RELO                                                          
         L     RF,NBHELLO                                                       
         LA    RE,3000             OVERRIDE PVFIL LENGTH                        
         LA    R1,DMCB                                                          
         ST    RE,16(R1)                                                        
         GOTO1 (RF),DMCB,(C'd',=C'PAVFIL'),(X'33',DUMMYREC),0,0                 
         CLI   DMCB+12,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
****     GOTO1 (RF),(R1),(X'97',=C'PAVFIL'),DUMMYREC,WORK,0,F'3000'             
         BAS   RE,GOPVFIL                                                       
*                                                                               
         MVC   DUB(2),DBACTBK      NOW LOOK UP THE HUT                          
         BAS   RE,SETDMPRE         SET DEMO PRECISSION                          
         GOTO1 NBDEMOUT,DMCB,(C'D',DIAHUT),DBLOCK,FULL                          
         MVC   DBACTBK,DUB                                                      
         TM    NPGSTAT,X'80'       TEST FOR RATING                              
         BZ    PRGTOES7            NO-SHARE HAS BEEN PROVIDED                   
*                                                                               
PRGTOES6 XC    WORK,WORK           YES-RATING IS GIVEN                          
         LA    R2,WORK                                                          
         USING DEOVD,R2                                                         
         MVI   DEOVEL,X'DD'        ESTIMATED OVERRIDE EL                        
         MVI   DEOVLEN,6           FOR                                          
         MVI   DEOVMOD,C'A'        FUDGE RATING HOMES MODIFIER TO               
         MVI   DEOVNUM,1           GET CORRECT RE-CALC OF EVN RECD.             
         SR    R1,R1               SCALE THE OVERRIDE VALUE                     
         ICM   R1,3,NPGSHARE                                                    
         MH    R1,=H'10'                                                        
         STCM  R1,3,DEOVVAL                                                     
*        GOTO1 NBHELLO,DMCB,(C'P',=C'PAVFIL'),DUMMYREC,WORK                     
****     L     RF,=V(HELLO)                                                     
****     A     RF,RELO                                                          
****     GOTO1 (RF),DMCB,(X'97',=C'PAVFIL'),DUMMYREC,WORK,0,F'3000'             
         BAS   RE,GOPVFIL                                                       
*                                                                               
         MVC   DEOVMOD(2),SHARE    COMPUTE AND ADD SHARE OVERRIDE               
         SR    R1,R1                                                            
         ICM   R1,3,NPGSHARE                                                    
         M     R0,=F'1000'         SCALE DIVIDEND                               
         OC    FULL,FULL           TEST FOR ZERO DIVISOR                        
         BZ    PRGTOES8                                                         
         SLDA  R0,1                                                             
         D     R0,FULL             SHARE=RATING/HUT                             
         AH    R1,=H'1'                                                         
         SRA   R1,1                ROUND TO ONE DECIMAL PLACE                   
         STCM  R1,3,DEOVVAL                                                     
         TM    NBINDS3,NBI3A2DC+NBI3B2DC                                        
***      BNO   NOROUND                                                          
         BZ    NOROUND                                                          
         SR    R0,R0                                                            
         AH    R1,=H'5'                                                         
         D     R0,=F'10'                                                        
         STCM  R1,3,DEOVVAL                                                     
NOROUND  EQU   *                                                                
*        GOTO1 NBHELLO,DMCB,(C'P',=C'PAVFIL'),DUMMYREC,WORK                     
****     L     RF,=V(HELLO)                                                     
****     A     RF,RELO                                                          
****     GOTO1 (RF),DMCB,(X'97',=C'PAVFIL'),DUMMYREC,WORK,0,F'3000'             
         BAS   RE,GOPVFIL                                                       
         B     PRGTOES8                                                         
*                                                                               
PRGTOES7 XC    WORK,WORK                                                        
         LA    R2,WORK                                                          
         USING DEOVD,R2                                                         
         MVI   DEOVEL,X'DD'        OVERRIDE FOR SHARE                           
         MVI   DEOVLEN,6                                                        
         MVC   DEOVMOD(2),SHARE                                                 
         MVC   DEOVVAL,NPGSHARE                                                 
*        GOTO1 NBHELLO,DMCB,(C'P',=C'PAVFIL'),DUMMYREC,WORK                     
***      L     RF,=V(HELLO)                                                     
***      A     RF,RELO                                                          
***      GOTO1 (RF),DMCB,(X'97',=C'PAVFIL'),DUMMYREC,WORK,0,F'3000'             
         BAS   RE,GOPVFIL                                                       
*                                                                               
         SR    R1,R1                                                            
         ICM   R1,3,NPGSHARE       GET THE SHARE                                
         M     R0,FULL             RATING=SHARE*HUT                             
         AH    R1,=H'500'                                                       
         D     R0,=F'1000'                                                      
         MH    R1,=H'10'           SCALE THE OVERRIDE VALUE                     
         STCM  R1,3,DEOVVAL                                                     
         MVI   DEOVMOD,C'A'        FUDGE RATING HOMES MODIFIER TO               
         MVI   DEOVNUM,1           GET CORRECT RE-CALC OF EVN RECD.             
*        GOTO1 NBHELLO,DMCB,(C'P',=C'PAVFIL'),DUMMYREC,WORK                     
***      L     RF,=V(HELLO)                                                     
***      A     RF,RELO                                                          
***      GOTO1 (RF),DMCB,(X'97',=C'PAVFIL'),DUMMYREC,WORK,0,F'3000'             
         BAS   RE,GOPVFIL                                                       
*                                                                               
PRGTOES8 LA    R1,DUMMYREC         RESET DBLOCK TO POINT TO DUMMYREC            
         ST    R1,DBAREC                                                        
         LA    R1,23(R1)                                                        
         ST    R1,DBAQUART                                                      
*                                  DO THIS TO SEED ADJUSTED HOMES               
*                                  IN RECORD WITHOUT AN OVERRIDE                
*                                  ELEMENT ON THE OUTPUT                        
         GOTO1 NBDEMMTH,DMCB,=C'REC',DUMMYREC,DUMMYREC,MATHFACS                 
*                                                                               
*        GOTO1 NBHELLO,(R1),(C'D',=C'PAVFIL'),(X'DD',DUMMYREC),0                
*                                                                               
*        L     RF,=V(HELLO)                                                     
*        A     RF,RELO                                                          
         L     RF,NBHELLO                                                       
         LA    RE,3000             OVERRIDE PVFIL REC LENGTH                    
         LA    R1,DMCB                                                          
         ST    RE,16(R1)                                                        
         GOTO1 (RF),DMCB,(C'd',=C'PAVFIL'),(X'DD',DUMMYREC),0,0                 
         CLI   DMCB+12,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         B     SUBRXIT                                                          
*                                                                               
         DROP  R2,R3                                                            
* COMMON ROUTINE FOR C'p' CALL                                                  
GOPVFIL  NTR1                                                                   
*        L     RF,=V(HELLO)                                                     
*        A     RF,RELO                                                          
         L     RF,NBHELLO                                                       
         GOTO1 (RF),DMCB,(C'p',=C'PAVFIL'),DUMMYREC,WORK,0,F'3000'              
         CLI   DMCB+12,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         XIT1                                                                   
*                                                                               
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.8.7                                                    *         
*  TITLE: OVERHOME - USED WHEN HOME DEMOS ARE FROM A DIFFERENT PLACE  *         
*                     THAN OTHER DEMOS. THE OTHER DEMOS ARE ASSUMED TO*         
*                     BE IN DUMMYREC (AS VPH ELEMENTS),               *         
*                     THE DESIRED HOME VALUES ARE IN THE HOMES AREA OF*         
*                     DOALIST. THIS CREATES OVERRIDE ELEMENTS IN      *         
*                     DUMMYREC, AND MERGES THEM WITH THE HOME ELEMENT.*         
*                                                                     *         
*                    NOW ALSO CREATES OVERRIDE ELEMENTS FOR ANY       *         
*                    CABLE HOMES VALUES                               *         
*                                                                     *         
*  CALLED FROM: STPUTEM - ANYTIME BLACKWEEK VPHS EXIST                *         
*                                                                     *         
*                                                                     *         
*  CALLS TO: HELLO - USED TO ADD OVERRIDE ELEMENTS                    *         
*            DEMOMATH - GETS RID OF OVERRIDE ELEMENTS, BUT MERGES THE *         
*                     THE OVERRIDE VALUES INTO THE DEMO ELEMENTS.     *         
*                                                                     *         
*  INPUTS: DUMMYREC - CONTAINS THE HOME ELEMENT TO BE CHANGED.        *         
*          DOALIST - LIST CONTAINING HOME DEMOS TO OVERRIDE           *         
*                                                                     *         
*                                                                     *         
*  GLOBAL OUTPUTS: DUMMYREC - WILL CONTAIN MODIFIED HOME DEMOS        *         
*                                                                     *         
*  LOCALS: R7 - POINTS TO OVERRIDE ELEMENT TO BE ADDED                *         
*          RF - ADDRESS OF HELLO                                      *         
*          R5 - POINTS TO CURRENT INPUT DEMO SET                      *         
*          R6 - POINTS TO CURRENT OUTPUT DEMO SET                     *         
*                                                                     *         
*                                                                     *         
*                                                                     *         
***********************************************************************         
         USING DEOVD,R7                                                         
OVERHOME DS    0H                                                               
         LA    R7,DEELEM           AREA WHERE ELEMENT IS BUILT                  
*                                                                               
         MVI   DEOVEL,X'DE'        OVERRIDE ELEMENT                             
         MVI   DEOVLEN,DEELLEN     LENGTH OF DE ELEMENT                         
         MVI   AFTERDE,X'00'       MARK END OF ELEMENT FOR HELLO                
*                                                                               
         MVC   DEOVMOD(2),DIAVHOME+1   MODIFIER,DEMO NUMBER                     
         MVC   DEOVVAL(2),DOAVHOME+2    DEMO VALUE                              
*                                                                               
         BAS   R4,OHHELLO                                                       
         CLI   DMCB+12,0                                                        
         BE    OH10                                                             
         DC    H'0'                                                             
*                                                                               
OH10     MVC   DEOVMOD(2),DIAGHOME+1   MODIFIER,DEMO NUMBER                     
         MVC   DEOVVAL(2),DOAGHOME+2   DEMO VALUE                               
         BAS   R4,OHHELLO                                                       
         CLI   DMCB+12,0                                                        
         BE    OH15                                                             
*                                                                               
OH15     MVC   DEOVMOD(2),DIAIHOME+1   MODIFIER,DEMO NUMBER                     
         MVC   DEOVVAL(2),DOAIHOME+2   DEMO VALUE                               
         BAS   R4,OHHELLO                                                       
         CLI   DMCB+12,0                                                        
         BE    OH20                                                             
         DC    H'0'                                                             
*                                                                               
OH20     MVC   DEOVMOD(2),DIASHARE+1   MODIFIER,DEMO NUMBER                     
         MVC   DEOVVAL(2),DOASHARE+2   DEMO VALUE                               
         BAS   R4,OHHELLO                                                       
         CLI   DMCB+12,0                                                        
         BE    OHCBEG                                                           
         DC    H'0'                                                             
*                                                                               
OHCBEG   OC    NBADEM,NBADEM       DOES NET DEMO BLOCK EXIST?                   
         BZ    OHEND                                                            
         L     R2,NBADEM                                                        
         USING NDDEMBLK,R2                                                      
         SR    R3,R3                CHECK FOR CABLE DEMOS                       
         ICM   R3,1,NDNDEMOS                                                    
         BZ    OHEND                                                            
         DROP  R2                                                               
*                                                                               
         LA    R5,DIADMSET                                                      
         LA    R6,DOADMSET           FOR EACH DEMO SET                          
OHCLOP   CLI   2(R5),16               IF ITS A CABLE DEMO                       
         BNE   OHCNEXT                                                          
         MVI   DEOVMOD,C'R'           RTG MODIFIER                              
         MVI   DEOVMOD+1,16           CABLE HOMES                               
         MVC   DEOVVAL(2),2+ALGRPOFF(R6)     DEMO VALUE                         
         BAS   R4,OHHELLO                                                       
         CLI   DMCB+12,0                                                        
         BE    OHC4                                                             
         DC    H'0'                                                             
OHC4     MVI   DEOVMOD,C'T'           RTG MODIFIER                              
         MVI   DEOVMOD+1,16           CABLE HOMES                               
         MVC   DEOVVAL(2),2+ALIMPOFF(R6)     DEMO VALUE                         
         BAS   R4,OHHELLO                                                       
         CLI   DMCB+12,0                                                        
         BE    OHCNEXT                                                          
         DC    H'0'                                                             
OHCNEXT  LA    R5,9(R5)             3 SETS OF 3 BYTE DEMO IDS                   
         LA    R6,ALLENGTH(R6)                                                  
         BCT   R3,OHCLOP                                                        
*                                                                               
** NOW CONDENSE INTO THE DEMO ELEMENTS. GET RID OF OVERRIDE ELEMS.              
OHEND    XC    DBLOCK,DBLOCK                                                    
         MVC   DBFILE,=C'NTI'                                                   
         LA    R7,DUMMYREC                                                      
         ST    R7,DBAREC                                                        
         LA    R1,23(R7)           A(FIRST ELEMENT OF DUMMYREC)                 
         ST    R1,DBAQUART                                                      
         MVC   DBCOMFCS,NBACOM                                                  
         BAS   RE,INITMATH         INITIALIZE MATHFACS BLOCK                    
         GOTO1 NBDEMMTH,DMCB,=C'REC',DUMMYREC,DUMMYREC,MATHFACS                 
*                                  NOW DELETE ALL OVERRIDE ELEMS                
*        GOTO1 NBHELLO,DMCB,(C'D',=C'PAVFIL'),(X'DE',DUMMYREC),0                
**       L     RF,=V(HELLO)                                                     
**       A     RF,RELO                                                          
         L     RF,NBHELLO                                                       
         LA    RE,3000             OVERRIDE PVFIL REC LEN                       
         LA    R1,DMCB                                                          
         ST    RE,16(R1)                                                        
         GOTO1 (RF),DMCB,(C'd',=C'PAVFIL'),(X'DE',DUMMYREC),0,0                 
         CLI   DMCB+12,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         B     SUBRXIT                                                          
         SPACE 1                                                                
*OHHELLO  GOTO1 NBHELLO,DMCB,(C'P',=C'PAVFIL'),DUMMYREC,(R7),0                  
*                                                                               
OHHELLO  DS    0H                                                               
**       L     RF,=V(HELLO)                                                     
**       A     RF,RELO                                                          
         L     RF,NBHELLO                                                       
         GOTO1 (RF),DMCB,(C'p',=C'PAVFIL'),DUMMYREC,(R7),0,F'3000'              
         CLI   DMCB+12,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         BR    R4                                                               
         DROP  R7                                                               
         EJECT                                                                  
***********************************************************************         
*  TITLE: USEFORM                                                     *         
*                                                                     *         
*  COMMENTS: ADJUST ALL DEMOS IN DUMMYREC BY (ACT HH RTG / EST HH RTG)*         
*                                                                     *         
*  CALLED FROM: F FLAVOR                                              *         
*                                                                     *         
*  CALLS TO: DEMOUT - GET THE ACTUAL HH RATING                        *         
*            DEMOMATH- PERFORM THE ADJUSTMENT                         *         
*                                                                     *         
*  INPUTS: DUMMYREC - CONTAINS ESTIMATED DEMOS                        *         
*          DOALIST - CONTAINS ACTUAL HH RATING                        *         
*                                                                     *         
*  GLOBAL OUTPUTS: DUMMYREC - CHANGED TO REFLECT FORMULA              *         
*                                                                     *         
* LOCAL OUTPUTS:  DBLOCK - FILLS IN VALUES NEEDED BY DEMOUT           *         
*                 MATHBLOK- FILLS IN VALUES NEEDED BY DEMOMATH        *         
*                 ESTHOME - THE ESTIMATED HOMES RATING                *         
***********************************************************************         
USEFORM  DS    0H                                                               
         XC    DBLOCK,DBLOCK       GET GARBAGE OUT OF DBLOCK                    
         MVC   DBFILE,=C'EVN'      TO WANT ESTIMATE DEMOS                       
         MVC   DBCOMFCS,NBACOM     A(COMFACS)                                   
         ST    R7,DBAREC           A(UNIT RECORD)                               
         MVC   DBAQUART,ELEM1      A(1ST ELEMENT)                               
*                                  GET ESTIMATED HOMES RATING                   
         BAS   RE,SETDMPRE         SET DEMO PRECISSION                          
         GOTO1 NBDEMOUT,DMCB,(C'D',=XL3'00D901'),DBLOCK,ESTHOME                 
*                                                                               
         XC    DBLOCK,DBLOCK       SET DBLOCK FOR DEMOMATHCK                    
         MVC   DBFILE,=C'NTI'      DUMMYREC CONTAINS ACTUALS                    
         MVC   DBCOMFCS,NBACOM     A(COMFACS)                                   
         LA    R1,DUMMYREC                                                      
         ST    R1,DBAREC           A(DUMMYREC)                                  
         LA    R1,23(R1)           A(1ST ELEMENT)                               
         ST    R1,DBAQUART                                                      
*                                                                               
         BAS   RE,INITMATH         SET UP MATHBLOCK                             
*                                                                               
         MVC   MATHFCTR,DOAGHOME   MULT BY ACT HOMES RTG                        
         GOTO1 NBDEMMTH,DMCB,=C'MULT',DUMMYREC,DUMMYREC,MATHFACS                
         MVC   MATHFCTR,ESTHOME    DIVIDE BY EST HOMES RTG                      
         GOTO1 NBDEMMTH,DMCB,=C'DIV',DUMMYREC,DUMMYREC,MATHFACS                 
XITUSEF  B     SUBRXIT                                                          
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.5.1                                                    *         
*  TITLE: GETDEMOS - GET DEMOS FROM DEMAND INTO DUMMYREC              *         
*                                                                     *         
*  CALLED FROM: STCKEX - CHECK IF EXIST STATE OF DODEMS               *         
*                                                                     *         
*  CALLS TO:                                                          *         
*   INITMATH - INITIALIZE MATHFAC BLOCK                               *         
*   INITDUM - INITIALIZE A DUMMY RECORD                               *         
*   DEMAND (DEMHOOK) - ADD SELECTED RECORDS IN DUMMY RECORD           *         
*   DEMOMATH - TO AVERAGE VALUES NOW IN DUMMY RECORD                  *         
*   DEMOUT                                                            *         
*                                                                     *         
*  INPUTS : DBLOCK - FILLED BY PREVIOUS ROUTINE TO REQUEST APPROPRIATE*         
*                      DEMOS.                                         *         
*           DIALIST - REQUESTED DEMOS.                                *         
*           DIULIST - REQUESTED UNIVERSES                             *         
*                                                                     *         
*  GLOBAL OUTPUTS: DUMMYREC - PAV RECORD CONTAINING ACTUAL VALUES     *         
*                  DOALIST - THE DEMO VALUES LOOKED UP                *         
*                  DOULIST - THE UNIVERSE VALUES LOOKED UP            *         
*                  STATUS - Y - IF RECORDS FOUND                      *         
*                           N - IF NONE FOUND                         *         
***********************************************************************         
GETDEMOS DS    0H                                                               
         BAS   RE,INITDUM          SET UP DUMMY RECORD                          
         BAS   RE,INITMATH         SET UP MATHFAC BLOCK FOR DEMOMATH            
         BRAS  RE,TSTVTOOH         OOH/OOHC VIEWING TYPE?                       
         JNE   *+8                                                              
         BRAS  RE,SETOOH           YES - SET DBLOCK FOR IT                      
*                                                                               
         MVC   SVDBTYPE,DBBTYPE                                                 
         CLI   NBTCARLV,0          TCAR LOOK UP?                                
         BE    *+8                                                              
         MVI   DBBTYPE,C'V'        FORCE BKTYPE V                               
*                                                                               
         CLI   NBTCARLV,C'5'       LESS THAN THIS NEEDS TO BE WB1 FILE          
         BL    WBOK                                                             
         CLC   DBSELSTA(4),=C'WB  ' WB NEEDS TO BE WB1 FILE                     
         BE    WBOK                                                             
         MVI   DBBTYPE,C'T'                                                     
WBOK     DS    0C                                                               
*                                                                               
         MVI   CABNAD,C'N'                                                      
         CLI   NBSURVEY,C'C'                                                    
         BNE   *+8                                                              
         CLI   HAVNAD,C'Y'                                                      
         BNE   *+8                                                              
         MVI   CABNAD,C'Y'         SET CABLE NAD INDICATOR                      
CNADOK   DS    0H                                                               
*                                                                               
         MVC   SAVMED,DBSELMED                                                  
*                                                                               
         CLI   CABNAD,C'Y'                                                      
         BNE   GETD2                                                            
         MVC   SAVFCT,DBFUNCT     - NEEDED FOR CABLE NAD -                      
         MVC   SAVSEL,DBSELECT                                                  
         LA    RE,SAVNTIPR        SAVE AREA FOR PROGRAMS/TRACKS/TCASTS          
         ST    RE,ANTIPR                                                        
         LA    RF,SAVNTIPX-SAVNTIPR                                             
         XCEF                                                                   
         LA    RE,SAVSB           TEMP SAVE AREA FOR TCASTS (SPEC/BKTS)         
         ST    RE,ASBPR                                                         
         XC    SAVSB,SAVSB                                                      
         XC    PREVNTI,PREVNTI    PREVIOUS NTI NUMBER                           
*                                                                               
GETD2    DS    0H                                                               
         CLC   =C'RLD',DBFILE                                                   
         BNE   *+8                                                              
         MVI   DBFUNCT,DBGETRLD                                                 
         MVI   DEMHKCTR,0          CLEAR DEMHOOK COUNTER                        
         XC    PDWEIGHT,PDWEIGHT   CLEAR POD WEIGHT TOTAL                       
         GOTO1 NBDEMAND,DMCB,DBLOCK,DEMHOOK                                     
*                                                                               
         CLI   CABNAD,C'Y'                                                      
         BNE   *+14                                                             
         MVC   NTIDIVSR,DBDIVSOR   FOR CABLE NAD                                
         BRAS  RE,ADDSB            ADD SPEC/BKOUTS FOR LAST PROGRAM             
*                                                                               
         MVC   DBBTYPE,SVDBTYPE    RESTORE POSSIBLY CLOBBERD BKTYPE             
         MVC   DMCB+20(4),DBACTSTA                                              
         MVI   STATUS,C'N'         INITIALIZE TO NONE FOUND                     
         OC    DBDIVSOR,DBDIVSOR   CK IF ANY FOUND                              
         BNZ   GETD2A                                                           
*                                                                               
         CLI   DBSELSTA+4,C'T'     PKT PC LK UP?                                
         BNE   XITGTDM                                                          
         MVC   WORK(5),DBSELDAY                                                 
         XC    DBSELDAY(5),DBSELDAY  CLEAR DAYS/TIMES/PRG# FOR DEMAND           
         MVI   DBSTYPE,0                                                        
         MVI   DBFUNCT,DBVLNBK                                                  
         GOTO1 NBDEMAND,DMCB,DBLOCK,0                                           
         CLI   DBACTSTA+4,C'D'     WAS THIS A DAILY BOOK?                       
         BNE   *+8                                                              
         MVI   STATUS,C'D'         D=DAILY LK UP BUT NOTHING FOUND              
         MVC   DBSELDAY(5),WORK                                                 
         B     XITGTDM                                                          
*                                                                               
GETD2A   CLI   MIRDIV,0                                                         
         BE    GETD3               DON'T DIV BY 0                               
         CLC   MIRDIV,DEMHKCTR     IF DEMAND RETURNED FEWER                     
         BNH   *+10                RECS THAN MIRDIV                             
         MVC   MIRDIV,DEMHKCTR     USE THE REC RETURNED NUMBER                  
GET2DB   SR    R0,R0                                                            
         ZICM  R1,DBDIVSOR,2                                                    
         ZIC   RF,MIRDIV                                                        
         DR    R0,RF                                                            
         STCM  R1,3,DBDIVSOR                                                    
*                                                                               
GETD3    MVI   STATUS,C'Y'         RECORDS FOUND                                
         MVC   MATHFCTR+2(2),DBDIVSOR                                           
         TM    NBVTYPS3,X'08'      MXM POD?                                     
         BNO   *+10                                                             
         MVC   MATHFCTR,PDWEIGHT                                                
         XC    PDWEIGHT,PDWEIGHT                                                
*                                                                               
         GOTO1 NBDEMMTH,DMCB,=C'DIV',DUMMYREC,DUMMYREC,MATHFACS                 
*                                                                               
         TM    NBUNST2,X'08'       IF NOT CABLE DELETE 49 ELEM                  
         BO    GETD5                                                            
         CLI   DBSELSTA+4,C'Q'     UNLESS DEMO FILE CABLE QH                    
         BE    GETD5                                                            
         CLI   DBSELSTA+4,C'C'     UNLESS DEMO FILE CABLE HH                    
         BE    GETD5                                                            
         CLI   DBSELSTA+4,C'H'     ALSO FOR HISPANIC                            
         BE    GETD5                                                            
*        GOTO1 NBHELLO,DMCB,(C'D',=C'PAVFIL'),(X'49',DUMMYREC),0                
**       L     RF,=V(HELLO)                                                     
**       A     RF,RELO                                                          
****     L     RF,NBHELLO                                                       
****     LA    RE,3000             OVERRIDE PVFIL REC LENGTH                    
****     LA    R1,DMCB                                                          
****     ST    RE,16(R1)                                                        
****     GOTO1 (RF),DMCB,(C'D',=C'PAVFIL'),(X'49',DUMMYREC),0,0                 
         BAS   RE,DELET49                                                       
         CLI   DMCB+12,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
GETD5    CLI   HAVBTU,C'O'         CHECK FOR OPI  FILE NEEDED                   
         BE    *+12                                                             
         CLI   HAVBTU,C'Y'         CHECK FOR TVQ  FILE NEEDED                   
         BNE   GETD51                                                           
*                                                                               
         XC    DBEXTEND,DBEXTEND                                                
         XC    BTUEXT(12),BTUEXT                                                
         BRAS  RE,BTUREADX                                                      
         CLI   GOTUTYPE,1                                                       
         BNE   GETD51                                                           
         MVC   BTUEXT,=C'UFIL'                                                  
         MVC   BTUEX2,BTUDB+4      SET DBAREC                                   
         XC    DBLOCK,DBLOCK                                                    
         LA    RE,BTUEXT                                                        
         ST    RE,DBEXTEND                                                      
         B     GETD52                                                           
*                                                                               
GETD51   XC    DBLOCK,DBLOCK       SET DBLOCK FOR DEMOUT FROM DUMMYREC          
GETD52   DS    0H                                                               
         MVC   DBCOMFCS,NBACOM     A(COMFACS)                                   
         LA    R1,DUMMYREC                                                      
         ST    R1,DBAREC           A(DUMMYREC)                                  
         LA    R1,23(R1)           FIRST ELEMENT OF DUMMYREC                    
         ST    R1,DBAQUART         A(1ST ELEM ON UNIT REC)                      
         BAS   RE,SETDMPRE         SET DEMO PRECISSION                          
         MVC   DBFILE,=C'NTI'      GET ACTUAL DEMOS                             
         MVI   DBFUNCT,0           IN CASE MXM SET IT FOR RLD                   
         GOTO1 NBDEMOUT,DMCB,(C'L',DIALIST),DBLOCK,DOALIST                      
         GOTO1 NBDEMOUT,DMCB,(C'L',DIULIST),DBLOCK,DOULIST                      
*                                                                               
         CLI   CABNAD,C'Y'         FOR CABLE NAD,                               
         BNE   GETD54              SAVE CABLE NTI US HOMES                      
         GOTO1 NBDEMOUT,DMCB,(C'D',NTIHOMEQ),DBLOCK,NTIHOMES                    
*                                                                               
GETD54   DS    0H                                                               
         BC    0,XITGTDM           <-----REMOVE FOR NAD LOOKUPS                 
*                                                                               
         CLI   HAVNAD,C'Y'         NAD/NHT DEMOS REQUESTED                      
         BNE   XITGTDM                                                          
         CLI   NBVTYPS,C'M'        IF MXM - DON'T DO NAD                        
         BE    XITGTDM                                                          
         OC    DINDMSET(3),DINDMSET ANY NAD DEMS FROM DEF RECORD                
         BZ    XITGTDM              NO - JUST EXIT                              
*                                                                               
GETNAD   DS    0H                  LK UP NAD/NHT DEMOS                          
*                                                                               
*NADWORK  EQU   DUMMYREC+1600       ACCUMULATED NAD DEMO VALUES                 
*NADWRKU  EQU   DUMMYREC+1600                                                   
*NADWRKV  EQU   DUMMYREC+1680       NADWORK+4*20                                
*NADWRKR  EQU   DUMMYREC+1760                                                   
*NADWRKT  EQU   DUMMYREC+1840                                                   
*NADWORKX EQU   4*4*20              LENGTH OF DEMOVALS BUFFER                   
*                                                                               
*NADWRKU2 EQU   DUMMYREC+1680       OVER UNUSED NADWRKVD                        
*NADWRKT2 EQU   DUMMYREC+1760       OVER UNUSED NADWRKRD                        
***                                                                             
NADWORK  EQU   DUMMYREC            ACCUMULATED NAD DEMO VALUES                  
NADWRKU  EQU   1600                                                             
NADWRKV  EQU   1800       NADWORK+4*50                                          
NADWRKR  EQU   2000                                                             
NADWRKT  EQU   2200                                                             
NADWORKX EQU   4*4*50              LENGTH OF DEMOVALS BUFFER                    
*                                                                               
NADWRKU2 EQU   1800                OVER UNUSED NADWRKVD                         
NADWRKT2 EQU   2000                OVER UNUSED NADWRKRD                         
*                                                                               
         CLI   CABNAD,C'Y'                                                      
         BNE   *+12                                                             
         BRAS  RE,GETCNAD          LOOK UP CABLE NAD DEMOS                      
         B     XITNAD                                                           
*                                  LOOK UP OTHER NAD DEMOS                      
*                                                                               
         MVC   SAVBTYPE,DBBTYPE    SAVE NTI BOOK TYPE                           
         MVC   DMCB+12(5),DBACTSTA                                              
         GOTO1 ASUBR,DMCB,('STPRDEME',(RC))   ASK FOR PROGRAM DEMOS             
*                                  STPRDEME CALLS INITDB RESETS DBLOCK.         
*                                  DBAREC NOW PTS BACK TO WORKREC               
         MVC   DBFILE,=C'NAD'                                                   
         MVI   DBSELSTA+3,C'P'     PROGRAM AVE                                  
         MVI   DBSELSTA+4,C'N'     NAD                                          
         MVI   DBBTYPE,0                                                        
** PXZ   CLI   NBPOSTYP,C'S'       SET FOR SYNDICATION IF REQ.                  
         CLI   NBSURVEY,C'S'       SET FOR SYNDICATION IF REQ.                  
         BNE   *+16                                                             
         MVC   DBSELSTA(4),DMCB+20                                              
         MVC   DBSELSTA+3(2),=C' M'                                             
*                                                                               
         MVC   SAVMED,DBSELMED     WKLY LK UP CLOBBERS DBSELMED                 
** PXZ   CLI   NBPOSTYP,C'H'       SET FOR HISPANIC IF REQ.                     
         CLI   NBSURVEY,C'H'       SET FOR HISPANIC IF REQ.                     
         BNE   GETND03                                                          
         MVC   DBSELSTA+3(2),=C' H'                                             
         CLI   NBUSER1+3,C'H'      WKLY NHT, DON'T CONVERT BK                   
         BE    GETND07                                                          
         CLI   NBUSER1+3,C'B'                                                   
         BE    GETND07                                                          
***      CLI   NBUSER1+3,C'D'                                                   
***      BE    GETND05                                                          
*                                                                               
GETND03  CLI   NBUSER1+3,C'D'      NAD WKLY LK UP                               
         BE    *+12                                                             
         CLI   NBUSER1+3,C'B'                                                   
         BNE   GETND05             MONTHLY LK UP, CONVERT BK                    
         CLC   DBSELBK,=X'6324'    NO WKLY NAD PRIOR TO SEP1/99                 
         BL    *+12                                                             
         MVI   DBSELMED,C'W'       WEEKLY NAD                                   
         B     GETND07             DON'T SET A MONTHLY BOOK                     
*                                 SET DEFAULT NAD/NHT BOOK                      
GETND05  GOTO1 NBDATCON,DMCB,(0,EBCDDAT),(3,DUB)                                
         MVC   DBSELBK+1(1),DUB+1  BOOK IS MONTH                                
**       L     RE,=A(NADMON)       SCAN TABLE FOR ACTUAL NAD/NHT DATES          
**       A     RE,RELO                                                          
         L     R2,NBACOM                                                        
         USING COMFACSD,R2                                                      
         GOTO1 CDEMTABS,DMCB,MTHDATES                                           
         ICM   R2,15,0(R1)   R2->MONTH/DATES TABLE                              
         USING MTHDATD,R2                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         L     R1,4(R1)            LENGTH OF TABLE ENTRY                        
*                                                                               
         CLC   DUB(3),MDEDATE      LE END DATE                                  
         BNH   *+10                                                             
         AR    R2,R1               NO TRY NEXT                                  
         B     *-12                                                             
         CLC   DUB(3),MDSDATE      LE START DATE                                
         BL    *+10                YES USE DEFAULT BOOK                         
         MVC   DBSELBK(2),MDNLBOOK    EOT WILL SET BK=9913                      
         DROP  R2                                                               
*                                                                               
GETND07  DS    0C                                                               
         MVI   BYTE,0              SET FOR UNIVERSE CONTROL                     
*                                                                               
GETND10  LA    RF,NADWORKX        ACCUM U/V/R/T DEMO VALUES IN NADWORK          
         LA    RE,NADWORK                                                       
         LA    RE,NADWRKU(RE)                                                   
         XCEF                                                                   
         XC    NHTUNIV(20),NHTUNIV                                              
         GOTO1 NBDEMAND,DMCB,DBLOCK,NADHOOK                                     
         OC    DBDIVSOR,DBDIVSOR                                                
         BNZ   GETND15             DEMOS FOUND                                  
         CLI   DBBTYPE,0           IF 1ST RECD NOT FOUND, WE'RE DONE            
         BE    XITNAD                                                           
         B     GETND25             ELSE LK FOR OPTNLS                           
*                                                                               
GETND15  MVI   NBRESULN,C'D'      DEMOS FOUND, INDICATE FROM DEMO FILE          
         CLI   BYTE,0              ONLY PROCESS UNIVS ONCE                      
***      BNE   *+16                                                             
         BNE   GETND17                                                          
         MVI   FULL,C'U'           SET UNIVERSE MODIFIER                        
         LA    R4,NADWORK                                                       
         LA    R4,NADWRKU(R4)      PT TO UNIV BUFFER                            
         BAS   RE,UWGTREL          UNWEIGHT VALUES AND RELS DE ELEM             
*                                                                               
GETND17  MVI   FULL,C'V'           VPH'S                                        
         LA    R4,NADWORK                                                       
         LA    R4,NADWRKV(R4)                                                   
         BAS   RE,UWGTREL                                                       
*                                                                               
** PXZ   CLI   NBPOSTYP,C'H'       FOR NHTI, RELS RTGS AND IMPS                 
         CLI   NBSURVEY,C'H'       FOR NHTI, RELS RTGS AND IMPS                 
         BNE   GETND20                                                          
         MVI   FULL,C'R'                                                        
         LA    R4,NADWORK          RTGS                                         
         LA    R4,NADWRKR(R4)      RTGS                                         
         BAS   RE,UWGTREL                                                       
         MVI   FULL,C'T'                                                        
         LA    R4,NADWORK          IMPS                                         
         LA    R4,NADWRKT(R4)      IMPS                                         
         BAS   RE,UWGTREL                                                       
*                                                                               
GETND20  DS    0H                  BUMP DBTYPE TO READ OTHER MKTBRKS            
         MVI   BYTE,2              UNIVERSES FIRST PASS ONLY                    
         CLI   DBBTYPE,18          SCAN ALL RECORDS W/REG MKTBRK DATA           
         BNL   GETND25             NOW SCAN OPTN MKT BRK RECDS                  
         ZIC   RE,DBBTYPE                                                       
         LA    RE,1(RE)                                                         
         STC   RE,DBBTYPE                                                       
         B     GETND10             GO CALL DEMAND AGAIN                         
*                                                                               
*  GETND25  CLI   NBPOSTYP,C'H'       NO OPTNAL ON NHTI FILE, JUST EXIT         
GETND25  CLI   NBSURVEY,C'H'       NO OPTNAL ON NHTI FILE, JUST EXIT            
         BE    XITNAD                                                           
         CLI   DBBTYPE,C'P'        WE'VE READ ALL REG+OPT MKT BRKS              
         BE    XITNAD                                                           
         CLI   DBBTYPE,C'O'        DONE W/1ST SET OF OPT'LS?                    
         BNE   *+12                                                             
         MVI   DBBTYPE,C'P'        NOW READ NEXT SET OF OPT'LS                  
         B     GETND10                                                          
         MVI   DBBTYPE,C'O'        READ 1ST OPTIONAL RECD                       
         B     GETND10                                                          
*                                                                               
XITNAD   MVC   DBFILE,=C'NTI'      GET ACTUAL DEMOS                             
         MVC   DBSELMED,SAVMED     RESTORE ORIG MEDIA                           
         MVC   DBBTYPE,SAVBTYPE                                                 
         MVC   DBCOMFCS,NBACOM     A(COMFACS)                                   
         LA    R1,DUMMYREC                                                      
         ST    R1,DBAREC           A(DUMMYREC)                                  
         LA    R1,23(R1)           FIRST ELEMENT OF DUMMYREC                    
         ST    R1,DBAQUART         A(1ST ELEM ON UNIT REC)                      
         LM    R4,R5,DUB                                                        
         PRINT NOGEN                                                            
*                                                                               
XITGTDM  DS    0H                                                               
         B     SUBRXIT                                                          
*                                                                               
         EJECT                                                                  
*---------------------------------------------------------------------          
*UWGTREL - UNWGT NAD/NHT DEMOS ACCUM IN HOOK.                                   
*        CREATE X'DE' OVERRIDE ELEMS AND ADD TO DUMMYREC                        
*        INUT: FULL=MODIFIER,R4->BUFFER W/VALUES                                
*---------------------------------------------------------------------          
UWGTREL  ST    RE,MYRE                                                          
         SR    RE,RE                                                            
         ICM   RE,3,DBDIVSOR                                                    
         ST    RE,DUB              FULL WORD ALLIGN                             
         LA    R5,DINDMSET                                                      
UWGTR10  CLI   0(R5),X'FF'         EOL                                          
         BE    UWGTRELX            DONE WITH THIS BUFFER                        
         CLI   0(R5),0             ONLY RELS NAD DEMOS (MKT BRK SET)            
         BE    UWGTNXT                                                          
         SR    RE,RE                                                            
         ICM   RF,15,0(R4)                                                      
         BZ    UWGTNXT             BYPASS ZERO VALUES                           
         SLDA  RE,1                                                             
         D     RE,DUB                                                           
         AHI   RF,1                                                             
         SRA   RF,1                                                             
         STCM  RF,15,0(R4)         RESLOT UNWGTD DEMO VALUE IN BUFFER           
         LTR   RF,RF                                                            
         BZ    UWGTNXT             DON'T CREATE DE FOR ZERO VALUES              
*                                                                               
         LA    RE,WORK             BLD DE OVERRIDE ELEM W/NAD/NHT VALUE         
         XC    0(30,RE),0(RE)                                                   
         USING NUOVEL,RE                                                        
         MVC   NUOVEL(3),=X'DE0C00'                                             
         MVC   NUOVCAT,0(R5)       MKT BRK                                      
         MVC   NUOVMOD,FULL        MODIFIER                                     
         MVC   NUOVNUM,2(R5)       DEMO NUMBER                                  
         MVI   NUOVFLG,X'80'       ACTUAL DEMO                                  
         MVI   NUOVPRE,X'81'       PRECISION OF FIELD                           
         MVC   NUOVVAL,0(R4)                                                    
*                                                                               
         LA    RF,NSPREC1          SET FIELD PRECISION                          
         CLI   CABNAD,C'Y'         CABLE NAD GETS CABLE PRECISION               
         BNE   *+8                                                              
         LA    RF,COPREC1                                                       
         CLC   NUOVMOD,0(RF)                                                    
         BE    *+12                                                             
         LA    RF,2(RF)                                                         
         B     *-14                                                             
         MVC   NUOVPRE,1(RF)                                                    
         DROP  RE                                                               
*                                 ADD OVERRIDE TO CUMMULATED NTI RECD           
*        GOTO1 NBHELLO,DMCB,(C'P',=C'PAVFIL'),DUMMYREC,WORK                     
**       L     RF,=V(HELLO)                                                     
**       A     RF,RELO                                                          
         L     RF,NBHELLO                                                       
         GOTO1 (RF),DMCB,(C'p',=C'PAVFIL'),DUMMYREC,WORK,0,F'3000'              
         CLI   DMCB+12,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
UWGTNXT  LA    R5,3(R5)            BUMP DEMO CODE POINTER                       
         LA    R4,4(R4)             AND VALUE POINTER                           
         B     UWGTR10                                                          
*                                                                               
UWGTRELX L     RE,MYRE                                                          
         BR    RE                                                               
*                                                                               
*---------------------------------------------------------------------          
*UWGTFLD - UNWGT DEMO FIELD                                                     
*          R4-> DEMO FIELD                                                      
*---------------------------------------------------------------------          
UWGTFLD  ST    RE,MYRE                                                          
         SR    RE,RE                                                            
         ICM   RE,3,DBDIVSOR                                                    
*                                                                               
         ST    RE,DUB              FULL WORD ALLIGN                             
         SR    RE,RE                                                            
         ICM   RF,15,0(R4)                                                      
         SLDA  RE,1                                                             
         D     RE,DUB                                                           
         AHI   RF,1                                                             
         SRA   RF,1                                                             
         STCM  RF,15,0(R4)         SAVE UNWGTD CABLE NAD HOMES                  
*                                                                               
UWGTFLDX L     RE,MYRE                                                          
         BR    RE                                                               
***********************************************************************         
*                                                                     *         
*  THE ROUTINES THAT FOLLOW ARE ONLY CALLED FROM ROUTINES WITHIN SUBR *         
*                                                                     *         
***********************************************************************         
***********************************************************************         
*  TITLE: DEMHOOK  - HOOK ROUTINE FOR ADDING RECORDS GIVEN BY DEMAND. *         
*                                                                     *         
*  CALLED FROM: DEMAND                                                *         
*                                                                     *         
*  GLOBAL OUTPUTS: DUMMYREC                                           *         
***********************************************************************         
DEMHOOK  EQU   *                                                                
         ST    RE,SAVERE           SAVE RETURN ADDRESS                          
*                                                                               
         BC    0,DEMHK30           JUST SO I CAN TEST                           
*                                                                               
         ZIC   R1,DEMHKCTR         HOW MANY RECS RETREIVED                      
         LA    R1,1(R1)                                                         
         STC   R1,DEMHKCTR                                                      
*                                                                               
         CLI   CABNAD,C'Y'         FOR CABLE NAD                                
         BNE   *+8                                                              
         BRAS  RE,GETCNTI          SAVE CABLE NTI NO'S IN SAVNTIPR              
*                                                                               
         MVI   HAVDAY,C'N'                                                      
         CLI   DBSELSTA+4,C'D'     IF DAYLY KEEP TRACK OF IT                    
         BNE   *+8                                                              
         MVI   HAVDAY,C'Y'                                                      
*                                                                               
         CLI   DBSELSTA+4,C'C'     IF CABLE                                     
         BE    DEMHK30             KEEP ALL ELEMENTS                            
*                                                                               
         CLI   DBSELSTA+4,C'Q'     IF CABLE QUARTER HOUR                        
         BE    DEMHK30             KEEP ALL ELEMENTS                            
*                                                                               
         CLI   DBSELSTA+4,C'H'     IF HISPANIC                                  
         BE    DEMHK30             KEEP ALL ELEMENTS                            
*                                                                               
         CLI   DBBTYPE,C'V'        TCAR LK UP? (WB1)                            
         BE    DEMHK30              ONLY KEEP ELEMS IN CHOSEN SECTION           
         CLI   NBTCARLV,0          TCAR LOOK UP? (REG)                          
         JNE   *+12                                                             
         CLI   DBBTYPE,C'T'                                                     
         BE    DEMHK30                                                          
*                                                                               
         CLI   NBUSER2+4,C'Y'                                                   
         BE    DEMHK5              GET GROSS PROG AVERAGES                      
*                                                                               
DEMHK0   L     R6,DBAQUART         START AT FIRST DEMO ELEMENT                  
DEMHK1   CLI   0(R6),0                                                          
         BE    DEMHK20                                                          
*                                                                               
         CLI   0(R6),X'4A'         TAKE OUT CALC. TPPUT ELEMENTS                
         BL    DEMHK3                                                           
         CLI   0(R6),X'4E'                                                      
         BL    DEMHK2                                                           
*                                                                               
         CLI   0(R6),X'53'        TAKE OUT TP HUT/PUT AND GAA ELEMENTS          
         BL    DEMHK3              AND LEAVE ALL OTHERS                         
         CLI   0(R6),X'5C'                                                      
         BH    DEMHK20                                                          
DEMHK2   MVI   0(R6),X'FF'                                                      
DEMHK3   BAS   RE,DEMHKBMP         BUMP THROUGH THE ELEMENTS                    
         B     DEMHK1                                                           
*                                                                               
*--CHECK FOR D LINE ELEMENTS                                                    
*                                                                               
DEMHK5   L     R6,DBAQUART         START AT FIRST DEMO ELEMENT                  
DEMHK5A  CLI   0(R6),0                                                          
         BE    DEMHK0                                                           
         CLI   0(R6),X'55'         ENSURE GAA EXITS                             
         BE    DEMHK5B             IN CASE MULTIPASS                            
         ZIC   RF,1(R6)                                                         
         AR    R6,RF                                                            
         B     DEMHK5A                                                          
*                                                                               
DEMHK5B  L     R6,DBAQUART         RESTART AT FIRST ELEMENT                     
*                                                                               
DEMHK6   CLI   0(R6),0                                                          
         BE    DEMHK0                                                           
         CLI   0(R6),X'54'                                                      
         BL    DEMHK12                                                          
         CLI   0(R6),X'5B'                                                      
         BH    DEMHK12                                                          
*                                                                               
         L     R6,DBAQUART         START AT FIRST DEMO ELEMENT                  
         OC    DBSELPRG,DBSELPRG   IF PROGRAM CODE NOT THERE                    
         BZ    *+8                 LEAVE RESULT ALONE                           
         MVI   RESULT,C'J'         SET D LINE RESULT                            
DEMHK9   CLI   0(R6),0                                                          
         BE    DEMHK20                                                          
*                                                                               
         CLI   0(R6),X'40'         TAKE OUT CALC. TPPUT ELEMENTS                
         BL    DEMHK11             AND PROGRAM PUTS                             
         CLI   0(R6),X'54'                                                      
         BL    DEMHK10                                                          
*                                                                               
         CLI   0(R6),X'54'        TAKE OUT TP HUT/PUT AND GAA ELEMENTS          
         BL    DEMHK11             AND LEAVE ALL OTHERS                         
         CLI   0(R6),X'5B'                                                      
         BH    DEMHK11                                                          
         ZIC   RE,0(R6)                                                         
         SH    RE,=H'20'                                                        
         STC   RE,0(R6)                                                         
         B     DEMHK11                                                          
DEMHK10  MVI   0(R6),X'FF'                                                      
DEMHK11  BAS   RE,DEMHKBMP         BUMP THROUGH THE ELEMENTS                    
         B     DEMHK9                                                           
DEMHK12  BAS   RE,DEMHKBMP                                                      
         B     DEMHK6                                                           
*                                                                               
DEMHK20  DS    0H                                                               
**       GOTO1 NBHELLO,DMCB,(C'D',=C'PAVFIL'),(X'FF',DBAREC),0                  
         L     RF,NBHELLO                                                       
         LA    RE,3000             OVERRIDE PVFIL REC LENGTH                    
         LA    R1,DMCB                                                          
         ST    RE,16(R1)                                                        
         GOTO1 (RF),DMCB,(C'd',=C'PAVFIL'),(X'FF',DBAREC),0,0                   
         CLI   DMCB+12,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
DEMHK30  TM    NBVTYPS3,X'08'       MXM POD                                     
         BNO   DEMHK40                                                          
         XC    PDMCMINF,PDMCMINF                                                
         XC    PDPODINF,PDPODINF                                                
         GOTO1 NBDEFINE,DMCB,=C'MCOMSEC',DBLOCK,PDMCMINF                        
         GOTO1 NBDEFINE,DMCB,=C'PODINFO',DBLOCK,PDPODINF                        
**ONEH135B CLI   OPTSCWGT,OPTSWPD    POD SECONDS WEIGHTING                      
**         BNE   ONEH135X                                                       
         LHI   RE,60               MIDDLE MINUTES HAVE WEIGHT 60 SECS           
         CLI   PDPD1LST,0                                                       
         BE    ONEH135C                                                         
         ZIC   RE,PDMCOMSC         COMM SECONDS FOR 1ST AND LAST MINUTE         
         ZIC   RF,PDMPROSC         + PROMO SECONDS                              
         AR    RE,RF                                                            
         ZIC   RF,PDMPSASC         + PSA SECONDS                                
         AR    RE,RF                                                            
ONEH135C STCM  RE,15,MATHFCTR                                                   
         ICM   RF,15,PDWEIGHT      POD WEIGHT ACCUMULATOR                       
         AR    RF,RE                                                            
         STCM  RF,15,PDWEIGHT                                                   
         B     *+10                                                             
*                                                                               
DEMHK40  MVC   MATHFCTR+2(2),DBFACTOR    NUMBER OF TIME PERIODS COVERED         
         CLI   DBSELSTA+4,C'H'                                                  
         BNE   DEMHSPX                                                          
         ICM   RE,15,DBSPANAD                                                   
         BZ    DEMHSPX                                                          
*                                                                               
         CLC   0(4,RE),=C'NTHU'    SAVE THE UNIVERSE RECORD                     
         BNE   DEMHSPX                                                          
         LA    RE,4(RE)                                                         
         USING PRKEY,RE                                                         
         LA    RE,PRFRSTEL                                                      
         SR    RF,RF                                                            
DEMHSP1  CLI   0(RE),0                                                          
         BE    DEMHSPX                                                          
         CLI   0(RE),X'23'                                                      
         BE    DEMHSP2                                                          
         IC    RF,1(RE)                                                         
         AR    RE,RF                                                            
         B     DEMHSP1                                                          
DEMHSP2  IC    RF,1(RE)                                                         
         AR    RE,RF                                                            
         CLI   0(RE),X'5E'                                                      
         BE    DEMHSPX                                                          
         CLI   0(RE),0                                                          
         BE    DEMHSPX                                                          
         CLI   0(RE),X'4B'                                                      
         BE    *+12                                                             
         CLI   0(RE),X'4C'                                                      
         BNE   DEMHSP2                                                          
         LR    R6,RE                                                            
*                                                                               
**       GOTO1 NBHELLO,DMCB,(C'P',=C'PAVFIL'),DBAREC,(R6),0                     
**       L     RF,=V(HELLO)                                                     
**       A     RF,RELO                                                          
         L     RF,NBHELLO                                                       
         GOTO1 (RF),DMCB,(C'p',=C'PAVFIL'),DBAREC,(R6),0,F'3000'                
         CLI   12(R1),0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         SR    RF,RF                                                            
         LR    RE,R6                                                            
         CLI   0(RE),X'4C'                                                      
         BNE   DEMHSP2                                                          
DEMHSPX  DS    0C                                                               
*                                                                               
         CLI   DBSELSTA+4,C'C'                                                  
         BNE   DEMCABX                                                          
         ICM   RE,15,DBSPANAD                                                   
         BZ    DEMCABX                                                          
*                                                                               
         CLC   0(3,RE),=C'CAB'     DIG OUT THE LEVEL/CONVERT TO ALPHA           
         BNE   DEMCABX                                                          
         MVI   NBDEMLEV,C'P'       DEFAULT TO PROGRAM                           
         CLI   3(RE),1                                                          
         BNE   *+8                                                              
         MVI   NBDEMLEV,C'T'       TRACK                                        
         CLI   3(RE),2                                                          
         BNE   *+8                                                              
         MVI   NBDEMLEV,C'S'       TELECAST(SOLO)                               
DEMCABX  DS    0H                                                               
*                                                                               
         CLI   NBTCARLV,0          TCAR LOOK UP? (REG)                          
         JE    DEMTCARX                                                         
         CLI   DBBTYPE,C'T'                                                     
         JE    *+12                                                             
*                                                                               
         CLI   DBBTYPE,C'V'        TCAR LK UP? (WB)                             
         BNE   DEMTCARX                                                         
         XC    DUB,DUB                                                          
         ZIC   RF,NBTCARLV         GET TCAR TYPE (CHAR 1-9)                     
         AHI   RF,-240             -X'F0'                                       
         AHI   RF,180              23 ELEM LOOK UP VALUE:181-186                
         STC   RF,DUB                                                           
         SR    RF,RF                                                            
         L     RE,DBAQUART                                                      
DEMTCR05 CLI   0(RE),0             END OF RECORD?                               
         BE    DEMTCR20                                                         
         CLI   0(RE),X'23'         LOOK FOR TCAR MATCH                          
         BL    DEMTCR15            KEEP AND BUMP TO NEXT ELEM                   
         BNE   DEMTCR10                                                         
         MVI   0(RE),X'FF'         DELETE 23 ELEM ITSELF                        
         MVI   DUB+4,C'D'          DELETE SECTION                               
         CLC   DUB(1),2(RE)        ARE WE WITHIN DESIRED TCAR SECTION?          
         BNE   *+12                                                             
         MVI   DUB+4,C'S'          SAVE SECTION                                 
         B     DEMTCR15                                                         
DEMTCR10 CLI   0(RE),X'5E'         DELETE ALL EXTRANEOUS ELEMS                  
         BNH   *+8                                                              
         MVI   DUB+4,C'D'                                                       
*                                                                               
         CLI   DUB+4,C'D'          DELETE ELEMS NOT IN SECTION                  
         BNE   *+8                                                              
         MVI   0(RE),X'FF'         DELETE ELEM -> RE                            
*                                                                               
DEMTCR15 IC    RF,1(RE)            BUMP THROUGH ELEMS ON RECD                   
         AR    RE,RF                                                            
         B     DEMTCR05                                                         
*                                                                               
DEMTCR20 DS    0H                                                               
*        GOTO1 NBHELLO,DMCB,(C'D',=C'PAVFIL'),(X'FF',DBAREC),0                  
**       L     RF,=V(HELLO)                                                     
**       A     RF,RELO                                                          
         L     RF,NBHELLO                                                       
         LA    RE,3000             OVERRIDE PVFIL REC LENGTH                    
         LA    R1,DMCB                                                          
         ST    RE,16(R1)                                                        
         GOTO1 (RF),DMCB,(C'd',=C'PAVFIL'),(X'FF',DBAREC),0,0                   
         CLI   DMCB+12,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
DEMTCARX DS    0H                                                               
*                                                                               
* FOR MINUTE BY MINUTE, THERE ARE SOME DEMO RECORDS WITH ALL ZERO DEMOS         
* AND AS A RESULT WITH NO '5E' ELEMENT. DEMAND HAS BEEN CHANGED TO              
* APPEND A '5E' ELEMENT IN THIS CASE, BUT DEMOMATH DROPS IT IF ALL              
* DEMOS ARE ZERO. THE CODE BELOW SAVES AROUND THE '5E' ELEMENT FROM             
* THE DEMO RECORD, AND IF THE RECORD AFTER DEMOMATH DOESN'T HAVE A              
* '5E' ELEMENT, IT RESTORES THE PREVIOUSLY STORED ELEMENT.                      
* NOTE: THE ABSENCE OF THE '5E' ELEMENT CAUSES DEMEL/DEMOMATH TO BE             
* CONFUSED ABOUT THE FILE/MEDIA/SOURCE.                                         
         CLI   NBVTYPS,C'M'        IS THIS MINUTE BY MINUTE LOOKUP?             
         BNE   *+8                                                              
         BRAS  RE,SAV5E            YES. SAVE THE '5E' ELEMENT FROM              
*                                  THE DEMO RECORD                              
         GOTO1 NBDEMMTH,DMCB,=C'MAD',DBAREC,DUMMYREC,MATHFACS                   
*                                                                               
         CLI   NBVTYPS,C'M'        IS THIS MINUTE BY MINUTE LOOKUP?             
         BNE   *+8                                                              
         BRAS  RE,PUT5E            YES. RESTORE THE '5E' ELEMENT IN             
*                                  DUMMY REC, IF IT DOESN'T EXIST               
         L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
DEMHKBMP ZIC   RF,1(R6)            BUMP THROUGH THE ELEMENTS                    
         AR    R6,RF                                                            
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
*NADHOOK - FOR NAD AND NHTI: SAVE WGTD DEMOS IN NADWORK                         
*        FOR NHTI, SAVE UNIVERSES IN NHTUNIV BUFFER                             
***********************************************************************         
NADHOOK  ST    RE,SAVERE                                                        
         CLI   DBSELSTA+4,C'H'     SAVE NHTI UNIV FOR DEMOUT                    
         BNE   NADHK10                                                          
         ICM   RE,15,DBSPANAD                                                   
         BZ    NADHK10                                                          
         CLC   0(4,RE),=C'NTHU'    SAVE THE UNIVERSE RECORD                     
         BNE   NADHK10                                                          
         CLC   NHTUNIV(4),0(RE)                                                 
         BE    NADHK10                                                          
         LA    R1,NHTUNIV                                                       
         LA    RF,9                                                             
         MVC   0(200,R1),0(RE)                                                  
         LA    R1,200(R1)                                                       
         LA    RE,200(RE)                                                       
         BCT   RF,*-14                                                          
*                                                                               
NADHK10  DS    0H                  SET MODIFIER FOR DEMO LK UPS                 
         CLI   BYTE,0              ONLY LK UP UNIVS FOR 1ST NAD RECD            
         BNE   NADHK12                                                          
         MVI   FULL,C'U'                                                        
         LA    R4,NADWORK          UNIVERSES IN NADWORK                         
         LA    R4,NADWRKU(R4)      UNIVERSES IN NADWORK                         
         BAS   RE,LKWGT            SLOT MODF/LKUP DEMS/WGT/ACCUM                
*                                                                               
NADHK12  MVI   FULL,C'V'                                                        
         LA    R4,NADWORK          UNIVERSES IN NADWORK                         
         LA    R4,NADWRKV(R4)      UNIVERSES IN NADWORK                         
         BAS   RE,LKWGT            SLOT MODF/LKUP DEMS/WGT/ACCUM                
*                                                                               
** PXZ   CLI   NBPOSTYP,C'H'       FOR NHTI, ALSO LK UP R AND T                 
         CLI   NBSURVEY,C'H'       FOR NHTI, ALSO LK UP R AND T                 
         BNE   NADHKX              DONE                                         
         MVI   FULL,C'R'           NHTI RATINGS                                 
         LA    R4,NADWORK                                                       
         LA    R4,NADWRKR(R4)                                                   
         BAS   RE,LKWGT                                                         
*                                                                               
         MVI   FULL,C'T'           NHTI IMPRESSIONS                             
         LA    R4,NADWORK                                                       
         LA    R4,NADWRKT(R4)                                                   
         BAS   RE,LKWGT                                                         
*                                                                               
NADHKX   L     RE,SAVERE                                                        
         BR    RE                                                               
                                                                                
*--------------------------------------------------------------------           
*LKWGT - SLOT MODIFIER SET IN FULL INTO DINDMSET, LOOK UP DEMOS                 
* AND ACCUM IN BUFFER PTD TO BY R4.  NOTE REGS NOT SAVED                        
*--------------------------------------------------------------------           
LKWGT    ST    RE,MYRE                                                          
*                                                                               
LKWGT20  DS    0H                                                               
         LA    R5,DINDMSET         SLOT MODIFIER IN FULL INTO DINDMSET          
         MVI   150(R5),X'FF'        PROTECT MYSELF - SET EOL                    
LKWGT25  CLI   0(R5),X'FF'                                                      
         BE    LKWGT30                                                          
         MVC   1(1,R5),FULL        SLOT MODIFIER INTO DEMO LIST                 
         LA    R5,3(R5)                                                         
         B     LKWGT25                                                          
*                                                                               
LKWGT30  LA    R5,DINDMSET         R5=DEMO LIST R4=BUCKET TO ACCUM IN           
         XC    WORK,WORK                                                        
         GOTO1 NBDEMOUT,MYDMCB,(C'L',(R5)),DBLOCK,WORK                          
         LA    R1,WORK                                                          
LKWGT35  CLI   0(R5),X'FF'         END OF DEMO LIST?                            
         BE    LKWGTX                                                           
         ICM   RF,15,0(R1)         PICK UP VALUE JUST FOUND                     
         SR    RE,RE                                                            
         ICM   RE,3,DBFACTOR       WGT IT                                       
         MR    RE,RE                                                            
         ICM   RE,15,0(R4)         PICK UP ACCUMULATED VALUE                    
         AR    RF,RE                                                            
         ST    RF,0(R4)            ACCUM AND STORE IN CORRECT BUFFER            
         LA    R1,4(R1)                                                         
         LA    R4,4(R4)                                                         
         LA    R5,3(R5)                                                         
         B     LKWGT35                                                          
*                                                                               
LKWGTX   L     RE,MYRE                                                          
         BR    RE                                                               
*--------------------------------------------------------------------           
*LKWGTFLD - LOOK UP NAD HOMES AND ACCUMULATE AT ADDRESS IN R4                   
*           NOTE REGS NOT SAVED                                                 
*--------------------------------------------------------------------           
LKWGTFLD ST    RE,MYRE                                                          
         GOTO1 NBDEMOUT,MYDMCB,(C'D',CBNHOMEQ),DBLOCK,WORK                      
         LA    R1,WORK                                                          
         ICM   RF,15,0(R1)         PICK UP VALUE JUST FOUND                     
         SR    RE,RE                                                            
         ICM   RE,3,DBFACTOR       WGT IT                                       
         MR    RE,RE                                                            
         ICM   RE,15,0(R4)         PICK UP ACCUMULATED VALUE                    
         AR    RF,RE                                                            
         STCM  RF,15,0(R4)         ACCUM AND STORE THE NAD HOMES(US)            
*                                                                               
         L     RE,MYRE                                                          
         BR    RE                                                               
         EJECT                                                                  
         EJECT                                                                  
*                                                                               
***********************************************************************         
*  NUMBER: NV1.7.3                                                    *         
*  TITLE: INITMATH - INITIALIZE MATHFAC BLOCK FOR USE BY DEMOMATH.    *         
*                                                                     *         
*  CALLED FROM: ALL ROUTINES PERFORMING DEMOMATH                      *         
*                                                                     *         
*  GLOBAL OUTPUTS: MATHFACS                                           *         
***********************************************************************         
INITMATH EQU   *                                                                
         XC    MATHFACS(MATHFACL),MATHFACS                                      
         LA    R1,DBLOCK                                                        
         ST    R1,MATHABLK                                                      
         MVC   MATHIFIL,DBFILE                                                  
         MVC   MATHOFIL,DBFILE                                                  
         MVC   MATHOSRC(1),DBSELSRC                                             
         CLI   DBSELSTA+4,C'Q'                                                  
         BE    *+8                                                              
         CLI   DBSELSTA+4,C'C'                                                  
         BNER  RE                                                               
         MVC   MATHIFIL,=C'CAB'    CABLE HAS DIFFERENT FORMULAS                 
         MVC   MATHOFIL,=C'CAB'    AND RECORD FORMAT                            
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.7.4                                                    *         
*  TITLE: INITDUM - INITIALIZE DUMMY RECORD FOR DEMOMATH. IN SOME     *         
*           SITUATIONS THIS DUMMY RECORD WILL LATER BE MERGED INTO THE*         
*           UNIT RECORD.                                              *         
*                                                                     *         
*  COMMENTS: CREATE A SKELETON PAV RECORD IN DUMMYREC                 *         
*             MAKE SURE X'00' AT END FOR HELLO                        *         
*                                                                     *         
*  CALLED FROM: ALL ROUTINES WHICH WILL CALL DEMOMTH                  *         
*                                                                     *         
*  GLOBAL OUTPUTS: DUMMY RECORD                                       *         
*                                                                     *         
***********************************************************************         
INITDUM  DS    0H                                                               
         XC    DUMMYREC(25),DUMMYREC   CLEAR SKELETON PAV                       
         MVI   DUMMYREC,C'P'           ID IT AS A PAV                           
         MVC   DUMMYREC+20(2),=H'24'   LENGTH OF RECORD                         
XITDUM   BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.4.4                                                    *         
*  TITLE: SETDMPRE - SET DBLOCK SO DEMAND WILL RETURN RATINGS         *         
*                    IN 2 DECIMAL PLACE PRECISION                     *         
*                                                                     *         
*  CALLED FROM: INITDB                                                *         
*               MANOUTS                                               *         
*               ACTOUTS                                               *         
*               ESTOUTS                                               *         
*               PRGTOEST                                              *         
*               USEFORM                                               *         
*               GETDEMOS                                              *         
*                                                                     *         
*  GLOBAL OUTPUTS: DBLOCK                                             *         
***********************************************************************         
SETDMPRE NTR1                                                                   
*                                                                               
         LA    R5,DBNETWXT         SET UP EXTENSION                             
         USING DBXNTID,R5                                                       
         XC    DBXNID(L'DBNETWXT),DBXNID                                        
         MVC   DBXNID(4),=CL4'NETW'                                             
         MVC   DBX0EVPH,NBUSER2+11 OPTION TO SUPPRESS CALCS                     
* TWO DECIMAL PRECISION AGENCY                                                  
         TM    NBINDS3,NBI3A2DC    TWO DECIMAL ON?                              
         BNO   SETD05              NO                                           
         TM    NBINDS3,NBI3B2DC    YES/  NEW BOOK?                              
         BO    SETD03                    YES NEW BOOK                           
         CLI   NBPOSTYP,C'N'              NO-OLD BOOK                           
         BE    SETD20                 DON'T SET FOR NETWORK/CABLE               
         CLI   NBPOSTYP,C'S'          HANDLE MYSELF AFTER DEMOUT                
         BE    SETD20                                                           
SETD03   MVI   DBXNNP1,C'Y'                                                     
         MVI   DBXNNS1,C'Y'                                                     
         MVI   DBXNCR2,C'Y'                                                     
         MVI   DBXNNR2,C'Y'                                                     
         MVI   DBXNHOPT,C'H'       H FOR 2 DEC PRECISION AGENCIES               
         B     SETD20                                                           
* CABLE PRECISION ONLY FOR CABLE                                                
SETD05   CLI   NBPOSTYP,C'N'                                                    
         BE    SETD20                                                           
         CLI   NBPOSTYP,C'S'                                                    
         BE    SETD20                                                           
SETD10   CLI   NBPREOPT,C'Y'       ARE WE USING CABLE PRECISION                 
         BNE   SETD20                                                           
         MVI   DBXNCR2,C'Y'                                                     
         MVI   DBXNNR2,C'Y'                                                     
*                                                                               
SETD20   OC    DBEXTEND,DBEXTEND                                                
         BNZ   *+12                                                             
         ST    R5,DBEXTEND                                                      
         B     XITDMPRE                                                         
*                                                                               
         L     RE,DBEXTEND                                                      
*                                                                               
         OC    4(4,RE),4(RE)       NEXT EXTENSION?                              
         BZ    *+12                NO MORE - LAST                               
         L     RE,4(RE)                                                         
         B     *-14                                                             
*                                                                               
         ST    R5,4(RE)                                                         
*                                                                               
XITDMPRE DS    0H                                                               
***********************************************************************         
***********************************************************************         
* ADD OR UPDATE 'NLIV' EXTENT FOR DEMAND                                        
***********************************************************************         
**       MVC   BYTE,NBVTYPS                                                     
**       OC    NBVTYPS,NBVTYPS      PROGRAM AVERAGE VTYPS                       
**       BNZ   NCAV02                                                           
**       OC    NBVTYPS2,NBVTYPS2    COMMERCIAL AVERAGE VTYPS                    
**       BZ    NLIVXTX              NO- EXIT                                    
**       MVC   BYTE,NBVTYPS2                                                    
**       B     NCAV02                                                           
*****************************************************************               
* WRITER USED TO PASS BYTE IN NBVTYPS OR NBVTYPS2 ***************               
* CHANGED TO PASS 2 CHARACTER VTYP AND HAVE TRANSLATION**********               
* HERE RATHER THAN IN CALLING MODULE*****************************               
*****************************************************************               
         MVI   BYTE,0                                                           
         OC    NBVTYPS(2),NBVTYPS                                               
         BZ    NLIVXTX                                                          
*                                                                               
         TM    NBVTYPS3,X'04'      IGNORE NBVTYPS?                              
         BO    NLIVXRS                                                          
*                                                                               
         BRAS  RE,TSTVTOOH         OOH/OOHC VIEWING TYPE?                       
         JE    NCAV02                                                           
         CLC   =C'PL',NBVTYPS      PL=DEFLAUT PROGRAM LIVE                      
         BE    NLIVXTX             SKIP                                         
         LA    R1,VTYPEQUS                                                      
NCAV01   CLC   0(2,R1),NBVTYPS                                                  
         BE    NCAV01A                                                          
         LA    R1,3(R1)                                                         
         CLI   0(R1),0                                                          
         BNE   NCAV01                                                           
         DC    H'0'                                                             
NCAV01A  OC    BYTE,2(R1)                                                       
         B     NCAV02                                                           
********************************************************************            
VTYPEQUS DC    C'PL',X'00',C'PS',X'01',C'P7',X'07'                              
         DC    C'P1',X'F1',C'P2',X'02',C'P3',X'03'                              
         DC    C'N3',X'FD'                                                      
VTYPEQU2 DC    C'CL',X'00',C'CS',X'01',C'C7',X'07'                              
         DC    C'C1',X'F1',C'C2',X'02',C'C3',X'03'                              
VTYPEQU3 DC    C'AL',X'FA',C'AS',X'FB',C'A7',X'FC'                              
VTYPEQU4 DC    C'ML',X'00',C'MS',X'01',C'M7',X'07'                              
         DC    C'M1',X'F1',C'M2',X'02',C'M3',X'03'                              
         DC    X'00'                                                            
********************************************************************            
* PROGRAM AVERAGE OR COMMERCIAL AVERAGE                                         
NCAV02   DS    0H                                                               
         ICM   RE,15,DBEXTEND                                                   
         USING DBXCAVD,RE                                                       
         LA    RF,CAVGEXT                                                       
****     TM    NBINDS7,X'02'       USE PASSED ADDR?                             
****     BZ    NCAV02B                                                          
***      L     RF,NBINVFLT         TESTING ON-LINE                              
***      LA    RF,9(RF)           BUMP OVER LABELS                              
*NCAV02B  CLC   DBXCAVID,CAVGEXT                                                
NCAV02B  CLC   DBXCAVID,=C'CAVG'                                                
         BE    NCAV04              GO CHANGE IT                                 
         ICM   RE,15,DBXCAVNX                                                   
         BZ    NCAV02D             GO ADD IT                                    
         B     NCAV02B                                                          
*                                                                               
NCAV02D  ICM   RF,15,DBEXTEND      ADD NEW EXTENT TO BEGINNING OF LIST          
         LA    RE,CAVGEXT                                                       
***      TM    NBINDS7,X'02'       USE PASSED ADDR?                             
***      BZ    *+12                                                             
***      L     RE,NBINVFLT         TEST                                         
***      LA    RE,9(RE)            BUMP TO CAVGEXT                              
         STCM  RE,15,DBEXTEND                                                   
         STCM  RF,15,4(RE)                                                      
*                                                                               
NCAV04   MVI   DBXCAV,0           UPDATE THE EXTENT DATA                        
         CLI   NBVTYPS,C'C'        COMMERCIAL AVERAGE?                          
         BNE   NCAV06                                                           
         OI    DBXCAV,X'01'        1=COMMERCIAL AVERAGE                         
         B     NCAV10                                                           
*                                                                               
NCAV06   CLI   NBVTYPS,C'M'        IF MINUTE BY MINUTE                          
         BNE   NCAV10                                                           
         CLC   DBFILE,=C'EVN'      AND NOT EVN                                  
         BE    NCAV10                                                           
         CLC   DBFILE,=C'EIN'      OR EIN                                       
         BE    NCAV10                                                           
*                                                                               
         MVC   DBFILE,=C'RLD'      SET DBFILE                                   
         MVI   DBFUNCT,DBGETRLD    SET DBFUNCT                                  
         CLI   DBSELSTA+4,C'Q'     IF QUARTER HOUR                              
         BNE   *+8                                                              
         MVI   DBSELSTA+4,C'C'     RESET TO CABLE                               
         B     NCAV10                                                           
*                                                                               
NCAV10   DS    0H                                                               
*                                                                               
                                                                                
* LIVE, SAME DAY , +1,+2,+3,+7                                                  
NLIV04   DS    0H                                                               
         ICM   RE,15,DBEXTEND                                                   
         USING DBXLIVD,RE                                                       
         LA    RF,NLIVEXT                                                       
****     TM    NBINDS7,X'02'       USE PASSED ADDR?                             
****     BZ    NLIV05                                                           
***      L     RF,NBINVFLT         TEST                                         
*NLIV05   CLC   DBXLIVID,NLIVEXT                                                
NLIV05   CLC   DBXLIVID,=C'NLIV'                                                
         BE    NLIV20              GO CHANGE IT                                 
         ICM   RE,15,DBXLIVNX                                                   
         BZ    NLIV15              GO ADD IT                                    
         B     NLIV05                                                           
*                                                                               
NLIV15   ICM   RF,15,DBEXTEND      ADD NEW EXTENT TO BEGINNING OF LIST          
         LA    RE,NLIVEXT                                                       
****     TM    NBINDS7,X'02'       USE PASSED ADDR?                             
****     BZ    *+8                                                              
****     L     RE,NBINVFLT         TEST                                         
         STCM  RE,15,DBEXTEND                                                   
         STCM  RF,15,4(RE)                                                      
*                                                                               
NLIV20   MVC   DBXLIVE,BYTE        UPDATE THE EXTENT DATA                       
         B     MPOD                                                             
*                                                                               
*                                                                               
* MINUTE BY MINUTE POD                                                          
MPOD     DS    0H                                                               
         TM    NBVTYPS3,X'08'      MXM POD                                      
         BNO   MPODX               NO                                           
         ICM   RE,15,DBEXTEND                                                   
         USING DBXPODM,RE                                                       
         LA    RF,DBXPODT                                                       
****     TM    NBINDS7,X'02'       USE PASSED ADDR?                             
****     BZ    MPOD05                                                           
****     L     RF,NBINVFLT         TEST                                         
MPOD05   CLC   DBXPMID,=C'PDMS'                                                 
         BE    MPOD20              GO CHANGE IT                                 
         ICM   RE,15,DBXPMNX                                                    
         BZ    MPOD15              GO ADD IT                                    
         B     MPOD05                                                           
*                                                                               
MPOD15   ICM   RF,15,DBEXTEND      ADD NEW EXTENT TO BEGINNING OF LIST          
         LA    RE,DBXPODT                                                       
****     TM    NBINDS7,X'02'       USE PASSED ADDR?                             
****     BZ    *+8                                                              
****     L     RE,NBINVFLT         TEST                                         
         STCM  RE,15,DBEXTEND                                                   
         STCM  RF,15,4(RE)                                                      
*                                                                               
MPOD20   MVC   DBXPMIN,NBAFFTIM    UPDATE EXTENT WITH AFFID TIME                
*                                                                               
MPODX    B     NLIVXTX                                                          
*                                                                               
NLIVXRS  NI    NBVTYPS3,X'FF'-X'04' CLEAR IGNORE VTYPS BIT                      
*                                                                               
NLIVXTX  XIT1                                                                   
         DROP  RE                                                               
*                                                                               
*                                  10 MAX DAY/TIMES - SEE PODDAYTM              
***NLIVEXT  DC    CL4'NLIV'                                                     
***         DC    XL5'00'             4(DBXLIVNX) + 1(DBXLIVE)                  
***CAVGEXT  DC    CL4'CAVG'                                                     
***         DC    XL5'00'             4(DBXCAVNX) + 1(DBXCAV)                   
***DBXPODT  DC    CL4'PDMS'                                                     
***         DC    XL6'00'             4(DBXPMNX) + 2(DBXPMIN)                   
         DROP  R5                                                               
*                                                                               
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.4.6                                                    *         
*  TITLE::SETDMPST - RESET THE X'33' ELEMENT TO THE WAY IT WAS        *         
*                    BEFOR THE SETDMPRE CALL                          *         
*                                                                     *         
*  CALLED FROM: MANOUTS                                               *         
*                                                                     *         
***********************************************************************         
SETDMPST NTR1                                                                   
         L     R5,DBAREC                                                        
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNITFIL  '),(X'39',(R5)),0                 
         CLI   DMCB+12,0           IF FOUND EXIT                                
         BNE   XITDMPST                                                         
         L     R5,12(R1)                                                        
         MVI   0(R5),X'33'                                                      
XITDMPST B     SUBRXIT                                                          
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.4.5                                                    *         
*  TITLE: SETDMSET - WIPE OUT VPH ELEMENT FROM DBAREC FOR RESULT=M    *         
*                                                                     *         
*  CALLED FROM: MANOUTS                                               *         
*                                                                     *         
*  GLOBAL OUTPUTS: DBLOCK                                             *         
*  IF NTI CALL WITH ACTUAL OVERRIDES, AND NO POSTING ELEMENT          *         
*  ALTER VPH ELEMENT X'33' TO VPH ELEMENT X'35'                       *         
***********************************************************************         
SETDMSET NTR1                                                                   
         CLC   DBFILE,=CL3'NTI'                                                 
         BNE   XITDMSET                                                         
*                                                                               
         L     R5,DBAREC                                                        
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNITFIL  '),(X'5E',(R5)),0                 
         CLI   DMCB+12,0           IF FOUND EXIT                                
         BE    XITDMSET                                                         
*                                                                               
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNITFIL  '),(X'DE',(R5)),0                 
         CLI   DMCB+12,0           IF NOT FOUND                                 
         BNE   XITDMSET                                                         
*                                                                               
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNITFIL  '),(X'33',(R5)),0                 
         CLI   DMCB+12,0           IF NOT FOUND                                 
         BNE   XITDMSET                                                         
         L     R5,12(R1)                                                        
         MVI   0(R5),X'39'                                                      
XITDMSET B     SUBRXIT                                                          
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
*  TITLE: SETCABLE - ADD X'49' AND X'5E' ELEMS TO UNIT REC WHEN       *         
*                    NECESSARY                                        *         
*                                                                     *         
*  CALLED FROM: SETDMPRE                                              *         
*                                                                     *         
*  INPUTS: R7 SHOULD POINT TO UNIT RECORD                             *         
*                                                                     *         
*  CABLE STATIONS WITH X'DE' (ACTUAL OVERRIDES) BUT NO X'5E' (ACT     *         
*  FORMAT) ELEMENT WILL HAVE X'49' ELEMENT AND VALUES AND X'5E'       *         
*  ELEMENT ADDED TO UNIT REC                                          *         
*                                                                     *         
***********************************************************************         
SETCABLE NTR1                                                                   
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNTFIL'),(X'DE',(R7)),0                    
         CLI   DMCB+12,0           IF NOT FOUND                                 
         BNE   XITCABLE                                                         
*                                                                               
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNTFIL'),(X'5E',(R7)),0                    
         TM    DMCB+12,X'06'       IF NOT FOUND                                 
         BZ    XITCABLE                                                         
*                                                                               
* GET ADDR OF DEMDISP (CORRES)                                                  
         GOTO1 NBDEMADR,DMCB,(X'FF',ATABLES),NBACOM                             
*                                                                               
         ZICM  R1,ATABLES+1,3      R1 TO TABLE HEADER                           
         ICM   R3,15,8(R1)         R3 = TABLE LENGTH                            
         LA    R3,0(R1,R3)         R3 TO END OF TABLE                           
         SR    R2,R2               DISP TO NXT ENTRY                            
         LA    R1,16(R1)           R1 TO 1ST DEMDISP ENTRY                      
         USING DSPHDRD,R1                                                       
*                                                                               
         CLC   =C'CNN',DSPFILE                                                  
         BE    *+18                                                             
         ICM   R2,7,DSPAET                                                      
         LA    R2,1(R2)                                                         
         BXLE  R1,R2,*-18                                                       
         DC    H'0'                NO CNN BOOK IN TABLE                         
*                                                                               
* BUILD X'5E' ELEM                                                              
         MVC   ELEM5E(2),=X'5E07'                                               
         MVC   ELEM5E+2(3),=C'CNN'                                              
         MVC   ELEM5E+5(2),DSPSBOOK                                             
         XC    ELEM5E+5(2),=X'FFFF'                                             
*                                                                               
         LR    R5,R1               SAVE R1                                      
         GOTO1 NBHELLO,DMCB,(C'P',=C'UNTFIL'),(R7),ELEM5E                       
         LR    R1,R5               RESTORE R1                                   
*                                                                               
         ZICM  R3,DSPAET,3         TABLE LENGTH                                 
         LA    R3,1(R1,R3)         R3 TO NEXT TABLE HEADER                      
         ZICM  R2,DSPLDE,2         DATA ELEM LENGTH                             
         LA    R1,DSPHDRLN(R1)     R1 TO DATA ELEMENT                           
         USING DSPDTAD,R1                                                       
*                                                                               
         CLI   DSPELCD,X'49'                                                    
         BE    *+10                                                             
         BXLE  R1,R2,*-8                                                        
         DC    H'0'                NO X'49' ELEM                                
*                                                                               
* HAVE X'49' ELEMS, BUILD DEMOUT INPUT LIST IN DIASC                            
         LA    R4,DIASC            INPUT TABLE                                  
         LA    R3,L'DIASC(R4)                                                   
         BCTR  R3,0                R3 TO END OF TABLE                           
         LA    R2,3                ENTRY LENGTH                                 
         SR    R5,R5               COUNT HOW MANY DEMOS IN LIST                 
*                                                                               
SC10     CLI   DSPELCD,X'49'       STILL ON X'49' ELS?                          
         BNE   SC20                                                             
         LA    R5,1(R5)            INC COUNTER                                  
         MVC   0(1,R4),DSPPREC     SET PRECISION                                
         MVI   1(R4),C'U'          CHANGE 'L' TO 'U' (MODIFIER CD)              
         MVC   2(1,R4),DSPDEMO     SET DEMO                                     
         LA    R1,5(R1)            ENTRIES ARE 5 BYTES LONG                     
         BXLE  R4,R2,SC10                                                       
         DC    H'0'                TABLE FULL                                   
         DROP  R1                                                               
*                                                                               
SC20     DS    0H                                                               
         MVI   0(R4),X'FF'         END OF DEMO LIST                             
         GOTO1 NBDEMOUT,DMCB,(C'P',DIASC),DBLOCK,DOASC                          
*                                                                               
* SET UP X'49' ELEM FOR UNIT REC                                                
         SLA   R5,2                (NUM DEMOS)*4                                
         LA    R5,3(R5)            EL CD, LEN, FLD LEN                          
         MVI   DREINFO,X'49'       DREINFO = X'49NN04'                          
         STC   R5,DREINFO+1                                                     
         MVI   DREINFO+2,X'44'                                                  
         GOTO1 NBHELLO,DMCB,(C'P',=C'UNTFIL'),(R7),DREINFO                      
*                                                                               
XITCABLE B     SUBRXIT                                                          
         EJECT                                                                  
BTUREAD  BRAS  RE,BTUREADX                                                      
         B     SUBRXIT                                                          
                                                                                
         LTORG                                                                  
*                                                                               
         EJECT                                                                  
* PARAMETERS                                                                    
***DEMHKCTR DS    CL1                                                           
*                                                                               
NTIVALS  DC    C'NTINPNN'          NTI OUTPUT FORMAT BLOCK                      
*                                                                               
SHARE    DC    C'S',X'01'                                                       
*                                                                               
NTIHOMEQ DC    XL3'00E801'         NTI HOMES (FOR DEMOUT)  'Y'                  
CBNHOMEQ DC    XL3'01E801'         CABLE NAD HOMES (FOR DEMOUT)  'Y'            
*                                                                               
BITTBL2  DC    X'80'               BIT POS 1 ON                                 
         DC    X'40'               BIT POS 2 ON                                 
         DC    X'20'               BIT POS 3 ON                                 
         DC    X'10'               BIT POS 4 ON                                 
*                                                                               
ATABLES  DC    X'D0',AL3(0)                                                     
         DC    X'FF'                                                            
*                           CALL LETTER EQUATES FOR AGENCY STATIONS             
*--DEMO OVERRIDE PRECISION TABLES                                               
COPREC1  DC    C'R',X'82',C'S',X'81',C'P',X'82',C'T',X'42',C'H',X'42'           
         DC    C'U',X'44',C'V',X'40'                                            
NSPREC1  DC    C'R',X'81',C'S',X'81',C'P',X'81',C'T',X'43',C'H',X'43'           
         DC    C'U',X'44',C'V',X'40'                                            
******************************************************                          
         SPACE 2                                                                
****   ++INCLUDE DENADMON                                                       
*ADMON   DC    AL1(92,08,30),AL1(92,09,27),AL1(92,09)                           
*        DC    AL1(92,09,28),AL1(92,10,25),AL1(92,10)                           
*        DC    AL1(92,10,26),AL1(92,11,22),AL1(92,11)                           
*        DC    AL1(92,11,23),AL1(92,12,27),AL1(92,12)                           
*        DC    AL1(92,12,28),AL1(93,01,24),AL1(93,01)                           
*        DC    AL1(93,01,25),AL1(93,02,21),AL1(93,02)                           
*        DC    AL1(93,02,22),AL1(93,03,28),AL1(93,03)                           
*        DC    AL1(93,03,29),AL1(93,04,25),AL1(93,04)                           
*        DC    AL1(93,04,26),AL1(93,05,30),AL1(93,05)                           
*        DC    AL1(93,05,31),AL1(93,06,27),AL1(93,06)                           
*        DC    AL1(93,06,28),AL1(93,07,25),AL1(93,07)                           
*        DC    AL1(93,07,26),AL1(93,08,29),AL1(93,08)                           
*        DC    AL1(93,08,30),AL1(93,09,26),AL1(93,09)                           
*        DC    AL1(93,09,27),AL1(93,10,24),AL1(93,10)                           
*        DC    AL1(93,10,25),AL1(93,11,21),AL1(93,11)                           
*        DC    AL1(93,11,22),AL1(93,12,26),AL1(93,12)                           
*        DC    AL1(93,12,27),AL1(94,01,30),AL1(94,01)                           
*        DC    AL1(94,01,31),AL1(94,02,27),AL1(94,02)                           
*        DC    AL1(94,02,28),AL1(94,03,27),AL1(94,03)                           
*        DC    AL1(94,03,28),AL1(94,05,01),AL1(94,04)                           
*        DC    AL1(94,05,02),AL1(94,05,29),AL1(94,05)                           
*        DC    AL1(94,05,30),AL1(94,07,03),AL1(94,06)                           
*        DC    AL1(94,07,04),AL1(94,07,31),AL1(94,07)                           
*        DC    AL1(94,08,01),AL1(94,09,04),AL1(94,08)                           
*        DC    AL1(94,09,05),AL1(94,10,02),AL1(94,09)                           
*        DC    AL1(94,10,03),AL1(94,10,30),AL1(94,10)                           
*        DC    AL1(94,10,31),AL1(94,11,27),AL1(94,11)                           
*        DC    AL1(94,11,28),AL1(95,01,01),AL1(94,12)                           
*        DC    AL1(95,01,02),AL1(95,01,29),AL1(95,01)                           
*        DC    AL1(95,01,30),AL1(95,02,26),AL1(95,02)                           
*        DC    AL1(95,02,27),AL1(95,03,26),AL1(95,03)                           
*        DC    AL1(95,03,27),AL1(95,04,30),AL1(95,04)                           
*        DC    AL1(95,05,01),AL1(95,05,28),AL1(95,05)                           
*        DC    AL1(95,05,29),AL1(95,06,25),AL1(95,06)                           
*        DC    AL1(95,06,26),AL1(95,07,30),AL1(95,07)                           
*        DC    AL1(95,07,31),AL1(95,09,03),AL1(95,08)                           
*        DC    AL1(95,09,05),AL1(99,12,30),AL1(99,13) EOF-FORCE BAD BK          
*                                                                               
         EJECT                                                                  
AGSTAEQU DS    0CL8          TABLE MUST BE KEPT IN SEQUENCE                     
         DC    C'CAB ',C'CAX '                                                  
         DC    C'FBC ',C'FOX '                                                  
         DC    C'IND ',C'INX '                                                  
         DC    C'NIK ',C'NICK'                                                  
         DC    C'PAY ',C'PAX '                                                  
         DC    C'PBS ',C'PBX '                                                  
         DC    C'TBS ',C'WTBS'                                                  
         DC    C'TCF ',C'FOX '                                                  
         DC    C'SUP ',C'SUX '                                                  
         DC    X'FF'                                                            
         DS    0F                                                               
         EJECT                                                                  
*                                                                               
* SET DBLOCK INFO IF OOH/OOHC VIEWING STREAMS                                   
*                                                                               
SETOOH   NTR1  BASE=*,LABEL=*                                                   
         ICM   RE,15,DBEXTEND                                                   
         USING DBXLIVD,RE                                                       
         LA    RF,NLIVEXT                                                       
*                                                                               
SOOH05   CLC   DBXLIVID,=C'NLIV'                                                
         JE    SOOH10              GO CHANGE IT                                 
         ICM   RE,15,DBXLIVNX                                                   
         JZ    *+8                                                              
         J     SOOH05                                                           
*                                                                               
         ICM   RF,15,DBEXTEND      ADD NEW EXTENT TO BEGINNING OF LIST          
         LA    RE,NLIVEXT                                                       
         STCM  RE,15,DBEXTEND                                                   
*                                                                               
SOOH10   CLI   DBSELSTA+4,C'T'                                                  
         JE    SOOH20                                                           
         CLI   DBFUNCT,DBVLNBK                                                  
         JE    SOOH20                                                           
         CLI   DBFUNCT,DBGETNTI                                                 
         JE    SOOH20                                                           
         MVI   DBFUNCT,DBVLNBK                                                  
*                                                                               
SOOH20   ICM   RE,15,DBEXTEND                                                   
         USING DBXCAVD,RE                                                       
SOOH22   CLC   DBXCAVID,=C'CAVG'                                                
         JE    SOOH24                                                           
         ICM   RE,15,DBXCAVNX                                                   
         JZ    SETOOHX                                                          
         J     SOOH22                                                           
*                                                                               
SOOH24   CLI   NBVTYPS,DBXLOCLQ    OOHC TYPES?                                  
         JE    *+8                                                              
         CLI   NBVTYPS,DBXLOCSQ                                                 
         JE    *+8                                                              
         CLI   NBVTYPS,DBXLOC3Q                                                 
         JE    *+8                                                              
         CLI   NBVTYPS,DBXLOC7Q                                                 
         JNE   SETOOHX                                                          
         OI    DBXCAV,X'01'        1=COMMERCIAL AVERAGE                         
*                                                                               
SETOOHX  J     XIT                                                              
*                                                                               
* TEST IF VTYPE= FILTER IS OOH                                                  
*         NBVTYPS (BYTE 0) = OOH VIEWING TYPE                                   
*         NBVTYPS+1 = X'00'                                                     
*                                                                               
TSTVTOOH NTR1  BASE=*,LABEL=*                                                   
         OC    NBVTYPS,NBVTYPS                                                  
         JZ    NO                                                               
         CLI   NBVTYPS+1,0                                                      
         JNE   NO                                                               
         CLI   NBVTYPS,DBXLOLQ     OOH LIVE?                                    
         JE    TVTOOH10                                                         
         CLI   NBVTYPS,DBXLOSQ     OOH LIVE+SD?                                 
         JE    TVTOOH10                                                         
         CLI   NBVTYPS,DBXLO3Q     OOH LIVE+3?                                  
         JE    TVTOOH10                                                         
         CLI   NBVTYPS,DBXLO7Q     OOH LIVE+7?                                  
         JE    TVTOOH10                                                         
         CLI   NBVTYPS,DBXLOCLQ    OOHC LIVE?                                   
         JE    TVTOOH10                                                         
         CLI   NBVTYPS,DBXLOCSQ    OOHC LIVE+SD?                                
         JE    TVTOOH10                                                         
         CLI   NBVTYPS,DBXLOC3Q    OOHC LIVE+3?                                 
         JE    TVTOOH10                                                         
         CLI   NBVTYPS,DBXLOC7Q    OOHC LIVE+7?                                 
         JNE   NO                                                               
TVTOOH10 MVC   BYTE,NBVTYPS                                                     
         J     YES                                                              
*                                                                               
YES      DS    0H                                                               
         CR    RB,RB                                                            
         J     SUBRXIT                                                          
NO       CHI   RB,0                                                             
         J     SUBRXIT                                                          
*                                                                               
***********************************************************************         
*  TITLE: CONOUT - AT ENTRY, DUMMYREC CONTAINS VPH'S FROM BACKWARD    *         
*           VPH AVERAGEING.  A USER OPTION SPECIFIES THAT THE         *         
*           UNIVERSES APPLY TO THE UNIT DATE.  FOR THIS OPTION, THE   *         
*           RECORD AT DUMMYREC MUST BE CONVERTED TO THE FORMAT OF THE *         
*           UNIT DATE TO INSURE CORRECT UNIVERSES AND TO DEALWITH     *         
*           POTENTIAL FILE FORMAT CHANGES.                            *         
*                                                                     *         
* CALLED FROM : DOBWBVPH - BACKWARD VPH AVERAGING (RESULTS C,R)       *         
*                                                                     *         
*  INPUTS: NBUSER1+9 - Y IF WE ARE SUPPOSED TO USE BOOK OF UNIT DATE  *         
*                                                                     *         
*  CALLS TO : DEMOMATH - TO EXPLODE DUMMYREC INTO THE UNIT DATE'S     *         
*               FORMAT AND THEN REPLACE THE DEMO ELEMENTS             *         
*             HELLO    - TO DELETE UNNECESSARY UNIVERSE ELEMENT       *         
*                                                                     *         
*  GLOBAL OUTPUTS: DUMMYREC - CONVERTED INTO UNIT DATE'S FORMAT       *         
*                                                                     *         
***********************************************************************         
XCONOUT  NTR1  BASE=*,LABEL=*                                                   
         CLI   NBUSER1+9,C'Y'      TEST FOR UNIT DATE FORMAT OPTION             
         BNE   XCONOUTX             NO                                          
         CLI   SAVEWEEK,33         TEST WEEKS 33 AND 34                         
         BE    CONOUT2                                                          
         CLI   SAVEWEEK,34                                                      
         BE    CONOUT2                                                          
         CLI   SAVEYEAR,87         AND WEEK 35 IN 1987                          
         BNE   XCONOUTX                                                         
         CLI   SAVEWEEK,35                                                      
         BNE   XCONOUTX                                                         
*                                                                               
* EXPLODE THE VPHS ON DUMMYREC INTO FORMAT OF THE UNIT DATE(SAVEBOOK)           
*                                                                               
CONOUT2  XC    DBLOCK,DBLOCK       DBLOCK DESCRIBES DUMMYREC                    
         MVC   DBFILE,=C'NTI'                                                   
         MVC   DBCOMFCS,NBACOM                                                  
         LA    RE,DUMMYREC                                                      
         ST    RE,DBAREC                                                        
         LA    RE,23(RE)                                                        
         ST    RE,DBAQUART                                                      
*                                                                               
         XC    OUTFORM,OUTFORM       OUTPUT FORMAT BLOCK =                      
         MVC   OUTFORM(7),NTIVALS    NTI VALUES +                               
         CLI   NBSURVEY,C'H'                                                    
         BNE   *+10                                                             
         MVC   OUTFORM+4(2),=C'NH'                                              
         MVC   OUTFORM+7(2),SAVEBOOK BOOK OF UNIT DATE                          
*                                                                               
         L     R5,AWORKREC                                                      
         GOTO1 NBDEMAIN,DMCB,=C'GET',DBLOCK,(R5),OUTFORM                        
         CLI   DBERROR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
* NOW REPLACE THE OLD DEMO ELEMENTS WITH THOSE IN UNIT DATE'S FORMAT            
*                                                                               
         GOTO1 NBDEMAIN,DMCB,=C'REP',DBLOCK,(R5),OUTFORM                        
         CLI   DBERROR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
* DELETE SPURIOUS UNIVERSE ELEMENT FROM NON-CABLE UNITS                         
*                                                                               
CONOUT4  TM    NBUNST2,X'08'       TEST FOR CABLE UNIT                          
         BO    XCONOUTX             YES                                         
         CLI   NBSURVEY,C'H'                                                    
         BE    XCONOUTX                                                         
*        GOTO1 NBHELLO,DMCB,(C'D',=C'PAVFIL'),(X'49',DBAREC),0                  
*        L     RF,=V(HELLO)                                                     
*        A     RF,RELO                                                          
         L     RF,NBHELLO                                                       
         GOTO1 (RF),DMCB,(C'd',=C'PAVFIL'),(X'49',DBAREC),0,0,F'3000'           
         CLI   DMCB+12,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
XCONOUTX XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
                                                                                
* SET MIRROR TIMES                                                              
SETMIRHR NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         XC    HALF,HALF                                                        
*                                                                               
         CLI   NBMIRTYP,C'A'                                                    
         BNE   MIR12                                                            
         MVC   HALF,=H'400'        +4 HOURS                                     
         MVI   MIRDIV,2            SET 2 DAY/TIME ENTRIES                       
         B     MIRXX                                                            
*                                                                               
MIR12    CLI   NBMIRTYP,C'B'       -5 HOURS                                     
         BNE   MIR13                                                            
         MVC   HALF,=H'-500'                                                    
         MVI   MIRDIV,2            SET 2 DAY/TIME ENTRIES                       
         B     MIRXX                                                            
*                                                                               
MIR13    CLI   NBMIRTYP,C'C'       -11 HOURS                                    
         BNE   MIR14                                                            
         MVC   HALF,=H'-1100'                                                   
         MVI   MIRDIV,2            SET 2 DAY/TIME ENTRIES                       
         B     MIRXX                                                            
*                                                                               
MIR14    CLI   NBMIRTYP,C'D'       -6 HOURS                                     
         BNE   MIR15                                                            
         MVC   HALF,=H'-0600'                                                   
         MVI   MIRDIV,2            SET 2 DAY/TIME ENTRIES                       
         B     MIRXX                                                            
*                                                                               
MIR15    CLI   NBMIRTYP,C'E'       -9 HOURS                                     
         BNE   MIR16                                                            
         MVC   HALF,=H'-0900'                                                   
         MVI   MIRDIV,2            SET 2 DAY/TIME ENTRIES                       
         B     MIRXX                                                            
*                                                                               
MIR16    CLI   NBMIRTYP,C'F'       +3 HOURS                                     
         BNE   MIR17                                                            
         MVC   HALF,=H'300'                                                     
         MVI   MIRDIV,2            SET 2 DAY/TIME ENTRIES                       
         B     MIRXX                                                            
*                                                                               
MIR17    CLI   NBMIRTYP,C'G'       +7 HOURS                                     
         BNE   MIR18                                                            
         MVC   HALF,=H'700'                                                     
         MVI   MIRDIV,2            SET 2 DAY/TIME ENTRIES                       
         B     MIRXX                                                            
*                                                                               
MIR18    CLI   NBMIRTYP,C'H'       -4 HOURS                                     
         BNE   MIR19                                                            
         MVC   HALF,=H'-0400'                                                   
         MVI   MIRDIV,2            SET 2 DAY/TIME ENTRIES                       
         B     MIRXX                                                            
*                                                                               
MIR19    CLI   NBMIRTYP,C'I'       +6.5 HOURS                                   
         BNE   MIR20                                                            
         MVC   HALF,=H'630'                                                     
         MVI   MIRDIV,2            SET 2 DAY/TIME ENTRIES                       
         B     MIRXX                                                            
*                                                                               
MIR20    CLI   NBMIRTYP,C'J'       +7.5 HOURS                                   
         BNE   MIR22                                                            
         MVC   HALF,=H'730'                                                     
         MVI   MIRDIV,2            SET 2 DAY/TIME ENTRIES                       
         B     MIRXX                                                            
*                                                                               
MIR22    CLI   NBMIRTYP,C'K'       +5 HOURS                                     
         BNE   MIR23                                                            
         MVC   HALF,=H'500'                                                     
         MVI   MIRDIV,2            SET 2 DAY/TIME ENTRIES                       
         B     MIRXX                                                            
*                                                                               
MIR23    CLI   NBMIRTYP,C'L'       +430 HOURS                                   
         BNE   MIR24                                                            
         MVC   HALF,=H'430'                                                     
         MVI   MIRDIV,2            SET 2 DAY/TIME ENTRIES                       
         B     MIRXX                                                            
*                                                                               
MIR24    CLI   NBMIRTYP,C'M'       DUAL MIRROR +3 AND +5                        
         BNE   MIR25                                                            
         MVC   HALF,=H'300'                                                     
         MVI   MIRDIV,3            SET 3 DAY/TIME ENTRIES                       
*                                                                               
MIR25    CLI   NBMIRTYP,C'N'       DUAL MIRROR +2 AND +4                        
         BNE   MIR26                                                            
         MVC   HALF,=H'200'                                                     
         MVI   MIRDIV,3            SET 3 DAY/TIME ENTRIES                       
*                                                                               
MIR26    CLI   NBMIRTYP,C'O'       +8 HOURS                                     
         BNE   MIR27                                                            
         MVC   HALF,=H'800'                                                     
         MVI   MIRDIV,2            SET 2 DAY/TIME ENTRIES                       
         B     MIRXX                                                            
*                                                                               
MIR27    CLI   NBMIRTYP,C'P'       +3 and +6                                    
         BNE   MIR28                                                            
         MVC   HALF,=H'300'                                                     
         MVI   MIRDIV,3            SET 3 DAY/TIME ENTRIES                       
         B     MIRXX                                                            
*                                                                               
MIR28    CLI   NBMIRTYP,C'Q'       +2 HOURS                                     
         BNE   MIR29                                                            
         MVC   HALF,=H'200'                                                     
         MVI   MIRDIV,2            SET 2 DAY/TIME ENTRIES                       
         B     MIRXX                                                            
*                                                                               
MIR29    CLI   NBMIRTYP,C'R'       +6 HOURS                                     
         BNE   MIR30                                                            
         MVC   HALF,=H'600'                                                     
         MVI   MIRDIV,2            SET 2 DAY/TIME ENTRIES                       
         B     MIRXX                                                            
*                                                                               
MIR30    DS    0H                                                               
*                                                                               
MIRXX    XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
* SAVE THE '5E' ELEMENT FROM THE DEMO RECORD                                    
SAV5E    NTR1 BASE=*,LABEL=*                                                    
         XC    DUB,DUB                                                          
         GOTO1 NBHELLO,DMCB,(C'G',=C'PAVFIL'),(X'5E',DBAREC),0                  
         CLI   DMCB+12,0                                                        
         BNE   SAV5EX              NOT FOUND. CONTINUE AS USUAL                 
         ICM   RE,15,DMCB+12       FOUND.                                       
         MVC   DUB,0(RE)           MOVE '5E' ELEM TO DUB (MAX LEN IS 7)         
SAV5EX   XIT1                                                                   
         LTORG                                                                  
         SPACE 2                                                                
* PUT THE '5E' ELEMENT TO THE DUMMY RECORD                                      
PUT5E    NTR1 BASE=*,LABEL=*                                                    
         GOTO1 NBHELLO,DMCB,(C'G',=C'PAVFIL'),(X'5E',DUMMYREC),0                
         CLI   DMCB+12,0                                                        
         BE    PUT5EX              '5E' PRESENT ON DUMMYREC. EXIT               
         OC    DUB,DUB                                                          
         BZ    PUT5EX                                                           
*                                  '5E' ELEMENT NOT PRESENT. NEED TO            
*                                   ADD IT FROM THE SAVED ELEM IN DUB           
         GOTO1 NBHELLO,DMCB,(C'P',=C'PAVFIL'),DUMMYREC,DUB,=C'ADD=END'          
PUT5EX   XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
* LOOK FOR GAP AFFID OVERRIDE                                                   
GET18    NTR1 BASE=*,LABEL=*                                                    
         XC    WORK,WORK                                                        
         L     R6,NBAIO                                                         
         MVI   SRCHEL,X'18'                                                     
         BAS   RE,GETEL4                                                        
         BNE   NO18EL                                                           
         USING NUDTAD,R6                                                        
         OC    NUDTGAFD,NUDTGAFD   OVERRIDE GAP AFFID                           
         BZ    NO18EL                                                           
         MVC   WORK(2),NUDTGAFD      GAP AFFID TIME                             
NO18EL   XIT1                                                                   
         DROP R6                                                                
         GETELN R6,NBDTADSP,SRCHEL,4                                            
         LTORG                                                                  
         EJECT                                                                  
* READ FOR USER DEFINED ADJUSTMENT FACTORS - TVQ                                
BTUREADX NTR1  BASE=*,LABEL=*                                                   
         MVI   GOTUTYPE,0                                                       
         TM    NBINDS3,NBI3AIRV    IF VENDING STATION TVQ NOT ALLOWED           
         BO    BTUXIT                                                           
         OC    NBNTI,NBNTI                                                      
         BZ    BTUXIT                                                           
         LA    R3,DBLOCK                                                        
         USING DBLOCK,R3                                                        
         MVI   DBMODE,DBMDSEQ                                                   
         MVC   BTUDB,DBLOCK                                                     
         LA    R3,BTUDB                                                         
         MVC   DBFILE,=C'NAD'                                                   
         MVI   DBMODE,DBMFRST                                                   
         MVI   DBFUNCT,DBGETNTI                                                 
         L     RE,DBAREC                                                        
         USING PMKEY,RE                                                         
         MVC   DBSELPRG,NBNTI                                                   
         CLC   DBSELSTA+3(2),=C'PN'                                             
         BNE   *+8                                                              
         MVI   DBSELSTA+3,C' '                                                  
         CLI   DBSELSTA+4,C'T'                                                  
         BNE   *+8                                                              
         MVI   DBSELSTA+4,C'N'                                                  
         CLI   DBSELSTA+4,C'S'                                                  
         BNE   *+8                                                              
         MVI   DBSELSTA+4,C'M'                                                  
         CLI   DBSELSTA+4,C'Q'                                                  
         BNE   *+8                                                              
         MVI   DBSELSTA+4,C'C'                                                  
         MVI   DBBTYPE,C'U'                                                     
         MVI   DBSELDAY,0                                                       
         XC    DBCABNUM,DBCABNUM                                                
         XC    DBSELTIM(4),DBSELTIM                                             
         MVI   DBSELDUR,X'FF'      ALL DURATIONS                                
         MVI   DBSELDAY,0                                                       
         XC    DBSELTIM(4),DBSELTIM                                             
         MVI   DBPRGDUR,C'Y'                                                    
         MVI   DBBEST,C'A'                                                      
         MVI   DBSELDUR,X'FF'                                                   
         CLI   BTUTYP,C'E'         ESTIMATED BOOK IS SET                        
         BE    BTUR4                                                            
*                                                                               
         L     R2,NBACOM                                                        
         USING COMFACSD,R2                                                      
         GOTO1 CDEMTABS,DMCB,NETVQCAL                                           
         ICM   RE,15,0(R1)   R2->MONTH/DATES TABLE                              
         BNZ   *+6                                                              
         DC    H'0'                                                             
         ICM   RF,15,4(R1)            LENGTH OF TABLE ENTRY                     
         USING NETVQD,RE                                                        
*                                                                               
BTUR2    CLI   0(RE),X'FF'         NOT FOUND - ALLOW A MISS                     
         BE    *+14                                                             
         XC    DBSELBK,DBSELBK     BK IS HI, LK UP LATEST                       
         B     BTUR4                                                            
         CLC   DBSELBK,NETVQEWK    CURRENT OUTSIDE RANGE                        
         BH    BTUR3               GET NEXT                                     
         CLC   DBSELBK,NETVQSWK    TOTALLY OUTSIDE RANGE - ALLOW MISS           
         BL    BTUR4               (BK IS LOW)                                  
         MVC   DBSELBK(2),NETVQMTH FOUND THE EQUATE - USE IT                    
         B     BTUR4                                                            
BTUR3    AR    RE,RF                                                            
         B     BTUR2                                                            
         DROP  RE                                                               
*                                                                               
BTUR4    XC    DBAQUART,DBAQUART                                                
         XC    BTUAQ,BTUAQ                                                      
                                                                                
*        CLI   DBSELSTA+4,C'C'                                                  
*        BNE   BTUR4A                                                           
*        OC    DBSELBK,DBSELBK     BOOK GIVEN                                   
*        BNZ   *+10                 YES - OK                                    
*        MVC   DBSELBK,=X'6309'     NO - USE LATEST  FOR CABLE                  
*        CLC   DBSELBK,=X'6309'    GREATER THAN LATEST                          
*        BNH   *+10                                                             
*        MVC   DBSELBK,=X'6309'    YES - USE LATEST FOR CABLE                   
*        B     BTUR5                                                            
*                                                                               
*TUR4A   OC    DBSELBK,DBSELBK     FOR NETWORK DIFF LATEST BK                   
*        BNZ   *+10                 YES - OK                                    
*        MVC   DBSELBK,=X'630B'     NO - USE LATEST                             
*        CLC   DBSELBK,=X'630B'    GREATER THAN LATEST                          
*        BNH   *+10                                                             
*        MVC   DBSELBK,=X'630B'    YES - USE LATEST                             
                                                                                
*                                                                               
BTUR5    CLI   HAVBTU,C'O'         OPI INDEX                                    
         BNE   BTUR6                                                            
         MVC   DBFILE,=C'OPI'                                                   
         MVC   DBSELOPI,NBACTPRG                                                
         MVI   DBFUNCT,DBGETOPI                                                 
         MVC   DBSELBK,=X'6601'                                                 
         MVI   DBSELSTA+4,C'T'                                                  
*                                                                               
BTUR6    LA    RE,BTUIO                                                         
         ST    RE,DBAREC                                                        
         L     RF,=F'1000'                                                      
         XCEF                                                                   
         DROP  R3                                                               
*                                                                               
         GOTO1 NBDEMAND,BTUDMCB,BTUDB,BTUHK                                     
*                                                                               
         LA    RF,BTUDB                                                         
         USING DBLOCK,RF                                                        
         OC    DBAQUART(4),DBAQUART                                             
         BNZ   BTUXIT                                                           
*        DROP  RF                                                               
         CLI   GOTUTYPE,1                                                       
         BNE   BTUXIT                                                           
         MVC   DBAQUART(4),BTUAQ                                                
         B     BTUXIT                                                           
         DROP  RF                                                               
BTUHK    LA    RF,BTUDB                                                         
         MVC   BTUAQ,8(RF)                                                      
         MVI   GOTUTYPE,1                                                       
         BR    RE                                                               
BTUXIT   XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
* R1 COMES IN WITH TIME TO BE ADJUSTED                                          
* HALF HAS TIME TO BE ADDED                                                     
* RETURNS WITH NEW TIME IN R1                                                   
HOURFIX  NTR1  BASE=*,LABEL=*                                                   
         AH    R1,HALF                                                          
         CH    R1,=H'2400'                                                      
         BL    *+8                                                              
         SH    R1,=H'2400'                                                      
         CVD   R1,DUB              SET TIME TO DUB                              
         DP    DUB,=P'100'         PUT MINUTES INTO REMAINDER                   
         CP    DUB+6(2),=P'59'     IF OVER 59 MINUTES                           
         BNH   HOURFXX                                                          
         AP    DUB(6),=P'1'        ADD 1 TO HOURS                               
         SP    DUB+6(2),=P'60'     SUBTRACT THE HOUR FROM MINUTES               
         MP    DUB(6),=P'100'      PUT HOURS BACK TO HOURS AND MINUTES          
         AP    DUB(6),DUB+6(2)     AND ADD THE REMINING MINUTES                 
         ZAP   WORK(8),DUB(6)                                                   
         ZAP   DUB,WORK(8)                                                      
         CVB   R1,DUB                                                           
HOURFXX  XIT1  REGS=(R1)                                                        
***********************************************************************         
* ITNDAY - FOR ITN, USE AFFID DATE WHEN IT EXISTS                               
***********************************************************************         
ITNDAY   NTR1  BASE=*,LABEL=*                                                   
         CLC   =C'SIN S',DBSELSTA                                               
         BE    *+10                                                             
         CLC   =C'ITN S',DBSELSTA                                               
         BNE   ITNDAYX                                                          
         OC    NBSDAFDT,NBSDAFDT                                                
         BZ    ITNDAYX                                                          
         GOTO1 NBDATCON,DMCB,(2,NBSDAFDT),(0,DUB)                               
         GOTO1 NBGETDAY,DMCB,DUB,FULL        GET AFFID DAY                      
*                                                                               
         OC    NBDAYPAK,NBDAYPAK                                                
         BNZ   ITNDAY5                                                          
         XC    DMCB,DMCB                                                        
         MVC   DMCB+4(4),=X'D9000A03'        GET DAYPAK                         
         GOTO1 NBCALLOV,DMCB                                                    
         L     RF,DMCB                                                          
         ST    RF,NBDAYPAK                                                      
ITNDAY5  GOTO1 NBDAYPAK,DMCB,(3,FULL),WORK,WORK+10                              
         CLI   WORK,0                                                           
         BNE   *+6                                                              
         DC    H'0'                                                             
         MVC   DBSELDAY,WORK                                                    
*                                                                               
ITNDAYX  XIT1                                                                   
         LTORG                                                                  
*                                                                               
         EJECT                                                                  
         DS    0H                                                               
INITDB1  NTR1  BASE=*,LABEL=*                                                   
         TM    NBUNST4,X'10'       =PCPR UNIT?                                  
         JNO   *+12                                                             
         MVI   NBPOSTYP,C'S'       YES-SET FOR SYNDICATION                      
         MVI   NBUSER2+4,C'Y'         SET TO Y                                  
*                                                                               
         XC    DBLOCK,DBLOCK                                                    
         MVC   DBFILE,=C'NTI'                                                   
         L     R1,AWORKREC                                                      
         ST    R1,DBAREC                                                        
         MVC   DBCOMFCS,NBACOM                                                  
         MVI   DBSELSRC,C'N'                                                    
         MVI   DBSELMED,C'N'       INITIALIZE FOR NETWORK                       
*                                                                               
         TM    NBINDS8,NBI8RNTK    RENTRAK OPTION?                              
         BNO   *+12                                                             
         MVI   DBSELSRC,C'R'       RENTRAK                                      
         MVI   DBSELMED,C'T'       RENTRAK                                      
*                                                                               
*                                                                               
** PXZ   CLI   NBPOSTYP,C'H'                                                    
         CLI   NBSURVEY,C'H'                                                    
         JNE   INITDB2                                                          
         CLI   NBUSER1+3,C'H'      ONLY NHTI WKLY                               
         JE    *+12                                                             
         CLI   NBUSER1+3,C'B'      NHTI AND NAD WKLY                            
         JNE   *+8                                                              
         MVI   DBSELMED,C'W'                                                    
*                                                                               
INITDB2  MVC   DBSELSTA(4),NBACTNET                                             
         CLC   NBAUTH,=XL2'4040'                                                
         JNH   *+10                                                             
         MVC   DBAUTH(2),NBAUTH    MOVE AUTH CODE FOR DEMO SECURITY             
* OUT->  CLI   NBPOSTYP,C'N'                                                    
* OUT->  JE    *+18                                                             
         CLI   NBNTISTA,X'40'                                                   
         JNH   *+10                                                             
         MVC   DBSELSTA(4),NBNTISTA  IF SYNDICATOR USE NTI STATION              
         MVI   DBSELSTA+4,C'T'                                                  
*** PXZ                                                                         
*8       CLI   NBPOSTYP,X'40'                                                   
**       JNH   *+18                                                             
**       CLI   NBPOSTYP,C'N'                                                    
**       JE    *+10                                                             
**       MVC   DBSELSTA+4(1),NBPOSTYP                                           
         CLI   NBSURVEY,X'40'                                                   
         JNH   *+18                                                             
         CLI   NBSURVEY,C'N'                                                    
         JE    *+10                                                             
         MVC   DBSELSTA+4(1),NBSURVEY                                           
*** PXZ                                                                         
*                                                                               
         CLC   =C'SIN ',DBSELSTA   IF ITN                                       
         BE    *+10                                                             
         CLC   =C'ITN ',DBSELSTA   IF ITN                                       
         BNE   *+8                                                              
         MVI   DBSELSTA+4,C'S'     IT'S SYNDICATION                             
*                                                                               
         MVC   DBSELBK,SAVEBOOK                                                 
         MVC   DBSELAGY,NBSELAGY                                                
         CLC   DBSELBK,=X'5D25'    SWITCH TO QH DATA                            
         JL    CABQHX                                                           
         CLC   DBSELBK,=X'5D31'    BLACK WEEK                                   
         JE    CABQHX                                                           
         CLC   DBSELBK,=X'5D32'    BLACK WEEK                                   
         JE    CABQHX                                                           
         CLC   DBSELBK,=X'5D33'    BLACK WEEK                                   
         JE    CABQHX                                                           
         CLI   DBSELSTA+4,C'C'                                                  
         JNE   CABQHX                                                           
         MVI   DBSELSTA+4,C'Q'                                                  
CABQHX   DS    0H                                                               
*                                                                               
         CLC   DBSELSTA,=C'PREVC'                                               
         JNE   *+8                                                              
         MVI   DBSELDUR,X'FF'      PICK UP ALL DUR INCL < 15MIN DURS            
**       CLC   =C'FXNC',DBSELSTA   WHETHER IT'S C OR Q OR WHATEVER              
**       JNE   ENDSETDR                                                         
** PXZ   CLI   NBPOSTYP,C'C'       IF POSTING CABLE                             
         CLI   NBSURVEY,C'C'       IF POSTING CABLE                             
         JNE   ENDSETDR                                                         
         OC    NBAFFTIM,NBAFFTIM                                                
         BNZ   SETDUR                                                           
         OC    NBNTI,NBNTI                                                      
         BZ    ENDSETDR                                                         
SETDUR   MVI   DBSELDUR,X'FF'      PICK UP ALL DUR INCL < 15MIN DURS            
ENDSETDR DS    0H                                                               
*                                                                               
         CLI   NBPOSTDT,C'C'       TEST FOR CONFORMED                           
         JNE   *+8                                                              
         MVI   DBBTYPE,C'C'                                                     
         CLI   NBPOSTDT,C'I'       TEST FOR INTEGRATED                          
         JNE   *+8                                                              
         MVI   DBBTYPE,C'I'                                                     
         CLI   NBPOSTDT,C'A'       TEST FOR ASCRIBED                            
         JNE   *+8                                                              
         MVI   DBBTYPE,C'A'                                                     
         CLI   NBPOSTDT,C'Z'       TEST FOR X-RATED                             
         JNE   *+8                                                              
         MVI   DBBTYPE,C'Z'                                                     
         CLI   NBPOSTDT,C'E'                                                    
         JNE   *+8                                                              
         MVI   DBBTYPE,C'E'                                                     
**       CLI   NBPOSTYP,C'H'                                                    
         CLI   NBSURVEY,C'H'                                                    
         JNE   *+8                                                              
         MVI   DBBTYPE,0                                                        
*                                                                               
         TM    NBINDS4,NBI4BRK     BREAKOUT?                                    
         JNO   *+8                                                              
         MVI   DBSTYPE,C'B'                                                     
*                                  FIX AGENCY SCREW-UPS                         
         L     RE,=A(AGSTAEQU)                                                  
         A     RE,RELO                                                          
AGSTEQ   CLC   DBSELSTA(4),0(RE)                                                
         JL    AGSTEQX         <---WON'T BE THERE                               
         JH    *+14            <---TRY NEXT                                     
         MVC   DBSELSTA(4),4(RE)   MOVE NEW STATION TO DBLOCK                   
         J     AGSTEQX                                                          
         LA    RE,L'AGSTAEQU(RE)   BUMP TO NEXT STA EQU                         
         J     AGSTEQ                                                           
*                                                                               
AGSTEQX  DS    0H                                                               
*                                                                               
         J     SUBRXIT                                                          
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* GETCNTI - SAVE CABLE NTI PROG INFORMATION AND WEIGHT IN SAVNTIPR              
*           WILL USE THEM TO READ SAME CBL-NAD PRG                              
* FORMAT OF ENTRIES IN THE TABLE:                                               
* PROG.NO + LEVEL 0 + PROG AVG + WEIGHT + FF                                    
*           LEVEL 1 + (TRACK1 + WEIGHT1) + (TRACK2 +WEIGHT2) +... FF            
*                                                                               
* IF SPECIAL OF BREAKOUT TELECASTS EXIST, THEN THEY WILL APPEAR AT              
* THE END OF A LEVEL 0 OR 1 PROGRAM ENTRY AND WILL HAVE THE FORMAT:             
*     1         (4 + 2)         (4 + 2)   + ...                                 
*   X'FE' + (TCAST1+WGT1) + (TCAST2+WGT2) + ... X'FF'                           
*                                                                               
**********************************************************************          
GETCNTI  NTR1  BASE=*,LABEL=*                                                   
*        CLI   DBSELSTA+4,C'C'                                                  
** PXZ   CLI   NBPOSTYP,C'C'                                                    
         CLI   NBSURVEY,C'C'                                                    
         JNE   GETCNTIX            ONLY FOR CABLE NAD PROG REQUESTS             
         CLI   HAVNAD,C'Y'                                                      
         JNE   GETCNTIX                                                         
         L     RE,DBAREC                                                        
         CLC   0(3,RE),=C'QCN'                                                  
         JNE   GETCNTIX                                                         
*                                                                               
         ICM   R4,15,DBSPANAD                                                   
         CLC   0(3,R4),=C'CAB'                                                  
         JNE   GETCNTIX                                                         
         XC    SVTRNUM,SVTRNUM                                                  
         XC    SVTLNUM,SVTLNUM                                                  
         OC    109(5,R4),109(R4)                                                
         JZ    GETCNT3                                                          
         PACK  DUB,109(5,R4)                                                    
         CVB   R1,DUB                                                           
         STCM  R1,3,SVTRNUM        SAVE TRACK NUMBER                            
GETCNT3  OC    130(5,R4),130(R4)                                                
         JZ    GETCNT4                                                          
         PACK  DUB,130(5,R4)                                                    
         CVB   R1,DUB                                                           
         STCM  R1,15,SVTLNUM       SAVE TELECAST NUMBER                         
*                                                                               
GETCNT4  L     RE,ANTIPR                                                        
         LA    RF,SAVNTIPX                                                      
         CR    RE,RF                                                            
         JNL   GETCNTIX                                                         
*                                                                               
         L     R6,DBAQUART                                                      
         CLI   0(R6),X'0F'                                                      
         JNE   GETCNTIX                                                         
         MVC   PRGLEV,2(R6)                                                     
*                                                                               
GETCNT5  CLI   0(R6),0                 LOOK FOR PROGRAM ELEMENT                 
         JE    GETCNTIX                                                         
         USING PHTELEM,R6                                                       
         CLI   0(R6),X'10'                                                      
         JE    GETCNT10                                                         
         ZIC   R1,1(R6)                                                         
         LA    R6,0(R1,R6)                                                      
         J     GETCNT5                                                          
*                                                                               
GETCNT10 CLC   PHTDDS,PREVNTI                                                   
         JE    GETCNT12                                                         
         BRAS  RE,ADDSB         ADD SPEC/BKOUTS TO END OF PROGRAM               
         L     RE,ANTIPR                                                        
         OC    PREVNTI,PREVNTI                                                  
         JZ    *+8                                                              
         LA    RE,1(RE)         SKIP 'FF'=END OF PROG.START NEW                 
         ST    RE,ASNTIPR       POINTER TO START OF THIS PROG ENTRY             
         MVC   0(2,RE),PHTDDS   PROG NO                                         
         LA    RE,2(RE)                                                         
         ST    RE,ANTIPR                                                        
*                                                                               
         CLI   PRGLEV,2         USE TEMP STORAGE FOR SPEC/BKOUTS                
         JE    GETCNT20                                                         
GETCNT11 MVC   0(1,RE),PRGLEV   LEVEL(0=PROG,1=TRACK)                           
         LA    RE,1(RE)                                                         
*                                                                               
GETCNT12 CLI   PRGLEV,0                                                         
         JNE   GETCNT15                                                         
         L     RE,ASNTIPR                                                       
         MVC   2(1,RE),PRGLEV   JUST IN CASE                                    
         LA    RE,3(RE)                                                         
         SR    R0,R0                                                            
         ICM   R0,3,0(RE)                                                       
         SR    R1,R1                                                            
         ICM   R1,3,DBFACTOR                                                    
         AR    R0,R1                                                            
         STCM  R0,3,0(RE)                                                       
         LA    RE,2(RE)                SAVE WEIGHT                              
         MVI   0(RE),X'FF'                                                      
         ST    RE,ANTIPR                                                        
         J     GETCTOUT                                                         
*                                                                               
GETCNT15 CLI   PRGLEV,1                                                         
         JNE   GETCNT20                                                         
*                                                                               
         L     RE,ASNTIPR        START AT BEGINNING OF PROG ENTRY               
         MVC   2(1,RE),PRGLEV    JUST IN CASE...                                
         LA    RE,3(RE)          AND SEE IF TRACK ALREADY EXISTS                
         OC    0(2,RE),0(RE)     NOTHING HERE YET                               
         JZ    GETCNT18                                                         
GETCNT16 CLI   0(RE),X'FF'                                                      
         JE    GETCNT18          NO MATCH FOUND. ADD NEW TRACK                  
         CLC   SVTRNUM,0(RE)     TRACK NUMBER                                   
         JE    GETCNT18                                                         
         LA    RE,4(RE)                                                         
         J     GETCNT16                                                         
*                                                                               
GETCNT18 MVC   0(2,RE),SVTRNUM         ADD TRACK NO TO SAVE LIST                
         LA    RE,2(RE)                                                         
         SR    R0,R0                                                            
         ICM   R0,3,0(RE)                                                       
         SR    R1,R1                                                            
         ICM   R1,3,DBFACTOR                                                    
         AR    R0,R1                                                            
         STCM  R0,3,0(RE)                                                       
         LA    RE,2(RE)                                                         
         ST    RE,ANTIPR                                                        
         MVI   0(RE),X'FF'                                                      
         J     GETCTOUT                                                         
*                                                                               
GETCNT20 CLI   PRGLEV,2                                                         
         JNE   GETCNTIX                                                         
         ST    RE,SVRE                                                          
         BRAS  RE,TSTSPBR           SEE IF TELECAST IS SPECIAL OR BKOUT         
         L     RE,SVRE                                                          
         JE    GETCNT21             AND SET SVTTYPE TO S OR B                   
         MVI   PRGLEV,0                                                         
         OC    SVTRNUM,SVTRNUM      IF IT HAS TRACK NO, GET CNAD TRACK          
         JZ    *+8                  OTHERWISE USE CNAD PROG AVG                 
         MVI   PRGLEV,1                                                         
         CLC   PHTDDS,PREVNTI                                                   
         JE    GETCNT12                                                         
         J     GETCNT11             NEW PROG. MARK PROG LEVEL FIRST             
*                                   DEAL WITH SPECIALS AND BREAKOUTS            
GETCNT21 L     RF,ASBPR                                                         
         OC    0(2,RF),0(RF)                                                    
         JNZ   *+12                                                             
         MVI   0(RF),X'FE'          MARKS START OF SPEC/BKOUTS                  
         LA    RF,1(RF)                                                         
*                                                                               
GETCNT23 MVC   0(4,RF),SVTLNUM      SAVE TELECAST NUMBER                        
         MVC   4(1,RF),SVTTYP       B OR S                                      
         MVC   5(2,RF),DBFACTOR     + ITS WEIGHT                                
         LA    RF,7(RF)                                                         
*                                                                               
GETCNT25 MVI   0(RF),X'FF'                                                      
         ST    RF,ASBPR             POINTER TO NEXT AVAIL SPOT FOR S/B          
*                                                                               
GETCTOUT MVC   PREVNTI,PHTDDS                                                   
*                                                                               
GETCNTIX J     SUBRXIT                                                          
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
* ADDSB - FOR CABLE NAD,                                                        
*         ADD SPECIALS AND BREAKOUTS AT END OF TABLE ENTRY FOR PROGRAM          
***********************************************************************         
ADDSB    NTR1  BASE=*,LABEL=*                                                   
** PXZ   CLI   NBPOSTYP,C'C'                                                    
         CLI   NBSURVEY,C'C'                                                    
         JNE   ADDSBX                                                           
         CLI   HAVNAD,C'Y'                                                      
         JNE   ADDSBX                                                           
*                                                                               
         L     RE,ANTIPR            END OF PROG IN PROGRAM TABLE                
         LA    RF,SAVSB             S/B WERE TEMPORARELY STORE HERE             
*                                                                               
         OC    0(2,RF),0(RF)                                                    
         JZ    ADDSBX                                                           
         CLI   0(RF),X'FE'                                                      
         JNE   ADDSBX                                                           
*                                                                               
         CLI   1(RF),X'FF'                                                      
         JE    ADDSBX               NO SPEC/BKOUTS                              
*                                                                               
         MVI   0(RE),X'FE'        MARK START OF B/S IN PROG TABLE               
         LA    RE,1(RE)                                                         
         LA    RF,1(RF)                                                         
ADDSB10  CLI   0(RF),X'FF'                                                      
         JE    ADDSB20                                                          
         MVC   0(7,RE),0(RF)     TCAST + B/S + WEIGHT                           
         LA    RE,7(RE)                                                         
         LA    RF,7(RF)                                                         
         J     ADDSB10                                                          
*                                                                               
ADDSB20  MVI   0(RE),X'FF'       MARK END OF PROGRAM                            
         ST    RE,ANTIPR                                                        
         LA    RF,SAVSB                                                         
         ST    RF,ASBPR                                                         
         XC    SAVSB,SAVSB                                                      
ADDSBX   J     SUBRXIT                                                          
         EJECT                                                                  
***********************************************************************         
* TSTSPBR - TEST IF TELECAST IS SPECIAL OR BREAKOUT                             
*           SAVE TELECAST AND TRACK NUMBER, IF ANY                              
*           R6-> PHTELEM                                                        
***********************************************************************         
TSTSPBR  NTR1  BASE=*,LABEL=*                                                   
         MVI   SVTTYP,0                                                         
         USING PHTELEM,R6                                                       
*                                                                               
TSTSB20  CLI   PHTRSF,X'01'       BREAKOUT                                      
         JNE   *+8                                                              
         MVI   SVTTYP,C'B'                                                      
         TM    PHTDTYP,X'04'      SPECIAL                                       
         JNO   *+8                                                              
         MVI   SVTTYP,C'S'        IF BOTH, SET IT TO SPECIAL                    
*                                 (THIS IS THE SAME AS CNAD CONVERSION)         
TSTSBX   CLI   SVTTYP,C'S'                                                      
         JE    *+8               STE CONDITION CODE                             
         CLI   SVTTYP,C'B'                                                      
         J     SUBRXIT                                                          
         DROP  R6                                                               
***********************************************************************         
* GETCNAD - LOOK UP CABLE NAD DEMOS                                             
***********************************************************************         
GETCNAD  NTR1  BASE=*,LABEL=*                                                   
         MVC   SAVBTYPE,DBBTYPE    SAVE NTI BOOK TYPE                           
         MVC   DMCB+12(5),DBACTSTA                                              
*                                                                               
         GOTO1 ASUBR,DMCB,('INITDBE',(RC))  SET UP DBLOCK FOR DEMAND            
*                                  DBAREC NOW PTS BACK TO WORKREC               
         MVC   DBFUNCT,SAVFCT     ORIGINAL FUNCTION CALL                        
         MVC   DBSELECT,SAVSEL    ORIGINAL CABLE CONTROL DATA                   
         MVI   DBBTYPE,C'N'        SET BOOK TYPE FOR CABLE NAD                  
         MVI   DBSELSTA+4,C'C'     ONLY HH DATA, NO QH DATA                     
         LA    RF,NADWORKX         ACCUM U/V DEMO VALUES IN NADWORK             
         LA    RE,NADWORK                                                       
         LA    RE,NADWRKU(RE)                                                   
         XCEF                                                                   
*                                                                               
         CLI   DBFUNCT,DBGETNTI    LOOKING FOR PROG DEMOS?                      
         JNE   GETCN10                                                          
*                                                                               
         LA    RE,SAVNTIPR         GO THROUGH TABLE OF NTI PROGRAMS             
         OC    0(2,RE),0(RE)                                                    
         JZ    XITCNAD                                                          
*                                                                               
GETCN5   LA    RF,SAVNTIPX                                                      
         CR    RE,RF                                                            
         JNL   GETCN20                                                          
         ST    RE,ANTIPR           POINTER TO NTI PROGRAM                       
*                                                                               
         MVI   FORCEDP,0                                                        
         CLI   2(RE),0                                                          
         JNE   *+12                                                             
         MVI   DBSELPTT,C'P'      GET PROG AVERAGE                              
         J     GETCN8                                                           
         CLI   2(RE),1                                                          
         JNE   *+12                                                             
         MVI   DBSELPTT,C'T'      GET TRACKS                                    
         J     GETCN8                                                           
         CLI   2(RE),X'FE'        ONLY S/B, NOTHING ELSE                        
         JNE   XITCNAD            ERROR                                         
         MVI   DBSELPTT,C'E'      GET EPISODES (SPEC/BKOUTS)                    
*                                                                               
GETCN8   DS    0H                                                               
         XC    DBSELDAY,DBSELDAY  DON'T FILTER ON DAY/TIME                      
         XC    DBSELTIM,DBSELTIM                                                
         L     RE,ANTIPR                                                        
         MVC   DBSELPRG,0(RE)     BUT ON THE PROGRAM NUMBER                     
*                                                                               
GETCN10  DS    0H                                                               
         XC    CNDIVSOR,CNDIVSOR   CLEAR MY LOCAL DIVSOR                        
         GOTO1 NBDEMAND,DMCB,DBLOCK,CNADHOOK                                    
         OC    DBDIVSOR,DBDIVSOR   IF NO MATCH FOUND                            
         JZ    GETCN11A            GO READ FOR NEXT PROGRAM IN LIST             
*                                                                               
         CLI   DBFUNCT,DBGETNTI                                                 
         JNE   GETCN11                                                          
         BRAS  RE,EXTRASB         CK IF ANY S/B NOT FOUND.ADJUST DIVSOR         
         L     RE,ANTIPR                                                        
         CLI   2(RE),1            LOOKING FOR TRACKS                            
         JNE   GETCN11                                                          
         TM    PROGSW,TRAKEQ      AND ACTUAL TRACK WAS FOUND                    
         JNO   GETCN11                                                          
         CLI   FORCEDP,1                                                        
         JE    GETCN11                                                          
         BRAS  RE,EXTRATK         NTI HAS EXTRA TRACKS COMP TO CNAD             
         JNE   GETCN11                                                          
         MVI   DBSELPTT,C'P'      ROLL BACK TO CNAD PROGRAM AVG                 
         MVI   FORCEDP,1          FORCED PROGRAM AVERAGE                        
         BRAS  RE,CLRTEMP         CLEAR TEMPORARY DEMO STORAGE                  
         J     GETCN8                                                           
*                                                                               
GETCN11  MVI   NBRESULN,C'D'      DEMOS FOUND, INDICATE FROM DEMO FILE          
         BRAS  RE,ACCDEMS         ACCUMULATE DEMOS FROM TEMP STORAGE            
         BRAS  RE,CLRTEMP         AND CLEAR TEMP STORAGE                        
*                                                                               
         CLI   DBFUNCT,DBGETNTI   LOOKING FOR PROG DEMOS?                       
         JNE   GETCN20                                                          
*                                 GO READ FOR NEXT PROGRAM                      
GETCN11A CLI   DBFUNCT,DBGETNTI   EXIT FOR TIME PERIOD                          
         JNE   XITCNAD                                                          
         L     RE,ANTIPR                                                        
         LA    RE,2(RE)           PAST PROGRAM NUMBER                           
         CLI   0(RE),0            CHECK LEVEL                                   
         JNE   *+12                                                             
         LA    R1,2               STEP SIZE FOR PROG AVG (WEIGHT=2)             
         J     GETCN12A                                                         
         CLI   0(RE),1                                                          
         JNE   *+12                                                             
         LA    R1,4               STEP SIZE FOR TRACKS                          
         J     GETCN12A                                                         
         LA    R1,7               STEP SIZE FOR S/B                             
GETCN12A LA    RE,1(RE)           PAST LEVEL NUMBER                             
GETCN12  CLI   0(RE),X'FF'                                                      
         JE    GETCN14                                                          
         CLI   0(RE),X'FE'        BEGINNG OF S/B(AFTER PROG.AVG/TRCKS)          
         JNE   *+16                                                             
         LA    RE,1(RE)                                                         
         LA    R1,7               STEP SIZE FOR S/B                             
         J     GETCN12            CONTINUE WITH STEP SIZE 7(FOR S/B)            
*                                                                               
         AR    RE,R1              ADVANCE ONE STEP                              
         J     GETCN12                                                          
*                                                                               
GETCN14  LA    RE,1(RE)                                                         
         OC    0(2,RE),0(RE)                                                    
         JZ    GETCN20            END OF TABLE                                  
         J     GETCN5                                                           
*                                                                               
GETCN20  MVC   DBDIVSOR,NTIDIVSR  JUST IN CASE LAST READ HAD NO FIND            
         OC    NTIDIVSR,NTIDIVSR                                                
         JZ    XITCNAD            NO MATCH AT ALL. EXIT                         
         LA    R4,CBNHOMES                                                      
         BRAS  RE,UWGTFLD         UNWEIGHT THE CABLE NAD HOMES                  
*                                                                               
         MVI   FULL,C'U'           SET UNIVERSE MODIFIER                        
         LA    R4,NADWORK          PT TO UNIV BUFFER                            
         LA    R4,NADWRKU(R4)      PT TO UNIV BUFFER                            
         BRAS  RE,UWGTREL          UNWEIGHT VALUES AND RELS DE ELEM             
*                                                                               
         LA    R4,NADWORK          UNWEIGHT IMPRESSIONS                         
         LA    R4,NADWRKT(R4)      UNWEIGHT IMPRESSIONS                         
         BRAS  RE,UNWEIGHT         BUT DON'T RELEASE OVERRIDE ELEMENTS          
*                                                                               
         OC    NTIHOMES,NTIHOMES  NO NTI HOMES - ERROR                          
         JZ    XITCNAD                                                          
         OC    CBNHOMES,CBNHOMES  NO CABLE NAD HOMES - ERROR                    
         JZ    XITCNAD                                                          
         BRAS  RE,GETCVIR         COMPUTE VPH'S, ADJUSTED IMPR, RATINGS         
*                                 STORE THEM IN NADWORK SLOTS                   
         MVI   FULL,C'V'          VPH'S                                         
         LA    R4,NADWORK                                                       
         LA    R4,NADWRKV(R4)                                                   
         BRAS  RE,OVREL           RELEASE WITHOUT UNWEIGHTING AGAIN             
*                                                                               
         MVI   FULL,C'H'          IMPRESSIONS                                   
         LA    R4,NADWORK                                                       
         LA    R4,NADWRKT(R4)                                                   
         BRAS  RE,OVREL           RELEASE WITHOUT UNWEIGHTING AGAIN             
*                                                                               
         MVI   FULL,C'R'          RATINGS                                       
         LA    R4,NADWORK                                                       
         LA    R4,NADWRKR(R4)                                                   
         BRAS  RE,OVREL           RELEASE WITHOUT UNWEIGHTING AGAIN             
*                                                                               
XITCNAD  J     SUBRXIT                                                          
         EJECT                                                                  
***********************************************************************         
*ACCDEMS - ADD DEMOS FROM TEMPORARY STORAGE TO TOTAL ACCUMULATORS.              
*          TEMPORARY STORAGE WAS NEEDED IF NOT SURE WHETHER WILL KEEP           
*          TRACK DEMOS FORCNAD OR SWITCH TO PROG AVERAGE                        
***********************************************************************         
ACCDEMS  NTR1  BASE=*,LABEL=*                                                   
         SR    R0,R0                                                            
         LA    R4,NADWORK          TEMP STORAGE FOR CURRENT UNIVS               
         LA    R4,NADWRKU2(R4)     TEMP STORAGE FOR CURRENT UNIVS               
         LA    R5,NADWORK          DESTINATION OF UNIVS IN NADWORK              
         LA    R5,NADWRKU(R5)      DESTINATION OF UNIVS IN NADWORK              
*                                                                               
ACCDM5   CLI   DBFUNCT,DBGETNTI                                                 
         JNE   ACCDM9                                                           
*                                  FOR PROGRAM RECORDS                          
**       MVC   DBFACTOR,NTIFCTR    DEFAULTS-- FROM NTI                          
         MVC   DBDIVSOR,NTIDIVSR                                                
*                                                                               
         TM    PROGSW,MULTEQ       TRACKS ONLY. THEY ARE WEIGHTED BY            
         JNO   ACCDM9              CNAD WEIGHTS.                                
         MVC   DBDIVSOR,CNDIVSOR                                                
         BRAS  RE,UNWEIGHT         UNWEIGHT USING CNAD DIVSOR                   
         MVC   DBFACTOR,NTIFCTR                                                 
         MVC   DBDIVSOR,NTIDIVSR                                                
         BRAS  RE,WEIGHT           AND RE-WEIGHT WITH THE NTI WEIGHT            
*                                                                               
*                                  ADD THEM TO DESTINATION                      
ACCDM9   LA    R1,DINDMSET                                                      
ACCDM10  CLI   0(R1),X'FF'         END OF DEMO LIST?                            
         JE    ACCDM20                                                          
         ICM   RE,15,0(R5)                                                      
         ICM   RF,15,0(R4)                                                      
         AR    RE,RF                                                            
         STCM  RE,15,0(R5)                                                      
         LA    R4,4(R4)                                                         
         LA    R5,4(R5)                                                         
         LA    R1,3(R1)                                                         
         J     ACCDM10                                                          
*                                                                               
*                                                                               
ACCDM20  AHI   R0,1                                                             
         CHI   R0,1                                                             
         JH    ACCDM30                                                          
         LA    R4,NADWORK          TEMP STORAGE FOR CURRENT IMPRSNS             
         LA    R4,NADWRKT2(R4)     TEMP STORAGE FOR CURRENT IMPRSNS             
         LA    R5,NADWORK          DESTINATION OF IMPRSNS IN NADWORK            
         LA    R5,NADWRKT(R5)      DESTINATION OF IMPRSNS IN NADWORK            
*                                  ADD THEM TO DESTINATION                      
         J     ACCDM5                                                           
*                                                                               
*                                  NOW DO THE HOMES                             
ACCDM30  TM    PROGSW,MULTEQ       TRACKS ONLY. THEY ARE WEIGHTED BY            
         JNO   ACCDM35             CNAD WEIGHTS.                                
         SR    R4,R4                                                            
         MVC   DBDIVSOR,CNDIVSOR                                                
         BRAS  RE,UNWEIGHT         UNWEIGHT USING CNAD DIVSOR                   
         MVC   DBFACTOR,NTIFCTR                                                 
         MVC   DBDIVSOR,NTIDIVSR                                                
         BRAS  RE,WEIGHT           AND RE-WEIGHT WITH THE NTI WEIGHT            
*                                                                               
ACCDM35  ICM   RE,15,CBNHOMES                                                   
         ICM   RF,15,CBNHOME2                                                   
         AR    RE,RF                                                            
         STCM  RE,15,CBNHOMES                                                   
*                                                                               
ACCDMX   J     SUBRXIT                                                          
         EJECT                                                                  
***********************************************************************         
*CLRTEMP - CLEAR TEMPORARY DEMO STORAGE                                         
***********************************************************************         
CLRTEMP  NTR1  BASE=*,LABEL=*                                                   
         SR    R0,R0                                                            
         LA    RE,NADWORK          TEMP STORAGE FOR UNIVERSES                   
         LA    RE,NADWRKU2(RE)     TEMP STORAGE FOR UNIVERSES                   
         LA    RF,80                                                            
         XCEF                                                                   
*                                                                               
         LA    RE,NADWORK          TEMP STORAGE FOR IMPRESSIONS                 
         LA    RE,NADWRKT2(RE)     TEMP STORAGE FOR IMPRESSIONS                 
         LA    RF,80                                                            
         XCEF                                                                   
*                                                                               
         XC    CBNHOME2,CBNHOME2   TEMP STORAGE FOR CNAD HOMES                  
*                                                                               
         J     SUBRXIT                                                          
         EJECT                                                                  
***********************************************************************         
*CNADHOOK - FOR CABLE NAD: SAVE WGTD DEMOS IN NADWORK                           
***********************************************************************         
CNADHOOK NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         CLI   DBFUNCT,DBGETNTI  -TIME PERIOD READ IS STRAIGHT FORWARD          
         JNE   CNADH20                                                          
*                                -PROGRAM READS NEED TO MATCH NTI PROG          
         L     RF,DBAQUART                                                      
         CLI   0(RF),X'0F'       NEW LEVEL                                      
         JE    *+16                                                             
         TM    PROGSW,SKIPEQ                                                    
         JO    CNADHKX                                                          
         J     CNADH20           USE SPANNING RECORDS                           
*                                                                               
         ICM   R4,15,DBSPANAD                                                   
         CLC   0(3,R4),=C'CB2'   CABLE NAD                                      
         JNE   CNADSKIP                                                         
         XC    SVTRNUM,SVTRNUM                                                  
         XC    SVTLNUM,SVTLNUM                                                  
         OC    109(5,R4),109(R4)                                                
         JZ    CNADH3                                                           
         PACK  DUB,109(5,R4)                                                    
         CVB   R1,DUB                                                           
         STCM  R1,3,SVTRNUM      SAVE TRACK NUMBER                              
CNADH3   OC    130(5,R4),130(R4)                                                
         JZ    CNADH4                                                           
         PACK  DUB,130(5,R4)                                                    
         CVB   R1,DUB                                                           
         STCM  R1,15,SVTLNUM     SAVE TELECAST NUMBER                           
*                                                                               
CNADH4   MVI   PROGSW,0                                                         
*                                                                               
         CLI   2(RF),2                                                          
         JNE   CNADH8                                                           
         BRAS  RE,FILSB          IF S/B FOUND, SEE IF MATCHES NTI               
         JNE   CNADSKIP                                                         
*                                                                               
CNADH8   CLI   2(RF),1                                                          
         JNE   *+8                                                              
         OI    PROGSW,TRAKEQ     ACTUAL TRACK FOUND                             
*                                                                               
         L     R6,ANTIPR                                                        
         CLI   2(R6),0           LOOKING FOR PROGRAM AVERAGE                    
         JE    *+12                                                             
         CLI   FORCEDP,1         OR ORIGINALLY LOOKING FOR TRACKS BUT           
         JNE   CNADH10            FORCED TO PROGRAM AVERAGE                     
         BRAS  RE,PGAVGWT        SET NTIFCTR AS COMBINED NTI WEIGHT             
         CLI   2(RF),1                                                          
         JNE   *+8                                                              
         OI    PROGSW,MULTEQ   NO PROG AVG FOUND. USE TRACKS AND                
         J    CNADH15            AVERAGE THEM USING CNAD WEIGHTS                
*                                                                               
CNADH10  CLI   2(R6),1                                                          
         JNE   CNADH15                                                          
         BRAS  RE,FILTRACK         AND SET NTIFCTR                              
         JNE   CNADSKIP            NOT VALID.WANT TO SWITCH TO PROG AVG         
*                                                                               
CNADH15  DS    0H                                                               
CNADH20  BRAS  RE,CNADDIV          GET ADJUSTED CNAD DIVSOR                     
**       MVC   DBDIVSOR,CNDIVSOR                                                
         CLI   DBFUNCT,DBGETNTI    LOOKING FOR PROG DEMOS?                      
         JNE   CNADH30                                                          
         TM    PROGSW,MULTEQ      ONLY TRACKS.USE CNAD WEIGHTS TO AVRG          
**       JO    *+16                                                             
**       MVC   DBDIVSOR,NTIDIVSR   USE NTI CABLE WEIGHTS                        
         JO    *+10                                                             
         MVC   DBFACTOR,NTIFCTR                                                 
*                                                                               
CNADH30  CLI   BYTE,0              ONLY LK UP UNIVS FOR 1ST NAD RECD            
         JNE   CNADH40                                                          
         MVI   FULL,C'U'                                                        
         LA    R4,NADWORK          PUT THEM IN TEMP STORAGE FIRST               
         LA    R4,NADWRKU2(R4)     PUT THEM IN TEMP STORAGE FIRST               
         BRAS  RE,LKWGT            (IN CASE WE DON'T KEEP THEM)                 
*                                                                               
CNADH40  MVI   FULL,C'Y'                                                        
         LA    R4,NADWORK          PUT THEM IN TEMP STORAGE FIRST               
         LA    R4,NADWRKT2(R4)     PUT THEM IN TEMP STORAGE FIRST               
         BRAS  RE,LKWGT                                                         
*                                                                               
         LA    R4,CBNHOME2         ACCUMULATE RAW CABLE NAD HOMES               
         BRAS  RE,LKWGTFLD                                                      
         J     CNADHKX                                                          
*                                                                               
CNADSKIP OI    PROGSW,SKIPEQ                                                    
CNADHKX  J     SUBRXIT                                                          
         EJECT                                                                  
***********************************************************************         
* FILSB   - USE ONLY SPECIALS AND BREAKOUTS THAT WERE ORIGINALLY FOUND          
*           ON THE NTI FILE FOR THIS DAY AND TIME                               
*           IF SAME S/B ON CNAD, USE IT AND WEIGHT IT BY THE FACTOR             
*           FROM NTI                                                            
***********************************************************************         
*                                                                               
FILSB    NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         L     RE,ANTIPR                                                        
         LA    RE,2(RE)         PAST PROG NUMBER                                
         LA    R1,2             STEP SIZE FOR PROG AVG                          
         CLI   0(RE),0          CHECK LEVEL                                     
         JE    FILSB26                                                          
         CLI   0(RE),1                                                          
         JNE   FILSB25          LEVEL=X'FE' -> ONLY S/B                         
         LA    R1,4             STEP SIZE FOR TRACKS (SEE SAVNTIPR)             
FILSB26  LA    RE,1(RE)         GO PAST LEVEL NUMBER                            
*                                                                               
FILSB25  CLI   0(RE),X'FF'                                                      
         JE    FILSBN           NO S/B FOUND                                    
         CLI   0(RE),X'FE'      SEARCH FOR START OF S/B                         
         JE    FILSB30                                                          
         AR    RE,R1            ADVANCE TO NEXT SLOT                            
         J     FILSB25                                                          
*                                                                               
FILSB30  LA    RE,1(RE)                                                         
FILSB35  CLI   0(RE),X'FF'     NO MATCH FOUND                                   
         JE    FILSBN                                                           
         CLC   SVTLNUM,0(RE)   MATCH ON TCAST NO                                
         JNE   FILSB40                                                          
         L     R5,DBAREC                                                        
         USING PMKEY,R5                                                         
         CLC   PMSTYP,4(RE)      C'B' OR 'S'                                    
         JNE   FILSB40                                                          
         MVC   NTIFCTR,5(RE)     WEIGHT OF NTI SPEC OR BKOUT                    
         XC    0(4,RE),0(RE)   CLEAR TCAST NUMBER TO MARK AS FOUND              
         J     FILSBY                                                           
         DROP  R5                                                               
*                                                                               
FILSB40  LA    RE,7(RE)                                                         
         J     FILSB35                                                          
*                                                                               
FILSBY   DS    0H                                                               
         CR    RB,RB                                                            
         J     FILSBX                                                           
FILSBN   CHI   RB,0                                                             
FILSBX   J     SUBRXIT                                                          
         EJECT                                                                  
***********************************************************************         
* FILTRACK- USE ONLY TRACKS THAT WERE FOUND ON THE NTI FILE FOR THE             
*           PROGRAM.                                                            
*           IF SAME TRACK ON CNAD, USE IT AND WEIGHT IT BY THE FACTOR           
*           FROM NTI                                                            
***********************************************************************         
*                                                                               
FILTRACK NTR1  BASE=*,LABEL=*                                                   
         L     R6,DBAQUART                                                      
         CLI   0(R6),X'0F'       START OF NEW TRACK/OR PROGRAM AVG?             
         JNE   FILTRY            NO. THE KEEP SPANNING RECORDS                  
*                                                                               
FILTR10  DS    0H                                                               
         CLI   2(R6),0                                                          
         JNE   FILTR15          -GOT PROG AVERAGE(IE NO TRACKS).KEEP IT         
         BRAS  RE,PGAVGWT                                                       
         J     FILTRY                                                           
*                                                                               
*                                                                               
FILTR15  CLI   2(R6),1                                                          
         JNE   FILTRN            DON'T KEEP SPEC/BKOUTS IF TRACK REQSTD         
*                               -GOT A TRACK. MATCH ON TRACK NUM                
FILTR20  L     RE,ANTIPR                                                        
         LA    RE,1+2(RE)        POINT TO FIRST TRACK                           
FILTR25  CLC   SVTRNUM,0(RE)     TRACK NO FROM DBSPANAD                         
         JNE   *+20                                                             
         XC    0(2,RE),0(RE)     CLEAR NTI TRACK NO TO SIGNAL FOUND             
         MVC   NTIFCTR,2(RE)     WEIGHT OF NTI TRACK                            
         J     FILTRY                                                           
*                                                                               
         LA    RE,2+2(RE)   NEXT TRACK                                          
         CLI   0(RE),X'FF'       NO MORE TRACKS TO MATCH ON                     
         JE    FILTRN            NO MATCH FOR THIS TRACK                        
         CLI   0(RE),X'FE'                                                      
         JE    FILTRN                                                           
         J     FILTR25                                                          
*                                                                               
FILTRY   DS    0H                                                               
         CR    RB,RB                                                            
         J     FILTRKX                                                          
FILTRN   CHI   RB,0                                                             
FILTRKX  J     SUBRXIT                                                          
         EJECT                                                                  
***********************************************************************         
* PGAVGWT - WEIGHT CNAD PROGRAM AVERAGE                                         
*          IF NTI HAS MATCHING PROG AVG, USE ITS WEIGHT                         
*          IF NTI HAS TRACKS, USE COMBINED WEIGHT OF ALL TRACKS                 
***********************************************************************         
PGAVGWT  NTR1  BASE=*,LABEL=*                                                   
         L     RE,ANTIPR                                                        
         LA    RE,2(RE)           POINT AT LABEL                                
         XC    NTIFCTR,NTIFCTR                                                  
         CLI   0(RE),0            NTI PROG AVG                                  
         JNE   *+14                                                             
         MVC   NTIFCTR,1(RE)      USE ITS WEIGHT                                
         J     PAVGWTX                                                          
*                                                                               
         LA    RE,1(RE)           POINT TO FIRST TRACK                          
PAVGW10  CLI   0(RE),X'FF'                                                      
         JE    PAVGWTX                                                          
         CLI   0(RE),X'FE'                                                      
         JE    PAVGWTX                                                          
         LA    RE,2(RE)                                                         
         SR    R0,R0                                                            
         ICM   R0,3,0(RE)         WEIGHT OF THIS TRACK                          
         SR    R1,R1                                                            
         ICM   R1,3,NTIFCTR                                                     
         AR    R0,R1                                                            
         STCM  R0,3,NTIFCTR                                                     
         LA    RE,2(RE)           POINT TO NEXT TRACK                           
         J     PAVGW10                                                          
*                                                                               
PAVGWTX  J     SUBRXIT                                                          
***********************************************************************         
* EXTRASB  - CHECK IF NTI HAD ANY SP/BKS THAT WERE NOT FOUND IN CNAD.           
*            TABLE OF NTI PROGRAMS HAS ZERO'S FOR S/B ENTRIES THAT              
*            MATCHED. IF NO MATCH O ONE OF THEM, THEN ADJUST NTIDIVSR           
***********************************************************************         
EXTRASB  NTR1  BASE=*,LABEL=*                                                   
         L     RE,ANTIPR                                                        
         LA    RE,2(RE)         GO PAST NTI NUM                                 
         CLI   0(RE),0                                                          
         JNE   *+12                                                             
         LA    R1,2             STEP SIZE FOR PROG AVG                          
         J     EXTRSB10                                                         
         CLI   0(RE),1                                                          
         JNE   EXTRSB20                                                         
         LA    R1,4             STEP SIZE FOR TRACKS                            
EXTRSB10 LA    RE,1(RE)         PAST LEVEL NO                                   
EXTRSB20 CLI   0(RE),X'FE'                                                      
         JE    EXTRSB40                                                         
         CLI   0(RE),X'FF'                                                      
         JE    EXTRASBX         NO S/B FOUND                                    
         AR    RE,R1                                                            
         J     EXTRSB20                                                         
*                                                                               
EXTRSB40 LA    RE,1(RE)         S/B LEVEL FOUND                                 
EXTRSB50 CLI   0(RE),X'FF'                                                      
         JE    EXTRASBX                                                         
         OC    0(4,RE),0(RE)    BLANK TCAST NO?                                 
         JZ    EXTRSB60         YES. IT HAD A MATCH ON CNAD FILE                
         SR    R4,R4            NO. SUBTRACT ITS WEIGHT FROM                    
         ICM   R4,3,NTIDIVSR     TOTAL NTI WEIGHT                               
         SR    R5,R5                                                            
         ICM   R5,3,5(RE)       WEIGHT OF THIS S/B                              
         SR    R4,R5                                                            
         STCM  R4,3,NTIDIVSR                                                    
EXTRSB60 LA    RE,7(RE)                                                         
         J     EXTRSB50                                                         
*                                                                               
EXTRASBX J     SUBRXIT                                                          
***********************************************************************         
* EXTRATK  - CHECK IF NTI HAD ANY TRACKS THAT WERE NOT FOUND IN CNAD.           
*            TABLE OF NTI PROGRAMS HAS ZERO'S FOR TRACK ENTRIES THAT            
*            MATCHED                                                            
***********************************************************************         
EXTRATK  NTR1  BASE=*,LABEL=*                                                   
         L     RE,ANTIPR                                                        
         LA    RE,1+2(RE)  GO PAST NTI NUM AND LEVEL NUM                        
*                                                                               
EXTRAT10 CLI   0(RE),X'FF'        NO MORE TRACKS. ALL MATCHED                   
         JE    EXTRATN                                                          
         CLI   0(RE),X'FE'                                                      
         JE    EXTRATN                                                          
         OC    0(2,RE),0(RE)      CLEARS MEANS MATCHED W/CNAD                   
         JNZ   EXTRATY                                                          
         LA    RE,2+2(RE)  GO PAST THIS TRACK AND ITS WEIGHT                    
         J     EXTRAT10                                                         
*                                                                               
EXTRATY  DS    0H                                                               
         CR    RB,RB                                                            
         J     *+8                                                              
EXTRATN  CHI   RB,0                                                             
EXTRATKX J     SUBRXIT                                                          
         EJECT                                                                  
***********************************************************************         
* GETCVIR - FILL OUT TABLES OF VPH'S,IMPRESSNS,& RATINGS FOR CABLE NAD          
*           VPH = IMPRESSIONS / CNAD-HOMES(US)                                  
*           ADJUSTED IMPRESSIONS = VPH * NTI-HOMES(US)                          
*           RATINGS = ADJUSTED IMPRESSIONS / UNIVERSES                          
***********************************************************************         
GETCVIR  NTR1  BASE=*,LABEL=*                                                   
         LA    R1,NADWORK        TABLE OF UNIVERSES                             
         LA    R2,NADWORK        BUILD TABLE OF VPH'S                           
         LA    R3,NADWORK        BUILD TABLE OF RATINGS HERE                    
         LA    R4,NADWORK        CURRENTLY HOLDS RAW IMPRESSIONS                
         LA    R1,NADWRKU(R1)    TABLE OF UNIVERSES                             
         LA    R2,NADWRKV(R2)    BUILD TABLE OF VPH'S                           
         LA    R3,NADWRKR(R3)    BUILD TABLE OF RATINGS HERE                    
         LA    R4,NADWRKT(R4)    CURRENTLY HOLDS RAW IMPRESSIONS                
*                                BUILD TABLE OF ADJ IMPRESSIONS HERE            
         LA    R5,DINDMSET       LIST OF REQUESTED DEMOS                        
GETCR1   CLI   0(R5),X'FF'                                                      
         JE    GETCRX                                                           
*                                                                               
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         ICM   RF,15,0(R4)       RAW NAD IMPRESSIONS                            
         JZ    GETCRX                                                           
         M     RE,=F'10000'                                                     
         D     RE,CBNHOMES       /CABLE NAD HOMES                               
         SR    RE,RE                                                            
         AH    RF,=H'5'          ROUND IT                                       
         D     RE,=F'10'                                                        
         STCM  RF,15,0(R2)       =VPH                                           
*                                                                               
         ICM   RF,15,0(R4)       RAW NAD IMPRESSIONS                            
         M     RE,NTIHOMES       *NTI HOMES                                     
         D     RE,CBNHOMES       /CABLE NAD HOMES                               
         SR    RE,RE                                                            
         AH    RF,=H'50'         ROUND AND                                      
         D     RE,=F'100'        ADJUST TO HUNDREDS                             
         STCM  RF,15,0(R4)       = ADJUSTED IMPRESSIONS                         
*                                                                               
         SR    RE,RE             ADJUSTED IMPRESSIONS                           
         M     RE,=F'1000'                                                      
         ICM   R0,15,0(R1)       /UNIVERSES                                     
         JZ    GETCRX                                                           
         DR    RE,R0                                                            
         SR    RE,RE                                                            
         AHI   RF,5                                                             
         D     RE,=F'10'                                                        
         STCM  RF,15,0(R3)       =RATINGS                                       
*                                                                               
GETCR10  LA    R5,3(R5)                                                         
         LA    R1,4(R1)                                                         
         LA    R2,4(R2)                                                         
         LA    R3,4(R3)                                                         
         LA    R4,4(R4)                                                         
         J     GETCR1                                                           
*                                                                               
GETCRX   J     SUBRXIT                                                          
         EJECT                                                                  
*---------------------------------------------------------------------          
*WEIGHT- WGT NAD/NHT DEMOS ACCUM IN HOOK.                                       
*        R4->BUFFER W/VALUES                                                    
*        IF R4=0, JUST WEIGHT THE CABLE NAD HOMES                               
*---------------------------------------------------------------------          
WEIGHT   NTR1  BASE=*,LABEL=*                                                   
         SR    RE,RE                                                            
         ICM   RE,3,DBFACTOR                                                    
         ST    RE,DUB              FULL WORD ALLIGN                             
*                                                                               
         LTR   R4,R4                                                            
         JZ    WEGHOM                                                           
*                                                                               
         LA    R5,DINDMSET                                                      
WEG10    CLI   0(R5),X'FF'         EOL                                          
         JE    WEGX                DONE WITH THIS BUFFER                        
         SR    RE,RE                                                            
         ICM   RF,15,0(R4)                                                      
         JZ    WEGXT               BYPASS ZERO VALUES                           
         M     RE,DUB                                                           
         STCM  RF,15,0(R4)         RESLOT WGTD DEMO VALUE IN BUFFER             
*                                                                               
WEGXT    LA    R5,3(R5)            BUMP DEMO CODE POINTER                       
         LA    R4,4(R4)             AND VALUE POINTER                           
         J     WEG10                                                            
*                                                                               
WEGHOM   SR    RE,RE                                                            
         ICM   RF,15,CBNHOME2                                                   
         JZ    WEGXT               BYPASS ZERO VALUES                           
         M     RE,DUB                                                           
         STCM  RF,15,CBNHOME2      RESLOT WGTD DEMO VALUE IN BUFFER             
*                                                                               
WEGX     J     SUBRXIT                                                          
         EJECT                                                                  
*---------------------------------------------------------------------          
*UNWEIGHT- UNWGT NAD/NHT DEMOS ACCUM IN HOOK.                                   
*          R4->BUFFER W/VALUES                                                  
*          IF R4=0, JUST UNWEIGHT THE CABLE NAD HOMES                           
*---------------------------------------------------------------------          
UNWEIGHT NTR1  BASE=*,LABEL=*                                                   
         SR    RE,RE                                                            
         ICM   RE,3,DBDIVSOR                                                    
         ST    RE,DUB              FULL WORD ALLIGN                             
*                                                                               
         LTR   R4,R4                                                            
         JZ    UNWHOM                                                           
*                                                                               
         LA    R5,DINDMSET                                                      
UNWEG10  CLI   0(R5),X'FF'         EOL                                          
         JE    UNWEGX              DONE WITH THIS BUFFER                        
         SR    RE,RE                                                            
         ICM   RF,15,0(R4)                                                      
         JZ    UNWEGXT             BYPASS ZERO VALUES                           
         SLDA  RE,1                                                             
         D     RE,DUB                                                           
         AHI   RF,1                                                             
         SRA   RF,1                                                             
         STCM  RF,15,0(R4)         RESLOT UNWGTD DEMO VALUE IN BUFFER           
*                                                                               
UNWEGXT  LA    R5,3(R5)            BUMP DEMO CODE POINTER                       
         LA    R4,4(R4)             AND VALUE POINTER                           
         J     UNWEG10                                                          
*                                                                               
UNWHOM   SR    RE,RE                                                            
         ICM   RF,15,CBNHOME2                                                   
         JZ    UNWEGXT                                                          
         SLDA  RE,1                                                             
         D     RE,DUB                                                           
         AHI   RF,1                                                             
         SRA   RF,1                                                             
         STCM  RF,15,CBNHOME2                                                   
*                                                                               
UNWEGX   J     SUBRXIT                                                          
         EJECT                                                                  
*---------------------------------------------------------------------          
*OVREL  - CREATE X'DE' OVERRIDE ELEMS AND ADD TO DUMMYREC                       
*         INUT: FULL=MODIFIER,R4->BUFFER W/VALUES                               
*---------------------------------------------------------------------          
OVREL    NTR1  BASE=*,LABEL=*                                                   
         LA    R5,DINDMSET                                                      
OVREL10  CLI   0(R5),X'FF'         EOL                                          
         JE    OVRELX              DONE WITH THIS BUFFER                        
         CLI   0(R5),0             ONLY RELS NAD DEMOS (MKT BRK SET)            
         JE    OVRELNXT                                                         
         SR    RE,RE                                                            
         ICM   RF,15,0(R4)                                                      
         JZ    OVRELNXT             BYPASS ZERO VALUES                          
*                                                                               
         LA    RE,WORK             BLD DE OVERRIDE ELEM W/NAD/NHT VALUE         
         XC    0(30,RE),0(RE)                                                   
         USING NUOVEL,RE                                                        
         MVC   NUOVEL(3),=X'DE0C00'                                             
         MVC   NUOVCAT,0(R5)       MKT BRK                                      
         MVC   NUOVMOD,FULL        MODIFIER                                     
         MVC   NUOVNUM,2(R5)       DEMO NUMBER                                  
         MVI   NUOVFLG,X'80'       ACTUAL DEMO                                  
         MVI   NUOVPRE,X'81'       PRECISION OF FIELD                           
         MVC   NUOVVAL,0(R4)                                                    
*                                                                               
         LA    RF,NSPREC1          SET FIELD PRECISION                          
         CLI   NBPOSTYP,C'C'                                                    
         JNE   *+8                                                              
         LA    RF,COPREC1          CABLE PRECISION                              
         CLC   NUOVMOD,0(RF)                                                    
         JE    *+12                                                             
         LA    RF,2(RF)                                                         
         J     *-14                                                             
         MVC   NUOVPRE,1(RF)                                                    
         DROP  RE                                                               
*                                 ADD OVERRIDE TO CUMMULATED NTI RECD           
*        GOTO1 NBHELLO,DMCB,(C'P',=C'PAVFIL'),DUMMYREC,WORK                     
**       L     RF,=V(HELLO)                                                     
**       A     RF,RELO                                                          
         L     RF,NBHELLO                                                       
         GOTO1 (RF),DMCB,(C'p',=C'PAVFIL'),DUMMYREC,WORK,0,F'3000'              
         CLI   DMCB+12,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
OVRELNXT LA    R5,3(R5)            BUMP DEMO CODE POINTER                       
         LA    R4,4(R4)             AND VALUE POINTER                           
         J     OVREL10                                                          
*                                                                               
OVRELX   J     SUBRXIT                                                          
         EJECT                                                                  
***********************************************************************         
* CNADDIV - ACCUMULATE DIVSOR FOR CABLE NAD                                     
*           WE COME BACK FROM DEMAND WITH ALL SPANNING RECORDS AND              
*           DBDIVSOR ACCOUNTS FOR ALL OF THEM                                   
* INPUT: DBAREC                                                                 
* OUTPUT: CNDIVSOR  (ADJUSTED DIVSOR)                                           
***********************************************************************         
CNADDIV  NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         MVI   BYTE,0                                                           
         L     R6,DBAQUART                                                      
CNADD5   CLI   0(R6),0             NO ELEMENT FOUND                             
         JE    CNADDIVX                                                         
         CLI   0(R6),X'23'         LEAD ELEMENT FOR FIRST MARKET BREAK          
         JE    CNADD10                                                          
         ZIC   R0,1(R6)                                                         
         AR    R6,R0                                                            
         CLI   0(R6),X'0F'                                                      
         JE    CNADDIVX                                                         
         J     CNADD5                                                           
*                                                                               
         USING SLELEM,R6                                                        
CNADD10  CLI   SLSECT,1            IS THIS THE FIRST SPANNING RECORD?           
         JE    *+12                SHOULD HAVE USA LEVEL ON IT(MKTBR=1)         
         MVI   BYTE,2              NO. UNIVERSES FIRST PASS ONLY                
         J     CNADDIVX                                                         
*                                                                               
         SR    RE,RE               YES. ADD DBFACTOR TO ACCUMULATOR             
         ICM   RE,3,CNDIVSOR                                                    
         SR    RF,RF                                                            
         ICM   RF,3,DBFACTOR                                                    
         AR    RE,RF                                                            
         STCM  RE,3,CNDIVSOR                                                    
*                                                                               
CNADDIVX J     SUBRXIT                                                          
*                                                                               
         LTORG                                                                  
*                                                                               
                                                                                
DODEMOUT NTR1  BASE=*,LABEL=*                                                   
         BRAS  RE,USEDLOCK         USE DEMOLOCK OVERRIDES                       
*                                                                               
         GOTO1 NBDEMOUT,DMCB,(C'L',DIALIST),DBLOCK,DOALIST                      
         GOTO1 NBDEMOUT,DMCB,(C'L',DIULIST),DBLOCK,DOULIST                      
*                                                                               
* COMSCORE DEMOS                                                                
*                                                                               
         TM    NBINDS9,NBI9CP1+NBI9CP2  CALL COMSCORE?                          
         JNZ   DODEM10                                                          
         BRAS  RE,GETCPROG         GET IMPRESSIONS FROM CPROG                   
         BRAS  RE,GETCOMOV         GET OVERRIDES FROM UNIT                      
         J     DODEMX                                                           
*                                                                               
DODEM10  BRAS  RE,DOCOMDEM         REQUEST/GET DEMOS FROM COMSCORE              
*                                                                               
DODEMX   BRAS  RE,RESDLOCK         RESTORE DEMOLOCK OVERRIDES                   
         BRAS  RE,DOVERIND         DEMO OVERRIDE INDICATORS                     
         J     SUBRXIT                                                          
         LTORG                                                                  
*                                                                               
* GET CPROG IMPRESSIONS FOR ESTIMATES                                           
*                                                                               
GETCPROG NTR1  BASE=*,LABEL=*                                                   
         TM    NBINDS9,NBI9GDM     REQUESTING GUARANTEES?                       
         JO    GCPROGX             YES - NO GUARANTEES FOR COMSCORE             
         CLI   SVBYTE,NUOVDDQ      ESTIMATED VALUES?                            
         JNE   GCPROGX             YES - USE CPROG                              
*                                                                               
         L     R6,AWORKREC         A(PROGRAM RECORD)                            
         USING NPGRECD,R6                                                       
         CLC   =X'0D20',0(R6)      HAVE PROGRAM RECORD?                         
         JE    GCPR05                                                           
                                                                                
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING NPGRECD,R6                                                       
         MVC   NPGKTYP,=X'0D20'                                                 
         MVC   NPGKAM,NBACTAM                                                   
         MVC   NPGKNET,NBMARKET                                                 
         MVC   NPGKPROG,NBACTPRG                                                
         MVC   NPGKEND,NBACTDAT                                                 
         CLI   NBMGDOPT,C'Y'       USE MADE GOOD FOR PROGGRAM                   
         BNE   GCPR02                                                           
         MVC   NPGKPROG,NBMGFPCD   YES/SET MADE GOOD FOR PROG/DATE              
         MVC   NPGKEND,NBMGFDAT                                                 
*                                                                               
GCPR02   MVC   KEYSAVE,KEY                                                      
         GOTO1 NBDM,DMCB,=C'DMRDHI',=C'SPTDIR',KEY,KEY                          
         CLC   KEY(NPGKEND-NPGKEY),KEYSAVE  TEST FOR SAME PROGRAM               
         BNE   GCPROGX             EXIT IMMEDIATELY W CC=NEQ                    
         LA    R2,KEY+14           A(DISK ADDRESS)                              
         L     R6,AWORKREC                                                      
         GOTO1 NBDM,DMCB,=C'GETREC',=C'SPTFIL',(R2),(R6),DMWORK                 
*                                                                               
GCPR05   L     R4,NBADEM                                                        
         LTR   R4,R4                                                            
         JZ    GCPROGX                                                          
         USING NDDEMBLK,R4                                                      
         LA    R4,NDDEMOS                                                       
         LA    R5,NDNTDMS                                                       
         TM    NBINDS6,NBI6XDEM                                                 
         JNO   *+12                                                             
         USING XDDEMBLK,R4                                                      
         LA    R4,XDDEMOS          A(INPUT DEMO CATEGORIES)                     
         LA    R5,XDNTDMS          A(COMSCORE DEMO NAME LIST)                   
*                                                                               
         OC    NBEXTEND,NBEXTEND                                                
         JZ    GCPR07                                                           
         L     RF,NBEXTEND                                                      
         USING NBXTNDD,RF                                                       
         OC    NBXCDNL,NBXCDNL     A(COMSCORE DEMO NAME LIST)                   
         JZ    *+8                                                              
         L     R5,NBXCDNL          A(COMSCORE DEMO NAME LIST)                   
GCPR07   ST    R5,AXCDNL                                                        
*                                                                               
         LA    R7,DOALIST                                                       
         AHI   R7,5*4              FIRST 5 ENTRIES ARE ALWAYS FOR HOMES         
*                                  AFTER HOMES, THEN 3 ENTRIES PER              
*                                  DEMO CAT, VPHS - RTGS - IMPS                 
*                                                                               
CGPR10   CLI   0(R4),X'FF'         NO MORE DEMOS?                               
         JE    GCPROGX                                                          
         CLI   2(R4),0             COMSCORE DEMO?                               
         JNE   CGPR30                                                           
*                                                                               
         L     R5,AXCDNL           A(COMSCORE DEMO NAME LIST)                   
         ZIC   RF,1(R4)            COMSCORE DEMO OFFSET                         
         SHI   RF,1                                                             
         MH    RF,=H'8'                                                         
         AR    R5,RF                                                            
*                                                                               
         MVI   SRCHEL,NPGCELQ      GET CPROG IMPRESSIONS                        
*                                                                               
         USING NPGCELDD,R6                                                      
         L     R6,AWORKREC         A(PROGRAM RECORD)                            
         BAS   RE,GETEL7           GET OVERRIDE ELEMENTS                        
         J     *+8                                                              
CGPR20   BAS   RE,NEXTEL7                                                       
         JNE   CGPR30                                                           
*                                                                               
         OC    NPGCCAT,=CL8' '                                                  
         CLC   NPGCCAT,0(R5)       SAME CATEGORY?                               
         JNE   CGPR20                                                           
         MVC   8(4,R7),NPGCAMT     SET IMPRESSION                               
         CLI   NBPOSTYP,C'C'       CABLE?                                       
         JNE   CGPR30                                                           
         ICM   RF,15,NPGCAMT                                                    
         MH    RF,=H'10'                                                        
         STCM  RF,15,8(R7)                                                      
         DROP  R6                                                               
*                                                                               
CGPR30   AHI   R4,3                BUMP TO NEXT DEMO CATEGORY                   
         AHI   R7,3*4              BUMP TO NEXT DEMO CAT ENTRIES                
         J     CGPR10                                                           
*                                                                               
GCPROGX  J     SUBRXIT                                                          
*                                                                               
         GETELN R6,24,SRCHEL,7     GETEL FOR SPOT                               
         LTORG                                                                  
*                                                                               
* GET COMSCORE OVERRIDES FROM UNIT AND SET INDICATOR IF FLAG IS ON              
*                                                                               
GETCOMOV NTR1  BASE=*,LABEL=*                                                   
         TM    NBINDS9,NBI9GDM     REQUESTING GUARANTEES?                       
         JO    GCOVX               YES - NO GUARANTEES FOR COMSCORE             
*                                                                               
         L     R4,NBADEM                                                        
         LTR   R4,R4                                                            
         JZ    GCOVX                                                            
         USING NDDEMBLK,R4                                                      
         LA    R4,NDDEMOS                                                       
         LA    R5,NDNTDMS                                                       
         TM    NBINDS6,NBI6XDEM                                                 
         JNO   *+12                                                             
         USING XDDEMBLK,R4                                                      
         LA    R4,XDDEMOS          A(INPUT DEMO CATEGORIES)                     
         LA    R5,XDNTDMS          A(COMSCORE DEMO NAME LIST)                   
*                                                                               
         OC    NBEXTEND,NBEXTEND                                                
         JZ    GCOV05                                                           
         L     RF,NBEXTEND                                                      
         USING NBXTNDD,RF                                                       
         OC    NBXCDNL,NBXCDNL     A(COMSCORE DEMO NAME LIST)                   
         JZ    *+8                                                              
         L     R5,NBXCDNL          A(COMSCORE DEMO NAME LIST)                   
GCOV05   ST    R5,AXCDNL                                                        
*                                                                               
         LA    R7,DOALIST                                                       
         AHI   R7,5*4              FIRST 5 ENTRIES ARE ALWAYS FOR HOMES         
*                                  AFTER HOMES, THEN 3 ENTRIES PER              
*                                  DEMO CAT, VPHS - RTGS - IMPS                 
*                                                                               
GCOV10   CLI   0(R4),X'FF'         NO MORE DEMOS?                               
         JE    GCOVX                                                            
         CLI   2(R4),0             COMSCORE DEMO?                               
         JNE   GCOV50                                                           
*                                                                               
         L     R5,AXCDNL           A(COMSCORE DEMO NAME LIST)                   
         ZIC   RF,1(R4)            COMSCORE DEMO OFFSET                         
         SHI   RF,1                                                             
         MH    RF,=H'8'                                                         
         AR    R5,RF                                                            
*                                                                               
         MVI   SRCHEL,NUCOV1LQ     DEFAULT TO ESTIMATE OVERRIDE                 
         CLI   SVBYTE,NUOVDEQ      ACTUAL OVERRIDES?                            
         JNE   *+8                                                              
         MVI   SRCHEL,NUCOV2LQ                                                  
*                                                                               
         USING NUCOVD,R6                                                        
         L     R6,NBAIO                                                         
         BAS   RE,GETEL8           GET OVERRIDE ELEMENTS                        
         J     *+8                                                              
GCOV20   BAS   RE,NEXTEL8                                                       
         JNE   GCOV50                                                           
*                                                                               
         CLC   NUCOVCAT,0(R5)      SAME CATEGORY?                               
         JNE   GCOV20                                                           
         CLI   NUCOVMOD,NUCOVMTQ   IMPRESSION OVERRIDE?                         
         JNE   GCOV30                                                           
         MVC   8(4,R7),NUCOVVAL    SET IMPRESSION OVERRIDE                      
*                                                                               
         TM    NBINDS9,NBI9DOV     RETURN OVERRIDE INDICATOR?                   
         JZ    *+8                                                              
         OI    8(R7),X'80'         SET OVERRIDE INDICATOR                       
*                                                                               
GCOV30   CLI   NUCOVMOD,NUCOVMRQ   RATING OVERRIDE?                             
         JNE   GCOV50                                                           
         MVC   4(4,R7),NUCOVVAL    SET IMPRESSION OVERRIDE                      
*                                                                               
         TM    NBINDS9,NBI9DOV     RETURN OVERRIDE INDICATOR?                   
         JZ    *+8                                                              
         OI    4(R7),X'80'         SET OVERRIDE INDICATOR                       
         DROP  R6                                                               
*                                                                               
GCOV50   AHI   R4,3                BUMP TO NEXT DEMO CATEGORY                   
         AHI   R7,3*4              BUMP TO NEXT DEMO CAT ENTRIES                
         J     GCOV10                                                           
*                                                                               
GCOVX    J     SUBRXIT                                                          
*                                                                               
         GETELN R6,NBDTADSP,SRCHEL,8                                            
         LTORG                                                                  
*                                                                               
* SET DEMO OVERRIDE INDICATORS                                                  
* HIGH ORDER BIT IN DOALIST VALUES WILL BE X'80'                                
*                                                                               
DOVERIND NTR1  BASE=*,LABEL=*                                                   
         TM    NBINDS9,NBI9DOV     RETURN OVERRIDE INDICATOR?                   
         JZ    DOVERX                                                           
*                                                                               
         LA    R3,DOALIST                                                       
         LA    R4,DIALIST                                                       
DOI10    CLI   0(R4),X'FF'         NO MORE DEMOS?                               
         JE    DOVERX                                                           
*                                                                               
         MVI   SRCHEL,NUOVDDQ      DEFAULT TO ESTIMATE OVERRIDE                 
         CLI   SVBYTE,NUOVDEQ      ACTUAL OVERRIDES?                            
         JNE   *+8                                                              
         MVI   SRCHEL,NUOVDEQ      ACTUAL OVERRIDES                             
*                                                                               
         USING NUOVD,R6                                                         
         L     R6,NBAIO                                                         
         BAS   RE,GETEL9           GET OVERRIDE ELEMENTS                        
         J     *+8                                                              
DOI20    BAS   RE,NEXTEL9                                                       
         JNE   DOI30                                                            
*                                                                               
         CLC   NUOVCAT,0(R4)       SAME CATEGORY?                               
         JNE   DOI20                                                            
         CLC   NUOVMOD,1(R4)       SAME MODIFIER?                               
         JNE   DOI20                                                            
         CLC   NUOVNUM,2(R4)       SAME DEMO NUMBER?                            
         JNE   DOI20                                                            
         MVI   0(R3),X'80'                                                      
         DROP  R6                                                               
*                                                                               
DOI30    AHI   R4,3                BUMP TO NEXT DEMO CATEGORY                   
         AHI   R3,4                BUMP TO NEXT DEMO VALUE                      
         J     DOI10                                                            
*                                                                               
DOVERX   J     SUBRXIT                                                          
*                                                                               
         GETELN R6,NBDTADSP,SRCHEL,9                                            
*                                                                               
* USE DEMOLOCK OVERRIDES ON UNITS FOR DEMOUT CALL                               
* THIS ROUTINE TEMPORARILY RENAMES THE FOLLOWING ELEMENTS                       
* IN THIS ORDER:                                                                
*      X'DD' TO X'F4'              EST OVERRIDE TO TEMP F4                      
*      X'D4' TO X'DD'              DEMOLOCK EST OVERRIDE TO EST                 
*                                                                               
USEDLOCK NTR1  BASE=*,LABEL=*                                                   
         NI    FLAGS1,X'FF'-F1DEMLCK                                            
         TM    NBINDS9,NBI9SLDV    SHOW LOCKED DEMO VALUES?                     
         JZ    UDLOCKX                                                          
         TM    NBINDS9,NBI9GDM     REQUESTING GUARANTEES?                       
         JZ    UDLOCKX                                                          
         CLI   SVBYTE,NUOVDDQ      ESTIMATE OVERRIDES?                          
         JNE   UDLOCKX                                                          
*                                                                               
         MVI   SRCHEL,NUOVD4Q      ANY LOCKED DEMOS?                            
         L     R6,NBAIO                                                         
         BRAS  RE,GETEL9           GET OVERRIDE ELEMENTS                        
         JE    UDLOCK05                                                         
*                                                                               
         MVI   SRCHEL,NUDTAELQ     WAS DEMOLOCK RUN ON THIS UNIT                
         L     R6,NBAIO            EVEN THO THERE WERE NO OVERRIDES?            
         BRAS  RE,GETEL9                                                        
         JNE   UDLOCKX                                                          
         USING NUDTAD,R6                                                        
         TM    NUUNST5,NST5DLCK    YES - DISPLAY ORIGINAL DEMO VALUES           
         JZ    UDLOCKX                                                          
*                                                                               
UDLOCK05 OI    FLAGS1,F1DEMLCK                                                  
*                                                                               
         MVI   SRCHEL,NUOVDDQ                                                   
         USING NUOVD,R6                                                         
         L     R6,NBAIO                                                         
         BRAS  RE,GETEL9           GET OVERRIDE ELEMENTS                        
         J     *+8                                                              
UDLOCK10 BRAS  RE,NEXTEL9                                                       
         JNE   UDLOCK20                                                         
         MVI   NUOVEL,X'F4'        TEMPORARILY MAKE THEM F4 ELEMENTS            
         J     UDLOCK10                                                         
*                                                                               
UDLOCK20 MVI   SRCHEL,NUOVD4Q      DEFAULT TO ESTIMATE OVERRIDE                 
         USING NUOVD,R6                                                         
         L     R6,NBAIO                                                         
         BRAS  RE,GETEL9           GET OVERRIDE ELEMENTS                        
         J     *+8                                                              
UDLOCK30 BRAS  RE,NEXTEL9                                                       
         JNE   UDLOCKX                                                          
         MVI   NUOVEL,NUOVDDQ      TEMPORARILY MAKE THEM DD ELEMENTS            
         J     UDLOCK30                                                         
*                                                                               
UDLOCKX  J     SUBRXIT                                                          
         LTORG                                                                  
*                                                                               
* RESTORE OVERRIDES ON UNITS AFTER DEMOUT CALL                                  
* THIS ROUTINE RENAMES THE FOLLOWING ELEMENTS IN THIS ORDER:                    
*      X'DD' TO X'D4'              EST OVERRIDE TO DEMOLOCK EST                 
*      X'F4' TO X'DD'              TEMP F4 TO EST OVERRIDE                      
*                                                                               
RESDLOCK NTR1  BASE=*,LABEL=*                                                   
         TM    FLAGS1,F1DEMLCK     EXECUTED USEDLOCK ROUTINE                    
         JZ    RDLOCKX                                                          
         TM    NBINDS9,NBI9SLDV    SHOW LOCKED DEMO VALUES?                     
         JZ    RDLOCKX                                                          
         TM    NBINDS9,NBI9GDM     REQUESTING GUARANTEES?                       
         JZ    RDLOCKX                                                          
         CLI   SVBYTE,NUOVDDQ      ESTIMATE OVERRIDES?                          
         JNE   RDLOCKX                                                          
*                                                                               
         MVI   SRCHEL,NUOVDDQ      DEFAULT TO ESTIMATE OVERRIDE                 
         USING NUOVD,R6                                                         
         L     R6,NBAIO                                                         
         BRAS  RE,GETEL9           GET OVERRIDE ELEMENTS                        
         J     *+8                                                              
RDLOCK10 BRAS  RE,NEXTEL9                                                       
         JNE   RDLOCK20                                                         
         MVI   NUOVEL,NUOVD4Q      NAME THEM BACK                               
         J     RDLOCK10                                                         
*                                                                               
* MAKE X'F4' TO X'DD'                                                           
*                                                                               
RDLOCK20 MVI   SRCHEL,X'F4'        DEFAULT TO ESTIMATE OVERRIDE                 
         USING NUOVD,R6                                                         
         L     R6,NBAIO                                                         
         BRAS  RE,GETEL9           GET OVERRIDE ELEMENTS                        
         J     *+8                                                              
RDLOCK30 BRAS  RE,NEXTEL9                                                       
         JNE   RDLOCKX                                                          
         MVI   NUOVEL,NUOVDDQ      NAME THEM BACK                               
         J     RDLOCK30                                                         
*                                                                               
RDLOCKX  J     SUBRXIT                                                          
         LTORG                                                                  
*                                                                               
* CALL COMSCORE TO REQUEST/GET DEMO VALUES                                      
*                                                                               
DOCOMDEM NTR1  BASE=*,LABEL=*                                                   
         TM    NBINDS9,NBI9CP1+NBI9CP2  COMSCORE?                               
         JZ    DCDX                                                             
         OC    NBEXTEND,NBEXTEND                                                
         JZ    DCDX                                                             
*                                                                               
         CLI   SVBYTE,NUOVDDQ      ESTIMATED VALUES?                            
         JE    DCD100              YES                                          
*                                                                               
* ACTUAL DEMO VALUES - CALL COMSCORE FOR IMPS & RTGS                            
*                                                                               
         USING DBCNWD,R3                                                        
DCD10    LA    R3,DBCNWX           BUILD COMSCORE PARAM BLOCK                   
         XC    0(DBCNWLNQ,R3),0(R3)                                             
         MVC   0(4,R3),=C'NWRI'                                                 
*                                                                               
         OC    DBEXTEND,DBEXTEND   ANY EXTENSIONS?                              
         JNZ   *+12                                                             
         ST    R3,DBEXTEND                                                      
         J     DCD40                                                            
*                                                                               
         L     RF,DBEXTEND         FIND FIRST AVAILABLE EXTENSION               
DCD20    CLC   =C'NWRI',0(RF)      ALREADY ADDED IT?                            
         JE    DCD30                                                            
         OC    4(4,RF),4(RF)       A(NEXT EXTENSION)                            
         JZ    *+12                                                             
         ICM   RF,15,4(RF)                                                      
         J     DCD20                                                            
DCD30    STCM  R3,15,4(RF)         SET EXTENSION ADDRESS                        
*                                                                               
DCD40    L     R4,NBADEM                                                        
         LTR   R4,R4                                                            
         JZ    DCDX                                                             
         USING NDDEMBLK,R4                                                      
         LA    R4,NDDEMOS                                                       
         LA    R5,NDNTDMS                                                       
         TM    NBINDS6,NBI6XDEM                                                 
         JNO   *+12                                                             
         USING XDDEMBLK,R4                                                      
         LA    R4,XDDEMOS          A(INPUT DEMO CATEGORIES)                     
         LA    R5,XDNTDMS          A(COMSCORE DEMO NAME LIST)                   
*                                                                               
         L     RF,NBEXTEND                                                      
         USING NBXTNDD,RF                                                       
         OC    NBXCDNL,NBXCDNL     A(COMSCORE DEMO NAME LIST)                   
         JZ    *+8                                                              
         L     R5,NBXCDNL          A(COMSCORE DEMO NAME LIST)                   
*                                                                               
         OC    0(200,R5),0(R5)     ANY COMSCORE DEMOS?                          
         JNZ   *+14                                                             
         OC    200(200,R5),200(R5)                                              
         JZ    DCDX                                                             
*                                                                               
         ST    R4,DBCNWRDL         A(REQUESTED DEMO LIST)                       
         ST    R5,DBCNWDNL         A(COMSCORE DEMO NAME LIST)                   
*                                                                               
         LA    R4,DOALIST                                                       
         AHI   R4,5*4              FIRST 5 ENTRIES ARE ALWAYS FOR HOMES         
*                                  AFTER HOMES, THEN 3 ENTRIES PER              
*                                  DEMO CAT, VPHS - RTGS - IMPS                 
*                                                                               
         ST    R4,DBCNWODV         A(OUTPUT DEMO VALUES)                        
*                                                                               
         MVI   DBCNWPRE,C'1'       PRECISION                                    
         TM    NBINDS3,NBI3A2DC    2 DECIMAL PRECISION?                         
         JZ    *+8                                                              
         MVI   DBCNWPRE,C'2'                                                    
*                                                                               
         OI    DBCNWFLG,DBCNWFPQ   DEFAULT TO PROGRAM AVERAGE                   
         MVC   DBCNWSD,NBACTDAT    UNIT START DATE                              
         MVC   DBCNWED,NBACTDAT    UNIT END DATE                                
         MVC   DBCNWST(L'NBTIME),NBTIME   START/END TIME                        
         MVC   DBCNWROT,NBSDROT    ROTATION                                     
*                                                                               
         ICM   R4,15,NBCNVNTI      CHECK FOR COMSCORE NETWORK #                 
         BZ    DCDX                                                             
         AHI   R4,3                BUMP TO START OF NTWKS                       
         LA    RF,799              MAX STATIONS IN LIST                         
DCD50    CLC   NBACTNET,0(R4)                                                   
         BE    DCD55                                                            
         AHI   R4,20               BUMP TO NEXT ENTRY                           
         BCT   RF,DCD50                                                         
         B     DCDX                                                             
DCD55    MVC   DBCNWNET,10(R4)     COMSCORE NETWORK NUMBER                      
*                                                                               
         L     RF,NBEXTEND                                                      
         USING NBXTNDD,RF                                                       
         MVC   DBCNWSN,NBXCSN      COMSCORE SERIES NUMBER                       
         MVC   DBCNWVT,NBXCVT      COMSCORE VIEWING TYPE                        
         OC    NBCVTYP,NBCVTYP     OVERRIDE COMSCORE VIEWING TYPE?              
         JZ    *+10                                                             
         MVC   DBCNWVT,NBCVTYP                                                  
*                                                                               
         TM    NBINDS9,NBI9CP2     COMSCORE PASS 2?                             
         JO    DCD60                                                            
*                                                                               
* PASS 1 - CALL COMINTER TO REQUEST ACTUALS                                     
*                                                                               
         GOTO1 NBXCOMIN,DMCB,=C'PUT',DBLOCK                                     
         J     DCDX                                                             
*                                                                               
* PASS 2 - CALL COMINTER TO GET ACTUALS                                         
*                                                                               
DCD60    GOTO1 NBXCOMIN,DMCB,=C'GET',DBLOCK                                     
         BRAS  RE,GETCOMOV         GET OVERRIDES FROM UNIT                      
         J     DCDX                                                             
         DROP  RF                                                               
*                                                                               
* ESTIMATED DEMO VALUES - CALL COMSCORE FOR UNIVERSES                           
*                                                                               
         USING DBCNWD,R3                                                        
DCD100   LA    R3,DBCNWX           BUILD COMSCORE PARAM BLOCK                   
         XC    0(DBCNWLNQ,R3),0(R3)                                             
         MVC   0(4,R3),=C'NWRI'                                                 
*                                                                               
         OC    DBEXTEND,DBEXTEND   ANY EXTENSIONS?                              
         JNZ   *+12                                                             
         ST    R3,DBEXTEND                                                      
         J     DCD120                                                           
*                                                                               
         L     RF,DBEXTEND         FIND FIRST AVAILABLE EXTENSION               
DCD110   CLC   =C'NWRI',0(RF)      ALREADY ADDED IT?                            
         JE    DCD115                                                           
         OC    4(4,RF),4(RF)       A(NEXT EXTENSION)                            
         JZ    *+12                                                             
         ICM   RF,15,4(RF)                                                      
         J     DCD110                                                           
DCD115   STCM  R3,15,4(RF)         SET EXTENSION ADDRESS                        
*                                                                               
DCD120   L     R4,NBADEM                                                        
         LTR   R4,R4                                                            
         JZ    DCDX                                                             
         USING NDDEMBLK,R4                                                      
         LA    R4,NDDEMOS                                                       
         LA    R5,NDNTDMS                                                       
         TM    NBINDS6,NBI6XDEM                                                 
         JNO   *+12                                                             
         USING XDDEMBLK,R4                                                      
         LA    R4,XDDEMOS          A(INPUT DEMO CATEGORIES)                     
         LA    R5,XDNTDMS          A(COMSCORE DEMO NAME LIST)                   
*                                                                               
         L     RF,NBEXTEND                                                      
         USING NBXTNDD,RF                                                       
         OC    NBXCDNL,NBXCDNL     A(COMSCORE DEMO NAME LIST)                   
         JZ    *+8                                                              
         L     R5,NBXCDNL          A(COMSCORE DEMO NAME LIST)                   
*                                                                               
         OC    0(200,R5),0(R5)     ANY COMSCORE DEMOS?                          
         JNZ   *+14                                                             
         OC    200(200,R5),200(R5)                                              
         JZ    DCDX                                                             
*                                                                               
         ST    R4,DBCNWRDL         A(REQUESTED DEMO LIST)                       
         ST    R5,DBCNWDNL         A(COMSCORE DEMO NAME LIST)                   
*                                                                               
         LA    R4,DOALIST                                                       
         AHI   R4,5*4              FIRST 5 ENTRIES ARE ALWAYS FOR HOMES         
*                                  AFTER HOMES, THEN 3 ENTRIES PER              
*                                  DEMO CAT, VPHS - RTGS - IMPS                 
*                                                                               
         ST    R4,DBCNWODV         A(OUTPUT DEMO VALUES)                        
*                                                                               
* CALL COMSCORE TO GET UNIVERSE VALUES.  WE ONLY NEED TO SEND SOME              
* DEFAULT VALUES FOR MOST FIELDS.                                               
*                                                                               
         MVI   DBCNWPRE,C'1'       PRECISION                                    
         TM    NBINDS3,NBI3A2DC    2 DECIMAL PRECISION?                         
         JZ    *+8                                                              
         MVI   DBCNWPRE,C'2'                                                    
*                                                                               
* START/END DATES ARE ALWAYS JAN1-JAN7 OF UNIT DATE YEAR                        
*                                                                               
         GOTO1 NBDATCON,DMCB,(2,NBACTDAT),(3,DUB)                               
         MVC   DUB+1(2),=X'0101'                                                
         GOTO1 NBDATCON,DMCB,(3,DUB),(2,DBCNWSD)    START DATE                  
         MVC   DUB+1(2),=X'0107'                                                
         GOTO1 NBDATCON,DMCB,(3,DUB),(2,DBCNWED)    END DATE                    
*                                                                               
         OI    DBCNWFLG,DBCNWFPQ   PROGRAM AVERAGE                              
         OI    DBCNWFLG,DBCNWUQ    GET UNIVERSES                                
         MVC   DBCNWST(L'NBTIME),=X'025804B0'   START/END TIME                  
         MVI   DBCNWROT,X'FF'      ROTATION (DEFAULT YYYYYYY)                   
         MVI   DBCNWVT,C'L'        COMSCORE VIEWING TYPE (DEFAULT LIVE)         
         MVI   DBCNWNET,C'3'       COMSCORE NETWORK # (DEFAULT ABC)             
*                                                                               
         L     RF,NBEXTEND                                                      
         USING NBXTNDD,RF                                                       
*                                                                               
         TM    NBINDS9,NBI9CP2     COMSCORE PASS 2?                             
         JO    DCD130                                                           
*                                                                               
* PASS 1 - CALL COMINTER TO REQUEST UNIVERSES                                   
*                                                                               
         GOTO1 NBXCOMIN,DMCB,=C'PUT',DBLOCK                                     
         J     DCDX                                                             
*                                                                               
* PASS 2 - CALL COMINTER TO GET UNIVERSES                                       
*                                                                               
DCD130   GOTO1 NBXCOMIN,DMCB,=C'GET',DBLOCK                                     
         DROP  RF                                                               
*                                                                               
         BRAS  RE,GETCPROG         GET IMPRESSIONS FROM CPROG                   
         BRAS  RE,GETCOMOV         GET OVERRIDES FROM UNIT                      
*                                                                               
* CALCULATE RATINGS = IMPRESSIONS/UNIVERSES                                     
*                                                                               
         L     R4,NBADEM                                                        
         LTR   R4,R4                                                            
         JZ    DCDX                                                             
         USING NDDEMBLK,R4                                                      
         LA    R4,NDDEMOS                                                       
         LA    R5,NDNTDMS                                                       
         TM    NBINDS6,NBI6XDEM                                                 
         JNO   *+12                                                             
         USING XDDEMBLK,R4                                                      
         LA    R4,XDDEMOS          A(INPUT DEMO CATEGORIES)                     
         LA    R5,XDNTDMS          A(COMSCORE DEMO NAME LIST)                   
*                                                                               
         L     RF,NBEXTEND                                                      
         USING NBXTNDD,RF                                                       
         OC    NBXCDNL,NBXCDNL     A(COMSCORE DEMO NAME LIST)                   
         JZ    *+8                                                              
         L     R5,NBXCDNL          A(COMSCORE DEMO NAME LIST)                   
*                                                                               
         LA    R7,DOALIST                                                       
         AHI   R7,5*4              FIRST 5 ENTRIES ARE ALWAYS FOR HOMES         
*                                  AFTER HOMES, THEN 3 ENTRIES PER              
*                                  DEMO CAT, VPHS - RTGS - IMPS                 
*                                                                               
DCD140   CLI   0(R4),X'FF'         NO MORE DEMOS?                               
         JE    DCDX                                                             
         CLI   2(R4),0             COMSCORE DEMO?                               
         JNE   DCD150                                                           
*                                                                               
* CALCULATE RATINGS = IMPRESSIONS/UNIVERSES                                     
*                                                                               
         OC    0(4,R7),0(R7)       ANY UNIVERSE VALUES?                         
         JZ    DCD150                                                           
         OC    8(4,R7),8(R7)       ANY IMPRESSIONS?                             
         JZ    DCD145                                                           
         OC    4(4,R7),4(R7)       ANY RATING OVERRIDES?                        
         JNZ   DCD145                                                           
         MVC   FULL,0(R7)          UNIVERSE VALUES                              
         ICM   RF,15,8(R7)         IMPRESSIONS                                  
         MHI   RF,1000                                                          
         SR    RE,RE                                                            
         D     RE,FULL                                                          
         STCM  RF,15,4(R7)         SET RATINGS                                  
*                                                                               
DCD145   XC    0(4,R7),0(R7)       SINCE WE USED VPH BYTES FOR UNIVS            
*                                  CLEAR IT OUT                                 
*                                                                               
DCD150   AHI   R4,3                BUMP TO NEXT DEMO CATEGORY                   
         AHI   R7,3*4              BUMP TO NEXT DEMO CAT ENTRIES                
         J     DCD140                                                           
*                                                                               
DCDX     J     SUBRXIT                                                          
*                                                                               
DBCNWX   DS    XL100               COMSCORE PARAM BLOCK                         
*                                                                               
         GETELN R6,NBDTADSP,SRCHEL,6                                            
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
SETEXPD  NTR1  BASE=*,LABEL=*                                                   
* SET EXPANDED DEMO REQUEST IN DEMO LIST                                        
         TM    NBSBKEND,NBEXPDEM+NBEXPDM6   EXPANDED DEMO VALUES?               
         BZ    SETUT20             NO                                           
         LA    R4,76                76 DEMOS IN LIST                            
         LA    R3,DIAIHOME                                                      
SETUT15  CLI   0(R3),X'FF'          IF END OF LIST                              
         BE    SETUT20                                                          
         CLI   1(R3),C'T'           IF DEMO IS C'T'                             
         BE    *+12                                                             
         CLI   1(R3),C'H'           IF DEMO IS C'H'  FOR CABLE                  
         BNE   SETUT17                                                          
         MVI   1(R3),C'Y'           SET IT TO C'Y'                              
         CLI   RESULT,C'J'         IF GAA AVAILABLE                             
         BNE   *+8                                                              
         MVI   1(R3),C'B'          GAA BASE IMPS                                
SETUT17  LA    R3,3(R3)                                                         
         BCT   R4,SETUT15           NEXT DEMO                                   
SETUT20  J     SUBRXIT                                                          
*                                                                               
* RESET EXPANDED DEMO REQUEST IF NEEDED                                         
RESEXPD  NTR1  BASE=*,LABEL=*                                                   
         TM    NBSBKEND,NBEXPDEM+NBEXPDM6   EXPANDED DEMO VALUES?               
         BZ    RESUT40                                                          
         LA    R4,76                                                            
         LA    R3,DIAIHOME                                                      
RESUT25  CLI   0(R3),X'FF'          IF END OF LIST                              
         BE    RESUT40                                                          
         CLI   1(R3),C'B'          IF DEMO IS GAA EXANDED OR                    
         BE    *+8                                                              
         CLI   1(R3),C'Y'           IF DEMO IS C'Y'                             
         BNE   RESUT30                                                          
         MVI   1(R3),C'T'          ..SET IT BACK TO 'T'                         
         CLI   NBPOSTYP,C'N'       ..ONLY FOR NETWORK                           
         BE    RESUT30                                                          
         CLI   NBPOSTYP,C'S'       ..AND SYNDICATION                            
         BE    RESUT30                                                          
         MVI   1(R3),C'H'          ..ELSE IT'S 'H' FOR CABLE                    
RESUT30  LA    R3,3(R3)                                                         
         BCT   R4,RESUT25           NEXT DEMO                                   
RESUT40  J     SUBRXIT                                                          
*                                                                               
         LTORG                                                                  
*                                                                               
NETVQD   DSECT                                                                  
NETVQSWK DS    XL2                 START WEEK                                   
NETVQEWK DS    XL2                 END WEEK                                     
NETVQMTH DS    XL2                 SCHEDULE MONTH                               
NETVQQ   EQU   *-NETVQD                                                         
*                                                                               
MTHDATD  DSECT                                                                  
MDSDATE  DS    0XL3                START DATE                                   
MDSDYR   DS    HL1                 -START DATE YEAR                             
MDSDMN   DS    HL1                 -START DATE MONTH                            
MDSDDY   DS    HL1                 -START DATE DAY                              
MDEDATE  DS    0XL3                END DATE                                     
MDEDYR   DS    HL1                 -END DATE YEAR                               
MDEDMN   DS    HL1                 -END DATE MONTH                              
MDEDDY   DS    HL1                 -END DATE DAY                                
MDNLBOOK DS    0XL2                NIELSEN BOOK                                 
MDNLSYR  DS    HL1                 -NIELSEN YEAR                                
MDNLSMN  DS    HL1                 -NIELSEN MONTH                               
MTHDATLQ EQU   *-MTHDATD                                                        
*                                                                               
MTHDATES EQU   18                                                               
NETVQCAL EQU   37                                                               
         EJECT                                                                  
SUBRWORK  DSECT                                                                 
*                                                                               
MYRE     DS    F                                                                
MYDMCB   DS    6F                                                               
DIASC    DS    XL75                DEMOUT INPUT FOR SETCABLE  (25*3)            
DREINFO  DS    XL3      X'49NN04'  X'49' EL CD, LEN, & FLD LEN                  
DOASC    DS    XL100               DEMOUT OUTPUT FOR SETCABLE (25*4)            
ELEM5E   DS    XL7                 WORK AREA FOR X'5E' ELEM                     
SVDBTYPE DS    C                   SAVED BKTYPE FOR TCAR CLOBBERING             
SAVMED   DS    C                   ORIG DBSELMED FOR WKLY LK UP                 
CABNAD   DS    C                   CABLE NAD INDICATOR                          
SAVFCT   DS    HL1                                                              
SAVSEL   DS    CL(L'DBSELECT)                                                   
CNDIVSOR DS    XL(L'DBDIVSOR)      CABLE NAD ACCUMULATOR                        
NTIHOMES DS    F                                                                
CBNHOMES DS    F                                                                
CBNHOME2 DS    F                                                                
PROGSW   DS    X                                                                
FORCEDP  DS    X                                                                
SKIPEQ   EQU   X'80'                                                            
MULTEQ   EQU   X'40'                                                            
TRAKEQ   EQU   X'20'                                                            
SVRE     DS    F                                                                
NTIDIVSR DS    XL2                 SAVE NTI'S DIVSOR (FROM REG CABLE)           
NTIFCTR  DS    XL2                                                              
PREVNTI  DS    XL2                                                              
PRGLEV   DS    X                                                                
SVTTYP   DS    C                                                                
SVTRNUM  DS    XL2                                                              
SVTLNUM  DS    XL4                                                              
ANTIPR   DS    A                 POINTER TO NEXT AVAIL SPOT IN SAVNTIPR         
ASNTIPR  DS    A                 POINTER TO BEGINNING OF PROG ENTRY             
SAVNTIPR DS    XL500             SAVE NTI NO'S AND TRACKS                       
SAVNTIPX DS    0X                                                               
SAVSB    DS    XL100               TEMP STORAGE FOR S/B                         
ASBPR    DS    A                   POINTER TO NEXT AVAIL SPOT IN SAVSB          
AXCDNL   DS    A                   A(COMSCORE DEMO LIST)                        
*                                                                               
FLAGS1   DS    X                                                                
F1DEMLCK EQU   X'80'               EXECUTED USEDLOCK ROUTINE                    
*                                                                               
PDMCMINF DS    0XL4                MINUTE COMMERCIAL INFORMATION                
PDMCOMF  DS    C                   COMMERCIAL FLAG: Y,N,U                       
PDMCOMSC DS    X                   COMMERCIAL SECONDS IN THIS MINUTE            
PDMPROSC DS    X                   PROMO SECONDS IN THIS MINUTE                 
PDMPSASC DS    X                   PSA SECONDS IN THIS MINUTE                   
*                                                                               
PDPODINF DS    0XL17               POD INFORMATION                              
PDPDSTIM DS    HL2                 POD START TIME(0600-2959)                    
PDPDETIM DS    HL2                 POD END TIME  (0600-2959)                    
PDPDLMIN DS    HL2                 POD LENGTH IN MINUTES                        
PDPDSEC  DS    HL4                 POD TOTAL COMMERCIAL SECONDS                 
PDPDNUM  DS    HL2                 POD NUMBER                                   
PDPD1LST DS    XL1                 FIRST/LAST MINUTE FLAG                       
PDPDTSEC DS    HL4                 POD TOTAL SECONDS                            
*                                      COMM+PROMO+PSA SEC FOR 1ST AND           
*                                      LAST MINUTE -                            
*                                      60 SEC FOR EA MID MINUTE                 
*                                                                               
PDWEIGHT DS    XL4                 TOTAL POD WEIGHT FOR UNIT                    
*                                                                               
DEMHKCTR DS    CL1                                                              
*   USER FILE STUFF (FOR TVQ LK UPS)                                            
GOTUTYPE DS    X                                                                
BTUTYP   DS    X                                                                
BTUAQ    DS    F                                                                
BTUDMCB  DS    10F                                                              
BTUDB    DS    XL256                                                            
         DS    XL4                                                              
BTUIO    DS    1000C                                                            
         ORG   GOTUTYPE                                                         
NHTUNIV  DS    1800C               NHTI LK UPS                                  
*                                                                               
SUBRWRKX EQU   *                                                                
         EJECT                                                                  
DSPHDRD  DSECT                     TABLE HEADER                                 
DSPFILE  DS    CL1                 FILE CODE                                    
DSPMED   DS    CL1                 SUB-FILE CODE                                
DSPSRC   DS    CL1                 SOURCE CODE                                  
DSPSBOOK DS    XL2                 START BOOK (BINARY YYMM)                     
DSPLDE   DS    XL2                 L'DATA ELEMENT                               
DSPAET   DS    AL3                 A(NEXT TABLE HEADER-1)                       
DSPHDRLN EQU   *-DSPHDRD                                                        
*                                                                               
DSPDTAD  DSECT                     DATE ELEMENT                                 
DSPMOD   DS    XL1                 MODIFIER CODE                                
DSPDEMO  DS    XL1                 DEMO NUMBER                                  
DSPELCD  DS    XL1                 ELEMENT CODE                                 
DSPFLDN  DS    XL1                 FIELD NUMBER                                 
DSPPREC  DS    XL1                 FIELD PRECISION                              
DSPDTALN EQU   *-DSPDTAD                                                        
*                                                                               
