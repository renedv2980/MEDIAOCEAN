*          DATA SET TADUECOMP  AT LEVEL 002 AS OF 12/10/13                      
*=====================================================================          
*        UPDATE P+ TATU'S W/ DUE COMPANY                                        
*        PARAM 1 = A(CHECK RECORD)                                              
*=====================================================================          
TATUDUE  NTR1  BASE=*,LABEL=*                                                   
         L     R2,0(R1)            A(CHECK RECORD)                              
         USING TLCKD,R2                                                         
*                                                                               
         USING TADWD,R3                                                         
         LA    R3,TLCKELEM                                                      
TDUE10   CLI   0(R3),0                                                          
         JE    TATUDUEX                                                         
         CLI   0(R3),TADWELQ                                                    
         JE    TDUE20                                                           
         ZIC   RF,1(R3)                                                         
         AR    R3,RF                                                            
         J     TDUE10                                                           
*                                                                               
TDUE20   MVC   FULL,TADWREC        SAVE DUE COMPANY AMOUNT RECOVERED            
*                                                                               
         USING TATUD,R3                                                         
         LA    R3,TLCKELEM                                                      
TDUE30   CLI   0(R3),0                                                          
         JE    TATUDUEX                                                         
         CLI   0(R3),TATUELQ                                                    
         JE    TDUE40                                                           
TDUE35   ZIC   RF,1(R3)                                                         
         AR    R3,RF                                                            
         J     TDUE30                                                           
*                                                                               
TDUE40   OC    FULL,FULL           ANY DUE COMP?                                
         JZ    TATUDUEX                                                         
*                                                                               
         CLC   TATUUNIT,=C'FD '                                                 
         JE    TDUE35                                                           
         CLC   TATUUNIT,=C'CN '                                                 
         JE    TDUE35                                                           
         CLI   TATUUNIT+2,C' '                                                  
         JNE   TDUE35                                                           
*                                                                               
         XC    TGFULL,TGFULL                                                    
         ICM   RF,15,TATUWAGE                                                   
         L     RE,FULL                                                          
         SR    RF,RE               WAGES - DUE COMP                             
         BM    TDUE50                                                           
*                                                                               
         STCM  RF,15,TGFULL        SAVE NEW WAGE                                
         XC    FULL,FULL           NO MORE DUE COMP                             
         J     TDUE60                                                           
*                                                                               
TDUE50   LPR   RF,RF                                                            
         ST    RF,FULL             SET NEW DUE COMP                             
*                                                                               
TDUE60   MVC   TATUWAGE,TGFULL     SET NEW WAGE                                 
         J     TDUE35                                                           
*                                                                               
TATUDUEX J     XIT                                                              
         DROP  R2,R3                                                            
         LTORG                                                                  
         EJECT                                                                  
