*          DATA SET MFPRINT    AT LEVEL 009 AS OF 05/01/02                      
         TITLE 'MFPRINT - V(PRINT) FOR MICRO FICHE TAPE'                        
PRINT    CSECT                                                                  
         NMOD1 0,*MFPRINT                                                       
         LM    R2,R3,0(R1)         R2=A(133 CHR LINE) R3=A(CONTROL)             
         CLC   2(2,R3),=C'00'      IGNORE ANY FUNNY SPACING                     
         BL    PRINTX                                                           
         CLC   2(2,R3),=C'50'                                                   
         BH    PRINTX                                                           
         EJECT                                                                  
         LA    R8,PASSDATA                                                      
         USING MFGN5CD,R8          R8=A(COMTREVE INTERFACE)                     
         L     R9,=V(COMC)                                                      
         USING COMD,R9             R9=A(REPORT PROFILE)                         
         LTR   R9,R9                                                            
         BZ    PRT                 ASSUME PRINT ONLY IF NO PROFILE              
         CLI   COMSTART,2                                                       
         BNE   COM                                                              
         SPACE 2                                                                
         LH    R4,COMOPN           CLOSE AT EOJ IF ANY COM OPEN                 
         LTR   R4,R4                                                            
         BZ    PRINTX                                                           
         MVC   MFFUNC,=C'CLS'                                                   
         GOTO1 COMTREVE,DUB,MFREC  CLS                                          
         SPACE 2                                                                
PRINTX   XMOD1 1                                                                
         EJECT                                                                  
COM      CLI   COMSTART,1          START OF NEW REPORT                          
         BNE   COM0                                                             
         LH    R4,COMCNT           BUMP COUNTER                                 
         LA    R4,1(R4)                                                         
         STH   R4,COMCNT                                                        
         XC    COMFLAGS,COMFLAGS   CLEAR FLAGS                                  
         MVI   COMSTART,0          SET TO CONTINUE REPROT                       
COM0     TM    COMIO,X'01'         COM REQUIRED                                 
         BZ    COMX                NO                                           
         CLI   COMBA,C'B'                                                       
         BE    *+14                                                             
         MVC   CNTRL,0(R3)         SET CONTROL TO THIS FOR AFTER LOGIC          
         B     COM2                                                             
         CLI   BFLAG,0             SET CONTROL TO LAST FOR BEFORE LOGIC         
         BE    COM1                                                             
         MVC   CNTRL,LCNTRL                                                     
         MVI   CNTRL,C'A'                                                       
         MVC   LCNTRL,0(R3)                                                     
         B     COM2                                                             
COM1     CLC   0(4,R3),=C'BC01'    IGNORE UNTIL NEW PAGE FOR BEFORES            
         BNE   COMX                                                             
         MVC   LCNTRL,0(R3)                                                     
         MVI   BFLAG,1                                                          
         B     COMX                                                             
         SPACE 2                                                                
COM2     CLI   CNTRL+1,C'C'                                                     
         BNE   *+18                                                             
         MVI   MFCTL,C'1'          CHANNEL 1 ONLY                               
         MVC   MFDATA,1(R2)                                                     
         B     COM4                                                             
         PACK  DUB,CNTRL+2(2)                                                   
         CVB   R4,DUB                                                           
         SRDA  R4,32                                                            
         BZ    COMX                IGNORE SPACE 0 LINES                         
         D     R4,=F'3'                                                         
         LTR   R4,R4                                                            
         BNZ   *+10                                                             
         LA    R4,3                R4=NUM OF LINES TO SPACE                     
         BCTR  R5,0                R5=NUM OF EXTRA SPACE 3 REQUIRED             
COM2A    LTR   R5,R5                                                            
         BZ    COM3                                                             
         MVI   MFCTL,C'-'                                                       
         MVI   MFDATA,C' '                                                      
         MVC   MFDATA+1(131),MFDATA                                             
         GOTO1 COMTREVE,DUB,MFREC  WRT(SPACE 3)                                 
         BCT   R5,COM2A                                                         
         SPACE 2                                                                
COM3     LA    R5,ALASA(R4)                                                     
         MVC   MFCTL,0(R5)                                                      
         MVC   MFDATA,1(R2)                                                     
         OC    NLCOUNT,NLCOUNT                                                  
         BNZ   COM4                                                             
         GOTO1 COMTREVE,DUB,MFREC  WRT(DATA)                                    
         B     COMX                                                             
         SPACE 2                                                                
COM4     LH    R6,NLCOUNT          SAVE HEAD LINE & BUMP COUNTER                
         MH    R6,=H'133'                                                       
         LA    R6,NLSAVE(R6)                                                    
         MVC   0(133,R6),MFREC                                                  
         LH    R6,NLCOUNT                                                       
         LA    R6,1(R6)                                                         
         STH   R6,NLCOUNT                                                       
         CLC   COMNL,NLCOUNT+1                                                  
         BH    COMX                                                             
         CLI   FFLAG,0                                                          
         BNE   COM6                                                             
         SPACE 2                                                                
COM5     MVI   FFLAG,1             FIRST PAGE ROUTINE                           
         MVI   MFREC,C' '                                                       
         MVC   MFREC+1(135),MFREC                                               
         MVC   MFFUNC,=C'OPN'                                                   
*                                                                               
         LA    R6,COMTITLE         GET REPORT TITLE                             
         BAS   RE,GETFLD                                                        
         EX    R7,*+8                                                           
         B     *+10                                                             
         MVC   MFTITLE(0),0(R6)                                                 
*                                                                               
         LA    R6,COMDATE          GET REPORT DATE IN DD/MM/YY FORMAT           
         BAS   RE,GETFLD                                                        
         XC    MFDATE,MFDATE                                                    
         MVC   MFDATE+0(2),0(R6)                                                
         MVC   MFDATE+6(2),7(R6)                                                
         LA    R0,12                                                            
         LA    RE,MONTHS                                                        
         CLC   0(3,RE),3(R6)                                                    
         BE    *+12                                                             
         LA    RE,3(RE)                                                         
         BCT   R0,*-14                                                          
         CVD   R0,DUB                                                           
         UNPK  MFDATE+3(2),DUB                                                  
         OC    MFDATE,=C'00/00/00'                                              
*                                                                               
         GOTO1 COMTREVE,DUB,MFREC  OPN                                          
         LH    R4,COMOPN                                                        
         LA    R4,1(R4)                                                         
         STH   R4,COMOPN           BUMP COM OPEN COUNTER                        
         SPACE 2                                                                
COM6     MVI   MFREC,C' '          NEW PAGE ROUTINE                             
         MVC   MFREC+1(135),MFREC                                               
         MVC   MFFUNC,=C'WRT'                                                   
         MVI   MFCTL,C'$'                                                       
*                                                                               
         LA    R6,COMDEX           GET INDEX ENTRY                              
         BAS   RE,GETFLD                                                        
         BZ    *+18                                                             
         EX    R7,*+8                                                           
         B     *+10                                                             
         MVC   MFDEX(0),0(R6)                                                   
*                                                                               
         LA    R6,COMCOL           GET COLUMN BREAK ENTRY                       
         BAS   RE,GETFLD                                                        
         BZ    *+18                                                             
         EX    R7,*+8                                                           
         B     *+10                                                             
         MVC   MFCOL(0),0(R6)                                                   
*                                                                               
         LA    R6,COMHED           GET FICHE BREAK ENTRY                        
         BAS   RE,GETFLD                                                        
         BZ    *+18                                                             
         EX    R7,*+8                                                           
         B     *+10                                                             
         MVC   MFHED(0),0(R6)                                                   
*                                                                               
         GOTO1 COMTREVE,DUB,MFREC  WRT($INDEX)                                  
         SPACE 2                                                                
COM7     LA    R6,NLSAVE           REALEASE HEADER RECORDS                      
         LH    R7,NLCOUNT                                                       
         MVC   MFREC(133),0(R6)                                                 
         GOTO1 COMTREVE,DUB,MFREC  WRT(DATA)                                    
         LA    R6,133(R6)                                                       
         BCT   R7,*-14                                                          
         STH   R7,NLCOUNT          RESET HEADER COUNTER                         
         SPACE 2                                                                
COMX     TM    COMIO,X'02'                                                      
         BZ    PRINTX                                                           
         B     PRT                                                              
         EJECT                                                                  
* R6=A(FOUR BYTE FIELD LOCATION)                                                
* FORMAT#1 X'00000000' - FIELD NOT DEFINED                                      
* FORMAT#2 AL1(LENGTH),AL3(ADDRESS)                                             
* FORMAT#3 X'00LLCCNN' - LL=LINE CC=COLUMN NN=LENGTH                            
* SET R6=A(FIELD DATA) R7=L'DATA-1 AND CC                                       
*                                                                               
GETFLD   MVC   DUB(4),0(R6)                                                     
         L     R6,DUB                                                           
         LTR   R6,R6                                                            
GETFLD1  BZ    GETFLDX             FORMAT#1 SET CC=ZERO                         
         SR    R7,R7                                                            
         IC    R7,DUB                                                           
         LTR   R7,R7                                                            
         BZ    GETFLD3                                                          
GETFLD2  BCTR  R7,0                FORMAT#2 SET CC=NONZERO                      
         LA    R6,0(R6)                                                         
         B     GETFLDX                                                          
GETFLD3  IC    R7,DUB+1                                                         
         BCTR  R7,0                                                             
         MH    R7,=H'133'                                                       
         SR    R6,R6                                                            
         IC    R6,DUB+2                                                         
         BCTR  R6,0                                                             
         AR    R6,R7               R6=(LINE-1)*133+COL-1                        
         LA    R6,NLSAVE(R6)                                                    
         SR    R7,R7                                                            
         IC    R7,DUB+3                                                         
         LTR   R7,R7               SET CC                                       
         BCTR  R7,0                                                             
GETFLDX  BR    RE                                                               
         EJECT                                                                  
PRT      LA    R2,1(R2)                                                         
         ST    R2,DUB                                                           
         MVC   CCWCNTL+1(3),DUB+1                                               
         MVC   CCWPRNT+1(3),DUB+1                                               
*                                                                               
         PACK  DUB,2(2,R3)                                                      
         CVB   R4,DUB                                                           
         SRDA  R4,32                                                            
         CLI   1(R3),C'L'                                                       
         BE    *+20                                                             
         CH    R5,=H'12'           R5=CHANNEL NUM FOR C                         
         BNH   PRT1                                                             
         LA    R5,1                SET CHANNEL TO 1 IF FUNNY                    
         B     PRT1                                                             
*                                                                               
         D     R4,=F'3'                                                         
         LTR   R4,R4                                                            
         BNZ   PRT1                                                             
         LTR   R5,R5                                                            
         BZ    PRT1                                                             
         LA    R4,3                R4=SPACE 0-3 FOR L                           
         BCTR  R5,0                R5=SPACE 3 EXTRAS REQUIRED FOR L             
*                                                                               
PRT1     LA    R7,PRTCCW                                                        
         CLI   0(R3),C'B'                                                       
         BNE   PRTA                                                             
         SPACE 2                                                                
PRTB     CLI   1(R3),C'C'          PRINT BEFORE SPACING                         
         BNE   PRTBL                                                            
*                                                                               
PRTBC    MVC   0(8,R7),CCWPRNT     PRINT BEFORE CHANNEL                         
         LA    R6,BC(R5)                                                        
         MVC   0(1,R7),0(R6)                                                    
         B     PRTIO                                                            
*                                                                               
PRTBL    MVC   0(8,R7),CCWPRNT     PRINT BEFORE LINES                           
         LA    R6,BL(R4)                                                        
         MVC   0(1,R7),0(R6)                                                    
PRTBL1   LTR   R5,R5               CHECK FOR EXTRA CCWS                         
         BZ    PRTIO                                                            
         OI    4(R7),X'40'         CHAIN TO SPACE 3                             
         LA    R7,8(R7)                                                         
         MVC   0(8,R7),CCWCNTL                                                  
         BCTR  R5,0                                                             
         B     PRTBL1                                                           
         SPACE 2                                                                
PRTA     CLI   1(R3),C'C'          PRINT AFTER SPACING                          
         BNE   PRTAL                                                            
*                                                                               
PRTAC    MVC   0(8,R7),CCWCNTL     PRINT AFTER CHANNEL                          
         LA    R6,AC(R5)                                                        
         MVC   0(1,R7),0(R6)                                                    
         OI    4(R7),X'40'         SKIP CHANNEL CHAIN TO PRINT NO SPACE         
         MVC   8(8,R7),CCWPRNT                                                  
         B     PRTIO                                                            
*                                                                               
PRTAL    LTR   R5,R5               PRINT AFTER LINES                            
         BZ    PRTAL1                                                           
         MVC   0(8,R7),CCWCNTL     EXTRA SPACE 3 CCWS                           
         OI    4(R7),X'40'                                                      
         LA    R7,8(R7)                                                         
         BCTR  R5,0                                                             
         B     PRTAL                                                            
PRTAL1   MVC   0(8,R7),CCWCNTL     SKIP LINES CHAIN TO PRINT NO SPACE           
         LA    R6,AL(R4)                                                        
         MVC   0(1,R7),0(R6)                                                    
         OI    4(R7),X'40'                                                      
         MVC   8(8,R7),CCWPRNT                                                  
         B     PRTIO                                                            
         SPACE 2                                                                
PRTIO    LA    R7,PRTCCW           CHANNEL PROGRAM AT PRTCCW                    
         LA    R1,CCB                                                           
         ST    R7,8(R1)                                                         
         EXCP  (1)                                                              
         WAIT  (1)                                                              
         B     PRINTX                                                           
         EJECT                                                                  
         LTORG                                                                  
         SPACE 2                                                                
DUB      DS    D                                                                
PRTCCW   DS    20D                 CCW CHAIN FOR 20 COMMANDS                    
CCB      CCB   SYSLST,0,X'0200'    LINE PRINTER CCB                             
CCWCNTL  CCW   X'1B',0,X'20',1     SPACE 3 LINES                                
CCWPRNT  CCW   X'01',0,X'20',132   WRITE WITH NO SPACE                          
COMTREVE DC    V(MFPGEN5C)                                                      
COMFLAGS DS    0CL4                                                             
NLCOUNT  DC    H'0'                                                             
BFLAG    DC    X'00'                                                            
FFLAG    DC    X'00'                                                            
COMCNT   DC    H'0'                                                             
COMOPN   DC    H'0'                                                             
LCNTRL   DC    C'BC01'                                                          
CNTRL    DC    C'    '                                                          
MONTHS   DC    C'DECNOVOCTSEPAUGJULJUNMAYAPRMARFEBJAN'                          
ALASA    DC    C'+ 0-'                                                          
AL       DC    X'010B131B'                                                      
AC       DC    X'8B8B939BA3ABB3BBC3CBD3DBE3'                                    
BL       DC    X'01091119'                                                      
BC       DC    X'89899199A1A9B1B9C1C9D1D9E1'                                    
*                                                                               
PASSDATA DS    CL136               COMTREVE INTERFACE                           
*                                                                               
NLSAVE   DS    5CL133              SAVE AREA FOR MAX 5 HEADLINES                
         EJECT                                                                  
MFGN5CD  DSECT                     COVERS 42X - 8 CHR COL BREAK INDEX           
*                                                                               
MFREC    DS    0CL136              DATA RECORD                                  
MFCTL    DS    CL1                 ASA CONTROL CHR                              
MFDATA   DS    CL132               PRINT LINE DATA                              
MFFUNC   DS    CL3                 WRT                                          
*                                                                               
         ORG   MFREC                                                            
MFSTART  DS    0CL136              START RECORD                                 
         DS    CL1                                                              
MFDATE   DS    CL8                 DATE YY/MM/DD                                
         DS    CL1                                                              
MFTITLE  DS    CL36                REPORT TITLE                                 
         DS    CL90                                                             
*                                                                               
         ORG   MFREC                                                            
MFPAGE   DS    0CL136              NEW PAGE RECORD                              
         DS    CL1                 CTL=$                                        
MFDEX    DS    CL25                INDEX ENTRY                                  
MFCOL    DS    CL04                COLUMN ENTRY                                 
MFHED    DS    CL16                FICHE ENTRY                                  
         DS    CL90                                                             
         SPACE 2                                                                
COMD     DSECT                                                                  
COMSTART DS    XL1                 O=CONTINUE 1=START 2=EOJ                     
COMIO    DS    XL1                 X'01'=COM  X'02'=PRINT                       
COMBA    DS    CL1                 BEFORE(B) OR AFTER(A) LOGIC                  
COMNL    DS    XL1                 NUMBER OF HEAD LINES PER PAGE                
COMDATE  DS    XL4                 DATE LOCATION                                
COMTITLE DS    XL4                 TITLE LOCATION                               
COMDEX   DS    XL4                 INDEX LOCATION                               
COMCOL   DS    XL4                 COLUMN BREAK LOCATION                        
COMHED   DS    XL4                 FICHE BREAK LOCATION                         
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'009MFPRINT   05/01/02'                                      
         END                                                                    
