*          DATA SET PGEXC      AT LEVEL 032 AS OF 05/01/02                      
         TITLE 'P+G EXCEPTION STANDARDS'                                        
PGEXC    CSECT                                                                  
         NMOD1 0,PGEXC                                                          
         L     RA,=V(RPWORKC)                                                   
         USING RPWORK,RA                                                        
         L     RC,=V(EXWORKC)                                                   
         USING EXWORK,RC                                                        
         EJECT                                                                  
         CLI   PGSW,3                                                           
         BE    PGMKT                                                            
         CLI   PGSW,2                                                           
         BE    PGSTA                                                            
         CLI   PGSW,1                                                           
         BE    PGBUY                                                            
         DC    H'0'                                                             
*                                                                               
*                                                                               
PGBUY    CLI   BDDAYPT,C'T'        TRAFFIC BUY - BYPASS THIS BUY                
         BE    EXCX                                                             
         CLC   BDPROG(2),=H'400'            4 AM - TRAFFIC BUY                  
         BE    EXCX                BYPASS                                       
         CLI   BDDAYPT,C'D'       DAY - IGNORE TIME OF DAY TEST                 
         BE    PGBRAT                                                           
         CLC   BDPROG+2(2),=X'0000'                                             
         BNE   CKEND                                                            
         CLC   BDPROG(2),=H'200'                                                
         BNH   PGBST                                                            
         CLC   BDPROG(2),=H'600'                                                
         BNL   PGBST                                                            
         OI    EXCIND,X'80'       BUY STARTED BETWEEN 2 + 6 AM                  
         B     PGBRAT                                                           
*                                                                               
CKEND    CLC   BDPROG+2(2),=H'200' BUY REC IN RPWORK                            
         BNH   PGBST                                                            
         CLC   BDPROG+2(2),=H'600'                                              
         BNL   PGBST                                                            
         OI    EXCIND,X'80'             SPOT ENDED BETWEEN 2 AND 6 AM           
         B     PGBRAT                   GO CHCCK RATING                         
*                                                                               
*                                                                               
PGBST    TM    BDDAY,X'7C'         WEEKDAY                                      
         BZ    PGBST1              NO- CHECK WEEKEND                            
         CLC   BDPROG(2),=H'1800'                                               
         BNL   PGBST1                                                           
         CLC   BDPROG(2),=H'600'                                                
         BNH   PGBST1                                                           
         OI    EXCIND,X'80'        SPOT STARTED BETWEEN 6 AM + 6 PM             
         B     PGBRAT              CHECK RATING                   WKDAY         
*                                                                               
PGBST1   TM    BDDAY,X'03'         WEEKEND                                      
         BZ    PGBRAT                                                           
         CLC   BDPROG(2),=H'1400'                                               
         BNL   PGBRAT                                                           
         CLC   BDPROG(2),=H'600'                                                
         BNH   PGBRAT                                                           
         OI    EXCIND,X'80'        SPOT START BETWEEN 6AM + 2PM WKENDS          
         B     PGBRAT              CHECK RATING                                 
         EJECT                                                                  
PGBRAT   MVI   WORK,X'03'          RATING HOMES #3                              
         BAS   R9,FINDEM                                                        
         CLI   WORK+1,X'FF'        NOT FOUND                                    
         BNE   PGBR1                                                            
         B     PGBVPS                                                           
*                                                                               
PGBR1    CLC   WORK+1(3),=X'00001E'       3.0 RATING                            
         BNL   CKDMA                                                            
         OI    EXCIND,X'40'             LESS THAN 3.0 RATING                    
         B     PGBVPS                                                           
*                                                                               
*                 WORK+1(3) HAS BUY'S RATING HMS                                
*                                                                               
CKDMA    MVC   HALF,KEY+4                                                       
         LH    R8,HALF                                                          
         CVD   R8,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  MKTKEY+2(4),DUB+5(3)                                             
         L     RF,=V(GETCORP)                                                   
         BASR  RE,RF                                                            
         CLI   BBREC,0       RECORD NOT FOUND                                   
         BE    PGBVPS                                                           
*                                                                               
CKDMA2   LA    R1,BBDEMS                                                        
         LA    R4,20      FOR BCT                                               
CKDMA3   CLI   0(R1),X'03'                                                      
         BE    CKDMA4                                                           
         LA    R1,4(R1)                                                         
         BCT   R4,CKDMA3                                                        
         B     PGBVPS          NO RATING HOMES FOUND                            
*                                                                               
CKDMA4   MVC   HALF,2(R1)                                                       
         LH    R5,HALF                                                          
         M     R4,=F'10'                                                        
         SLL   R5,1         X 2                                                 
         STH   R5,HALF                                                          
         CLC   WORK+2(2),HALF                                                   
         BNH   PGBVPS                                                           
         OI    EXCIND,X'20'  BETTER THAN DOUBLE THE AVG DMA RATING              
         B     PGBVPS                                                           
         EJECT                                                                  
PGBVPS   MVI   WORK,X'40'          TOT HMS  #64                                 
         BAS   R9,FINDEM                                                        
         CLI   WORK+1,X'FF'                                                     
         BE    PGBX                                                             
         XC    FULL(8),FULL        FIRST DEMO IN DEMLST SHOULD TARGET           
         MVC   FULL+1(3),WORK+1                                                 
         MVC   HALF+1(3),DEMLST+1                                               
         CLI   TARAUD,0                                                         
         BE    PGBVPSA                                                          
         MVC   WORK(1),TARAUD                                                   
         BAS   R9,FINDEM       TARGET AUD OVERRIDE                              
         CLI   WORK+1,X'FF'                                                     
         BE    PGBX            DEMO NOT FOUND                                   
         MVC   HALF+1(3),WORK+1                                                 
*                                                                               
PGBVPSA  L     R5,HALF                                                          
         M     R4,=F'1000'                                                      
         L     R6,FULL                                                          
         LTR   R6,R6                                                            
         BZ    PGBX          AVOID DIVIDING BY 0 TOTAL HMS                      
         DR    R4,R6                                                            
         ST    R5,FULL                  SAVE TARGET AND VPS                     
*                        FIND P+G NATIONAL AVERAGE VPS FOR TARGET AUD           
*                        FROM TABLE                                             
*                                                                               
         MVC   WORK(1),DEMLST                                                   
         NI    WORK,X'7F'                                                       
         CLI   TARAUD,0                                                         
         BE    *+10                                                             
         MVC   WORK(1),TARAUD        USING OVERRIDE                             
         LA    R2,PGVPSTAB                                                      
PGBVPS1  CLC   WORK(1),0(R2)                                                    
         BE    PGBVPS2             FOUND IT                                     
         LA    R2,9(R2)                                                         
         CLI   0(R2),0             END OF TABLE                                 
         BNE   PGBVPS1                                                          
         B     PGBX                NOT FOUND - GET OUT                          
*                                                                               
PGBVPS2  CLC   FULL,1(R2)                                                       
         BNL   PGBVPS3                                                          
         OI    EXCIND,X'10'        LOWER                                        
         B     PGBX                                                             
PGBVPS3  CLC   FULL,5(R2)                                                       
         BNH   PGBX                                                             
         OI    EXCIND,X'08'        HIGHER                                       
         B     PGBX                                                             
*                                                                               
PGBX     B     EXCODES             GO BUILD EXCEPTION CODES FIELD               
*                                                                               
*                                                                               
*                                                                               
PGVPSTAB DS    0C                                                               
         DC    X'52',X'000000CE',X'0000017E'  82 WN 25-49  .206 - .382          
         DC    X'53',X'0000018C',X'000002E0'  83 WMN 25+  .396 - .736           
         DC    X'4F',X'0000012C',X'0000022E'   79 WN 18-49 .300 - .558          
         DC    X'50',X'000001F4',X'000003A0'   80 WN 18+   .500 - .928          
         DC    X'0000'                                                          
*                                                                               
*                                                                               
*                                                                               
         DS    F                                                                
FINDEM   ST    R9,*-4              DESIRED DEMO CODE IS IN WORK                 
         LA    R2,DEMLST                                                        
         LA    R3,14                                                            
FD1      MVC   WORK+1(1),0(R2)                                                  
         NI    WORK+1,X'7F'                                                     
         CLC   WORK(1),WORK+1                                                   
         BE    FD2                                                              
         LA    R2,4(R2)                                                         
         BCT   R3,FD1                                                           
         MVI   WORK+1,X'FF'        NOT FOUND                                    
         B     FDX                                                              
FD2      MVC   WORK+1(3),1(R2)                                                  
FDX      L     R9,FINDEM-4                                                      
         BR    R9                  RETURN                                       
         EJECT                                                                  
PGSTA    CLI   TARAUD,0                                                         
         BNE   USECORP          GET CORP CPM/CPP                                
         L     R3,TMKTGDOL                                                      
         L     R4,TMKTGEQP                                                      
         LTR   R4,R4                  NO POINTS AVOID DIVIDE BY ZERO            
         BZ    PGSZ                                                             
         LTR   R3,R3                                                            
         BZ    PGSZ               NO DOLLARS - BYPASS THIS TEST                 
         M     R2,=F'20'       BECAUSE GOAL PTS ARE 1 DECIMAL                   
         SR    R2,R2                                                            
         DR    R2,R4                                                            
         LTR   R2,R2                                                            
         BM    *+8                                                              
         AH    R3,=H'1'                                                         
         SRA   R3,1           ROUNDING                                          
         ST    R3,FULL                                                          
         B     GETRNG        COMPUTE RANGES                                     
*                                                                               
*                                                                               
USECORP  MVC   HALF,KEYSAVE+4                                                   
         LH    R8,HALF                                                          
         CVD   R8,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  MKTKEY+2(4),DUB+5(3)                                             
         L     RF,=V(GETCORP)                                                   
         BASR  RE,RF                                                            
         CLI   BBREC,0                                                          
         BE    PGSZ           NO CORP DATA - EXIT                               
         LA    R1,BBDEMS                                                        
         LA    R4,20       FOR BCT                                              
GETC1    CLC   0(1,R1),TARAUD                                                   
         BE    GETC2                                                            
         LA    R1,4(R1)                                                         
         BCT   R4,GETC1                                                         
         B     PGSZ           TARAUD NOT FOUND - EXIT                           
*                                                                               
GETC2    MVC   FULL,0(R1)                                                       
         NI    FULL,0        ZERO DEMO NUMBER                                   
         OC    FULL,FULL                                                        
         BZ    PGSZ           NO CPM                                            
         CLI   DATEB,C'R'      SEE IF TARGET DEMO IS A RATING                   
         BNE   GETRNG                                                           
         B     PGSZ       NO CPP ARE ON CORP DATA FILE                          
*                                                                               
*                                                                               
GETRNG   L     R3,FULL         FULL HAS EITHER GOAL OR CORP CPM                 
         M     R2,=F'120'                                                       
         D     R2,=F'100'                                                       
         ST    R3,DUB                                                           
         L     R3,FULL                                                          
         M     R2,=F'80'                                                        
         D     R2,=F'100'                                                       
         ST    R3,DUB+4                                                         
         L     R6,STAACC+16      EQIV DEMO                                      
         LTR   R6,R6       NO ACTUAL POINTS AVOID DIVIDE BY ZERO                
         BZ    PGSZ                                                             
*                                                                               
*           GO TO DEMAVE TO GET CPP FOR EQUIV DEMS                              
*                                                                               
         MVC   MYDUB,DUB          SAVE  COMPUTED RANGES                         
         MVC   DEMLST(16),STAACC+16                                             
         MVC   DLLRS(4),STAACC+32                                               
         GOTO  DEMAVE,DMCB,DEMBLK,X'01'DMLSTEQ                                  
         BAS   R9,ERRTST                                                        
         MVC   DUB(4),DMLSTEQ+20                                                
         CLI   TARAUD,0                                                         
         BE    PGSB          NO OVERRIDE - USE FIRST DEMO                       
         LA    R2,4      FOR BCT                                                
         LA    R1,DMLSTEQ+20                                                    
PGSA     MVC   DUB(4),0(R1)                                                     
         NI    DUB,X'7F'       SET OFF DEMO OVERRIDE BIT                        
         CLC   DUB(1),TARAUD                                                    
         BE    PGSB                                                             
         LA    R1,4(R1)                                                         
         BCT   R2,PGSA                                                          
         B     PGSZ      DEMO NOT FOUND - EXIT                                  
*                                                                               
PGSB     NI    DUB,X'00'                                                        
         L     R5,DUB         R5 NOW HAS ACTUAL CPP                             
         MVC   DUB,MYDUB                                                        
         C     R5,DUB                                                           
         BNH   PGS1                                                             
         OI    EXCIND,X'02'                                                     
         LR    R7,R5                                                            
         L     R8,FULL                                                          
         SR    R7,R8                                                            
         SR    R6,R6                                                            
         M     R6,=F'2000'                                                      
         DR    R6,R8                                                            
         LTR   R6,R6                                                            
         BM    *+8                                                              
         AH    R7,=H'1'                                                         
         SRA   R7,1                                                             
         LA    R4,PGSTAMSG+54                                                   
         EDIT  (R7),(6,0(R4)),1,FLOAT=+                                         
         MVC   PGSTAMSG+61(4),=C'PCT.'                                          
         B     PGSX                                                             
*                                                                               
PGS1     C     R5,DUB+4                                                         
         BH    PGSZ                                                             
         OI    EXCIND,X'04'                                                     
         L     R7,FULL                                                          
         SR    R7,R5                                                            
         SR    R6,R6                                                            
         M     R6,=F'2000'                                                      
         L     R8,FULL                                                          
         DR    R6,R8                                                            
         LTR   R6,R6                                                            
         BM    *+8                                                              
         AH    R7,=H'1'                                                         
         SRA   R7,1                                                             
         LNR   R7,R7                                                            
         LA    R4,PGSTAMSG+54                                                   
         EDIT  (R7),(6,0(R4)),1,FLOAT=-                                         
         MVC   PGSTAMSG+61(4),=C'PCT.'                                          
         B     PGSX                                                             
*                                                                               
PGSX     MVC  PGSTAMSG(24),=C'GOAL TARGET AUDIENCE CPM'                         
         CLI   TARAUD,0                                                         
         BE    PGSX1                                                            
         MVC   PGSTAMSG(4),=C'CORP'                                             
         CLI   DATEB,C'R'        SEE IF RATING                                  
         BNE   *+8                                                              
         MVI   PGSTAMSG+23,C'P'                                                 
         B     PGSX2                                                            
*                                                                               
PGSX1    CLI   EDEMOS+2,C'R'          IF A RATING MAKE IT CPP                   
         BNE   *+8                                                              
         MVI   PGSTAMSG+23,C'P'                                                 
PGSX2    L     R4,FULL         FULL HAS  GOAL CPM                               
         LA    R3,PGSTAMSG+25                                                   
         EDIT  (R4),(6,0(R3)),2                                                 
         MVI   PGSTAMSG+31,C'+'                                                 
         MVC   PGSTAMSG+33(10),=C'ACTUAL CPM'                                   
         CLI   TARAUD,0                                                         
         BE    PGSX3                                                            
         CLI   DATEB,C'R'                                                       
         BNE   *+8                                                              
         MVI   PGSTAMSG+42,C'P'                                                 
         B     PGSX4                                                            
*                                                                               
PGSX3    CLI   EDEMOS+2,C'R'          IF A RATING MAKE IT CPP                   
         BNE   *+8                                                              
         MVI   PGSTAMSG+42,C'P'                                                 
PGSX4    LA    R3,PGSTAMSG+44                                                   
         EDIT  (R5),(6,0(R3)),2            R5 HAS ACTUAL CPM                    
         MVI   PGSTAMSG+50,C'+'                                                 
         B     PGSZ                                                             
*                                                                               
PGSZ     B     EXCODES                                                          
         EJECT                                                                  
PGMKT    L     R5,TMKTGDOL                                                      
         LTR   R5,R5                                                            
         BZ    PGMX                                                             
         M     R4,=F'20'                                                        
         D     R4,=F'100'                                                       
         L     R6,TMKTGDOL                                                      
         AR    R6,R5           R6=MKT GOAL + 50 %                               
         L     R7,TMKTGDOL                                                      
         SR    R7,R5            R7= MKT GOAL - 50 %                             
         L     R8,MKTACC+4      R8= ACTUAL MKT DOLS                             
         CR    R8,R7                                                            
         BH    PGM1                                                             
         OI    EXCIND,X'01'        BELOW 50% OF GOAL                            
         L     R5,TMKTGDOL                                                      
         SR    R5,R8                                                            
         M     R4,=F'2000'                                                      
         L     R7,TMKTGDOL                                                      
         DR    R4,R7                                                            
         LTR   R4,R4                                                            
         BM    *+8                                                              
         AH    R5,=H'1'                                                         
         SRA   R5,1                                                             
         LNR   R5,R5                                                            
         LA    R4,PGMKTMSG+51                                                   
         EDIT  (R5),(6,0(R4)),1,FLOAT=-                                         
         MVC   PGMKTMSG+58(4),=C'PCT.'                                          
         B     PGMEX                                                            
*                                                                               
PGM1     CR    R8,R6                                                            
         BL    PGMX                                                             
         OI    EXCIND1,X'80'       ABOVE 50% OF GOAL                            
         L     R5,TMKTGDOL                                                      
         SR    R8,R5                                                            
         LR    R5,R8                                                            
         M     R4,=F'2000'                                                      
         L     R7,TMKTGDOL                                                      
         DR    R4,R7                                                            
         LTR   R4,R4                                                            
         BM    *+8                                                              
         AH    R5,=H'1'                                                         
         SRA   R5,1                                                             
         LA    R4,PGMKTMSG+51                                                   
         EDIT  (R5),(6,0(R4)),1,FLOAT=+                                         
         MVC   PGMKTMSG+58(4),=C'PCT.'                                          
         B     PGMEX                                                            
PGMEX    MVC   PGMKTMSG(4),=C'GOAL'                                             
         L     R3,TMKTGDOL                                                      
         LA    R4,PGMKTMSG+5                                                    
         EDIT  (R3),(14,0(R4)),2,COMMAS=YES,MINUS=YES,FLOAT=$                   
         MVC   PGMKTMSG+24(6),=C'ACTUAL'                                        
         L     R3,MKTACC+4                                                      
         LA    R4,PGMKTMSG+31                                                   
         EDIT  (R3),(14,0(R4)),2,COMMAS=YES,MINUS=YES,FLOAT=$                   
         B     PGMX                                                             
*                                                                               
PGMX     B     EXCODES             GO BUILD EXCEPTION CODES FIELD               
         EJECT                                                                  
EXCODES  MVC   WORK+5(2),EXCIND                                                 
         LA    R2,WORK+5                                                        
         LA    R6,PGECDS                                                        
         LA    R3,2                FOR BIG BCT                                  
EXC1     LA    R4,128                                                           
         SR    R5,R5                                                            
         SR    R9,R9                                                            
EXC2     STC   R4,TMINS+1          ALTER NEXT INSTRUCTION                       
TMINS    TM    0(R2),X'00'                                                      
         BO    EXC4                                                             
         CLI   0(R2),0                                                          
         BE    NXTIND                                                           
         SRL   R4,1                                                             
         B     NEXTCD        CHECK NEXT CODE                                    
*                                                                               
         B     NXTIND                                                           
EXC4     LA    R7,PGEXTAB                                                       
         LA    R8,PRDEXACC                                                      
         STC   R3,WORK                                                          
         CLI   WORK,2                                                           
         BE    EXC5                                                             
         LA    R7,PGEXTAB1                                                      
         LA    R8,PRDEXACC+32                                                   
EXC5     LA    R7,0(R5,R7)                                                      
         LA    R8,0(R9,R8)                                                      
         MVC   0(3,R6),0(R7)                                                    
         L     R1,0(R8)                                                         
         LA    R1,1(R1)                                                         
         ST    R1,0(R8)                                                         
         MVI   3(R6),C','                                                       
         LA    R6,4(R6)                                                         
         XC    0(1,R2),TMINS+1                                                  
         CLI   0(R2),0                                                          
         BE    NXTIND                                                           
         SRL   R4,1                                                             
NEXTCD   AH    R9,=H'4'                                                         
         AH    R5,=H'3'                                                         
         CH    R5,=H'21'                                                        
         BH    NXTIND                                                           
         B     EXC2                                                             
         B     NXTIND                                                           
*                                                                               
NXTIND   LA    R2,1(R2)                                                         
         BCT   R3,EXC1                                                          
         BCTR  R6,0                                                             
         MVI   0(R6),C' '          BLANK LAST ,                                 
         B     EXCX                                                             
*                                                                               
EXCX     XIT1                                                                   
*                                                                               
*                                                                               
MYDUB    DS    D                                                                
*                                                                               
ERRTST   CLI   ERROR,0                                                          
         BNE   EXCX                                                             
         BR    R9                                                               
*                                                                               
         LTORG                                                                  
