*          DATA SET TAVALTYP   AT LEVEL 001 AS OF 02/25/15                      
***********************************************************************         
*        ROUTINE ENSURES THAT THE USE TYPE IS VALID FOR THE           *         
*        COMMERCIAL TYPE                                              *         
*        ON ENTRY ... P1 = A(COMMERCIAL TYPE)                         *         
*                     R4 = A(COMMERCIAL RECORD)                       *         
***********************************************************************         
                                                                                
VTYPE    NTR1  BASE=*,LABEL=*                                                   
         L     R1,0(R1)            R1=A(COMM'L TYPE)                            
                                                                                
         CLI   TGUSEQU,UPEN        PEN                                          
         JE    YES                                                              
         CLI   TGUSEQU,UOTH        OTH                                          
         JE    YES                                                              
         CLI   TGUSEQU,UMBO        MBO                                          
         JE    YES                                                              
         CLI   TGUSEQU,UEQY        AND EQY ARE VALID FOR ALL                    
         JE    YES                 COMMERCIAL TYPES                             
                                                                                
         CLI   TGUSEQU,UMVI        MVI                                          
         JE    VT10                                                             
         CLI   TGUSEQU,UMVN        AND MVN ARE VALID FOR ALL TYPES              
         JNE   VT20                                                             
VT10     CLI   0(R1),CTYAUD        EXCEPT AUDITION                              
         JE    NO                                                               
         CLI   0(R1),CTYCAN        CANCELLATION                                 
         JE    NO                                                               
         CLI   0(R1),CTYSDEM       SPANISH DEMO                                 
         JE    NO                                                               
         CLI   0(R1),CTYDEM        DEMO                                         
         JE    NO                                                               
         CLI   0(R1),CTYGRT        AND GUARANTEE                                
         JNE   YES                                                              
         J     NO                                                               
                                                                                
VT20     CLI   TGUSEQU,UVRE        VRE                                          
         JE    VT21                                                             
         CLI   TGUSEQU,UVNR        VNR                                          
         JE    VT21                                                             
         CLI   TGUSEQU,UEDS        AND ARE VALID FOR ALL TYPES                  
         JNE   VT25                                                             
VT21     CLI   0(R1),CTYADD        EXCEPT ADDENDUM                              
         JNE   YES                                                              
         J     NO                                                               
                                                                                
VT25     CLI   0(R1),CTYFGN        IF COMM'L TYPE IS FOREIGN                    
         JNE   VT30                                                             
         CLI   TGUSEQU,UFGS        FGS                                          
         JE    VT150                                                            
         CLI   TGUSEQU,UFGR        FGR                                          
         JE    VT150                                                            
         CLI   TGUSEQU,UFGM        FGM                                          
         JE    VT150                                                            
         CLI   TGUSEQU,UPNH        PNH                                          
         JE    VT150                                                            
         CLI   TGUSEQU,UDWN        DWN                                          
         JE    VT150                                                            
         CLI   TGUSEQU,ULFT        AND LFT ARE ONLY VALID USES                  
         JE    VT150                                                            
         J     NO                                                               
                                                                                
VT30     CLI   TGUSEQU,UGRT        GRT IS ONLY VALID WHEN COMMERCIAL            
         JNE   VT40                TYPE IS GUARANTEE                            
         CLI   0(R1),CTYGRT                                                     
         JNE   NO                                                               
                                                                                
VT40     CLI   0(R1),CTYGRT        IF COMM'L TYPE IS GUARANTEE                  
         JNE   VT50                                                             
         CLI   TGUSEQU,UGRT        GRT                                          
         JE    VT150                                                            
         CLI   TGUSEQU,UPNH        PNH                                          
         JE    VT150                                                            
         CLI   TGUSEQU,UOTM        OTM                                          
         JE    VT150                                                            
         CLI   TGUSEQU,UOTH        AND OTH ARE ONLY VALID USES                  
         JE    VT150                                                            
         J     NO                                                               
                                                                                
VT50     LR    R0,R1                                                            
         GOTOR UTEST,DMCB,TGUSXUNS,AFM,0,0,0                                    
         LR    R1,R0                                                            
         JZ    VT150                                                            
                                                                                
         CLI   0(R1),CTYSDEM       IF COMM'L TYPE IS SPANISH DEMO               
         JNE   VT60                                                             
         CLI   TGUSEQU,USNA        SNA                                          
         JE    YES                                                              
         CLI   TGUSEQU,UPNH        AND PNH ARE ONLY VALID USES                  
         JE    YES                                                              
         JNE   NO                                                               
                                                                                
VT60     CLI   0(R1),CTYDEM        IF COMM'L TYPE IS DEMO                       
         JNE   VT70                                                             
         CLI   TGUSEQU,UDEM        DEM                                          
         JE    VT150                                                            
         CLI   TGUSEQU,UPNH        PNH                                          
         JE    VT150                                                            
         CLI   TGUSEQU,UCDM        AND CDM ARE ONLY VALID USES                  
         JE    VT150                                                            
         J     NO                                                               
                                                                                
VT70     CLI   0(R1),CTYAUD        IF COMM'L TYPE IS AUDITION                   
         JNE   VT80                                                             
         CLI   TGUSEQU,UAUD        AUD                                          
         JE    VT150                                                            
         CLI   TGUSEQU,UPNH        PNH                                          
         JE    VT150                                                            
         CLI   TGUSEQU,UCAU        AND CAU ARE ONLY VALID USES                  
         JE    VT150                                                            
         J     NO                                                               
                                                                                
VT80     CLI   0(R1),CTYADD        IF COMM'L TYPE IS ADDENDUM                   
         JNE   VT90                                                             
         CLI   TGUSEQU,UOTH        OTH                                          
         JE    VT150                                                            
         CLI   TGUSEQU,UCBL        CBL                                          
         JE    VT150                                                            
         CLI   TGUSEQU,ULCB        LCB                                          
         JE    VT150                                                            
         CLI   TGUSEQU,UPNH        PNH                                          
         JE    VT150                                                            
         TM    TGUSSTA3,ADDENUSE   AND ADDENDUM USES ARE ONLY VALID             
         JO    VT150               USES                                         
         J     NO                                                               
                                                                                
VT90     CLI   0(R1),CTYPUB        IF COMM'L TYPE IS PUBLIC SERVICE             
         JNE   VT100                                                            
         CLI   TGUSEQU,UPUB        PUB                                          
         JE    VT150                                                            
         CLI   TGUSEQU,UPBS        PBS                                          
         JE    VT150                                                            
         CLI   TGUSEQU,UPNH        PNH                                          
         JE    VT150                                                            
         CLI   TGUSEQU,ULFT        LFT                                          
         JE    VT150                                                            
         CLI   TGUSEQU,UDWN        AND DWN ARE ONLY VALID USES                  
         JE    VT150                                                            
         J     NO                                                               
                                                                                
VT100    CLI   0(R1),CTYCAN        IF COMM'L TYPE IS CANCELLATION               
         JNE   VT110                                                            
         CLI   TGUSEQU,UCNL        CNL                                          
         JE    VT150                                                            
         CLI   TGUSEQU,UPNH        PNH                                          
         JE    VT150                                                            
         CLI   TGUSEQU,UPPF        AND PPF ARE ONLY VALID USES                  
         JE    VT150                                                            
         J     NO                                                               
                                                                                
VT110    CLI   0(R1),CTYSTRM       IF COMM'L TYPE IS SHORT TERM                 
         JNE   VT120                                                            
         TM    TGUSSTA2,HLDTYPE    HOLDING FEE PAYMENTS ARE INVALID             
         JZ    VT150                                                            
         J     NO                                                               
                                                                                
VT120    CLI   0(R1),CTYMUS        IF COMM'L TYPE IS MUSIC                      
         JNE   VT130                                                            
         CLI   TGUSEQU,UPNH        PNH IS ONLY VALID USE                        
         JNE   NO                                                               
                                                                                
VT130    CLI   0(R1),CTYSPAN       IF COMM'L TYPE IS SPANISH                    
         JNE   VT140                                                            
         CLI   TGUSEQU,UPNH        PNH                                          
         JE    VT150                                                            
         CLI   TGUSEQU,UDWN        DWN                                          
         JE    VT150                                                            
         TM    TGUSSTA2,SPANUSE    AND SPANISH USES ARE THE ONLY                
         JO    VT150               VALID USES                                   
         J     NO                                                               
                                                                                
VT140    CLI   0(R1),CTYPROMO      IF COMM'L TYPE IS PROMO                      
         JNE   VT150                                                            
         CLI   TGUSEQU,UPRM        PRM                                          
         JE    VT150                                                            
         CLI   TGUSEQU,UPNH        PNH                                          
         JE    VT150                                                            
         CLI   TGUSEQU,UDWN        DWN                                          
         JE    VT150                                                            
         CLI   TGUSEQU,ULFT        LFT                                          
         JE    VT150                                                            
         CLI   TGUSEQU,UPRR        AND PRR ARE ONLY VALID USES                  
         JNE   NO                                                               
                                                                                
VT150    TM    TGUSSTA2,SPANUSE    IF SPANISH USE                               
         JZ    VT160                                                            
         CLI   0(R1),CTYSPAN       COMM'L TYPE MUST BE SPANISH                  
         JNE   NO                                                               
                                                                                
VT160    TM    TGUSSTA3,ADDENUSE   IF ADDENDUM USE                              
         JZ    VT170                                                            
         CLI   0(R1),CTYADD        COMM'L TYPE MUST BE ADDENDUM                 
         JNE   NO                                                               
                                                                                
VT170    TM    TGUSSTA3,SOAPUSE    IF SOAP USE                                  
         JZ    VT180                                                            
         CLI   0(R1),CTYSOAP       COMM' TYPE MUST BE SOAP                      
         JNE   NO                                                               
                                                                                
VT180    CLI   TGUSEQU,UPUB        IF PUB                                       
         JE    VT190                                                            
         CLI   TGUSEQU,UPBS        OR PBS USE                                   
         JNE   VT200                                                            
VT190    CLI   0(R1),CTYPUB        COMM'L TYPE MUST BE PUBLIC SERV              
         JNE   NO                                                               
                                                                                
VT200    CLI   TGUSEQU,UPRR        IF PRR                                       
         JE    VT210                                                            
         CLI   TGUSEQU,UPRM        OR PRM USE                                   
         JNE   VT220                                                            
VT210    CLI   0(R1),CTYPROMO      COMM'L TYPE MUST BE PROMO                    
         JNE   NO                                                               
                                                                                
VT220    CLI   TGUSEQU,UINR        IF INR                                       
         JNE   VT230                                                            
         CLI   0(R1),CTYIND        COMM'L TYPE CANNOT BE INDUSTRIAL             
         JE    NO                                                               
         CLI   0(R1),CTYICAT1      OR INDUSTRIAL CATEGORY 1                     
         JE    NO                                                               
         CLI   0(R1),CTYICAT2      OR INDUSTRIAL CATEGORY 2                     
         JE    NO                                                               
                                                                                
VT230    TM    TGUSSTA4,INDUSTRL   IF INDUSTRIAL USE                            
         JZ    YES                                                              
         CLI   0(R1),CTYICAT1      COMM'L TYPE MUST BE INDUSTRIAL               
         JE    YES                 CATEGORY 1                                   
         CLI   0(R1),CTYICAT2      OR INDUSTRIAL CATEGORY 2                     
         JE    YES                                                              
         J     NO                                                               
                                                                                
***********************************************************************         
*        LITERALS AND CONSTANTS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
