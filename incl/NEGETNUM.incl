*          DATA SET NEGETNUM   AT LEVEL 001 AS OF 09/20/95                      
*          DATA SET SPBU2A     AT LEVEL 120 AS OF 09/12/95                      
         TITLE 'GETNUM - GET AND FORMAT INVOICE NUMBER'                         
GETNUM   CSECT                                                                  
         NMOD1 0,GETNUM                                                         
         LA    RC,SPACEND                                                       
         SPACE 2                                                                
         MVI   RCTRACE,C'N'        SUPPRESS TRACE                               
         ST    R1,SAVR1                                                         
         LM    R2,R3,0(R1)                                                      
         CLI   0(R1),0                                                          
         BNE   GN31                                                             
         UNPK W(1),TODAY+1(1)       PUT YM IN ONE BYTE                          
         NI    W,X'F0'                                                          
         OC    W(1),TODAYB+1                                                    
         OC    0(2,R2),0(R2)                                                    
         BNZ   GN22                DONT SEARCH FILE                             
         OI    DMINBTS,X'08'       SET TO PASS DELETES                          
         MVI   DMOUTBTS,X'FD'                                                   
         XC    HALF,HALF                                                        
*                                                                               
         CLI   PROOFSW,C'Y'        IF DRAFT BILLING                             
         BE    GN20                                                             
*                                                                               
         MVC   KEY1,KEY            PRESERVE KEY                                 
         XC    KEY,KEY                                                          
         MVC   KEY+1(1),BAGYMD     USE REQ MEDIA                                
         CLI   AGMSEQ,C'A'         UNLESS BY AGY                                
         BE    GN3                                                              
         CLI   AGMSEQ,C'D'         OR BY CLIENT ACROSS MEDIA                    
         BNE   GN3B                                                             
GN3      DS    0H                                                               
         NI    KEY+1,X'F0'                                                      
         OI    KEY+1,X'01'         ELSE, SET FIRST MEDIA                        
GN3B     DS    0H                                                               
         CLI   AGMSEQ,C'C'                                                      
         BE    GN3D                                                             
         CLI   AGMSEQ,C'D'                                                      
         BNE   *+10                                                             
GN3D     DS    0H                                                               
         MVC   KEY+2(2),BCLT       SET CLIENT                                   
         GOTO1 HIGH                                                             
         B     GN4B                                                             
GN4      DS    0H                                                               
         GOTO1 SEQ                                                              
GN4B     DS    0H                                                               
         CLC   KEY(2),KEYSAVE           AGM                                     
         BNE   GN6                                                              
         CLI   AGMSEQ,C'C'                                                      
         BE    GN4C                                                             
         CLI   AGMSEQ,C'D'                                                      
         BNE   *+14                                                             
GN4C     DS    0H                                                               
         CLC   KEY+2(2),KEYSAVE+2       CLIENT                                  
         BNE   GN6D                                                             
         CLI   INRSQYM,0           TEST HAVE RESEQ DATE                         
         BE    GN5                 NO- RESEQ WITHIN MONTH                       
*                                                                               
         ZIC   RF,KEY+10                                                        
         CLI   KEY+10,X'6F'        ***** 0 THRU 6 ARE 1990'S *******            
         BH    *+8                                                              
         LA    RF,X'A0'(RF)        ADD 10 YEARS                                 
         CLM   RF,1,INRSQYM        TEST KEY VS RESEQ DATE                       
         BL    GN4                 SKIP BILLS BEFORE RESEQ YM                   
         BH    GN5B                IF AFTER, MUST COUNT                         
         CLC   INRSQDA,=C'01'      IF WITHIN MONTH MUST READ BILL               
         BE    GN5B                UNLESS START DATE IS 01                      
*                                                                               
         GOTO1 GETBILL                                                          
         L     R7,ADBILL                                                        
         CLC   BDATE+4-BILLREC(2,R7),INRSQDA  SKIP IF BEFORE RSQ DAY            
         BL    GN4                                                              
         B     GN5B                                                             
GN5      DS    0H                                                               
         CLC   KEY+10(1),W              YM                                      
         BNE   GN4                                                              
GN5B     DS    0H                                                               
         CLC   QAGY,=C'MC'         **TEMP - FOR MCCANN                          
         BNE   *+14                                                             
         CLC   KEY+11(2),=H'9000'  SKIP 9000 +                                  
         BNL   GN4                                                              
         CLC   HALF,KEY+11              INVOICE NO.                             
         BNL   GN4                                                              
         MVC   HALF,KEY+11                                                      
         B     GN4                                                              
*                                                                               
GN6      DS    0H                  END OF AGY/MED                               
         CLI   AGMSEQ,C'A'         TEST BY AGENCY                               
         BE    GN7                                                              
GN6D     CLI   AGMSEQ,C'D'         OR BY CLIENT ACROSS MEDIA                    
         BNE   GN20                                                             
GN7      DS    0H                  TRY NEXT MEDIA                               
         CLI   KEY,0               START OF BUYS                                
         BNE   GN20                                                             
         MVC   BYTE,KEY+1                                                       
         NI    BYTE,X'F0'                                                       
         CLC   BYTE,BAGYMD                                                      
         BH    GN20                END OF AGY                                   
         CLI   AGMSEQ,C'D'         IF BY CLIENT WITHIN MEDIA                    
         BE    GN7D                YES                                          
         MVC   KEYSAVE,KEY         ELSE JUST CONTINUE                           
         B     GN4B                                                             
*                                                                               
GN7D     DS    0H                  BY CLIENT WITHIN MEDIA                       
         CLC   KEY+2(2),BCLT       IF HAVENT REACHED CLIENT                     
         BL    GN3D                TRY WITHIN THIS MEIDA                        
         TM    KEY+1,X'0F'         ELSE, BUMP TO NEXT MEDIA                     
         BO    GN20                                                             
         ZIC   RF,KEY+1                                                         
         LA    RF,1(RF)                                                         
         STC   RF,KEY+1                                                         
         B     GN3D                                                             
*                                  HAVE HIGHEST NO.                             
GN20     DS    0H                                                               
         LH    RF,HALF                                                          
         LA    RF,1(RF)                                                         
         STH   RF,0(R2)                                                         
         CLC   0(2,R2),AGMSTANO                                                 
         BNL   *+10                                                             
         MVC   0(2,R2),AGMSTANO                                                 
         B     GN23                                                             
*                                  BUMP TO NEXT NO.                             
GN22     DS    0H                                                               
         LH    RF,0(R2)                                                         
         LA    RF,1(RF)                                                         
         STH   RF,0(R2)                                                         
*                                  CHECK INV. NO. AGAINST SKIP RANGES           
GN23     DS    0H                                                               
         CLC   0(2,R2),=H'9999'    NO BILL OVER 9999                            
         BNH   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R4,AGMSKP1S                                                      
GN25     DS    0H                                                               
         OC    0(4,R4),0(R4)                                                    
         BZ    GN30                                                             
         CLC   0(2,R2),0(R4)                                                    
         BL    GN30                                                             
         CLC   0(2,R2),2(R4)                                                    
         BNL   GN26                                                             
         MVC   0(2,R2),2(R4)                                                    
         LH    RF,0(R2)                                                         
         LA    RF,1(RF)                                                         
         STH   RF,0(R2)                                                         
GN26     DS    0H                                                               
*                                                                               
*                                  FORMAT INVOICE NUMBER                        
GN30     DS    0H                                                               
GN31     DS    0H                                                               
*        NOTE- SPFMTINO REPLACES OLD INVOICE FORMATTING CODE                    
*                                                                               
         NI    W,X'0F'             W HAS YM (ONOY MONTH WAS/IS USED)            
         ZIC   R0,W                                                             
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  W+2(2),DUB                                                       
*                                                                               
         ZIC   R0,TODAYB           YEAR (NOTE-TODAYB SET BY CALLER,             
         CVD   R0,DUB                   NOT NECESSARILY TODAY                   
         OI    DUB+7,X'0F'                                                      
         UNPK  W(2),DUB                                                         
         MVC   W+4(2),=C'01'       DAY, FOR GOOD MEASURE                        
         GOTO1 =V(SPFMTINO),DMCB,W,(2,(R2)),(QMED,PROFB1),PROFB1X               
         L     RF,DMCB             A(FULL INVOICE NUMBER)                       
         MVC   0(10,R3),0(RF)                                                   
         L     RF,DMCB+8           A(MONTH)                                     
         MVC   SVINVMO,0(RF)                                                    
*                                                                               
GNX      DS    0H                                                               
         NI    DMINBTS,X'F7'       NO LONGER PASS DELETES                       
         MVI   DMOUTBTS,X'FF'                                                   
         MVC   RCTRACE,SAVTRACE                                                 
         XIT1                                                                   
