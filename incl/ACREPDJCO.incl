*          DATA SET ACREPDJCO  AT LEVEL 046 AS OF 05/12/16                      
         TITLE 'DAILY JOURNAL COMMON ROUTINES'                                  
         SPACE 1                                                                
***********************************************************************         
* PRINT START LOGOS                                                   *         
***********************************************************************         
         SPACE 1                                                                
LOGOUP   NTR1                                                                   
         L     R2,LOGOC                                                         
         USING LOGOD,R2                                                         
         MVC   ATTN,=C'A* '                                                     
         GOTO1 VGETLOGO,DMCB,(1,HALF),LOGOD,DATAMGR,ATTN                        
         MVC   LOGOJOB(4),DMCB     DESTINATION                                  
         CLI   LOGOJOB+3,C' '                                                   
         BH    *+8                                                              
         MVI   LOGOJOB+3,C'X'                                                   
         MVI   LOGOJOB+4,C'A'                                                   
         MVC   LOGOJOB+5(2),RCPROG                                              
         MVI   LOGOTYPE,C'S'                                                    
         MVI   LOGOEND,C'X'                                                     
         L     R3,ADBXAREA                                                      
         USING BOXD,R3                                                          
         MVC   BYTE,BOXYORN                                                     
         MVI   BOXYORN,C'N'                                                     
         GOTO1 LOGO,DMCB,LOGOD                                                  
         MVC   BOXYORN,BYTE                                                     
         MVC   PAGE,=H'1'                                                       
         MVI   FORCEHED,YES                                                     
         B     XIT                                                              
         DROP  R2,R3                                                            
         SPACE 1                                                                
***********************************************************************         
* PRINT END LOGOS                                                     *         
***********************************************************************         
         SPACE 1                                                                
LOGODOWN NTR1                                                                   
         L     R2,LOGOC                                                         
         USING LOGOD,R2                                                         
         CLI   JPROG,JPIJ                                                       
         BNE   LOGD02                                                           
         MVC   ATTN,=C'A* '                                                     
         GOTO1 VGETLOGO,DMCB,(1,HALF),LOGOD,DATAMGR,ATTN                        
         MVC   LOGOJOB(4),DMCB     DESTINATION                                  
         CLI   LOGOJOB+3,C' '                                                   
         BH    *+8                                                              
         MVI   LOGOJOB+3,C'X'                                                   
         MVI   LOGOJOB+4,C'A'                                                   
         MVC   LOGOJOB+5(2),RCPROG                                              
LOGD02   MVI   LOGOTYPE,C'E'                                                    
         L     R3,ADBXAREA                                                      
         USING BOXD,R3                                                          
         MVC   BYTE,BOXYORN                                                     
         MVI   BOXYORN,C'N'                                                     
         GOTO1 LOGO,DMCB,LOGOD                                                  
         MVC   BOXYORN,BYTE                                                     
         B     XIT                                                              
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* SET SPECIFIC COMPANY REQUIREMENTS                                   *         
***********************************************************************         
         SPACE 1                                                                
SPECPY   ST    RE,RETURN1                                                       
*                                                                               
         USING TBARECD,R4                                                       
         XC    IOKEY,IOKEY         SEARCH FOR COMPANY RECORD                    
         MVC   IOKEY,TBAKCPY                                                    
         GOTO1 AIO,IOHI+IOACCDIR+IO2                                            
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 AIO,IOGETREC+IOACCMST+IO2                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     RF,AIOBUFF                                                       
         USING CPYRECD,RF                                                       
         LA    RF,CPYRFST                                                       
         L     R2,LOGOC                                                         
         USING LOGOD,R2                                                         
         MVC   CMPNAME,SPACES                                                   
         MVI   CMPNAME,C'?'                                                     
SPEC02   CLI   0(RF),0                                                          
         BE    SPEC12                                                           
         CLI   0(RF),CPYELQ        COMPANY ELEMENT                              
         BE    SPEC06                                                           
         CLI   0(RF),NAMELQ        NAME ELEMENT                                 
         BE    SPEC08                                                           
         CLI   0(RF),ADRELQ        ADDRESS ELEMENT                              
         BE    SPEC10                                                           
SPEC04   XR    R0,R0                                                            
         IC    R0,1(RF)                                                         
         AR    RF,R0                                                            
         B     SPEC02                                                           
*                                                                               
         USING CPYELD,RF                                                        
SPEC06   CLI   CPYLN,CPYLN2Q                                                    
         BL    *+10                                                             
         MVC   COMP8,CPYSTAT8                                                   
         XC    CMPAPRV,CMPAPRV                                                  
         TM    CPYSTAT5-CPYELD(RF),CPYSBAPR  REGULAR APPROVAL REQUIRED          
         BZ    *+8                                                              
         OI    CMPAPRV,APRREG                                                   
         TM    CPYSTAT5-CPYELD(RF),CPYSBAPE  EFF. DATE APPROVAL REQD.           
         BZ    *+8                                                              
         OI    CMPAPRV,APREFF                                                   
         CLI   JPROG,JPIJ                                                       
         BE    *+16                                                             
         MVC   ALPHAID,CPYALPHA  ALPHA ID                                       
         MVC   SECALPHA,CPYALPHA SECURITY ALPHA ID                              
         MVC   CMPUID,CPYUID     PRINCIPAL ID                                   
         MVC   PRODUNIT,CPYPROD  UNIT/LEDGER FOR PRODUCTION                     
         MVC   RECUNIT,CPYRECV   UNIT/LEDGER FOR RECEIVABLES                    
         MVC   LOGO1,CPYLOGO                                                    
         TM    CPYSTAT1,CPYSOROE TEST COMPANY ON OFFICES                        
         BNO   SPEC04                                                           
         OI    OFFICES,ON1                                                      
         TM    CPYSTAT4,CPYSOFF2 TEST TWO CHARACTER OFFICES                     
         BNO   SPEC04                                                           
         OI    OFFICES,ON2                                                      
         B     SPEC04                                                           
*                                                                               
         USING NAMELD,RF                                                        
SPEC08   XR    RE,RE                                                            
         IC    RE,NAMLN                                                         
         SH    RE,=Y(NAMLN1Q+1)                                                 
         MVC   CMPNAME(0),NAMEREC                                               
         EX    RE,*-6                                                           
         MVC   LOGONAME,CMPNAME                                                 
         B     SPEC04                                                           
*                                                                               
         USING ADRELD,RF                                                        
SPEC10   MVC   LOGOADD,ADRADD1                                                  
         B     SPEC04                                                           
         DROP  R2                                                               
*                                                                               
SPEC12   MVC   HALF,PRODUNIT                                                    
         XC    PRODL1,PRODL1                                                    
         XC    PRODL2,PRODL2                                                    
         XC    RECLENS,RECLENS                                                  
SPEC14   XC    IOKEY,IOKEY                                                      
         LA    RF,IOKEY                                                         
         USING LDGRECD,RF                                                       
         MVC   LDGKEY,SPACES                                                    
         MVC   LDGKCPY,TBAKCPY                                                  
         MVC   LDGKUNT(L'PRODUNIT),HALF                                         
         GOTO1 AIO,IOHI+IOACCDIR+IO2                                            
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 AIO,IOGETREC+IOACCMST+IO2                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     RF,AIOBUFF                                                       
         LA    RF,LDGRFST                                                       
SPEC16   CLI   0(RF),0                                                          
         BE    SPECX                                                            
         CLI   0(RF),ACLELQ                                                     
         BE    SPEC18                                                           
         XR    R0,R0                                                            
         IC    R0,1(RF)                                                         
         AR    RF,R0                                                            
         B     SPEC16                                                           
*                                                                               
         USING ACLELD,RF                                                        
SPEC18   CLC   HALF,PRODUNIT                                                    
         BNE   *+16                                                             
         LA    RE,PRODL1                                                        
         LA    R0,L'PRODL1                                                      
         B     *+12                                                             
         LA    RE,RECLENS                                                       
         LA    R0,L'RECLENS                                                     
         LA    R1,ACLVALS                                                       
SPEC20   CLI   0(R1),0                                                          
         BE    SPEC22                                                           
         MVC   0(L'PRODLENA,RE),0(R1)                                           
         LA    RE,L'PRODLENA(RE)                                                
         LA    R1,L'ACLVALS(R1)                                                 
         BCT   R0,SPEC20                                                        
         DROP  RF                                                               
*                                                                               
SPEC22   CLC   HALF,PRODUNIT     TEST FOR PRODUCTION LEDGER                     
         BNE   SPEC24                                                           
         MVC   PRODL2,PRODL1                                                    
         LA    R0,L'PRODL1-1                                                    
         LA    RF,PRODL1                                                        
         BAS   RE,SPECLL                                                        
         MVC   HALF,RECUNIT      SET FOR RECEIVABLES LEDGER                     
         B     SPEC14                                                           
SPEC24   LA    R0,L'RECLENS-1    ASSUME RECEIVABLES LEDGER                      
         LA    RF,RECLENS                                                       
         BAS   RE,SPECLL                                                        
*                                                                               
         XC    UNITMAX,UNITMAX                                                  
*                                                                               
         L     RF,=A(DDIN1)                                                     
         GOTO1 ADDICTAT,DMCB,C'LU  ',(RF),DDOUT1                                
         L     RF,=A(DDIN2)                                                     
         GOTO1 ADDICTAT,DMCB,C'LL  ',(RF),DDOUT2                                
*                                                                               
         CLI   JPROG,JPIJ                                                       
         BE    SPECX                                                            
         BAS   RE,SECAID           SET SECURITY AGENCY ALPHA ID                 
         B     SPECX                                                            
*                                                                               
SPECLL   XR    R1,R1             CALCULATE LEDGER LENGTHS                       
         XR    R2,R2                                                            
         AR    RF,R0                                                            
         IC    R2,0(RF)                                                         
         BCTR  RF,0                                                             
         IC    R1,0(RF)                                                         
         SR    R2,R1                                                            
         STC   R2,1(RF)                                                         
         BCT   R0,*-16                                                          
         BR    RE                                                               
*                                                                               
SPECX    L     RE,RETURN1                                                       
         BR    RE                                                               
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* SET SECURITY AGENCY ALPHA ID                                        *         
***********************************************************************         
         SPACE 1                                                                
SECAID   ST    RE,RETURN2                                                       
         XC    IOKEY,IOKEY       READ SYSTEM ACCESS RECORD                      
         LA    R2,IOKEY                                                         
         USING CT5REC,R2                                                        
         MVI   CT5KTYP,CT5KTYPQ                                                 
         MVC   CT5KALPH,ALPHAID                                                 
         GOTO1 AIO,IORD+IOCLFILE+IO2                                            
         BNE   SECAIDX                                                          
*                                                                               
         L     R2,AIOBUFF                                                       
         LA    RE,CT5DATA                                                       
         XR    R0,R0                                                            
SECAID02 CLI   0(RE),0                                                          
         BE    SECAIDX                                                          
         CLI   0(RE),CTSEAELQ    SECURITY AGENCY ALPHA ID ELEMENT               
         BNE   *+14                                                             
         MVC   SECALPHA,CTSEAAID-CTSEAD(RE)                                     
         B     SECAIDX                                                          
         IC    R0,1(RE)                                                         
         AR    RE,R0                                                            
         B     SECAID02                                                         
SECAIDX  L     RE,RETURN2                                                       
         BR    RE                                                               
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* ADD COMPANY ENTRY TO NAME TABLE (COMPANY NAME)                      *         
*                   TO PROFILE TABLE (COMPANY PROFILES)               *         
***********************************************************************         
         SPACE 1                                                                
CMPNYADD NTR1                                                                   
         L     RE,ANBUFF           CHECK COMPANY IS IN NAME BUFFER              
         USING NTABD,RE                                                         
         LR    R1,RE                                                            
         AH    R1,=Y(NSIZE-NTLNQ)                                               
CMPNY02  OC    NTNTRY(NTLNQ),NTNTRY                                             
         BZ    CMPNY06             NO - ADD COMPANY                             
         CLI   NTTYP,NTTYPC                                                     
         BNE   CMPNY04                                                          
         CLC   NTAGY,ALPHAID                                                    
         BE    CMPNY08             YES - DON'T ADD COMPANY AGAIN                
CMPNY04  LA    RE,NTLNQ(RE)                                                     
         CR    R1,RE                                                            
         BH    CMPNY02                                                          
         DC    H'0'                TABLE IS FULL                                
CMPNY06  MVI   NTTYP,NTTYPC                                                     
         MVC   NTAGY,ALPHAID                                                    
         MVC   NTID,CMPUID                                                      
         MVC   NTNAME,CMPNAME                                                   
         DROP  RE                                                               
*                                                                               
CMPNY08  L     RE,APBUFF           CLEAR PROFILES TABLE                         
         ST    RE,TABLE                                                         
         L     RF,=A(PSIZE)                                                     
         XR    R0,R0                                                            
         XR    R1,R1                                                            
         MVCL  RE,R0                                                            
         ST    RE,TABLEX           SET END OF TABLE                             
*                                                                               
         L     R2,TABLE            R2=A(PROFILE TABLE)                          
         USING PTABD,R2                                                         
         LA    RF,PTLNQ                                                         
         STH   RF,PTLEN                                                         
         MVI   PTTYP,PTTYPC                                                     
         MVC   PTID,CMPUID                                                      
*                                                                               
         LA    R0,PTPMAX           NO. OF PROFILES                              
         LA    R3,PTPROF           R3=A(PROFILE BLOCK IN TABLE ENTRY)           
         XC    WORK,WORK                                                        
         MVC   WORK(3),=C'A0J'                                                  
         MVI   WORK+3,C'1'                                                      
         MVC   WORK+12(2),ALPHAID  AGENCY LEVEL                                 
CMPNY10  DS    0H                                                               
         GOTO1 GETPROF,DMCB,(X'C0',WORK),(R3),DATAMGR                           
         NI    WORK+3,X'0F'                                                     
         XR    RF,RF                                                            
         IC    RF,WORK+3                                                        
         TM    PRMODE,PRMSHRT      TEST SHORT RUN ACTIVE                        
         BZ    CMPNY12                                                          
         MVI   0(R3),C'N'                                                       
         LA    RE,SKRBTS           ONLY SHOW BATCH TYPE SUMMARY                 
         CR    RF,RE                                                            
         BNE   CMPNY12                                                          
         MVI   0(R3),C'Y'                                                       
CMPNY12  LA    RF,1(RF)                                                         
         STC   RF,WORK+3                                                        
         OI    WORK+3,X'F0'                                                     
         LA    R3,PTPLEN(R3)       SET FOR NEXT PROFILE                         
         BCT   R0,CMPNY10                                                       
         ST    R3,TABLE            SET FOR NEXT ENTRY                           
         B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* ADD ID-GROUP ENTRIES TO NAME TABLE (GROUP NAME)                     *         
*                      TO PROFILE TABLE (GROUP PROFILES & USER-IDS)   *         
***********************************************************************         
         SPACE 1                                                                
GROUPADD NTR1                                                                   
         XC    IOKEY,IOKEY         READ ID-GROUP LIST RECORD                    
         LA    R2,IOKEY                                                         
         USING CTWREC,R2                                                        
         MVI   CTWKTYP,CTWKTYPQ                                                 
         MVC   CTWKAGY,ALPHAID                                                  
         MVI   CTWKREC,CTWKRIDG                                                 
GROUP02  GOTO1 AIO,IOHI+IOCLFILE+IO2                                            
         BNE   GROUPX              ERROR                                        
         L     R2,AIOBUFF                                                       
         CLC   IOKEY(CTWKID-CTWKEY),0(R2)                                       
         BNE   GROUPX              RECORD NOT FOUND                             
         GOTO1 AIO,IOSEQ+IOCLFILE+IO3                                           
         BNE   GROUPX              ERROR                                        
         XC    IDGRPKEY,IDGRPKEY                                                
         L     R3,AIOBUFF                                                       
         CLC   IOKEY(CTWKID-CTWKEY),0(R3)                                       
         BNE   *+10                                                             
         MVC   IDGRPKEY,0(R3)      SET NEXT RECORD UNLESS AT END                
*                                                                               
         XC    IOKEY,IOKEY                                                      
         LA    R3,IOKEY            READ ID RECORD FOR ID-GROUP                  
         USING CTIREC,R3           (GROUP MUST HAVE AN ID NUMBER)               
         MVI   CTIKTYP,CTIKTYPQ                                                 
         MVC   CTIKID,SPACES                                                    
         MVC   CTIKID(L'CTWKID),CTWKID                                          
         GOTO1 AIO,IORD+IOCLFILE+IO3                                            
         BNE   GROUP24             ERROR - GET NEXT ID-GROUP                    
*                                                                               
         L     R3,AIOBUFF                                                       
         LA    R3,CTIDATA                                                       
GROUP04  CLI   0(R3),0                                                          
         BE    GROUP12                                                          
         CLI   0(R3),CTDSCELQ      DESCRIPTION ELEMENT (ID-GROUP #)             
         BNE   *+14                                                             
         MVC   HALF2,CTDSC-CTDSCD(R3)                                           
         B     GROUP05                                                          
         XR    R0,R0                                                            
         IC    R0,1(R3)                                                         
         AR    R3,R0                                                            
         B     GROUP04                                                          
         DROP  R3                                                               
*                                                                               
GROUP05  L     R3,TABLE            START BUILDING NEW ENTRY                     
         USING PTABD,R3                                                         
         MVI   PTTYP,PTTYPG                                                     
         MVC   PTID,HALF2                                                       
         LA    RE,PTIDL            RE=A(USER-ID BLOCK IN ENTRY)                 
         XR    RF,RF               ZERO USER-ID BLOCK LENGTH                    
         MVC   WORK2,SPACES                                                     
         MVI   WORK2,C'?'          DEFAULT ID-GROUP NAME                        
*                                                                               
         LA    R2,CTWDATA                                                       
GROUP06  CLI   0(R2),0                                                          
         BE    GROUP12                                                          
*                                                                               
         CLI   0(R2),CTDSCELQ      DESCRIPTION ELEMENT (ID-GROUP NAME)          
         BNE   GROUP08                                                          
         USING CTDSCD,R2                                                        
         XR    R1,R1                                                            
         IC    R1,CTDSCLEN                                                      
         SH    R1,=Y(CTDSC-CTDSCD+1)                                            
         MVC   WORK2(0),CTDSC                                                   
         EX    R1,*-6                                                           
         B     GROUP10                                                          
*                                                                               
GROUP08  CLI   0(R2),CTLSTELQ      LIST ELEMENT (USER-ID #)                     
         BNE   GROUP10                                                          
         USING CTLSTD,R2                                                        
         MVC   0(L'PTIDL,RE),CTLSTDTA+L'CTIKID                                  
         LA    RE,L'PTIDL(RE)      SET FOR NEXT USER-ID #                       
         LA    RF,L'PTIDL(RF)      INCREMENT RECORD LENGTH                      
*                                                                               
GROUP10  XR    R0,R0                                                            
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     GROUP06                                                          
*                                                                               
GROUP12  LA    RF,PTLNQ(RF)        ADD STANDARD & USER-ID BLOCK LENGTHS         
         STH   RF,PTLEN                                                         
         AR    RF,R3                                                            
         C     RF,TABLEX           CHECK TABLE IS LARGE ENOUGTH                 
         BNH   *+6                                                              
         DC    H'0'                                                             
         ST    RF,TABLE            SET FOR NEXT ENTRY                           
*                                                                               
         LA    R0,PTPMAX           NO. OF PROFILES                              
         LA    R4,PTPROF           R4=A(PROFILE BLOCK IN TABLE ENTRY)           
         XC    WORK,WORK                                                        
         MVC   WORK(3),=C'A0J'                                                  
         MVI   WORK+3,C'1'                                                      
GROUP14  MVC   WORK+12(2),HALF2    ID-GROUP LEVEL                               
         GOTO1 GETPROF,DMCB,WORK,(R4),DATAMGR                                   
         OC    0(PTPLEN,R4),0(R4)                                               
         BNZ   GROUP16                                                          
         MVC   WORK+12(2),DEFLTID  DEFAULT ID                                   
         GOTO1 GETPROF,DMCB,(X'C0',WORK),(R4),DATAMGR                           
GROUP16  NI    WORK+3,X'0F'                                                     
         XR    RF,RF                                                            
         IC    RF,WORK+3                                                        
         LA    RF,1(RF)                                                         
         STC   RF,WORK+3                                                        
         OI    WORK+3,X'F0'                                                     
         TM    PRMODE,PRMSHRT      TEST SHORT RUN ACTIVE                        
         BZ    *+8                                                              
         MVI   0(R4),C'N'                                                       
         LA    R4,PTPLEN(R4)       SET FOR NEXT PROFILE                         
         BCT   R0,GROUP14                                                       
         DROP  R3                                                               
*                                                                               
         L     RE,ANBUFF           CHECK ID-GROUP IS IN NAME BUFFER             
         USING NTABD,RE                                                         
         LR    R1,RE                                                            
         AH    R1,=Y(NSIZE-NTLNQ)                                               
GROUP18  OC    NTNTRY(NTLNQ),NTNTRY                                             
         BZ    GROUP22             NO - ADD ID-GROUP                            
         CLI   NTTYP,NTTYPG                                                     
         BNE   GROUP20                                                          
         CLC   NTAGY,ALPHAID                                                    
         BNE   GROUP20                                                          
         CLC   NTID,HALF2                                                       
         BE    GROUP24             YES - DON'T ADD ID-GROUP AGAIN               
GROUP20  LA    RE,NTLNQ(RE)                                                     
         CR    R1,RE                                                            
         BH    GROUP18                                                          
         DC    H'0'                TABLE IS FULL                                
GROUP22  MVI   NTTYP,NTTYPG                                                     
         MVC   NTAGY,ALPHAID                                                    
         MVC   NTID,HALF2                                                       
         MVC   NTNAME,WORK2                                                     
*                                                                               
GROUP24  OC    IDGRPKEY,IDGRPKEY                                                
         BZ    GROUPX                                                           
         MVC   IOKEY,IDGRPKEY                                                   
         B     GROUP02                                                          
*                                                                               
GROUPX   B     XIT                                                              
         DROP  R2,RE                                                            
         EJECT                                                                  
***********************************************************************         
* ADD USER-ID ENTRY TO NAME TABLE (USER-ID NAME)                      *         
*                   TO PROFILE TABLE (USER-ID PROFILES)               *         
***********************************************************************         
         SPACE 1                                                                
USERADD  NTR1                                                                   
         L     R2,ANBUFF           CHECK USER-ID IS IN NAME BUFFER              
         USING NTABD,R2                                                         
         LR    R1,R2                                                            
         AH    R1,=Y(NSIZE-NTLNQ)                                               
USER02   OC    NTNTRY(NTLNQ),NTNTRY                                             
         BZ    USER06              NO - ADD USER-ID                             
         CLI   NTTYP,NTTYPU                                                     
         BNE   USER04                                                           
         CLC   NTAGY,ALPHAID                                                    
         BNE   USER04                                                           
         CLC   NTID,BATUID                                                      
         BE    USER18              YES - DON'T ADD USER-ID AGAIN                
USER04   LA    R2,NTLNQ(R2)                                                     
         CR    R1,R2                                                            
         BH    USER02                                                           
         DC    H'0'                TABLE IS FULL                                
USER06   MVI   NTTYP,NTTYPU                                                     
         MVC   NTAGY,ALPHAID                                                    
         MVC   NTID,BATUID                                                      
         MVC   NTNAME,SPACES       DEFAULT NAME                                 
         MVI   NTNAME,C'?'                                                      
*                                                                               
         L     RE,APBUFF           CHECK USER-ID IS IN PROFILE BUFFER           
         USING PTABD,RE                                                         
USER08   OC    PTNTRY(PTLNQ),PTNTRY                                             
         BZ    USER10              NO - ADD USER-ID                             
         CLI   PTTYP,PTTYPU                                                     
         BNE   *+14                                                             
         CLC   PTID,BATUID                                                      
         BE    USERX               YES - DON'T ADD USER-ID AGAIN                
         LH    RF,PTLEN                                                         
         AR    RE,RF                                                            
         C     RE,TABLEX                                                        
         BNH   USER08                                                           
         DC    H'0'                TABLE IS FULL                                
         DROP  RE                                                               
*                                                                               
USER10   XC    IOKEY,IOKEY         READ ID RECORD FOR USER-ID                   
         LA    R3,IOKEY                                                         
         USING CTIREC,R3                                                        
         MVI   CTIKTYP,CTIKTYPQ                                                 
         MVC   CTIKNUM,BATUID                                                   
         GOTO1 AIO,IORD+IOCLFILE+IO3                                            
         BNE   USER18              ERROR - USE DEFAULT NAME                     
*                                                                               
         L     R3,AIOBUFF          R3=A(USER-ID RECORD)                         
         LA    RE,CTIDATA                                                       
         XR    R0,R0                                                            
USER12   CLI   0(RE),0                                                          
         BE    USER18              EOR - USE DEFAULT NAME                       
*&&US*&& CLI   0(RE),CTDSCELQ      DESCRIPTION ELEMENT (USER-ID)                
*&&US*&& BE    USER14                                                           
*&&UK*&& CLI   0(RE),CTDSTELQ      DESTINATION ELEMENT (USER-ID NAME)           
*&&UK*&& BE    USER16                                                           
         IC    R0,1(RE)                                                         
         AR    RE,R0                                                            
         B     USER12                                                           
*&&US                                                                           
         USING CTDSCD,RE                                                        
USER14   XR    R1,R1                                                            
         IC    R1,CTDSCLEN                                                      
         SH    R1,=Y(CTDSC-CTDSCD+1)                                            
         MVC   NTNAME(0),CTDSC                                                  
         EX    R1,*-6                                                           
         B     USER18                                                           
*&&                                                                             
*&&UK                                                                           
         USING CTDSTD,RE                                                        
USER16   MVC   NTNAME(L'CTDSTNAM),CTDSTNAM                                      
*&&                                                                             
         DROP  R2,R3,RE                                                         
*                                                                               
         USING PTABD,R2                                                         
USER18   L     R2,TABLE            R2=A(PROFILE TABLE)                          
         LA    R3,PTPROF           R3=A(PROFILE BLOCK IN TABLE ENTRY)           
         LA    RF,PTLNQ            FIXED LENGTH ENTRY                           
         C     RF,TABLEX           CHECK TABLE IS LARGE ENOUGTH                 
         BNH   *+6                                                              
         DC    H'0'                                                             
         STH   RF,PTLEN                                                         
         AR    RF,R2                                                            
         ST    RF,TABLE            SET FOR NEXT ENTRY                           
         MVI   PTTYP,PTTYPU                                                     
         MVC   PTID,BATUID                                                      
*                                                                               
         XC    WORK,WORK                                                        
*                                                                               
         CLI   JPROG,JPIJ                                                       
         BNE   USER20                                                           
         MVC   WORK+5(2),STARALL                                                
         MVC   DEFLTID,BATUID                                                   
         B     USER28                                                           
*                                                                               
USER20   L     R2,APBUFF           CHECK USER-ID IS IN A GROUP                  
USER22   LH    RF,PTLEN                                                         
         LTR   RF,RF                                                            
         BZ    USER28                                                           
         LA    RE,PTIDL                                                         
         LA    R1,0(RF,R2)                                                      
         CLI   PTTYP,PTTYPG                                                     
         BNE   USER26                                                           
USER24   CLC   0(L'BATUID,RE),BATUID                                            
         BNE   *+20                                                             
         MVC   WORK+5(2),STARALL   YES - SET MEDIA TO **                        
         MVC   DEFLTID,PTID            - SET GROUP PROFILES AS DEFAULT          
         B     USER28                                                           
         LA    RE,L'PTIDL(RE)      NO - CHECK REST OF GROUP                     
         CR    RE,R1                                                            
         BL    USER24                                                           
USER26   C     R1,TABLEX              - AND ALL OTHER GROUPS                    
         BNL   USER28                                                           
         AR    R2,RF                                                            
         B     USER22                                                           
*                                                                               
USER28   LA    R0,PTPMAX           NO. OF PROFILES                              
         MVC   WORK(3),=C'A0J'                                                  
         MVI   WORK+3,C'1'                                                      
USER30   MVC   WORK+12(2),BATUID                                                
         GOTO1 GETPROF,DMCB,WORK,(R3),DATAMGR                                   
         OC    0(PTPLEN,R3),0(R3)                                               
         BNZ   USER34                                                           
         XC    WORK+5(2),WORK+5                                                 
USER32   MVC   WORK+12(2),DEFLTID                                               
         GOTO1 GETPROF,DMCB,WORK,(R3),DATAMGR                                   
         OC    0(PTPLEN,R3),0(R3)                                               
         BNZ   USER34                                                           
         MVC   DEFLTID,ALPHAID                                                  
         MVC   WORK+12(2),DEFLTID                                               
         GOTO1 GETPROF,DMCB,(X'C0',WORK),(R3),DATAMGR                           
USER34   NI    WORK+3,X'0F'                                                     
         XR    RF,RF                                                            
         IC    RF,WORK+3                                                        
         LA    RF,1(RF)                                                         
         STC   RF,WORK+3                                                        
         OI    WORK+3,X'F0'                                                     
         TM    PRMODE,PRMSHRT      TEST SHORT RUN ACTIVE                        
         BZ    *+8                                                              
         MVI   0(R3),C'N'                                                       
         LA    R3,PTPLEN(R3)       SET FOR NEXT PROFILE                         
         BCT   R0,USER30                                                        
         L     R1,ADMASTC                                                       
         MVC   MCORIGID-MASTD(L'ALPHAID,R1),ALPHAID                             
*                                                                               
USERX    B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* DAILY JOURNAL REPORTS                                               *         
* NTRY - R4 = A(BATCH HEADER RECORD)                                  *         
***********************************************************************         
         SPACE 1                                                                
DJREPS   NTR1                                                                   
         USING TBARECD,R4                                                       
         LA    RF,TBARFST                                                       
         XR    R0,R0                                                            
REPS02   CLI   0(RF),0                                                          
         BE    REPS06                                                           
         CLI   0(RF),BHDELQ        BATCH HEADER ELEMENT                         
         BE    REPS04                                                           
         IC    R0,1(RF)                                                         
         AR    RF,R0                                                            
         B     REPS02                                                           
*                                                                               
         USING BHDELD,RF                                                        
REPS04   XR    R1,R1                                                            
         ICM   R1,3,BHDITEMC                                                    
         CVD   R1,DTCITM                                                        
         MVC   DTCACD,BHDCASHC                                                  
         MVC   DTCACC,DTCACD                                                    
         MVC   BATNAME,BHDNAME                                                  
         MVC   BATLUID,BHDLUID                                                  
         MVC   BATAPRVR,BHDAPRVR                                                
         MVC   BATHSTAT,BHDSTAT1                                                
         DROP  RF                                                               
*                                                                               
REPS06   TM    TBAHRIND,TBAHIGDJ   TEST FOR A NON-INPUT BATCH                   
         BZ    *+8                                                              
         OI    RIND2,R2NON                                                      
*                                                                               
         LA    RF,TIMBATCH         TEST FOR A TIMESHEET BATCH                   
REPS08   CLI   0(RF),0                                                          
         BE    REPS10                                                           
         CLC   SKTYP,0(RF)                                                      
         BE    *+12                                                             
         LA    RF,1(RF)                                                         
         B     REPS08                                                           
         OI    RIND3,R3TIME                                                     
         B     REPS26                                                           
*                                                                               
REPS10   LA    RF,SETBATCH         TEST FOR A SPECIAL JOURNAL ENTRY             
REPS12   CLI   0(RF),0             BATCH                                        
         BE    REPS14                                                           
         CLC   SKTYP,0(RF)                                                      
         BE    *+12                                                             
         LA    RF,1(RF)                                                         
         B     REPS12                                                           
         OI    RIND3,R3SPENT                                                    
         B     REPS26                                                           
*                                                                               
REPS14   LA    RF,ENTBATCH         TEST FOR A JOURNAL ENTRY BATCH               
REPS16   CLI   0(RF),0                                                          
         BE    REPS26                                                           
         CLC   SKTYP,0(RF)                                                      
         BE    *+12                                                             
         LA    RF,1(RF)                                                         
         B     REPS16                                                           
         OI    RIND3,R3ENTRY                                                    
         DROP  R4                                                               
*                                                                               
REPS26   L     R1,=A(REPFRM01)     SET REPORT FORMAT FOR SUB-PROGS 0-2          
         CLI   RCSUBPRG,0                                                       
         BNE   REPS28                                                           
         TM    RIND3,R3TIME                                                     
         BZ    REPS30                                                           
         L     R1,=A(REPFRM02)                                                  
         B     REPS30                                                           
REPS28   L     R1,=A(REPFRM03)                                                  
         CLI   RCSUBPRG,1                                                       
         BNE   REPS30                                                           
         TM    RIND3,R3TIME                                                     
         BZ    REPS30                                                           
         L     R1,=A(REPFRM04)                                                  
REPS30   ST    R1,FORMAT                                                        
*                                                                               
         ZAP   DTBHRS,=P'0'        CLEAR DJ TOTALS                              
         ZAP   DTBACD,=P'0'                                                     
         ZAP   DTBACC,=P'0'                                                     
         ZAP   DTBAND,=P'0'                                                     
         ZAP   DTBANC,=P'0'                                                     
*                                                                               
         OI    RIND2,R2FPAGE       FIRST TX ON PAGE                             
         ZAP   DTBITD,=P'0'        CLEAR DELETED/ACTIVE ITEMS TOTALS            
         ZAP   DTBITA,=P'0'                                                     
*                                                                               
         MVC   IOKEY,BATAKEY                                                    
REPS32   GOTO1 AIO,IORD+IOACCDIR+IO2  RE-READ TO ESTABLISH SEQUENCE             
         BE    *+6                                                              
         DC    H'0'                                                             
REPS34   GOTO1 AIO,IOSEQ+IOACCDIR+IO2 GET BATCH ITEM RECORD                     
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   IOKEYSAV(L'TBAKEY-L'TBAKTSEQ),IOKEY                              
         BNE   REPS128                                                          
         XR    RF,RF                                                            
         TM    IOKEY+(TBAKESTA-TBAKEY),TBAESLDE  TEST LOGICALLY DELETED         
         BZ    *+14                                                             
         AP    DTBITD,=P'1'                                                     
         B     REPS34                                                           
         AP    DTBITA,=P'1'                                                     
         TM    RIND2,R2FPAGE       TEST FOR FIRST TX ON PAGE                    
         BO    REPS38                                                           
         CLI   SDPROF+7,YES        ARE WE RULING OFF BETWEEN ITEMS?             
         BE    REPS36                                                           
         GOTO1 ACREPORT            NO - LEAVE BLANK LINE                        
         B     REPS38                                                           
REPS36   MVI   BBYTE2,RLINE                                                     
         GOTO1 PAGER,DATA                                                       
REPS38   GOTO1 AIO,IOGETREC+IOACCMST+IO2                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R2,AIOBUFF          R2=A(BATCH ITEM RECORD)                      
         ST    R2,ABITREC                                                       
         USING TBARECD,R2                                                       
         MVC   BATIKEY,TBAKEY                                                   
         LA    R2,TBARFST                                                       
         USING ASKELD,R2                                                        
         OI    RIND2,R2FITEM       FIRST TX ON ITEM                             
         TM    RIND2,R2NON         IS THIS BATCH A NON-INPUT                    
         BO    *+12                                                             
         TM    RIND3,R3SPENT+R3TIME OR SPECIAL JRNL ENTRY OR TIMESHEET?         
         BZ    REPS40                                                           
         BAS   RE,ANASCAN          YES - CHECK ITEM HAS ACCOUNT TXS             
REPS40   CLI   0(R2),0             TEST EOR                                     
         BNE   *+18                                                             
         NI    RIND3,X'FF'-R3NOACS                                              
         MVC   IOKEY,BATIKEY                                                    
         B     REPS32                                                           
         CLI   0(R2),ASKELQ                                                     
         BNE   *+14                                                             
         MVC   IOKEY,ASKKEY                                                     
         B     REPS41                                                           
         CLI   0(R2),GINELQ                                                     
         BNE   REPS126                                                          
REPS40A  BAS   RE,PASSGIN          PASS GROUP INVOICE TXS                       
         TM    RIND3,R3GIN                                                      
         BZ    REPS126                                                          
         MVC   IOKEY,LGINAKEY                                                   
*                                                                               
REPS41   LA    R1,ANAUNIT          TEST FOR AN ANALYSIS ACCOUNT                 
REPS42   CLI   0(R1),0                                                          
         BE    REPS44                                                           
         CLC   0(1,R1),IOKEY+1                                                  
         BE    *+12                                                             
         LA    R1,1(R1)                                                         
         B     REPS42                                                           
         OI    RIND3,R3ANAL                                                     
*                                                                               
REPS44   TM    RIND3,R3GIN                                                      
         BO    REPS47                                                           
*                                                                               
         CLI   SDPROF+3,YES        CHECK WE ARE SHOWING ALL ACCOUNTS            
         BE    REPS46                                                           
         TM    RIND3,R3ANAL        NO - BUT SHOW IF NOT AN ANALYSIS A/C         
         BZ    REPS46                                                           
         TM    RIND3,R3NOACS          - OR ITEM HAS ONLY ANALYSIS A/CS          
         BO    REPS46                                                           
         TM    RIND3,R3ENTRY          - OR IF A JOURNAL ENTRY BATCH             
         BO    REPS46                                                           
         TM    RIND3,R3TIME           - OR IF A RECEIVABLE ANALYSIS A/C         
         BZ    REPS126                  ON A TIMESHEET BATCH                    
         CLC   =C'1R',IOKEY+1                                                   
         BNE   REPS126                                                          
*                                                                               
REPS46   GOTO1 AIO,IORD+IOACCDIR+IO3  READ TRANSACTION RECORD                   
         BNE   REPS126                                                          
         GOTO1 AIO,IOGETREC+IOACCMST+IO3                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
REPS47   L     R3,AIOBUFF          R3=A(TRANSACTION RECORD)                     
         ST    R3,ATRNREC                                                       
         USING TRNRECD,R3                                                       
*                                                                               
         TM    RIND1,R1RACT        TEST REPORT ACTIVE?                          
         BO    *+16                                                             
         BAS   RE,PAGENEW          NO - PREDEFINE PAGE/BOXES FOR FIRST          
         OI    RIND1,R1RACT             PASS                                    
         B     REPS48                                                           
         CLI   SDPROF+7,YES        YES - SKIP TO FIRST LINE OF DATA             
         BNE   REPS48                    BOXES AT TURN OF PAGE                  
         TM    RIND2,R2FPAGE                                                    
         BZ    REPS48                                                           
         GOTO1 ACREPORT                                                         
*                                                                               
REPS48   CLI   SDPROF+3,YES        ARE WE SHOWING ALL ACCOUNTS?                 
         BE    REPS50                                                           
         LA    R1,L'TRNKULC        NO - SHOW CONTRA IN C/A COLUMN               
         LA    RF,PCACT                                                         
         MVC   0(L'TRNKULC,RF),TRNKULC  *CONTRA ACCOUNT*                        
         CLI   0(RF),C' '                                                       
         BH    *+18                                                             
         MVC   0(L'TRNKULC-1,RF),1(RF)                                          
         MVI   L'TRNKULC-1(RF),C' '                                             
         BCT   R1,*-18                                                          
REPS50   TM    RIND2,R2FITEM+R2NON 1ST TX ON ITEM OR NON-INPUT BATCH?           
         BNZ   *+12                                                             
         CLI   SDPROF+6,YES        NO - TEST IF DATE IS REQUIRED AGAIN          
         BE    REPS52                                                           
         GOTO1 DATCON,DMCB,(1,TRNKDATE),(17,PDATE)  *DATE*                      
*                                                                               
REPS52   LA    R4,TRNRFST                                                       
REPS54   CLI   0(R4),0                                                          
         BE    REPS70                                                           
         CLI   0(R4),TRNELQ        DEBITS/CREDITS                               
         BE    REPS58                                                           
         CLI   RCSUBPRG,2                                                       
         BE    REPS56                                                           
         TM    RIND3,R3TIME        SHOW HOURS IF A TIMESHEET BATCH              
         BZ    REPS56                                                           
         TM    RIND2,R2NON                                                      
         BO    REPS55                                                           
         CLC   =C'1R',TRNKUNT      AND RECEIVABLE ANALYSIS A/C (INPUT)          
         BNE   REPS56                                                           
         CLI   0(R4),SCIELQ                                                     
         BE    REPS66                                                           
         B     REPS56                                                           
REPS55   CLC   =C'SJ',TRNKUNT      OR A PRODUCTION A/C (NON-INPUT)              
         BNE   REPS56                                                           
         CLI   0(R4),PRTELQ                                                     
         BE    REPS69                                                           
REPS56   XR    RE,RE                                                            
         IC    RE,1(R4)                                                         
         AR    R4,RE                                                            
         B     REPS54                                                           
*                                                                               
         USING TRNELD,R4                                                        
REPS58   TM    RIND3,R3GIN         ALWAYS SHOW REF FOR SPLIT INVOICES           
         BZ    *+12                                                             
         TM    RIND3,R3ANAL        EXCEPT ANALYSIS POSTINGS                     
         BZ    *+20                                                             
         TM    RIND2,R2FITEM+R2NON 1ST TX ON ITEM OR NON-INPUT BATCH?           
         BNZ   *+12                                                             
         CLI   SDPROF+6,YES        NO - TEST IF REF# IS REQUIRED AGAIN          
         BE    *+10                                                             
         MVC   PREF(L'PREF),TRNREF *REFERENCE*                                  
         CLI   RCSUBPRG,0                                                       
         BNE   *+12                                                             
         TM    RIND3,R3ANAL                                                     
         BO    REPS62                                                           
*                                                                               
         TM    TRNSTAT,TRNSDR                                                   
         BO    REPS60                                                           
         CURED TRNAMNT,(L'PACC,PACC),2,MINUS=YES  *ACCOUNT CREDITS*             
         AP    DTBACC,TRNAMNT                                                   
         B     REPS56                                                           
REPS60   CURED TRNAMNT,(L'PACD,PACD),2,MINUS=YES  *ACCOUNT DEBITS*              
         AP    DTBACD,TRNAMNT                                                   
         B     REPS56                                                           
*                                                                               
REPS62   TM    TRNSTAT,TRNSDR                                                   
         BO    REPS64                                                           
         CURED TRNAMNT,(L'PANC,PANC),2,MINUS=YES  *ANALYSIS CREDITS*            
         AP    DTBANC,TRNAMNT                                                   
         B     REPS56                                                           
REPS64   CURED TRNAMNT,(L'PAND,PAND),2,MINUS=YES  *ANALYSIS DEBITS*             
         AP    DTBAND,TRNAMNT                                                   
         B     REPS56                                                           
*                                                                               
         USING SCIELD,R4                                                        
REPS66   CLI   SCITYPE,SCITHOUR                                                 
         BNE   REPS56                                                           
         AP    DTBHRS,SCIAMNT                                                   
         CLI   RCSUBPRG,0                                                       
         BNE   REPS68                                                           
         CURED SCIAMNT,(L'PHRS1,PHRS1),2,MINUS=YES                              
         B     REPS56                                                           
REPS68   CURED SCIAMNT,(L'PHRS2,PHRS2),2,MINUS=YES                              
         B     REPS56                                                           
*                                                                               
         USING PRTELD,R4                                                        
REPS69   AP    DTBHRS,PRTHOUR                                                   
         CLI   RCSUBPRG,0                                                       
         BNE   REPS69A                                                          
         CURED PRTHOUR,(L'PHRS1,PHRS1),2,MINUS=YES                              
         B     REPS56                                                           
REPS69A  CURED PRTHOUR,(L'PHRS2,PHRS2),2,MINUS=YES                              
         B     REPS56                                                           
*                                                                               
REPS70   LA    R3,IOKEY            READ ACCOUNT RECORD                          
         USING ACTRECD,R3                                                       
         MVC   ACTKEY,SPACES                                                    
         LA    RF,ASKKEY                                                        
         TM    RIND3,R3GIN                                                      
         BZ    *+8                                                              
         LA    RF,LGINAKEY                                                      
         MVC   ACTKCULA(L'ACTKCULA),0(RF)                                       
         GOTO1 AIO,IORD+IOACCDIR+IO4                                            
         BNE   REPS80                                                           
         GOTO1 AIO,IOGETREC+IOACCMST+IO4                                        
         BNE   REPS80                                                           
         MVC   PACT,ACTKULA        *ACCOUNT CODE*                               
*                                                                               
         L     R3,AIOBUFF          R3=A(ACCOUNT RECORD)                         
         LA    R3,ACTRFST                                                       
REPS72   CLI   0(R3),0                                                          
         BE    REPS80                                                           
         CLI   0(R3),NAMELQ                                                     
         BE    REPS76                                                           
*&&UK*&& CLI   0(R3),RSTELQ                                                     
*&&UK*&& BE    REPS78                                                           
REPS74   XR    RF,RF                                                            
         IC    RF,1(R3)                                                         
         AR    R3,RF                                                            
         B     REPS72                                                           
*                                                                               
         USING NAMELD,R3                                                        
REPS76   IC    RF,NAMLN                                                         
         SH    RF,=Y(NAMLN1Q)                                                   
         GOTO1 CHOPPER,DMCB,((RF),NAMEREC),(L'PACCN,WORK),4                     
         XR    RE,RE                                                            
         ICM   RE,1,11(R1)                                                      
         BZ    REPS74                                                           
         LA    R1,WORK                                                          
         LA    RF,PACCN                                                         
         MVC   0(L'PACCN,RF),0(R1) *ACCOUNT NAME*                               
         LA    RF,L'P(RF)                                                       
         LA    R1,L'PACCN(R1)                                                   
         BCT   RE,*-14                                                          
         B     REPS74                                                           
*&&UK                                                                           
         USING RSTELD,R3                                                        
REPS78   CLI   RSTLN,RSTLN3Q                                                    
         BL    REPS74                                                           
         TM    RSTSTAT5,RSTSSUND   SUNDRY CREDITOR                              
         BZ    REPS74                                                           
         OI    RIND3,R3SUNDRY                                                   
         B     REPS74                                                           
*&&                                                                             
         DROP  R3                                                               
*                                                                               
REPS80   CLI   RCSUBPRG,2                                                       
         BE    REPS100                                                          
         L     R3,ATRNREC                                                       
         BAS   RE,EXTRATX          *WORK CODE/OFFICE & TX DETAILS*              
         NI    RIND2,X'FF'-R2FITEM                                              
*                                                                               
REPS100  L     RE,DETSTART         START OF TX DETAILS LIST                     
         OC    0(L'PDET1,RE),0(RE)                                              
         BZ    REPS124                                                          
REPS102  LA    RF,L'PDET1          RF=L'DETAILS WINDOW                          
         CLI   SDPROF+3,YES        ARE WE SHOWING ALL ACCOUNTS?                 
         BE    REPS104                                                          
         LA    RF,L'PDET3          LONGER LENGTH IF ONLY MAIN POSTINGS          
         TM    RIND3,R3TIME                                                     
         BZ    REPS106                                                          
         LA    RF,L'PDET4                                                       
         B     REPS106                                                          
REPS104  TM    RIND3,R3TIME                                                     
         BZ    REPS106                                                          
         LA    RF,L'PDET2                                                       
REPS106  LA    R0,4                NUMBER OF PRINTLINES                         
         LA    R4,PDET1            R4=A(DETAILS WINDOW)                         
         CLI   SDPROF+3,YES        ARE WE SHOWING CONTRA COLUMN?                
         BE    *+8                                                              
         LA    R4,PDET3                                                         
         ST    R4,SPLIT            PRESET DUMMY BREAK TO START OF LINE          
*                                                                               
REPS108  LA    R1,0(RF,RE)                                                      
REPS110  CLI   0(R1),C','          FIND LAST REAL BREAK IN LINE                 
         BE    REPS118                                                          
         C     R4,SPLIT            HAS LAST DUMMY BREAK BEEN SET?               
         BNE   REPS112                                                          
         CLI   0(R1),C' '          NO - SET IF WE HAVE A SPACE                  
         BNE   *+8                                                              
         ST    R1,SPLIT                                                         
         CLI   0(R1),C'='               OR AN EQUALS SIGN                       
         BNE   REPS112                                                          
         ST    R1,SPLIT                                                         
         SR    R1,RF                    (EQUALS CAN'T BE END CHR)               
         CR    R1,RE                                                            
         L     R1,SPLIT                                                         
         BL    REPS112                                                          
         ST    R4,SPLIT                                                         
REPS112  CR    R1,RE                                                            
         BNH   *+8                                                              
         BCT   R1,REPS110                                                       
*                                                                               
         C     R4,SPLIT            MOVE WHOLE LINE IF NO DUMMY BREAK            
         BE    REPS114                                                          
         L     R1,SPLIT            MOVE LINE AS FAR AS DUMMY BREAK              
         CLI   0(R1),C'='                                                       
         BNE   REPS118                                                          
         SR    R1,RE                                                            
         B     REPS116                                                          
REPS114  LR    R1,RF                                                            
         BCTR  R1,0                                                             
REPS116  MVC   0(0,R4),0(RE)                                                    
         EX    R1,*-6                                                           
         LA    RE,1(R1,RE)                                                      
         B     REPS120                                                          
*                                                                               
REPS118  SR    R1,RE               MOVE LINE AS FAR AS REAL BREAK               
         BCTR  R1,0                                                             
         MVC   0(0,R4),0(RE)                                                    
         EX    R1,*-6                                                           
         LA    RE,2(R1,RE)                                                      
*                                                                               
REPS120  L     R1,DETEND           END OF DETAILS IN BUFFER                     
         CR    RE,R1                                                            
         BNL   REPS122                                                          
         LA    R4,L'P(R4)                                                       
         ST    R4,SPLIT            SET DUMMY BREAK TO START NEXT LINE           
         BCT   R0,REPS108                                                       
         OI    RIND2,R2LASTX       LAST TRANSACTION ACTIVE                      
REPS122  ST    RE,DETSTART                                                      
*                                                                               
REPS124  GOTO1 ACREPORT                                                         
         GOTO1 PAGER,DATA                                                       
         NI    RIND2,X'FF'-R2LASTX                                              
         L     RE,DETSTART         ANY MORE TX DETAILS?                         
         C     RE,DETEND                                                        
         BL    REPS102             YES - GET NEXT BLOCK                         
*                                                                               
         TM    RIND3,R3GIN                                                      
         BZ    REPS126                                                          
         NI    RIND3,X'FF'-R3ANAL                                               
         B     REPS40A                                                          
REPS126  NI    RIND3,X'FF'-(R3ANAL+R3SUNDRY)                                    
         XR    R0,R0                                                            
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     REPS40                                                           
*                                                                               
REPS128  TM    RIND1,R1RACT        QUIT HERE IF REPORT NOT ACTIVE               
         BZ    XIT                                                              
         BAS   RE,BATTOTS          DO BATCH TOTALS                              
         XC    RIND1,RIND1                                                      
         B     XIT                                                              
         DROP  R2,R4                                                            
         EJECT                                                                  
***********************************************************************         
* BUILD TRANSACTION DETAILS AND SUBSIDIARY INFORMATION BLOCK          *         
*                                                                     *         
* NTRY - R3=A(TRANSACTION RECORD) - AIO3                              *         
***********************************************************************         
         SPACE 1                                                                
EXTRATX  NTR1                                                                   
         L     RE,AD1BUFF          CLEAR PRIMARY TX DETAILS BUFFER              
         ST    RE,DETSTART         CURRENT START OF DETAILS LIST                
         L     RF,=A(DSIZE)                                                     
         XR    R0,R0                                                            
         XR    R1,R1                                                            
         MVCL  RE,R0                                                            
         L     R2,AD1BUFF          R2=A(PRIMARY TX DETAILS BUFFER)              
         ST    R2,DETEND           CURRENT END OF DETAILS LIST                  
*                                                                               
         USING TRNRECD,R3                                                       
         LA    R4,TRNRFST                                                       
EXTRA002 CLI   0(R4),0                                                          
         BE    EXTRA008                                                         
         CLI   0(R4),TRNELQ        44 - TRANSACTION DETAILS                     
         BE    EXTRA020                                                         
         CLI   0(R4),MDTELQ        1A - MEDIA TRANSFER DETAILS                  
         BE    EXTRA090                                                         
*&&UK*&& CLI   0(R4),NAMELQ        20 - SUNDRY CREDITOR NAME                    
*&&UK*&& BE    EXTRA100                                                         
*&&UK*&& CLI   0(R4),ADRELQ        22 - SUNDRY CREDITOR ADDRESS                 
*&&UK*&& BE    EXTRA110                                                         
         CLI   0(R4),OTHELQ        23 - SUBREFERENCE                            
         BE    EXTRA120                                                         
         CLI   0(R4),FFNELQ        25 - PURCHASE ORDER DETAILS                  
         BE    EXTRA130                                                         
*&&US*&& CLI   0(R4),PRTELQ        40 - PERSONNEL RATE DETAILS                  
*&&US*&& BE    EXTRA140                                                         
         CLI   0(R4),PXDELQ        4E - POSTING TRANSFER DETAILS                
         BE    EXTRA150                                                         
         CLI   0(R4),CPJELQ        4F - SOURCE ACCOUNT (OLD)                    
         BE    EXTRA160                                                         
         CLI   0(R4),SCIELQ        50 - SUBSIDIARY CASH DETAILS                 
         BE    EXTRA170                                                         
         CLI   0(R4),PCIELQ        51 - PROJECT CONTROL DETAILS                 
         BE    EXTRA180                                                         
*&&US*&& CLI   0(R4),SUTELQ        5F - SALES/USE TAX DETAILS                   
*&&US*&& BE    EXTRA190                                                         
*&&US*&& CLI   0(R4),TRSELQ        60 - TIMESHEET DETAILS                       
*&&US*&& BE    EXTRA200                                                         
*&&UK*&& CLI   0(R4),MPYELQ        64 - MANUAL PAYMENT DETAILS                  
*&&UK*&& BE    EXTRA210                                                         
         CLI   0(R4),ANOELQ        65 - ANALYSIS OFFICE DETAILS                 
         BE    EXTRA220                                                         
*&&UK*&& CLI   0(R4),AFCELQ        7A - FOREIGN CURRENCY DETAILS                
*&&UK*&& BE    EXTRA230                                                         
*&&UK*&& CLI   0(R4),SORELQ        7B - SOURCE ACCOUNT (NEW)                    
*&&UK*&& BE    EXTRA240                                                         
*&&US*&& CLI   0(R4),UNPELQ        7C - UNIT PRICE DETAILS                      
*&&US*&& BE    EXTRA250                                                         
*&&US*&& CLI   0(R4),UFSELQ        A2 - SPECIAL NUMBER DETAILS                  
*&&US*&& BE    EXTRA260                                                         
         CLI   0(R4),FFTELQ        DB - PAYEE NAME OR TAX CODE                  
         BE    EXTRA270                                                         
         B     EXTRA006                                                         
*                                                                               
EXTRA004 L     RF,DETEND           REMOVE TRAILING BLANKS                       
         CR    R2,RF                                                            
         BNH   EXTRA006                                                         
         BCTR  R2,0                                                             
         B     *+10                                                             
         CR    R2,RF                                                            
         BNH   EXTRA006                                                         
         CLI   0(R2),C' '                                                       
         BNE   *+8                                                              
         BCT   R2,*-14                                                          
         MVI   1(R2),C','          SEPERATE DETAILS WITH COMMA                  
         LA    R2,2(R2)                                                         
         ST    R2,DETEND                                                        
EXTRA006 XR    RF,RF               GET NEXT ELEMENT                             
         IC    RF,1(R4)                                                         
         AR    R4,RF                                                            
         B     EXTRA002                                                         
*                                                                               
EXTRA008 C     R2,DETSTART         ANY TX DETAILS?                              
         BNH   EXTRAX                                                           
         L     RE,AD2BUFF          YES - CLEAR SECONDARY BUFFER                 
         L     RF,=A(DSIZE)                                                     
         XR    R0,R0                                                            
         XR    R1,R1                                                            
         MVCL  RE,R0                                                            
         L     RE,AD2BUFF                                                       
         L     RF,DETSTART                                                      
EXTRA010 CLI   0(RF),C','                                                       
         BE    EXTRA014                                                         
EXTRA012 MVC   0(1,RE),0(RF)       TRANSFER DETAILS TO SECONDARY BUFFER         
         LA    RF,1(RF)                                                         
         B     EXTRA016                                                         
EXTRA014 CLI   1(RF),C' '          REMOVE BLANKS AFTER COMMAS                   
         BH    EXTRA012                                                         
         MVC   0(1,RE),0(RF)                                                    
         LA    RF,2(RF)                                                         
EXTRA016 C     RF,DETEND                                                        
         BNL   EXTRA018                                                         
         LA    RE,1(RE)                                                         
         B     EXTRA010                                                         
EXTRA018 ST    RE,DETEND                                                        
         L     RF,AD2BUFF                                                       
         ST    RF,DETSTART                                                      
         B     EXTRAX                                                           
*                                                                               
         USING TRNELD,R4                                                        
EXTRA020 OC    TRNOFFC,TRNOFFC                                                  
         BZ    EXTRA022                                                         
         CLI   SDPROF+3,YES        ARE WE SHOWING ALL ACCOUNTS?                 
         BNE   *+14                                                             
         MVC   PWC1,TRNOFFC        WORK CODE/OFFICE                             
         B     EXTRA022                                                         
         MVC   PWC2,TRNOFFC                                                     
EXTRA022 TM    TRNSTAT,TRNSNOCM    HIGHLIGHT NON-COMMISSIONABLE WCS             
         BZ    EXTRA023                                                         
         MVC   0(L'AC@NCOM,R2),AC@NCOM                                          
         LA    RF,L'AC@NCOM-1(R2)                                               
         CLI   0(RF),C' '                                                       
         BH    *+14                                                             
         CR    RF,R2                                                            
         BNH   EXTRA023                                                         
         BCT   RF,*-14                                                          
         LA    R2,1(RF)                                                         
EXTRA023 MVC   STATUS,TRNSTAT      SAVE STATUS DETAIL FOR TRANSACTION           
         TM    RIND2,R2FITEM+R2NON 1ST TX ON ITEM OR NON-INPUT BATCH?           
         BNZ   *+16                                                             
         CLI   SDPROF+3,YES        NO - ONLY SHOW CONTRA IF TABLE               
         BE    EXTRA042                 FORMAT HAS NO C/A COLUMN                
         B     EXTRA004                                                         
         TM    RIND3,R3GIN         TEST FOR A SPLIT INVOICE                     
         BZ    EXTRA024                                                         
         C     R2,DETEND                                                        
         BE    *+12                                                             
         MVI   0(R2),C','                                                       
         LA    R2,1(R2)                                                         
         MVC   0(5,R2),=C'SPLIT'                                                
         LA    R2,5(R2)                                                         
EXTRA024 TM    TRNSTAT,TRNSURG                                                  
         BZ    EXTRA026                                                         
         C     R2,DETEND                                                        
         BE    *+12                                                             
         MVI   0(R2),C','                                                       
         LA    R2,1(R2)                                                         
         MVC   0(3,R2),=C'*U*'     URGENT TRANSACTION                           
         LA    R2,3(R2)                                                         
*                                                                               
EXTRA026 DS    0H                                                               
*&&UK                                                                           
         CLI   BATTYPE,BT70        TEST FOR AN INVOICE BATCH                    
         BNE   EXTRA028                                                         
         CLI   TRNTYPE,BT01        BILLABLE OR NON-BILLABLE?                    
         BE    EXTRA030                                                         
         B     EXTRA032                                                         
EXTRA028 CLI   BATTYPE,BT71        TEST FOR A CASH BATCH                        
         BNE   EXTRA034                                                         
         CLI   TRNTYPE,BT03        BILLABLE OR NON-BILLABLE?                    
         BE    EXTRA030                                                         
         B     EXTRA032                                                         
EXTRA030 C     R2,DETEND                                                        
         BE    *+12                                                             
         MVI   0(R2),C','                                                       
         LA    R2,1(R2)                                                         
         MVC   0(L'AC@BLB,R2),AC@BLB                                            
         LA    R2,L'AC@BLB(R2)                                                  
         B     EXTRA034                                                         
EXTRA032 C     R2,DETEND                                                        
         BE    *+12                                                             
         MVI   0(R2),C','                                                       
         LA    R2,1(R2)                                                         
         MVC   0(L'AC@NBLB,R2),AC@NBLB                                          
         LA    R2,L'AC@NBLB(R2)                                                 
*&&                                                                             
EXTRA034 XR    RF,RF                                                            
         IC    RF,TRNLN                                                         
         SH    RF,=Y(TRNLN1Q+1)    NARRATIVE LENGTH                             
         BM    EXTRA040                                                         
         EX    RF,*+8              CHECK THERE IS SOMETHING TO DISPLAY          
         B     *+10                                                             
         CLC   TRNNARR(0),SPACES                                                
         BNH   EXTRA040                                                         
         CLI   BATTYPE,BT06        FIXED LENGTH FOR MANUAL BILLS                
         BE    EXTRA036                                                         
         CLI   BATTYPE,BT07        AND CLIENT BILLS                             
         BE    EXTRA036                                                         
         CLI   BATTYPE,BT99        AND WFM POSTINGS                             
         BNE   *+16                                                             
EXTRA036 CH    RF,=H'14'                                                        
         BL    *+8                                                              
         LH    RF,=H'14'                                                        
         C     R2,DETEND                                                        
         BE    *+12                                                             
         MVI   0(R2),C','                                                       
         LA    R2,1(R2)                                                         
         MVC   0(0,R2),TRNNARR                                                  
         EX    RF,*-6                                                           
*&&US                                                                           
         CLI   BATTYPE,BT06        BLANK OUT BILL NO. FOR MANUAL BILLS          
         BNE   EXTRA038                                                         
         MVC   0(0,R2),SPACES                                                   
         EX    RF,*-6                                                           
         BCTR  R2,0                                                             
         MVC   0(1,R2),SPACES      AND COMMA                                    
         B     EXTRA040                                                         
*&&                                                                             
EXTRA038 LA    R2,1(RF,R2)                                                      
EXTRA040 CLI   SDPROF+3,YES        ARE WE SHOWING ALL ACCOUNTS?                 
         BNE   EXTRA044                                                         
EXTRA042 C     R2,DETEND           YES - SHOW CONTRA HERE                       
         BE    *+12                      AND REMOVE LEADING SPACES              
         MVI   0(R2),C','                                                       
         LA    R2,1(R2)                                                         
         MVC   0(L'AC@CTRA1,R2),AC@CTRA1                                        
         LA    R2,L'AC@CTRA1(R2)                                                
         MVI   0(R2),C'='                                                       
         LA    R1,L'TRNKULC                                                     
         MVC   1(L'TRNKULC,R2),TRNKULC                                          
         CLI   1(R2),C' '                                                       
         BH    *+18                                                             
         MVC   1(L'TRNKULC-1,R2),2(R2)                                          
         MVI   L'TRNKULC(R2),C' '                                               
         BCT   R1,*-18                                                          
         LA    R2,L'TRNKULC+1(R2)                                               
         B     EXTRA004                                                         
*                                                                               
EXTRA044 TM    RIND3,R3ENTRY       NO - BUILD TABLE OF ANALYSIS A/CS            
         BO    EXTRA004                 (EXIT IF JOURNAL ENTRY BATCH)           
         TM    RIND3,R3NOACS                                                    
         BO    EXTRA004                 (EXIT IF ONLY ANALYSIS A/CS)            
         BAS   RE,ANALIST                                                       
         BNE   EXTRA004                 (EXIT IF NO ANALYSIS A/CS)              
         C     R2,DETEND                                                        
         BE    *+12                                                             
         MVI   0(R2),C','                                                       
         LA    R2,1(R2)                                                         
         MVC   0(L'AC@ANACS,R2),AC@ANACS                                        
         MVI   L'AC@ANACS(R2),C':'                                              
         LA    R2,L'AC@ANACS+1(R2)                                              
*                                                                               
         L     RE,ASBUFF           RE=A(SUBSIDIARY POSTINGS TABLE)              
         USING STABD,RE                                                         
         LR    RF,R2                                                            
EXTRA046 OC    0(STLENQ,RE),0(RE)  ANYTHING IN THIS SLOT?                       
         BZ    EXTRA050                                                         
         MVC   0(L'STULA+1,R2),SPACES                                           
         LA    R1,L'STULA                                                       
         BCTR  R1,0                                                             
         MVC   0(R1,R2),0(RE)      TRANSFER TO DETAILS BUFFER                   
         EX    R1,*-6                                                           
         AR    R2,R1                                                            
*                                                                               
         CLI   0(R2),C' '          REMOVE TRAILING BLANKS                       
         BNE   *+14                                                             
         CR    R2,RF                                                            
         BNH   EXTRA048                                                         
         BCT   R2,*-14                                                          
*                                                                               
         LA    R2,2(R2)                                                         
         LR    RF,R2                                                            
EXTRA048 LA    RE,STLENQ(RE)                                                    
         B     EXTRA046                                                         
         DROP  RE                                                               
*                                                                               
EXTRA050 BCTR  R2,0                                                             
         B     EXTRA004                                                         
*                                                                               
         USING MDTELD,R4                                                        
EXTRA090 MVC   WORK,SPACES                                                      
         LA    RF,WORK                                                          
*&&US                                                                           
         OC    MDTGRS,MDTGRS       BILLING (GROSS)                              
         BZ    EXTRA091                                                         
         MVC   0(7,RF),=C'ABASIS='                                              
         CURED MDTGRS,(10,0(RF)),2,MINUS=YES,ALIGN=LEFT                         
         LA    RF,WORK+7                                                        
         AR    RF,R0                                                            
         OC    MDTCLI,MDTCLI                                                    
         BZ    EXTRASQ                                                          
         B     *+14                                                             
*&&                                                                             
EXTRA091 OC    MDTCLI,MDTCLI                                                    
         BZ    EXTRA006                                                         
         MVC   0(L'AC@CLI,RF),AC@CLI                                            
         MVI   L'AC@CLI(RF),C'='                                                
         MVC   L'AC@CLI+1(L'MDTCLI,RF),MDTCLI                                   
         LA    RF,L'AC@CLI+L'MDTCLI+2(RF)                                       
*                                                                               
         OC    MDTPRD,MDTPRD                                                    
         BZ    EXTRA092                                                         
         MVC   0(L'AC@PRO,RF),AC@PRO                                            
         MVI   L'AC@PRO(RF),C'='                                                
         MVC   L'AC@PRO+1(L'MDTPRD,RF),MDTPRD                                   
         LA    RF,L'AC@PRO+L'MDTPRD+2(RF)                                       
*                                                                               
         OC    MDTJOB,MDTJOB                                                    
         BZ    EXTRA092                                                         
         LA    R1,AC@JOEST                                                      
         LA    RE,L'AC@JOEST-1                                                  
         CLI   BATTYPE,BT06                                                     
         BE    *+12                                                             
         CLI   BATTYPE,BT51                                                     
         BNE   *+12                                                             
         LA    R1,AC@EST                                                        
         LA    RE,L'AC@EST-1                                                    
         MVC   0(0,RF),0(R1)                                                    
         EX    RE,*-6                                                           
         LA    RF,1(RE,RF)                                                      
         MVI   0(RF),C'='                                                       
         MVC   1(L'MDTJOB,RF),MDTJOB                                            
         LA    RF,L'AC@JOEST+L'MDTJOB+2(RF)                                     
*                                                                               
*XTRA092 CLI   MDTMOS,X'99'                                                     
*        BH    EXTRASQ                                                          
EXTRA092 MVC   0(L'AC@MOS,RF),AC@MOS                                            
         MVI   L'AC@MOS(RF),C'='                                                
         LA    RF,L'AC@MOS+1(RF)                                                
         MVC   DUB,MDTMOS                                                       
         MVI   DUB+L'MDTMOS,1                                                   
         GOTO1 DATCON,DMCB,(1,DUB),(6,(RF))                                     
         B     EXTRASQ                                                          
*&&UK                                                                           
         USING NAMELD,R4                                                        
EXTRA100 TM    RIND3,R3SUNDRY      SUNDRY CREDITOR NAME                         
         BZ    EXTRA006                                                         
         MVC   0(L'AC@CROR,R2),AC@CROR                                          
         MVI   L'AC@CROR(R2),C'='                                               
         LA    R2,L'AC@CROR+1(R2)                                               
         XR    RF,RF                                                            
         IC    RF,NAMLN                                                         
         SH    RF,=Y(NAMLN1Q+1)                                                 
         LTR   RF,RF                                                            
         BM    EXTRA006                                                         
         MVC   0(0,R2),NAMEREC                                                  
         EX    RF,*-6                                                           
         LA    R2,1(RF,R2)                                                      
         B     EXTRA004                                                         
*                                                                               
         USING ADRELD,R4                                                        
EXTRA110 TM    RIND3,R3SUNDRY      SUNDRY CREDITOR ADDRESS                      
         BZ    EXTRA006                                                         
         XR    R1,R1                                                            
         IC    R1,ADRNUM                                                        
         LTR   R1,R1                                                            
         BZ    EXTRA006                                                         
         LA    RE,ADRADD1                                                       
EXTRA112 MVC   0(L'ADRADD1,R2),0(RE)                                            
         LR    R0,R2                                                            
         LA    R2,L'ADRADD1-1(R2)                                               
         B     *+10                                                             
EXTRA114 CR    R0,R2                                                            
         BNL   EXTRA116                                                         
         CLI   0(R2),C' '                                                       
         BH    *+8                                                              
         BCT   R2,EXTRA114                                                      
         MVI   1(R2),C','                                                       
         LA    R2,2(R2)                                                         
         LA    RE,L'ADRADD1(RE)                                                 
EXTRA116 BCT   R1,EXTRA112                                                      
         BCTR  R2,0                                                             
         MVI   0(R2),C' '                                                       
         B     EXTRA004                                                         
*&&                                                                             
         USING OTHELD,R4                                                        
EXTRA120 CLI   BATTYPE,BT07        SUPRESS SUBREF FOR CREATIVE BILLING          
         BE    EXTRA006                                                         
         CLI   BATTYPE,BT26        AND FOR MEDIA/SPEC BILL                      
         BE    EXTRA006                                                         
         CLI   BATTYPE,BT33        AND FOR BILLABLE TIME BATHCES                
         BE    EXTRA006                                                         
         CLI   BATTYPE,BT51        AND FOR INTER AGENCY                         
         BE    EXTRA006                                                         
         CLI   BATTYPE,BT55        AND FOR INCOME ACCRUAL                       
         BE    EXTRA006                                                         
*&&US                                                                           
         CLI   BATTYPE,BT03        AND FOR CHEQUES IF SAME AS MAIN REF          
         BNE   *+14                                                             
         CLC   TRNKREF,OTHNUM                                                   
         BE    EXTRA006                                                         
*                                                                               
         MVC   WORK,SPACES                                                      
         LA    RF,WORK                                                          
         CLC   PRODUNIT,TRNKULC    TEST FOR PRODUCTION LEDGER                   
         BNE   EXTRA006                                                         
         XR    RE,RE                                                            
         MVC   0(L'AC@CLI,RF),AC@CLI                                            
         MVI   L'AC@CLI(RF),C'='                                                
         IC    RE,PRODLENA                                                      
         BCTR  RE,0                                                             
         MVC   L'AC@CLI+1(0,RF),TRNKCACT                                        
         EX    RE,*-6                                                           
         LA    RF,L'AC@CLI+3(RE,RF)                                             
         LA    R1,OTHNUM                                                        
         IC    RE,PRODLENB                                                      
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R1),SPACES                                                   
         BNH   EXTRASQ                                                          
         MVC   0(L'AC@PRO,RF),AC@PRO                                            
         MVI   L'AC@PRO(RF),C'='                                                
         MVC   L'AC@PRO+1(0,RF),0(R1)                                           
         EX    RE,*-6                                                           
         LA    RF,L'AC@PRO+3(RE,RF)                                             
         IC    RE,PRODLENC                                                      
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         CLC   6(0,R1),SPACES                                                   
         BNH   EXTRASQ                                                          
         MVC   0(L'AC@JOB,RF),AC@JOB                                            
         MVI   L'AC@JOB(RF),C'='                                                
         MVC   L'AC@JOB+1(0,RF),6(R1)                                           
         EX    RE,*-6                                                           
         B     EXTRASQ                                                          
*&&                                                                             
*&&UK                                                                           
         TM    RIND2,R2FITEM       SHOW SUBREF ON FIRST TX ONLY                 
         BZ    EXTRA006                                                         
         MVC   0(L'AC@SUBR,R2),AC@SUBR                                          
         MVI   L'AC@SUBR(R2),C'='                                               
         MVC   L'AC@SUBR+1(L'OTHNUM-2,R2),OTHNUM                                
         LA    R2,L'OTHNUM+L'AC@SUBR-1(R2)                                      
         B     EXTRA004                                                         
*&&                                                                             
         USING FFNELD,R4                                                        
*&&UK                                                                           
EXTRA130 MVC   0(L'AC@ORDER,R2),AC@ORDER                                        
         MVI   L'AC@ORDER(R2),C'='                                              
         MVC   L'AC@ORDER+1(L'FFNONUM,R2),FFNONUM                               
         LA    R2,L'FFNONUM+L'AC@ORDER+1(R2)                                    
*&&                                                                             
*&&US                                                                           
EXTRA130 MVC   0(4,R2),=C'PO#='                                                 
         MVC   4(L'FFNONUM,R2),FFNONUM  ORDER NUMBER                            
         LA    R2,L'FFNONUM+4(R2)                                               
*&&                                                                             
         CLI   FFNSTAT,FFNSPRTQ                                                 
         BNE   EXTRA004                                                         
         MVI   0(R2),FFNSPRTQ                                                   
         LA    R2,1(R2)                                                         
         B     EXTRA004                                                         
*&&US                                                                           
         USING PRTELD,R4                                                        
EXTRA140 MVC   0(L'AC@TIME,R2),AC@TIME                                          
         LA    R2,L'AC@TIME(R2)                                                 
         MVC   0(2,R2),=C'=B'      BILLABLE                                     
         TM    PRTSTAT,PRTSNOTQ    NON-BILLABLE                                 
         BZ    *+8                                                              
         MVI   1(R2),C'N'                                                       
         TM    PRTSTAT,PRTSRTEQ    SPECIAL NON-BILLABLE                         
         BZ    *+8                                                              
         MVI   1(R2),C'R'                                                       
         TM    PRTSTAT,PRTSNOTQ    SKIP RATE IF NON-BILLABLE                    
         BO    *+12                                                             
         CLI   SDPROF+4,YES        PRINT RATE ?                                 
         BE    *+12                                                             
         LA    R2,2(R2)                                                         
         B     EXTRA004                                                         
*                                                                               
         MVC   3(1,R2),AC@RATE                                                  
         MVI   4(R2),C'='                                                       
         LA    R2,5(R2)                                                         
         CURED PRTRATE,(8,(R2)),2,MINUS=YES,ALIGN=LEFT                          
         AR    R2,R0                                                            
         TM    PRTSTAT,X'01'       WAS RATE ADJUSTED ?                          
         BZ    EXTRA004            NO                                           
         MVI   0(R2),C'A'                                                       
         B     EXTRA004                                                         
*&&                                                                             
         USING PXDELD,R4                                                        
EXTRA150 MVC   WORK,SPACES                                                      
         LA    RF,WORK                                                          
         MVC   0(L'AC@TO,RF),AC@TO                                              
         LA    RE,L'AC@TO(RF)                                                   
         CLI   PXDTYPE,PXDTTO                                                   
         BE    EXTRA155                                                         
         MVC   0(L'AC@FROM,RF),AC@FROM                                          
         LA    RE,L'AC@FROM(RF)                                                 
         CLI   PXDTYPE,PXDTFROM                                                 
         BE    EXTRA155                                                         
         MVC   WORK,SPACES                                                      
         B     EXTRA006                                                         
*                                                                               
EXTRA155 MVC   1(L'PXDFRTOA,RE),PXDFRTOA                                        
         B     EXTRASQ                                                          
*                                                                               
         USING CPJELD,R4                                                        
EXTRA160 MVC   WORK,SPACES                                                      
         LA    RF,WORK                                                          
         MVI   0(RF),C'S'          SOURCE ACCOUNT (OLD)                         
         MVC   1(L'CPJTYPE,RF),CPJTYPE                                          
         CLI   CPJTYPE,CPJTOTH     CHANGE TYPE 'OTHER' FROM 'X' TO 'O'          
         BNE   *+8                                                              
         MVI   1(RF),C'O'                                                       
         MVI   1+L'CPJTYPE(RF),C'='                                             
         LA    RF,L'CPJTYPE+2(RF)                                               
         XR    R1,R1                                                            
         IC    R1,CPJLN                                                         
         SH    R1,=Y(CPJDATA-CPJELD+1)                                          
         MVC   0(0,RF),CPJCLI                                                   
         EX    R1,*-6                                                           
         CLI   CPJTYPE,CPJTJOB                                                  
         BNE   EXTRA162                                                         
         IC    R1,CPJLN                                                         
         SH    R1,=Y(CPJWRK-CPJELD)                                             
         BNP   EXTRA162                                                         
         CLC   CPJWRK,SPACES                                                    
         BNH   EXTRA162                                                         
         MVC   CPJCPJLQ(L'CPJWRK,RF),SPACES                                     
         MVC   CPJCPJLQ+1(L'CPJWRK,RF),CPJWRK                                   
EXTRA162 EQU   *                                                                
*&&US                                                                           
         CLI   CPJTYPE,CPJTCOM     IF COMMERCIAL                                
         BNE   EXTRASQ                                                          
         CLI   CPJCOMU,C'T'        AND UNIT=T,SHOW DPS COMPANY CODE             
         BNE   EXTRASQ                                                          
         GOTO1 HEXOUT,DMCB,CPJCOML,CPJCOML-CPJDATA(RF),1                        
*&&                                                                             
         B     EXTRASQ                                                          
*                                                                               
         USING SCIELD,R4                                                        
EXTRA170 DS    0H                                                               
*&&US                                                                           
         TM    RIND3,R3TIME        IS THIS A TIMESHEET BATCH?                   
         BZ    EXTRA172            NO                                           
         CLI   SCITYPE,SCITHOUR    YES - DON'T SHOW HOURS HERE                  
         BE    EXTRA004                                                         
*&&                                                                             
EXTRA172 MVC   0(L'SCITYPE,R2),SCITYPE  SUBSIDIARY CASH                         
         MVI   L'SCITYPE(R2),C'='                                               
*&&UK*&& LA    R2,L'SCITYPE+1(R2)                                               
*&&US                                                                           
         CLI   SCITYPE,SCITCDSC                                                 
         BE    *+12                                                             
         LA    R2,L'SCITYPE+1(R2)                                               
         B     EXTRA174                                                         
         MVC   0(L'AC@CSHDS,R2),AC@CSHDS                                        
         MVI   L'AC@CSHDS(R2),C'='                                              
         LA    R2,L'AC@CSHDS+1(R2)                                              
*                                                                               
EXTRA174 DS    0H                                                               
*&&                                                                             
*&&UK                                                                           
         CLI   SCITYPE,SCITMILE    MILEAGE STORED IN WHOLE UNITS                
         BNE   *+10                                                             
         SRP   SCIAMNT,2,0                                                      
*&&                                                                             
         CURED SCIAMNT,(13,0(R2)),2,MINUS=YES,ALIGN=LEFT                        
         AR    R2,R0                                                            
         B     EXTRA004                                                         
*                                                                               
         USING PCIELD,R4                                                        
EXTRA180 CLI   PCILN,PCILN2Q                                                    
         BL    EXTRA006                                                         
         XR    RE,RE                                                            
         MVC   WORK,SPACES                                                      
         LA    RF,WORK                                                          
*                                                                               
         IC    RE,PRODLENA                                                      
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         CLC   PCIPRJT+3(0),SPACES                                              
         BNH   EXTRA006                                                         
         MVC   0(L'AC@CLI,RF),AC@CLI                                            
         MVI   L'AC@CLI(RF),C'='                                                
         MVC   L'AC@CLI+1(0,RF),PCIPRJT+3                                       
         EX    RE,*-6                                                           
         LA    RF,L'AC@CLI+3(RE,RF)                                             
*                                                                               
         IC    RE,PRODLENB                                                      
         BCTR  RE,0                                                             
         CLC   PCIPRJT+6(0),SPACES                                              
         EX    RE,*-6                                                           
         BNH   EXTRASQ                                                          
         MVC   0(L'AC@PRO,RF),AC@PRO                                            
         MVI   L'AC@PRO(RF),C'='                                                
         MVC   L'AC@PRO+1(0,RF),PCIPRJT+6                                       
         EX    RE,*-6                                                           
         LA    RF,L'AC@PRO+3(RE,RF)                                             
*                                                                               
         IC    RE,PRODLENC                                                      
         BCTR  RE,0                                                             
         CLC   PCIPRJT+9(0),SPACES                                              
         EX    RE,*-6                                                           
         BNH   EXTRASQ                                                          
         MVC   0(L'AC@JOB,RF),AC@JOB                                            
         MVI   L'AC@JOB(RF),C'='                                                
         MVC   L'AC@JOB+1(0,RF),PCIPRJT+9                                       
         EX    RE,*-6                                                           
         LA    RF,L'AC@JOB+3(RE,RF)                                             
*                                                                               
         CLC   PCITSK,SPACES                                                    
         BNH   EXTRASQ                                                          
         MVC   0(L'AC@TASK,RF),AC@TASK                                          
         MVI   L'AC@TASK(RF),C'='                                               
         MVC   L'AC@TASK+1(L'PCITSK,RF),PCITSK                                  
         B     EXTRASQ                                                          
*                                                                               
         USING SUTELD,R4                                                        
EXTRA190 TM    STATUS,TRNSDR       CREDITS ONLY                                 
         BO    EXTRA006                                                         
         MVC   0(L'AC@BASIS,R2),AC@BASIS                                        
         LA    R2,L'AC@BASIS(R2)                                                
         MVI   0(R2),C'='                                                       
         CURED SUTBAS,(10,1(R2)),2,MINUS=YES,ALIGN=LEFT                         
         AR    R2,R0                                                            
         MVC   2(L'AC@RATE,(R2)),AC@RATE                                        
         LA    R2,L'AC@RATE+2(R2)                                               
         MVI   0(R2),C'='                                                       
         CURED SUTRTE,(8,1(R2)),4,ALIGN=LEFT                                    
         AR    R2,R0                                                            
         MVC   2(L'AC@LOC,(R2)),AC@LOC                                          
         MVI   L'AC@LOC+2(R2),C'='                                              
         MVC   L'AC@LOC+3(8,(R2)),SUTLOC+3                                      
         LA    R2,L'AC@LOC+11(R2)                                               
         B     EXTRA004                                                         
*&&US                                                                           
         USING TRSELD,R4                                                        
EXTRA200 TM    TRSSTAT2,TRSSTIME+TRSSTADJ+TRSSTMSS                              
         BZ    EXTRA006                                                         
         MVC   0(L'AC@TMSTT,R2),AC@TMSTT                                        
         LA    R2,L'AC@TMSTT(R2)                                                
         MVC   0(2,R2),=C'=T'      REGULAR                                      
         TM    TRSSTAT2,TRSSTADJ   ADJUSTMENT                                   
         BZ    *+8                                                              
         MVI   1(R2),C'A'                                                       
         TM    TRSSTAT2,TRSSTMSS   MISSING                                      
         BZ    *+8                                                              
         MVI   1(R2),C'M'                                                       
         LA    R2,2(R2)                                                         
         B     EXTRA004                                                         
*&&                                                                             
*&&UK                                                                           
         USING MPYELD,R4                                                        
EXTRA210 CLI   BATTYPE,BT70        INVOICE BATCHES ONLY                         
         BNE   EXTRA006                                                         
         CLC   MPYBNK,SPACES                                                    
         BNH   EXTRA006                                                         
         MVC   0(L'AC@CHK,R2),AC@CHK  CHEQUE NUMBER/DATE                        
         MVI   L'AC@CHK(R2),C'='                                                
         MVC   L'AC@CHK+1(L'MPYNO,R2),MPYNO                                     
         LA    R2,L'AC@CHK+L'MPYNO+1(R2)                                        
         MVI   0(R2),C'/'                                                       
         GOTO1 DATCON,DMCB,(X'82',MPYDTE),(17,1(R2))                            
         XR    RF,RF                                                            
         IC    RF,4(R1)                                                         
         LA    R2,1(RF,R2)                                                      
         B     EXTRA004                                                         
*&&                                                                             
         USING ANOELD,R4                                                        
EXTRA220 CLI   BATTYPE,BT21        EXPENSE TRANSFERS                            
         BE    *+12                                                             
         CLI   BATTYPE,BT22        CASH RECEIPTS                                
         BNE   EXTRA006                                                         
         MVC   0(L'AC@ANLC,R2),AC@ANLC  ANALYSIS CODE                           
         LA    R2,L'AC@ANLC(R2)                                                 
         MVI   0(R2),C'='                                                       
         MVC   1(L'ANOOFFC,R2),ANOOFFC                                          
         LA    R2,L'ANOOFFC+1(R2)                                               
         B     EXTRA004                                                         
*&&UK                                                                           
         USING AFCELD,R4                                                        
EXTRA230 GOTO1 VBLDCUR,DMCB,AFCCURR,(X'00',WORK),ADCOMFAC                       
         CLI   0(R1),0                                                          
         BNE   EXTRA006                                                         
         MVC   0(L'AC@CURRY,R2),AC@CURRY  CURRENCY                              
         LA    R2,L'AC@CURRY(R2)                                                
         MVI   0(R2),C'='                                                       
         CURED AFCAMNT,(15,1(R2)),WORK,MINUS=YES,ALIGN=LEFT,CURSYMB=YES         
         AR    R2,R0                                                            
         LA    R2,1(R2)                                                         
         TM    RIND2,R2FITEM       FIRST POSTING ONLY                           
         BZ    EXTRA004                                                         
         MVC   1(2,R2),=C'ER'      EXCHANGE RATE                                
         LA    R2,3(R2)                                                         
         MVI   0(R2),C'='                                                       
         XC    DUB,DUB                                                          
         MVO   DUB,AFCXRATE                                                     
         OI    DUB+L'DUB-1,X'0C'   CONVERT TO PACKED                            
         CURED (P8,DUB),(10,1(R2)),5,ALIGN=LEFT                                 
         AR    R2,R0                                                            
         LA    R2,1(R2)                                                         
         LA    RF,10(R2)                                                        
EXTRA232 CLI   0(RF),C','          DROP TRAILING ZEROS                          
         BE    *+8                                                              
         CLI   0(RF),C'.'                                                       
         BNE   *+12                                                             
         MVI   1(RF),C'0'                                                       
         B     EXTRA004                                                         
         CLI   0(RF),C'0'                                                       
         BH    EXTRA004                                                         
         MVI   0(RF),C' '                                                       
         BCT   RF,EXTRA232                                                      
         B     EXTRA004                                                         
*                                                                               
         USING SORELD,R4                                                        
EXTRA240 CLI   SORSYS,SORSACC      SOURCE A/C (NEW)                             
         BNE   EXTRA006                                                         
         CLC   =C'SJ',SORAUNT                                                   
         BE    EXTRA242                                                         
         CLC   =C'SE',SORAUNT                                                   
         BE    EXTRA242                                                         
         MVC   0(2,R2),=C'SO'                                                   
         MVI   2(R2),C'='                                                       
         MVC   3(L'SORAULA,R2),SORAULA                                          
         LA    R2,3+L'SORAULA(R2)                                               
         B     EXTRA004                                                         
EXTRA242 MVC   0(L'SORAUNT+L'SORALDG,R2),SORAUNT                                
         MVI   L'SORAUNT+L'SORALDG(R2),C'='                                     
         MVC   L'SORAUNT+L'SORALDG+1(L'SORAACT,R2),SORAACT                      
         LA    R2,L'SORAUNT+L'SORALDG+L'SORAACT+1(R2)                           
         B     EXTRA004                                                         
*&&                                                                             
*&&US                                                                           
         USING UNPELD,R4                                                        
EXTRA250 MVC   0(2,R2),=C'U='    UNIT PRICING                                   
         CURED UNPUNIT,(6,2(R2)),0,MINUS=YES,ALIGN=LEFT                         
         ORG   *-2                                                              
         TM    UNPSTAT,UNPSQTRH                                                 
         BZ    EXTRA255                                                         
         MVI   11(R1),2                                                         
         MVC   0(2,R2),=C'H='                                                   
*                                                                               
EXTRA255 BASR  RE,RF                                                            
         AR    R2,R0                                                            
         LA    R2,3(R2)                                                         
         MVC   0(2,R2),=C'P='                                                   
         CURED UNPRICE,(8,2(R2)),2,MINUS=YES,ALIGN=LEFT                         
         AR    R2,R0                                                            
         LA    R2,2(R2)                                                         
         B     EXTRA004                                                         
*                                                                               
         USING UFSELD,R4                                                        
EXTRA260 CLC   =C'SN',UFSCODE    "SPECIAL NUMBER" INDICATOR                     
         BE    *+14                                                             
         CLC   =C'E2',UFSCODE    AOR                                            
         BNE   EXTRA006                                                         
         XR    R1,R1                                                            
         IC    R1,UFSLN                                                         
         SH    R1,=Y(UFSLN1Q+1)                                                 
         BM    EXTRA006                                                         
         MVC   0(L'UFSCODE,R2),UFSCODE                                          
         MVI   L'UFSCODE(R2),C'='                                               
         MVC   L'UFSCODE+1(0,R2),UFSDATA                                        
         EX    R1,*-6                                                           
         LA    R2,L'UFSCODE+2(R1,R2)                                            
         B     EXTRA004                                                         
*&&                                                                             
*                                                                               
         USING FFTELD,R4                                                        
EXTRA270 CLI   FFTTYPE,FFTTINVN    SUPPLIER INVOICE NUMBER                      
*&&UK*&& BNE   EXTRA272                                                         
*&&US*&& BNE   EXTRA006                                                         
         XR    RF,RF                                                            
         ICM   RF,1,FFTDLEN                                                     
         BZ    EXTRA006                                                         
         MVC   0(L'AC@INV,R2),AC@INV                                            
         MVI   L'AC@INV(R2),C'='                                                
         LA    R2,L'AC@INV+1(R2)                                                
         B     EXTRA276                                                         
*&&UK                                                                           
EXTRA272 CLI   FFTTYPE,FFTTTAXI    TAX CODE (DUTCH SE ACCOUNTS)                 
         BNE   EXTRA274                                                         
         XR    RF,RF                                                            
         ICM   RF,1,FFTDLEN                                                     
         BZ    EXTRA006                                                         
         MVC   0(L'AC@VPB,R2),AC@VPB                                            
         MVI   L'AC@VPB(R2),C'='                                                
         LA    R2,L'AC@VPB+1(R2)                                                
         B     EXTRA276                                                         
EXTRA274 CLI   BATTYPE,BT71        CASH BATCHES ONLY                            
         BNE   EXTRA006                                                         
         TM    RIND2,R2FITEM       FIRST POSTING ONLY                           
         BZ    EXTRA006                                                         
         CLI   FFTTYPE,FFTTPNAM    PAYEE NAME                                   
         BNE   EXTRA006                                                         
         XR    RF,RF                                                            
         ICM   RF,1,FFTDLEN                                                     
         BZ    EXTRA006                                                         
         MVC   0(L'AC@PAYEE,R2),AC@PAYEE                                        
         MVI   L'AC@PAYEE(R2),C'='                                              
         LA    R2,L'AC@PAYEE+1(R2)                                              
*&&                                                                             
EXTRA276 BCTR  RF,0                                                             
         MVC   0(0,R2),FFTDATA                                                  
         EX    RF,*-6                                                           
         LA    R2,1(RF,R2)                                                      
         B     EXTRA004                                                         
*                                                                               
EXTRASQ  GOTO1 ADSQUASH,DMCB,WORK,L'WORK                                        
         XR    RF,RF                                                            
         IC    RF,7(R1)                                                         
         BCTR  RF,0                                                             
         MVC   0(0,R2),WORK                                                     
         EX    RF,*-6                                                           
         LA    R2,1(RF,R2)                                                      
         B     EXTRA004                                                         
*                                                                               
EXTRAX   B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* BUILD TABLE OF ANALYSIS ACCOUNTS AND SUPPLEMENTARY ACCOUNT DETAILS  *         
*                                                                     *         
* NTRY - R2=A(BATCH ITEM RECORD)                                      *         
* EXIT - CC SET EQUAL/ENTRIES,NOT EQUAL/NO ENTRIES                    *         
***********************************************************************         
         SPACE 1                                                                
ANALIST  NTR1                                                                   
         L     RE,ASBUFF           CLEAR SUBSIDIARY POSTINGS BUFFER             
         ST    RE,TABLE                                                         
         L     RF,=A(SSIZE)                                                     
         XR    R0,R0                                                            
         XR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         L     R2,ABITREC                                                       
         USING TBARECD,R2                                                       
         LA    R2,TBARFST                                                       
         USING ASKELD,R2                                                        
ANAL02   CLI   0(R2),0                                                          
         BE    ANALX                                                            
         CLI   0(R2),ASKELQ        KEY ELEMENT                                  
         BNE   ANAL14                                                           
*                                                                               
         LA    R3,ASKKEY           RE=A(TRANSACTION RECORD KEY)                 
         USING TRNRECD,R3                                                       
         LA    R1,ANAUNIT          TEST FOR ANALYSIS ACCOUNT                    
ANAL04   CLI   0(R1),0                                                          
         BE    ANAL14                                                           
         CLC   0(1,R1),TRNKUNT                                                  
         BE    *+12                                                             
         LA    R1,1(R1)                                                         
         B     ANAL04                                                           
*                                                                               
         TM    RIND3,R3TIME        DON'T INCLUDE RECEIVABLE A/CS ON             
         BZ    *+14                TIMESHEET BATCHES                            
         CLC   =C'1R',TRNKUNT                                                   
         BE    ANAL14                                                           
*                                                                               
         MVC   WORK(L'STULA),TRNKULA                                            
ANAL06   L     R4,TABLE            R4=A(SUBSIDIARY POSTINGS TABLE)              
         USING STABD,R4                                                         
ANAL08   OC    0(STLENQ,R4),0(R4)  ANYTHING IN THIS SLOT?                       
         BZ    ANAL10                                                           
         CLC   0(L'STULA,R4),WORK  IS IT ALREADY AN ENTRY?                      
         BE    ANAL12                                                           
         LA    R4,STLENQ(R4)                                                    
         B     ANAL08                                                           
ANAL10   MVC   0(L'STULA,R4),WORK  NO - ADD IT                                  
         LA    R4,STLENQ(R4)            BUMP ALONG BUFFER                       
ANAL12   CLC   TRNKULA,WORK             AND THEN ADD CONTRA                     
         BNE   ANAL14                   UNLESS THIS WAS THE CONTRA              
         CLC   =C'2P',TRNKULA                                                   
         BE    ANAL14                                                           
         CLC   =C'29',TRNKULA                                                   
         BE    ANAL14                                                           
         MVC   WORK(L'STULA),TRNKULC                                            
         B     ANAL06                                                           
*                                                                               
ANAL14   XR    R0,R0               NEXT ELEMENT                                 
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     ANAL02                                                           
*                                                                               
ANALX    L     R4,TABLE                                                         
         OC    0(STLENQ,R4),0(R4)  ANYTHING IN TABLE?                           
         BZ    *+10                                                             
         CR    RE,RE               YES - CC SET EQUAL                           
         B     XIT                                                              
         LTR   RE,RE               NO  - CC SET NOT EQUAL                       
         B     XIT                                                              
         DROP  R2,R3,R4                                                         
         EJECT                                                                  
***********************************************************************         
* CHECK ITEM FOR ACCOUNT TRANSACTIONS                                 *         
***********************************************************************         
         SPACE 1                                                                
ANASCAN  ST    RE,RETURN1                                                       
         OI    RIND3,R3NOACS                                                    
         XR    RF,RF                                                            
         LR    R0,R2                                                            
ANAS02   CLI   0(R2),0                                                          
         BE    ANASX                                                            
         CLI   0(R2),GINELQ                                                     
         BE    ANAS08                                                           
         CLI   0(R2),ASKELQ                                                     
         BE    *+14                                                             
ANAS04   IC    RF,1(R2)                                                         
         AR    R2,RF                                                            
         B     ANAS02                                                           
*                                                                               
         USING ASKELD,R2                                                        
         LA    R1,ANAUNIT                                                       
ANAS06   CLI   0(R1),0                                                          
         BE    ANAS08                                                           
         CLC   0(1,R1),ASKKEY+1                                                 
         BE    ANAS04                                                           
         LA    R1,1(R1)                                                         
         B     ANAS06                                                           
*                                                                               
ANAS08   NI    RIND3,X'FF'-R3NOACS                                              
*                                                                               
ANASX    LR    R2,R0                                                            
         L     RE,RETURN1                                                       
         BR    RE                                                               
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* BATCH TOTALS ROUTINE                                                *         
***********************************************************************         
         SPACE 1                                                                
BATTOTS  ST    RE,RETURN1                                                       
         GOTO1 PAGER,TOTS          PREDEFINE PAGE/BOXES FOR NEXT PASS           
*                                                                               
         LA    R4,DTBACD                                                        
         LA    R2,PTOT1                                                         
         CLI   SDPROF+3,YES                                                     
         BE    *+8                                                              
         LA    R2,PTOT3                                                         
         MVC   0(L'PTOT1,R2),SPACES                                             
         CLI   RCSUBPRG,2                                                       
         BNE   *+14                                                             
         MVC   0(L'AC@TOTLS,R2),AC@TOTLS  TOTALS                                
         B     BATT02                                                           
         CLI   RCSUBPRG,1                                                       
         BE    BATT00                                                           
         TM    RIND3,R3TIME                                                     
         BZ    BATT00                                                           
         MVC   0(L'AC@BATS1,R2),AC@BATS1  B/TOT                                 
         B     *+10                                                             
BATT00   MVC   0(L'AC@BATS2,R2),AC@BATS2  BATCH TOTALS                          
*                                                                               
         CLI   RCSUBPRG,0                                                       
         BNE   BATT01                                                           
         CURED DTBAND,(L'PAND,PAND),2,MINUS=YES  ANALYSIS DEBITS                
         CURED DTBANC,(L'PANC,PANC),2,MINUS=YES  ANALYSIS CREDITS               
         TM    RIND3,R3TIME                                                     
         BZ    BATT02                                                           
         CURED DTBHRS,(L'PHRS1,PHRS1),2,MINUS=YES  HOURS                        
         B     BATT02                                                           
BATT01   TM    RIND3,R3TIME                                                     
         BZ    BATT02                                                           
         CURED DTBHRS,(L'PHRS2,PHRS2),2,MINUS=YES  HOURS                        
*                                                                               
BATT02   MVC   DUB,0(R4)                                                        
         CURED DUB,(L'PACD,PACD),2,MINUS=YES  A/C DRS                           
         LA    R4,L'TOTALS(R4)                                                  
         MVC   DUB,0(R4)                                                        
         CURED DUB,(L'PACC,PACC),2,MINUS=YES  A/C CRS                           
*                                                                               
BATT04   CLI   RCSUBPRG,0                                                       
         BNE   *+12                                                             
         TM    RIND3,R3TIME                                                     
         BO    BATT06                                                           
         LA    R4,L'TOTALS(R4)                                                  
         BAS   RE,BATTRA                                                        
         CLI   RCSUBPRG,2          NO DELETED TOTALS ON W/FILE DETAIL           
         BE    BATT08                                                           
         CLI   SDPROF+5,NO         AND ONLY IF REQUESTED ON STANDARD            
         BE    BATT08                                                           
         LA    RF,DTCITM                                                        
         CR    R4,RF                                                            
         BE    BATT08                                                           
         LA    R4,L'TOTALS(R4)                                                  
         OC    0(L'TOTALS,R4),0(R4)                                             
         BZ    BATT08                                                           
         GOTO1 ACREPORT                                                         
         MVC   0(L'AC@DELD1,R2),AC@DELD1                                        
         BAS   RE,BATTRA                                                        
         B     BATT08                                                           
*                                                                               
BATT06   LA    R4,L'TOTALS(R4)                                                  
         BAS   RE,BATTRB                                                        
         CLI   SDPROF+5,YES        SHOW DELETED ITEMS TOTAL                     
         BNE   BATT08                                                           
         LA    RF,DTCITM                                                        
         CR    R4,RF                                                            
         BE    BATT08                                                           
         LA    R4,L'TOTALS(R4)                                                  
         OC    0(L'TOTALS,R4),0(R4)                                             
         BZ    BATT08                                                           
         GOTO1 ACREPORT                                                         
         MVC   0(L'AC@DELD2,R2),AC@DELD2                                        
         BAS   RE,BATTRB                                                        
         B     BATT08                                                           
*                                                                               
BATT08   GOTO1 ACREPORT                                                         
*                                                                               
         CLI   RCSUBPRG,2          NO CONTROL TOTALS ON W/FILE DETAILS          
         BE    BATTOTX                                                          
         CLI   SDPROF+5,NO         AND ONLY IF REQUESTED ON STANDARD            
         BE    BATTOTX                                                          
         LA    RF,DTCITM                                                        
         CR    R4,RF                                                            
         BE    BATTOTX                                                          
         LA    R4,DTCACD                                                        
         TM    RIND3,R3TIME                                                     
         BO    BATT12                                                           
BATT10   MVC   0(L'AC@CNTS2,R2),AC@CNTS2  CONTROL TOTALS                        
         B     BATT02                                                           
*                                                                               
BATT12   MVC   0(L'PTOT1,R2),SPACES                                             
         MVC   DUB,0(R4)                                                        
         CLI   RCSUBPRG,1                                                       
         BE    BATT14                                                           
         MVC   0(L'AC@CNTS1,R2),AC@CNTS1                                        
         CURED DUB,(L'PHRS1,PHRS1),2,MINUS=YES                                  
         B     BATT16                                                           
BATT14   MVC   0(L'AC@CNTS2,R2),AC@CNTS2                                        
         CURED DUB,(L'PHRS2,PHRS2),2,MINUS=YES                                  
BATT16   LA    R4,L'TOTALS(R4)                                                  
         B     BATT04                                                           
*                                                                               
BATTOTX  BAS   RE,OUTBOX                                                        
         L     RE,RETURN1                                                       
         BR    RE                                                               
*                                                                               
BATTRA   ST    RE,RETURN2                                                       
         ST    R2,FULL                                                          
         LA    R2,L'PTOT1+1(R2)                                                 
         CP    0(L'TOTALS,R4),=P'1000'                                          
         BNL   BATTRA02                                                         
         CURED (P8,(R4)),(L'PITT1,(R2)),0                                       
         B     BATTRA04                                                         
BATTRA02 BCTR  R2,0                                                             
         CURED (P8,(R4)),(L'PITT1+1,(R2)),0                                     
         LA    R2,1(R2)                                                         
BATTRA04 LA    R2,L'PITT1+1(R2)                                                 
         MVC   0(L'AC@ITEM,R2),AC@ITEM                                          
         CP    0(L'TOTALS,R4),=P'1'                                             
         BE    BATTRAX                                                          
         MVC   0(L'AC@ITMS1,R2),AC@ITMS1                                        
         CLI   RCSUBPRG,0                                                       
         BE    BATTRAX                                                          
         MVC   0(L'AC@ITMS2,R2),AC@ITMS2                                        
BATTRAX  L     R2,FULL                                                          
         L     RE,RETURN2                                                       
         BR    RE                                                               
*                                                                               
BATTRB   ST    RE,RETURN2                                                       
         MVC   PITM2(L'AC@ITEM),AC@ITEM                                         
         CP    0(L'TOTALS,R4),=P'1'                                             
         BE    *+10                                                             
         MVC   PITM2(L'AC@ITMS1),AC@ITMS1                                       
         CP    0(L'TOTALS,R4),=P'1000'                                          
         BNL   BATTRB02                                                         
         CURED (P8,(R4)),(L'PITT2,PITT2),0                                      
         B     BATTRBX                                                          
BATTRB02 CURED (P8,(R4)),(L'PITT2+1,PITT2-1),0                                  
BATTRBX  L     RE,RETURN2                                                       
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* BUILD LEDGER AND OFFICE TABLES FOR SUMMARY REPORTS                  *         
***********************************************************************         
         SPACE 1                                                                
LOTSUM   NTR1                                                                   
         CLI   SDREPS,SKROFS       TEST LEDGER OR OFFICE REPORT                 
         BNE   LOTS01                                                           
         CLC   SREC1(SKREPS-SKEY),SREC3  HAS USER ID JUST CHANGED?              
         BNE   LOTS03                                                           
         CLI   SREC3+(SDREPS-SKEY),SKRLGS  ARE OFFICE DETAILS ADDED?            
         BNE   LOTS03                                                           
         B     LOTS38                                                           
*                                                                               
LOTS01   TM    RIND1,R1LACT        TEST LEDGER REPORT ACTIVE                    
         BO    LOTS02                                                           
         L     RE,ALBUFF           NO - CLEAR LEDGER BUFFER                     
         L     RF,=A(LSIZE)                                                     
         XR    R0,R0                                                            
         XR    R1,R1                                                            
         MVCL  RE,R0                                                            
         OI    RIND1,R1LACT        LEDGER REPORT ACTIVE                         
*                                                                               
LOTS02   CLC   SREC1(SKREPS-SKEY),SREC2  IS USER-ID ABOUT TO CHANGE?            
         BNE   LOTS04                                                           
         CLI   SREC2+(SDREPS-SKEY),SKROFS  CAN OFFICE DETAILS BE ADDED?         
         BNE   LOTS04                                                           
*                                                                               
LOTS03   TM    OFFICES,ON1         TEST COMPANY ON OFFICES                      
         BZ    LOTS04                                                           
         TM    RIND1,R1OACT        TEST OFFICE REPORT ACTIVE                    
         BO    LOTS04                                                           
         L     RE,AOBUFF           NO - CLEAR OFFICE BUFFER                     
         L     RF,=A(OSIZE)                                                     
         XR    R0,R0                                                            
         XR    R1,R1                                                            
         MVCL  RE,R0                                                            
         OI    RIND1,R1OACT        OFFICE REPORT ACTIVE                         
*                                                                               
LOTS04   MVC   IOKEY,BATAKEY                                                    
LOTS06   GOTO1 AIO,IORD+IOACCDIR+IO2  RE-READ TO ESTABLISH SEQUENCE             
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 AIO,IOSEQ+IOACCDIR+IO2 GET BATCH ITEM RECORD                     
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   IOKEYSAV(L'TBAKEY-L'TBAKTSEQ),IOKEY                              
         BNE   LOTS38                                                           
         GOTO1 AIO,IOGETREC+IOACCMST+IO2                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R2,AIOBUFF          R2=A(BATCH ITEM RECORD)                      
         USING TBARECD,R2                                                       
         MVC   BATIKEY,TBAKEY                                                   
         TM    TBARESTA,TBAESLDE   TEST LOGICALLY DELETED                       
         BZ    *+14                                                             
         MVC   IOKEY,BATIKEY                                                    
         B     LOTS06                                                           
         LA    R2,TBARFST                                                       
         USING ASKELD,R2                                                        
LOTS08   CLI   0(R2),0             TEST EOR                                     
         BNE   *+14                                                             
         MVC   IOKEY,BATIKEY                                                    
         B     LOTS06                                                           
         CLI   0(R2),ASKELQ                                                     
         BE    LOTS09                                                           
         CLI   0(R2),GINELQ                                                     
         BNE   LOTS36                                                           
LOTS08A  BAS   RE,PASSGIN          PASS GROUP INVOICE TXS                       
         TM    RIND3,R3GIN                                                      
         BO    LOTS09A                                                          
         B     LOTS36                                                           
*                                                                               
LOTS09   MVC   IOKEY,ASKKEY        READ TRANSACTION RECORD                      
         GOTO1 AIO,IORD+IOACCDIR+IO3                                            
         BNE   LOTS36                                                           
         GOTO1 AIO,IOGETREC+IOACCMST+IO3                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
LOTS09A  TM    RIND1,R1LACT        TEST LEDGER REPORT ACTIVE                    
         BZ    LOTS20                                                           
         L     R3,AIOBUFF          R3=A(TRANSACTION RECORD)                     
         USING TRNRECD,R3                                                       
         LA    RF,TRNRFST                                                       
         L     RE,ALBUFF           RE=A(LEDGER TABLE)                           
         USING LTABD,RE                                                         
         LR    R1,RE                                                            
         AH    R1,=Y(LSIZE-LTLNQ)                                               
LOTS10   OC    LTNTRY(LTLNQ),LTNTRY                                             
         BZ    LOTS14              ADD NEW TABLE ENTRY                          
         CLC   LTUL(L'LTUL),TRNKULA                                             
         BNE   LOTS12                                                           
         CLC   LTBTYPE,BATTYPE                                                  
         BE    LOTS16              ADD TO AN EXISTING ENTRY                     
LOTS12   LA    RE,LTLNQ(RE)                                                     
         CR    R1,RE                                                            
         BH    LOTS10                                                           
         DC    H'0'                TABLE IS FULL                                
*                                                                               
LOTS14   MVC   LTUL,TRNKULA                                                     
         MVC   LTBTYPE,BATTYPE                                                  
         ZAP   LTOFFD,=P'0'                                                     
         ZAP   LTOFFC,=P'0'                                                     
         ZAP   LTOND,=P'0'                                                      
         ZAP   LTONC,=P'0'                                                      
LOTS16   CLI   0(RF),0                                                          
         BE    LOTS20                                                           
         CLI   0(RF),TRNELQ                                                     
         BE    LOTS18                                                           
         XR    R0,R0                                                            
         IC    R0,1(RF)                                                         
         AR    RF,R0                                                            
         B     LOTS16                                                           
*                                                                               
         USING TRNELD,RF                                                        
LOTS18   LA    R1,LTOND                                                         
         CLI   SDFVAL,FVINSU       TEST ONLINE OR OFFLINE                       
         BE    *+16                                                             
         CLI   SDFVAL,FVONLU                                                    
         BE    *+8                                                              
         LA    R1,LTOFFD                                                        
         TM    TRNSTAT,TRNSDR                                                   
         BZ    *+14                                                             
         AP    0(LTLAMT,R1),TRNAMNT                                             
         B     LOTS20                                                           
         AP    LTLAMT(LTLAMT,R1),TRNAMNT                                        
         DROP  RF                                                               
*                                                                               
LOTS20   TM    RIND1,R1OACT        TEST OFFICE REPORT ACTIVE                    
         BZ    LOTS35                                                           
         L     R3,AIOBUFF          R3=A(TRANSACTION RECORD)                     
         USING TRNRECD,R3                                                       
         CLI   TRNKUNT,C'G'        TEST GENERAL UNIT                            
         BE    LOTS22                                                           
         CLI   TRNKUNT,C'S'        TEST SUBSIDIARY UNIT                         
         BNE   LOTS35                                                           
         CLC   PRODUNIT,TRNKUNT    TEST PRODUCTION                              
         BNE   *+14                                                             
         MVC   PRODCULA,TRNKCULA                                                
         BAS   RE,PUNASET          SET WORK TO UNIT FOR ANALYSIS                
*                                                                               
LOTS22   LA    RF,TRNRFST                                                       
         XR    R0,R0                                                            
LOTS24   CLI   0(RF),0                                                          
         BE    LOTS35                                                           
         CLI   0(RF),TRNELQ                                                     
         BE    LOTS26                                                           
         IC    R0,1(RF)                                                         
         AR    RF,R0                                                            
         B     LOTS24                                                           
*                                                                               
         USING TRNELD,RF                                                        
LOTS26   L     RE,AOBUFF           RE=A(GENERAL TABLE)                          
         USING OTABD,RE                                                         
         LR    R1,RE                                                            
         AH    R1,=Y(OSIZE-LTLNQ)                                               
LOTS28   OC    OTNTRY(OTLNQ),OTNTRY                                             
         BZ    LOTS32              ADD NEW TABLE ENTRY                          
         CLC   OTMOA,TRNRSMOS                                                   
         BNE   LOTS30                                                           
         CLC   PRODUNIT,TRNKUNT                                                 
         BNE   *+18                                                             
         CLC   OTCODE,WORK                                                      
         BE    LOTS34                                                           
         B     LOTS30                                                           
         CLC   OTCODE,TRNOFFC                                                   
         BE    LOTS34              ADD TO AN EXISTING ENTRY                     
LOTS30   LA    RE,OTLNQ(RE)                                                     
         CR    R1,RE                                                            
         BH    LOTS28                                                           
         DC    H'0'                TABLE IS FULL                                
*                                                                               
LOTS32   MVC   OTMOA,TRNRSMOS                                                   
         MVC   OTCODE,TRNOFFC                                                   
         CLC   PRODUNIT,TRNKUNT                                                 
         BNE   *+10                                                             
         MVC   OTCODE,WORK                                                      
         ZAP   OTOFFD,=P'0'                                                     
         ZAP   OTOFFC,=P'0'                                                     
         ZAP   OTOND,=P'0'                                                      
         ZAP   OTONC,=P'0'                                                      
*                                                                               
LOTS34   LA    R1,OTOND                                                         
         CLI   SDFVAL,FVINSU       TEST ONLINE OR OFFLINE                       
         BE    *+16                                                             
         CLI   SDFVAL,FVONLU                                                    
         BE    *+8                                                              
         LA    R1,OTOFFD                                                        
         TM    TRNSTAT,TRNSDR                                                   
         BZ    *+14                                                             
         AP    0(OTLAMT,R1),TRNAMNT                                             
         B     *+10                                                             
         AP    OTLAMT(OTLAMT,R1),TRNAMNT                                        
         DROP  RF                                                               
*                                                                               
LOTS35   TM    RIND3,R3GIN                                                      
         BO    LOTS08A                                                          
LOTS36   XR    R0,R0                                                            
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     LOTS08                                                           
*                                                                               
LOTS38   CLC   SREC1,SREC2         END OF SORT RECORDS?                         
         BE    *+14                YES - SORT TABLE                             
         CLC   SREC1(SKSSA2-SKEY),SREC2  LAST FOR THIS CONSOLIDATION?           
         BE    LOTSX                                                            
         TM    RIND1,R1LACT                                                     
         BZ    LOTS40                                                           
         MVI   RCSUBPRG,3          LEDGER/BATCH TYPE SUMMARY                    
         L     R1,=A(REPFRM05)                                                  
         ST    R1,FORMAT                                                        
         BAS   RE,LEDGSUM                                                       
LOTS40   TM    RIND1,R1OACT                                                     
         BZ    LOTS42                                                           
         MVI   RCSUBPRG,4          OFFICE SUMMARY                               
         L     R1,=A(REPFRM06)                                                  
         ST    R1,FORMAT                                                        
         BAS   RE,OFFSUM                                                        
LOTS42   XC    RIND1,RIND1         REPORTS INACTIVE                             
LOTSX    B     XIT                                                              
         DROP  R2,R3,RE                                                         
         EJECT                                                                  
***********************************************************************         
* LEDGER SUMMARY REPORT                                               *         
***********************************************************************         
         SPACE 1                                                                
LEDGSUM  NTR1                                                                   
         L     R2,ALBUFF           SORT TABLE                                   
         USING LTABD,R2                                                         
         OC    0(LTLNQ,R2),0(R2)   TEST FOR NO ENTRIES                          
         BZ    LEGSX                                                            
LEGS02   LA    RF,LTLNQ(R2)                                                     
         OC    0(LTLNQ,RF),0(RF)   TEST FOR ONE ENTRY                           
         BZ    LEGS06                                                           
LEGS04   CLC   0(L'LTUL+L'LTBTYPE,R2),0(RF)                                     
         BNH   *+22                                                             
         XC    0(LTLNQ,R2),0(RF)                                                
         XC    0(LTLNQ,RF),0(R2)                                                
         XC    0(LTLNQ,R2),0(RF)                                                
         LA    RF,LTLNQ(RF)                                                     
         OC    0(LTLNQ,RF),0(RF)                                                
         BNZ   LEGS04                                                           
         LA    R2,LTLNQ(R2)                                                     
         OC    0(LTLNQ,R2),0(R2)                                                
         BNZ   LEGS02                                                           
*                                                                               
LEGS06   ZAP   L3OFFD,=P'0'        CLEAR OVERALL LEDGER TOTALS                  
         ZAP   L3OFFC,=P'0'                                                     
         ZAP   L3OND,=P'0'                                                      
         ZAP   L3ONC,=P'0'                                                      
         ZAP   L3TOTD,=P'0'                                                     
         ZAP   L3TOTC,=P'0'                                                     
*                                                                               
         L     R2,ALBUFF                                                        
         OI    RIND1,R1FIRST       SET FIRST FOR LEDGER TYPE                    
         XC    SAVELTUL,SAVELTUL                                                
*                                                                               
LEGS08   BAS   RE,PAGENEW          PREDEFINE PAGE/BOXES FOR FIRST PASS          
*                                                                               
LEGS10   TM    RIND1,R1FIRST       TEST FIRST FOR LEDGER TYPE                   
         BO    LEGS12                                                           
         OC    0(LTLNQ,R2),0(R2)   NO - IS THIS THE END OF THE TABLE?           
         BZ    LEGS22                                                           
         CLC   SAVELTUL,LTUL       HAS UNIT/LEDGER CHANGED?                     
         BE    LEGS16                                                           
         BAS   RE,LEGSR2           YES - DO LEDGER TOTALS                       
         MVI   BBYTE2,RLINE        FORCE RULED LINE AFTER LEDGER TOTALS         
         GOTO1 PAGER,DATA          PREDEFINE PAGE/BOXES FOR NEXT PASS           
         OI    RIND1,R1FIRST                                                    
LEGS12   ZAP   L2OFFD,=P'0'        CLEAR LEDGER TOTALS                          
         ZAP   L2OFFC,=P'0'                                                     
         ZAP   L2OND,=P'0'                                                      
         ZAP   L2ONC,=P'0'                                                      
         ZAP   L2TOTD,=P'0'                                                     
         ZAP   L2TOTC,=P'0'                                                     
LEGS14   MVC   PUL,LTUL            *UNIT/LEDGER*                                
         GOTO1 ACREPORT                                                         
LEGS16   XR    RF,RF                                                            
         IC    RF,LTBTYPE                                                       
         CVD   RF,DUB                                                           
         UNPK  PBT(2),DUB          *BATCH TYPE*                                 
         OI    PBT+1,X'F0'                                                      
         XR    R1,R1                                                            
         L     RE,=A(BTYPTAB)                                                   
         USING BTTABD,RE                                                        
LEGS18   IC    R1,BTTYPE                                                        
         LTR   R1,R1                                                            
         BNP   LEGS20                                                           
         CR    R1,RF                                                            
         BE    *+12                                                             
         LA    RE,BTTABQ(RE)                                                    
         B     LEGS18                                                           
         ICM   R1,3,BTDISP                                                      
         LA    RE,DDOUT1                                                        
         AR    RE,R1                                                            
         MVC   PBTNAME(L'AC@TT001),0(RE)                                        
LEGS20   BAS   RE,LEGSR1           DO BATCH TOTALS                              
         MVC   SAVELTUL,LTUL                                                    
         NI    RIND1,X'FF'-R1FIRST                                              
*                                                                               
         LA    R2,LTLNQ(R2)        NEXT TABLE ENTRY                             
         GOTO1 PAGER,DATA                                                       
         B     LEGS10                                                           
*                                                                               
LEGS22   BAS   RE,LEGSR2           DO INDIVIDUAL LEDGER TOTALS                  
         GOTO1 PAGER,TOTS          PREDEFINE PAGE/BOXES FOR NEXT PASS           
         BAS   RE,LEGSR3           DO OVERALL TOTALS                            
         BAS   RE,OUTBOX                                                        
         B     LEGSX                                                            
*                                                                               
LEGSR1   ST    RE,RETURN1          ** BATCH TOTALS ROUTINE **                   
         CURED LTOFFD,(L'POFFD,POFFD),2,MINUS=YES                               
         CURED LTOFFC,(L'POFFC,POFFC),2,MINUS=YES                               
         CURED LTOND,(L'POND,POND),2,MINUS=YES                                  
         CURED LTONC,(L'PONC,PONC),2,MINUS=YES                                  
         AP    L2OFFD,LTOFFD                                                    
         AP    L2OFFC,LTOFFC                                                    
         AP    L2OND,LTOND                                                      
         AP    L2ONC,LTONC                                                      
*                                                                               
         AP    LTOFFD,LTOND                                                     
         AP    LTOFFC,LTONC                                                     
         CURED LTOFFD,(L'PTOTD,PTOTD),2,MINUS=YES                               
         CURED LTOFFC,(L'PTOTC,PTOTC),2,MINUS=YES                               
         AP    L2TOTD,LTOFFD                                                    
         AP    L2TOTC,LTOFFC                                                    
         GOTO1 ACREPORT                                                         
         L     RE,RETURN1                                                       
         BR    RE                                                               
*                                                                               
LEGSR2   ST    RE,RETURN1          ** LEDGER TOTALS ROUTINE **                  
         GOTO1 ACREPORT                                                         
         MVC   PLTOT,AC@TOTFR                                                   
         MVC   PLTUL,SAVELTUL                                                   
         CURED L2OFFD,(L'POFFD,POFFD),2,MINUS=YES                               
         CURED L2OFFC,(L'POFFC,POFFC),2,MINUS=YES                               
         CURED L2OND,(L'POND,POND),2,MINUS=YES                                  
         CURED L2ONC,(L'PONC,PONC),2,MINUS=YES                                  
         AP    L3OFFD,L2OFFD                                                    
         AP    L3OFFC,L2OFFC                                                    
         AP    L3OND,L2OND                                                      
         AP    L3ONC,L2ONC                                                      
*                                                                               
         AP    L2OFFD,L2OND                                                     
         AP    L2OFFC,L2ONC                                                     
         CURED L2OFFD,(L'PTOTD,PTOTD),2,MINUS=YES                               
         CURED L2OFFC,(L'PTOTC,PTOTC),2,MINUS=YES                               
         AP    L3TOTD,L2OFFD                                                    
         AP    L3TOTC,L2OFFC                                                    
*                                                                               
         GOTO1 ACREPORT                                                         
         L     RE,RETURN1                                                       
         BR    RE                                                               
*                                                                               
LEGSR3   ST    RE,RETURN1          ** OVERALL TOTALS ROUTINE **                 
         MVC   PLTOT,AC@OVRTS                                                   
         CURED L3OFFD,(L'POFFD,POFFD),2,MINUS=YES                               
         CURED L3OFFC,(L'POFFC,POFFC),2,MINUS=YES                               
         CURED L3OND,(L'POND,POND),2,MINUS=YES                                  
         CURED L3ONC,(L'PONC,PONC),2,MINUS=YES                                  
         AP    L3OFFD,L3OND                                                     
         AP    L3OFFC,L3ONC                                                     
         CURED L3OFFD,(L'PTOTD,PTOTD),2,MINUS=YES                               
         CURED L3OFFC,(L'PTOTC,PTOTC),2,MINUS=YES                               
         GOTO1 ACREPORT                                                         
         L     RE,RETURN1                                                       
         BR    RE                                                               
*                                                                               
LEGSX    B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* OFFICE SUMMARY REPORT                                               *         
***********************************************************************         
         SPACE 1                                                                
OFFSUM   NTR1                                                                   
         L     R2,AOBUFF         SORT TABLE                                     
         USING OTABD,R2                                                         
         OC    0(OTLNQ,R2),0(R2) TEST FOR NO ENTRIES                            
         BZ    OFFSX                                                            
OFFS02   LA    RF,OTLNQ(R2)                                                     
         OC    0(OTLNQ,RF),0(RF)   TEST FOR ONE ENTRY                           
         BZ    OFFS06                                                           
OFFS04   CLC   0(L'OTCODE+L'OTMOA,R2),0(RF)                                     
         BNH   *+22                                                             
         XC    0(OTLNQ,R2),0(RF)                                                
         XC    0(OTLNQ,RF),0(R2)                                                
         XC    0(OTLNQ,R2),0(RF)                                                
         LA    RF,OTLNQ(RF)                                                     
         OC    0(OTLNQ,RF),0(RF)                                                
         BNZ   OFFS04                                                           
         LA    R2,OTLNQ(R2)                                                     
         OC    0(OTLNQ,R2),0(R2)                                                
         BNZ   OFFS02                                                           
*                                                                               
OFFS06   ZAP   L3OFFD,=P'0'        CLEAR OVERALL OFFICE TOTALS                  
         ZAP   L3OFFC,=P'0'                                                     
         ZAP   L3OND,=P'0'                                                      
         ZAP   L3ONC,=P'0'                                                      
         ZAP   L3TOTD,=P'0'                                                     
         ZAP   L3TOTC,=P'0'                                                     
*                                                                               
         L     R2,AOBUFF                                                        
         OI    RIND1,R1FIRST       SET FIRST FOR OFFICE TYPE                    
         XC    SAVEOTOC,SAVEOTOC                                                
*                                                                               
OFFS08   BAS   RE,PAGENEW          PREDEFINE PAGE/BOXES FOR FIRST PASS          
*                                                                               
OFFS10   TM    RIND1,R1FIRST       TEST FIRST FOR OFFICE TYPE                   
         BO    OFFS12                                                           
         OC    0(OTLNQ,R2),0(R2)   NO - IS THIS THE END OF THE TABLE?           
         BZ    OFFS20                                                           
         CLC   SAVEOTOC,OTCODE     HAS OFFICE CODE CHANGED?                     
         BE    OFFS16                                                           
         BAS   RE,OFFSR2           YES - DO OFFICE TOTALS                       
         MVI   BBYTE2,RLINE        FORCE RULED LINE AFTER OFFICE TOTALS         
         GOTO1 PAGER,DATA          PREDEFINE PAGE/BOXES FOR NEXT PASS           
         OI    RIND1,R1FIRST                                                    
OFFS12   ZAP   L2OFFD,=P'0'        CLEAR OFFICE TOTALS                          
         ZAP   L2OFFC,=P'0'                                                     
         ZAP   L2OND,=P'0'                                                      
         ZAP   L2ONC,=P'0'                                                      
         ZAP   L2TOTD,=P'0'                                                     
         ZAP   L2TOTC,=P'0'                                                     
OFFS14   MVC   HALF,OTCODE                                                      
         CLC   OTCODE,SPACES                                                    
         BH    *+10                                                             
         MVC   HALF,=C'??'                                                      
         BAS   RE,OFFCON           DISPLAY OFFICE CODE AND NAME                 
         GOTO1 ACREPORT                                                         
OFFS16   MVC   DUB,OTMOA                                                        
         MVI   DUB+L'OTMOA,1                                                    
         GOTO1 DATCON,DMCB,(1,DUB),(6,PONAME)                                   
OFFS18   BAS   RE,OFFSR1           DO BATCH TOTALS                              
         MVC   SAVEOTOC,OTCODE                                                  
         NI    RIND1,X'FF'-R1FIRST                                              
*                                                                               
         LA    R2,OTLNQ(R2)        NEXT TABLE ENTRY                             
         GOTO1 PAGER,DATA                                                       
         B     OFFS10                                                           
*                                                                               
OFFS20   BAS   RE,OFFSR2           DO OFFICE TOTALS                             
         GOTO1 PAGER,TOTS          PREDEFINE PAGE/BOXES FOR NEXT PASS           
         BAS   RE,OFFSR3           DO OVERALL TOTALS                            
         BAS   RE,OUTBOX                                                        
         B     OFFSX                                                            
*                                                                               
OFFSR1   ST    RE,RETURN1          ** OFFICE MOA TOTALS ROUTINE*                
         CURED OTOFFD,(L'POFFD,POFFD),2,MINUS=YES                               
         CURED OTOFFC,(L'POFFC,POFFC),2,MINUS=YES                               
         CURED OTOND,(L'POND,POND),2,MINUS=YES                                  
         CURED OTONC,(L'PONC,PONC),2,MINUS=YES                                  
         AP    L2OFFD,OTOFFD                                                    
         AP    L2OFFC,OTOFFC                                                    
         AP    L2OND,OTOND                                                      
         AP    L2ONC,OTONC                                                      
*                                                                               
         AP    OTOFFD,OTOND                                                     
         AP    OTOFFC,OTONC                                                     
         CURED OTOFFD,(L'PTOTD,PTOTD),2,MINUS=YES                               
         CURED OTOFFC,(L'PTOTC,PTOTC),2,MINUS=YES                               
         AP    L2TOTD,OTOFFD                                                    
         AP    L2TOTC,OTOFFC                                                    
         SP    OTOFFD,OTOFFC                                                    
         CURED OTOFFD,(L'PTOTB,PTOTB),2,MINUS=YES                               
         GOTO1 ACREPORT                                                         
         L     RE,RETURN1                                                       
         BR    RE                                                               
*                                                                               
OFFSR2   ST    RE,RETURN1          ** OFFICE TOTALS ROUTINE **                  
         GOTO1 ACREPORT                                                         
         MVC   PONAME,AC@TOTLS                                                  
         CURED L2OFFD,(L'POFFD,POFFD),2,MINUS=YES                               
         CURED L2OFFC,(L'POFFC,POFFC),2,MINUS=YES                               
         CURED L2OND,(L'POND,POND),2,MINUS=YES                                  
         CURED L2ONC,(L'PONC,PONC),2,MINUS=YES                                  
         AP    L3OFFD,L2OFFD                                                    
         AP    L3OFFC,L2OFFC                                                    
         AP    L3OND,L2OND                                                      
         AP    L3ONC,L2ONC                                                      
*                                                                               
         AP    L2OFFD,L2OND                                                     
         AP    L2OFFC,L2ONC                                                     
         CURED L2OFFD,(L'PTOTD,PTOTD),2,MINUS=YES                               
         CURED L2OFFC,(L'PTOTC,PTOTC),2,MINUS=YES                               
         AP    L3TOTD,L2OFFD                                                    
         AP    L3TOTC,L2OFFC                                                    
         SP    L2OFFD,L2OFFC                                                    
         CURED L2OFFD,(L'PTOTB,PTOTB),2,MINUS=YES                               
         GOTO1 ACREPORT                                                         
         L     RE,RETURN1                                                       
         BR    RE                                                               
*                                                                               
OFFSR3   ST    RE,RETURN1          ** OVERALL TOTALS ROUTINE **                 
         MVC   POTOT,AC@OVRTS                                                   
         CURED L3OFFD,(L'POFFD,POFFD),2,MINUS=YES                               
         CURED L3OFFC,(L'POFFC,POFFC),2,MINUS=YES                               
         CURED L3OND,(L'POND,POND),2,MINUS=YES                                  
         CURED L3ONC,(L'PONC,PONC),2,MINUS=YES                                  
         AP    L3OFFD,L3OND                                                     
         AP    L3OFFC,L3ONC                                                     
         CURED L3OFFD,(L'PTOTD,PTOTD),2,MINUS=YES                               
         CURED L3OFFC,(L'PTOTC,PTOTC),2,MINUS=YES                               
         SP    L3OFFD,L3OFFC                                                    
         CURED L3OFFD,(L'PTOTB,PTOTB),2,MINUS=YES                               
         GOTO1 ACREPORT                                                         
         L     RE,RETURN1                                                       
         BR    RE                                                               
*                                                                               
OFFSX    B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* SET PRODUCTION LEDGER UNIT FOR ANLYSIS (OFFICE)                     *         
***********************************************************************         
         SPACE 1                                                                
PUNASET  NTR1                                                                   
         MVC   WORK(2),SPACES      PRESET UNIT FOR ANALYSIS TO SPACES           
         XR    RF,RF                                                            
         ICM   RF,3,UNITMAX        RF=MAX NUMBER OF ENTRIES                     
         BNZ   PUNA02                                                           
         XR    RE,RE                                                            
         L     R0,AUBUFF                                                        
         LH    R1,=Y(USIZE)                                                     
         MVCL  R0,RE               CLEAR TABLE                                  
         IC    RE,PRODLAB                                                       
         LA    RE,1(RE)                                                         
         TM    OFFICES,ON2         TEST TWO CHARACTER OFFICES                   
         BZ    *+8                                                              
         LA    RE,1(RE)                                                         
         STC   RE,UNITNTRY         SAVE ENTRY LENGTH                            
         LR    R0,RE                                                            
         XR    RE,RE                                                            
         LH    RF,=Y(USIZE)                                                     
         DR    RE,R0                                                            
         STCM  RF,3,UNITMAX        SAVE MAX NUMBER OF TABLE ENTRIES             
         B     *+10                                                             
*                                                                               
PUNA02   XR    R0,R0                                                            
         IC    R0,UNITNTRY         RE=ENTRY LENGTH                              
         XR    R1,R1                                                            
         IC    R1,PRODLAB                                                       
         BCTR  R1,0                                                             
         L     R2,AUBUFF                                                        
PUNA04   CLI   0(R2),0                                                          
         BE    PUNA06                                                           
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   PRODACT(0),0(R2)                                                 
         BNE   *+10                                                             
         XR    R3,R3               SET ENTRY FOUND IN TABLE                     
         B     PUNA16                                                           
         AR    R2,R0                                                            
         BCT   RF,PUNA04                                                        
         L     R2,AUBUFF           NO MORE ROOM - OVERWRITE FIRST ENTRY         
*                                                                               
PUNA06   LA    R0,2                TRY PRODUCT, THEN CLIENT                     
         LA    R3,PRODLAB                                                       
         XC    IOKEY,IOKEY                                                      
PUNA08   LA    RF,IOKEY                                                         
         USING ACTRECD,RF                                                       
         MVC   ACTKEY,SPACES                                                    
         MVC   ACTKCPY,PRODCULA                                                 
         XR    R1,R1                                                            
         IC    R1,0(R3)                                                         
         LA    R1,L'PRODUNIT-1(R1)                                              
         MVC   ACTKUNT(0),PRODCUL+1                                             
         EX    R1,*-6                                                           
         GOTO1 AIO,IOHI+IOACCDIR+IO4                                            
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 AIO,IOGETREC+IOACCMST+IO4                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     RF,AIOBUFF                                                       
         LA    R1,ACTRFST                                                       
         XR    RF,RF                                                            
         USING PPRELD,R1                                                        
PUNA10   IC    RF,PPRLN            BUMP TO NEXT ELEMENT                         
         AR    R1,RF                                                            
         CLI   PPREL,0             TEST END OF RECORD                           
         BE    PUNA14                                                           
         CLI   PPREL,PPRELQ        TEST PRODUCTION PROFILE ELEMENT              
         BNE   PUNA10                                                           
         XC    WORK(2),WORK                                                     
         CLC   PPRUFORA,SPACES     TEST OLD UNIT FOR ANALYSIS                   
         BNH   *+10                                                             
         MVC   WORK(L'PPRUFORA),PPRUFORA  EXTRA0CT IT                           
         OC    PPRGAOFF,PPRGAOFF   TEST NEW OFFICE CODE                         
         BZ    PUNA12                                                           
         MVC   WORK(L'PPRGAOFF),PPRGAOFF  EXTRA0CT IT                           
         TM    OFFICES,ON2         TEST TWO CHARACTER OFFICES                   
         BO    *+8                                                              
         MVI   WORK+1,C' '         NO - CLEAR SECOND CHARACTER                  
PUNA12   CLC   WORK(2),SPACES      TEST ANYTHING FOUND                          
         BH    *+10                                                             
PUNA14   BCTR  R3,0                R3=A(PRODLA) FOR SECOND ATTEMPT              
         BCT   R0,PUNA08           TRY AGAIN AT CLIENT LEVEL                    
         XR    R1,R1                                                            
         IC    R1,PRODLAB          SET CLI/PRO OFFICE CODE                      
         BCTR  R1,0                                                             
         MVC   0(0,R2),PRODACT                                                  
         EX    R1,*-6                                                           
*                                                                               
PUNA16   LA    R2,1(R1,R2)         A(OFFICE CODE)                               
         LA    RF,WORK                                                          
         LTR   R3,R3               TEST WE ARE EXTRA0CTING                      
         BZ    *+10                YES                                          
         XR    RF,R2               ELSE WANT TO SET IT IN UNITBUFF              
         XR    R2,RF                                                            
         XR    RF,R2                                                            
         XR    R1,R1               SET FOR ONE CHARACTER OFFICE                 
         TM    OFFICES,ON2         TEST TWO CHARACTER OFFICES                   
         BZ    *+8                                                              
         LA    R1,1(R1)            SET FOR TWO CHARACTER OFFICE                 
         MVC   0(0,RF),0(R2)       SET/EXTRA0CT THE OFFICE                      
         EX    R1,*-6                                                           
*                                                                               
         CLC   WORK(2),SPACES      TEST FOR BAD OFFICE CODES                    
         BNL   XIT                                                              
         MVC   WORK(2),=C'??'      YES - CHANGE CODE TO HIGHLIGHT ERROR         
         B     XIT                                                              
         DROP  R1,RF                                                            
         EJECT                                                                  
***********************************************************************         
* DISPLAY OFFICE CODE AND NAME                                        *         
***********************************************************************         
         SPACE 1                                                                
OFFCON   NTR1                                                                   
         OC    HALF,HALF                                                        
         BZ    OFFCX                                                            
         MVC   POCODE,HALF         OFFICE CODE                                  
         TM    OFFICES,ON2         OLD OR NEW OFFICES?                          
         BZ    OFFC02                                                           
*                                                                               
         USING TBARECD,R4                                                       
         LA    RF,IOKEY            READ OFFICE RECORD                           
         USING OFFRECD,RF          (NEW OFFICES)                                
         MVC   OFFKEY,SPACES                                                    
         MVI   OFFKTYP,OFFKTYPQ                                                 
         MVC   OFFKCPY,TBAKCPY                                                  
         MVC   OFFKOFF,HALF                                                     
         GOTO1 AIO,IORD+IOACCDIR+IO2                                            
         BNE   OFFCX                                                            
         GOTO1 AIO,IOGETREC+IOACCMST+IO2                                        
         BNE   OFFCX                                                            
         L     RF,AIOBUFF                                                       
         GOTO1 OFFNAME,OFFRFST                                                  
         MVC   PONAME,WORK                                                      
         B     OFFCX                                                            
*                                                                               
OFFC02   LA    RF,IOKEY            READ DEPARTMENT RECORD                       
         USING ACTRECD,RF          (OLD OFFICES)                                
         MVC   ACTKEY,SPACES                                                    
         MVC   ACTKCPY,TBAKCPY                                                  
         MVI   ACTKUNT,C'2'                                                     
         MVI   ACTKLDG,C'D'                                                     
         MVC   ACTKACT(1),HALF                                                  
         GOTO1 AIO,IORD+IOACCDIR+IO2                                            
         BNE   OFFCX                                                            
         GOTO1 AIO,IOGETREC+IOACCMST+IO2                                        
         BNE   OFFCX                                                            
         L     RF,AIOBUFF                                                       
         GOTO1 OFFNAME,ACTRFST                                                  
         MVC   PONAME,WORK                                                      
*                                                                               
OFFCX    B     XIT                                                              
         DROP  R4,RF                                                            
*                                                                               
         USING NAMELD,R1                                                        
OFFNAME  XR    RF,RF                                                            
         MVC   WORK,SPACES                                                      
OFFN02   CLI   NAMEL,0                                                          
         BER   RE                                                               
         IC    RF,NAMLN                                                         
         CLI   NAMEL,NAMELQ                                                     
         BE    *+10                                                             
         AR    R1,RF                                                            
         B     OFFN02                                                           
         SH    RF,=Y(NAMLN1Q+1)                                                 
         EX    RF,*+6                                                           
         BR    RE                                                               
         MVC   WORK(0),NAMEREC                                                  
         DROP  R1                                                               
         EJECT                                                                  
***********************************************************************         
* BATCH SUMMARY REPORT                                                *         
* NTRY - R4 = A(BATCH HEADER RECORD)                                  *         
***********************************************************************         
         SPACE 1                                                                
BATSUM   NTR1                                                                   
         TM    RIND1,R1BACT        TEST BATCH ACTIVE                            
         BO    BATS02                                                           
         ZAP   L2NUMB,=P'0'        NO - CLEAR TYPE TOTALS                       
         ZAP   L2ITEM,=P'0'                                                     
         ZAP   L2ACP,=P'0'                                                      
         ZAP   L2ANP,=P'0'                                                      
         OI    RIND1,R1BACT        BATCH ACTIVE                                 
*                                                                               
BATS02   TM    RIND1,R1RACT        TEST REPORT ACTIVE                           
         BO    BATS04                                                           
         ZAP   L3NUMB,=P'0'        NO - CLEAR COMPANY TOTALS                    
         ZAP   L3ITEM,=P'0'                                                     
         ZAP   L3ACP,=P'0'                                                      
         ZAP   L3ANP,=P'0'                                                      
         BAS   RE,PAGENEW          PREDEFINE PAGE/BOXES FOR FIRST PASS          
         OI    RIND1,R1RACT        REPORT ACTIVE                                
*                                                                               
         USING TBARECD,R4                                                       
BATS04   TM    TBAHRIND,TBAHIGDJ   TEST FOR A NON-INPUT BATCH                   
         BZ    *+8                                                              
         OI    RIND2,R2NON                                                      
*                                                                               
         MVC   PBBMOA,TBAKBMOS     BATCH MOA+REF/SEQ                            
         OI    PBBMOA,X'F0'                                                     
         XR    RF,RF                                                            
         IC    RF,TBAKBMOS+1                                                    
         LA    RF,MOATAB-1(RF)                                                  
         MVC   PBBMOA+1(1),0(RF)                                                
         GOTO1 BLDRFSQ,PBBREF                                                   
         EDIT  TBAKBTYP,(2,PBBTYPE),FILL=0                                      
         GOTO1 DATCON,DMCB,(2,TBAHRADT),(17,PBBADATE)                           
*                                                                               
         LA    R2,TBARFST                                                       
BATS18   CLI   0(R2),0                                                          
         BE    BATS26                                                           
         CLI   0(R2),BHDELQ                                                     
         BE    BATS22                                                           
BATS20   XR    R0,R0                                                            
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     BATS18                                                           
*                                                                               
         USING BHDELD,R2                                                        
BATS22   DS    0H                                                               
*&&US*&& CLI   TBAKBTYP,BT58                                                    
*&&US*&& BE    BATS24                                                           
         MVC   PBBNAME(L'BHDNAME),BHDNAME                                       
         GOTO1 PERSID,BATIBNO                                                   
         OC    DUB,DUB                                                          
         BZ    *+10                                                             
         MVC   PBPRSN(L'DUB),DUB                                                
BATS24   CURED BHDITEMA,(L'PBITEM-1,PBITEM),0                                   
         MVC   BATAPRVR,BHDAPRVR                                                
         MVC   BATHSTAT,BHDSTAT1                                                
*                                                                               
         XR    RF,RF               INCREMENT ITEM/BATCH TOTALS                  
         ICM   RF,3,BHDITEMA                                                    
         CVD   RF,DUB                                                           
         AP    L2ITEM,DUB                                                       
         AP    L3ITEM,DUB                                                       
         AP    L2NUMB,=P'1'                                                     
         AP    L3NUMB,=P'1'                                                     
         B     BATS20                                                           
         DROP  R4                                                               
*                                                                               
BATS26   ZAP   L1ACPD,=P'0'        CLEAR TRANSACTION TOTALS                     
         ZAP   L1ACPC,=P'0'                                                     
         ZAP   L1ANPD,=P'0'                                                     
         ZAP   L1ANPC,=P'0'                                                     
*                                                                               
         MVC   IOKEY,BATAKEY                                                    
BATS28   GOTO1 AIO,IORD+IOACCDIR+IO2  RE-READ TO ESTABLISH SEQUENCE             
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 AIO,IOSEQ+IOACCDIR+IO2 GET BATCH ITEM RECORD                     
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   IOKEYSAV(L'TBAKEY-L'TBAKTSEQ),IOKEY                              
         BNE   BATS44                                                           
         GOTO1 AIO,IOGETREC+IOACCMST+IO2                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R2,AIOBUFF          R2=A(BATCH ITEM RECORD)                      
         USING TBARECD,R2                                                       
         MVC   BATIKEY,TBAKEY                                                   
         TM    TBARESTA,TBAESLDE   TEST LOGICALLY DELETED                       
         BZ    *+14                                                             
         MVC   IOKEY,BATIKEY                                                    
         B     BATS28                                                           
         LA    R2,TBARFST                                                       
         USING ASKELD,R2                                                        
BATS30   CLI   0(R2),0             TEST EOR                                     
         BNE   *+14                                                             
         MVC   IOKEY,BATIKEY                                                    
         B     BATS28                                                           
         CLI   0(R2),ASKELQ                                                     
         BE    BATS31                                                           
         CLI   0(R2),GINELQ                                                     
         BNE   BATS42                                                           
BATS30A  BAS   RE,PASSGIN          PASS GROUP INVOICE TXS                       
         TM    RIND3,R3GIN                                                      
         BO    BATS31A                                                          
         B     BATS42                                                           
*                                                                               
BATS31   MVC   IOKEY,ASKKEY        READ TRANSACTION RECORD                      
         GOTO1 AIO,IORD+IOACCDIR+IO3                                            
         BNE   BATS42                                                           
         GOTO1 AIO,IOGETREC+IOACCMST+IO3                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
BATS31A  L     R3,AIOBUFF          R3=A(TRANSACTION RECORD)                     
         USING TRNRECD,R3                                                       
*                                                                               
         LA    R1,ANAUNIT          TEST FOR ANALYSIS UNIT                       
BATS32   CLI   0(R1),0                                                          
         BE    BATS34              ACCOUNT POSTING                              
         CLC   0(1,R1),TRNKUNT                                                  
         BNE   *+12                                                             
         OI    RIND3,R3ANAL        ANALYSIS (SUBSIDIARY) POSTING                
         B     BATS34                                                           
         LA    R1,1(R1)                                                         
         B     BATS32                                                           
*                                                                               
BATS34   LA    R3,TRNRFST                                                       
BATS36   CLI   0(R3),0                                                          
         BE    BATS41                                                           
         CLI   0(R3),TRNELQ                                                     
         BE    BATS38                                                           
         XR    RE,RE                                                            
         IC    RE,1(R3)                                                         
         AR    R3,RE                                                            
         B     BATS36                                                           
*                                                                               
         USING TRNELD,R3                                                        
BATS38   TM    RIND3,R3ANAL                                                     
         BO    BATS40                                                           
*                                                                               
         TM    TRNSTAT,TRNSDR                                                   
         BO    *+14                                                             
         AP    L1ACPC,TRNAMNT                                                   
         B     BATS41                                                           
         AP    L1ACPD,TRNAMNT                                                   
         B     BATS41                                                           
*                                                                               
BATS40   TM    TRNSTAT,TRNSDR                                                   
         BO    *+14                                                             
         AP    L1ANPC,TRNAMNT                                                   
         B     BATS41                                                           
         AP    L1ANPD,TRNAMNT                                                   
*                                                                               
BATS41   TM    RIND3,R3GIN                                                      
         BZ    BATS42                                                           
         NI    RIND3,X'FF'-R3ANAL                                               
         B     BATS30A                                                          
BATS42   NI    RIND3,X'FF'-R3ANAL                                               
         XR    R0,R0                                                            
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     BATS30                                                           
         DROP  R2,R3                                                            
*                                                                               
BATS44   CURED L1ACPD,(L'PBACP,PBACP),2,MINUS=YES                               
         CURED L1ANPD,(L'PBANP,PBANP),2,MINUS=YES                               
*                                                                               
         CP    L1ACPD,L1ACPC       TEST FOR ERROR                               
         BE    *+16                                                             
         MVI   PBACP,C'E'                                                       
         MVI   PBTLP,C'E'                                                       
         OI    ERBYTE,ERBTACP      ACCOUNTING POSTING ERROR IN BATCH            
         CP    L1ANPD,L1ANPC                                                    
         BE    *+16                                                             
         MVI   PBANP,C'E'                                                       
         MVI   PBTLP,C'E'                                                       
         OI    ERBYTE,ERBTANP      ANALYSIS POSTING ERROR IN BATCH              
*                                                                               
         AP    L2ACP,L1ACPD                                                     
         AP    L2ANP,L1ANPD                                                     
         AP    L1ACPD,L1ANPD                                                    
         CURED L1ACPD,(L'PBTLP,PBTLP),2,MINUS=YES                               
*                                                                               
         LA    RF,PBOTHER                                                       
         CLI   SDFVAL,FVINSU                                                    
         BNE   *+18                                                             
         MVC   0(L'AC@INSUP,RF),AC@INSUP   INSTANT                              
         LA    RF,L'AC@INSUP+1(RF)                                              
         B     BATS46                                                           
         CLI   SDFVAL,FVONLU                                                    
         BNE   *+18                                                             
         MVC   0(L'AC@ONLIN,RF),AC@ONLIN    ONLINE                              
         LA    RF,L'AC@ONLIN+1(RF)                                              
         B     BATS46                                                           
         CLI   SDFVAL,FVSAVW                                                    
         BNE   *+18                                                             
         MVC   0(L'AC@SAVED,RF),AC@SAVED    SAVED (CLOSED OPTION)               
         LA    RF,L'AC@SAVED+1(RF)                                              
         B     BATS46                                                           
         CLI   SDFVAL,FVDUEU                                                    
         BNE   BATS46                                                           
         MVC   0(L'AC@UAPR,RF),AC@UAPR      UNAPPROVED (CLOSED OPTION)          
         LA    RF,L'AC@UAPR+1(RF)                                               
*                                                                               
BATS46   TM    BATHSTAT,BHDSACRU                                                
         BZ    *+14                                                             
         MVC   0(L'AC@ACRL,RF),AC@ACRL      ACCRUAL                             
         B     *+18                                                             
         TM    BATHSTAT,BHDSACRV                                                
         BZ    *+14                                                             
         MVC   0(L'AC@RVRSL,RF),AC@RVRSL    REVERSAL                            
         LA    RF,L'AC@RVRSL+1(RF)                                              
*                                                                               
         LR    R0,RF               R0 = WHERE WE ARE IN PBOTHER                 
         LA    R1,PBOTHER          R1 = START OF PBOTHER                        
         SR    R0,R1               R0 = HOW MANY BYTES USED SO FAR              
*                                                                               
         LA    R1,L'PBOTHER        LOAD NUMBER OF BYTES IN PBOTHER              
         SR    R1,R0               SUBTRACT NUMBER USED SO FAR                  
         CHI   R1,12               DO WE HAVE AT LEAST 12 BYTES LEFT?           
         BNL   BATS46A             YES, MOVE IN APPROVER                        
*                                                                               
         GOTO1 ACREPORT            NO, PRINT WHAT WE HAVE THEN PRINT            
         LA    RF,PBOTHER            APPROVER.                                  
         MVC   PBOTHER,SPACES                                                   
*                                                                               
BATS46A  LR    R0,RF                                                            
         GOTO1 PERSID,BATAPRVR                                                  
         LR    RF,R0                                                            
         OC    DUB,DUB                                                          
         BZ    BATS47                                                           
         MVC   0(L'AC@APR,RF),AC@APR                                            
         MVI   L'AC@APR(RF),C'='                                                
         MVC   L'AC@APR+1(L'DUB,RF),DUB                                         
*                                                                               
BATS47   GOTO1 ACREPORT                                                         
*                                                                               
         CLC   SREC1,SREC2         END OF SORT RECORDS?                         
         BE    BATS48              YES - DO TYPE TOTALS                         
         CLC   SREC1(SKSSA2-SKEY),SREC2  LAST FOR THIS CONSOLIDATION?           
         BNE   BATS48                    YES - DO TYPE TOTALS                   
         CLC   SKTYP,SREC2+(SKTYP-SKEY)  LAST FOR BATCH TYPE?                   
         BNE   BATS48                                                           
         GOTO1 PAGER,DATA                NO - SET FOR NEXT PASS                 
         B     BATSX                                                            
*                                                                               
BATS48   GOTO1 PAGER,TOTS          PREDEFINE PAGE/BOXES FOR NEXT PASS           
         MVC   PBTOT(L'AC@TYPTS),AC@TYPTS                                       
         EDIT  BATTYPE,(2,PBBTYPE),FILL=0                                       
         MVC   PBATS(L'AC@BAT),AC@BAT                                           
         CP    L2NUMB,=P'1'                                                     
         BE    *+10                                                             
         MVC   PBATS(L'AC@BATS),AC@BATS                                         
         CURED L2NUMB,(L'PBATN,PBATN),0                                         
         CURED L2ITEM,(L'PBITEM-1,PBITEM),0                                     
*                                                                               
         CURED L2ACP,(L'PBACP,PBACP),2,MINUS=YES                                
         CURED L2ANP,(L'PBANP,PBANP),2,MINUS=YES                                
         AP    L3ACP,L2ACP                                                      
         AP    L3ANP,L2ANP                                                      
         AP    L2ACP,L2ANP                                                      
         CURED L2ACP,(L'PBTLP,PBTLP),2,MINUS=YES                                
*                                                                               
         TM    ERBYTE,ERBTACP      TEST FOR ERROR                               
         BZ    *+20                                                             
         MVI   PBACP,C'E'                                                       
         MVI   PBTLP,C'E'                                                       
         NI    ERBYTE,X'FF'-ERBTACP                                             
         OI    ERBYTE,ERRPACP      ACCOUNTING POSTING ERROR IN REPORT           
         TM    ERBYTE,ERBTANP                                                   
         BZ    *+20                                                             
         MVI   PBANP,C'E'                                                       
         MVI   PBTLP,C'E'                                                       
         NI    ERBYTE,X'FF'-ERBTANP                                             
         OI    ERBYTE,ERRPANP      ANALYSIS POSTING ERROR IN REPORT             
*                                                                               
         GOTO1 ACREPORT                                                         
*                                                                               
         NI    RIND1,X'FF'-R1BACT  BATCH INACTIVE                               
         CLC   SREC1,SREC2         END OF SORT RECORDS?                         
         BE    BATS50              YES - DO OVERALL TOTALS                      
         CLC   SREC1(SKSSA2-SKEY),SREC2  LAST FOR THIS CONSOLIDATION?           
         BNE   BATS50                    YES - DO OVERALL TOTALS                
         MVI   BBYTE2,RLINE        FORCE RULED LINE AFTER TOTALS BOXES          
         GOTO1 PAGER,DATA          PREDEFINE PAGE/BOXES FOR NEXT PASS           
         B     BATSX                                                            
*                                                                               
BATS50   GOTO1 PAGER,TOTSX                                                      
         MVC   PBTOT(L'AC@CPYTS),AC@OVRTS                                       
         MVC   PBATS(L'AC@BAT),AC@BAT                                           
         CP    L3NUMB,=P'1'                                                     
         BE    *+10                                                             
         MVC   PBATS(L'AC@BATS),AC@BATS                                         
         CURED L3NUMB,(L'PBATN,PBATN),0                                         
         CURED L3ITEM,(L'PBITEM-1,PBITEM),0                                     
*                                                                               
         CURED L3ACP,(L'PBACP,PBACP),2,MINUS=YES                                
         CURED L3ANP,(L'PBANP,PBANP),2,MINUS=YES                                
         AP    L3ACP,L3ANP                                                      
         CURED L3ACP,(L'PBTLP,PBTLP),2,MINUS=YES                                
*                                                                               
         TM    ERBYTE,ERRPACP      TEST FOR ERROR                               
         BZ    *+16                                                             
         MVI   PBACP,C'E'                                                       
         MVI   PBTLP,C'E'                                                       
         NI    ERBYTE,X'FF'-ERRPACP                                             
         TM    ERBYTE,ERRPANP                                                   
         BZ    *+16                                                             
         MVI   PBANP,C'E'                                                       
         MVI   PBTLP,C'E'                                                       
         NI    ERBYTE,X'FF'-ERRPANP                                             
*                                                                               
         GOTO1 ACREPORT                                                         
*                                                                               
         BAS   RE,OUTBOX                                                        
         XC    RIND1,RIND1         REPORT INACTIVE                              
*                                                                               
BATSX    B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* BATCH EXCEPTION LIST                                                *         
***********************************************************************         
         SPACE 1                                                                
EXLIST   NTR1                                                                   
         TM    RIND1,R1RACT        TEST REPORT ACTIVE                           
         BO    *+12                                                             
         BAS   RE,PAGENEW          PREDEFINE PAGE/BOXES FOR FIRST PASS          
         OI    RIND1,R1RACT        REPORT ACTIVE                                
*                                                                               
         USING TBARECD,R4                                                       
         LA    RF,TBARFST                                                       
         USING BHDELD,RF                                                        
         XR    R0,R0                                                            
EXLS02   CLI   0(RF),0                                                          
         BE    EXLS18                                                           
         CLI   0(RF),BHDELQ        BATCH HEADER ELEMENT                         
         BNE   EXLS16                                                           
         MVC   PEBNAME(L'BHDNAME),BHDNAME  *BATCH NAME*                         
         MVC   BATAPRVR,BHDAPRVR                                                
         MVC   STATUS,BHDSTAT1     SAVE BATCH HEADER STATUS BYTE                
         MVI   PEDDATE,C'?'                                                     
         OC    BHDDELDT,BHDDELDT   SHOW DELETION DATE IF SET                    
         BZ    EXLS04                                                           
         GOTO1 DATCON,DMCB,(2,BHDDELDT),(17,PEDDATE)  *DELETION DATE*           
         B     EXLS18                                                           
EXLS04   XR    R0,R0               CALCULATE DELETION DATE                      
         CLI   JPROG,JPDJ                                                       
         BNE   EXLS06                                                           
         ICM   R0,1,SDFDAYS        DJ - DAYS TO DELETION SET BY DJ              
         BNZ   EXLS08                                                           
         ICM   R0,1,BHDWDAYS          - DAYS TO DELETION SET BY BG              
         BNZ   EXLS08                                                           
         B     EXLS18                   ERROR                                   
EXLS06   ICM   R0,1,SDFDAYS        IJ - DAYS TO DELETION SET BY IJ              
         BZ    EXLS12                   USE TODAY'S DATE IF NOT SET             
         B     EXLS09                   AND AS BASE IF EFF BEFORE TODAY         
EXLS08   TM    TBARHSTA,TBAHSIIP+TBAHSSAV                                       
         BNZ   EXLS12                                                           
EXLS09   CLC   TBAHREDT,TODAYC                                                  
         BL    EXLS12                                                           
EXLS10   GOTO1 DATCON,DMCB,(2,TBAHREDT),(0,TEMP)                                
         GOTO1 ADDAY,DMCB,TEMP,WORK,(R0)                                        
         B     EXLS14                                                           
EXLS12   GOTO1 ADDAY,DMCB,TODAYE,WORK,(R0)                                      
EXLS14   GOTO1 DATCON,DMCB,(0,WORK),(17,PEDDATE)                                
         B     EXLS18                                                           
EXLS16   IC    R0,1(RF)                                                         
         AR    RF,R0                                                            
         B     EXLS02                                                           
         DROP  RF                                                               
*                                                                               
EXLS18   MVC   PEBMOA,TBAKBMOS     *BATCH MOA+REF/SEQ*                          
         OI    PEBMOA,X'F0'                                                     
         XR    RF,RF                                                            
         IC    RF,TBAKBMOS+1                                                    
         LA    RF,MOATAB-1(RF)                                                  
         MVC   PEBMOA+1(1),0(RF)                                                
         GOTO1 BLDRFSQ,PEBREF                                                   
         XR    RF,RF                                                            
         IC    RF,SKTYP                                                         
         CVD   RF,DUB                                                           
         UNPK  PEBTYPE(2),DUB      *BATCH TYPE*                                 
         OI    PEBTYPE+1,X'F0'                                                  
         XR    R1,R1                                                            
         L     RE,=A(BTYPTAB)                                                   
         USING BTTABD,RE                                                        
EXLS20   IC    R1,BTTYPE                                                        
         LTR   R1,R1                                                            
         BNP   EXLS22                                                           
         CR    R1,RF                                                            
         BE    *+12                                                             
         LA    RE,BTTABQ(RE)                                                    
         B     EXLS20                                                           
         ICM   R1,3,BTDISP                                                      
         LA    RE,DDOUT1                                                        
         AR    RE,R1                                                            
         MVC   PEBTNAME(L'AC@TT001),0(RE)  *BATCH TYPE NAME*                    
EXLS22   MVI   PEPRSN,C'?'                                                      
         GOTO1 PERSID,BATIBNO                                                   
         OC    DUB,DUB                                                          
         BZ    *+10                                                             
         MVC   PEPRSN(L'DUB),DUB   *PERSON*                                     
         MVC   DUB,TBAKBMOS                                                     
         MVI   DUB+L'TBAKBMOS,1                                                 
         GOTO1 DATCON,DMCB,(1,DUB),(6,PEBMON)  *BATCH MONTH*                    
         GOTO1 DATCON,DMCB,(2,TBAHRADT),(17,PECDATE)  *CREATED DATE*            
         GOTO1 DATCON,DMCB,(2,TBAHREDT),(17,PEEDATE)  *EFFECTIVE DATE*          
*                                                                               
         LA    R2,WORK             *STATUS*                                     
         MVC   WORK,SPACES                                                      
         MVI   0(R2),C'?'                                                       
*                                                                               
         CLI   JPROG,JPDJ                                                       
         BE    EXLS23                                                           
         TM    STATUS,BHDSDORD                                                  
         BZ    EXLS23                                                           
         MVC   PESTATE(L'AC@PRGED),AC@PRGED                                     
         B     EXLS25                                                           
EXLS23   CLI   SDFVAL,FVDELD       DELETED BY BATCH PROGRAM                     
         BNE   EXLS24                                                           
         MVC   PESTATE(L'AC@DELD1),AC@DELD1                                     
*&&US*&& MVC   0(L'AC@BATPD,R2),AC@BATPD                                        
*&&UK*&& MVC   0(L'AC@DELPM,R2),AC@DELPM                                        
         B     EXLS28                                                           
*                                                                               
EXLS24   TM    SDFVAL,FVPRGE       PURGED                                       
         BNO   EXLS26                                                           
         MVC   PESTATE(L'AC@PRGED),AC@PRGED                                     
         TM    STATUS,BHDSDORD                                                  
         BZ    *+18                                                             
EXLS25   MVC   0(L'AC@ORDOB,R2),AC@ORDOB  ORDERS ON BATCH                       
         LA    RF,L'AC@ORDOB-1(R2)                                              
         B     *+14                                                             
         MVC   0(L'AC@EXPRD,R2),AC@EXPRD  EXPIRED                               
         LA    RF,L'AC@EXPRD-1(R2)                                              
         CLI   0(RF),C' '                                                       
         BH    *+14                                                             
         CR    RF,R2                                                            
         BNH   *+16                                                             
         BCT   RF,*-14                                                          
         MVI   1(RF),C','                                                       
         LA    R2,2(RF)                                                         
         BAS   RE,EXLSR1                                                        
         B     EXLS28                                                           
*                                                                               
EXLS26   MVC   PESTATE(L'AC@NPSTD),AC@NPSTD   NOT POSTED                        
         CLC   TBAHREDT,TODAYC                                                  
         BNH   *+16                                                             
         MVC   PESTATE,SPACES                                                   
         MVC   PESTATE(L'AC@NDUE),AC@NDUE     NOT DUE                           
         BAS   RE,EXLSR1                                                        
*                                                                               
EXLS28   GOTO1 ADSQUASH,DMCB,WORK,L'WORK                                        
         MVC   PEEXCPT(L'PEEXCPT),WORK                                          
*                                                                               
         GOTO1 ACREPORT                                                         
*                                                                               
         CLC   SREC1,SREC2         END OF SORT RECORDS?                         
         BE    EXLS30              YES - CLOSE TABLE                            
         CLC   SREC1(SKSSA2-SKEY),SREC2  LAST FOR THIS CONSOLIDATION?           
         BNE   EXLS30                    YES - CLOSE TABLE                      
         GOTO1 PAGER,DATA                NO - PREDEFINE PAGE/BOXES              
         B     EXLSX                          FOR NEXT PASS                     
*                                                                               
EXLS30   BAS   RE,OUTBOX                                                        
         XC    RIND1,RIND1                                                      
*                                                                               
EXLSX    B     XIT                                                              
*                                                                               
EXLSR1   ST    RE,RETURN1                                                       
         LR    R1,R2                                                            
         TM    TBARHSTA,TBAHSIIP   OPEN                                         
         BZ    EXLSR1A                                                          
         MVC   0(L'AC@OPEN2,R2),AC@OPEN2                                        
         B     EXLSR1X                                                          
EXLSR1A  TM    TBARHSTA,TBAHSSAV   SAVED                                        
         BZ    EXLSR1B                                                          
         MVC   0(L'AC@SAVED,R2),AC@SAVED                                        
         B     EXLSR1X                                                          
EXLSR1B  TM    TBAHRIND,TBAHIMAL   MOA LOCKED                                   
         BZ    EXLSR1C                                                          
         MVC   0(L'AC@MOALK,R2),AC@MOALK                                        
         LA    R2,L'AC@MOALK(R2)                                                
EXLSR1C  TM    CMPAPRV,APRREG+APREFF                                            
         BZ    EXLSR1X                                                          
         TM    TBARHSTA,TBAHSAPR   UNAPPROVED                                   
         BO    EXLSR1X                                                          
         BAS   RE,EXLSR2                                                        
         MVC   0(L'AC@UAPR,R2),AC@UAPR                                          
EXLSR1X  L     RE,RETURN1                                                       
         BR    RE                                                               
*                                                                               
EXLSR2   ST    RE,RETURN2                                                       
         CR    R1,R2               TEST FOR NEW ADDITION                        
         BE    EXLSR2X                                                          
         MVI   0(R2),C','                                                       
         LA    R2,1(R2)                                                         
         LR    R1,R2                                                            
EXLSR2X  L     RE,RETURN2                                                       
         BR    RE                                                               
*                                                                               
         EJECT                                                                  
***********************************************************************         
* BATCH DUE LIST                                                      *         
***********************************************************************         
         SPACE 1                                                                
DUELIST  NTR1                                                                   
         TM    RIND1,R1RACT        TEST REPORT ACTIVE                           
         BO    *+12                                                             
         BAS   RE,PAGENEW          PREDEFINE PAGE/BOXES FOR FIRST PASS          
         OI    RIND1,R1RACT        REPORT ACTIVE                                
*                                                                               
         USING TBARECD,R4                                                       
         LA    RF,TBARFST                                                       
         USING BHDELD,RF                                                        
         XR    R0,R0                                                            
DUEL02   CLI   0(RF),0                                                          
         BE    DUEL04                                                           
         CLI   0(RF),BHDELQ        BATCH HEADER ELEMENT                         
         BE    DUEL03                                                           
         IC    R0,1(RF)                                                         
         AR    RF,R0                                                            
         B     DUEL02                                                           
DUEL03   MVC   PDBNAME(L'BHDNAME),BHDNAME  *BATCH NAME*                         
         MVC   BATAPRVR,BHDAPRVR                                                
         MVC   BATHSTAT,BHDSTAT1                                                
         DROP  RF                                                               
*                                                                               
DUEL04   GOTO1 DATCON,DMCB,(2,TBAHREDT),(17,PDEDATE)  *EFFECTIVE DATE*          
         MVC   PDBMOA,TBAKBMOS     *BATCH MOA+REF/SEQ*                          
         OI    PDBMOA,X'F0'                                                     
         XR    RF,RF                                                            
         IC    RF,TBAKBMOS+1                                                    
         LA    RF,MOATAB-1(RF)                                                  
         MVC   PDBMOA+1(1),0(RF)                                                
         GOTO1 BLDRFSQ,PDBREF                                                   
         XR    RF,RF                                                            
         IC    RF,SKTYP                                                         
         CVD   RF,DUB                                                           
         UNPK  PDBTYPE(2),DUB      *BATCH TYPE*                                 
         OI    PDBTYPE+1,X'F0'                                                  
         XR    R1,R1                                                            
         L     RE,=A(BTYPTAB)                                                   
         USING BTTABD,RE                                                        
DUEL04B  IC    R1,BTTYPE                                                        
         LTR   R1,R1                                                            
         BNP   DUEL05                                                           
         CR    R1,RF                                                            
         BE    *+12                                                             
         LA    RE,BTTABQ(RE)                                                    
         B     DUEL04B                                                          
         ICM   R1,3,BTDISP                                                      
         LA    RE,DDOUT1                                                        
         AR    RE,R1                                                            
         MVC   PDBTNAME(L'AC@TT001),0(RE)  *BATCH TYPE NAME*                    
DUEL05   MVI   PDPRSN,C'?'                                                      
         GOTO1 PERSID,BATIBNO                                                   
         OC    DUB,DUB                                                          
         BZ    *+10                                                             
         MVC   PDPRSN(L'DUB),DUB   *PERSON*                                     
         GOTO1 DATCON,DMCB,(2,TBAHRADT),(17,PDCDATE)  *CREATED DATE*            
*                                                                               
         MVI   PDSTATE,C'?'        *STATUS*                                     
         LA    R2,WORK                                                          
         MVC   WORK,SPACES                                                      
         MVI   0(R2),C'?'                                                       
*                                                                               
         CLI   SDFVAL,FVDUEN       DUE LIVE SOON - NO APPROVAL REQUIRED         
         BNE   DUEL08                                                           
         TM    BATHSTAT,BHDSPEND   TEST UPDATE PENDING                          
         BZ    *+14                                                             
         MVC   PDSTATE(L'AC@PENDG),AC@PENDG                                     
         B     *+10                                                             
         MVC   PDSTATE(L'AC@DULIS),AC@DULIS                                     
         MVC   0(L'AC@NOAPR,R2),AC@NOAPR                                        
         B     DUEL16                                                           
*                                                                               
DUEL08   CLI   SDFVAL,FVDUEA       DUE LIVE SOON - APPROVED BY                  
         BNE   DUEL12                                                           
         TM    BATHSTAT,BHDSPEND   TEST UPDATE PENDING                          
         BZ    *+14                                                             
         MVC   PDSTATE(L'AC@PENDG),AC@PENDG                                     
         B     *+10                                                             
         MVC   PDSTATE(L'AC@DULIS),AC@DULIS                                     
         MVC   0(L'AC@APRVB,R2),AC@APRVB                                        
         LA    R2,L'AC@APRVB+1(R2)                                              
         MVI   0(R2),C'?'                                                       
         GOTO1 PERSID,BATAPRVR                                                  
         OC    DUB,DUB                                                          
         BZ    *+10                                                             
         MVC   0(L'DUB,R2),DUB                                                  
*                                                                               
DUEL12   CLI   SDFVAL,FVDUEU       DUE LIVE, WARN                               
         BNE   DUEL16                                                           
         MVC   PDSTATE(L'AC@DULIW),AC@DULIW                                     
*        TM    TBARHSTA,TBAHSIIP   OPEN                                         
*        BZ    *+14                                                             
*        MVC   0(L'AC@OPEN2,R2),AC@OPEN2                                        
*        B     DUEL16                                                           
*        TM    TBARHSTA,TBAHSSAV   SAVED                                        
*        BZ    *+14                                                             
*        MVC   0(L'AC@SAVED,R2),AC@SAVED                                        
*        B     DUEL16                                                           
*                                                                               
         TM    TBAHRIND,TBAHIMAL   MOA LOCKED                                   
         BZ    *+18                                                             
         MVC   0(L'AC@MOALK,R2),AC@MOALK                                        
         MVI   L'AC@MOALK(R2),C','                                              
         LA    R2,L'AC@MOALK+1(R2)                                              
*                                                                               
         TM    CMPAPRV,APREFF      NO APPROVAL REQUIRED                         
         BO    *+14                                                             
         MVC   0(L'AC@NOAPR,R2),AC@NOAPR                                        
         B     DUEL16                                                           
         TM    TBARHSTA,TBAHSAPR   UNAPPROVED/APPROVED BY                       
         BO    *+14                                                             
         MVC   0(L'AC@UAPR,R2),AC@UAPR                                          
         B     DUEL16                                                           
         MVC   0(L'AC@APRVB,R2),AC@APRVB                                        
         LA    R2,L'AC@APRVB+1(R2)                                              
         MVI   0(R2),C'?'                                                       
         GOTO1 PERSID,BATAPRVR                                                  
         OC    DUB,DUB                                                          
         BZ    *+10                                                             
         MVC   0(L'DUB,R2),DUB                                                  
*                                                                               
DUEL16   GOTO1 ADSQUASH,DMCB,WORK,L'WORK                                        
         MVC   PDOTHER(L'PDOTHER),WORK                                          
*                                                                               
DUEL18   GOTO1 ACREPORT                                                         
*                                                                               
         CLC   SREC1,SREC2         END OF SORT RECORDS?                         
         BE    DUEL20              YES - CLOSE TABLE                            
         CLC   SREC1(SKSSA2-SKEY),SREC2  LAST FOR THIS CONSOLIDATION?           
         BNE   DUEL20                    YES - CLOSE TABLE                      
         GOTO1 PAGER,DATA                NO - PREDEFINE PAGE/BOXES              
         B     DUELX                          FOR NEXT PASS                     
*                                                                               
DUEL20   BAS   RE,OUTBOX                                                        
         XC    RIND1,RIND1                                                      
*                                                                               
DUELX    B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* SET HEADLINES                                                       *         
***********************************************************************         
         SPACE 1                                                                
HOOK     ST    RE,RETURN1                                                       
         L     R4,ABHDREC                                                       
         USING TBARECD,R4                                                       
         L     R3,ADBXAREA                                                      
         USING BOXD,R3                                                          
         MVC   BOXCOLS,SPACES                                                   
         MVC   BOXROWS,SPACES                                                   
*                                                                               
         CLI   RCSUBPRG,8                                                       
         BNE   *+18                                                             
         MVI   HEAD1+70,C'-'                                                    
         MVC   HEAD1+73(L'SFILNAME),SFILNAME                                    
         B     HOOK66                                                           
*                                                                               
         L     RE,ANBUFF           GET ID NAME FROM BUFFER                      
         USING NTABD,RE                                                         
         LR    R1,RE                                                            
         AH    R1,=Y(NSIZE-NTLNQ)                                               
         CLC   NTAGY,SKAGY         MATCH ON AGENCY ALPHA                        
         BNE   HOOK08                                                           
*                                                                               
HOOK02   CLI   SKCONS,SKCONCPY     TEST COMPANY CONSOLIDATION                   
         BNE   HOOK04                                                           
         MVC   HEAD4+1(L'AC@CPY),AC@CPY                                         
         CLI   NTTYP,NTTYPC                                                     
         BE    HOOK10                                                           
         B     HOOK08                                                           
*                                                                               
HOOK04   CLI   SKCONS,SKCONGRP     TEST ID-GROUP CONSOLIDATION                  
         BNE   HOOK06                                                           
         MVC   HEAD4+1(L'AC@GROUP),AC@GROUP                                     
         CLI   NTTYP,NTTYPG                                                     
         BE    HOOK10                                                           
         B     HOOK08                                                           
*                                                                               
HOOK06   MVC   HEAD4+1(L'AC@USER),AC@USER                                       
         CLI   NTTYP,NTTYPU                                                     
         BE    HOOK10                                                           
HOOK08   LA    RE,NTLNQ(RE)                                                     
         CR    R1,RE                                                            
         BH    HOOK02                                                           
         DC    H'0'                END OF TABLE ENCOUNTERED                     
HOOK10   CLC   NTID,SKOUID                                                      
         BNE   HOOK08                                                           
         MVC   HEAD4+11(L'NTNAME),NTNAME                                        
*                                                                               
         CLI   RCSUBPRG,3                                                       
         BE    HOOK64                                                           
         CLI   RCSUBPRG,4                                                       
         BE    HOOK64                                                           
         CLI   RCSUBPRG,5                                                       
         BE    HOOK64                                                           
         CLI   RCSUBPRG,6                                                       
         BE    HOOK64                                                           
         CLI   RCSUBPRG,7                                                       
         BE    HOOK64                                                           
*                                                                               
         CLC   SREC1(SKGRP-SKEY),SREC3  TEST CHANGE OF USER-ID                  
         BE    HOOK20                                                           
         MVI   HEAD5+11,C'?'                                                    
         XC    IOKEY,IOKEY                                                      
         LA    R2,IOKEY                                                         
         USING CTIREC,R2                                                        
         MVI   CTIKTYP,CTIKTYPQ                                                 
         MVC   CTIKNUM,SKSUID                                                   
         GOTO1 AIO,IORD+IOCLFILE+IO4                                            
         BNE   HOOK22                                                           
*                                                                               
         L     R2,AIOBUFF          R3=A(USER-ID RECORD)                         
         LA    R1,CTIDATA                                                       
HOOK12   CLI   0(R1),0                                                          
         BE    HOOK20                                                           
         CLI   0(R1),CTDSCELQ      DESCRIPTION ELEMENT (USER-ID)                
         BE    HOOK16                                                           
*&&US*&& CLI   0(R1),CTORGELQ      ORIGIN ELEMENT (USER-ID NAME)                
*&&US*&& BE    HOOK18                                                           
HOOK14   XR    R0,R0                                                            
         IC    R0,1(R1)                                                         
         AR    R1,R0                                                            
         B     HOOK12                                                           
*                                                                               
         USING CTDSCD,R1                                                        
HOOK16   MVC   UID,SPACES                                                       
         XR    RE,RE                                                            
         IC    RE,CTDSCLEN                                                      
         SH    RE,=Y(CTDSC-CTDSCD+1)                                            
         MVC   UID(0),CTDSC                                                     
         EX    RE,*-6                                                           
         B     HOOK14                                                           
*&&US                                                                           
         USING CTORGD,R1                                                        
HOOK18   MVC   UIDNAME,SPACES                                                   
         MVC   UIDNAME,CTORGNAM                                                 
         B     HOOK14                                                           
*&&                                                                             
HOOK20   MVC   HEAD5+11(L'UID),UID                                              
*&&US*&& MVC   HEAD5+18(L'UIDNAME),UIDNAME                                      
         DROP  R1,R2                                                            
*                                                                               
HOOK22   CLI   RCSUBPRG,0                                                       
         BE    HOOK24                                                           
         CLI   RCSUBPRG,1                                                       
         BE    HOOK24                                                           
         CLI   RCSUBPRG,2                                                       
         BE    HOOK62                                                           
         B     HOOKX                                                            
*                                                                               
HOOK24   TM    RIND2,R2NON                                                      
         BZ    *+14                                                             
         CLC   BATIBNO,SPACES                                                   
         BNH   HOOK28                                                           
         MVC   HEAD6+1(L'AC@PRSN),AC@PRSN                                       
         MVI   HEAD6+11,C'?'                                                    
         GOTO1 PERSID,BATIBNO                                                   
         OC    DUB,DUB                                                          
         BZ    HOOK28                                                           
         MVC   HEAD6+11(L'DUB),DUB                                              
*                                                                               
         XC    IOKEY,IOKEY         BUILD KEY FOR PERSON RECORD READ             
         LA    R2,IOKEY                                                         
         USING SAPEREC,R2                                                       
         MVI   SAPETYP,SAPETYPQ                                                 
         MVI   SAPESUB,SAPESUBQ                                                 
         MVC   SAPEAGY,SECALPHA    USE SECURITY ALPHA ID FOR PERSON             
         MVC   SAPEPID,DUB                                                      
         GOTO1 AIO,IOHI+IOCLFILE+IO4                                            
         BNE   HOOK28                                                           
         L     R2,AIOBUFF                                                       
         CLC   SAPEKEY(SAPEDEF-SAPEKEY),IOKEY                                   
         BNE   HOOK28                                                           
*                                                                               
         LA    RE,HEAD6+22                                                      
         LA    R1,SAPEDATA                                                      
         XR    R0,R0                                                            
HOOK26   CLI   0(R1),0                                                          
         BE    HOOK28                                                           
         CLI   0(R1),SANAMELQ      PERSON NAME ELEMENT                          
         BE    *+14                                                             
         IC    R0,1(R1)                                                         
         AR    R1,R0                                                            
         B     HOOK26                                                           
         USING SANAMD,R1                                                        
         TM    SANAMIND,SANAMIFN+SANAMILN                                       
         BNO   HOOK28                                                           
         MVC   DUB(1),SANAMIND                                                  
         LA    R1,SANAMELN                                                      
         USING SANAMELN,R1                                                      
         XR    RF,RF                                                            
         IC    RF,SANAMELN                                                      
         BCTR  RF,0                                                             
         MVC   0(0,RE),SANAME                                                   
         EX    RF,*-6                                                           
         LA    RE,2(RF,RE)                                                      
         LA    R1,2(RF,R1)                                                      
         TM    DUB,SANAMIMN                                                     
         BZ    *+12                                                             
         IC    RF,SANAMELN                                                      
         LA    R1,1(RF,R1)                                                      
         IC    RF,SANAMELN                                                      
         BCTR  RF,0                                                             
         MVC   0(0,RE),SANAME                                                   
         EX    RF,*-6                                                           
         DROP  R1,R2                                                            
*                                                                               
HOOK28   TM    RIND2,R2NON                                                      
*        BZ    *+14                                                             
         CLC   BATLUID,SPACES                                                   
         BNH   HOOK30                                                           
         MVC   HEAD7+1(L'AC@LUID),AC@LUID                                       
         MVI   HEAD7+11,C'?'                                                    
         OC    BATLUID,BATLUID                                                  
         BZ    *+10                                                             
         MVC   HEAD7+11(L'BATLUID),BATLUID                                      
*                                                                               
HOOK30   MVI   HEAD8+1,X'00'                                                    
         TM    RIND2,R2NON                                                      
         BO    HOOK32                                                           
         CLI   SDFVAL,FVDFTU       TEST DRAFT BATCH                             
         BNE   *+16                                                             
         TM    CMPAPRV,APREFF      TEST EFFECTIVE DATE APPROVAL REQD.           
         BZ    HOOK32                                                           
         B     *+12                                                             
         TM    CMPAPRV,APRREG      TEST REGULAR APPROVAL REQD.                  
         BZ    HOOK32                                                           
         MVC   HEAD8+1(L'AC@APRVR),AC@APRVR                                     
         MVI   HEAD8+11,C'?'                                                    
         GOTO1 PERSID,BATAPRVR                                                  
         OC    DUB,DUB                                                          
         BZ    *+10                                                             
         MVC   HEAD8+11(L'DUB),DUB                                              
*                                                                               
HOOK32   GOTO1 DATCON,DMCB,(2,TBAHRADT),(17,HEAD4+68)                           
         GOTO1 (RF),(R1),(2,TBAHREDT),(17,HEAD5+68)                             
         CLI   JPROG,JPDJ                                                       
         BE    HOOK33                                                           
         CLC   TBAHRUDT,SPACES                                                  
         BH    HOOK33                                                           
         GOTO1 (RF),(R1),(2,TODAYC),(17,HEAD6+68)                               
         B     HOOK33A                                                          
HOOK33   GOTO1 (RF),(R1),(2,TBAHRUDT),(17,HEAD6+68)                             
*                                                                               
HOOK33A  MVI   EXLINE,NO           ASSUME ONLY 2 COMMENT LINES REQUIRED         
         XR    RE,RE                                                            
         LA    RF,HEAD7+68                                                      
*                                                                               
         CLI   SDFVAL,FVSAVW                                                    
         BNE   HOOK33B                                                          
         MVC   0(L'AC@BATSD,RF),AC@BATSD  BATCH SAVED                           
         B     HOOK36                                                           
HOOK33B  CLI   SDFVAL,FVDUEU                                                    
         BNE   HOOK33C                                                          
         MVC   0(L'AC@BATUA,RF),AC@BATUA  BATCH UNAPPROVED                      
         B     HOOK36                                                           
HOOK33C  CLI   SDFVAL,FVINSU                                                    
         BNE   HOOK34                                                           
         MVC   0(L'AC@INSUQ,RF),AC@INSUQ  INSTANT UPDATE                        
         B     HOOK36                                                           
HOOK34   CLI   SDFVAL,FVONLU                                                    
         BNE   HOOK38                                                           
         MVC   0(L'AC@ONLUQ,RF),AC@ONLUQ  ONLINE UPDATE                         
HOOK36   LA    RF,L'P(RF)                                                       
         LA    RE,1(RE)                                                         
*                                                                               
HOOK38   TM    BATHSTAT,BHDSACRU                                                
         BZ    HOOK40                                                           
         MVC   0(L'AC@ACRLQ,RF),AC@ACRLQ  ACCRUAL                               
         B     HOOK42                                                           
HOOK40   TM    BATHSTAT,BHDSACRV                                                
         BZ    HOOK44                                                           
         MVC   0(L'AC@ACRRQ,RF),AC@ACRRQ  ACCRUAL REVERSAL                      
HOOK42   LA    RF,L'P(RF)                                                       
         LA    RE,1(RE)                                                         
*                                                                               
HOOK44   TM    BATHSTAT,BHDSCOPY                                                
         BZ    HOOK46                                                           
         MVC   0(L'AC@BATCP,RF),AC@BATCP  BATCH COPIED                          
         B     HOOK50                                                           
HOOK46   TM    BATHSTAT,BHDSREVS                                                
         BZ    HOOK48                                                           
         MVC   0(L'AC@BATRV,RF),AC@BATRV  BATCH REVERSED                        
         B     HOOK50                                                           
HOOK48   TM    BATHSTAT,BHDSGENE                                                
         BZ    HOOK52                                                           
         MVC   0(L'AC@BATGN,RF),AC@BATGN  BATCH GENERATED                       
HOOK50   LA    RE,1(RE)                                                         
*                                                                               
         CH    RE,=H'3'            CHECK IF WE NEED 3 COMMENT LINES             
         BL    HOOK52                                                           
         MVI   EXLINE,YES                                                       
*                                                                               
HOOK52   XR    RF,RF               BATCH TYPE                                   
         IC    RF,SKTYP                                                         
         CVD   RF,DUB                                                           
         UNPK  HEAD4+113(2),DUB                                                 
         OI    HEAD4+114,X'F0'                                                  
*                                                                               
         XR    R1,R1               BATCH TYPE NAME                              
         L     RE,=A(BTYPTAB)                                                   
         USING BTTABD,RE                                                        
HOOK54   IC    R1,BTTYPE                                                        
         LTR   R1,R1                                                            
         BNP   HOOK56                                                           
         CR    R1,RF                                                            
         BE    *+12                                                             
         LA    RE,BTTABQ(RE)                                                    
         B     HOOK54                                                           
         ICM   R1,3,BTDISP                                                      
         LA    RE,DDOUT1                                                        
         AR    RE,R1                                                            
         MVC   HEAD4+116(L'AC@TT001),0(RE)                                      
*                                                                               
HOOK56   XC    DUB,DUB             BATCH MONTH                                  
         MVC   DUB,TBAKBMOS                                                     
         MVI   DUB+L'TBAKBMOS,1                                                 
         GOTO1 DATCON,DMCB,(1,DUB),(6,HEAD5+113)                                
*                                                                               
         GOTO1 BLDRFSQ,HEAD6+113   BATCH REFERENCE/SEQUENCE                     
*                                                                               
*&&US*&& CLI   TBAKBTYP,BT58       BATCH NAME                                   
*&&US*&& BE    HOOK58                                                           
         MVC   HEAD7+97(L'AC@BATN),AC@BATN                                      
         MVI   HEAD7+113,C'?'                                                   
         OC    BATNAME,BATNAME                                                  
         BZ    *+10                                                             
         MVC   HEAD7+113(L'BATNAME),BATNAME                                     
*                                                                               
HOOK58   CLI   EXLINE,YES         EXTRA LINE REQUIRED FOR COMMENTS              
         BE    HOOK60                                                           
         MVI   BOXROWS+8,C'T'     SUB-PROGRAMS 0 AND 1                          
         MVI   BOXROWS+11,C'M'                                                  
         B     HOOK68                                                           
HOOK60   MVI   BOXROWS+9,C'T'                                                   
         MVI   BOXROWS+12,C'M'                                                  
         B     HOOK68                                                           
*                                                                               
HOOK62   MVC   DUB,TBAKBMOS        SUB-PROGRAM 2                                
         MVI   DUB+L'TBAKBMOS,1                                                 
         GOTO1 DATCON,DMCB,(1,DUB),(6,HEAD5+68)                                 
         GOTO1 DATCON,DMCB,(2,TBAHRADT),(17,HEAD4+117)                          
         MVI   BOXROWS+5,C'T'                                                   
         MVI   BOXROWS+8,C'M'                                                   
         B     HOOK68                                                           
*                                                                               
HOOK64   MVI   BOXROWS+4,C'T'      SUB-PROGRAMS 3-7                             
         MVI   BOXROWS+7,C'M'                                                   
         B     HOOK68                                                           
HOOK66   MVI   BOXROWS+2,C'T'      SUB-PROGRAM 8                                
         MVI   BOXROWS+5,C'M'                                                   
HOOK68   DS    0H                                                               
*&&UK*&& MVI   BOXROWS+56,C'B'                                                  
*&&US*&& MVI   BOXROWS+55,C'B'                                                  
         MVI   BBYTE1,HEAD                                                      
         GOTO1 BOXES               BUILD HEADINGS BOXES                         
*                                                                               
HOOKX    MVI   BOXYORN,YES         ALL SUB-PROGRAMS                             
         MVI   BOXWT,1                                                          
         MVI   BOXINIT,0                                                        
         L     RE,RETURN1                                                       
         BR    RE                                                               
         DROP  R3,R4                                                            
         EJECT                                                                  
***********************************************************************         
* END OF PAGE ROUTINE                                                 *         
* NTRY - R1 = NEXT BOX LEVEL                                          *         
***********************************************************************         
         SPACE 1                                                                
PAGER    NTR1                                                                   
         STC   R1,BBYTE1                                                        
         MVC   BBYTES,BBYTE1       SAVE BOX INDICATOR                           
         NI    RIND2,X'FF'-R2FPAGE                                              
         XR    RF,RF               IS PAGE BOTTOM NEAR?                         
         IC    RF,LINE                                                          
         LA    RF,5(RF)                                                         
         CLM   RF,1,MAXLINES                                                    
         BL    PAGE04                                                           
         MVI   FORCEHED,YES        YES - FORCE PAGE/DEFINE NEXT BOXES           
         OI    REPTINDS,REPTINOP                                                
         OI    REPTINDS,REPTISSM                                                
         GOTO1 ACREPORT                                                         
         MVC   BBYTE1,BBYTES       RESTORE BOX INDICATOR                        
         XC    BBYTE2,BBYTE2       CANCEL OTHER SPECIFICS                       
         GOTO1 BOXES                                                            
         L     RF,ADBXAREA                                                      
         MVI   BOXREQ-BOXD(RF),NEW                                              
         OI    RIND2,R2FPAGE       FIRST TX ON PAGE                             
         CLI   RCSUBPRG,2          IF THIS IS THE DETAIL JOURNAL -              
         BH    PAGE02                                                           
         CLI   SDPROF+7,YES            - HOLD NEW PAGE IF RULING                
         BNE   PAGE02                    BETWEEN ITEMS                          
         TM    RIND2,R2LASTX           - UNLESS IN THE MIDDLE OF                
         BZ    PAGEX                     PRINTING A TRANSACTION                 
         NI    RIND2,X'FF'-R2FPAGE                                              
PAGE02   GOTO1 ACREPORT                                                         
         B     PAGEX                                                            
*                                                                               
PAGE04   CLI   BBYTE1,DATA         NO - ONLY DEFINE NEXT BOXES                  
         BNE   PAGE06                                                           
         CLI   BBYTE2,RLINE                                                     
         BE    PAGE06                                                           
         GOTO1 BOXES                                                            
         L     RF,ADBXAREA                                                      
         MVI   BOXREQ-BOXD(RF),NEW                                              
         B     PAGEX                                                            
*                                                                               
PAGE06   GOTO1 BOXES               RULE LINE BEFORE NEXT BOXES                  
         L     RF,ADBXAREA                                                      
         MVI   BOXREQ-BOXD(RF),MIDLINE                                          
         OI    REPTINDS,REPTINOP                                                
         OI    REPTINDS,REPTISSM                                                
         GOTO1 ACREPORT                                                         
         XC    BBYTE2,BBYTE2                                                    
*                                                                               
PAGEX    B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* FORCE NEW PAGE AND PRESET BOXES FOR DATA                            *         
***********************************************************************         
         SPACE 1                                                                
PAGENEW  ST    RE,RETURN2                                                       
         MVI   FORCEHED,YES        FORCE NEW PAGE                               
         OI    REPTINDS,REPTINOP   SUPPRESS PRINT LINE PRINTING                 
         OI    REPTINDS,REPTISSM   SUPPRESS SPACE LINE AFTER MIDLINES           
         GOTO1 ACREPORT            PRINT HEADING BOXES                          
         MVI   BBYTE1,DATA         PREDEFINE DATA BOXES                         
         GOTO1 BOXES                                                            
         L     RF,ADBXAREA                                                      
         MVI   BOXREQ-BOXD(RF),NEW                                              
         GOTO1 ACREPORT                                                         
         XC    BBYTE2,BBYTE2       DON'T RULE LINE BETWEEN DATA ITEMS           
         L     RE,RETURN2                                                       
         BR    RE                                                               
         SPACE 1                                                                
***********************************************************************         
* CLOSE BOXES                                                         *         
***********************************************************************         
         SPACE 1                                                                
OUTBOX   ST    RE,RETURN2                                                       
         L     RF,ADBXAREA                                                      
         MVI   BOXREQ-BOXD(RF),CLOSE                                            
         GOTO1 ACREPORT                                                         
         L     RE,RETURN2                                                       
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* SET BOX COLUMN DETAILS                                              *         
* NTRY BBYTE1 = BOX LEVEL (HEADINGS/DATA/TOTALS)                      *         
***********************************************************************         
         SPACE 1                                                                
BOXES    NTR1                                                                   
         USING COLFRMD,R4                                                       
         L     R3,ADBXAREA                                                      
         USING BOXD,R3                                                          
         MVC   BOXCOLS,SPACES                                                   
         L     RF,FORMAT                                                        
BOXES02  LA    R2,BOXCOLS                                                       
         CLI   0(RF),0             ANY MORE COLUMNS REQUIRED?                   
         BE    BOXES20                                                          
         LA    R4,COLFRM                                                        
BOXES04  CLI   CFID,0              ANY MORE FORMATS DEFINED?                    
         BNE   *+12                                                             
         LA    RF,1(RF)                                                         
         B     BOXES02                                                          
         CLC   CFID,0(RF)          MATCH ON COLUMN ID                           
         BE    *+12                                                             
         LA    R4,COLFRMQ(R4)                                                   
         B     BOXES04                                                          
         ST    RF,FORMATX                                                       
*                                                                               
         XR    R0,R0                                                            
         CLI   BBYTE1,HEAD         TEST FOR HEADINGS BOXES                      
         BNE   BOXES10                                                          
         CLI   CFHLOC,0            COLUMN REQUIRED AT THIS LEVEL?               
         BE    BOXES18                                                          
         IC    R0,CFHLOC                                                        
         XR    RF,RF                                                            
         XR    R1,R1                                                            
         CLI   CFDSP1,0            ANYTHING FOR HEADING LINE 1?                 
         BE    BOXES06                                                          
         LA    RE,MID1                                                          
         AR    RE,R0                                                            
         IC    RF,CFLEN1                                                        
         ICM   R1,3,CFDSP1                                                      
         LA    R1,WORKD(R1)                                                     
         BCTR  RF,0                                                             
         MVC   0(0,RE),0(R1)                                                    
         EX    RF,*-6                                                           
BOXES06  CLI   CFDSP2,0            OR HEADING LINE 2?                           
         BE    BOXES08                                                          
         XR    RF,RF                                                            
         XR    R1,R1                                                            
         LA    RE,MID2                                                          
         AR    RE,R0                                                            
         IC    RF,CFLEN2                                                        
         ICM   R1,3,CFDSP2                                                      
         LA    R1,WORKD(R1)                                                     
         BCTR  RF,0                                                             
         MVC   0(0,RE),0(R1)                                                    
         EX    RF,*-6                                                           
BOXES08  XR    RE,RE                                                            
         IC    RE,CFHWID                                                        
         LTR   RE,RE                                                            
         BZ    BOXES18                                                          
         B     BOXES14                                                          
*                                                                               
BOXES10  XR    RE,RE                                                            
         CLI   BBYTE1,DATA         TEST FOR DATA BOXES                          
         BNE   BOXES12                                                          
         CLI   CFDLOC,0            COLUMNS REQUIRED AT THIS LEVEL?              
         BE    BOXES18                                                          
         IC    R0,CFDLOC                                                        
         IC    RE,CFDWID                                                        
         B     BOXES14                                                          
*                                                                               
BOXES12  CLI   BBYTE1,TOTS         TEST FOR TOTALS BOXES                        
         BNE   BOXES13                                                          
         CLI   CFTLOC,0            COLUMNS REQUIRED AT THIS LEVEL?              
         BE    BOXES18                                                          
         IC    R0,CFTLOC                                                        
         IC    RE,CFTWID                                                        
         B     BOXES14                                                          
*                                                                               
BOXES13  CLI   BBYTE1,TOTSX        TEST FOR SECONDARY TOTALS BOXES              
         BNE   XIT                                                              
         CLI   CFXLOC,0            COLUMNS REQUIRED AT THIS LEVEL?              
         BE    BOXES18                                                          
         IC    R0,CFXLOC                                                        
         IC    RE,CFXWID                                                        
*                                                                               
BOXES14  AR    R2,R0                                                            
         CLC   BOXCOLS,SPACES      SET LEFT LIMIT AT START                      
         BNE   BOXES16                                                          
         BCTR  R2,0                                                             
         MVI   0(R2),C'L'                                                       
         LA    R2,1(R2)                                                         
BOXES16  AR    R2,RE                                                            
         MVI   0(R2),C'C'                                                       
         LTR   RE,RE                                                            
         BZ    BOXES18                                                          
         ST    R2,FULL                                                          
BOXES18  L     RF,FORMATX                                                       
         LA    RF,1(RF)                                                         
         B     BOXES02                                                          
*                                                                               
BOXES20  CLC   BOXCOLS,SPACES      ANY BOXES DEFINED?                           
         BE    XIT                                                              
         L     R2,FULL                                                          
         MVI   0(R2),C'R'          SET RIGHT LIMIT AT END                       
         B     XIT                                                              
         DROP  R3,R4                                                            
         EJECT                                                                  
***********************************************************************         
* GET PERSONAL-ID FROM NEW SECURITY SYSTEM                            *         
* NTRY R1 = PERSON AUTHORIZATION NUMBER                               *         
* EXIT DUB = PERSONAL-ID                                              *         
***********************************************************************         
         SPACE 1                                                                
PERSID   NTR1                                                                   
         XC    DUB,DUB                                                          
         XC    IOKEY,IOKEY         BUILD KEY FOR PERSONAL AUTH RECORD           
         LA    R2,IOKEY                                                         
         USING SA0REC,R2                                                        
         MVI   SA0KTYP,SA0KTYPQ                                                 
         MVC   SA0KAGY,SECALPHA    USE SECURITY ALPHA ID FOR PERSON             
         MVC   SA0KNUM,0(R1)                                                    
         GOTO1 AIO,IORD+IOCLFILE+IO4                                            
         BNE   PERSIDX                                                          
*                                                                               
         L     R2,AIOBUFF                                                       
         LA    R1,SA0DATA                                                       
         USING SAPALD,R1                                                        
         XR    R0,R0                                                            
PERSID02 CLI   0(R1),0                                                          
         BE    PERSIDX                                                          
         CLI   0(R1),SAPALELQ      PERSON PERSONAL-ID ELEMENT                   
         BE    *+14                                                             
         IC    R0,1(R1)                                                         
         AR    R1,R0                                                            
         B     PERSID02                                                         
         MVC   DUB(L'SAPALPID),SAPALPID                                         
*                                                                               
PERSIDX  B     XIT                                                              
         DROP  R1,R2                                                            
         EJECT                                                                  
***********************************************************************         
* PASS GROUP INVOICE TRANSACTIONS DISCRETELY                          *         
* NTRY - R2=A(GROUP INVOICE ELEMENT)                                  *         
* EXIT - AIO3=GROUP INVOICE TRANSACTION RECORD                        *         
***********************************************************************         
          SPACE 1                                                               
PASSGIN   NTR1                                                                  
          TM    RIND3,R3GIN                                                     
          BZ    PGIN01                                                          
          MVC   IOKEY,LGINPKEY                                                  
          GOTO1 AIO,IORD+IOACCDIR+IO2  RE-READ TO ESTABLISH SEQUENCE            
          BE    PGIN02                                                          
          DC    H'0'                                                            
PGIN01    LA    RF,IOKEY           BUILD GROUP INVOICE PASSIVE KEY              
          USING GINPASD,RF                                                      
          XC    GINPKEY,GINPKEY                                                 
          MVI   GINPTYP,GINPTYPQ                                                
          MVC   GINPCPY,BATCPY                                                  
          MVC   GINPINV,GININV-GINELD(R2)                                       
          LA    R1,IOHI+IOACCDIR+IO3                                            
          B     *+8                                                             
PGIN02    LA    R1,IOSEQ+IOACCDIR+IO3                                           
          GOTO1 AIO                                                             
          BE    *+6                                                             
          DC    H'0'                                                            
          CLC   IOKEY(GINPISN-GINPKEY),IOKEYSAV                                 
          BE    *+12                                                            
          NI    RIND3,X'FF'-R3GIN                                               
          B     PGINX                                                           
          GOTO1 AIO,IOGETREC+IOACCMST+IO3                                       
          BE    *+6                                                             
          DC    H'0'                                                            
          MVC   LGINPKEY,IOKEY     SAVE PASSIVE KEY                             
          L     RF,AIOBUFF                                                      
          MVC   LGINAKEY,0(RF)                                                  
          OI    RIND3,R3GIN                                                     
PGINX     XIT1                                                                  
          DROP  RF                                                              
          EJECT                                                                 
***********************************************************************         
* ROUTINE TO BUILD BATCH REFERENCE/SEQUENCE NUMBER                    *         
***********************************************************************         
         SPACE 1                                                                
BLDRFSQ  NTR1                                                                   
         MVC   0(L'SKREF,R1),SKREF                                              
         CLI   SDSEQN,0                                                         
         BE    BLDRFSQX                                                         
         LA    RF,L'SKREF-1(R1)                                                 
         CR    RF,R1                                                            
         BE    *+16                                                             
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-14                                                          
         MVI   1(RF),C'/'                                                       
         EDIT  SDSEQN,(3,2(RF)),FILL=0                                          
BLDRFSQX XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* ROUTINES TO READ/WRITE ACCMST/ACCDIR RECORDS                        *         
***********************************************************************         
         SPACE 1                                                                
IOEXEC   NTR1                                                                   
         ST    R1,IOCTRL                                                        
         MVI   IOERROR,0           CLEAR ERROR BYTE                             
         MVI   IOQUAL,0            ESTABLISH IO QUALIFIERS                      
         TM    IOCTFILE,IORDL      TEST READ DELETES                            
         BZ    *+8                                                              
         OI    IOQUAL,IOQDELT                                                   
*                                  CLEAR READ FOR DELETE BIT                    
         NI    IOCTFILE,255-IORDL                                               
*                                                                               
         LA    R1,IOAREALL         TEST IOAREA NUMBER PASSED                    
         N     R1,IOCTRL           SET IOAREA IN R1                             
         BNZ   *+6                                                              
         DC    H'0'                IOAREA NUMBER MISSING                        
         SRL   R1,4                ESTABLISH IO AREA ADDRESSES                  
         CLM   R1,1,=AL1(IOAREAS)                                               
         BNH   *+6                                                              
         DC    H'0'                INVALID IO AREA                              
         BCTR  R1,0                                                             
         MH    R1,=Y(IOAREALN)                                                  
         L     R0,AIO1                                                          
         AR    R1,R0                                                            
         ST    R1,AIOSAVE          SAVE A(SAVED D/A IODA/IOWORK)                
         LA    R1,L'IODA+L'IOWORK(R1)                                           
         ST    R1,AIOBUFF          SAVE A(DATA RECORD AREA)                     
*                                                                               
         LA    R1,IOFILES          ESTABLISH FILE                               
         N     R1,IOCTRL                                                        
         BNZ   *+6                                                              
         DC    H'0'                FILE NUMBER MISSING                          
         SRL   R1,8                                                             
         BCTR  R1,0                                                             
         MH    R1,=Y(L'ACCDIR)                                                  
         LA    R1,ACCDIR(R1)                                                    
         MVC   IOFILE,0(R1)        SET FILE NAME                                
*                                                                               
         LA    R1,IOCMNDS          ESTABLISH DATAMGR COMMAND                    
         N     R1,IOCTRL                                                        
         BNZ   *+6                                                              
         DC    H'0'                COMMAND NUMBER MISSING                       
         BCTR  R1,0                                                             
         SLL   R1,3                                                             
         TM    IOCTFILE,IOCNTRL    TEST CONTROL FILE IO                         
         BNO   *+12                                                             
         LA    R1,ISCMNDS(R1)      INDEX INTO I/S COMMANDS                      
         B     IOEX2                                                            
         TM    IOCTFILE,IOWKWRK    TEST WKR IO (TO WKFILE)                      
         BNO   *+12                                                             
         LA    R1,WKCMNDS(R1)      INDEX INTO WORKER COMMANDS                   
         B     IOEX2                                                            
         TM    IOCTFILE,IOACCIS    TEST I/S IO (TO ACCDIR)                      
         BNO   *+12                                                             
         LA    R1,ISCMNDS(R1)      INDEX INTO I/S COMMANDS                      
         B     IOEX2                                                            
         LA    R1,DACMNDS(R1)      INDEX INTO D/A COMMANDS                      
*                                                                               
IOEX2    MVC   IOCOMM,0(R1)        SET COMMAND NAME                             
         MVC   IOCTIND,L'IOCOMM(R1)  SET COMMAND TYPE                           
         TM    IOCTFILE,IOWKWRK                                                 
         BNO   IOEX4                                                            
*                                                                               
         ICM   R0,15,AIOBUFF                                                    
         BNZ   *+6                                                              
         DC    H'0'                A(IO BUFFER) MISSING                         
*                                                                               
         GOTO1 DATAMGR,DMCB,IOCOMM,IOFILE,WKID,(R0),AWBUFF                      
         MVC   IOERROR,8(R1)                                                    
         B     IOEXECX                                                          
*                                                                               
IOEX4    TM    IOCTFILE,IOACCIS    TEST I/S DATAMGR IO                          
         BNO   IOEX6                                                            
         TM    IOCTCOMM,IOHI       TEST READ HIGH                               
         BO    *+12                                                             
         TM    IOCTCOMM,IOSEQ      TEST READ SEQUENTIAL                         
         BNO   *+10                                                             
         MVC   IOKEYSAV,IOKEY      SAVE KEY                                     
*                                                                               
         TM    IOCTFILE,IOCNTRL    TEST CONTROL FILE IO                         
         BNO   IOEX5                                                            
         ICM   R0,15,AIOBUFF                                                    
         BNZ   *+6                                                              
         DC    H'0'                A(IO BUFFER) MISSING                         
         GOTO1 DATAMGR,DMCB,(IOQUAL,IOCOMM),IOFILE,IOKEY,(R0)                   
         MVC   IOERROR,8(R1)                                                    
         B     IOEXECX                                                          
*                                                                               
IOEX5    GOTO1 DATAMGR,DMCB,(IOQUAL,IOCOMM),IOFILE,IOKEY,IOKEY                  
         MVC   IOERROR,8(R1)                                                    
         TM    IOERROR,IOEALL-IOEDEL                                            
         BNZ   IOEXECX                                                          
         MVC   IODA,IOKEY+(ACCKDA-ACCRECD)                                      
         B     IOEXECX                                                          
*                                                                               
IOEX6    ICM   R0,15,AIOBUFF       D/A DATAMGR IO                               
         BNZ   *+6                                                              
         DC    H'0'                A(IO BUFFER) MISSING                         
         GOTO1 DATAMGR,DMCB,(IOQUAL,IOCOMM),IOFILE,IODA,(R0),IOWORK             
         MVC   IOERROR,8(R1)                                                    
         TM    IOERROR,IOEALL-IOEDEL                                            
         BNZ   IOEXECX                                                          
         L     R1,AIOSAVE                                                       
         MVC   0(L'IODA,R1),IODA                                                
         MVC   L'IODA(L'IOWORK,R1),IOWORK                                       
         B     IOEXECX                                                          
*                                                                               
IOEXECX  TM    IOERROR,IOEALL                                                   
         BZ    IOEXECQ                                                          
         TM    IOERROR,IOEEOF+IOERNF                                            
         BO    IOEXECH                                                          
         TM    IOCTFILE,IORDL      ARE WE READING FOR DELETES?                  
         BNZ   *+16                                                             
         TM    IOERROR,IOEDEL      YES                                          
         BO    IOEXECQ                                                          
         B     IOEXECL                                                          
         TM    IOERROR,IOEDEL      NO                                           
         BO    IOEXECH                                                          
         B     IOEXECL                                                          
IOEXECQ  CR    RE,RE               NO ERROR - CC EQUAL                          
         B     XIT                                                              
IOEXECL  CLI   *,255               HARD ERROR - CC LOW                          
         B     XIT                                                              
IOEXECH  CLI   *,0                 LOGICAL ERROR - CC HIGH                      
         B     XIT                                                              
