*          DATA SET TAMQHFR    AT LEVEL 001 AS OF 10/10/14                      
***********************************************************************         
*        ROUTINE SENDS MQ MESSAGE WITH INTERNAL COMMERCIAL            *         
*        NUMBER OF COMMERCIAL BEING UPDATED                           *         
*        ON ENTRY ... P1 BYTE 0 = X'80' CONNECTED TO TST              *         
*                                 X'04' CONNECTED TO FQA              *         
*                     P1 = A(INTERNAL COMMERCIAL NUMBER)              *         
*                     P2 BYTE 0 = SYSTEM STATUS 2                     *         
*                     P2 = A(HEXOUT)                                  *         
*                     P3 = A(MQIO)                                    *         
***********************************************************************         
                                                                                
SNDMQHFR NTR1  BASE=*,LABEL=*                                                   
         TM    4(R1),TASYSMQR      IF SENDING OUT MQ MESSAGE                    
         JZ    XIT                 FOR HF REISSUE ...                           
                                                                                
         LA    RE,MQMSG                                                         
         LHI   RF,MQMLNQ                                                        
         LA    R2,BLOCK                                                         
         LR    R3,RF               COPY MQ MESSAGE XML TEMPLATE                 
         MVCL  R2,RE               TEMPLATE INTO BLOCK                          
                                                                                
         ZICM  R2,1(R1),3          R2 = A(INTERNAL COMM' NUMBER)                
         ZICM  R3,5(R1),3          R3 = A(HEXOUT)                               
         ZICM  R4,9(R1),3          R4 = A(MQIO)                                 
                                                                                
         MVC   BLOCK(16),=C'TALHOLD*********'                                   
         TM    0(R1),TESTSYS                                                    
         JZ    *+10                                                             
         MVC   BLOCK(3),=C'TST'                                                 
         TM    0(R1),FQASYS                                                     
         JZ    *+10                                                             
         MVC   BLOCK(3),=C'FQA'                                                 
                                                                                
         GOTO1 (R3),DMCB,0(R2),BLOCK+MQMCOM-MQMSG,4,0                           
                                                                                
         GOTO1 (R4),DMCB,=CL8'PUT',BLOCK,MQMLNQ,0,0,DUB                         
         J     XIT                                                              
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
MQMSG    DC    CL16' '                                                          
         DC    C'<?xml version="1.0" encoding="UTF-8"?>'                        
         DC    C'<HoldingFeeUpdateAlert>'                                       
         DC    C'<AssetPrimaryKey>'                                             
MQMCOM   DC    CL8' '                                                           
         DC    C'</AssetPrimaryKey>'                                            
         DC    C'</HoldingFeeUpdateAlert>'                                      
MQMLNQ   EQU   *-MQMSG                                                          
                                                                                
         LTORG                                                                  
         EJECT                                                                  
