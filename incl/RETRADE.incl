*          DATA SET RETRADE    AT LEVEL 008 AS OF 01/30/01                      
**RETRADE**                                                                     
*                                                                               
*   RETRADE :  AGENCY EOP RECORDS MUST BE ADDED.  THE ORDER HAS                 
*        BEEN RECOGNIZED AS A 'TRADE' ORDER.  THE AGENCY CORPORATE AND          
*        AGENCY OFFICE RECORDS WILL DETAIL WHETHER A TRADE ALTERNATE            
*        CODE HAS BEEN ESTABLISHED, OR WHETHER ONE MUST BE SET.                 
*                                                                               
*        P1  =   A(PASSBACK AREA FOR ALTERNATE TRADE CODE: 1 BYTE)              
*        P2  =   A(CONTRACT RECORD)                                             
*        P3  =   A(AVAILABLE IO AREA)                                           
*                                                                               
RETRADE  NTR1  BASE=*,LABEL=*                                                   
         L     R3,0(R1)            SET A(PASSBACK AREA)                         
         XC    0(1,R3),0(R3)       CLEAR PASSBACK                               
         XC    KEY,KEY                                                          
         MVI   KEY,X'1A'           SET KEY FOR AGENCY2 RECORD                   
         MVC   KEY+19(6),RCONKAGY  INSERT AGY/AOF INTO KEY                      
*                                     FIRST ACCESS WILL BE FOR THE              
*                                     AGENCY+AGYOFF OF THE ORDER                
         MVC   KEY+25(2),RCONKREP  INSERT REP CODE INTO KEY                     
         GOTO1 VHIGH                                                            
         CLC   KEY(27),KEYSAVE     KEY FOUND ON FILE?                           
         BE    *+6                 YES - MUST BE ON FILE                        
         DC    H'0'                NO  -                                        
         MVI   UPDATE,C'Y'                                                      
         GOTO1 VGETREC,DMCB,IOAREA                                              
         LA    R6,IOAREA                                                        
         USING RAGY2REC,R6                                                      
         LA    RF,RAGY2FXE         1ST ELEMENT IN RAGY2REC                      
RETR0020 EQU   *                                                                
         CLI   0(RF),0             END OF RECORD?                               
         BNE   *+6                 NO  -                                        
         DC    H'0'                MUST FIND THE ELEMENT                        
         CLI   0(RF),X'1F'         AGENCY ELEMENT?                              
         BE    RETR0040            YES -                                        
         ZIC   RE,1(RF)                                                         
         AR    RF,RE                                                            
         B     RETR0020            GO BACK FOR NEXT                             
RETR0040 EQU   *                                                                
         CLI   RAG2TRAD-RAG2ELEM(RF),C' '                                       
*                                  ANYTHING IN ALT TRADE CODE FIELD?            
         BNH   RETR0060            NO  - MUST GENERATE NEXT CODE                
         MVC   0(1,R3),RAG2TRAD-RAG2ELEM(RF)                                    
*                                  PASS BACK ALT TRADE CODE                     
         B     RETR0260            EXIT ROUTINE                                 
RETR0060 EQU   *                                                                
*                                                                               
*   IF ORDER IS FOR A STAND-ALONE AGENCY (NO OFFICES), THE 'CORPORATE'          
*        AGENCY2 RECORD MUST BE UPDATED TO INDICATE ITS USE                     
*                                                                               
         CLC   RCONKAOF,=C'  '     AGY OFFICE, OR CORPORATE?                    
         BH    RETR0080            AGENCY OFFICE                                
         MVI   RAG2TRAD-RAG2ELEM(RF),C'0'                                       
*                                  NO  - CORPORATE:  INSERT A ZERO              
*                                     (X'F0' - ALPHA CHAR)                      
         GOTO1 VPUTREC,DMCB,IOAREA                                              
         MVI   0(R3),C'0'          PASS BACK A ZERO                             
         B     RETR0260            EXIT ROUTINE                                 
RETR0080 EQU   *                                                                
*                                                                               
*   IF ORDER IS FOR AN AGENCY OFFICE, BOTH THE CORPORATE AND THE                
*        AGENCY/OFFICE RECORDS MUST BE UPDATED.  THE CORPORATE                  
*        WILL CONTAIN THE LAST UTILIZED CODE.  THE NEXT CODE                    
*        WILL REPLACE THE LAST CODE ON THE CORPORATE RECORD, AND                
*        BE INSERTED INTO THE AGENCY OFFICE RECORD.                             
*                                                                               
         MVC   KEY+23(2),=C'  '    CLEAR AGENCY OFFC FIELD                      
         GOTO1 VHIGH                                                            
         CLC   KEY(27),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                CORPORATE MUST BE ON FILE                    
         MVI   UPDATE,C'Y'                                                      
         GOTO1 VGETREC,DMCB,IOAREA                                              
         LA    R6,IOAREA                                                        
         USING RAGY2REC,R6                                                      
         LA    RF,RAGY2FXE         1ST ELEMENT IN RAGY2REC                      
RETR0100 EQU   *                                                                
         CLI   0(RF),0             END OF RECORD?                               
         BNE   *+6                 NO  -                                        
         DC    H'0'                MUST FIND THE ELEMENT                        
         CLI   0(RF),X'1F'         AGENCY ELEMENT?                              
         BE    RETR0120            YES -                                        
         ZIC   RE,1(RF)                                                         
         AR    RF,RE                                                            
         B     RETR0100            GO BACK FOR NEXT                             
RETR0120 EQU   *                                                                
         CLI   RAG2TRAD-RAG2ELEM(RF),C' '                                       
*                                  ANYTHING IN ALT TRADE CODE FIELD?            
         BNH   RETR0180            NO  - MUST GENERATE NEXT CODE                
         MVC   0(1,R3),RAG2TRAD-RAG2ELEM(RF)                                    
*                                  GET PRESENT HIGH CODE                        
         LA    RE,CODELIST         FIND CURRENT CODE IN LIST                    
RETR0140 EQU   *                                                                
         CLI   0(RE),X'FF'         END OF LIST?                                 
         BNE   *+6                 NO                                           
         DC    H'0'                NOT IN LIST!!                                
         CLC   0(1,RE),0(R3)       PRESENT CODE FOUND?                          
         BE    RETR0160            YES                                          
         LA    RE,1(RE)            BUMP TO NEXT SLOT                            
         B     RETR0140            GO BACK FOR NEXT                             
RETR0160 EQU   *                                                                
         LA    RE,1(RE)            BUMP TO NEXT SLOT                            
         CLI   0(RE),X'FF'         END OF LIST?                                 
         BNE   *+6                 NO                                           
         DC    H'0'                NO CODES LEFT!! WHAT NOW?                    
         MVC   0(1,R3),0(RE)       INSERT NEXT CODE INTO PASSBACK               
         MVC   RAG2TRAD-RAG2ELEM(1,RF),0(R3)                                    
*                                  INSERT NEXT CODE INTO CORP RECRD             
         B     RETR0200                                                         
RETR0180 EQU   *                                                                
         MVI   RAG2TRAD-RAG2ELEM(RF),C'1'                                       
*                                  NO CODE PREVIOUSLY ON FILE                   
*                                     MAKE IT '1' (X'F1')                       
         MVC   0(1,R3),RAG2TRAD-RAG2ELEM(RF)                                    
*                                  PASS BACK NEW NUMBER                         
         B     RETR0200            DO REWRITE                                   
RETR0200 EQU   *                                                                
         GOTO1 VPUTREC,DMCB,IOAREA                                              
*                                  REWRITE CORPORATE AGENCY2 RECORD             
         MVC   KEY+23(2),RCONKAOF  REINSERT AGENCY OFFICE                       
         GOTO1 VHIGH               RETRIEVE AGENCY+AGYOFF KEY                   
         CLC   KEY(27),KEYSAVE     KEY FOUND?                                   
         BE    *+6                                                              
         DC    H'0'                MUST BE ON FILE                              
         MVI   UPDATE,C'Y'                                                      
         GOTO1 VGETREC,DMCB,IOAREA                                              
         LA    R6,IOAREA                                                        
         USING RAGY2REC,R6                                                      
         LA    RF,RAGY2FXE         1ST ELEMENT IN RAGY2REC                      
RETR0220 EQU   *                                                                
         CLI   0(RF),0             END OF RECORD?                               
         BNE   *+6                 NO  -                                        
         DC    H'0'                MUST FIND THE ELEMENT                        
         CLI   0(RF),X'1F'         AGENCY ELEMENT?                              
         BE    RETR0240            YES -                                        
         ZIC   RE,1(RF)                                                         
         AR    RF,RE                                                            
         B     RETR0220            GO BACK FOR NEXT                             
RETR0240 EQU   *                                                                
         MVC   RAG2TRAD-RAG2ELEM(1,RF),0(R3)                                    
*                                  INSERT NEW NEXT CODE INTO AGYOFF REC         
         GOTO1 VPUTREC,DMCB,IOAREA                                              
*                                  REWRITE AGENCY/AGYOFF RECORD                 
RETR0260 EQU   *                                                                
         XIT1                                                                   
CODELIST DC    C'0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ'                          
         DC    X'FFFF'             DELIMITER                                    
         DC    H'0'                                                             
         DROP  R6                                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
