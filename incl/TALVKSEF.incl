*          DATA SET TALVKSEF   AT LEVEL 003 AS OF 09/24/14                      
*              VALIDATE KEY / REC FOR SCREEN TAREPEF                            
*                                                                               
VREC     NTR1                                                                   
         TM    TWAWHEN,X'04'       OVERNIGHT?                                   
         BO    VREC10                                                           
         MVI   REQSML,C'L'         SOON / LONG JOB                              
                                                                                
VREC10   MVI   PRINTOPT,C'Y'       PRESET OPTIONS                               
         MVI   TRACOPT,C'N'                                                     
         MVI   TAPEOPT,C'Y'                                                     
                                                                                
         LA    R2,SPLYEARH         VALIDATE YEAR                                
         GOTO1 ANY                                                              
         CLI   5(R2),4             MUST BE 4 CHARACTERS                         
         BNE   FLDINV                                                           
         CLC   8(2,R2),=C'19'                                                   
         BE    *+14                                                             
         CLC   8(2,R2),=C'20'                                                   
         BNE   FLDINV                                                           
         MVC   THISYEAR,WORK                                                    
         MVC   WORK(4),ALLZEROS                                                 
         MVZ   WORK(4),8(R2)                                                    
         CLC   WORK(4),ALLZEROS                                                 
         BNE   FLDINV                                                           
*                                                                               
         MVC   WORK(4),THISYEAR                                                 
         MVC   WORK+4(4),=C'0101'                                               
         GOTO1 DATCON,DMCB,(9,WORK),(1,TIQPSTR)                                 
         MVC   WORK(4),THISYEAR                                                 
         MVC   WORK+4(4),=C'1231'                                               
         GOTO1 DATCON,DMCB,(9,WORK),(1,TIQPEND)                                 
*                                                                               
         LA    R2,SPLEMPH          VALIDATE EMPLOYER                            
         GOTO1 ANY                                                              
         MVC   TIFEMP,WORK                                                      
         GOTO1 RECVAL,DMCB,TLEMCDQ,(R2),0                                       
*                                                                               
         XC    TIFUNIT,TIFUNIT                                                  
         LA    R2,SPLUNTH          VALIDATE TAX UNIT                            
         GOTO1 ANY                                                              
                                                                                
*        CLI   RECNUM,T4B          BUILD PROGRAMS CAN DO ALL                    
*        BE    VREC20                                                           
*        CLI   RECNUM,NR4B                                                      
*        BE    VREC20                                                           
*        BNE   VREC25                                                           
                                                                                
         CLI   RECNUM,RL1                                                       
         BNE   VREC20                                                           
         CLC   =C'QC ',WORK        QUEBEC ONLY ALLOWED FOR RL1                  
         BNE   FLDINV                                                           
         B     VREC28                                                           
                                                                                
VREC20   CLC   =C'ALL',WORK        ALLOW REQUESTING ALL UNITS                   
         BE    VREC30                                                           
         CLI   RECNUM,TR                                                        
         BE    FLDINV                                                           
                                                                                
VREC25   CLC   =C'CN ',WORK                                                     
         BE    VREC30                                                           
                                                                                
VREC28   MVC   TGCTRY,=C'CA'       HAS TO BE CANADIAN PROVINCE                  
         GOTO1 TAXVAL,DMCB,(X'FF',WORK)                                         
         BNE   FLDINV                                                           
         MVC   TIFUNIT,WORK                                                     
*                                                                               
VREC30   LA    R2,SPLOPTH          VALIDATE OPTIONS                             
         BAS   RE,VOPTS                                                         
         B     XIT                                                              
                                                                                
         EJECT                                                                  
