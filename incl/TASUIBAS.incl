*          DATA SET TASUIBAS   AT LEVEL 009 AS OF 12/29/14                      
***********************************************************************         
*        ROUTINE SETS SUI BASE AMOUNT                                 *         
*        ON ENTRY ... P1 = A(CHECK RECORD)                            *         
*                     P2 = A(TAX UNIT CODE)                                     
*                     P3 = A(TMTAXBLE)                                          
*                     P4 = A(MEDICARE OVER)                                     
*  ALSO THISEARN ALSO HAS TO BE SET                                             
*  EXIT - P2 =A(FIELD TO SAVE SUI BASE)                                         
*         P4=  TAX UNIT CODE SUI APPLIED TO FOR THIS CHECK                      
***********************************************************************         
* FOR A GIVEN UNIT ONLY CALCULATE SUI IF THE UNIT IS HAS THE HIGHEST            
* YTD EARNINGS FOR THAT CHECK.  IF THE UNIT WE ARE PROCESSING                   
* IS NOT A UNIT WHICH HAD THE HIGHEST YTD EARNINGS THE SUI SHOULD BE            
* ZERO FOR THAT CHECK                                                           
* THE UNIT WITH THE HIGHEST EARNINGS - TAKE SUI WAGES FROM TAYETSUI             
***********************************************************************         
                                                                                
SUIBASE  NTR1  BASE=*,LABEL=*                                                   
         L     R3,4(R1)           R3 = A(TAX UNIT CODE)                         
         L     R2,8(R1)           R2=A(TMTAXABLE)                               
         L     R6,0(R1)                                                         
                                                                                
         CLC   =C'P+',TIEMP       NON P+ GO STRAIGHT TO MEDICARE                
         BNE   SB400              COVERAGE SECTION                              
*                                                                               
* ADJUST THISEARN WITH DUE COMPANY                                              
*                                                                               
* COMFIRM WITH SUSHEEL- DUE COMPANY WILL ONLY BE ON POSTIVE CHECKS              
*                                                                               
* NO-NOP 05/14 B/C OF NEW TATUWAAD FIELD                                        
*                                                                               
*&&DO                                                                           
         L     R6,0(R1)                                                         
         MVI   ELCODE,TADWELQ                                                   
         BRAS  RE,GETEL           ELEMENTS                                      
         JNE   SB08                                                             
         USING TADWD,R6                                                         
         L     R0,TADWREC         R0=AMOUNT RECOVERED                           
************                                                                    
         USING TATUD,R6                                                         
         L     R6,0(R1)                                                         
         MVI   ELCODE,TATUELQ     READ THROUGH ALL YTD WITHHOLDING              
         BRAS  RE,GETEL           ELEMENTS                                      
         JE    *+8                                                              
SB02     BRAS  RE,NEXTEL                                                        
         JNE   SB08                                                             
         CLI   TATUUNIT+2,C' '    IF OPTIONAL TAX UNIT CODE IS NOT              
         JH    SB02               PROVIDED, READ ALL STATE YTD                  
         CLC   TATUUNIT,=C'FD '   WITHHOLDING ELEMENTS                          
         JE    SB02                                                             
         CLC   TATUUNIT,=C'CN '                                                 
         JE    SB02                                                             
*                                                                               
*        ZICM  RE,TATUWAGE,(15)                                                 
*        ZICM  RF,TATUSTRE,(15)                                                 
         ZICM  RE,TATUWAAD,(15)                                                 
         ZICM  RF,TATUTNAD,(15)                                                 
         AR    RE,RF              RE= GROSS EARNINGS FOR UNIT                   
         LPR   RE,RE              ALWAYS WORK WITH POSITIVE NUMBERS             
*                                 SO WE CAN DEAL W VOIDS TOO                    
         CLC   TATUUNIT,0(R3)                                                   
         BE    SB06                                                             
         SR    R0,RE              DIDNT FIND UNIT AND STILL MORE DUE            
         BP    SB02               COMPANY - THEN READ NEXT UNIT                 
         J     SB08               NO DUE COMPANY LEFT OVER                      
                                                                                
                                                                                
         J     SB02                                                             
*  FOUND UNIT  - R0 =DUE COMPANY NOT APPLIED YET ,RE=EARNINGS                   
SB06     DS    0C                                                               
         SR    RE,R0                                                            
         BP    *+14               IF REMAINING DUE COMP > EARNINGS THEN         
         XC    THISEARN,THISEARN  EARNINGS =0 FOR THIS UNIT                     
         B     *+8                ELSE                                          
         STCM  RE,15,THISEARN     EARNINGS =EARNINGS-REMAINING DUE COMP         
*&&                                                                             
*                                                                               
         L     R6,0(R1)                                                         
         MVI   ELCODE,TADWELQ                                                   
         BRAS  RE,GETEL           ELEMENTS                                      
         JNE   SB08                                                             
*                                                                               
         XC    THISEARN,THISEARN                                                
*                                                                               
         USING TATUD,R6                                                         
         L     R6,0(R1)                                                         
         MVI   ELCODE,TATUELQ     ACCUMULATE EARNINGS AFTER DUE COMP            
         BRAS  RE,GETEL                                                         
         JE    *+8                                                              
SB02     BRAS  RE,NEXTEL                                                        
         JNE   SB08                                                             
         CLI   TATUUNIT+2,C' '    IF OPTIONAL TAX UNIT CODE IS NOT              
         JH    SB02               PROVIDED, READ ALL STATE YTD                  
         CLC   TATUUNIT,=C'FD '   WITHHOLDING ELEMENTS                          
         JE    SB02                                                             
         CLC   TATUUNIT,=C'CN '                                                 
         JE    SB02                                                             
         CLC   TATUUNIT,0(R3)                                                   
         JNE   SB02                                                             
*                                                                               
         ZICM  RE,TATUWAAD,(15)                                                 
*        ZICM  RF,TATUSTRE,(15)                                                 
         ZICM  RF,TATUTNAD,(15)                                                 
         AR    RE,RF              RE= GROSS EARNINGS FOR UNIT                   
         STCM  RE,15,THISEARN                                                   
*                                                                               
*----------------------                                                         
* GET TACY TO GET SUI |                                                         
*----------------------                                                         
SB08     LA    R5,8(R2)           R5 = A(FIELD TO SAVE SUI BASE IN)             
         USING TACYD,R6                                                         
         L     R6,0(R1)                                                         
         MVI   ELCODE,TACYELQ     READ THROUGH ALL YTD WITHHOLDING              
         BRAS  RE,GETEL           ELEMENTS                                      
         JE    *+8                                                              
SB10     BRAS  RE,NEXTEL                                                        
******** JNE   XIT                                                              
         JNE   SB30                                                             
         CLI   TACYUNIT+2,C' '    IF OPTIONAL TAX UNIT CODE IS NOT              
         JH    SB10               PROVIDED, READ ALL STATE YTD                  
         CLC   TACYUNIT,=C'FD '   WITHHOLDING ELEMENTS                          
         JE    SB10                                                             
         CLC   TACYUNIT,=C'CN '                                                 
         JE    SB10                                                             
         CLC   TACYUNIT,0(R3)                                                   
         BNE   SB10                                                             
         MVC   0(L'TACYTSUI,R5),TACYTSUI    IF SUI PROVIDED TAKE IT             
         CLI   TACYLEN,TACYLN4Q              ELSE SUI MUST BE OVER              
         JE    *+10                          STATE LIMIT SO MAKE IT 0           
SB30     XC    0(L'TAYETSUI,R5),0(R5)                                           
                                                                                
         L     R6,0(R1)                                                         
         MVI   ELCODE,TATUELQ     IF NO TATU ELEMENTS GO STRAIGT                
         BRAS  RE,GETEL           TO MEDICARE COVERAGE SECTION                  
         JNE   SB400                                                            
* ---------------------                                                         
* GET TAYETFUI       |                                                          
* ---------------------                                                         
         USING TAYED,R6                                                         
         L     R6,0(R1)                                                         
         MVI   ELCODE,TAYEELQ     READ THROUGH ALL YTD WITHHOLDING              
         BRAS  RE,GETEL           ELEMENTS                                      
         JE    SB80                                                             
         JNE   XIT                                                              
*                                                                               
*        DC    H'0'                                                             
*                                                                               
SB70     BRAS  RE,NEXTEL                                                        
         JNE   XIT                                                              
SB80     CLI   TAYETYPE,C'C'                                                    
         JNE   SB70                                                             
         LR    R4,R6              R0= TAYE ELEMENT                              
         DROP  R6                                                               
         USING TAYED,R4                                                         
                                                                                
* ---------------------------------------------------------                     
* FIGURE OUT FUIWAGES FOR THIS STATE FROM TATU ELEMENTS                         
* KEEP SUBTRACTING THE TATU EARNINGS FROM THE FUI LIMIT FOR                     
* THIS CHECK UNTIL WE FIND THE UNIT                                             
* ---------------------------------------------------------                     
         USING TATUD,R6                                                         
         LA    R5,4(R2)           R5 = A(FIELD TO SAVE FUI BASE IN)             
         L     R0,TAYETFUI        R0= FUI LIMIT FOR THIS CHECK                  
         LPR   R0,R0              ALWAYS WORK WITH POSITIVE NUMBERS             
         L     R6,0(R1)                                                         
         MVI   ELCODE,TATUELQ     READ THROUGH ALL YTD WITHHOLDING              
         BRAS  RE,GETEL           ELEMENTS                                      
         JE    *+8                                                              
SB100    BRAS  RE,NEXTEL                                                        
         JNE   SB200                                                            
         CLI   TATUUNIT+2,C' '    IF OPTIONAL TAX UNIT CODE IS NOT              
         JH    SB100              PROVIDED, READ ALL STATE YTD                  
         CLC   TATUUNIT,=C'FD '   WITHHOLDING ELEMENTS                          
         JE    SB100                                                            
         CLC   TATUUNIT,=C'CN '                                                 
         JE    SB100                                                            
*                                                                               
*        ZICM  RE,TATUWAGE,(15)                                                 
*        ZICM  RF,TATUSTRE,(15)                                                 
         ZICM  RE,TATUWAAD,(15)                                                 
         ZICM  RF,TATUTNAD,(15)                                                 
         AR    RE,RF              RE= GROSS EARNINGS FOR UNIT                   
*                                                                               
         LPR   RE,RE              ALWAYS WORK WITH POSITIVE NUMBERS             
*                                 SO WE CAN DEAL W VOIDS TOO                    
         CLC   TATUUNIT,0(R3)                                                   
         BE    SB140                                                            
         SR    R0,RE                                                            
         BP    SB100                                                            
* WE CANT STOP AT ZERO FOR VOIDS WE NEED TO SEE THE NEGATIVE AMOUNTS            
* SINCE WE ARE BACKING OUT NUMBERS                                              
         TM    THISEARN,X'80'    IF WE ARE DEALING W VOIDS (NEG WAGES)          
         BO    *+6               DONT STOP AT ZERO - NEGATIVE IS FINE           
         SR    R0,R0             ALREADY REACHED FUI LIMIT  FUI=0               
         J     SB100                                                            
*  FOUND UNIT                                                                   
SB140    DS    0C                                                               
*                                                                               
*  VOID CHECK LOGIC                                                             
         TM    THISEARN,X'80'    IF WE ARE DEALING W VOIDS (NEG WAGES)          
         BNO   SB180                                                            
         XC    0(4,R5),0(R5)                                                    
         CHI   R0,0              IF NO FUI LEFT TO BACK OUT THEN EXIT           
         BNH   SB200                                                            
         LNR   R0,R0                                                            
         STCM  R0,15,0(R5)       DEFAULT FUI TO FUI LIMIT                       
         LPR   R0,R0                                                            
         B     SB190                                                            
*                                                                               
SB180    STCM  R0,15,0(R5)              DEFAULT FUI TO FUI LIMIT                
         CHI   R0,0                     NO FUI LEFT OVER                        
         BNH   SB200                    SO JUST EXIT                            
SB190    LR    RF,R0                                                            
         SR    RF,RE                    R1=LIMIT - EARNINGS                     
         BNP   SB200                    IF LIMIT < WAGES EXIT                   
*                                                                               
         TM    THISEARN,X'80'    IF WE ARE DEALING W VOIDS (NEG WAGES)          
         BNO   *+6               THEN FLIP THE WAGES BACK TO NEGATIVE           
         LNR   RE,RE                                                            
*                                                                               
         STCM  RE,15,0(R5)              ELSE FUI = WAGES                        
                                                                                
**********************************************************************          
* FIGURE OUT FICA FOR THIS STATE FROM TATU ELEMENTS                             
* KEEP SUBTRACTING THE TATU EARNINGS FROM THE FICA LIMIT FOR                    
* THIS CHECK UNTIL WE FIND THE UNIT                                             
* ---------------------------------------------------------                     
         USING TATUD,R6                                                         
SB200    LA    R5,12(R2)          R5 = A(FIELD TO SAVE FIC BASE IN)             
                                                                                
         L     R0,TAYETFIC        R0= FICA LIMIT FOR THIS CHECK                 
         LPR   R0,R0              ALWAYS WORK WITH POSITIVE NUMBERS             
         L     R6,0(R1)                                                         
         MVI   ELCODE,TATUELQ     READ THROUGH ALL YTD WITHHOLDING              
         BRAS  RE,GETEL           ELEMENTS                                      
         JE    *+8                                                              
SB260    BRAS  RE,NEXTEL                                                        
         JNE   SB400                                                            
         CLI   TATUUNIT+2,C' '    IF OPTIONAL TAX UNIT CODE IS NOT              
         JH    SB260              PROVIDED, READ ALL STATE YTD                  
         CLC   TATUUNIT,=C'FD '   WITHHOLDING ELEMENTS                          
         JE    SB260                                                            
         CLC   TATUUNIT,=C'CN '                                                 
         JE    SB260                                                            
*        ZICM  RE,TATUWAGE,(15)                                                 
*        ZICM  RF,TATUSTRE,(15)                                                 
         ZICM  RE,TATUWAAD,(15)                                                 
         ZICM  RF,TATUTNAD,(15)                                                 
         AR    RE,RF              RE= GROSS EARNINGS FOR UNIT                   
         CLC   TATUUNIT,0(R3)                                                   
         BE    SB340                                                            
         SR    R0,RE                                                            
         BP    SB260                                                            
* WE CANT STOP AT ZERO FOR VOIDS WE NEED TO SEE THE NEGATIVE AMOUNTS            
* SINCE WE ARE BACKING OUT NUMBERS                                              
         TM    THISEARN,X'80'    IF WE ARE DEALING W VOIDS (NEG WAGES)          
         BO    *+6               DONT STOP AT ZERO - NEGATIVE IS FINE           
         SR    R0,R0              ALREADY REACHED FUI LIMIT                     
         J     SB260                                                            
*  FOUND UNIT                                                                   
SB340    DS    0C                                                               
*  VOID CHECK LOGIC                                                             
         TM    THISEARN,X'80'    IF WE ARE DEALING W VOIDS (NEG WAGES)          
         BNO   SB380                                                            
         XC    0(4,R5),0(R5)                                                    
         CHI   R0,0              IF NO FUI LEFT TO BACK OUT THEN EXIT           
         BNH   SB400                                                            
         LNR   R0,R0                                                            
         STCM  R0,15,0(R5)              DEFAULT FUI TO FUI LIMIT                
         LPR   R0,R0                                                            
         B     SB390                                                            
SB380    STCM  R0,15,0(R5)              DEFAULT FUI TO FUI LIMIT                
         CHI   R0,0                     NO FUI LEFT OVER                        
         BNH   SB400                    SO JUST EXIT                            
SB390    LR    RF,R0                                                            
         SR    RF,RE                    R1=LIMIT - EARNINGS                     
         BNP   SB400                    IF LIMIT < WAGES EXIT                   
         TM    THISEARN,X'80'    IF WE ARE DEALING W VOIDS (NEG WAGES)          
         BNO   *+6               THEN FLIP THE WAGES BACK TO NEGATIVE           
         LNR   RE,RE                                                            
*                                                                               
         STCM  RE,15,0(R5)              ELSE FUI = WAGES                        
                                                                                
         DROP  R6,R4                                                            
**********************************************************************          
* FIGURE OUT MEDICARE  , AND MEDICARE OVER 200K LIMIT                           
* FOR THIS STATE FROM TATU ELEMENTS                                             
* KEEP SUBTRACTING THE TATU EARNINGS FROM THE MEDICARE LIMIT FOR                
* THIS CHECK UNTIL WE FIND THE UNIT                                             
* ---------------------------------------------------------                     
         USING TAYED,R6                                                         
SB400    L     R6,0(R1)                                                         
         L     R5,12(R1)          MEDICARE OVER OUTPUT FIELD                    
         MVI   ELCODE,TAYEELQ     READ THROUGH ALL YTD WITHHOLDING              
         BRAS  RE,GETEL           ELEMENTS                                      
         JE    SB580                                                            
         DC    H'0'                                                             
SB570    BRAS  RE,NEXTEL                                                        
         JNE   XIT                                                              
SB580    CLI   TAYETYPE,C'C'                                                    
         JNE   SB570                                                            
         ZICM  R0,TAYEMED,(15)                                                  
         ZICM  RF,TAYETMED,(15)                                                 
         SR    R0,RF              R0=MEDICARE YTD BEFORE THIS CHECK             
         BNP   SB600              IF YTD BEFORE THIS CHECK                      
         CL    R0,=F'20000000'    IF YTD BEFORE THIS CHECK ALREADY              
         BL    SB600              OVER 200K THEN                                
* YTD MEDICARE BEFORE THIS CHECK >200K                                          
* SINCE THIS IS A POSITIVE CHECK - IF WE ARE OVER THE 200K LIMIT                
* PRIOR TO THIS CHECK RUN THEN THE ENTIRE AMOUNT GOES TO OVER 200K              
* BUCKET                                                                        
         CLI   RECNUM,UREPORT     ONLY ADJUST MEDICARE                          
         JNE   XIT                FOR UREPORT                                   
         CLC   RYEAR,=C'2012'     ONLY HAVE NEW MEDICARE OVER 200K              
         BNH   XIT                FOR 2013 AND LATER                            
         TM    THISEARN,X'80'     VOID CHECK?                                   
         BO    SB590                                                            
         LA    R5,16(R2)                                                        
         XC    0(4,R5),0(R5)      MEDICARE = 0  AND                             
         L     R5,12(R1)                                                        
         MVC   0(4,R5),THISEARN   MEDICARE OVER = THISEARN                      
         J     SB900                                                            
* VOIDS WHERE YTD BEFORE THIS CHECK IS ALREADY OVER 200K                        
* WE HAVE TO FIGURE OUT HOW MUCH OVER 200K-APPLIES TO OVER 200K BUCKET          
* COMPARE OVER AMOUNT TO THISEARN..IF OVERAMOUNT <THISEARN THEN                 
* BACK OUT OVERAMOUNT FROM OVER 200K BUCKET                                     
* REGULAR MEDICARE BUCKET = THISEARN-OVERAMOUNT                                 
* IF OVERAMOUNT >=THISEARN THEN BACK OUT THISEARN FROM OVER200K BUCKET          
* REGULAR MEDICARE BUCKET = 0                                                   
SB590    DS    0C                                                               
         L     R4,=F'20000000'    R4=MEDICARE LIMT TILL WE HIT 200K             
         SR    R0,R4              YTD BEFORE THIS CHECK -200K LIMIT             
*                                 R0=AMOUNT OVER 200K LIMIT                     
         ZICM  RF,THISEARN,(15)                                                 
         LPR   RF,RF              ALWAYS WORK W POSITVE NUMBER HERE             
         CR    R0,RF              TO DO THIS COMPARE                            
         BL    SB594                                                            
*  OVERAMOUNT >=THISEARN                                                        
SB592    DS    0C                                                               
         LA    R5,16(R2)                                                        
         XC    0(4,R5),0(R5)      MEDICARE = 0  AND                             
         L     R5,12(R1)                                                        
         ZICM  RE,THISEARN,(15)                                                 
         LNR   RE,RE                                                            
         STCM  RE,15,0(R5)        MEDICARE OVER= THISEARN (NEGATED)             
         J     SB900                                                            
*  OVERAMOUNT < THISEARN                                                        
SB594    DS    0C                                                               
         LA    R5,16(R2)                                                        
         SR    RF,R0              RF=THISEARN-OVER AMOUNT                       
         LNR   RF,RF              MEDICARE=EARNINGS-OVERAMOUNT(NEGATE)          
         STCM  RF,15,0(R5)                                                      
         L     R5,12(R1)          OVERAMOUNT                                    
         LNR   R0,R0                                                            
         STCM  R0,15,0(R5)        BACK OUT OVERAMOUNT FROM OVER 200K            
         J     SB900                                                            
         DROP  R6                                                               
* YTD MEDICARE BEFORE THIS CHECK < 200K                                         
* SO WE HAVE TO PROCESS THIS CHECK TO SEE IF WE GO OVER 200K                    
SB600    DS    0H                                                               
         USING TATUD,R6                                                         
         OC    0(L'TATUUNIT,R3),0(R3)    IF NO UNIT AS PARAMATER                
         BZ    SB610              THEN JUST PROCESS AS TP NOT P+                
         L     R6,0(R1)                                                         
         MVI   ELCODE,TATUELQ     ANY TATU ELEMENT?                             
         BRAS  RE,GETEL           ELEMENTS                                      
         JE    SB800                                                            
SB610    CLI   RECNUM,UREPORT     ONLY ADJUST MEDICARE                          
         JNE   XIT                FOR UREPORT                                   
         CLC   RYEAR,=C'2012'     ONLY HAVE NEW MEDICARE OVER 200K              
         BNH   XIT                FOR 2013 AND LATER                            
SB620    DS    0H                 COMMERICAL WORLD                              
         L     R4,=F'20000000'    R4=MEDICARE LIMT TILL WE HIT 200K             
         SR    R4,R0              200K-(MEDICARE YTD BEFORE THIS CHECK)         
SB630    DS    0H                                                               
         LR    RE,R4                                                            
         ZICM  RF,THISEARN,(15)   RE=MEDICARE LIMIT TILL 200K                   
         SR    RE,RF                 - (THIS CHECKS WAGES)                      
SB640    DS    0H                                                               
         BNP   SB650                                                            
*  WE DIDNT GO OVER MEDICARE LIMIT                                              
         LA    R5,16(R2)                                                        
         MVC   0(4,R5),THISEARN   MEDICARE = THIS CHECKS EARNINGS               
         L     R5,12(R1)                                                        
         XC    0(4,R5),0(R5)      MEDICARE OVER = 0                             
         J     SB900                                                            
*  WE WENT GO OVER MEDICARE LIMIT                                               
SB650    DS    0H                                                               
         LA    R5,16(R2)                                                        
         STCM  R4,15,0(R5)       MEDICARE = LIMIT TILL WE HIT 200K              
         L     R5,12(R1)                                                        
         LPR   RE,RE                                                            
         STCM  RE,15,0(R5)       MEDICARE OVER = RE                             
         J     SB900                                                            
*-------------------------------------------------------------------            
*  P+ WORLD - AT THIS POINT  R0 HAS YTD MEDICARE BEFORE THIS CHECK              
*-------------------------------------------------------------------            
SB800    DS    0H                 P+ WORLD                                      
         L     R4,=F'20000000'    R4=MEDICARE LIMT TILL WE HIT 200K             
         SR    R4,R0              200K LIMIT - YTD BEFORE THIS CHECK            
         B     SB840                                                            
SB820    BRAS  RE,NEXTEL                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
SB840    DS    0H                                                               
         CLI   TATUUNIT+2,C' '    IF OPTIONAL TAX UNIT CODE IS NOT              
         JH    SB820              PROVIDED, READ ALL STATE YTD                  
         CLC   TATUUNIT,=C'FD '   WITHHOLDING ELEMENTS                          
         JE    SB820                                                            
         CLC   TATUUNIT,=C'CN '                                                 
         JE    SB820                                                            
         CLC   TATUUNIT,0(R3)                                                   
         BE    SB860                                                            
***      ZICM  RE,TATUWAGE,(15)                                                 
****     ZICM  RF,TATUSTRE,(15)                                                 
****     AR    RE,RF              RE= GROSS EARNINGS FOR UNIT                   
         ZICM  RE,THISEARN,(15)   THISEARN SHOULD ALREADY HAVE WAGES            
         SR    R4,RE                                                            
         BP    SB820                                                            
* ALREADY EXCEEDED 200K MEDICARE LIMIT BEFORE REACHING THE                      
* STATE WE NEED TO PROCESS                                                      
SB850    LA    R5,16(R2)                                                        
         XC    0(4,R5),0(R5)      MEDICARE = 0                                  
         L     R5,12(R1)                                                        
         STCM  RE,15,0(R5)       MEDICARE OVER=TOTAL EARNINGS FOR UNIT          
*                                                                               
         CLI   RECNUM,UREPORT     ONLY ADJUST MEDICARE OVER                     
         JE    XIT                FOR UREPORT  OTHER                            
         CLC   RYEAR,=C'2012'     ONLY HAVE NEW MEDICARE OVER 200K              
         BH    XIT                FOR 2013 AND LATER                            
*  ALL OTHER REPORTS MEDICARE OVER=0 NO MATTER WHAT                             
* WE ARENT SUPPORTED MEDICARE OVER ANYWHERE ELSE BESIDES UREPORT                
         LA    R5,16(R2)                                                        
         STCM  RE,15,0(R5)       MEDICARE =TOTAL EARNINGS FOR UNIT              
         L     R5,12(R1)                                                        
         XC    0(4,R5),0(R5)     MEDICARE OVER=0                                
*                                                                               
         J     SB900                                                            
* FOUND TATU WE NEED TO PROCESS                                                 
SB860    DS    0H                                                               
         LR    R0,R4             R0=R4= LEFT OVER LIMIT BEFORE UNIT             
****     ZICM  RE,TATUWAGE,(15)                                                 
****     ZICM  RF,TATUSTRE,(15)                                                 
****     AR    RE,RF              RE= GROSS EARNINGS FOR UNIT                   
         ZICM  RE,THISEARN,(15)   THISEARN HAS THE EARNINGS                     
         SR    R0,RE              R4= LIMIT - EARNINGS                          
         BP    SB880              ARE WE STILL UNDER 200K LIMIT?                
         CLI   RECNUM,UREPORT     ONLY ADJUST MEDICARE OVER                     
         JNE   SB880              FOR UREPORT -OTHERS GET FULL MEDICARE         
         CLC   RYEAR,=C'2012'     ONLY HAVE NEW MEDICARE OVER 200K              
         BNH   SB880              FOR 2013 AND LATER                            
* FOUND TATU WE NEED TO PROCESS BUT WE WILL GO OVER 200K LIMIT                  
         LA    R5,16(R2)                                                        
*                                                                               
         STCM  R4,15,0(R5)       MEDICARE = LEFT OVER LIMIT IN (R4)             
         L     R5,12(R1)                                                        
         LPR   R0,R0             ABSOLUTE VALUE= DIFFERENCE                     
         STCM  R0,15,0(R5)       MEDICARE OVER=DIFF(LIMIT - EARNINGS)           
         J     SB900                                                            
                                                                                
* FOUND TATU WE NEED TO PROCESS AND WE ARE STILL UNDER 200K LIMIT               
SB880    DS    0H                                                               
         LA    R5,16(R2)                                                        
         STCM  RE,15,0(R5)       MEDICARE =TOTAL EARNINGS FOR UNIT              
         L     R5,12(R1)                                                        
         XC    0(4,R5),0(R5)     MEDICARE OVER =0                               
         J     SB900                                                            
                                                                                
* DISABILITY FOR HAWAII                                                         
SB900    DS    0H                                                               
         CLC   =C'HI ',0(R3)                                                    
         JE    *+14                                                             
         CLC   =C'HI ',EXTUNIT                                                  
         JNE   XIT                                                              
         L     R6,0(R1)                                                         
         USING TACQD,R6                                                         
         MVI   ELCODE,TACQELQ     READ THROUGH ALL QTD WITHHOLDING              
SB910    BRAS  RE,GETEL           ELEMENTS                                      
         JE    *+8                                                              
SB920    BRAS  RE,NEXTEL                                                        
         JNE   XIT                                                              
         LA    R5,20(R2)          R5 = A(FIELD TO SAVE SDI BASE IN)             
         CLI   TACQLEN,TACQLN2Q                                                 
         JNE   XIT                                                              
         CLC   =C'HI ',TACQUNIT                                                 
         JNE   SB920                                                            
         MVC   0(4,R5),TACQTSDI                                                 
         J     XIT                                                              
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
