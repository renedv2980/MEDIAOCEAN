*          DATA SET REREPRANK3 AT LEVEL 055 AS OF 05/01/02                      
*              TEST MODE SETTINGS                                               
         SPACE 3                                                                
TESTMODE CLI   MODE,REQFRST                                                     
         BNE   MD2                                                              
         MVI   RCSUBPRG,0                                                       
         BAS   RE,CLEARTAB                                                      
         BAS   RE,ADJMONTH                                                      
         MVC   PAGE,=H'1'                                                       
         MVI   FORCEHED,C'Y'                                                    
         XC    CNT,CNT                                                          
         XC    NEWITEM,NEWITEM                                                  
         B     MDEXT                                                            
         SPACE 2                                                                
MD2      CLI   MODE,OFFLAST                                                     
         BNE   MD4                                                              
         CLI   QSEQ,C'S'                                                        
         BE    MDEXT                                                            
         B     MD6                                                              
         SPACE 2                                                                
MD4      CLI   MODE,STALAST                                                     
         BNE   MD8                                                              
         CLI   QSEQ,C'S'                                                        
         BNE   MDEXT                                                            
         SPACE 2                                                                
MD6      LA    R1,ACCYDCUR-ACCUMD                                               
         BAS   RE,RANKDETS                                                      
         CLC   DETNUM,=F'0'        NOTHING TO RANK                              
         BE    MDEXT                                                            
         BAS   RE,PRNTDETS                                                      
         B     MDEXT                                                            
         SPACE 2                                                                
MD8      CLI   MODE,REQLAST                                                     
         BNE   MDEXT                                                            
         CLI   QSEQ,C'S'           TEST FOR STATION ORDER                       
         BNE   MD9                                                              
         CLC   QSTATION,SPACES     TEST FOR 1 STATION REQUEST                   
         BNE   MDEXT               DON'T PRINT TOTALS                           
         B     MD10                                                             
*                                                                               
*                                  WE HAVE OFFICE ORDER                         
MD9      CLC   QOFFICE,SPACES      TEST FOR OFFICE REQUEST                      
         BNE   MDEXT                                                            
MD10     LA    R1,ACCYTCUR-ACCUMD                                               
         BAS   RE,RANKTOTS                                                      
         CLC   DETNUM,=F'0'                                                     
         BE    MDEXT                                                            
         BAS   RE,PRNTTOTS                                                      
         SPACE 1                                                                
         MVI   RCSUBPRG,2                                                       
         MVI   FORCEFUT,C'Y'                                                    
         GOTO1 REPORT                                                           
         SPACE 2                                                                
MDEXT    XMOD1 1                                                                
         EJECT                                                                  
       ++INCLUDE REREPRANK2                                                     
*              ROUTINE TO CLEAR TABLE                                           
         SPACE 3                                                                
CLEARTAB L     R2,=A(OTHLINES)                                                  
         A     R2,RELO                                                          
         XC    0(120,R2),0(R2)                                                  
         MVC   2(26,R2),=CL26'      OTHER AGENCIES'                             
         CLI   QPROG,C'6' '                                                     
         BNE   *+10                                                             
         MVC   2(26,R2),=CL26'      OTHER ADVERTISERS'                          
         MVC   62(26,R2),=C'      ** TOTALS **'                                 
         BR    RE                                                               
         EJECT                                                                  
*              ROUTINE TO POST A LINE                                           
*              ANY NEW ENTRIES WILL NOW HAVE NAME AS WELL                       
         SPACE 2                                                                
POSTLINE NTR1                                                                   
         LA    R5,28(R2)                                                        
         CLC   NEWITEM,=F'0'      IS IT A NEW ITEM                              
         BNE   *+8       YES, DO NOT ADD TO TOTAL                               
         BAS   RE,POSTADD               ADD WORK TO TABLE                       
         L     R5,=A(OTHLINES)                                                  
         LA    R5,88(R5)                                                        
         A     R5,RELO                                                          
         BAS   RE,POSTADD               ADD WORK TO TOTAL                       
         B     XIT                                                              
         SPACE 2                                                                
POSTADD  LA    R4,WORK+28                                                       
         LA    R6,8                                                             
POSTADD2 L     R7,0(R4)                                                         
         A     R7,0(R5)                                                         
         ST    R7,0(R5)                                                         
         LA    R4,4(R4)                                                         
         LA    R5,4(R5)                                                         
         BCT   R6,POSTADD2                                                      
         BR    RE                                                               
         EJECT                                                                  
*              ROUTINES TO RANK TABLE                                           
         SPACE 3                                                                
RANKDETS NTR1                                                                   
         LA    R4,36               DISPLACE TO CURRENT YTD (DETAIL)             
         B     RANKALL                                                          
         SPACE 2                                                                
RANKTOTS NTR1                                                                   
         LA    R4,52               DISPLACE TO CURRENT YTD (TOTAL)              
         SPACE 2                                                                
RANKALL  L     R2,=A(ANTABLE)                                                   
         A     R2,RELO                                                          
         LA    R2,4(R2)                                                         
         L     R3,CNT                   NUMBER IN TABLE                         
         SR    R8,R8                                                            
         LTR   R3,R3                                                            
         BZ    RANK15                                                           
         SR    R1,R1                                                            
         LA    R5,RKTB                  RANK TABLE                              
RANK10   LA    R7,0(R4,R2)              POINT TO BUCKETS                        
         OC    0(8,R7),0(R7)            ZERO - DO NOT RANK                      
         BZ    RANK14                                                           
         STH   R1,0(R5)            SAVE RELATIVE NUMBER                         
         MVC   2(8,R5),0(R7)       YTD  AMOUNT                                  
         XI    2(R5),X'80'                                                      
         LA    R5,10(R5)           NEXT RANK TABLE ENTRY                        
         LA    R8,1(R8)            COUNT NUMBER TO BE RANKED                    
RANK14   LA    R1,1(R1)            COUNT RELATIVE NUMBER                        
         LA    R2,60(R2)           NEXT TABLE ENTRY                             
         BCT   R3,RANK10           LOOK AT NEXT                                 
RANK15   ST    R8,DETNUM           NUMBER TO RANK                               
         CLC   DETNUM,=F'0'                                                     
         BE    XIT                                                              
*                                                                               
*              SORT RANK TABLE                                                  
*                                                                               
         GOTO1 XSORT,DMCB,(1,RKTB),(R8),10,8,2                                  
         B     XIT                                                              
LASTVALS DS    F                                                                
         EJECT                                                                  
*              ROUTINE TO PRINT TABLE                                           
         SPACE 3                                                                
PRNTDETS NTR1                                                                   
         LA    R4,28               DISPLACE TO DETAIL  VALUES                   
         B     PRNTALL                                                          
         SPACE 2                                                                
PRNTTOTS NTR1                                                                   
         LA    R4,44               DISPLACE TO TOTAL VALUES                     
         SPACE 2                                                                
PRNTALL  EQU   *                                                                
         L     R2,=A(ANTABLE)                                                   
         A     R2,RELO                                                          
         L     R3,DETNUM                                                        
         LA    R2,4(R2)                                                         
         MVI   FORCEHED,C'Y'                                                    
         LA    R8,RKTB                                                          
         SR    R9,R9                                                            
         ST    R2,LASTVALS         SAVE BEGIN OF TABLE                          
PRNT2    LH    R7,0(R8)           RELATIVE NUMBER                               
         SR    R6,R6                                                            
         M     R6,=F'60'      TABLE LENGTH                                      
         A     R7,LASTVALS        POINT TO TABLE ENTRY                          
         LR    R5,R7                                                            
         LR    R2,R5                                                            
         SPACE 2                                                                
PRNT3    LA    R5,0(R2,R4)                                                      
         OC    0(16,R5),0(R5)                                                   
         BZ    PRNT12                                                           
         LR    R6,R8                                                            
         SH    R6,=H'10'                                                        
         CLC   2(4,R6),2(R8)                                                    
         BE    *+12                                                             
         LA    R9,1(R9)                                                         
         B     PRNT4                                                            
         MVI   P+4,C'='                                                         
PRNT4    STH   R9,0(R2)                                                         
         SPACE 2                                                                
         EDIT  (2,(R2)),(4,P)                                                   
         SPACE 2                                                                
PRNT6    MVC   P+6(6),2(R2)        CODE                                         
         CLI   QPROG,C'6'                                                       
         BE    PRNT8                                                            
         MVC   P+13(20),8(R2)      AGENCY NAME OR ...                           
         B     PRNT10                                                           
         SPACE 2                                                                
PRNT8    MVC   P+11(20),8(R2)      ADVERTISER NAME                              
         SPACE 2                                                                
PRNT10   LA    R6,P+33             DETAIL LINE                                  
         BAS   RE,FORMAT                                                        
         LA    R5,8(R5)                                                         
         LA    R6,P+73                                                          
         BAS   RE,FORMAT                                                        
         MVI   SPACING,2                                                        
         GOTO1 REPORT                                                           
         SPACE 2                                                                
PRNT12   LA    R8,10(R8)                                                        
         BCT   R3,PRNT2                                                         
         OC    DETNUM,DETNUM                                                    
         BZ    PRNT13                                                           
         L     R2,=A(OTHLINES)                                                  
         A     R2,RELO                                                          
         XC    DETNUM,DETNUM                                                    
         XC    0(2,R2),0(R2)                                                    
         LA    R3,1                                                             
         B     PRNT3                                                            
PRNT13   EQU   *                                                                
         MVI   SPACING,2                                                        
         GOTO1 REPORT                                                           
         MVC   P+11(12),=C'** TOTALS **'                                        
         CLI   QPROG,C'6'                                                       
         BE    PRNT14                                                           
         MVC   P+11(2),SPACES                                                   
         MVC   P+13(12),=C'** TOTALS **'                                        
         SPACE 2                                                                
PRNT14   LA    R6,P+33             TOTALS LINE                                  
         LA    R2,60(R2)                                                        
         LA    R5,0(R2,R4)                                                      
         BAS   RE,FORMAT                                                        
         LA    R5,8(R5)                                                         
         LA    R6,P+73                                                          
         BAS   RE,FORMAT                                                        
         GOTO1 REPORT                                                           
         B     XIT                                                              
         EJECT                                                                  
*              FORMAT THE NUMBERS                                               
         SPACE 3                                                                
FORMAT   NTR1                                                                   
         MVC   WORK,SPACES         FIRST REFRESH HEAD7                          
         CH    R4,=H'44'                                                        
         BE    FORMAT4                                                          
         CLI   QSEQ,C'S'                                                        
         BE    FORMAT2                                                          
         MVC   WORK(6),=C'OFFICE'                                               
         MVC   WORK+7(2),ROFFKOFF                                               
         MVC   WORK+10(20),ROFFNAME                                             
         B     FORMAT10                                                         
         SPACE 2                                                                
FORMAT2  MVC   WORK+4(7),=C'STATION'                                            
         MVC   WORK+12(4),RSTAKSTA                                              
         MVC   WORK+16(3),=C'-TV'                                               
         CLI   RCONKSTA+4,C' '                                                  
         BE    FORMAT10                                                         
         MVC   WORK+17(1),RSTAKSTA+4                                            
         MVI   WORK+18,C'M'                                                     
         B     FORMAT10                                                         
         SPACE 2                                                                
FORMAT4  CLI   QSEQ,C'S'                                                        
         BE    FORMAT6                                                          
         MVC   WORK(21),=C'ALL REQUESTED OFFICES'                               
         B     FORMAT10                                                         
         SPACE 2                                                                
FORMAT6  MVC   WORK(22),=C'ALL REQUESTED STATIONS'                              
         SPACE 2                                                                
FORMAT10 MVC   HEAD7+45(30),WORK                                                
         LM    R2,R3,0(R5)                                                      
         XC    0(8,R5),0(R5)                                                    
         LR    R4,R2                                                            
         SR    R4,R3                                                            
         STM   R2,R4,DMCB                                                       
         LA    R7,3                                                             
         LA    R5,DMCB                                                          
         SPACE 2                                                                
FORMAT12 MVI   0(R6),C' '          (CLEAR PREVIOUS SIGN)                        
         OC    0(4,R5),0(R5)                                                    
         BE    FORMAT14                                                         
         CLC   P+13(12),=C'** TOTALS **'                                        
         BE    FMTA                                                             
         CLC   P+11(12),=C'** TOTALS **'                                        
         BE    FMTA                                                             
         EDIT  (4,(R5)),(9,(R6)),MINUS=YES                                      
         B     FORMAT14                                                         
         SPACE 1                                                                
FMTA     BCTR  R6,0                                                             
         CH    R7,=H'2' PRIOR                                                   
         BE    FMTB                                                             
         EDIT  (4,(R5)),(10,(R6)),MINUS=YES                                     
         B     FMTE                                                             
         SPACE 1                                                                
FMTB     EDIT  (4,(R5)),(10,132(R6)),MINUS=YES                                  
FMTE     LA    R6,1(R6)                                                         
         SPACE 2                                                                
FORMAT14 LA    R5,4(R5)                                                         
         LA    R6,9(R6)                                                         
         BCT   R7,FORMAT12                                                      
         LTR   R4,R4               TEST FOR 0 CHANGE                            
         BZ    XIT                                                              
         BCTR  R6,R0                                                            
         CLI   0(R6),C'-'                                                       
         BE    *+8                                                              
         MVI   0(R6),C'+'                                                       
         LA    R6,1(R6)                                                         
         LTR   R3,R3                                                            
         BZ    FORMAT16                                                         
         XR    R2,R3               TEST FOR DIFFERENT SIGNS                     
         LTR   R2,R2                                                            
         BM    XIT                                                              
         SRDA  R4,32                                                            
         M     R4,=F'10000'                                                     
         DR    R4,R3                                                            
         MVC   4(4,R6),=C'HIGH'                                                 
         C     R5,=F'99999'                                                     
         BH    XIT                                                              
         EDIT  (R5),(7,2(R6)),2,MINUS=YES                                       
         CLI   8(R6),C' '                                                       
         BNE   XIT                                                              
         MVI   8(R6),C'+'                                                       
         B     XIT                                                              
         SPACE 1                                                                
FORMAT16 LTR   R2,R2               TEST NO PRIOR DATA, BUT CURRENT              
         BZ    XIT                                                              
         MVC   4(4,R6),=C'HIGH'                                                 
         SPACE 2                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
         SPACE 2                                                                
DETNUM   DS    F                                                                
CNT      DC    F'0'                                                             
NEWITEM  DC    F'0'                                                             
         EJECT                                                                  
         LTORG                                                                  
         SPACE 2                                                                
         DS    0F                                                               
RKTB     DS    4000CL10                                                         
         SPACE 2                                                                
         ENTRY ANTABLE                                                          
         ENTRY DETLINES                                                         
         ENTRY OTHLINES                                                         
         SPACE 1                                                                
ANTABLE  DS    0D                                                               
         DC    F'4000'                                                          
DETLINES DS    240000C                                                          
OTHLINES DS    120C                                                             
         EJECT                                                                  
*              ACCUMULATOR DESIGN                                               
         SPACE 3                                                                
ACCUMD   DSECT                                                                  
ACCLINE  DS    0CL60                                                            
ACCRANK  DS    CL2                                                              
ACCCODE  DS    CL6                 AGENCY OR ADVERTISER CODE                    
ACCNAME  DS    CL20                AGENCY OR ADVERTISER NAME                    
ACCPDCUR DS    CL4                 DETAIL  PERIOD  CURRENT                      
ACCPDPRI DS    CL4                                 PRIOR                        
ACCYDCUR DS    CL4                         YTD     CURRENT                      
ACCYDPRI DS    CL4                                 PRIOR                        
ACCPTCUR DS    CL4                 TOTAL   PERIOD  CURRENT                      
ACCPTPRI DS    CL4                                 PRIOR                        
ACCYTCUR DS    CL4                         YTD     CURRENT                      
ACCYTPRI DS    CL4                                 PRIOR                        
         SPACE 2                                                                
*                                  LINES 1-N ARE FOR ANAYSIS                    
*                                  LINE  N+1 IS  FOR OTHERS                     
*                                  LINE  N+2 IS  FOR TOTALS                     
*                                  VALUE OF N IS DEFINED IN FIRST               
*                                  FOUR BYTES OF ANTABLE                        
         EJECT                                                                  
