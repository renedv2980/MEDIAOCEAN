*          DATA SET DMWRKRR    AT LEVEL 003 AS OF 04/30/96                      
* ROUTINE TO LOCK/UNLOCK WORKER DISK FILE                                       
*                                                                               
WKLOCK   LA    R0,C'E'                                                          
         B     *+8                                                              
WKUNLK   LA    R0,C'D'                                                          
         ST    RE,CISAVRE                                                       
         L     RF,CIENQDEQ                                                      
         GOTO1 (RF),CIP1,((R0),=C'WRKR')                                        
         L     RE,CISAVRE                                                       
         BR    RE                                                               
         SPACE 1                                                                
FWLOCK   LA    R0,C'E'                                                          
         B     *+8                                                              
FWUNLK   LA    R0,C'D'                                                          
         ST    RE,CISAVRE                                                       
         L     RF,CIENQDEQ                                                      
         GOTO1 (RF),CIP1,((R0),=C'FACW')                                        
         L     RE,CISAVRE                                                       
         BR    RE                                                               
         SPACE 1                                                                
* ROUTINE TO INITIALISE INDEX SEARCH LOOP - SETS R5=A(FIRST ENTRY)              
*                                                                               
CXLOOPI  XC    CXPAGE,CXPAGE                                                    
         LH    R5,CICINDX                                                       
         B     *+14                                                             
CXLOOPJ  MVC   CXPAGE,CJPAGE                                                    
         LH    R5,CJENTRY                                                       
         STH   R5,CXENTRY                                                       
         MH    R5,CINDXLN                                                       
         LA    R5,CXREC(R5)                                                     
         BR    RE                                                               
         SPACE 1                                                                
* ROUTINE TO TERMINATE INDEX SERCH LOOP - NEXT ENTRY RETURN - 0(RE)             
*                                       - PAGE FAULT RETURN - 4(RE)             
*                                       - END OF NDX RETURN - 8(RE)             
CXLOOPX  LH    RF,CXENTRY                                                       
         LA    RF,1(RF)                                                         
         STH   RF,CXENTRY                                                       
         CH    RF,CIENTRYS                                                      
         BNL   CXLOOPX1                                                         
         AH    R5,CINDXLN                                                       
         CLC   0(2,R5),=X'FFFF'                                                 
         BNER  RE                                                               
         B     8(RE)               END OF INDEX                                 
CXLOOPX1 XC    CXENTRY,CXENTRY                                                  
         LH    RF,CXPAGE                                                        
         LA    RF,1(RF)                                                         
         STH   RF,CXPAGE                                                        
         LA    R5,CXREC                                                         
         B     4(RE)               END OF PAGE                                  
         EJECT                                                                  
* INDEX PAGE/ENTRY TO CONTROL INTERVAL DISK ADDR                                
*                                                                               
GETCAD   LH    RF,CXPAGE           CAD=(CXP*ENT+CXE)*TRKS + 1                   
         CLC   CXPAGE(4),CJPAGE                                                 
         BNL   GETCAD1                                                          
         MH    RF,CIENTRYS                                                      
         AH    RF,CXENTRY                                                       
         MH    RF,CITRKS                                                        
         LA    RF,1(RF)                                                         
         LH    R0,CITRKS                                                        
         B     GETCAD2                                                          
GETCAD1  SH    RF,CJPAGE           CI IS IN PART 2 OF INDEX                     
         MH    RF,CIENTRYS                                                      
         AH    RF,CXENTRY                                                       
         SH    RF,CJENTRY                                                       
         MH    RF,CJTRKS                                                        
         AH    RF,CJSTTRK                                                       
         LH    R0,CJTRKS                                                        
GETCAD2  SLL   RF,16                                                            
         ST    RF,CIADDR                                                        
         MVI   CIADDR+2,1                                                       
         SRL   RF,16                                                            
         AR    RF,R0                                                            
         BCTR  RF,0                                                             
         SLL   RF,16                                                            
         ST    RF,CIADDRX                                                       
         MVC   CIADDRX+2(1),CIHIREC+1                                           
         BR    RE                                                               
         SPACE 1                                                                
* INDEX PAGE/ENTRY TO INDEX DISK ADDR                                           
*                                                                               
GETXAD   ST    RE,CISAVRE          XADT=CXP/TRKS + 1                            
         SR    RE,RE               XADB=REM + 1                                 
         LH    RF,CXPAGE                                                        
         LH    R0,CIHIREC                                                       
         DR    RE,R0                                                            
         LA    RE,1(RE)                                                         
         LA    RF,1(RF)                                                         
         STH   RF,CXADDR                                                        
         STC   RE,CXADDR+2                                                      
         MVI   CXADDR+3,0                                                       
         L     RE,CISAVRE                                                       
         BR    RE                                                               
         EJECT                                                                  
* CONTROL INTERVAL DISK ADDR TO INDEX PAGE/ENTRY                                
*                                                                               
GETXPE   ST    RE,CISAVRE          CXP=(CAD-1)/(CXE*TRKS)                       
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         ICM   RF,3,CIADDR                                                      
         CLM   RF,3,CJSTTRK                                                     
         BNL   GETXPE1                                                          
         BCTR  RF,0                                                             
         LH    R0,CITRKS                                                        
         MH    R0,CIENTRYS                                                      
         DR    RE,R0                                                            
         STH   RF,CXPAGE                                                        
         SRDL  RE,32                                                            
         LH    R0,CITRKS                                                        
         DR    RE,R0                                                            
         STH   RF,CXENTRY                                                       
         B     GETXPEX                                                          
GETXPE1  SH    RF,CJSTTRK          CI IS IN PART 2 OF INDEX                     
         LH    R0,CJTRKS                                                        
         MH    R0,CIENTRYS                                                      
         DR    RE,R0                                                            
         AH    RF,CJPAGE                                                        
         STH   RF,CXPAGE                                                        
         SRDL  RE,32                                                            
         LH    R0,CJTRKS                                                        
         DR    RE,R0                                                            
         AH    RF,CJENTRY                                                       
         CH    RF,CIENTRYS                                                      
         BL    *+20                                                             
         SH    RF,CIENTRYS         ADJUST FOR PART 2 INDEX OFFSET               
         LH    RE,CXPAGE                                                        
         LA    RE,1(RE)                                                         
         STH   RE,CXPAGE                                                        
         STH   RF,CXENTRY                                                       
GETXPEX  L     RE,CISAVRE                                                       
         BR    RE                                                               
