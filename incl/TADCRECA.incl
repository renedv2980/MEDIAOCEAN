*          DATA SET TADCRECA   AT LEVEL 044 AS OF 05/01/02                      
*                                                                               
*              ROUTINE TO GET THE ADVICE CAST RECORD INTO AIO                   
*              INCLUDED BY TAGEN8C AND TAGEN8E                                  
         SPACE 1                                                                
*                                        - P1  =A(SEQTAB)-OPTIONAL              
*                                        - AIO =A(ADVICE CAST RECS)             
*                                        - AIO3=A(WORK AREA)                    
*                                  XIT   - CC EQU IF RECORD FOUND               
         SPACE 2                                                                
TADCREC  NTR1  BASE=*,LABEL=*                                                   
         SPACE 1                                                                
         L     R4,0(R1)            SEQTAB                                       
         NI    DMINBTS,X'F7'       TURN OFF READ-DELETED IN CASE SET            
*        XC    KEY,KEY                                                          
*        MVI   KEY,TLDCCDQ                                                      
*        MVC   KEY+2(L'TGAGY),TGAGY                                             
*        MVC   KEY+10(L'TGCID),TGCID                                            
*        MVC   KEY+23(L'TGADV),TGADV                                            
*        GOTO1 HIGH                                                             
*        CLC   KEY(29),KEYSAVE                                                  
*        BE    *+6                                                              
*        DC    H'00'                                                            
*        GOTO1 READ                                                             
         GOTO1 RECVAL,DMCB,TLDCCDQ,(X'20',0)                                    
         BNE   TADCRNO                                                          
         SPACE 1                                                                
         BAS   RE,SETSEQ           SET FIRST ELES SEQUENCE NUMBER               
         L     R6,AIO              SAVE A(VIRTUAL ADVICE CAST RECS)             
         MVC   AIO,AIO3            SET AIO FOR SEQUENTIAL RECORDS               
         SPACE 1                                                                
         USING TLDCD,R3                                                         
TADCR4   GOTO1 SEQ                                                              
         CLC   KEY(TLDCSEQ-TLDCD),KEYSAVE                                       
         BNE   TADCRX                                                           
         SPACE 1                                                                
         GOTO1 GETREC              GET NEW RECORD INTO IO3                      
         BAS   RE,SETSEQ           SET FIRST ELES SEQUENCE NUMBER               
         L     RE,AIO              NOW COMBINE THEM                             
         LR    R3,R6                                                            
         LH    RF,TLDCLEN-TLDCD(RE) L'NEW RECORD                                
         SH    RF,DATADISP                                                      
         SPACE 1                                                                
         LH    R1,TLDCLEN                                                       
         BCTR  R1,0                                                             
         LR    R0,R1               R0=R1=L'MAIN RECORD (-1 FOR EOR)             
         SPACE 1                                                                
         AR    R1,RF               PLUS L'NEW RECORD                            
         STH   R1,TLDCLEN          IS L'COMBINED RECORD                         
         SPACE 1                                                                
         AR    R0,R3               R0=A(END OF MAIN RECORD)                     
         LR    R1,RF               R1=RF=L'NEW RECORD ELEMENTS                  
         AH    RE,DATADISP         RE=A(1ST EL. IN NEW RECORD)                  
         SPACE 1                                                                
         MVCL  R0,RE               MOVE NEW RECORD AFTER MAIN                   
         B     TADCR4                                                           
         SPACE 1                                                                
TADCRX   ST    R6,AIO              RESTORE AIO OF VIRTUAL ADV CAST RECS         
         XR    RC,RC                                                            
         SPACE 1                                                                
TADCRNO  LTR   RC,RC                                                            
         XIT1                                                                   
         DROP  R3                                                               
         SPACE 2                                                                
*              SETS IN SEQTAB THE SEQUENCE NUMBER OF THE FIRST                  
*              ELEMENT IN EVERY RECORD                                          
         SPACE 1                                                                
SETSEQ   NTR1                                                                   
         LTR   R4,R4               IF SEQTAB PASSED                             
         BZ    SETSEQX                                                          
         L     RE,AIO                                                           
         AH    RE,DATADISP                                                      
         CLI   0(RE),TAVCELQ                                                    
         BNE   SETSEQX                                                          
         MVC   0(1,R4),TAVCSEQ-TAVCD(RE)                                        
         LA    R4,1(R4)                                                         
SETSEQX  XIT1  REGS=(R4)                                                        
