*          DATA SET REDARPAS   AT LEVEL 133 AS OF 12/11/14                      
*                                                                               
         TITLE 'DAR PASSIVE POINTER GENERATION LOGIC'                           
**********************************************************************          
*  THIS IS INCLUDE CODE TO SUPPORT THE BLACK BOX REQUIRED FOR RADIO  *          
*        EDI.  TO ENSURE THAT THE SAME CODE IS BEING USED THROUGH    *          
*        ALL FUNCTIONS, THIS AREA OF CODE HAS BEEN LIFTED FROM ITS   *          
*        ORIGINAL LOCATION IN 'RELDCPTR'.  IT IS NOW ++INCLUDED INTO *          
*        THAT MODULE, AS WELL AS THE BLACK BOX FUNCTION.             *          
*                                                                    *          
*                                                                    *          
*  HISTORY OF CHANGES                                                *          
**********************************************************************          
*  16OCT02 (BU ) --- PASSIVES FOR DARE                               *          
*  14NOV02 (BU ) --- PASSIVES FOR RADIO ONLY                         *          
*  24DEC02 (BU ) --- NO PRIORITY/STATUS SETTING                      *          
*  13JAN03 (BU ) --- SET CONFIRMED COLOR TO YELLOW                   *          
*  02OCT03 (BU ) --- REMOVE TV RESTRICTION                           *          
*  19MAR04 (BU ) --- TV: BYPASS X'0F' ABSENCE CHECK                  *          
*  27OCT11 (BOB) --- ELIMINATE DUPLICATE PASSIVES                    *          
*  30JUN13 (BOB) --- PASSIVES FOR RECEIVED DATE                      *          
*                                                                    *          
**********************************************************************          
PPDAR    DS    0H                  DARE RECORD                                  
         USING RDARREC,R2                                                       
         CLI   RDARKRT,X'10'       AGENCY HEADER?                               
         BNE   EXIT                NO  - NOTHING TO PROCESS IF NOT HDR          
*                                                                               
*   THIS VERSION OF REDARPAS WILL GENERATE PASSIVES FOR TV STATIONS             
*                                                                               
*   THIS IS TEMPORARY UNTIL TELEVISION USES THE FEATURE                         
*                                                                               
***      CLI   RDARKSTA+4,C'A'     AM STATION?                                  
***      BE    PPDA0002            YES - ACCEPT                                 
***      CLI   RDARKSTA+4,C'F'     FM STATION?                                  
***      BNE   EXIT                NO  - REJECT NON-RADIO RECORDS               
PPDA0002 EQU   *                                                                
         CLI   RDARKSTY,X'01'      X'4101' RECORD?                              
         BE    EXIT                YES - SKIP THIS ONE: SHADOW COPY             
*                                  YES - MUST BE 'CONFIRMED' TO                 
*                                     GENERATE ANY PASSIVES                     
         XC    ALTDUB,ALTDUB       CLEAR COLOR/STATUS                           
         CLI   RDARKTYP,X'51'      TYPE 51 RECORD?                              
         BNE   PPDA0010            NO                                           
         LA    RF,RDARELEM         YES - SET A(01 ELEMENT)                      
PPDA0005 EQU   *                                                                
         CLI   0(RF),0             END OF RECORD?                               
         BE    EXIT                NO X'0F' ELT: EXIT                           
         CLI   0(RF),X'0F'         MISC FLAG ELT?                               
         BE    PPDA0008            YES                                          
         ZIC   RE,1(RF)            NO  - BUMP TO NEXT ELEMENT                   
         AR    RF,RE                                                            
         B     PPDA0005            GO BACK FOR NEXT                             
PPDA0008 EQU   *                                                                
         TM    RDARFLG1-RDARFLEM(RF),X'02'                                      
*                                  TYPE 41 ELEMENT EXISTS?                      
         BO    EXIT                YES - NOT CONFIRMED                          
*                                  NO  - CONFIRMED - GEN PASSIVES               
****>>>> MVC   ALTDUB,=X'018020'   FORCE COLOR/STATUS CODE TO                   
*                                     COLOR    = BLACK                          
*                                     PRIORITY = 8                              
*                                     STATE    = 32                             
***      MVC   ALTDUB,=X'030000'   FORCE COLOR/STATUS CODE TO                   
*                                     COLOR    = BLACK                          
         MVC   ALTDUB,=X'040000'   FORCE COLOR/STATUS CODE TO                   
*                                     COLOR    = YELLOW                         
*                                     PRIORITY = 0                              
*                                     STATE    = 0                              
*                                                                               
         B     PPDA0015            USE FORCED VALUE                             
PPDA0010 EQU   *                                                                
         GOTO1 COLCODE,DMCB,(R2),ALTDUB                                         
PPDA0015 EQU   *                                                                
*                                  DETERMINE COLOR/STATUS CODE ONCE             
         XC    0(32,R3),0(R3)                                                   
         MVI   DARTYPE,X'D1'       SET 'SPOT PASSIVE'                           
         MVI   MASTFIND,C'N'       SET 'NOT MASTER' FLAG                        
         MVC   MASTCODE,RDARKREP   SET MASTER CODE TO REP OF AGY ORDER          
         LA    RF,POWERCDS         SET A(POWER CODE LIST)                       
PPDA0020 EQU   *                                                                
         CLI   0(RF),0             END OF LIST?                                 
         BE    PPDA0060            YES                                          
         CLC   RDARKREP,0(RF)      POWER CODE FOUND IN LIST?                    
         BE    PPDA0040            YES                                          
         LA    RF,4(RF)            SKIP SUBREP+MASTER REP CODES                 
         B     PPDA0020            GO BACK FOR NEXT                             
PPDA0040 EQU   *                                                                
         MVC   MASTCODE,2(RF)      SET MASTER CODE FOR SUBREP FOUND             
         MVI   MASTFIND,C'Y'       SET 'MASTER' FLAG                            
PPDA0060 EQU   *                                                                
*                                                                               
         LA    RF,RDARELEM         SET A(01 ELEMENT)                            
         XC    ADAR0A,ADAR0A       CLEAR A(X'0A' ELEMENT)                       
*                                                                               
PPDA0080 EQU   *                                                                
         CLI   0(RF),X'00'         END OF RECORD?                               
         BE    PPDA0160            YES - ELEMENT(S) NOT FOUND                   
         CLI   0(RF),X'0A'         POINTPERSON ELEMENT?                         
         BNE   PPDA0100            NO                                           
         ST    RF,ADAR0A           YES - SAVE ADDRESS OF ELEMENT                
         B     PPDA0120                                                         
PPDA0100 EQU   *                                                                
         CLI   0(RF),X'0F'         MISC FLAGS ELEMENT?                          
         BE    PPDA0140                                                         
PPDA0120 EQU   *                                                                
         ZIC   RE,1(RF)            ELEMENT LENGTH                               
         AR    RF,RE               BUMP TO NEXT ELEMENT                         
         B     PPDA0080            GO BACK FOR NEXT                             
PPDA0140 EQU   *                                                                
         CLI   RDARKTYP,X'51'      TYPE 51 DARE RECORD?                         
         BNE   PPDA0150            NO  - MUST BE TYPE 41                        
         TM    RDARFLG1-RDARFLEM(RF),X'02'                                      
*                                  TYPE 41 RECORDS EXIST FOR THIS               
*                                     AGENCY ORDER?                             
         BO    EXIT                YES - NOT CONFIRMED: NO PASSIVES             
PPDA0150 EQU   *                                                                
         TM    RDARFLG1-RDARFLEM(RF),X'01'                                      
*                                  UNWIRED ORDER?                               
         BNO   PPDA0160            NO  - LEAVE AS 'SPOT PASSIVE'                
         MVI   DARTYPE,X'D0'       YES - SET AS 'UNWIRED PASSIVE'               
PPDA0160 EQU   *                                                                
U        USING RED01KEY,R3                                                      
         XC    0(32,R3),0(R3)      CLEAR NEXT KEY SLOT                          
         MVC   U.RED01TYP,DARTYPE  INSERT RECORD TYPE                           
         CLI   DARTYPE,X'D1'       SPOT ORDER?                                  
         BE    PPDA0180            YES - NO SUBTYPE 01 KEY                      
         CLI   MASTFIND,C'N'       MASTER FOUND?                                
         BE    PPDA0180            NO  - NO 'D001' KEY                          
         MVI   U.RED01STY,1        INSERT SUBKEY TYPE                           
         MVC   U.RED01RAG,RDARKAGY INSERT ROUTING AGENCY+OFFICE                 
         MVC   U.RED01MRP,MASTCODE INSERT MASTER REP CODE                       
         LA    RF,RDARELEM         SET A(DARE 01 ELEM)                          
         ZIC   RE,1(RF)            GET ELEMENT LENGTH                           
         AR    RE,RF               BUMP TO NEXT ELEMENT                         
         MVC   U.RED01CLI,RDARCLI-RDARCLEM(RE)                                  
*                                  INSERT CLIENT CODE                           
         MVC   U.RED01PRD,RDARPRD1-RDARCLEM(RE)                                 
*                                  INSERT PRODUCT CODE                          
         MVC   U.RED01EST+1(3),RDAREST#                                         
*                                  INSERT DARE ESTIMATE NUMBER INTO             
*                                     LOWER THREE BYTES OF FOUR                 
         MVC   U.RED01ORD,RDARKORD INSERT AGENCY ORDER NUMBER                   
         MVC   27(1,R3),29(R2)     STATUS                                       
         CLI   RDARKTYP,X'51'      TYPE 51 RECORD?                              
         BNE   *+8                 NO  - NOT 'CONFIRMED'                        
         OI    27(R3),X'04'        YES - SET 'CONFIRMED' STATUS                 
*                                                                               
         BRAS  RE,CHKPSV           ELIMINATE DUPLICATE PASSIVES                 
*                                                                               
         DROP  U                                                                
*                                                                               
PPDA0180 EQU   *                                                                
U        USING RED02KEY,R3                                                      
         XC    0(32,R3),0(R3)      CLEAR NEXT KEY SLOT                          
         MVC   U.RED02TYP,DARTYPE  INSERT RECORD TYPE                           
         MVI   U.RED02STY,2        INSERT SUBKEY TYPE                           
         MVC   U.RED02RAG,RDARKAGY INSERT ROUTING AGENCY+OFFICE                 
         MVC   U.RED02SRP,RDARKREP INSERT SUB    REP CODE                       
         LA    RF,RDARELEM         SET A(DARE 01 ELEM)                          
         ZIC   RE,1(RF)            GET ELEMENT LENGTH                           
         AR    RE,RF               BUMP TO NEXT ELEMENT                         
         MVC   U.RED02CLI,RDARCLI-RDARCLEM(RE)                                  
*                                  INSERT CLIENT CODE                           
         MVC   U.RED02PRD,RDARPRD1-RDARCLEM(RE)                                 
*                                  INSERT PRODUCT CODE                          
         MVC   U.RED02EST+1(3),RDAREST#                                         
*                                  INSERT DARE ESTIMATE NUMBER INTO             
*                                     LOWER THREE BYTES OF FOUR                 
         MVC   U.RED02ORD,RDARKORD INSERT DARE ORDER    NUMBER                  
         MVC   27(1,R3),29(R2)     STATUS                                       
         CLI   RDARKTYP,X'51'      TYPE 51 RECORD?                              
         BNE   *+8                 NO  - NOT 'CONFIRMED'                        
         OI    27(R3),X'04'        YES - SET 'CONFIRMED' STATUS                 
*                                                                               
         BRAS  RE,CHKPSV           ELIMINATE DUPLICATE PASSIVES                 
*                                                                               
         DROP  U                                                                
*                                                                               
         OC    ADAR0A,ADAR0A       POINT PERSON ELEMENT FOUND?                  
         BNZ   PPDA0190            YES - GENERATE MORE KEYS                     
         CLI   RDARKSTA+4,C'A'     AM STATION?                                  
         BE    EXIT                YES - NO MORE KEYS W/NO 0A ELT               
         CLI   RDARKSTA+4,C'F'     NO  - FM STATION?                            
         BE    EXIT                YES - NO MORE KEYS W/NO 0A ELT               
PPDA0190 EQU   *                                                                
*                                                                               
U        USING RED03KEY,R3                                                      
         MVC   U.RED03TYP,DARTYPE  INSERT RECORD TYPE                           
         CLI   DARTYPE,X'D1'       SPOT ORDER?                                  
         BE    PPDA0200            YES - NO SUBTYPE 01 KEY                      
         CLI   MASTFIND,C'N'       MASTER FOUND?                                
         BE    PPDA0200            NO  - NO 'D001' KEY                          
         MVI   U.RED03STY,3        INSERT SUBKEY TYPE                           
         MVC   U.RED03MRP,MASTCODE INSERT MASTER REP CODE                       
         L     RF,ADAR0A           SET A(POINT PERSON ELEMENT)                  
         OC    ADAR0A,ADAR0A       POINT PERSON ELEMENT FOUND?                  
         BZ    PPDA0195            NO  - NO POINT PERSON CODE                   
         MVC   U.RED03PP,RDARPPSP-RDARPPEL(RF)                                  
*                                  INSERT POINT PERSON CODE                     
PPDA0195 EQU   *                                                                
         MVC   U.RED03COL(3),ALTDUB       SET COLOR/STATUS CODE                 
         MVC   U.RED03STA,RDARKSTA INSERT STATION (FIRST FIVE CHARS)            
         MVC   U.RED03RAG,RDARKAGY INSERT ROUTING AGENCY+OFFICE                 
         MVC   U.RED03ORD,RDARKORD INSERT AGENCY ORDER NUMBER                   
         MVC   U.RED03SP,U.RED03PP POINT PERSON/SP SAME NOW                     
*                                     MAY DIFFER AT A LATER DATE                
         MVC   27(1,R3),29(R2)     STATUS                                       
         CLI   RDARKTYP,X'51'      TYPE 51 RECORD?                              
         BNE   *+8                 NO  - NOT 'CONFIRMED'                        
         OI    27(R3),X'04'        YES - SET 'CONFIRMED' STATUS                 
*                                                                               
         BRAS  RE,CHKPSV           ELIMINATE DUPLICATE PASSIVES                 
*                                                                               
         DROP  U                                                                
*                                                                               
U        USING RED0BKEY,R3                                                      
*                                                                               
         XC    0(32,R3),0(R3)      CLEAR NEXT KEY SLOT                          
*                                                                               
         MVC   U.RED0BTYP,DARTYPE  INSERT RECORD TYPE                           
*                                                                               
         CLI   DARTYPE,X'D1'       SPOT ORDER?                                  
         BE    PPDA0200            YES - NO SUBTYPE 0B KEY                      
*                                                                               
         CLI   MASTFIND,C'N'       MASTER FOUND?                                
         BE    PPDA0200            NO  - NO 'D00B' KEY                          
*                                                                               
         MVI   U.RED0BSTY,X'0B'    INSERT SUBKEY TYPE                           
         MVC   U.RED0BMRP,MASTCODE INSERT MASTER REP CODE                       
*                                                                               
         LA    RF,RDARELEM         SET A(DARE 01 ELEM)                          
         MVC   U.RED0BDTE,RDARDATE-RDARELEM(RF)                                 
*                                  INSERT RECEIVED DATE                         
         MVC   U.RED0BRAG,RDARKAGY INSERT ROUTING AGENCY+OFFICE                 
         MVC   U.RED0BORD,RDARKORD INSERT AGENCY ORDER NUMBER                   
*                                  INSERT RECEIVED DATE                         
         MVC   27(1,R3),29(R2)     STATUS                                       
*                                                                               
         CLI   RDARKTYP,X'51'      TYPE 51 RECORD?                              
         BNE   *+8                 NO  - NOT 'CONFIRMED'                        
         OI    27(R3),X'04'        YES - SET 'CONFIRMED' STATUS                 
*                                                                               
         BRAS  RE,CHKPSV           ELIMINATE DUPLICATE PASSIVES                 
*                                                                               
         DROP  U                                                                
*                                                                               
PPDA0200 EQU   *                                                                
U        USING RED04KEY,R3                                                      
         XC    0(32,R3),0(R3)      CLEAR NEXT KEY SLOT                          
         MVC   U.RED04TYP,DARTYPE  INSERT RECORD TYPE                           
         MVI   U.RED04STY,4        INSERT SUBKEY TYPE                           
         MVC   U.RED04REP,RDARKREP INSERT SUB    REP CODE                       
         L     RF,ADAR0A           SET A(POINT PERSON ELEMENT)                  
         OC    ADAR0A,ADAR0A       POINT PERSON ELEMENT FOUND?                  
         BZ    PPDA0205            NO  - NO POINT PERSON CODE                   
         MVC   U.RED04PP,RDARPPSP-RDARPPEL(RF)                                  
*                                  INSERT POINT PERSON CODE                     
PPDA0205 EQU   *                                                                
         MVC   U.RED04COL(3),ALTDUB       SET COLOR/STATUS CODE                 
         MVC   U.RED04STA,RDARKSTA INSERT STATION (FIRST FIVE CHARS)            
         MVC   U.RED04RAG,RDARKAGY INSERT ROUTING AGENCY+OFFICE                 
         MVC   U.RED04ORD,RDARKORD INSERT AGENCY ORDER NUMBER                   
         MVC   U.RED04SP,RED04PP   POINT PERSON/SP SAME NOW                     
*                                     MAY DIFFER AT A LATER DATE                
         MVC   27(1,R3),29(R2)     STATUS                                       
         CLI   RDARKTYP,X'51'      TYPE 51 RECORD?                              
         BNE   *+8                 NO  - NOT 'CONFIRMED'                        
         OI    27(R3),X'04'        YES - SET 'CONFIRMED' STATUS                 
*                                                                               
         BRAS  RE,CHKPSV           ELIMINATE DUPLICATE PASSIVES                 
*                                                                               
         DROP  U                                                                
*                                                                               
U        USING RED0CKEY,R3                                                      
*                                                                               
         XC    0(32,R3),0(R3)      CLEAR NEXT KEY SLOT                          
         MVC   U.RED0CTYP,DARTYPE  INSERT RECORD TYPE                           
*                                                                               
         MVI   U.RED0CSTY,X'0C'    INSERT SUBKEY TYPE                           
         MVC   U.RED0CREP,RDARKREP INSERT SUB REP CODE                          
*                                                                               
         LA    RF,RDARELEM         SET A(DARE 01 ELEM)                          
         MVC   U.RED0CDTE,RDARDATE-RDARELEM(RF)                                 
*                                  INSERT RECEIVED DATE                         
         MVC   U.RED0CRAG,RDARKAGY INSERT ROUTING AGENCY+OFFICE                 
         MVC   U.RED0CORD,RDARKORD INSERT AGENCY ORDER NUMBER                   
*                                                                               
         MVC   27(1,R3),29(R2)     STATUS                                       
*                                                                               
         CLI   RDARKTYP,X'51'      TYPE 51 RECORD?                              
         BNE   *+8                 NO  - NOT 'CONFIRMED'                        
         OI    27(R3),X'04'        YES - SET 'CONFIRMED' STATUS                 
*                                                                               
         BRAS  RE,CHKPSV           ELIMINATE DUPLICATE PASSIVES                 
*                                                                               
         DROP  U                                                                
*                                                                               
U        USING RED05KEY,R3                                                      
         XC    0(32,R3),0(R3)      CLEAR NEXT KEY SLOT                          
         MVC   U.RED05TYP,DARTYPE  INSERT RECORD TYPE                           
         CLI   DARTYPE,X'D1'       SPOT ORDER?                                  
         BE    PPDA0220            YES - NO SUBTYPE 01 KEY                      
         CLI   MASTFIND,C'N'       MASTER FOUND?                                
         BE    PPDA0220            NO  - NO 'D001' KEY                          
         MVI   U.RED05STY,5        INSERT SUBKEY TYPE                           
         MVC   U.RED05MRP,MASTCODE INSERT MASTER REP CODE                       
         L     RF,ADAR0A           SET A(POINT PERSON ELEMENT)                  
         OC    ADAR0A,ADAR0A       POINT PERSON ELEMENT FOUND?                  
         BZ    PPDA0210            NO  - NO POINT PERSON CODE                   
         MVC   U.RED05PP,RDARPPSP-RDARPPEL(RF)                                  
*                                  INSERT POINT PERSON CODE                     
PPDA0210 EQU   *                                                                
         MVC   U.RED05BYR,RDARBUYC INSERT BUYER CODE                            
         MVC   U.RED05COL(3),ALTDUB       SET COLOR/STATUS CODE                 
         MVC   U.RED05STA,RDARKSTA INSERT STATION (FIRST FIVE CHARS)            
         MVC   U.RED05RAG,RDARKAGY INSERT ROUTING AGENCY+OFFICE                 
         MVC   U.RED05ORD,RDARKORD INSERT AGENCY ORDER NUMBER                   
         MVC   27(1,R3),29(R2)     STATUS                                       
         CLI   RDARKTYP,X'51'      TYPE 51 RECORD?                              
         BNE   *+8                 NO  - NOT 'CONFIRMED'                        
         OI    27(R3),X'04'        YES - SET 'CONFIRMED' STATUS                 
*                                                                               
         BRAS  RE,CHKPSV           ELIMINATE DUPLICATE PASSIVES                 
*                                                                               
         DROP  U                                                                
*                                                                               
PPDA0220 EQU   *                                                                
U        USING RED06KEY,R3                                                      
         XC    0(32,R3),0(R3)      CLEAR NEXT KEY SLOT                          
         MVC   U.RED06TYP,DARTYPE  INSERT RECORD TYPE                           
         MVI   U.RED06STY,6        INSERT SUBKEY TYPE                           
         MVC   U.RED06REP,RDARKREP INSERT SUB    REP CODE                       
         L     RF,ADAR0A           SET A(POINT PERSON ELEMENT)                  
         OC    ADAR0A,ADAR0A       POINT PERSON ELEMENT FOUND?                  
         BZ    PPDA0225            NO  - NO POINT PERSON CODE                   
         MVC   U.RED06PP,RDARPPSP-RDARPPEL(RF)                                  
*                                  INSERT POINT PERSON CODE                     
PPDA0225 EQU   *                                                                
         MVC   U.RED05BYR,RDARBUYC INSERT BUYER CODE                            
         MVC   U.RED06COL(3),ALTDUB       SET COLOR/STATUS CODE                 
         MVC   U.RED06STA,RDARKSTA INSERT STATION (FIRST FIVE CHARS)            
         MVC   U.RED06RAG,RDARKAGY INSERT ROUTING AGENCY+OFFICE                 
         MVC   U.RED06ORD,RDARKORD INSERT AGENCY ORDER NUMBER                   
         MVC   27(1,R3),29(R2)     STATUS                                       
         CLI   RDARKTYP,X'51'      TYPE 51 RECORD?                              
         BNE   *+8                 NO  - NOT 'CONFIRMED'                        
         OI    27(R3),X'04'        YES - SET 'CONFIRMED' STATUS                 
*                                                                               
         BRAS  RE,CHKPSV           ELIMINATE DUPLICATE PASSIVES                 
*                                                                               
         DROP  U                                                                
*                                                                               
U        USING RED07KEY,R3                                                      
         XC    0(32,R3),0(R3)      CLEAR NEXT KEY SLOT                          
         MVC   U.RED07TYP,DARTYPE  INSERT RECORD TYPE                           
         CLI   DARTYPE,X'D1'       SPOT ORDER?                                  
         BE    PPDA0240            YES - NO SUBTYPE 01 KEY                      
         CLI   MASTFIND,C'N'       MASTER FOUND?                                
         BE    PPDA0240            NO  - NO 'D001' KEY                          
         MVI   U.RED07STY,7        INSERT SUBKEY TYPE                           
         MVC   U.RED07MRP,MASTCODE INSERT MASTER REP CODE                       
         L     RF,ADAR0A           SET A(POINT PERSON ELEMENT)                  
         OC    ADAR0A,ADAR0A       POINT PERSON ELEMENT FOUND?                  
         BZ    PPDA0230            NO  - NO POINT PERSON CODE                   
         MVC   U.RED07PP,RDARPPSP-RDARPPEL(RF)                                  
*                                  INSERT POINT PERSON CODE                     
PPDA0230 EQU   *                                                                
         MVC   U.RED07FST,RDARESST INSERT ESTIMATE START DATE                   
         MVC   U.RED07COL(3),ALTDUB       SET COLOR/STATUS CODE                 
         MVC   U.RED07STA,RDARKSTA INSERT STATION (FIRST FIVE CHARS)            
         MVC   U.RED07RAG,RDARKAGY INSERT ROUTING AGENCY+OFFICE                 
         MVC   U.RED07ORD,RDARKORD INSERT AGENCY ORDER NUMBER                   
         MVC   27(1,R3),29(R2)     STATUS                                       
         CLI   RDARKTYP,X'51'      TYPE 51 RECORD?                              
         BNE   *+8                 NO  - NOT 'CONFIRMED'                        
         OI    27(R3),X'04'        YES - SET 'CONFIRMED' STATUS                 
*                                                                               
         BRAS  RE,CHKPSV           ELIMINATE DUPLICATE PASSIVES                 
*                                                                               
         DROP  U                                                                
*                                                                               
PPDA0240 EQU   *                                                                
U        USING RED08KEY,R3                                                      
         XC    0(32,R3),0(R3)      CLEAR NEXT KEY SLOT                          
         MVC   U.RED08TYP,DARTYPE  INSERT RECORD TYPE                           
         MVI   U.RED08STY,8        INSERT SUBKEY TYPE                           
         MVC   U.RED08REP,RDARKREP INSERT SUB    REP CODE                       
         L     RF,ADAR0A           SET A(POINT PERSON ELEMENT)                  
         OC    ADAR0A,ADAR0A       POINT PERSON ELEMENT FOUND?                  
         BZ    PPDA0245            NO  - NO POINT PERSON CODE                   
         MVC   U.RED08PP,RDARPPSP-RDARPPEL(RF)                                  
*                                  INSERT POINT PERSON CODE                     
PPDA0245 EQU   *                                                                
         MVC   U.RED08FST,RDARESST INSERT ESTIMATE START DATE                   
         MVC   U.RED08COL(3),ALTDUB       SET COLOR/STATUS CODE                 
         MVC   U.RED08STA,RDARKSTA INSERT STATION (FIRST FIVE CHARS)            
         MVC   U.RED08RAG,RDARKAGY INSERT ROUTING AGENCY+OFFICE                 
         MVC   U.RED08ORD,RDARKORD INSERT AGENCY ORDER NUMBER                   
         MVC   27(1,R3),29(R2)     STATUS                                       
         CLI   RDARKTYP,X'51'      TYPE 51 RECORD?                              
         BNE   *+8                 NO  - NOT 'CONFIRMED'                        
         OI    27(R3),X'04'        YES - SET 'CONFIRMED' STATUS                 
*                                                                               
         BRAS  RE,CHKPSV           ELIMINATE DUPLICATE PASSIVES                 
*                                                                               
         DROP  U                                                                
*                                                                               
U        USING RED09KEY,R3                                                      
         XC    0(32,R3),0(R3)      CLEAR NEXT KEY SLOT                          
         MVC   U.RED09TYP,DARTYPE  INSERT RECORD TYPE                           
         CLI   DARTYPE,X'D1'       SPOT ORDER?                                  
         BE    PPDA0260            YES - NO SUBTYPE 01 KEY                      
         CLI   MASTFIND,C'N'       MASTER FOUND?                                
         BE    PPDA0260            NO  - NO 'D001' KEY                          
         MVI   U.RED09STY,9        INSERT SUBKEY TYPE                           
         MVC   U.RED09MRP,MASTCODE INSERT MASTER REP CODE                       
         L     RF,ADAR0A           SET A(POINT PERSON ELEMENT)                  
         OC    ADAR0A,ADAR0A       POINT PERSON ELEMENT FOUND?                  
         BZ    PPDA0250            NO  - NO POINT PERSON CODE                   
         MVC   U.RED09PP,RDARPPSP-RDARPPEL(RF)                                  
*                                  INSERT POINT PERSON CODE                     
PPDA0250 EQU   *                                                                
         MVC   U.RED09NDT,RDARDATE INSERT NEW/REVNEW DATE: COMPRESSED           
         MVC   U.RED09COL(3),ALTDUB       SET COLOR/STATUS CODE                 
         MVC   U.RED09STA,RDARKSTA INSERT STATION (FIRST FIVE CHARS)            
         MVC   U.RED09RAG,RDARKAGY INSERT ROUTING AGENCY+OFFICE                 
         MVC   U.RED09ORD,RDARKORD INSERT AGENCY ORDER NUMBER                   
         MVC   27(1,R3),29(R2)     STATUS                                       
         CLI   RDARKTYP,X'51'      TYPE 51 RECORD?                              
         BNE   *+8                 NO  - NOT 'CONFIRMED'                        
         OI    27(R3),X'04'        YES - SET 'CONFIRMED' STATUS                 
*                                                                               
         BRAS  RE,CHKPSV           ELIMINATE DUPLICATE PASSIVES                 
*                                                                               
         DROP  U                                                                
*                                                                               
PPDA0260 EQU   *                                                                
U        USING RED0AKEY,R3                                                      
         XC    0(32,R3),0(R3)      CLEAR NEXT KEY SLOT                          
         MVC   U.RED0ATYP,DARTYPE  INSERT RECORD TYPE                           
         MVI   U.RED0ASTY,X'0A'    INSERT SUBKEY TYPE                           
         MVC   U.RED0AREP,RDARKREP INSERT SUB    REP CODE                       
         L     RF,ADAR0A           SET A(POINT PERSON ELEMENT)                  
         OC    ADAR0A,ADAR0A       POINT PERSON ELEMENT FOUND?                  
         BZ    PPDA0265            NO  - NO POINT PERSON CODE                   
         MVC   U.RED0APP,RDARPPSP-RDARPPEL(RF)                                  
*                                  INSERT POINT PERSON CODE                     
PPDA0265 EQU   *                                                                
         MVC   U.RED0ANDT,RDARDATE INSERT NEW/REVNEW DATE: COMPRESSED           
         MVC   U.RED0ACOL(3),ALTDUB       SET COLOR/STATUS CODE                 
         MVC   U.RED0ASTA,RDARKSTA INSERT STATION (FIRST FIVE CHARS)            
         MVC   U.RED0ARAG,RDARKAGY INSERT ROUTING AGENCY+OFFICE                 
         MVC   U.RED0AORD,RDARKORD INSERT AGENCY ORDER NUMBER                   
         MVC   27(1,R3),29(R2)     STATUS                                       
         CLI   RDARKTYP,X'51'      TYPE 51 RECORD?                              
         BNE   *+8                 NO  - NOT 'CONFIRMED'                        
         OI    27(R3),X'04'        YES - SET 'CONFIRMED' STATUS                 
*                                                                               
         BRAS  RE,CHKPSV           ELIMINATE DUPLICATE PASSIVES                 
*                                                                               
         DROP  U                                                                
*                                                                               
*                                                                               
         B     EXIT                                                             
*                                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
*        ELIMINATE DUPLICATE PASSIVES                                 *         
*                                                                     *         
***********************************************************************         
         DS    0D                                                               
CHKPSV   NTR1  LABEL=*                                                          
*                                                                               
         LA    R3,0(R3)            CLEAR HOB                                    
*                                                                               
         L     RF,SAVER1           POINT TO START OF PASSIVE POINTERS           
         L     RF,4(RF)                                                         
         LA    RF,0(RF)            CLEAR HOB                                    
*                                                                               
         CR    RF,R3               DONE IF LAST ENTRY REACHED                   
         BE    CHKPSV10                                                         
         CLC   0(32,RF),0(R3)      LOOK FOR DUPLICATE                           
         BE    *+12                   DUPE FOUND                                
         LA    RF,32(RF)              BUMP TO NEXT PASSIVE                      
         B     *-20                                                             
*                                  DUPLICATE PASSIVE                            
         B     CHKPSV20                                                         
*                                                                               
CHKPSV10 DS    0H                  NO DUPLICATE                                 
*                                                                               
*LOAD (ONCE) REDARPD PHASE AND SET THE ADDRESS.                                 
*                                                                               
         CLC   AREDARPD,=F'0'      TABLE LOADED?                                
         BNE   CHKPSV12            YES                                          
*                                  NO - LOAD REDARPD PHASE                      
         LOAD  EP=REDARPD                                                       
         LTR   RF,RF                                                            
         JNZ   *+2                 DIE=CAN'T LOAD REDARPD                       
         ST    R0,AREDARPD                                                      
*                                                                               
*                                                                               
CHKPSV12 L     RF,AREDARPD         SEARCH FOR KNOWN DUPES                       
*                                                                               
         CLI   0(RF),X'FF'         DONE AT END OF TABLE                         
         BE    CHKPSV15                                                         
         CLC   0(27,RF),0(R3)      MATCH TABLE ENTRY                            
         BE    *+12                                                             
         LA    RF,96(RF)           NEXT ENTRY                                   
         B     *-22                                                             
*                                                                               
         OC    32(32,RF),32(RF)    LOOK FOR EMPTY SLOT                          
         BNZ   *+14                                                             
         MVC   32(32,RF),0(R2)                                                  
         B     CHKPSV15                                                         
*                                                                               
         MVC   64(32,RF),0(R2)                                                  
         B     CHKPSV20                                                         
*                                                                               
CHKPSV15 DS    0H                                                               
         LA    R3,32(R3)                                                        
*                                                                               
CHKPSV20 DS    0H                                                               
         XC    0(32,R3),0(R3)         CLEAR LAST POINTER                        
*                                                                               
CHKPSVX  DS    0H                                                               
         XIT1  REGS=(R3)                                                        
*                                                                               
AREDARPD DC    F'0'                                                             
*                                                                               
         LTORG                                                                  
*                                                                               
         DS    0D                                                               
         EJECT                                                                  
