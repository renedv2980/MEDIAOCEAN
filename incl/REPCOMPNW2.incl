*          DATA SET REPCOMPNW2 AT LEVEL 010 AS OF 04/27/99                      
*              DETERMINE COMPARABLE, OLD, OR NEW                                
***********************************************************************         
*  HISTORY OF CHANGES                                                 *         
***********************************************************************         
*  DEC05/91 (BU ) --- CHANGE TO NEW MONTH TABLE FORMAT - INITIALIZES  *         
*                     72 BUCKETS, MAX, AND USES NEW TABLE SIZES FOR   *         
*                     NEW MONTH/MONTH INFO TABLES                     *         
*                                                                     *         
*  APR27/99 (BU ) --- USE NEW FORMAT DATES FOR YEAR 2000              *         
*                                                                     *         
*                                                                     *         
*                                                                     *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
RECOMP   NTR1                                                                   
         MVC   WORK(4),=X'0000FFFF'                                             
         OC    RSTASTRT,RSTASTRT                                                
         BZ    RCOM0006                                                         
         GOTO1 DATCON,DMCB,(3,RSTASTRT),(0,WORK+6)                              
         GOTO1 GETBROAD,DMCB,WORK+6,WORK+12                                     
         CLC   WORK+6(6),WORK+12                                                
         BE    RCOM0002            STARTED ON FIRST DAY                         
         MVC   WORK+12(6),WORK+18  ELSE GO TO NEXT MONTH                        
         L     R6,=F'15'                                                        
         GOTO1 ADDAY,DMCB,WORK+12,WORK+18,(R6)                                  
RCOM0002 EQU   *                                                                
         GOTO1 DATCON,DMCB,(0,WORK+18),(3,WORK)                                 
         MVI   WORK+2,X'FF'                                                     
         SPACE 1                                                                
RCOM0006 OC    RSTAEND,RSTAEND                                                  
         BZ    RCOM0014                                                         
         GOTO1 DATCON,DMCB,(3,RSTAEND),(0,WORK+6)                               
         GOTO1 GETBROAD,DMCB,WORK+6,WORK+12                                     
         CLC   WORK+6(6),WORK+18                                                
         BE    RCOM0010            END ON LAST DAY                              
         L     R6,=F'-15'                                                       
         GOTO1 ADDAY,DMCB,WORK+12,WORK+18,(R6)                                  
RCOM0010 GOTO1 DATCON,DMCB,(0,WORK+18),(3,WORK+2)                               
         SPACE 1                                                                
RCOM0014 EQU   *                                                                
         L     R6,AMONINFO         MONTH INFORMATION TABLE                      
         L     R2,ANEWMON          A(NEW MONTH TABLE)                           
*                                                                               
*   FIND REQUEST START DATE IN 'NEWMON' TABLE.  THIS SYNCHS THE                 
*    TWO TABLES:  NEWMON COVERS THREE YEARS PRIOR TO THE REQUEST                
*    DATES AS WELL AS ONE YEAR AFTER, AND MONINFO COVERS ONLY THE               
*    REQUEST PERIOD.                                                            
*                                                                               
RCOM0016 EQU   *                                                                
         CLC   0(4,R2),QFASTART    TABLE ENTRY VS REQ START DATE                
         BE    RCOM0018            FOUND                                        
         LA    R2,NEXTBUCK(R2)     BUMP TO NEXT BUCKET                          
         B     RCOM0016            GO BACK FOR NEXT                             
         SPACE 1                                                                
RCOM0018 CLI   0(R2),0                                                          
         BE    RCOM0099                                                         
         CLC   0(4,R2),QFAEND      TABLE ENTRY VS REQ END   DATE                
         BH    RCOM0099            TABLE > END DATE - EXIT                      
         MVI   7(R2),C'O'          OLD                                          
         CLC   WORK+2(2),0(R6)     LOST BEFORE CURRENT MONTH                    
         BL    RCOM0022                                                         
         MVI   7(R2),C'N'          NEW                                          
         CLC   WORK(2),4(R6)       STARTED AFTER PRIOR MONTH                    
         BH    RCOM0022                                                         
         MVI   7(R2),C' '          COMPARABLE                                   
         SPACE 1                                                                
RCOM0022 LA    R6,16(R6)           MONINFO                                      
         LA    R2,NEXTBUCK(R2)     MONTABLE                                     
         B     RCOM0018            GO BACK FOR NEXT                             
RCOM0099 EQU   *                                                                
         XIT1                                                                   
