*          DATA SET PRBLKICEVC AT LEVEL 010 AS OF 06/27/19                      
       ++INCLUDE IEBUPDTHDR                                                     
                                                                                
**** SUPP=N IS FOR DEBUGGING ONLY. USE THIS WHEN NO SUPPORT DATA IS             
****     AVAILABLE, SO THAT ACC TRANSACTIONS WILL BE PROCESSED THAT             
****     WOULD OTHERWISE ONLY GO TO THE EXCEPTION FILE.                         
* SET DEFAULT TO SUPP=Y. CAN BE OVERRIDDEN IN THE JCL TO SUPP=N.                
*&&      SET   SUPP=Y                                                           
                                                                                
***********************************************************************         
                                                                                
       SAP MEDIA VENDOR CLEARANCE EXPORT: XML FILE GENERATION                   
                                                                                
IMPORTANT NOTE: this ICETOOL application sets RC=4 if there are no data         
records in the generated XML file. We do this by using the NULLOFL              
option. The JCL downstream from this ICETOOL step tests for RC=4, and           
does not transmit the XML file if this step completes with a non-zero           
return code. Therefore, if any COUNT operators are added to this                
program which may set the return code to a non-zero value, then the             
JCL may need to be changed!                                                     
                                                                                
***********************************************************************         
./ ADD NAME=SYMNAMES   ** SYMBOLICS USED IN CONTROL CARDS                       
*                                                                               
PIPE,C'|'                          DELIMITER                                    
TAB,X'05'                          DELIMITER                                    
*                                                                               
F1_ONLY,C'1'                       JOIN RESULT INDICATORS (AS PER IBM)          
F2_ONLY,C'2'                                                                    
BOTH_F1_AND_F2,C'B'                                                             
*                                                                               
* Parsed record variables (for the ACC extract dataset)                         
*                                                                               
P_PXG$ACT,%101                     ACTION                                       
P_PXG$SEQ,%104                     SEQUENCE                                     
P_PXG$RTYP,%105                    RECORD TYPE                                  
P_PXG$TTYP,%106                    TRANSACTION TYPE                             
P_PXG$ACCD,%107                    ACCOUNT CODE                                 
P_PXG$OFFC,%109                    OFFICE CODE                                  
P_PXG$TRDT,%110                    TRANSACTION DATE                             
P_PXG$TRRF,%111                    REFERENCE                                    
P_PXG$TRSR,%112                    SUB-REFERENCE                                
P_PXG$SYS,%113                     SYSTEM CODE                                  
P_PXG$MED,%114                     MEDIA                                        
P_PXG$CNT,%115                     CLIENT                                       
P_PXG$PRD,%116                     CLIENT/PRODUCT                               
P_PXG$EST,%117                     ESTIMATE                                     
P_PXG$CR,%119                      DEBIT                                        
P_PXG$ACDT,%121                    ACTIVITY DATE                                
P_PXG$MOS,%123                     MOS                                          
P_PXG$BINC,%125                    BILLING INCOME (COMMISSION)                  
P_PXG$DUDT,%132                    DUE DATE                                     
P_PXG$INV,%133                     INVOICE NUMBER (LONG)                        
P_PXG$NARR,%146                    NARRATIVE                                    
P_PXG$CURR,%147                    CURRENCY (USD/CND)                           
P_PXG$STYP,%148                    TRANACTION TYPE                              
P_PXG$NETW,%149                    NETWORK                                      
P_PXG$PID,%157                     PID                                          
P_PXG$ITMN,%158                    ITEM NO                                      
P_PXG$VNDC,%159                    VENDOR CODE                                  
P_PXG$NET,%162                     NET AMOUNT                                   
P_PXG$TAXA,%163                    Tax Amount                                   
P_PXG$TAXC,%164                    TAx Code                                     
P_PXG$INSO,%165                    INSERTION ORDER NUMBER                       
P_PXG$CMPG,%166                    CAMPAIGN                                     
P_PXG$AGY,%167                     AGENCY                                       
P_PXG$MEDA,%168                    MEDIA AUTHORIZATION NUMBER                   
P_PXG$ESTU,%169                    ESTIMATE UNIQUE NUMBER                       
P_PXG$BUYU,%170                    BUY UNIQUE NUBMER                            
P_PXG$VOUC,%171                    VOUCHER NUMBER                               
P_PXG$ACN,%172                     ACTION NAME                                  
P_PXG$PLID,%173                    PLACEMENT ID                                 
*                                                                               
*                                                                               
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*         
* Make sure all NAME=??????? are defined in your PROC(BLKVC)                    
* TOOLIN and all the ????CNTL                                                   
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*         
*                                                                               
./ ADD NAME=TOOLIN     ** CONTROL CARDS FOR ICETOOL                             
*                                                                               
* Parse the ACC extract file, keeping only Spot and Print transactions.         
COPY FROM(XTRACTIN) USING(PARS)                                                 
*                                                                               
* Discard all transactions with errors, producing an FB dataset of              
* transactions to be joined with vendor data. This is the "detail               
* work file".                                                                   
COPY FROM(ALLRECS) TO(GOODTRNS) USING(COPG)                                     
*                                                                               
* Copy GOODTRNS to another dataset, so we can join it with itself.              
COPY FROM(GOODTRNS) TO(TEMP1)                                                   
*                                                                               
* Seed the invoice gross $ into the detail work file.                           
COPY JKFROM TO(JOINVEND) USING(JONH)                                            
*                                                                               
* Copy FINAL to a tab-delimited file we can import into Excel (for              
*  debugging).                                                                  
*COPY FROM(JOINVEND) TO(JOINDEB) USING(JDEB)                                    
COPY FROM(GOODTRNS) TO(JOINDEB) USING(JDEB)                                     
*                                                                               
* Generate an XML output file. This file is syntactically correct, but          
* contains the more verbose syntax for empty fields.                            
*COPY FROM(JOINVEND) USING(COPX)                                                
COPY FROM(GOODTRNS) USING(COPX)                                                 
*                                                                               
* Prettify the XML output file by shortening tags with empty fields.            
COPY FROM(TEMPXML) TO(XMLOUT) USING(CPTX)                                       
                                                                                
                                                                                
./ ADD NAME=PARSCNTL                                                            
*                                                                               
 INCLUDE COND=(5,5,CH,EQ,C'05082')  Note: RECTYPE is always in column 5         
*                                                                               
 INREC PARSE=(%=(ENDBEFR=PIPE),                                                 
           P_PXG$ACT=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$ACT),                         
           %=(ENDBEFR=PIPE,REPEAT=2),                                           
           P_PXG$SEQ=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$SEQ),                         
           P_PXG$RTYP=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$RTYP),                       
           P_PXG$TTYP=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$TTYP),                       
           P_PXG$ACCD=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$ACCD), for debugging         
           %=(ENDBEFR=PIPE),                                                    
           P_PXG$OFFC=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$OFFC),                       
           P_PXG$TRDT=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$TRDT),                       
           P_PXG$TRRF=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$TRRF),                       
           P_PXG$TRSR=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$TRSR),                       
           P_PXG$SYS=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$SYS),                         
           P_PXG$MED=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$MED),                         
           P_PXG$CNT=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$CNT),                         
           P_PXG$PRD=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$PRD),  CLIENT/PRODUCT         
           P_PXG$EST=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$EST),                         
           %=(ENDBEFR=PIPE),                                                    
           P_PXG$CR=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$CR),                           
           %=(ENDBEFR=PIPE),                                                    
           P_PXG$ACDT=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$ACDT),                       
           %=(ENDBEFR=PIPE),                                                    
           P_PXG$MOS=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$MOS),                         
           %=(ENDBEFR=PIPE),                                                    
           P_PXG$BINC=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$BINC),                       
           %=(ENDBEFR=PIPE,REPEAT=6),                                           
           P_PXG$DUDT=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$DUDT),                       
           P_PXG$INV=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$INV),                         
           %=(ENDBEFR=PIPE,REPEAT=12),                                          
           P_PXG$NARR=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$NARR),                       
           P_PXG$CURR=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$CURR),                       
           P_PXG$STYP=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$STYP),                       
           P_PXG$NETW=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$NETW),                       
           %=(ENDBEFR=PIPE,REPEAT=7),                                           
           P_PXG$PID=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$PID),                         
           P_PXG$ITMN=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$ITMN),                       
           P_PXG$VNDC=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$VNDC),                       
           %=(ENDBEFR=PIPE,REPEAT=2),                                           
           P_PXG$NET=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$NET),                         
           P_PXG$TAXA=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$TAXA),                       
           P_PXG$TAXC=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$TAXC),                       
           P_PXG$INSO=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$INSO),                       
           P_PXG$CMPG=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$CMPG),                       
           P_PXG$AGY=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$AGY),                         
           P_PXG$MEDA=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$MEDA),                       
           P_PXG$ESTU=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$ESTU),                       
           P_PXG$BUYU=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$BUYU),                       
           P_PXG$VOUC=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$VOUC),                       
           P_PXG$ACN=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$ACN),                         
           P_PXG$PLID=(ENDBEFR=PIPE,FIXLEN=LEN_PXG$PLID)),                      
    BUILD=(PXG$LEN,                                                             
           PXG$ACT:P_PXG$ACT,                                                   
           PXG$SEQ:P_PXG$SEQ,                                                   
           PXG$RTYP:P_PXG$RTYP,                                                 
           PXG$TTYP:P_PXG$TTYP,                                                 
           PXG$ACCD:P_PXG$ACCD,                                                 
           PXG$OFFC:P_PXG$OFFC,                                                 
           PXG$TRDT:P_PXG$TRDT,                                                 
           PXG$TRRF:P_PXG$TRRF,                                                 
           PXG$TRSR:P_PXG$TRSR,                                                 
           PXG$SYS:P_PXG$SYS,                                                   
           PXG$MED:P_PXG$MED,                                                   
           PXG$CNT:P_PXG$CNT,                                                   
           PXG$PRD:P_PXG$PRD,                                                   
           PXG$EST:P_PXG$EST,                                                   
           PXG$CR:P_PXG$CR,SFF,TO=ZD,                                           
           PXG$ACDT:P_PXG$ACDT,                                                 
           PXG$MOS:P_PXG$MOS,                                                   
           PXG$BINC:P_PXG$BINC,SFF,TO=ZD,                                       
           PXG$DUDT:P_PXG$DUDT,                                                 
           PXG$INV:P_PXG$INV,                                                   
           PXG$NARR:P_PXG$NARR,                                                 
           PXG$CURR:P_PXG$CURR,                                                 
           PXG$STYP:P_PXG$STYP,                                                 
           PXG$NETW:P_PXG$NETW,                                                 
           PXG$PID:P_PXG$PID,                                                   
           PXG$ITMN:P_PXG$ITMN,                                                 
           PXG$VNDC:P_PXG$VNDC,                                                 
           PXG$NET:P_PXG$NET,SFF,TO=ZD,                                         
           PXG$TAXA:P_PXG$TAXA,SFF,TO=ZD,                                       
           PXG$TAXC:P_PXG$TAXC,                                                 
           PXG$INSO:P_PXG$INSO,                                                 
           PXG$CMPG:P_PXG$CMPG,                                                 
           PXG$AGY:P_PXG$AGY,                                                   
           PXG$MEDA:P_PXG$MEDA,                                                 
           PXG$ESTU:P_PXG$ESTU,                                                 
           PXG$BUYU:P_PXG$BUYU,                                                 
           PXG$VOUC:P_PXG$VOUC,                                                 
           PXG$ACN:P_PXG$ACN,                                                   
           PXG$PLID:P_PXG$PLID)                                                 
                                                                                
*                                                                               
 OUTFIL FNAMES=ALLRECS,              ** "real" transactions only                
   VTOF,                                                                        
** Start building in column 5, to account for the absence of the RDW !          
   BUILD=(5:PXG$RECORD)         *** start in column 5                           
                                                                                
                                                                                
./ ADD NAME=COPGCNTL                                                            
*                                                                               
* Discard all records which were kept around only so that they could            
* be written to the exception file.                                             
*                                                                               
 OUTFIL VTOF,BUILD=(D_PXG$ACT:PXG$ACT,                                          
                    D_PXG$SEQ:PXG$SEQ,                                          
                    D_PXG$SYS:PXG$SYS,                                          
                    D_PXG$MED:PXG$MED,                                          
                    D_PXG$CNT:PXG$CNT,                                          
                    D_PXG$PRD:PXG$PRD_PRD,                                      
                    D_PXG$EST:PXG$EST,                                          
                    D_PXG$INV:PXG$INV,                                          
                    D_PXG$AGY:PXG$AGY,                                          
                    D_PXG$ACCD:PXG$ACCD,                                        
                    D_PXG$OFFC:PXG$OFFC,                                        
                    D_PXG$TRDT:PXG$TRDT,                                        
                    D_PXG$TRRF:PXG$TRRF,                                        
                    D_PXG$CR:PXG$CR,                                            
                    D_PXG$MOA:PXG$MOA,                                          
                    D_PXG$ACDT:PXG$ACDT,                                        
                    D_PXG$MOS:PXG$MOS,                                          
                    D_PXG$PID:PXG$PID,                                          
                    D_PXG$DUDT:PXG$DUDT,                                        
                    D_PXG$ITMN:PXG$ITMN,                                        
                    D_PXG$NARR:PXG$NARR,                                        
                    D_PXG$CURR:PXG$CURR,                                        
                    D_PXG$STYP:PXG$STYP,                                        
                    D_PXG$NETW:PXG$NETW,                                        
                    D_PXG$NETA:PXG$NET,                                         
                    D_PXG$TAXA:PXG$TAXA,                                        
                    D_PXG$TAXC:PXG$TAXC,                                        
                    D_PXG$INSO:PXG$INSO,                                        
                    D_PXG$CMPG:PXG$CMPG,                                        
                    D_PXG$AGY1:PXG$AGY,                                         
                    D_PXG$MEDA:PXG$MEDA,                                        
                    D_PXG$ESTU:PXG$ESTU,                                        
                    D_PXG$BUYU:PXG$BUYU,                                        
                    D_PXG$VOUC:PXG$VOUC,                                        
                    D_PXG$ACN:PXG$ACN,                                          
                    D_PXG$PLID:PXG$PLID)                                        
                                                                                
                                                                                
./ ADD NAME=JONHCNTL                                                            
*                                                                               
* Produce gross $ totals across months-of-service for each invoice.             
*                                                                               
 JOINKEYS F1=GOODTRNS,                                                          
             FIELDS=(D_PXG$MED,A,                                               
                     D_PXG$ACT,A,                                               
                     D_PXG$CNT,A,                                               
                     D_PXG$PRD,A,                                               
                     D_PXG$EST,A,                                               
                     D_PXG$INV,A)                                               
 JOINKEYS F2=TEMP1,                                                             
             TASKID=T1,          <<<< SEE T1F2CNTL                              
             FIELDS=(D_PXG$MED,A,                                               
                     D_PXG$ACT,A,                                               
                     D_PXG$CNT,A,                                               
                     D_PXG$PRD,A,                                               
                     D_PXG$EST,A,                                               
                     D_PXG$INV,A)                                               
 REFORMAT FIELDS=(F2:D_PXG$NETA,    <===== computed gross (from totals)         
                  F1:D_JOINED_DATA)                                             
*                                                                               
* Build our work record from the reformatted fields.                            
 INREC BUILD=(D_GROSS$:REFH_D_GROSS$,                                           
              D_JOINED_DATA:REFH_D_JOINED_DATA)                                 
                                                                                
                                                                                
./ ADD NAME=T1F2CNTL                                                            
*                                                                               
* This collapses F2 *after* the sort (where the sort key is defined             
* by the F2 JOINKEYS fields above), retaining only the first duplicate.         
* The gross $ (i.e., the SUM) winds up in the totalled record.                  
*                                                                               
 SUM FIELDS=(D_PXG$NETA)                                                        
                                                                                
./ ADD NAME=JDEBCNTL                                                            
*                                                                               
* Produce a tab-delimited file for debugging (this can easily be                
* imported into Excel).                                                         
*                                                                               
 OUTREC IFTHEN=(WHEN=GROUP,KEYBEGIN=(D_ITEM_#_KEY),                             
                PUSH=(D_GROSS$:D_GROSS$))                                       
                                                                                
 OUTFIL REMOVECC,                                                               
        HEADER1=('SEP=',TAB,/,         *** SEP= only works in Excel!!!          
                 'ACTION',TAB,                                                  
                 'SEQ#',TAB,                                                    
                 'SYSTEM',TAB,                                                  
                 'MEDIA',TAB,                                                   
                 'CLT',TAB,                                                     
                 'PRD',TAB,                                                     
                 'EST',TAB,                                                     
                 'INV',TAB,                                                     
                 'AGY',TAB,                                                     
                 'ACCT CODE',TAB,                                               
                 'OFFICE',TAB,                                                  
                 'INVOICE DATE',TAB,                                            
                 'REFNUM',TAB,                                                  
                 'GROSS',TAB,                                                   
                 'MOA',TAB,                                                     
                 'ACTIV DATE',TAB,                                              
                 'MOS',TAB,                                                     
                 'USERID',TAB,                                                  
                 'DUE DATE',TAB,                                                
                 'ITEM NO',TAB,                                                 
                 'NARRATIVE',TAB,                                               
                 'CURRENCY',TAB,                                                
                 'TRANSACTION TYPE',TAB,                                        
                 'NETWORK',TAB,                                                 
                 'NET AMT',TAB,                                                 
                 'TAX AMOUNT',TAB,                                              
                 'TAX CODE',TAB,                                                
                 'INSERTION NUM',TAB,                                           
                 'MEDIA AUTH',TAB,                                              
                 'CAMPAIGN',TAB,                                                
                 'AGENCY ALPHA',TAB,                                            
                 'EST UNIQUE',TAB,                                              
                 'BUY UNIQUE NO',TAB,                                           
                 'VOUCHER NUM',TAB,                                             
                 'REC ACT NAME',TAB,                                            
                 'PLACEMENT ID',TAB),                                           
       BUILD=(D_PXG$ACT,TAB,                                                    
              D_PXG$SEQ,TAB,                                                    
              D_PXG$SYS,TAB,                                                    
              D_PXG$MED,TAB,                                                    
              D_PXG$CNT,TAB,                                                    
              D_PXG$PRD,TAB,                                                    
              D_PXG$EST,TAB,                                                    
              D_PXG$INV,TAB,                                                    
              D_PXG$AGY,TAB,                                                    
              D_PXG$ACCD,TAB,                                                   
              D_PXG$OFFC,TAB,                                                   
              D_PXG$TRDT,TAB,                                                   
              D_PXG$TRRF,TAB,                                                   
              D_GROSS$,TAB,                                                     
              D_PXG$MOA,TAB,                                                    
              D_PXG$ACDT,TAB,                                                   
              D_PXG$MOS,TAB,                                                    
              D_PXG$PID,TAB,                                                    
              D_PXG$DUDT,TAB,                                                   
              D_PXG$ITMN,TAB,                                                   
              D_PXG$NARR,TAB,                                                   
              D_PXG$CURR,TAB,                                                   
              D_PXG$STYP,TAB,                                                   
              D_PXG$NETW,TAB,                                                   
              D_PXG$NETA,TAB,                                                   
              D_PXG$TAXA,TAB,                                                   
              D_PXG$TAXC,TAB,                                                   
              D_PXG$INSO,TAB,                                                   
              D_PXG$MEDA,TAB,                                                   
              D_PXG$CMPG,TAB,                                                   
              D_PXG$AGY1,TAB,                                                   
              D_PXG$ESTU,TAB,                                                   
              D_PXG$BUYU,TAB,                                                   
              D_PXG$VOUC,TAB,                                                   
              D_PXG$ACN,TAB,                                                    
              D_PXG$PLID,TAB)                                                   
                                                                                
                                                                                
./ ADD NAME=COPHCNTL                                                            
*                                                                               
* Produce a tab-delimited file which can be imported into Excel.                
*                                                                               
 OUTFIL REMOVECC,                                                               
        VTOF,                                                                   
        HEADER1=('Canadian agency',TAB,                                         
                 'Acct#',TAB,                                                   
                 'Ref#',TAB,                                                    
                 'System',TAB,                                                  
                 'Media',TAB,                                                   
                 'Client',TAB,                                                  
                 'Product',TAB,                                                 
                 'Est',TAB,                                                     
                 'Media Office',TAB,                                            
                 'Activity Date',TAB,                                           
                 'Month of Service',TAB,                                        
                 'Invoice Number',TAB,                                          
                 'Payee',TAB,                                                   
                 'Payee Type',TAB,                                              
                 'Error Message',TAB),                                          
       BUILD=(A_RESOURCES_AGY,TAB,                                              
              PXG$ACCD,TAB,                                                     
              PXG$TRRF,TAB,                                                     
              PXG$SYS,TAB,                                                      
              PXG$MED,TAB,                                                      
              PXG$PRD_CLT,TAB,                                                  
              PXG$PRD_PRD,TAB,                                                  
              PXG$EST_EST,TAB,                                                  
              PXG$ACDT,TAB,                                                     
              PXG$MOS,TAB,                                                      
              PXG$INV,TAB,                                                      
              PXG$PAYE,TAB,                                                     
              PXG$VORR,TAB,                                                     
              A_ERROR_TEXT,TAB,                                                 
              200:X)         *** record must be long enough for header          
                                                                                
                                                                                
./ ADD NAME=COPXCNTL                                                            
*                                                                               
* Generate the almost-final XML dataset.                                        
*                                                                               
* This OUTREC is used to format the Invoice Header field lines in XML           
* format. This is necessary because the formatting features are not             
* available under HEADER3.                                                      
*                                                                               
 OUTREC OVERLAY=(X_MEDIA_SYSTEM:                                                
        C'<Media_System>MEDIAOCEAN_US</Media_System>',                          
   X_AGY_ALPHA:D_PXG$AGY,JFY=(SHIFT=LEFT,LEAD=C'<Agency_Alpha_Code>',           
      TRAIL=C'</Agency_Alpha_Code>',LENGTH=60),                                 
     X_SEQ:D_PXG$SEQ,JFY=(SHIFT=LEFT,LEAD=C'<Sequence>',                        
      TRAIL=C'</Sequence>',LENGTH=60),                                          
     X_AGY_ACTION:D_PXG$ACN,JFY=(SHIFT=LEFT,LEAD=C'<Action>',                   
      TRAIL=C'</Action>',LENGTH=60),                                            
     X_VOUCHER:D_PXG$VOUC,JFY=(SHIFT=LEFT,LEAD=C'<Voucher_No>',                 
      TRAIL=C'</Voucher_No>',LENGTH=60),                                        
     X_VENDOR:D_PXG$NETW,JFY=(SHIFT=LEFT,LEAD=C'<Vendor>',                      
      TRAIL=C'</Vendor>',LENGTH=60),                                            
     X_INVOICE_NO:D_PXG$INV,JFY=(SHIFT=LEFT,                                    
      LEAD=C'<Invoice_No>',TRAIL=C'</Invoice_No>',LENGTH=60),                   
     X_COMPANY_CODE:D_PXG$OFFC,JFY=(SHIFT=LEFT,                                 
      LEAD=C'<Company_Code>',TRAIL=C'</Company_Code>',LENGTH=60),               
     X_GROSS$:D_GROSS$,EDIT=(SIIIIIIIIIIIT.TT),SIGNS=(,-),                      
     X_NETA:D_PXG$NETA,EDIT=(SIIIIIIIIIIIT.TT),SIGNS=(,-),                      
     X_TAXA:D_PXG$TAXA,EDIT=(SIIIIIIIIIIIT.TT),SIGNS=(,-),                      
     X_GROSS_AMOUNT:X_NETA,JFY=(SHIFT=LEFT,                                     
      LEAD=C'<Gross_Amount>',TRAIL=C'</Gross_Amount>',LENGTH=60),               
     X_CURRENCY:D_PXG$CURR,JFY=(SHIFT=LEFT,                                     
      LEAD=C'<Currency>',TRAIL=C'</Currency>',LENGTH=60),                       
     X_INVOICE_DATE:D_PXG$TRDT,JFY=(SHIFT=LEFT,                                 
      LEAD=C'<Invoice_Date>',TRAIL=C'</Invoice_Date>',LENGTH=60),               
     X_DUE_DATE:D_PXG$DUDT,JFY=(SHIFT=LEFT,                                     
      LEAD=C'<Due_Date>',TRAIL=C'</Due_Date>',LENGTH=60),                       
     X_TRANS_TYPE:D_PXG$STYP,JFY=(SHIFT=LEFT,                                   
      LEAD=C'<Trans_Type>',TRAIL=C'</Trans_Type>',LENGTH=60),                   
     X_USERID:D_PXG$PID,JFY=(SHIFT=LEFT,                                        
      LEAD=C'<User_Id>',TRAIL=C'</User_Id>',LENGTH=60),                         
     X_ADVERTISER:D_PXG$CNT,JFY=(SHIFT=LEFT,                                    
      LEAD=C'<Advertiser>',TRAIL=C'</Advertiser>',LENGTH=60),                   
     X_POSTING_DATE:D_PXG$TRDT,JFY=(SHIFT=LEFT,                                 
      LEAD=C'<Posting_Date>',TRAIL=C'</Posting_Date>',LENGTH=60))               
*                                                                               
 OUTFIL FNAMES=TEMPXML,                                                         
        REMOVECC,                                                               
        NULLOFL=RC4,          *** SET RC=4 IF THERE ARE NO DATA RECORDS         
 BUILD=(05:C'<LineItems>',/,                                                    
     07:D_PXG$ITMN,JFY=(SHIFT=LEFT,                                             
        LEAD=C'<Item_No>',TRAIL=C'</Item_No>',LENGTH=74),/,                     
     07:C'<GL_Account/>'/,        *** REQUIRED BY XSD SCHEMA                    
     07:X_NETA,JFY=(SHIFT=LEFT,                                                 
        LEAD=C'<Net_Amount>',TRAIL=C'</Net_Amount>',LENGTH=74),/,               
     07:D_PXG$TAXC,JFY=(SHIFT=LEFT,                                             
        LEAD=C'<Tax_Code>',TRAIL=C'</Tax_Code>',LENGTH=74),/,                   
     07:X_TAXA,JFY=(SHIFT=LEFT,                                                 
        LEAD=C'<Tax_Amount>',TRAIL=C'</Tax_Amount>',LENGTH=74),/,               
     07:C'<Customer/>'/,                                                        
     07:C'<Bill_Ind>B</Bill_Ind>'/,                                             
     07:D_PXG$MOS,JFY=(SHIFT=LEFT,                                              
        LEAD=C'<MOS>',TRAIL=C'</MOS>',LENGTH=74),/,                             
     07:D_PXG$SYS,JFY=(SHIFT=LEFT,                                              
        LEAD=C'<Media_Type>',TRAIL=C'</Media_Type>',LENGTH=74),/,               
     07:D_PXG$MED,JFY=(SHIFT=LEFT,                                              
        LEAD=C'<Submedia>',TRAIL=C'</Submedia>',LENGTH=74),/,                   
     07:D_PXG$NARR,JFY=(SHIFT=LEFT,                                             
        LEAD=C'<Item_Text>',TRAIL=C'</Item_Text>',LENGTH=74),/,                 
     07:D_PXG$PRD,JFY=(SHIFT=LEFT,                                              
        LEAD=C'<Product>',TRAIL=C'</Product>',LENGTH=74),/,                     
     07:D_PXG$EST,JFY=(SHIFT=LEFT,                                              
        LEAD=C'<Estimate>',TRAIL=C'</Estimate>',LENGTH=74),/,                   
     07:D_PXG$INSO,JFY=(SHIFT=LEFT,                                             
        LEAD=C'<Insertion_Order>',                                              
        TRAIL=C'</Insertion_Order>',LENGTH=75),/,                               
     07:D_PXG$MEDA,JFY=(SHIFT=LEFT,LEAD=C'<Media_Authorization>',               
        TRAIL=C'</Media_Authorization>',LENGTH=75),/,                           
     07:D_PXG$BUYU,JFY=(SHIFT=LEFT,LEAD=C'<Buy_Unique_Num>',                    
        TRAIL=C'</Buy_Unique_Num>',LENGTH=100),/,                               
     07:D_PXG$PLID,JFY=(SHIFT=LEFT,LEAD=C'<Placement_Id>',                      
        TRAIL=C'</Placement_Id>',LENGTH=100),/,                                 
     05:C'</LineItems>',80:X),                                                  
 HEADER1=('<?xml version="1.0" encoding="UTF-8" ?>',/,                          
          '<n1:MediaVendorClearance',/,                                         
          'xmlns:n1="urn:publicis.com:0054:PUBLICIS_I_PUR_MEDIA1.0">'),         
 SECTIONS=(D_PXG$SEQ,                                                           
           D_PXG$SYS,                                                           
           D_PXG$MED,                                                           
           D_PXG$CNT,                                                           
           D_PXG$PRD,                                                           
           D_PXG$VOUC,                                                          
           D_PXG$EST,                                                           
           D_PXG$INV,                                                           
           D_PXG$INSO,                                                          
           D_PXG$MEDA,                                                          
           D_PXG$BUYU,                                                          
       HEADER3=(03:'<Header>',/,                                                
*               05:X_SEQ,/,                                                     
                05:X_MEDIA_SYSTEM,/,                                            
                05:X_AGY_ALPHA,/,                                               
                05:X_AGY_ACTION,/,                                              
                05:X_VOUCHER,/,                                                 
                05:X_VENDOR,/,                                                  
                05:X_INVOICE_NO,/,                                              
                05:X_COMPANY_CODE,/,                                            
                05:X_GROSS_AMOUNT,/,                                            
                05:X_CURRENCY,/,                                                
                05:'<Exchange_Rate/>',/,                                        
                05:X_INVOICE_DATE,/,                                            
                05:X_DUE_DATE,/,                                                
                05:X_TRANS_TYPE,/,                                              
                05:X_USERID,/,                                                  
                05:X_ADVERTISER,/,                                              
                05:X_POSTING_DATE),                                             
       TRAILER3=(03:'</Header>')),                                              
 TRAILER1=('</n1:MediaVendorClearance>')                                        
                                                                                
                                                                                
./ ADD NAME=CPTXCNTL                                                            
*                                                                               
* Generate the final XML dataset. Simplify all XML tags that contain            
* empty fields.                                                                 
*                                                                               
 OUTFIL FINDREP=(INOUT=(C'<MOS></MOS>',C'<MOS/>',                               
      C'<Media_System></Media_System>',C'<Media_System/>',                      
      C'<Invoice_No></Invoice_No>',C'<Invoice_No/>',                            
      C'<Company_Code></Company_Code>',C'<Company_Code/>',                      
      C'<Gross_Amount></Gross_Amount>',C'<Gross_Amount/>',                      
      C'<Currency></Currency>',C'<Currency/>',                                  
      C'<Voucher_No></Voucher_No>',C'<Voucher_No/>',                            
      C'<Exchange_Rate></Exchange_Rate>',C'<Exchange_Rate/>',                   
      C'<Invoice_Date></Invoice_Date>',C'<Invoice_Date/>',                      
      C'<Due_Date></Due_Date>',C'<Due_Date/>',                                  
      C'<Trans_Type></Trans_Type>',C'<Trans_Type/>',                            
      C'<Media_Client></Media_Client>',C'<Media_Client/>',                      
      C'<Media_Product></Media_Product>',C'<Media_Product/>',                   
      C'<Campaign_Ref></Campaign_Ref>',C'<Campaign_Ref/>',                      
      C'<Discount></Discount>',C'<Discount/>',                                  
      C'<Accrual></Accrual>',C'<Accrual/>',                                     
      C'<Advertiser></Advertiser>',C'<Advertiser/>',                            
      C'<Posting_Date></Posting_Date>',C'<Posting_Date/>',                      
      C'<Item_No></Item_No>',C'<Item_No/>',                                     
      C'<Net_Amount></Net_Amount>',C'<Net_Amount/>',                            
      C'<Tax_Code></Tax_Code>',C'<Tax_Code/>',                                  
      C'<Tax_Amount></Tax_Amount>',C'<Tax_Amount/>',                            
      C'<Vendor></Vendor>',C'<Vendor/>',                                        
      C'<MOS></MOS>',C'<MOS/>',                                                 
      C'<Media_Type></Media_Type>',C'<Media_Type/>',                            
      C'<Submedia></Submedia>',C'<Submedia/>',                                  
      C'<Item_Text></Item_Text>',C'<Item_Text/>',                               
      C'<Media_Campaign></Media_Campaign>',C'<Media_Campaign/>',                
      C'<Network></Network>',C'<Network/>',                                     
      C'<Estimate></Estimate>',C'<Estimate/>',                                  
      C'<Insertion_Order></Insertion_Order>',C'<Insertion_Order/>',             
      C'<Product></Product>',C'<Product/>',                                     
      C'<Media_Authorization></Media_Authorization>',                           
      C'<Media_Authorization/>',                                                
      C'<Estimate_Unique_Num></Estimate_Unique_Num>',                           
      C'<Estimate_Unique_Num/>',                                                
      C'<Buy_Unique_Num></Buy_Unique_Num>',C'<Buy_Unique_Num/>',                
      C'<Placement_Id></Placement_Id>',C'<Placement_Id/>'))                     
                                                                                
