*          DATA SET OP151      AT LEVEL 009 AS OF 05/01/02                      
         TITLE 'TAPE LIBRARY UPDATE'                                            
TLIB     CSECT                                                                  
R00      EQU   0                                                                
R01      EQU   1                                                                
R02      EQU   2                                                                
R03      EQU   3                                                                
R04      EQU   4                                                                
R05      EQU   5                                                                
R06      EQU   6                                                                
R07      EQU   7                                                                
R08      EQU   8                                                                
R09      EQU   9                                                                
R10      EQU   10                                                               
R11      EQU   11                                                               
R12      EQU   12                                                               
R13      EQU   13                                                               
R14      EQU   14                                                               
R15      EQU   15                                                               
*                                                                               
IN       DTFMT DEVADDR=SYS010,IOAREA1=T1,IOAREA2=T2,FILABL=STD,        X        
               EOFADDR=TEND,RECFORM=FIXBLK,REWIND=UNLOAD,TYPEFLE=INPUT,X        
               WORKA=YES,                                              X        
               BLKSIZE=800,RECSIZE=80                                           
*                                                                               
OUT      DTFMT DEVADDR=SYS011,IOAREA1=T3,IOAREA2=T4,FILABL=STD,        X        
               RECFORM=FIXBLK,TYPEFLE=OUTPUT,                          X        
               WORKA=YES,                                              X        
               BLKSIZE=800,RECSIZE=80                                           
*                                                                               
PRINT    DTFPR BLKSIZE=132,CONTROL=YES,DEVADDR=SYSLST,IOAREA1=P1,      X        
               PRINTOV=YES,RECFORM=FIXUNB,WORKA=YES                             
*                                                                               
CARD     DTFCD BLKSIZE=80,DEVADDR=SYSIPT,DEVICE=3505,                  X        
               EOFADDR=CEND,IOAREA1=C1,                                X        
               RECFORM=FIXUNB,TYPEFLE=INPUT,WORKA=YES                           
         ENTRY START                                                            
T1       DS    800C                                                             
T2       DS    800C                                                             
T3       DS    800C                                                             
T4       DS    800C                                                             
P1       DS    132C                                                             
C1       DS    80C                                                              
*                                                                               
         EJECT                                                                  
START    BASR  R11,0               BASE REGISTER                                
         USING *,R11                                                            
         COMRG                                                                  
         MVC   DATE(2),0(R01)                                                   
         MVC   DATE+2(2),3(R01)    BUILD DATE                                   
         MVC   DATE+4(2),6(R01)                                                 
         XC    PL(132),PL          ZERO PRINT LINE                              
         BAS   R09,HDL             PRINT HEADLINE                               
*                                                                               
         OPEN  IN,OUT                                                           
         MVC   AWRK(5),=C'A0000'   PRESET                                       
         BAS   R10,GCARD           GET CARD                                     
S1       BAS   R10,RIN             READ TAPE                                    
*                                                                               
S2       CLC   CNUM,TNUM           CARD COMP TO TAPE                            
         BC    12,S4               CARD EQ OR LS                                
*                                                                               
S3       BAS   R10,WOUT            WRITE                                        
         B     S1                  GO GET NEXT INPUT                            
*                                                                               
S4       BC    8,S5                CARD EQ TAPE. GO TEST TRANSACTION            
         B     CERR1               PRINT ERROR CARD                             
*                                                                               
S4C  EQU  *   WAS  CNTRL CARD,PS,1                                              
S4B      BAS   R10,GCARD           GET CARD                                     
         BAS   R09,SKIP1           SKIP 1 LINE                                  
         B     S1                                                               
*                                                                               
S5       MVC   PL(5),TWRK          MOVE THIS TAPE REC TO PRINT LINE             
         MVC   PL+7(75),TWRK+5                                                  
         CLI   CODE,C'A'           TEST TRAN CODE                               
         BE    ADD                                                              
         CLC   AWRK(5),CNUM        TEST FOR ADD OVERRUN                         
         BC    2,CERR8             YES                                          
         CLI   CODE,C'C'                                                        
         BE    CHANGE                                                           
         CLI   CODE,C'D'                                                        
         BE    DELETE                                                           
         CLI   CODE,C'P'                                                        
         BE    PURGE                                                            
         CLI   CODE,C'S'                                                        
         BE    SAVE                                                             
         B     CERR2               CODE NOT VALID                               
*                                                                               
         EJECT                                                                  
         DS    0H                                                               
ADD      EQU   *                                                                
         LA    R02,CNRLS           POINT TO NUMBER OF REEELS                    
         LA    R03,7                                                            
         BAS   R09,NTST            TEST FOR NUMERICS                            
         B     CERR3                                                            
         BAS   R10,WOUT            WRITE MATCHED OUTPUT                         
ADD2     PACK  PKFLD(8),CNUM+1(4)                                               
         CVB   R14,PKFLD                                                        
         ST    R14,NEXT            SET NEXT REEL NUMBER TO ASSIGN               
         PACK  PKFLD(8),CNRLS(3)                                                
         CVB   R04,PKFLD                                                        
         ST    R04,NUMS            SET NUMBER OF REELS                          
         LTR   R04,R04             NUMBER OF REELS ZERO                         
         BC    7,*+8               NO. OK                                       
         B     CERR4               ERROR                                        
         MVC   PL(80),CWRK                                                      
         BAS   R09,PT              PRINT CARD IMAGE                             
          MVC  PL(5),TWRK                                                       
         MVC   PL+7(75),TWRK+5                                                  
* ADD A RECORD                                                                  
         BAS   R09,PT              PRINT LAST GOOD LINE                         
ADD3     L     R14,NEXT            BUMP REEL NUMBER                             
         LA    R14,1(R14)                                                       
         ST    R14,NEXT                                                         
         MVI   AWRK,C' '                                                        
         MVC   AWRK+1(79),AWRK                                                  
         MVC   AWRK(1),CNUM                                                     
         L     R14,NEXT                                                         
         BAS   R10,CVD                                                          
         MVC   AWRK+1(4),CNFD+4                                                 
         MVC   AWRK+5(7),=C'0000000'                                            
         MVC   AWRK+64(6),DATE     LAST ACT                                     
         MVC   AWRK+70(6),DATE     INTO LIBRARY                                 
         MVC   AWRK+76(4),CDENS    DENSITY                                      
         PUT   OUT,AWRK                                                         
         L     R14,OCNT            BUMP OUTPUT CNTR                             
         LA    R14,1(R14)                                                       
         ST    R14,OCNT                                                         
         L     R14,ACNT            BUMP ADD COUNTER                             
         LA    R14,1(R14)                                                       
         ST    R14,ACNT                                                         
         MVC   PL(5),AWRK          SET UP PRINT LINE                            
         MVC   PL+7(75),AWRK+5                                                  
         MVC   PL+85(5),=C'ADDED'                                               
         BAS   R09,PT              PRINT                                        
         BCT   R04,ADD3            LOOP                                         
         B     S4C                 ALL DONE. GO SELECT GOOD POCKET              
*                                                                               
NUMS     DC    F'0'                                                             
NEXT     DC    F'0'                                                             
*                                                                               
         EJECT                                                                  
CHANGE   CLC   TRUN(5),=C'00000'   SCRATCH TAPE?                                
         BC    8,CERR5                                                          
         MVC   AWRK(80),TWRK       SAVE OLD                                     
*                                                                               
CH1      CLC   CRUN(5),=5CL1' '                                                 
         BE    CH2                                                              
         MVC   TRUN(5),CRUN                                                     
CH2      CLC   CFLE(1),=1CL1' '                                                 
         BE    CH3                                                              
         BAS   R10,FLETST                                                       
         MVC   TFLE(1),CFLE                                                     
CH3      CLC   CNON(2),=2CL1' '                                                 
         BE    CH4                                                              
         BAS   R10,NONTST                                                       
         MVC   TNON(4),CNON                                                     
CH4      CLC   CRDTE(6),=6CL1' '                                                
         BE    CH5                                                              
         BAS   R10,RDTETST                                                      
         MVC   TRDTE(6),CRDTE                                                   
CH5      CLC   CDES(20),=20CL1' '                                               
         BE    CH6                                                              
         MVC   TDES,CDES                                                        
CH6      CLC   CPDTE(6),=6CL1' '                                                
         BE    CH7                                                              
         BAS   R10,PDTETST                                                      
         MVC   TPDTE(6),CPDTE                                                   
CH7      CLC   CDDAT(6),=6CL1' '                                                
         BE    CH8                                                              
         BAS   R10,DDATST                                                       
         MVC   TDDAT(6),CDDAT                                                   
*                                                                               
CH8      MVC   PL(80),CWRK                                                      
         BAS   R09,PT              PRINT CARD IMAGE                             
         MVC   TDLACT(6),DATE      SET DATE OF LAST ACTIVITY                    
         BAS   R10,WOUT                                                         
         MVC   PL(5),AWRK                                                       
         MVC   PL+7(75),AWRK+5     MOVE OLD TO PRINT LINE                       
         BAS   R09,PT              WRITE OLD                                    
         MVC   PL(5),TWRK          SET                                          
         MVC   PL+7(75),TWRK+5     NEW                                          
         MVC   PL+85(7),=C'CHANGED'                                             
         BAS   R09,PT              WRITE NEW                                    
         L     R14,CCNT                                                         
         LA    R14,1(R14)                                                       
         ST    R14,CCNT                                                         
         B     S4C                 GO SELECT GOOD POCKET                        
*                                                                               
         EJECT                                                                  
DELETE   LA    R02,CNRLS                                                        
         LA    R03,3                                                            
         BAS   R09,NTST                                                         
         B     CERR3                                                            
         PACK  PKFLD(8),CNRLS(3)                                                
         CVB   R04,PKFLD                                                        
         LTR   R04,R04                                                          
         BC    8,CERR4                                                          
         MVC   PL(80),CWRK                                                      
         BAS   R09,PT              PRINT CARD IMAGE                             
*                                                                               
DE1      CLC   TRUN(5),=C'00000'   MUST BE SCRATCH                              
         BNE   CERR6               NO. ERROR                                    
         MVC   PL(5),TWRK                                                       
         MVC   PL+7(75),TWRK+5                                                  
         MVC   PL+85(7),=C'DELETED'                                             
         BAS   R09,PT              WRITE DELETED REC                            
         L     R14,DCNT                                                         
         LA    R14,1(R14)                                                       
         ST    R14,DCNT                                                         
         BCT   R04,DE2             LOOP ON NUMBER TO  BE DELETED                
         B     S4C                 ALL DONE                                     
DE2      BAS   R10,RIN             NO, DELETE                                   
         B     DE1                 LOOP                                         
*                                                                               
         EJECT                                                                  
PURGE    MVC   PL(80),CWRK                                                      
         BAS   R09,PT              PRINT CARD IMAGE                             
         MVC   PL(5),TWRK                                                       
         MVC   PL+7(75),TWRK+5                                                  
         BAS   R09,PT              PRINT OLD                                    
         MVI   TRUN,C' '           SET TO BLANK                                 
         MVC   TRUN+1(51),TRUN                                                  
         MVC   TRUN(7),=C'0000000'                                              
*                                                                               
         MVC   TDLACT(6),DATE                                                   
         MVC   PL(5),TWRK                                                       
         MVC   PL+7(75),TWRK+5     MOVE TO PRIN YLINE                           
         MVC   PL+85(6),=C'PURGED'                                              
         L     R14,PUNT                                                         
         LA    R14,1(R14)                                                       
         ST    R14,PUNT                                                         
         BAS   R09,PT                                                           
         BAS   R10,WOUT            WRITE NEW OUT PUT                            
         B     S4C                                                              
         EJECT                                                                  
SAVE     CLC   TRUN(5),=C'00000'                                                
         BNE   CERR7                                                            
         MVC   AWRK(80),TWRK       SAVE OLD                                     
*                                                                               
         MVC   TRUN(7),CRUN        SET RUN NUMBER                               
         BAS   R10,FLETST                                                       
         MVC   TFLE(1),CFLE                                                     
         BAS   R10,NONTST                                                       
         MVC   TNON(4),CNON                                                     
         BAS   R10,RDTETST                                                      
         MVC   TRDTE(6),CRDTE                                                   
         MVC   TDES,CDES                                                        
         BAS   R10,PDTETST                                                      
         MVC   TPDTE(6),CPDTE                                                   
         BAS   R10,DDATST                                                       
         MVC   TDDAT(6),CDDAT                                                   
*                                                                               
         MVC   TDLACT(6),DATE      SET DATE OF LAST ACTIVITY                    
         BAS   R10,WOUT                                                         
         MVC   PL(80),CWRK         MOVE CARD OMAGE                              
         BAS   R09,PT              PRINT IT                                     
         MVC   PL(5),AWRK          MOVE OLD TO PRINT LINE                       
         MVC   PL+7(75),AWRK+5                                                  
         BAS   R09,PT              PRINT IT                                     
         MVC   PL(5),TWRK                                                       
         MVC   PL+7(75),TWRK+5                                                  
         MVC   PL+85(5),=C'SAVED'                                               
         BAS   R09,PT                                                           
         L     R14,SCNT                                                         
         LA    R14,1(R14)                                                       
         ST    R14,SCNT                                                         
         B     S4C                                                              
*                                                                               
         EJECT                                                                  
FLETST   LA    R02,CFLE                                                         
         LA    R03,1                                                            
FLE1     BAS   R09,NTST                                                         
         B     CERR3                                                            
         BR    R10                                                              
NONTST   LA    R02,CNON                                                         
         LA    R03,4                                                            
         B     FLE1                                                             
RDTETST  LA    R02,CRDTE                                                        
         LA    R03,6                                                            
         B     FLE1                                                             
PDTETST  LA    R02,CPDTE                                                        
         CLC   0(6,R02),=C'      '    BLANKS                                    
         BNE   PDTET2              NO                                           
         MVC   TPDTE(6),TRDTE      MOVE RUN DATE TO PURGE DATE                  
         PACK  PKFLD(8),TRDTE(2)                                                
         AP    PKFLD(8),=P'3'      ADD 3 MONTHS                                 
         CP    PKFLD+6(2),=P'13'   GR TN 13?                                    
         BL    PDTET1              NO                                           
         SP    PKFLD+6(2),=P'12'                                                
         UNPK  CNFD(8),PKFLD(8)                                                 
         OI    CNFD+7,X'F0'                                                     
         MVC   TPDTE(2),CNFD+6                                                  
         PACK  PKFLD(8),TRDTE+4(2)  PACK YEAR                                   
         AP    PKFLD+6(2),=P'1'    ADD ONE TO YEAR                              
         UNPK  CNFD(8),PKFLD(8)                                                 
         OI    CNFD+7,X'F0'                                                     
         MVC   TPDTE+4(2),CNFD+6                                                
         B     6(R10)              BRANCH AROUN MOVE                            
PDTET1   UNPK  CNFD(8),PKFLD(8)                                                 
         OI    CNFD+7,X'F0'                                                     
         MVC   TPDTE(2),CNFD+6                                                  
         B     6(R10)              BRANCH AROUN MOVE                            
PDTET2   EQU   *                                                                
         LA    R03,6                                                            
         B     FLE1                                                             
DDATST   LA    R02,CDDAT                                                        
         LA    R03,6                                                            
         B     FLE1                                                             
*                                                                               
NTST     CLI   0(R02),C'0'                                                      
         BCR   4,R09                                                            
         CLI   0(R02),C'9'                                                      
         BCR   2,R09                                                            
NTST2    LA    R02,1(R02)                                                       
         BCT   R03,NTST                                                         
         B     4(R09)                                                           
*                                                                               
ICNT     DC    F'0'                INPUT CNTR                                   
ACNT     DC    F'0'                ADD CNTR                                     
DCNT     DC    F'0'                DELETE CNTR                                  
OCNT     DC    F'0'                OUTPUT CNTR                                  
PUNT     DC    F'0'                PURGE CNTR                                   
SCNT     DC    F'0'                SAVE CNTR                                    
CCNT     DC    F'0'                CHANGE CNTR                                  
CDCNT    DC    F'0'                CARD READ CNTR                               
CDECT    DC    F'0'                ERROR CARD CNTR                              
*                                                                               
MS1      DC    C'INPUT   00000'                                                 
         DC    C'ADDS    00000'                                                 
         DC    C'DELETES 00000'                                                 
         DC    C'OUTPUT  00000'                                                 
         DC    C'PURGES  00000'                                                 
         DC    C'SAVES   00000'                                                 
         DC    C'CHANGES 00000'                                                 
         DC    C'CARD IN 00000'                                                 
         DC    C'REJECTS 00000'                                                 
DATE     DS    6C                                                               
*                                                                               
         EJECT                                                                  
*                                                                               
CVD      CVD   R14,PKFLD                                                        
         UNPK  CNFD(8),PKFLD(8)                                                 
         OI    CNFD+7,X'F0'                                                     
         BR    R10                                                              
PKFLD    DS    D                                                                
CNFD     DS    D                                                                
*                                                                               
PT       PUT   PRINT,PL                                                         
         XC    PL(132),PL                                                       
         LH    R14,LCNT                                                         
         LA    R14,1(R14)                                                       
         STH   R14,LCNT                                                         
         CLC   LCNT(2),=H'57'                                                   
         BCR   12,R09              RETURN                                       
HDL      CNTRL PRINT,SK,1          SKIP TO TOP                                  
         MVC   PL+3(4),=C'DATE'                                                 
         MVC   PL+8(2),DATE                                                     
         MVC   PL+11(2),DATE+2                                                  
         MVC   PL+14(2),DATE+4                                                  
         MVI   PL+10,C'/'                                                       
         MVI   PL+13,C'/'                                                       
         MVC   PL+55(24),=C'TAPE LIBRARY UPDATE PASS'                           
         LH    R14,PCNT                                                         
         LA    R14,1(R14)                                                       
         STH   R14,PCNT                                                         
         BAS   R10,CVD                                                          
         MVC   PL+121(4),=C'PAGE'                                               
         MVC   PL+126(3),CNFD+5                                                 
         PUT   PRINT,PL                                                         
         CNTRL PRINT,SP,2          SKIP 2 LINES                                 
         XC    LCNT(2),LCNT                                                     
         XC    PL(132),PL                                                       
         BR    R09                                                              
*                                                                               
LCNT     DC    H'0'                                                             
PCNT     DC    H'0'                                                             
*                                                                               
SKIP1    CNTRL PRINT,SP,1                                                       
         BR    R09                                                              
SKIP2    CNTRL PRINT,SP,2                                                       
         BR    R09                                                              
*                                                                               
RIN      GET   IN,TWRK                                                          
         L     R14,ICNT                                                         
         LA    R14,1(R14)                                                       
         ST    R14,ICNT                                                         
         BR    R10                                                              
*                                                                               
WOUT     PUT   OUT,TWRK                                                         
         L     R14,OCNT                                                         
         LA    R14,1(R14)                                                       
         ST    R14,OCNT                                                         
         BR    R10                                                              
*                                                                               
GCARD    GET   CARD,CWRK                                                        
         L     R14,CDCNT                                                        
         LA    R14,1(R14)                                                       
         ST    R14,CDCNT                                                        
         BR    R10                                                              
*                                                                               
CEND     MVC   CNUM(5),=5XL1'FF'   FORCE TO HI                                  
         BR    R10                                                              
*                                                                               
TEND     CLOSE IN,OUT                                                           
         BAS   R09,HDL             PRINT HEADLINE                               
         MVC   PL(13),=C'RECORD COUNTS'                                         
         BAS   R09,PT                                                           
         BAS   R09,SKIP1                                                        
         LA    R06,9               SET LOOP COUNTER                             
         LA    R08,MS1             POINT TO OUTPUT                              
         LA    R07,ICNT            POINT TO COUNTERS                            
TE1      L     R14,0(R07)          LOAD CNTR                                    
         BAS   R10,CVD             CONVERT                                      
         MVC   8(5,R08),CNFD+3     MOVE T O OUTPUT                              
         LA    R08,13(R08)         BUMP OUTPUT PTR                              
         LA    R07,4(R07)          BUMP CNTR PTR                                
         BCT   R06,TE1             LOOP                                         
*                                                                               
         LA    R08,MS1             POINT TO FIRST MESSAGE                       
         LA    R06,9               SET LOOP COUNTER                             
         LA    R07,13              SET LENGTH                                   
TE2      MVC   PL(13),0(R08)                                                    
         BAS   R09,PT                                                           
         LA    R08,13(R08)         BUMP MSG PTR                                 
         BCT   R06,TE2             LOOP                                         
         EOJ                                                                    
*                                                                               
         EJECT                                                                  
CERR1    MVC   PL+99(11),=C'NOT A MATCH'                                        
CERRX    MVC   PL(80),CNUM                                                      
         MVC   PL+85(11),=C'***ERROR***'                                        
         BAS   R09,PT                                                           
*        CNTRL CARD,PS,2           SELECT POCKET 2                              
         L     R14,CDECT                                                        
         LA    R14,1(R14)                                                       
         ST    R14,CDECT                                                        
CERRD    BAS   R10,WOUT            WRITE MATCED OUTPUT TO TAPE                  
         B     S4B                 GO READ NEXT CARD AND TAPE                   
CERR2    MVC   PL+99(14),=C'CODE NOT VALID'                                     
         B     CERRX                                                            
CERR3    MVC   PL+99(17),=C'FIELD NOT NUMERIC'                                  
         CLI   CODE,C'S'           SAVE                                         
         BE    CERRA                                                            
         CLI    CODE,C'C'                                                       
         BNE   CERRD                                                            
CERRA    MVC   TWRK(80),AWRK       MOVE NACK OLD                                
         B     CERRX                                                            
CERR4    MVC   PL+99(20),=C'NUMBER OF REELS ZERO'                               
         B     CERRX                                                            
CERR5    MVC   PL+99(18),=C'CHANGING A SCRATCH'                                 
         B     CERRX                                                            
CERR6    MVC   PL+99(23),=C'DELETING AN ACTIVE TAPE'                            
         B     CERRX                                                            
CERR7    MVC   PL+99(24),=C'SAVING OVER A SAVED TAPE'                           
         B     CERRX                                                            
CERR8    MVC   PL+99(11),=C'ADD OVERRUN'                                        
         MVC   CERRD(2),=X'0A0E'   SET TO EOJ                                   
         B     CERRX                                                            
*                                                                               
         EJECT                                                                  
*                                                                               
TWRK     DS    0CL80                                                            
TNUM     DS    CL5                 REEL NUMBER                                  
TRUN     DS    CL7                 RUN NUMBER                                   
TDDAT    DS    CL6                 DATE OF DATA                                 
TRDTE    DS    CL6                 RUN DATE                                     
TNON     DS    CL4                 REEL OF REEL                                 
TDES     DS    CL29                DESCRIPTION                                  
TPDTE    DS    CL6                 PURGE DATE                                   
TFLE     DS    CL1                 OUTPUT FILE                                  
TDLACT   DS    CL6                 DATE LAST ACTIVITY                           
TDLIB    DS    CL6                 DATE INTO LIBRARY                            
TDENS    DS    CL4                 DENSITY                                      
*                                                                               
CWRK     DS    0CL80                                                            
CNUM     DS    CL5                 REEL NUMBER                                  
CRUN     DS    CL7                 RUN NUMBER                                   
CDDAT    DS    CL6                 DATE OF DATA                                 
CRDTE    DS    CL6                 RUN DATE                                     
CNON     DS    CL4                 REEL OF REEL                                 
CDES     DS    CL30                DESCRIPTION                                  
CPDTE    DS    CL6                 PURGE DATE                                   
CFLE     DS    CL1                 OUTPUT FILE                                  
CODE     DS    CL1                 TRANSACTION CODE                             
         DS    CL14                SPARE                                        
CNRLS    EQU   CRUN                NUMBER OF REELS                              
CDENS    EQU   CRUN+3              DENSITY                                      
*                                                                               
*                                                                               
AWRK     DS    80CL1' '                                                         
PL       DS    132C                                                             
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'009OP151     05/01/02'                                      
         END   START                                                            
