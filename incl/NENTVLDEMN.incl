*          DATA SET NENTVLDEMN AT LEVEL 167 AS OF 05/01/02                      
***********************************************************************         
*  TITLE: SUBR                                                        *         
*                                                                     *         
*  INCLUDED FROM: NENETVALUE                                          *         
*                                                                     *         
*  COMMENTS: SEPARATES DEMO FUNCTIONS FROM NETVALUE                   *         
*            ROUTINES CALLED BY:                                      *         
*        GOTO1 SUBR,PLIST,('EQU',(RC)) {,P1}                          *         
*                                                                     *         
*---------------------------------------------------------------------*         
* MOD LOG:                                                            *         
*                                                                     *         
* 28JUN99  (NS ) --- ESTOUT ROUTINE MOVE BOOK FROM BUY                          
* 05APR99  (PXZ) --- CABLE PRECISION ONLY FOR CABLE                             
* 07OCT91  (EFJ) --- ADD CODE TO ADD ELEMS TO UNIT REC FOR CABLE      *         
*                    STATIONS (SETCABLE)                              *         
*                --- ADD IN NMOD STORAGE (OFF R8)                     *         
*                --- CHANGE WHERE SETATPDM LOOKS FOR MIRROR TYPE CODE *         
*                                                                     *         
*                                                                     *         
***********************************************************************         
SUBR     CSECT                                                                  
         DS    8000C               1F40 HEX                                     
         ORG   *-8000                                                           
         NMOD1 SUBRWRKX-SUBRWORK,**SUBR**,R9,CLEAR=YES                          
         LR    R8,RC                                                            
         USING SUBRWORK,R8                                                      
         L     RC,0(R1)            RE-ESTABLISH WORKING STORAGE                 
         USING CONVWORK,RC                                                      
*                                                                               
         L     RE,0(R1)                                                         
         SRL   RE,24               GET THE ROUTINE NUMBER                       
         SLL   RE,2                                                             
         LA    RF,*+2(RE)                                                       
         BR    RF                                                               
*                                                                               
INITDBE  EQU   (INITDB#-*)/4+1                                                  
MANOUTSE EQU   (MANOUTS#-*)/4+1                                                 
ACTOUTSE EQU   (ACTOUTS#-*)/4+1                                                 
ESTOUTSE EQU   (ESTOUTS#-*)/4+1                                                 
STATPDME EQU   (STATPDM#-*)/4+1                                                 
STPRDEME EQU   (STPRDEM#-*)/4+1                                                 
STTPDEME EQU   (STTPDEM#-*)/4+1                                                 
CONOUTE  EQU   (CONOUT#-*)/4+1                                                  
ESTOACTE EQU   (ESTOACT#-*)/4+1                                                 
PRGTOESE EQU   (PRGTOES#-*)/4+1                                                 
OVRHOMEE EQU   (OVRHOME#-*)/4+1                                                 
USEFORME EQU   (USEFORM#-*)/4+1                                                 
GETDEMOE EQU   (GETDEMO#-*)/4+1                                                 
SETATPDE EQU   (SETATPD#-*)/4+1                                                 
BTUREADE EQU   (BTUREAD#-*)/4+1                                                 
*                                                                               
INITDB#  B     INITDB      01      INITIALIZE DBLOCK                            
MANOUTS# B     MANOUTS     02      ACTOUTS, BUT FOR MAN OVERRIDES               
ACTOUTS# B     ACTOUTS     03      ESTOUTS, BUT FILE=NTI                        
ESTOUTS# B     ESTOUTS     04      GET REQUESTED DEMOS FROM UNIT REC            
STATPDM# B     SETATPDM    05      MAKE DEMAND 1/2 HR TIME PER DEMOS            
STPRDEM# B     SETPRDEM    06      MAKE DEMAND RETURN PGM DEMOS                 
STTPDEM# B     SETTPDEM    07      MAKE DEMAND RETURN TIME PER DEMOS            
CONOUT#  B     CONOUT      08      VPH STUFF                                    
ESTOACT# B     ESTTOACT    09      CREATE DUMMY REC WITH ACT DEMO ELEMS         
PRGTOES# B     PRGTOEST    10      CREATE DUMMY REC WITH EST DEMO ELEMS         
OVRHOME# B     OVERHOME    11      USED WHEN DEMOS ARE FM DIFF PLACES           
USEFORM# B     USEFORM     12      ADJUST DEMOS IN DUMMY REC                    
GETDEMO# B     GETDEMOS    13      GET DEMOS FM DEMAND INTO DUMMYREC            
SETATPD# B     SETATPD2    14      SET AFFID TIME FOR CABLE PROG POST           
BTUREAD# B     BTUREAD     15      READ FOR USER FILE                           
*                                                                               
SUBRXIT  XIT1                                                                   
*                                                                               
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.7.2                                                    *         
*  TITLE: INITDB - INITIALIZE DBLOCK FOR ALL USES OF DEMAND.          *         
* ADDITIONAL FIELDS IN DBLOCK ARE SET BY THE CALLING ROUTINE.         *         
*                                                                     *         
*  CALLED FROM: ALL ROUTINES READING THE DEMO FILE                    *         
*                                                                     *         
*  GLOBAL OUTPUTS: DBLOCK                                             *         
***********************************************************************         
INITDB   DS    0H                                                               
         XC    DBLOCK,DBLOCK                                                    
         MVC   DBFILE,=C'NTI'                                                   
         LA    R1,WORKREC                                                       
         ST    R1,DBAREC                                                        
         MVC   DBCOMFCS,NBACOM                                                  
         MVI   DBSELMED,C'N'       INITIALIZE FOR NETWORK                       
         MVI   DBSELSRC,C'N'                                                    
         MVC   DBSELSTA(4),NBACTNET                                             
         CLC   NBAUTH,=XL2'4040'                                                
         BNH   *+10                                                             
         MVC   DBAUTH(2),NBAUTH    MOVE AUTH CODE FOR DEMO SECURITY             
* OUT->  CLI   NBPOSTYP,C'N'                                                    
* OUT->  BE    *+18                                                             
         CLI   NBNTISTA,X'40'                                                   
         BNH   *+10                                                             
         MVC   DBSELSTA(4),NBNTISTA  IF SYNDICATOR USE NTI STATION              
         MVI   DBSELSTA+4,C'T'                                                  
         CLI   NBPOSTYP,X'40'                                                   
         BNH   *+18                                                             
         CLI   NBPOSTYP,C'N'                                                    
         BE    *+10                                                             
         MVC   DBSELSTA+4(1),NBPOSTYP                                           
         MVC   DBSELBK,SAVEBOOK                                                 
         MVC   DBSELAGY,NBSELAGY                                                
         CLC   DBSELBK,=X'5D25'    SWITCH TO QH DATA                            
         BL    CABQHX                                                           
         CLC   DBSELBK,=X'5D31'    BLACK WEEK                                   
         BE    CABQHX                                                           
         CLC   DBSELBK,=X'5D32'    BLACK WEEK                                   
         BE    CABQHX                                                           
         CLC   DBSELBK,=X'5D33'    BLACK WEEK                                   
         BE    CABQHX                                                           
         CLI   DBSELSTA+4,C'C'                                                  
         BNE   CABQHX                                                           
         MVI   DBSELSTA+4,C'Q'                                                  
CABQHX   DS    0H                                                               
         CLI   NBPOSTDT,C'C'       TEST FOR CONFORMED                           
         BNE   *+8                                                              
         MVI   DBBTYPE,C'C'                                                     
         CLI   NBPOSTDT,C'I'       TEST FOR INTEGRATED                          
         BNE   *+8                                                              
         MVI   DBBTYPE,C'I'                                                     
         CLI   NBPOSTDT,C'A'       TEST FOR ASCRIBED                            
         BNE   *+8                                                              
         MVI   DBBTYPE,C'A'                                                     
         CLI   NBPOSTDT,C'Z'       TEST FOR X-RATED                             
         BNE   *+8                                                              
         MVI   DBBTYPE,C'Z'                                                     
         CLI   NBPOSTYP,C'H'                                                    
         BNE   *+8                                                              
         MVI   DBBTYPE,0                                                        
*                                  FIX AGENCY SCREW-UPS                         
         LA    RE,AGSTAEQU                                                      
AGSTEQ   CLC   DBSELSTA(4),0(RE)                                                
         BL    AGSTEQX         <---WON'T BE THERE                               
         BH    *+14            <---TRY NEXT                                     
         MVC   DBSELSTA(4),4(RE)   MOVE NEW STATION TO DBLOCK                   
         B     AGSTEQX                                                          
         LA    RE,L'AGSTAEQU(RE)   BUMP TO NEXT STA EQU                         
         B     AGSTEQ                                                           
*                                                                               
AGSTEQX  BAS   RE,SETDMPRE         SET DEMO PRECISSION                          
*                                                                               
         B     SUBRXIT                                                          
*                           CALL LETTER EQUATES FOR AGENCY STATIONS             
AGSTAEQU DS    0CL8          TABLE MUST BE KEPT IN SEQUENCE                     
         DC    C'CAB ',C'CAX '                                                  
         DC    C'FBC ',C'FOX '                                                  
         DC    C'IND ',C'INX '                                                  
         DC    C'NIK ',C'NICK'                                                  
         DC    C'PAY ',C'PAX '                                                  
         DC    C'PBS ',C'PBX '                                                  
         DC    C'TBS ',C'WTBS'                                                  
         DC    C'TCF ',C'FOX '                                                  
         DC    C'SUP ',C'SUX '                                                  
         DC    X'FF'                                                            
         DS    0F                                                               
         EJECT                                                                  
***********************************************************************         
*  TITLE: MANOUTS                                                     *         
*                                                                     *         
*  CALLED FROM : DOUSEACT                                             *         
*                                                                     *         
*  COMMENTS: SAME AS ACTOUTS, EXCEPT USED FOR MANUAL OVERRIDES        *         
*            GETS ACTUAL DEMOS FROM THE UNIT RECORD                   *         
***********************************************************************         
MANOUTS  DS    0H                                                               
         XC    DBLOCK,DBLOCK       GET GARBAGE OUT OF DBLOCK                    
         MVC   DBFILE,=C'NTI'      TO WANT ACTUAL   DEMOS                       
         MVC   DBCOMFCS,NBACOM     A(COMFACS)                                   
         ST    R7,DBAREC           A(UNIT RECORD)                               
         MVC   DBAQUART,ELEM1      A(1ST ELEM ON UNIT REC)                      
         MVI   DBSELMED,C'N'                                                    
         MVI   DBSELSRC,C'N'                                                    
         MVC   DBSELBK,SAVEBOOK                                                 
         GOTO1 =V(NETWEEK),DMCB,EBCDDAT,NBGETDAY,NBADDAY,RR=RELO                
         MVC   DBSELBK(1),4(R1)        NTI YEAR                                 
*951110  MVC   DBSELBK+1(1),0(R1)      NTI WEEK IS HUT WEEK                     
         MVC   DBSELBK+1(1),8(R1)      NTI WEEK IS RTG WEEK                     
*                                                                               
         MVI   BYTE,X'DE'                                                       
         BAS   RE,SETDMPRE         SET DEMO PRECISSION                          
         BAS   RE,SETDMSET         WIPE OUT VPH ELEMENT IN DBAREC               
         GOTO1 NBDEMOUT,DMCB,(C'L',DIALIST),DBLOCK,DOALIST                      
         GOTO1 NBDEMOUT,DMCB,(C'L',DIULIST),DBLOCK,DOULIST                      
         BAS   RE,SETDMPST         RESET DBAREC                                 
         MVC   DBSELBK,SAVEBOOK                                                 
*                                                                               
XITMANO  B     SUBRXIT                                                          
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.8.1                                                    *         
*  TITLE: ACTOUTS                                                     *         
*                                                                     *         
*  CALLED FROM : DOUSEACT                                             *         
*                                                                     *         
*  COMMENTS: SHOULD BE SAME AS ESTOUTS, EXCEPT FILE=NTI.              *         
*            GETS ACTUAL DEMOS FROM THE UNIT RECORD                   *         
*                                                                     *         
*  IF UNIVERSES ARE CABLE, THEN CHANGES ALL OCCURENCES OF HOMES       *         
*    TO CABLE-HOMES.                                                  *         
***********************************************************************         
ACTOUTS  DS    0H                                                               
         CLI   HAVBTU,C'Y'         CHECK FOR USER FILE NEEDED                   
         BNE   ACTOUT1                                                          
*                                                                               
         GOTO1 ASUBR,DMCB,('INITDBE',(RC))                                      
*                                                                               
         MVC   DBSELBK,SAVEBOOK                                                 
*                                                                               
         MVI   BTUTYP,0                                                         
         XC    BTUEXT(12),BTUEXT                                                
         XC    DBEXTEND,DBEXTEND                                                
*        GOTO1 ASUBR,FULL,('BTUREADE',(RC))                                     
         BAS   RE,BTUREAD                                                       
         CLI   GOTUTYPE,1                                                       
         BNE   ACTOUT1                                                          
         MVC   BTUEXT,=C'UFIL'                                                  
         MVC   BTUEX2,BTUDB+4      SET DBAREC                                   
         XC    DBLOCK,DBLOCK                                                    
         LA    RE,BTUEXT                                                        
         ST    RE,DBEXTEND                                                      
         B     ACTOUT1A                                                         
*                                                                               
ACTOUT1  XC    DBLOCK,DBLOCK       GET GARBAGE OUT OF DBLOCK                    
ACTOUT1A MVC   DBFILE,=C'NTI'      TO WANT ACTUAL   DEMOS                       
         MVC   DBCOMFCS,NBACOM     A(COMFACS)                                   
         ST    R7,DBAREC           A(UNIT RECORD)                               
         MVC   DBAQUART,ELEM1      A(1ST ELEM ON UNIT REC)                      
*                                                                               
         B     ACOUT12             BYPASS CABLE UNIVERSE LOGIC                  
*                                                                               
         TM    NBUNST2,X'08'        IF USING CABLE DEMOS,                       
         BZ    ACOUT12              CHANGE HOMES TO CABLE                       
         LA    R4,65                65 DEMOS IN LIST                            
         LA    R3,DIALIST                                                       
ACOUT2   CLI   0(R3),X'FF'          IF END OF LIST                              
         BE    ACOUT12                                                          
         CLI   2(R3),1              IF DEMO IS HOMES                            
         BNE   ACOUT4                                                           
         MVI   2(R3),16             SET IT TO CABLE                             
ACOUT4   LA    R3,3(R3)                                                         
         BCT   R4,ACOUT2            NEXT DEMO                                   
*                                                                               
         LA    R4,21                21 DEMOS IN UNIV LIST                       
         LA    R3,DIULIST                                                       
ACOUT8   CLI   0(R3),X'FF'          IF END OF LIST                              
         BE    ACOUT12                                                          
         CLI   2(R3),1              IF DEMO IS HOMES                            
         BNE   ACOUT10                                                          
         MVI   2(R3),16             SET IT TO CABLE                             
ACOUT10  LA    R3,3(R3)                                                         
         BCT   R4,ACOUT8            NEXT DEMO                                   
*                                                                               
ACOUT12  MVI   BYTE,X'DE'          ACT OVERRIDE                                 
         BAS   RE,SETDMPRE         SET DEMO PRECISSION                          
         GOTO1 NBDEMOUT,DMCB,(C'L',DIALIST),DBLOCK,DOALIST                      
         GOTO1 NBDEMOUT,DMCB,(C'L',DIULIST),DBLOCK,DOULIST                      
*                                                                               
XITACTO  B     SUBRXIT                                                          
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.3.1                                                    *         
*  TITLE: ESTOUTS                                                     *         
*                                                                     *         
*  COMMENTS: GETS THE REQUESTED DEMOS FROM THE UNIT RECORD AND STICKS *         
*          THE RETURNED VALUES IN DOALIST.                            *         
*                                                                     *         
*  CALLED FROM: STDOEST                                               *         
*                 DOESTACT                                            *         
*                 DOPRGREC                                            *         
*                                                                     *         
*  CALLS TO: DEMOUT - RETURNS THE REQUESTED DEMO VALUES               *         
*                                                                     *         
*  INPUTS: NDDEMOS - LIST OF THE DEMOS REQUESTED (WITHOUT PREFIX)     *         
*          NDNDEMOS - THE NUMBR OF SUCH REQUSTS                       *         
*          NBAIO - THE ADDDRESS OF THE UNIT RECORD                    *         
*          ELEM1 - THE LOCATION OF THE FIRST ELEMENT IN UNITREC       *         
*                                                                     *         
*  GLOBAL OUTPUTS: DOALIST - THE VALUES FOR THE REQUESTED DEMOS.      *         
*                      INCLUDES HOME DATA, VPHS GRPS, AND IMPS        *         
*                  DOULIST - UNIVERSES                                *         
*                                                                     *         
* LOCAL OUTPUTS:  DBLOCK - FILLS IN VALUES NEEDED BY DEMOUT           *         
***********************************************************************         
ESTOUTS  DS    0H                                                               
         CLI   HAVBTU,C'Y'         CHECK FOR USER FILE NEEDED                   
         BNE   ESTOUT5                                                          
*                                                                               
         GOTO1 ASUBR,DMCB,('INITDBE',(RC))                                      
*                                                                               
*        MVC   DBSELBK,SAVEBOOK                                                 
         MVC   DBSELBK,=X'6303'    <<<FORCE FOR NOW DEFAULT BOOK                
         MVI   BTUTYP,C'E'                                                      
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNTFIL'),(X'60',NBAIO),(1,=C'J')           
         CLI   DMCB+12,0           IF FOUND EXIT                                
         BNE   ESTOUT1                                                          
         L     RE,12(R1)                                                        
         MVC   DBSELBK,3(RE)        BOOK OVERRIDE FROM BUY RECORD               
         MVI   BTUTYP,0                                                         
*                                                                               
ESTOUT1  XC    BTUEXT(12),BTUEXT                                                
         XC    DBEXTEND,DBEXTEND                                                
*        GOTO1 ASUBR,FULL,('BTUREADE',(RC))                                     
         BAS   RE,BTUREAD                                                       
         CLI   GOTUTYPE,1                                                       
         BNE   ESTOUT5                                                          
         MVC   BTUEXT,=C'UFIL'                                                  
         MVC   BTUEX2,BTUDB+4      SET DBAREC                                   
         XC    DBLOCK,DBLOCK                                                    
         LA    RE,BTUEXT                                                        
         ST    RE,DBEXTEND                                                      
         LA    RF,DOALEN                                                        
         XCEF  DOALIST,(RF)        CLEAR DOALIST FOR ESTIMATED DEMOS            
         B     ESTOUT5A                                                         
*                                                                               
ESTOUT5  LA    RF,DOALEN                                                        
         XCEF  DOALIST,(RF)        CLEAR DOALIST FOR ESTIMATED DEMOS            
*                                                                               
         XC    DBLOCK,DBLOCK       GET GARBAGE OUT OF DBLOCK                    
ESTOUT5A MVC   DBFILE,=C'EVN'      TO WANT ESTIMATE DEMOS                       
         MVC   DBCOMFCS,NBACOM     A(COMFACS)                                   
         ST    R7,DBAREC           A(UNIT RECORD)                               
         MVC   DBAQUART,ELEM1      A(1ST ELEM ON UNIT REC)                      
*                                                                               
         MVI   BYTE,X'DD'                                                       
         BAS   RE,SETDMPRE         SET DEMO PRECISSION                          
         GOTO1 NBDEMOUT,DMCB,(C'L',DIALIST),DBLOCK,DOALIST                      
         GOTO1 NBDEMOUT,DMCB,(C'L',DIULIST),DBLOCK,DOULIST                      
*                                                                               
XITESTO  B     SUBRXIT                                                          
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.4.1                                                    *         
*  TITLE: SETATPDM - SET DBLOCK SO THAT DEMAND WILL RETURN HALF-HOUR  *         
*                    (AFFADAVIT) TIME PERIOD DEMOS                    *         
*                                                                     *         
*  CALLED FROM: STASSRES -ASSIGN RESULT STATE                         *         
*                                                                     *         
*  GLOBAL OUTPUTS: DBLOCK                                             *         
***********************************************************************         
SETATPDM DS    0H                                                               
         MVI   MIRDIV,1            INIT DIVIDOR                                 
         GOTO1 ASUBR,DMCB,('INITDBE',(RC))   SET UP DBLOCK FOR DEMAND           
         MVI   DBFUNCT,DBGETDEM                                                 
         MVC   DBSELDAY,NBDAY                                                   
SETATPD2 XC    DBSELTIM(4),DBSELTIM          RESET START/END TIME               
         MVC   DBSELTIM(2),NBAFFTIM                                             
         DS    0H                                                               
         OC    NBSDAFDT,NBSDAFDT                                                
         BZ    DAF10                                                            
         GOTO1 NBGETDAY,DMCB,EBCDDAT,FULL    GET AFFID DAY                      
         XC    DMCB,DMCB                                                        
         MVC   DMCB+4(4),=X'D9000A03'        GET DAYPAK                         
         GOTO1 NBCALLOV,DMCB                                                    
         L     RF,DMCB                                                          
         GOTO1 (RF),DMCB,(3,FULL),WORK,WORK+10                                  
         CLI   WORK,0                                                           
         BNE   *+6                                                              
         DC    H'0'                                                             
         MVC   DBSELDAY,WORK                                                    
DAF10    DS    0H                                                               
         CLI   NBUSER2+8,C'N'      CHECK FOR BREAKOUT AVG.                      
         BNE   *+10                NO                                           
         MVC   DBSELTIM+2(2),NBAFFTIM                                           
*                                                                               
* BUILD DBXTLD & CHAIN                                                          
DAF11    CLI   NBMIRTYP,C'A'                                                    
         BNE   DAF12                                                            
         MVC   HALF,=H'400'        +4 HOURS                                     
         MVI   MIRDIV,2            SET 2 DAY/TIME ENTRIES                       
         B     DAF50                                                            
*                                                                               
DAF12    CLI   NBMIRTYP,C'B'       -5 HOURS                                     
         BNE   DAF13                                                            
         MVC   HALF,=H'-500'                                                    
         MVI   MIRDIV,2            SET 2 DAY/TIME ENTRIES                       
         B     DAF50                                                            
*                                                                               
DAF13    CLI   NBMIRTYP,C'C'       -11 HOURS                                    
         BNE   DAF14                                                            
         MVC   HALF,=H'-1100'                                                   
         MVI   MIRDIV,2            SET 2 DAY/TIME ENTRIES                       
         B     DAF50                                                            
*                                                                               
DAF14    CLI   NBMIRTYP,C'D'       -6 HOURS                                     
         BNE   DAF15                                                            
         MVC   HALF,=H'-0600'                                                   
         MVI   MIRDIV,2            SET 2 DAY/TIME ENTRIES                       
         B     DAF50                                                            
*                                                                               
DAF15    CLI   NBMIRTYP,C'E'       -9 HOURS                                     
         BNE   DAF16                                                            
         MVC   HALF,=H'-0900'                                                   
         MVI   MIRDIV,2            SET 2 DAY/TIME ENTRIES                       
         B     DAF50                                                            
*                                                                               
DAF16    CLI   NBMIRTYP,C'F'       +3 HOURS                                     
         BNE   XITATPD                                                          
         MVC   HALF,=H'300'                                                     
         MVI   MIRDIV,2            SET 2 DAY/TIME ENTRIES                       
         B     DAF50                                                            
*                                                                               
DAF50    LA    RF,DBXTLWRK                                                      
         USING DBXTLD,RF                                                        
         MVC   DBXTLID,=C'DYTM'                                                 
         XC    DBXTLNXT,DBXTLNXT                                                
         MVI   DBXTLIDX,0                                                       
         MVC   DBXTLIST+0(1),DBSELDAY                                           
         MVC   DBXTLIST+1(2),DBSELTIM                                           
         MVC   DBXTLIST+3(2),DBSELTIM+2                                         
         MVC   DBXTLIST+5(1),DBSELDAY                                           
*                                                                               
         ZICM  RE,DBSELTIM,2                                                    
         AH    RE,HALF                                                          
         CH    RE,=H'2400'                                                      
         BL    *+8                                                              
         SH    RE,=H'2400'                                                      
         STCM  RE,3,DBXTLIST+6                                                  
*                                                                               
         ICM   RE,3,DBSELTIM+2                                                  
         BZ    DAF80                                                            
         AH    RE,HALF                                                          
         CH    RE,=H'2400'                                                      
         BL    *+8                                                              
         SH    RE,=H'2400'                                                      
         STCM  RE,3,DBXTLIST+8                                                  
*                                                                               
DAF80    XC    DBXTLIST+10(5),DBXTLIST+10                                       
         DROP  RF                                                               
*                                                                               
* SET DBEXTEND LINKED LIST CHAIN                                                
         OC    DBEXTEND,DBEXTEND                                                
         BNZ   *+12                                                             
         ST    RF,DBEXTEND                                                      
         B     XITATPD                                                          
*                                                                               
         L     RE,DBEXTEND                                                      
*                                                                               
         OC    4(4,RE),4(RE)       NEXT EXTENSION?                              
         BZ    *+12                NO MORE - LAST                               
         L     RE,4(RE)                                                         
         B     *-14                                                             
*                                                                               
         ST    RF,4(RE)                                                         
*                                                                               
XITATPD  XIT1                                                                   
         B     SUBRXIT                                                          
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.4.2                                                    *         
*  TITLE: SETPRDEM - SET DBLOCK SO THAT DEMAND WILL RETURN PROGRAM    *         
*                    DEMOS                                            *         
*                                                                     *         
*  CALLED FROM: STASSRES - ASSIGN RESULT STATE                        *         
*               STCKVPEX - CHECK IF VPHS EXIST STATE                  *         
*                                                                     *         
*  GLOBAL OUTPUTS: DBLOCK                                             *         
***********************************************************************         
SETPRDEM DS    0H                                                               
         GOTO1 ASUBR,DMCB,('INITDBE',(RC))  SET UP DBLOCK FOR DEMAND            
         MVI   DBFUNCT,DBGETNTI                                                 
         MVC   DBSELPRG,NBNTI      PROGRAM NUMBER                               
         MVC   DBSELDAY,NBDAY      (EXACT DAY)                                  
         MVC   DBSELTIM,NBTIME     (EXACT TIME)                                 
*                                                                               
         CLI   NBPOSTYP,C'C'       CABLE                                        
         BNE   SETPRDM2                                                         
         MVI   DBSELSTA+4,C'C'     SET UP 5TH CHARACTER                         
         MVC   DBSELPTT,SVPSTAFD   AND DETAIL LEVEL                             
         CLI   DBSELPTT,C'D'       DAYLY IS REALLY EPISODE                      
         BNE   *+8                                                              
         MVI   DBSELPTT,C'E'                                                    
         CLI   NBMIRTYP,C' '       CHECK FOR MIRRORS                            
         BH    DAF11                                                            
*                                                                               
SETPRDM2 CLI   NBPOSTYP,C'H'       HISPANIC IS SPECIAL                          
         BNE   XITPRDM                                                          
         OC    NBNTI,NBNTI         OK IF WE HAVE PROGRAM NUMBER                 
         BNZ   XITPRDM                                                          
         MVC   DBSELDAY,NBSDROT    OTHERWISE USE ROTATION                       
*                                                                               
XITPRDM  B     SUBRXIT                                                          
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.4.3                                                    *         
*  TITLE: SETTPDEM - SET DBLOCK SO DEMAND WILL RETURN TIME *                    
*                    PERIOD DEMOS                                     *         
*                                                                     *         
*  CALLED FROM: STASSRES                                              *         
*               STCKEX                                                *         
*               STCKBW                                                *         
*               STCKVPEX                                              *         
*                                                                     *         
*  GLOBAL OUTPUTS: DBLOCK                                             *         
***********************************************************************         
SETTPDEM DS    0H                                                               
         GOTO1 ASUBR,DMCB,('INITDBE',(RC))   SET UP DBLOCK FOR DEMAND           
         MVI   DBFUNCT,DBGETDEM                                                 
         MVC   DBSELDAY,NBDAY                                                   
         MVC   DBSELTIM,NBTIME                                                  
         CLI   NBMIRTYP,C' '       ANY MIRRORS ACTIVE                           
         BH    DAF11               YES PROCESS THEM                             
*                                                                               
XITTPDM  B     SUBRXIT                                                          
         EJECT                                                                  
***********************************************************************         
*  TITLE: CONOUT - AT ENTRY, DUMMYREC CONTAINS VPH'S FROM BACKWARD    *         
*           VPH AVERAGEING.  A USER OPTION SPECIFIES THAT THE         *         
*           UNIVERSES APPLY TO THE UNIT DATE.  FOR THIS OPTION, THE   *         
*           RECORD AT DUMMYREC MUST BE CONVERTED TO THE FORMAT OF THE *         
*           UNIT DATE TO INSURE CORRECT UNIVERSES AND TO DEALWITH     *         
*           POTENTIAL FILE FORMAT CHANGES.                            *         
*                                                                     *         
* CALLED FROM : DOBWBVPH - BACKWARD VPH AVERAGING (RESULTS C,R)       *         
*                                                                     *         
*  INPUTS: NBUSER1+9 - Y IF WE ARE SUPPOSED TO USE BOOK OF UNIT DATE  *         
*                                                                     *         
*  CALLS TO : DEMOMATH - TO EXPLODE DUMMYREC INTO THE UNIT DATE'S     *         
*               FORMAT AND THEN REPLACE THE DEMO ELEMENTS             *         
*             HELLO    - TO DELETE UNNECESSARY UNIVERSE ELEMENT       *         
*                                                                     *         
*  GLOBAL OUTPUTS: DUMMYREC - CONVERTED INTO UNIT DATE'S FORMAT       *         
*                                                                     *         
***********************************************************************         
CONOUT   DS    0H                                                               
         CLI   NBUSER1+9,C'Y'      TEST FOR UNIT DATE FORMAT OPTION             
         BNE   CONOUTX             NO                                           
         CLI   SAVEWEEK,33         TEST WEEKS 33 AND 34                         
         BE    CONOUT2                                                          
         CLI   SAVEWEEK,34                                                      
         BE    CONOUT2                                                          
         CLI   SAVEYEAR,87         AND WEEK 35 IN 1987                          
         BNE   CONOUTX                                                          
         CLI   SAVEWEEK,35                                                      
         BNE   CONOUTX                                                          
*                                                                               
* EXPLODE THE VPHS ON DUMMYREC INTO FORMAT OF THE UNIT DATE(SAVEBOOK)           
*                                                                               
CONOUT2  XC    DBLOCK,DBLOCK       DBLOCK DESCRIBES DUMMYREC                    
         MVC   DBFILE,=C'NTI'                                                   
         MVC   DBCOMFCS,NBACOM                                                  
         LA    RE,DUMMYREC                                                      
         ST    RE,DBAREC                                                        
         LA    RE,23(RE)                                                        
         ST    RE,DBAQUART                                                      
*                                                                               
         XC    OUTFORM,OUTFORM       OUTPUT FORMAT BLOCK =                      
         MVC   OUTFORM(7),NTIVALS    NTI VALUES +                               
         CLI   NBPOSTYP,C'H'                                                    
         BNE   *+10                                                             
         MVC   OUTFORM+4(2),=C'NH'                                              
         MVC   OUTFORM+7(2),SAVEBOOK BOOK OF UNIT DATE                          
*                                                                               
         GOTO1 NBDEMAIN,DMCB,=C'GET',DBLOCK,WORKREC,OUTFORM                     
         CLI   DBERROR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
* NOW REPLACE THE OLD DEMO ELEMENTS WITH THOSE IN UNIT DATE'S FORMAT            
*                                                                               
         GOTO1 NBDEMAIN,DMCB,=C'REP',DBLOCK,WORKREC,OUTFORM                     
         CLI   DBERROR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
* DELETE SPURIOUS UNIVERSE ELEMENT FROM NON-CABLE UNITS                         
*                                                                               
CONOUT4  TM    NBUNST2,X'08'       TEST FOR CABLE UNIT                          
         BO    CONOUTX             YES                                          
         CLI   NBPOSTYP,C'H'                                                    
         BE    CONOUTX                                                          
         GOTO1 NBHELLO,DMCB,(C'D',=C'PAVFIL'),(X'49',DBAREC),0                  
*                                                                               
CONOUTX  B     SUBRXIT                                                          
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.8.D.2                                                  *         
*  TITLE: ESTTOACT - CREATE A DUMMY RECORD WHICH CONTAINS ACTUAL      *         
*           DEMO ELEMENTS. THE VALUES OF THESE ELEMENTS ARE IDENTICAL *         
*           TO THOSE IN THE ESTIMATE ELEMENTS OF THE UNIT RECORD.     *         
*                                                                     *         
*  CALLS TO: DEMAINT - CONVERTS ESTIMATED ELEMENTS TO ACTUAL          *         
*                                                                     *         
*  CALLED FROM: DOPRGREC CASE OF STPUTEM                              *         
*               DOLATEST CASE OF STPUTEM                              *         
*                                                                     *         
*  AT ENTRY     P2=0 CONVERT FROM NBAIO TO DUMMYREC                   *         
*               P2^0 CONVERT DUMMYREC IN PLACE                        *         
*                                                                     *         
*  GLOBAL OUTPUTS: DUMMYREC - CONTAINS THE ACTUAL ELEMENTS            *         
*                                                                     *         
*                                                                     *         
***********************************************************************         
ESTTOACT DS    0H                                                               
         XC    DBLOCK,DBLOCK       INITIALIZE DBLOCK TO DESCRIBE                
         MVC   DBFILE,=C'EVN'      UNIT RECORD (SOURCE OF EST. DEMOS)           
         OC    4(4,R1),4(R1)       TEST FOR CONVERSION TYPE  (P2=0?)            
         BNZ   ESTTOAC1            ITS DUMMYREC IN PLACE                        
         MVC   DBAREC,NBAIO                                                     
         MVC   DBAQUART,ELEM1                                                   
         BAS   RE,INITDUM          SET UP DUMMY RECORD                          
         B     ESTTOAC2                                                         
         SPACE 1                                                                
ESTTOAC1 LA    RE,DUMMYREC                                                      
         ST    RE,DBAREC                                                        
         LA    RE,23(RE)           POINT TO FIRST ELEMENT                       
         ST    RE,DBAQUART                                                      
         SPACE 1                                                                
ESTTOAC2 MVC   DBCOMFCS,NBACOM                                                  
         XC    OUTFORM,OUTFORM     BUILD NTI OUTPUT FORMAT BLOCK                
         MVC   OUTFORM(7),NTIVALS                                               
         GOTO1 =V(NETWEEK),DMCB,EBCDDAT,NBGETDAY,NBADDAY,RR=RELO                
         MVC   OUTFORM+7(1),4(R1)  NTI YEAR                                     
         MVC   OUTFORM+8(1),0(R1)  NTI WEEK IS HUT WEEK                         
*                                  GET ESTIMATED DEMOS IN NTI FORMAT            
         GOTO1 NBDEMAIN,DMCB,=C'GET',DBLOCK,WORKREC,OUTFORM                     
         CLI   DBERROR,0           TEST FOR ERROR                               
         BE    *+6                 NO-VALUES ARE NOW IN WORKREC                 
         DC    H'0'                                                             
*                                                                               
         MVC   DBFILE,=C'NTI'      CREATE ACTUAL ELEMENTS FROM                  
         LA    R1,DUMMYREC         ESTIMATED VALUES AND PUT TO                  
         ST    R1,DBAREC           DUMMYREC                                     
         LA    R1,23(R1)                                                        
         ST    R1,DBAQUART                                                      
         GOTO1 NBDEMAIN,DMCB,=C'REP',DBLOCK,WORKREC,OUTFORM                     
         CLI   DBERROR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         TM    NBUNST2,X'08'       IF NOT CABLE DELETE 49 ELEM                  
         BO    ESTOACTX                                                         
         CLI   NBPOSTYP,C'H'                                                    
         BE    ESTOACTX                                                         
         GOTO1 NBHELLO,DMCB,(C'D',=C'PAVFIL'),(X'49',DUMMYREC),0                
ESTOACTX B     SUBRXIT                                                          
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.8.L.3                                                  *         
*  TITLE: PRGTOEST - CREATE A DUMMY RECORD WHICH CONTAINS ESTIMATE    *         
*           DEMO ELEMENTS. THE VALUES OF THESE ELEMENTS COMBINE THE   *         
*           PROGRAM RECORD VPHS AND RATING WITH THE UNIT RECORD'S     *         
*           HOMES DATA AND UNIVERSE ELEMENTS                          *         
*                                                                     *         
*  CALLS TO: DEMOMATH - PUTS UNIT RECORDS ESTIMATED DEMOS ON DUMMYREC *         
*            HELLO    - REPLACES ESTIMATED VPH ELEMENT AND ADDS       *         
*                     - OVERRIDE ELEMENTS FOR RATING/SHARE            *         
*            DEMOUT   - LOOK UP HUT FOR CALCULATING RATING/SHARE      *         
*            DEMOMATH - RECALCULATE DUMMY RECORD TO MERGE RATING/SHARE*         
*            HELLO    - DELETE OVERRIDE ELEMENTS FROM DUMMYREC        *         
*                                                                     *         
*  CALLED FROM: DOPRGREC CASE OF STPUTEM, DOLATEST CASE OF STPUTEM    *         
*  INPUTS: WORKREC - PROGRAM RECORD                                   *         
*  GLOBAL OUTPUTS: DUMMYREC - CONTAINS THE ESTIMATED ELEMENTS         *         
***********************************************************************         
PRGTOEST DS    0H                                                               
         BAS   RE,INITDUM          RESET THE DUMMY RECORD                       
*                                                                               
         XC    DBLOCK,DBLOCK       SET DBLOCK TO DESCRIBE EVN                   
         MVC   DBFILE,=C'EVN'      FILE ON THE UNIT RECORD                      
         MVC   DBAREC,NBAIO                                                     
         MVC   DBAQUART,ELEM1                                                   
         MVC   DBCOMFCS,NBACOM                                                  
         BAS   RE,INITMATH         INITIALIZE MATHFACS BLOCK                    
         GOTO1 NBDEMMTH,DMCB,=C'CON',NBAIO,DUMMYREC,MATHFACS                    
*                                                                               
         DS    0H                                                               
PRGTOES2 GOTO1 NBHELLO,DMCB,(C'G',=C'SPTFILE'),(X'92',WORKREC),0                
         CLI   DMCB+12,0           TEST FOR PROGRAM ELEMENT FOUND               
         BE    *+6                 YES                                          
         DC    H'0'                                                             
         L     R3,12(R1)                                                        
         USING NPGELEM,R3                                                       
         LA    R2,WORK             POINT TO AREA FOR VPH ELEM                   
         USING NUEVD,R2                                                         
         MVI   NUEVD,X'33'         BUILD ESTIMATED VPH ELEMENT                  
         MVI   NUEVLEN,37          OLD VPH ELEMENT LENGTH                       
         MVI   NUEVELEN,1                                                       
         MVC   NUEVPHS,NPGVPHS     OLD VPH'S                                    
*                                                                               
         ZIC   R1,1(R3)            GET PROG ELEMENT LENGTH (ADDR IN R3)         
         AR    R1,R3                                                            
         CLI   0(R1),X'93'         TEST FOR NEW PROGRAM ELEMENT FOUND           
         BNE   PRGTOES4            NO                                           
         USING NPG2ELEM,R1                                                      
         MVI   NUEVLEN,119         ENLARGED VPH ELEMENT LENGTH                  
         MVI   NUEVELEN,X'42'      ENLARGED VPH CELL LENGTH                     
         MVC   NUEVPHS,NPG2VPHS    ENLARGED NUMBER OF VPH'S                     
         DROP  R1                                                               
         DROP  R2                                                               
*                                                                               
PRGTOES4 GOTO1 NBHELLO,(R1),(C'D',=C'PAVFIL'),(X'33',DUMMYREC),0                
         GOTO1 (RF),(R1),(C'P',=C'PAVFIL'),DUMMYREC,WORK                        
*                                                                               
         MVC   DUB(2),DBACTBK      NOW LOOK UP THE HUT                          
         BAS   RE,SETDMPRE         SET DEMO PRECISSION                          
         GOTO1 NBDEMOUT,DMCB,(C'D',DIAHUT),DBLOCK,FULL                          
         MVC   DBACTBK,DUB                                                      
         TM    NPGSTAT,X'80'       TEST FOR RATING                              
         BZ    PRGTOES7            NO-SHARE HAS BEEN PROVIDED                   
*                                                                               
PRGTOES6 XC    WORK,WORK           YES-RATING IS GIVEN                          
         LA    R2,WORK                                                          
         USING DEOVD,R2                                                         
         MVI   DEOVEL,X'DD'        ESTIMATED OVERRIDE EL                        
         MVI   DEOVLEN,6           FOR                                          
         MVI   DEOVMOD,C'A'        FUDGE RATING HOMES MODIFIER TO               
         MVI   DEOVNUM,1           GET CORRECT RE-CALC OF EVN RECD.             
         SR    R1,R1               SCALE THE OVERRIDE VALUE                     
         ICM   R1,3,NPGSHARE                                                    
         MH    R1,=H'10'                                                        
         STCM  R1,3,DEOVVAL                                                     
         GOTO1 NBHELLO,DMCB,(C'P',=C'PAVFIL'),DUMMYREC,WORK                     
*                                                                               
         MVC   DEOVMOD(2),SHARE    COMPUTE AND ADD SHARE OVERRIDE               
         SR    R1,R1                                                            
         ICM   R1,3,NPGSHARE                                                    
         M     R0,=F'1000'         SCALE DIVIDEND                               
         OC    FULL,FULL           TEST FOR ZERO DIVISOR                        
         BZ    PRGTOES8                                                         
         SLDA  R0,1                                                             
         D     R0,FULL             SHARE=RATING/HUT                             
         AH    R1,=H'1'                                                         
         SRA   R1,1                ROUND TO ONE DECIMAL PLACE                   
         STCM  R1,3,DEOVVAL                                                     
         GOTO1 NBHELLO,DMCB,(C'P',=C'PAVFIL'),DUMMYREC,WORK                     
         B     PRGTOES8                                                         
*                                                                               
PRGTOES7 XC    WORK,WORK                                                        
         LA    R2,WORK                                                          
         USING DEOVD,R2                                                         
         MVI   DEOVEL,X'DD'        OVERRIDE FOR SHARE                           
         MVI   DEOVLEN,6                                                        
         MVC   DEOVMOD(2),SHARE                                                 
         MVC   DEOVVAL,NPGSHARE                                                 
         GOTO1 NBHELLO,DMCB,(C'P',=C'PAVFIL'),DUMMYREC,WORK                     
*                                                                               
         SR    R1,R1                                                            
         ICM   R1,3,NPGSHARE       GET THE SHARE                                
         M     R0,FULL             RATING=SHARE*HUT                             
         AH    R1,=H'500'                                                       
         D     R0,=F'1000'                                                      
         MH    R1,=H'10'           SCALE THE OVERRIDE VALUE                     
         STCM  R1,3,DEOVVAL                                                     
         MVI   DEOVMOD,C'A'        FUDGE RATING HOMES MODIFIER TO               
         MVI   DEOVNUM,1           GET CORRECT RE-CALC OF EVN RECD.             
         GOTO1 NBHELLO,DMCB,(C'P',=C'PAVFIL'),DUMMYREC,WORK                     
*                                                                               
PRGTOES8 LA    R1,DUMMYREC         RESET DBLOCK TO POINT TO DUMMYREC            
         ST    R1,DBAREC                                                        
         LA    R1,23(R1)                                                        
         ST    R1,DBAQUART                                                      
*                                  DO THIS TO SEED ADJUSTED HOMES               
*                                  IN RECORD WITHOUT AN OVERRIDE                
*                                  ELEMENT ON THE OUTPUT                        
         GOTO1 NBDEMMTH,DMCB,=C'REC',DUMMYREC,DUMMYREC,MATHFACS                 
         GOTO1 NBHELLO,(R1),(C'D',=C'PAVFIL'),(X'DD',DUMMYREC),0                
         B     SUBRXIT                                                          
*                                                                               
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.8.7                                                    *         
*  TITLE: OVERHOME - USED WHEN HOME DEMOS ARE FROM A DIFFERENT PLACE  *         
*                     THAN OTHER DEMOS. THE OTHER DEMOS ARE ASSUMED TO*         
*                     BE IN DUMMYREC (AS VPH ELEMENTS),               *         
*                     THE DESIRED HOME VALUES ARE IN THE HOMES AREA OF*         
*                     DOALIST. THIS CREATES OVERRIDE ELEMENTS IN      *         
*                     DUMMYREC, AND MERGES THEM WITH THE HOME ELEMENT.*         
*                                                                     *         
*                    NOW ALSO CREATES OVERRIDE ELEMENTS FOR ANY       *         
*                    CABLE HOMES VALUES                               *         
*                                                                     *         
*  CALLED FROM: STPUTEM - ANYTIME BLACKWEEK VPHS EXIST                *         
*                                                                     *         
*                                                                     *         
*  CALLS TO: HELLO - USED TO ADD OVERRIDE ELEMENTS                    *         
*            DEMOMATH - GETS RID OF OVERRIDE ELEMENTS, BUT MERGES THE *         
*                     THE OVERRIDE VALUES INTO THE DEMO ELEMENTS.     *         
*                                                                     *         
*  INPUTS: DUMMYREC - CONTAINS THE HOME ELEMENT TO BE CHANGED.        *         
*          DOALIST - LIST CONTAINING HOME DEMOS TO OVERRIDE           *         
*                                                                     *         
*                                                                     *         
*  GLOBAL OUTPUTS: DUMMYREC - WILL CONTAIN MODIFIED HOME DEMOS        *         
*                                                                     *         
*  LOCALS: R7 - POINTS TO OVERRIDE ELEMENT TO BE ADDED                *         
*          RF - ADDRESS OF HELLO                                      *         
*          R5 - POINTS TO CURRENT INPUT DEMO SET                      *         
*          R6 - POINTS TO CURRENT OUTPUT DEMO SET                     *         
*                                                                     *         
*                                                                     *         
*                                                                     *         
***********************************************************************         
         USING DEOVD,R7                                                         
OVERHOME DS    0H                                                               
         LA    R7,DEELEM           AREA WHERE ELEMENT IS BUILT                  
*                                                                               
         MVI   DEOVEL,X'DE'        OVERRIDE ELEMENT                             
         MVI   DEOVLEN,DEELLEN     LENGTH OF DE ELEMENT                         
         MVI   AFTERDE,X'00'       MARK END OF ELEMENT FOR HELLO                
*                                                                               
         MVC   DEOVMOD(2),DIAVHOME+1   MODIFIER,DEMO NUMBER                     
         MVC   DEOVVAL(2),DOAVHOME+2    DEMO VALUE                              
*                                                                               
         BAS   R4,OHHELLO                                                       
         CLI   DMCB+12,0                                                        
         BE    OH10                                                             
         DC    H'0'                                                             
*                                                                               
OH10     MVC   DEOVMOD(2),DIAGHOME+1   MODIFIER,DEMO NUMBER                     
         MVC   DEOVVAL(2),DOAGHOME+2   DEMO VALUE                               
         BAS   R4,OHHELLO                                                       
         CLI   DMCB+12,0                                                        
         BE    OH15                                                             
*                                                                               
OH15     MVC   DEOVMOD(2),DIAIHOME+1   MODIFIER,DEMO NUMBER                     
         MVC   DEOVVAL(2),DOAIHOME+2   DEMO VALUE                               
         BAS   R4,OHHELLO                                                       
         CLI   DMCB+12,0                                                        
         BE    OH20                                                             
         DC    H'0'                                                             
*                                                                               
OH20     MVC   DEOVMOD(2),DIASHARE+1   MODIFIER,DEMO NUMBER                     
         MVC   DEOVVAL(2),DOASHARE+2   DEMO VALUE                               
         BAS   R4,OHHELLO                                                       
         CLI   DMCB+12,0                                                        
         BE    OHCBEG                                                           
         DC    H'0'                                                             
*                                                                               
OHCBEG   OC    NBADEM,NBADEM       DOES NET DEMO BLOCK EXIST?                   
         BZ    OHEND                                                            
         L     R2,NBADEM                                                        
         USING NDDEMBLK,R2                                                      
         SR    R3,R3                CHECK FOR CABLE DEMOS                       
         ICM   R3,1,NDNDEMOS                                                    
         BZ    OHEND                                                            
         DROP  R2                                                               
*                                                                               
         LA    R5,DIADMSET                                                      
         LA    R6,DOADMSET           FOR EACH DEMO SET                          
OHCLOP   CLI   2(R5),16               IF ITS A CABLE DEMO                       
         BNE   OHCNEXT                                                          
         MVI   DEOVMOD,C'R'           RTG MODIFIER                              
         MVI   DEOVMOD+1,16           CABLE HOMES                               
         MVC   DEOVVAL(2),2+ALGRPOFF(R6)     DEMO VALUE                         
         BAS   R4,OHHELLO                                                       
         CLI   DMCB+12,0                                                        
         BE    OHC4                                                             
         DC    H'0'                                                             
OHC4     MVI   DEOVMOD,C'T'           RTG MODIFIER                              
         MVI   DEOVMOD+1,16           CABLE HOMES                               
         MVC   DEOVVAL(2),2+ALIMPOFF(R6)     DEMO VALUE                         
         BAS   R4,OHHELLO                                                       
         CLI   DMCB+12,0                                                        
         BE    OHCNEXT                                                          
         DC    H'0'                                                             
OHCNEXT  LA    R5,9(R5)             3 SETS OF 3 BYTE DEMO IDS                   
         LA    R6,ALLENGTH(R6)                                                  
         BCT   R3,OHCLOP                                                        
*                                                                               
** NOW CONDENSE INTO THE DEMO ELEMENTS. GET RID OF OVERRIDE ELEMS.              
OHEND    XC    DBLOCK,DBLOCK                                                    
         MVC   DBFILE,=C'NTI'                                                   
         LA    R7,DUMMYREC                                                      
         ST    R7,DBAREC                                                        
         LA    R1,23(R7)           A(FIRST ELEMENT OF DUMMYREC)                 
         ST    R1,DBAQUART                                                      
         MVC   DBCOMFCS,NBACOM                                                  
         BAS   RE,INITMATH         INITIALIZE MATHFACS BLOCK                    
         GOTO1 NBDEMMTH,DMCB,=C'REC',DUMMYREC,DUMMYREC,MATHFACS                 
*                                  NOW DELETE ALL OVERRIDE ELEMS                
         GOTO1 NBHELLO,DMCB,(C'D',=C'PAVFIL'),(X'DE',DUMMYREC),0                
         B     SUBRXIT                                                          
         SPACE 1                                                                
         PRINT GEN                                                              
OHHELLO  GOTO1 NBHELLO,DMCB,(C'P',=C'PAVFIL'),DUMMYREC,(R7),0                   
         PRINT NOGEN                                                            
         BR    R4                                                               
         DROP  R7                                                               
         EJECT                                                                  
***********************************************************************         
*  TITLE: USEFORM                                                     *         
*                                                                     *         
*  COMMENTS: ADJUST ALL DEMOS IN DUMMYREC BY (ACT HH RTG / EST HH RTG)*         
*                                                                     *         
*  CALLED FROM: F FLAVOR                                              *         
*                                                                     *         
*  CALLS TO: DEMOUT - GET THE ACTUAL HH RATING                        *         
*            DEMOMATH- PERFORM THE ADJUSTMENT                         *         
*                                                                     *         
*  INPUTS: DUMMYREC - CONTAINS ESTIMATED DEMOS                        *         
*          DOALIST - CONTAINS ACTUAL HH RATING                        *         
*                                                                     *         
*  GLOBAL OUTPUTS: DUMMYREC - CHANGED TO REFLECT FORMULA              *         
*                                                                     *         
* LOCAL OUTPUTS:  DBLOCK - FILLS IN VALUES NEEDED BY DEMOUT           *         
*                 MATHBLOK- FILLS IN VALUES NEEDED BY DEMOMATH        *         
*                 ESTHOME - THE ESTIMATED HOMES RATING                *         
***********************************************************************         
USEFORM  DS    0H                                                               
         XC    DBLOCK,DBLOCK       GET GARBAGE OUT OF DBLOCK                    
         MVC   DBFILE,=C'EVN'      TO WANT ESTIMATE DEMOS                       
         MVC   DBCOMFCS,NBACOM     A(COMFACS)                                   
         ST    R7,DBAREC           A(UNIT RECORD)                               
         MVC   DBAQUART,ELEM1      A(1ST ELEMENT)                               
*                                  GET ESTIMATED HOMES RATING                   
         BAS   RE,SETDMPRE         SET DEMO PRECISSION                          
         GOTO1 NBDEMOUT,DMCB,(C'D',=XL3'00D901'),DBLOCK,ESTHOME                 
*                                                                               
         XC    DBLOCK,DBLOCK       SET DBLOCK FOR DEMOMATHCK                    
         MVC   DBFILE,=C'NTI'      DUMMYREC CONTAINS ACTUALS                    
         MVC   DBCOMFCS,NBACOM     A(COMFACS)                                   
         LA    R1,DUMMYREC                                                      
         ST    R1,DBAREC           A(DUMMYREC)                                  
         LA    R1,23(R1)           A(1ST ELEMENT)                               
         ST    R1,DBAQUART                                                      
*                                                                               
         BAS   RE,INITMATH         SET UP MATHBLOCK                             
*                                                                               
         MVC   MATHFCTR,DOAGHOME   MULT BY ACT HOMES RTG                        
         GOTO1 NBDEMMTH,DMCB,=C'MULT',DUMMYREC,DUMMYREC,MATHFACS                
         MVC   MATHFCTR,ESTHOME    DIVIDE BY EST HOMES RTG                      
         GOTO1 NBDEMMTH,DMCB,=C'DIV',DUMMYREC,DUMMYREC,MATHFACS                 
XITUSEF  B     SUBRXIT                                                          
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.5.1                                                    *         
*  TITLE: GETDEMOS - GET DEMOS FROM DEMAND INTO DUMMYREC              *         
*                                                                     *         
*  CALLED FROM: STCKEX - CHECK IF EXIST STATE OF DODEMS               *         
*                                                                     *         
*  CALLS TO:                                                          *         
*   INITMATH - INITIALIZE MATHFAC BLOCK                               *         
*   INITDUM - INITIALIZE A DUMMY RECORD                               *         
*   DEMAND (DEMHOOK) - ADD SELECTED RECORDS IN DUMMY RECORD           *         
*   DEMOMATH - TO AVERAGE VALUES NOW IN DUMMY RECORD                  *         
*   DEMOUT                                                            *         
*                                                                     *         
*  INPUTS : DBLOCK - FILLED BY PREVIOUS ROUTINE TO REQUEST APPROPRIATE*         
*                      DEMOS.                                         *         
*           DIALIST - REQUESTED DEMOS.                                *         
*           DIULIST - REQUESTED UNIVERSES                             *         
*                                                                     *         
*  GLOBAL OUTPUTS: DUMMYREC - PAV RECORD CONTAINING ACTUAL VALUES     *         
*                  DOALIST - THE DEMO VALUES LOOKED UP                *         
*                  DOULIST - THE UNIVERSE VALUES LOOKED UP            *         
*                  STATUS - Y - IF RECORDS FOUND                      *         
*                           N - IF NONE FOUND                         *         
***********************************************************************         
GETDEMOS DS    0H                                                               
         BAS   RE,INITDUM          SET UP DUMMY RECORD                          
         BAS   RE,INITMATH         SET UP MATHFAC BLOCK FOR DEMOMATH            
         GOTO1 NBDEMAND,DMCB,DBLOCK,DEMHOOK                                     
         MVC   DMCB+20(4),DBACTSTA                                              
         MVI   STATUS,C'N'         INITIALIZE TO NONE FOUND                     
         OC    DBDIVSOR,DBDIVSOR   CK IF ANY FOUND                              
         BZ    XITGTDM                                                          
*                                                                               
         CLI   MIRDIV,0                                                         
         BE    GETD3               DON'T DIV BY 0                               
         SR    R0,R0                                                            
         ZICM  R1,DBDIVSOR,2                                                    
         ZIC   RF,MIRDIV                                                        
         DR    R0,RF                                                            
         STCM  R1,3,DBDIVSOR                                                    
*                                                                               
GETD3    MVI   STATUS,C'Y'         RECORDS FOUND                                
         MVC   MATHFCTR+2(2),DBDIVSOR                                           
*                                                                               
         GOTO1 NBDEMMTH,DMCB,=C'DIV',DUMMYREC,DUMMYREC,MATHFACS                 
*                                                                               
         TM    NBUNST2,X'08'       IF NOT CABLE DELETE 49 ELEM                  
         BO    GETD5                                                            
         CLI   DBSELSTA+4,C'Q'     UNLESS DEMO FILE CABLE QH                    
         BE    GETD5                                                            
         CLI   DBSELSTA+4,C'C'     UNLESS DEMO FILE CABLE HH                    
         BE    GETD5                                                            
         CLI   DBSELSTA+4,C'H'     ALSO FOR HISPANIC                            
         BE    GETD5                                                            
         GOTO1 NBHELLO,DMCB,(C'D',=C'PAVFIL'),(X'49',DUMMYREC),0                
*                                                                               
GETD5    CLI   HAVBTU,C'Y'         CHECK FOR USER FILE NEEDED                   
         BNE   GETD51                                                           
*                                                                               
         XC    DBEXTEND,DBEXTEND                                                
         XC    BTUEXT(12),BTUEXT                                                
*        GOTO1 ASUBR,FULL,('BTUREADE',(RC))                                     
         BAS   RE,BTUREAD                                                       
         CLI   GOTUTYPE,1                                                       
         BNE   GETD51                                                           
         MVC   BTUEXT,=C'UFIL'                                                  
         MVC   BTUEX2,BTUDB+4      SET DBAREC                                   
         XC    DBLOCK,DBLOCK                                                    
         LA    RE,BTUEXT                                                        
         ST    RE,DBEXTEND                                                      
         B     GETD52                                                           
*                                                                               
GETD51   XC    DBLOCK,DBLOCK       SET DBLOCK FOR DEMOUT FROM DUMMYREC          
GETD52   MVC   DBFILE,=C'NTI'      GET ACTUAL DEMOS                             
         MVC   DBCOMFCS,NBACOM     A(COMFACS)                                   
         LA    R1,DUMMYREC                                                      
         ST    R1,DBAREC           A(DUMMYREC)                                  
         LA    R1,23(R1)           FIRST ELEMENT OF DUMMYREC                    
         ST    R1,DBAQUART         A(1ST ELEM ON UNIT REC)                      
         BAS   RE,SETDMPRE         SET DEMO PRECISSION                          
*                                                                               
         GOTO1 NBDEMOUT,DMCB,(C'L',DIALIST),DBLOCK,DOALIST                      
         GOTO1 NBDEMOUT,DMCB,(C'L',DIULIST),DBLOCK,DOULIST                      
*                                                                               
         BC    0,XITGTDM           <-----REMOVE FOR NAD LOOKUPS                 
*                                                                               
         CLI   HAVNAD,C'Y'         NAD DEMOS REQUESTED                          
         BNE   XITGTDM                                                          
         OC    DINDMSET(3),DINDMSET ANY NAD DEMS FROM DEF RECORD                
         BZ    XITGTDM              NO - JUST EXIT                              
*                                                                               
         MVC   SAVBTYPE,DBBTYPE    SAVE NTI BOOK TYPE                           
         MVC   DMCB+12(5),DBACTSTA                                              
         MVI   DBBTYPE,0                                                        
         GOTO1 ASUBR,DMCB,('STPRDEME',(RC))   ASK FOR PROGRAM DEMOS             
         MVC   DBFILE,=C'NAD'                                                   
         MVC   DBSELAGY,NBSELAGY                                                
         MVI   DBSELSTA+3,C'P'     PROGRAM AVE                                  
         MVI   DBSELSTA+4,C'N'     NAD                                          
*                                                                               
         CLI   NBPOSTYP,C'S'       SET FOR SYNDICATION IF REQ.                  
         BNE   *+16                                                             
         MVC   DBSELSTA(4),DMCB+20                                              
         MVC   DBSELSTA+3(2),=C' M'                                             
*                                                                               
         CLI   NBPOSTYP,C'H'       SET FOR HISPANIC IF REQ.                     
         BNE   *+10                                                             
         MVC   DBSELSTA+3(2),=C' H'                                             
* SET DEFAULT NAD/NHT BOOK                                                      
         GOTO1 NBDATCON,DMCB,(0,EBCDDAT),(3,DUB)                                
         MVC   DBSELBK+1(1),DUB+1  BOOK IS MONTH                                
         SPACE 1                                                                
* SCAN TABLE FOR ACTUAL NAD/NHT DATES                                           
         LA    RE,NADMON                                                        
         CLC   DUB(3),3(RE)        LE END DATE                                  
         BNH   *+12                                                             
         LA    RE,8(RE)            NO TRY NEXT                                  
         B     *-14                                                             
         CLC   DUB(3),0(RE)        LE START DATE                                
         BL    *+10                YES USE DEFAULT BOOK                         
         MVC   DBSELBK(2),6(RE)    EOT WILL SET BK=9913                         
*                                                                               
         MVI   BYTE,0              SET FOR UNIVERSE CONTROL                     
NTRNAD   DS    0C                                                               
         XC    NHTUNIV(20),NHTUNIV                                              
         GOTO1 NBDEMAND,DMCB,DBLOCK,NADHOOK                                     
         OC    DBDIVSOR,DBDIVSOR                                                
         BZ    NADFINI                                                          
*                                                                               
         CLC   NHTUNIV(4),=C'NTHU' POINT TO HISPANIC UNIV REC                   
         BNE   *+12                                                             
         LA    RE,NHTUNIV                                                       
         STCM  RE,15,DBSPANAD                                                   
*                                                                               
         MVI   NBRESULN,C'D'       SET FROM DEMO FILE                           
         L     RE,DBAREC                                                        
         LA    RE,23(RE)                                                        
         ST    RE,DBAQUART                                                      
NADWORK  EQU   DUMMYREC+1600                                                    
         STM   R4,R5,DUB                                                        
GETNAD   LA    R4,NADWORK                                                       
         LA    R5,DINDMSET         CONVERT INPUT TO VPH OR UNIV                 
         MVI   60(R5),X'FF'        PROTECT MYSELF - SET EOL                     
*                                                                               
GETNAD2  CLI   0(R5),X'FF'         EOL                                          
         BE    GETNAD8                                                          
         MVI   1(R5),C'V'          VPH AS DEFAULT                               
*                                                                               
         CLI   BYTE,1                                                           
         BNE   *+8                                                              
         MVI   1(R5),C'U'          UNIVERSE FOR SECOND PASS                     
         LA    R5,3(R5)                                                         
         B     GETNAD2                                                          
*                                                                               
GETNAD8  LA    R5,DINDMSET                                                      
         GOTO1 NBDEMOUT,DMCB,(C'L',(R5)),DBLOCK,(R4)                            
GETD6    CLI   0(R5),X'FF'         EOL                                          
         BE    GETDH8                                                           
         CLI   0(R5),X'00'         REGULAR NTI DEMO                             
         BNE   GETD7                                                            
GETD6A   LA    R4,4(R4)            BUMP THE VALUE POINTER                       
         LA    R5,3(R5)            BUMP THE DEMO CODE POINTER                   
         B     GETD6                                                            
*                                                                               
GETD7    LA    RE,WORK             PUT NAD DEMOS IN DUMMY RECORD                
         OC    0(4,R4),0(R4)       BYPASS NULL VALUES                           
         BZ    GETD6A                                                           
*                                                                               
         CLI   NBPOSTYP,C'H'       HISPANIC NEEDS R/T/U/V                       
         BE    *+8                                                              
*                                                                               
         CLI   1(R5),C'V'          ONLY VPH                                     
         BE    *+12                                                             
         CLI   1(R5),C'U'          AND UNIVERSE                                 
         BNE   GETD6A                                                           
         XC    0(30,RE),0(RE)                                                   
         USING NUOVEL,RE                                                        
         MVC   NUOVEL(3),=X'DE0C00'                                             
         MVC   NUOVCAT(3),0(R5)                                                 
         MVI   NUOVFLG,X'80'       ACTUAL DEMO                                  
         MVI   NUOVPRE,X'81'       PRECISION OF FIELD                           
         MVC   NUOVVAL,0(R4)                                                    
*                                                                               
         CLI   NBPOSTYP,C'H'       UNWEIGHT HISPANIC VALUES                     
         BNE   NHTPSTX                                                          
         CLI   1(R5),C'V'          UNWEIGHT VPH ONLY                            
         BNE   NHTPSTX                                                          
         ICM   R3,15,NUOVVAL       VALUE                                        
         ICM   R1,2,DBDIVSOR       WEIGHT                                       
         SR    R2,R2                                                            
         SLDA  R2,1                                                             
         DR    R2,R1               UNWIEGHT                                     
         AHI   R3,1                AND ROUND                                    
         SRL   R3,1                                                             
         STCM  R3,15,NUOVVAL       REPLACE VALUE                                
*                                                                               
NHTPSTX  LA    RF,NSPREC1          SET FIELD PRECISION                          
         CLC   NUOVMOD,0(RF)                                                    
         BE    *+12                                                             
         LA    RF,2(RF)                                                         
         B     *-14                                                             
         MVC   NUOVPRE,1(RF)                                                    
         DROP  RE                                                               
         GOTO1 NBHELLO,DMCB,(C'P',=C'PAVFIL'),DUMMYREC,WORK                     
         B     GETD6A                                                           
*                                                                               
GETDH8   CLI   NBPOSTYP,C'H'       HISPANIC NEEDS R/T/U/V                       
         BNE   GETD8                                                            
         LA    R5,DINDMSET                                                      
         CLI   1(R5),C'T'          DONE                                         
         BE    GETD8                                                            
         CLI   1(R5),C'U'          UNIVERSE PASS                                
         BE    GETD8                                                            
         CLI   1(R5),C'V'          RATINGS ARE NEXT                             
         BNE   *+8                                                              
         MVI   FULL,C'R'                                                        
         CLI   1(R5),C'R'          AND THEN IMPS                                
         BNE   *+8                                                              
         MVI   FULL,C'T'                                                        
GETDH82  CLI   0(R5),X'FF'         SEED THE MODIFIER                            
         BE    GETNAD8                                                          
         MVC   1(1,R5),FULL                                                     
         LA    R5,3(R5)                                                         
         B     GETDH82                                                          
         SPACE 2                                                                
*        RESTORE NTI VALUES                                                     
GETD8    CLI   BYTE,0              CHECK FOR UNIVERSE DONE                      
         BH    *+12                 YES - I'M ALMOST OUTA HERE                  
         MVI   BYTE,1               NO  - DO THE UNIVERSES                      
         B     GETNAD                                                           
         MVI   BYTE,2              UNIVERSES FIRST PASS ONLY                    
         CLI   DBBTYPE,C'O'        DONE IF OPTIONAL                             
         BE    NADOPTP                                                          
         CLI   DBBTYPE,C'P'        DONE IF OPTIONAL                             
         BE    XITNAD                                                           
         CLI   DBBTYPE,15          NEED TO SCAN ALL RECORDS                     
         BE    NADOPT                                                           
         ZIC   RE,DBBTYPE                                                       
         LA    RE,1(RE)                                                         
         STC   RE,DBBTYPE                                                       
         B     NTRNAD                                                           
NADFINI  DS    0C                                                               
         CLI   DBBTYPE,C'O'        DONE IF OPTIONAL                             
         BE    NADOPTP                                                          
         CLI   DBBTYPE,C'P'        DONE IF OPTIONAL                             
         BE    XITNAD                                                           
*                                                                               
NADOPT   MVI   DBBTYPE,C'O'        REG DONE - SET FOR OPTIONAL                  
         B     NTRNAD                                                           
*                                                                               
NADOPTP  MVI   DBBTYPE,C'P'        REG DONE - SET FOR OPTIONAL R2               
         B     NTRNAD                                                           
*                                                                               
XITNAD   MVC   DBFILE,=C'NTI'      GET ACTUAL DEMOS                             
         MVC   DBBTYPE,SAVBTYPE                                                 
         MVC   DBCOMFCS,NBACOM     A(COMFACS)                                   
         LA    R1,DUMMYREC                                                      
         ST    R1,DBAREC           A(DUMMYREC)                                  
         LA    R1,23(R1)           FIRST ELEMENT OF DUMMYREC                    
         ST    R1,DBAQUART         A(1ST ELEM ON UNIT REC)                      
         LM    R4,R5,DUB                                                        
         PRINT NOGEN                                                            
*                                                                               
XITGTDM  B     SUBRXIT                                                          
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
*  THE ROUTINES THAT FOLLOW ARE ONLY CALLED FROM ROUTINES WITHIN SUBR *         
*                                                                     *         
***********************************************************************         
***********************************************************************         
*  TITLE: DEMHOOK  - HOOK ROUTINE FOR ADDING RECORDS GIVEN BY DEMAND. *         
*                                                                     *         
*  CALLED FROM: DEMAND                                                *         
*                                                                     *         
*  GLOBAL OUTPUTS: DUMMYREC                                           *         
***********************************************************************         
DEMHOOK  EQU   *                                                                
         ST    RE,SAVERE           SAVE RETURN ADDRESS                          
*                                                                               
         BC    0,DEMHK30           JUST SO I CAN TEST                           
*                                                                               
         MVI   HAVDAY,C'N'                                                      
         CLI   DBSELSTA+4,C'D'     IF DAYLY KEEP TRACK OF IT                    
         BNE   *+8                                                              
         MVI   HAVDAY,C'Y'                                                      
*                                                                               
         CLI   DBSELSTA+4,C'C'     IF CABLE                                     
         BE    DEMHK30             KEEP ALL ELEMENTS                            
*                                                                               
         CLI   DBSELSTA+4,C'Q'     IF CABLE QUARTER HOUR                        
         BE    DEMHK30             KEEP ALL ELEMENTS                            
*                                                                               
         CLI   DBSELSTA+4,C'H'     IF HISPANIC                                  
         BE    DEMHK30             KEEP ALL ELEMENTS                            
*                                                                               
         CLI   NBUSER2+4,C'Y'                                                   
         BE    DEMHK5              GET GROSS PROG AVERAGES                      
*                                                                               
DEMHK0   L     R6,DBAQUART         START AT FIRST DEMO ELEMENT                  
DEMHK1   CLI   0(R6),0                                                          
         BE    DEMHK20                                                          
*                                                                               
         CLI   0(R6),X'4A'         TAKE OUT CALC. TPPUT ELEMENTS                
         BL    DEMHK3                                                           
         CLI   0(R6),X'4E'                                                      
         BL    DEMHK2                                                           
*                                                                               
         CLI   0(R6),X'53'        TAKE OUT TP HUT/PUT AND GAA ELEMENTS          
         BL    DEMHK3              AND LEAVE ALL OTHERS                         
         CLI   0(R6),X'5C'                                                      
         BH    DEMHK20                                                          
DEMHK2   MVI   0(R6),X'FF'                                                      
DEMHK3   BAS   RE,DEMHKBMP         BUMP THROUGH THE ELEMENTS                    
         B     DEMHK1                                                           
*                                                                               
*--CHECK FOR D LINE ELEMENTS                                                    
*                                                                               
DEMHK5   L     R6,DBAQUART         START AT FIRST DEMO ELEMENT                  
DEMHK5A  CLI   0(R6),0                                                          
         BE    DEMHK0                                                           
         CLI   0(R6),X'55'         ENSURE GAA EXITS                             
         BE    DEMHK5B             IN CASE MULTIPASS                            
         ZIC   RF,1(R6)                                                         
         AR    R6,RF                                                            
         B     DEMHK5A                                                          
*                                                                               
DEMHK5B  L     R6,DBAQUART         RESTART AT FIRST ELEMENT                     
*                                                                               
DEMHK6   CLI   0(R6),0                                                          
         BE    DEMHK0                                                           
         CLI   0(R6),X'54'                                                      
         BL    DEMHK12                                                          
         CLI   0(R6),X'5B'                                                      
         BH    DEMHK12                                                          
*                                                                               
         L     R6,DBAQUART         START AT FIRST DEMO ELEMENT                  
         OC    DBSELPRG,DBSELPRG   IF PROGRAM CODE NOT THERE                    
         BZ    *+8                 LEAVE RESULT ALONE                           
         MVI   RESULT,C'J'         SET D LINE RESULT                            
DEMHK9   CLI   0(R6),0                                                          
         BE    DEMHK20                                                          
*                                                                               
         CLI   0(R6),X'40'         TAKE OUT CALC. TPPUT ELEMENTS                
         BL    DEMHK11             AND PROGRAM PUTS                             
         CLI   0(R6),X'54'                                                      
         BL    DEMHK10                                                          
*                                                                               
         CLI   0(R6),X'54'        TAKE OUT TP HUT/PUT AND GAA ELEMENTS          
         BL    DEMHK11             AND LEAVE ALL OTHERS                         
         CLI   0(R6),X'5B'                                                      
         BH    DEMHK11                                                          
         ZIC   RE,0(R6)                                                         
         SH    RE,=H'20'                                                        
         STC   RE,0(R6)                                                         
         B     DEMHK11                                                          
DEMHK10  MVI   0(R6),X'FF'                                                      
DEMHK11  BAS   RE,DEMHKBMP         BUMP THROUGH THE ELEMENTS                    
         B     DEMHK9                                                           
DEMHK12  BAS   RE,DEMHKBMP                                                      
         B     DEMHK6                                                           
*                                                                               
DEMHK20  GOTO1 NBHELLO,DMCB,(C'D',=C'PAVFIL'),(X'FF',DBAREC),0                  
*                                                                               
DEMHK30  MVC   MATHFCTR+2(2),DBFACTOR    NUMBER OF TIME PERIODS COVERED         
         CLI   DBSELSTA+4,C'H'                                                  
         BNE   DEMHSPX                                                          
         ICM   RE,15,DBSPANAD                                                   
         BZ    DEMHSPX                                                          
*                                                                               
         CLC   0(4,RE),=C'NTHU'    SAVE THE UNIVERSE RECORD                     
         BNE   DEMHSPX                                                          
         LA    RE,4(RE)                                                         
         USING PRKEY,RE                                                         
         LA    RE,PRFRSTEL                                                      
         SR    RF,RF                                                            
DEMHSP1  CLI   0(RE),0                                                          
         BE    DEMHSPX                                                          
         CLI   0(RE),X'23'                                                      
         BE    DEMHSP2                                                          
         IC    RF,1(RE)                                                         
         AR    RE,RF                                                            
         B     DEMHSP1                                                          
DEMHSP2  IC    RF,1(RE)                                                         
         AR    RE,RF                                                            
         CLI   0(RE),X'5E'                                                      
         BE    DEMHSPX                                                          
         CLI   0(RE),0                                                          
         BE    DEMHSPX                                                          
         CLI   0(RE),X'4B'                                                      
         BE    *+12                                                             
         CLI   0(RE),X'4C'                                                      
         BNE   DEMHSP2                                                          
         LR    R6,RE                                                            
         GOTO1 NBHELLO,DMCB,(C'P',=C'PAVFIL'),DBAREC,(R6)                       
         SR    RF,RF                                                            
         LR    RE,R6                                                            
         CLI   0(RE),X'4C'                                                      
         BNE   DEMHSP2                                                          
DEMHSPX  DS    0C                                                               
*                                                                               
         CLI   DBSELSTA+4,C'C'                                                  
         BNE   DEMCABX                                                          
         ICM   RE,15,DBSPANAD                                                   
         BZ    DEMCABX                                                          
*                                                                               
         CLC   0(3,RE),=C'CAB'     DIG OUT THE LEVEL/CONVERT TO ALPHA           
         BNE   DEMCABX                                                          
         MVI   NBDEMLEV,C'P'       DEFAULT TO PROGRAM                           
         CLI   3(RE),1                                                          
         BNE   *+8                                                              
         MVI   NBDEMLEV,C'T'       TRACK                                        
         CLI   3(RE),2                                                          
         BNE   *+8                                                              
         MVI   NBDEMLEV,C'S'       TELECAST(SOLO)                               
DEMCABX  DS    0C                                                               
*                                                                               
         GOTO1 NBDEMMTH,DMCB,=C'MAD',DBAREC,DUMMYREC,MATHFACS                   
*                                                                               
         L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
DEMHKBMP ZIC   RF,1(R6)            BUMP THROUGH THE ELEMENTS                    
         AR    R6,RF                                                            
         BR    RE                                                               
         EJECT                                                                  
NADHOOK  EQU   *                                                                
         ST    RE,SAVERE                                                        
*   1. SAVE THE NAD UNIVERSE RECORD FOR DEMOUT                                  
         CLI   DBSELSTA+4,C'H'                                                  
         BNE   NADHKX                                                           
         ICM   RE,15,DBSPANAD                                                   
         BZ    NADHKX                                                           
*                                                                               
         CLC   0(4,RE),=C'NTHU'    SAVE THE UNIVERSE RECORD                     
         BNE   NADHKX                                                           
         CLC   NHTUNIV(4),0(RE)                                                 
         BE    NADHK2                                                           
         LA    R1,NHTUNIV                                                       
         LA    RF,9                                                             
         MVC   0(200,R1),0(RE)                                                  
         LA    R1,200(R1)                                                       
         LA    RE,200(RE)                                                       
         BCT   RF,*-14                                                          
*                                                                               
NADHK2   CLC   NHTUNIV(4),=C'NTHU' POINT TO HISPANIC UNIV REC                   
         BNE   *+12                                                             
         LA    RE,NHTUNIV                                                       
         STCM  RE,15,DBSPANAD                                                   
         CLI   BYTE,1                                                           
         BE    NADHKX                                                           
*                                                                               
         MVI   NBRESULN,C'D'       SET FROM DEMO FILE                           
         L     RE,DBAREC                                                        
         LA    RE,23(RE)                                                        
         ST    RE,DBAQUART                                                      
         STM   R4,R5,DUB                                                        
GETNHT   LA    R4,NADWORK                                                       
         LA    R5,DINDMSET         CONVERT INPUT TO VPH OR UNIV                 
         MVI   60(R5),X'FF'        PROTECT MYSELF - SET EOL                     
*                                                                               
GETNHT2  CLI   0(R5),X'FF'         EOL                                          
         BE    GETNHT8                                                          
         MVI   1(R5),C'V'          VPH AS DEFAULT                               
*                                                                               
         CLI   BYTE,1                                                           
         BNE   *+8                                                              
         MVI   1(R5),C'U'          UNIVERSE FOR SECOND PASS                     
         LA    R5,3(R5)                                                         
         B     GETNHT2                                                          
*                                                                               
GETNHT8  LA    R5,DINDMSET                                                      
         GOTO1 NBDEMOUT,DMCB,(C'L',(R5)),DBLOCK,(R4)                            
GETNHT6  CLI   0(R5),X'FF'         EOL                                          
         BE    NADHKX                                                           
*                                                                               
         L     RF,0(R4)            WEIGHT THE VALUES                            
         ICM   RE,3,DBFACTOR                                                    
         MR    RE,RE                                                            
         ST    RF,0(R4)                                                         
*                                                                               
         LA    R4,4(R4)            BUMP THE VALUE POINTER                       
         LA    R5,3(R5)            BUMP THE DEMO CODE POINTER                   
         B     GETNHT6                                                          
NADHKX   L     RE,SAVERE                                                        
         BR    RE                                                               
***********************************************************************         
*  NUMBER: NV1.4.4                                                    *         
*  TITLE: SETDMPRE - SET DBLOCK SO DEMAND WILL RETURN RATINGS         *         
*                    IN 2 DECIMAL PLACE PRECISION                     *         
*                                                                     *         
*  CALLED FROM: INITDB                                                *         
*               MANOUTS                                               *         
*               ACTOUTS                                               *         
*               ESTOUTS                                               *         
*               PRGTOEST                                              *         
*               USEFORM                                               *         
*               GETDEMOS                                              *         
*                                                                     *         
*  GLOBAL OUTPUTS: DBLOCK                                             *         
***********************************************************************         
SETDMPRE NTR1                                                                   
*                                                                               
         LA    R5,DBNETWXT         SET UP EXTENSION                             
         USING DBXNTID,R5                                                       
         XC    DBXNID(L'DBNETWXT),DBXNID                                        
         MVC   DBXNID(4),=CL4'NETW'                                             
         MVC   DBX0EVPH,NBUSER2+11 OPTION TO SUPPRESS CALCS                     
* CABLE PRECISION ONLY FOR CABLE                                                
         CLI   NBPOSTYP,C'N'                                                    
         BE    SETD20                                                           
         CLI   NBPOSTYP,C'S'                                                    
         BE    SETD20                                                           
         DROP  R1                                                               
SETD10   CLI   NBPREOPT,C'Y'       ARE WE USING CABLE PRECISION                 
         BNE   *+12                                                             
         MVI   DBXNCR2,C'Y'                                                     
         MVI   DBXNNR2,C'Y'                                                     
*                                                                               
SETD20   OC    DBEXTEND,DBEXTEND                                                
         BNZ   *+12                                                             
         ST    R5,DBEXTEND                                                      
         B     XITDMPRE                                                         
*                                                                               
         L     RE,DBEXTEND                                                      
*                                                                               
         OC    4(4,RE),4(RE)       NEXT EXTENSION?                              
         BZ    *+12                NO MORE - LAST                               
         L     RE,4(RE)                                                         
         B     *-14                                                             
*                                                                               
         ST    R5,4(RE)                                                         
*                                                                               
XITDMPRE B     SUBRXIT                                                          
         DROP  R5                                                               
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.7.3                                                    *         
*  TITLE: INITMATH - INITIALIZE MATHFAC BLOCK FOR USE BY DEMOMATH.    *         
*                                                                     *         
*  CALLED FROM: ALL ROUTINES PERFORMING DEMOMATH                      *         
*                                                                     *         
*  GLOBAL OUTPUTS: MATHFACS                                           *         
***********************************************************************         
INITMATH EQU   *                                                                
         XC    MATHFACS(MATHFACL),MATHFACS                                      
         LA    R1,DBLOCK                                                        
         ST    R1,MATHABLK                                                      
         MVC   MATHIFIL,DBFILE                                                  
         MVC   MATHOFIL,DBFILE                                                  
         MVC   MATHOSRC(1),DBSELSRC                                             
         CLI   DBSELSTA+4,C'Q'                                                  
         BE    *+8                                                              
         CLI   DBSELSTA+4,C'C'                                                  
         BNER  RE                                                               
         MVC   MATHIFIL,=C'CAB'    CABLE HAS DIFFERENT FORMULAS                 
         MVC   MATHOFIL,=C'CAB'    AND RECORD FORMAT                            
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.7.4                                                    *         
*  TITLE: INITDUM - INITIALIZE DUMMY RECORD FOR DEMOMATH. IN SOME     *         
*           SITUATIONS THIS DUMMY RECORD WILL LATER BE MERGED INTO THE*         
*           UNIT RECORD.                                              *         
*                                                                     *         
*  COMMENTS: CREATE A SKELETON PAV RECORD IN DUMMYREC                 *         
*             MAKE SURE X'00' AT END FOR HELLO                        *         
*                                                                     *         
*  CALLED FROM: ALL ROUTINES WHICH WILL CALL DEMOMTH                  *         
*                                                                     *         
*  GLOBAL OUTPUTS: DUMMY RECORD                                       *         
*                                                                     *         
***********************************************************************         
INITDUM  DS    0H                                                               
         XC    DUMMYREC(25),DUMMYREC   CLEAR SKELETON PAV                       
         MVI   DUMMYREC,C'P'           ID IT AS A PAV                           
         MVC   DUMMYREC+20(2),=H'24'   LENGTH OF RECORD                         
XITDUM   BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.4.6                                                    *         
*  TITLE::SETDMPST - RESET THE X'33' ELEMENT TO THE WAY IT WAS        *         
*                    BEFOR THE SETDMPRE CALL                          *         
*                                                                     *         
*  CALLED FROM: MANOUTS                                               *         
*                                                                     *         
***********************************************************************         
SETDMPST NTR1                                                                   
         L     R5,DBAREC                                                        
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNITFIL  '),(X'39',(R5)),0                 
         CLI   DMCB+12,0           IF FOUND EXIT                                
         BNE   XITDMPST                                                         
         L     R5,12(R1)                                                        
         MVI   0(R5),X'33'                                                      
XITDMPST B     SUBRXIT                                                          
         EJECT                                                                  
***********************************************************************         
*  NUMBER: NV1.4.5                                                    *         
*  TITLE: SETDMSET - WIPE OUT VPH ELEMENT FROM DBAREC FOR RESULT=M    *         
*                                                                     *         
*  CALLED FROM: MANOUTS                                               *         
*                                                                     *         
*  GLOBAL OUTPUTS: DBLOCK                                             *         
*  IF NTI CALL WITH ACTUAL OVERRIDES, AND NO POSTING ELEMENT          *         
*  ALTER VPH ELEMENT X'33' TO VPH ELEMENT X'35'                       *         
***********************************************************************         
SETDMSET NTR1                                                                   
         CLC   DBFILE,=CL3'NTI'                                                 
         BNE   XITDMSET                                                         
*                                                                               
         L     R5,DBAREC                                                        
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNITFIL  '),(X'5E',(R5)),0                 
         CLI   DMCB+12,0           IF FOUND EXIT                                
         BE    XITDMSET                                                         
*                                                                               
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNITFIL  '),(X'DE',(R5)),0                 
         CLI   DMCB+12,0           IF NOT FOUND                                 
         BNE   XITDMSET                                                         
*                                                                               
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNITFIL  '),(X'33',(R5)),0                 
         CLI   DMCB+12,0           IF NOT FOUND                                 
         BNE   XITDMSET                                                         
         L     R5,12(R1)                                                        
         MVI   0(R5),X'39'                                                      
XITDMSET B     SUBRXIT                                                          
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
*  TITLE: SETCABLE - ADD X'49' AND X'5E' ELEMS TO UNIT REC WHEN       *         
*                    NECESSARY                                        *         
*                                                                     *         
*  CALLED FROM: SETDMPRE                                              *         
*                                                                     *         
*  INPUTS: R7 SHOULD POINT TO UNIT RECORD                             *         
*                                                                     *         
*  CABLE STATIONS WITH X'DE' (ACTUAL OVERRIDES) BUT NO X'5E' (ACT     *         
*  FORMAT) ELEMENT WILL HAVE X'49' ELEMENT AND VALUES AND X'5E'       *         
*  ELEMENT ADDED TO UNIT REC                                          *         
*                                                                     *         
***********************************************************************         
SETCABLE NTR1                                                                   
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNTFIL'),(X'DE',(R7)),0                    
         CLI   DMCB+12,0           IF NOT FOUND                                 
         BNE   XITCABLE                                                         
*                                                                               
         GOTO1 NBHELLO,DMCB,(C'G',=C'UNTFIL'),(X'5E',(R7)),0                    
         TM    DMCB+12,X'06'       IF NOT FOUND                                 
         BZ    XITCABLE                                                         
*                                                                               
* GET ADDR OF DEMDISP (CORRES)                                                  
         GOTO1 NBDEMADR,DMCB,(X'FF',ATABLES),NBACOM                             
*                                                                               
         ZICM  R1,ATABLES+1,3      R1 TO TABLE HEADER                           
         ICM   R3,15,8(R1)         R3 = TABLE LENGTH                            
         LA    R3,0(R1,R3)         R3 TO END OF TABLE                           
         SR    R2,R2               DISP TO NXT ENTRY                            
         LA    R1,16(R1)           R1 TO 1ST DEMDISP ENTRY                      
         USING DSPHDRD,R1                                                       
*                                                                               
         CLC   =C'CNN',DSPFILE                                                  
         BE    *+18                                                             
         ICM   R2,7,DSPAET                                                      
         LA    R2,1(R2)                                                         
         BXLE  R1,R2,*-18                                                       
         DC    H'0'                NO CNN BOOK IN TABLE                         
*                                                                               
* BUILD X'5E' ELEM                                                              
         MVC   ELEM5E(2),=X'5E07'                                               
         MVC   ELEM5E+2(3),=C'CNN'                                              
         MVC   ELEM5E+5(2),DSPSBOOK                                             
         XC    ELEM5E+5(2),=X'FFFF'                                             
*                                                                               
         LR    R5,R1               SAVE R1                                      
         GOTO1 NBHELLO,DMCB,(C'P',=C'UNTFIL'),(R7),ELEM5E                       
         LR    R1,R5               RESTORE R1                                   
*                                                                               
         ZICM  R3,DSPAET,3         TABLE LENGTH                                 
         LA    R3,1(R1,R3)         R3 TO NEXT TABLE HEADER                      
         ZICM  R2,DSPLDE,2         DATA ELEM LENGTH                             
         LA    R1,DSPHDRLN(R1)     R1 TO DATA ELEMENT                           
         USING DSPDTAD,R1                                                       
*                                                                               
         CLI   DSPELCD,X'49'                                                    
         BE    *+10                                                             
         BXLE  R1,R2,*-8                                                        
         DC    H'0'                NO X'49' ELEM                                
*                                                                               
* HAVE X'49' ELEMS, BUILD DEMOUT INPUT LIST IN DIASC                            
         LA    R4,DIASC            INPUT TABLE                                  
         LA    R3,L'DIASC(R4)                                                   
         BCTR  R3,0                R3 TO END OF TABLE                           
         LA    R2,3                ENTRY LENGTH                                 
         SR    R5,R5               COUNT HOW MANY DEMOS IN LIST                 
*                                                                               
SC10     CLI   DSPELCD,X'49'       STILL ON X'49' ELS?                          
         BNE   SC20                                                             
         LA    R5,1(R5)            INC COUNTER                                  
         MVC   0(1,R4),DSPPREC     SET PRECISION                                
         MVI   1(R4),C'U'          CHANGE 'L' TO 'U' (MODIFIER CD)              
         MVC   2(1,R4),DSPDEMO     SET DEMO                                     
         LA    R1,5(R1)            ENTRIES ARE 5 BYTES LONG                     
         BXLE  R4,R2,SC10                                                       
         DC    H'0'                TABLE FULL                                   
         DROP  R1                                                               
*                                                                               
SC20     DS    0H                                                               
         MVI   0(R4),X'FF'         END OF DEMO LIST                             
         GOTO1 NBDEMOUT,DMCB,(C'P',DIASC),DBLOCK,DOASC                          
*                                                                               
* SET UP X'49' ELEM FOR UNIT REC                                                
         SLA   R5,2                (NUM DEMOS)*4                                
         LA    R5,3(R5)            EL CD, LEN, FLD LEN                          
         MVI   DREINFO,X'49'       DREINFO = X'49NN04'                          
         STC   R5,DREINFO+1                                                     
         MVI   DREINFO+2,X'44'                                                  
         GOTO1 NBHELLO,DMCB,(C'P',=C'UNTFIL'),(R7),DREINFO                      
*                                                                               
XITCABLE B     SUBRXIT                                                          
         EJECT                                                                  
* READ FOR USER DEFINED ADJUSTMENT FACTORS - TVQ                                
BTUREAD  NTR1                                                                   
         MVI   GOTUTYPE,0                                                       
         OC    NBNTI,NBNTI                                                      
         BZ    BTUXIT                                                           
         LA    RF,DBLOCK                                                        
         USING DBLOCK,RF                                                        
         MVI   DBMODE,DBMDSEQ                                                   
         MVC   BTUDB,DBLOCK                                                     
         LA    RF,BTUDB                                                         
         MVC   DBFILE,=C'NAD'                                                   
         MVI   DBMODE,DBMFRST                                                   
         MVI   DBFUNCT,DBGETNTI                                                 
         L     RE,DBAREC                                                        
         USING PMKEY,RE                                                         
         MVC   DBSELPRG,NBNTI                                                   
         CLC   DBSELSTA+3(2),=C'PN'                                             
         BNE   *+8                                                              
         MVI   DBSELSTA+3,C' '                                                  
         CLI   DBSELSTA+4,C'T'                                                  
         BNE   *+8                                                              
         MVI   DBSELSTA+4,C'N'                                                  
         CLI   DBSELSTA+4,C'S'                                                  
         BNE   *+8                                                              
         MVI   DBSELSTA+4,C'M'                                                  
         MVI   DBBTYPE,C'U'                                                     
         MVI   DBSELDAY,0                                                       
         XC    DBSELTIM(4),DBSELTIM                                             
         MVI   DBSELDUR,X'FF'      ALL DURATIONS                                
         MVI   DBSELDAY,0                                                       
         XC    DBSELTIM(4),DBSELTIM                                             
         MVI   DBPRGDUR,C'Y'                                                    
         MVI   DBBEST,C'A'                                                      
         MVI   DBSELDUR,X'FF'                                                   
         CLI   BTUTYP,C'E'         ESTIMATED BOOK IS SET                        
         BE    BTUR4                                                            
         LA    RE,TVQDATES         YES - CONVET TO TVQ BOOK                     
BTUR2    CLI   0(RE),X'FF'         NOT FOUND - ALLOW A MISS                     
         BE    BTUR4                                                            
         CLC   DBSELBK,2(RE)       CURRENT OUTSIDE RANGE                        
         BH    BTUR3               GET NEXT                                     
         CLC   DBSELBK,0(RE)       TOTALLY OUTSIDE RANGE - ALLOW MISS           
         BL    BTUR4                                                            
         MVC   DBSELBK(2),4(RE)    FOUND THE EQUATE - USE IT                    
         B     BTUR4                                                            
BTUR3    LA    RE,6(RE)                                                         
         B     BTUR2                                                            
*                                                                               
BTUR4    XC    DBAQUART,DBAQUART                                                
         XC    BTUAQ,BTUAQ                                                      
         LA    RE,BTUIO                                                         
         ST    RE,DBAREC                                                        
         L     RF,=F'1000'                                                      
         XCEF                                                                   
         DROP  RE                                                               
         DROP  RF                                                               
*                                                                               
         GOTO1 NBDEMAND,BTUDMCB,BTUDB,BTUHK                                     
*                                                                               
         LA    RF,BTUDB                                                         
         USING DBLOCK,RF                                                        
         OC    DBAQUART(4),DBAQUART                                             
         BNZ   BTUXIT                                                           
*        DROP  RF                                                               
         CLI   GOTUTYPE,1                                                       
         BNE   BTUXIT                                                           
         MVC   DBAQUART(4),BTUAQ                                                
         B     BTUXIT                                                           
         DROP  RF                                                               
BTUHK    LA    RF,BTUDB                                                         
         MVC   BTUAQ,8(RF)                                                      
         MVI   GOTUTYPE,1                                                       
         BR    RE                                                               
BTUXIT   XIT1                                                                   
       ++INCLUDE DETVQDATE                                                      
         LTORG                                                                  
         EJECT                                                                  
*--DEMO OVERRIDE PRECISION TABLES                                               
COPREC1  DC    C'R',X'82',C'S',X'81',C'P',X'82',C'T',X'42',C'H',X'42'           
         DC    C'U',X'44',C'V',X'40'                                            
NSPREC1  DC    C'R',X'81',C'S',X'81',C'P',X'81',C'T',X'43',C'H',X'43'           
         DC    C'U',X'44',C'V',X'40'                                            
******************************************************                          
* PARAMETERS                                                                    
NTIVALS  DC    C'NTINPNN'          NTI OUTPUT FORMAT BLOCK                      
*                                                                               
SHARE    DC    C'S',X'01'                                                       
*                                                                               
BITTBL2  DC    X'80'               BIT POS 1 ON                                 
         DC    X'40'               BIT POS 2 ON                                 
         DC    X'20'               BIT POS 3 ON                                 
         DC    X'10'               BIT POS 4 ON                                 
*                                                                               
ATABLES  DC    X'D0',AL3(0)                                                     
         DC    X'FF'                                                            
         SPACE 2                                                                
       ++INCLUDE DENADMON                                                       
*ADMON   DC    AL1(92,08,30),AL1(92,09,27),AL1(92,09)                           
*        DC    AL1(92,09,28),AL1(92,10,25),AL1(92,10)                           
*        DC    AL1(92,10,26),AL1(92,11,22),AL1(92,11)                           
*        DC    AL1(92,11,23),AL1(92,12,27),AL1(92,12)                           
*        DC    AL1(92,12,28),AL1(93,01,24),AL1(93,01)                           
*        DC    AL1(93,01,25),AL1(93,02,21),AL1(93,02)                           
*        DC    AL1(93,02,22),AL1(93,03,28),AL1(93,03)                           
*        DC    AL1(93,03,29),AL1(93,04,25),AL1(93,04)                           
*        DC    AL1(93,04,26),AL1(93,05,30),AL1(93,05)                           
*        DC    AL1(93,05,31),AL1(93,06,27),AL1(93,06)                           
*        DC    AL1(93,06,28),AL1(93,07,25),AL1(93,07)                           
*        DC    AL1(93,07,26),AL1(93,08,29),AL1(93,08)                           
*        DC    AL1(93,08,30),AL1(93,09,26),AL1(93,09)                           
*        DC    AL1(93,09,27),AL1(93,10,24),AL1(93,10)                           
*        DC    AL1(93,10,25),AL1(93,11,21),AL1(93,11)                           
*        DC    AL1(93,11,22),AL1(93,12,26),AL1(93,12)                           
*        DC    AL1(93,12,27),AL1(94,01,30),AL1(94,01)                           
*        DC    AL1(94,01,31),AL1(94,02,27),AL1(94,02)                           
*        DC    AL1(94,02,28),AL1(94,03,27),AL1(94,03)                           
*        DC    AL1(94,03,28),AL1(94,05,01),AL1(94,04)                           
*        DC    AL1(94,05,02),AL1(94,05,29),AL1(94,05)                           
*        DC    AL1(94,05,30),AL1(94,07,03),AL1(94,06)                           
*        DC    AL1(94,07,04),AL1(94,07,31),AL1(94,07)                           
*        DC    AL1(94,08,01),AL1(94,09,04),AL1(94,08)                           
*        DC    AL1(94,09,05),AL1(94,10,02),AL1(94,09)                           
*        DC    AL1(94,10,03),AL1(94,10,30),AL1(94,10)                           
*        DC    AL1(94,10,31),AL1(94,11,27),AL1(94,11)                           
*        DC    AL1(94,11,28),AL1(95,01,01),AL1(94,12)                           
*        DC    AL1(95,01,02),AL1(95,01,29),AL1(95,01)                           
*        DC    AL1(95,01,30),AL1(95,02,26),AL1(95,02)                           
*        DC    AL1(95,02,27),AL1(95,03,26),AL1(95,03)                           
*        DC    AL1(95,03,27),AL1(95,04,30),AL1(95,04)                           
*        DC    AL1(95,05,01),AL1(95,05,28),AL1(95,05)                           
*        DC    AL1(95,05,29),AL1(95,06,25),AL1(95,06)                           
*        DC    AL1(95,06,26),AL1(95,07,30),AL1(95,07)                           
*        DC    AL1(95,07,31),AL1(95,09,03),AL1(95,08)                           
*        DC    AL1(95,09,05),AL1(99,12,30),AL1(99,13) EOF-FORCE BAD BK          
*                                                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
SUBRWORK  DSECT                                                                 
*                                                                               
DIASC    DS    XL75                DEMOUT INPUT FOR SETCABLE  (25*3)            
DREINFO  DS    XL3      X'49NN04'  X'49' EL CD, LEN, & FLD LEN                  
DOASC    DS    XL100               DEMOUT OUTPUT FOR SETCABLE (25*4)            
ELEM5E   DS    XL7                 WORK AREA FOR X'5E' ELEM                     
*   USER FILE STUFF ****************************                                
GOTUTYPE DS    X                                                                
BTUTYP   DS    X                                                                
BTUAQ    DS    F                                                                
BTUDMCB  DS    10F                                                              
BTUDB    DS    XL256                                                            
         DS    XL4                                                              
BTUIO    DS    1000C                                                            
************************************************                                
         ORG   GOTUTYPE                                                         
NHTUNIV  DS    1800C                                                            
*                                                                               
SUBRWRKX EQU   *                                                                
         EJECT                                                                  
DSPHDRD  DSECT                     TABLE HEADER                                 
DSPFILE  DS    CL1                 FILE CODE                                    
DSPMED   DS    CL1                 SUB-FILE CODE                                
DSPSRC   DS    CL1                 SOURCE CODE                                  
DSPSBOOK DS    XL2                 START BOOK (BINARY YYMM)                     
DSPLDE   DS    XL2                 L'DATA ELEMENT                               
DSPAET   DS    AL3                 A(NEXT TABLE HEADER-1)                       
DSPHDRLN EQU   *-DSPHDRD                                                        
*                                                                               
DSPDTAD  DSECT                     DATE ELEMENT                                 
DSPMOD   DS    XL1                 MODIFIER CODE                                
DSPDEMO  DS    XL1                 DEMO NUMBER                                  
DSPELCD  DS    XL1                 ELEMENT CODE                                 
DSPFLDN  DS    XL1                 FIELD NUMBER                                 
DSPPREC  DS    XL1                 FIELD PRECISION                              
DSPDTALN EQU   *-DSPDTAD                                                        
*                                                                               
