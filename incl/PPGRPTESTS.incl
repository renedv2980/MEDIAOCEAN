*          DATA SET PPGRPTESTS AT LEVEL 009 AS OF 02/05/97                      
         SPACE 2                                                                
*       *************************                                               
******  TEST CLIENT GROUP SECURITY  ROUTINE                                     
*                                                                               
*       RA IS ASSUMED TO POINT TO TWA                                           
*       AND CLIENT IS ASSUMED TO BE PRESENT                                     
*       PGENGRP MUST BE INCLUDED IN THE CALLING PROGRAM                         
*       *************************                                               
*                                                                               
VALGROUP NTR1                                                                   
*                                                                               
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING GRPRECD,R4          CLIENT GROUP PASSIVE POINTER                 
*                                                                               
         MVI   GRPPTYP,GRPPCGQ     RECORD TYPE                                  
         MVC   GRPPAGY(3),PCLTKAGY   AGENCY/MEDIA                               
         MVC   GRPPVAL(3),PCLTKCLT CLIENT                                       
         OC    GRPPVAL,=6C' '      SPACE PADDED                                 
         MVC   GRPPID(1),7(RA)       GROUP ID                                   
*                                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(14),KEYSAVE     CHECK THROUGH ID                             
         BNE   VALGR10                                                          
*                                                                               
         MVC   GRPFULL,=6C' '      (6 TO SAVE USING ANOTHER LITERAL)            
         MVC   GRPFULL(2),8(RA)    GROUP CODE                                   
         OC    GRPFULL,=C'0000'    REPLACE BLANKS WITH X'F0'                    
         PACK  DUB,GRPFULL                                                      
         L     R0,DUB+4                                                         
         SRL   R0,4                GET RID OF SIGN NIBBLE                       
         STCM  R0,3,HALF           LEFT-JUSTIFIED, PWOS                         
*                                                                               
         CLC   HALF,GRPPCODE       GROUP CODE MATCH?                            
         BE    VALGR20             YES                                          
*                                                                               
VALGR10  LTR   RE,RE               ACCESS DENIED EXIT (NOT EQUAL)               
         B     VALGXIT                                                          
*                                                                               
VALGR20  CR    RE,RE               ACCESS OK EXIT (EQUAL)                       
*                                                                               
VALGXIT  XIT1                                                                   
*                                                                               
         DROP  R4                                                               
*                                                                               
GRPFULL  DS    F                                                                
*******************************************************                         
