*          DATA SET TAT2TACC   AT LEVEL 001 AS OF 10/29/11                      
***********************************************************************         
*        ROUTINE TRANSLATES TAMC AND TATR ELEMENT INTO TACC FORMAT    *         
*        ON ENTRY ... P1=A(COMMERCIAL RECORD WITH TAMC AND TATR ELS)  *         
*                     P2=A(RECORD TO SAVE TACC ELEMENTS INTO)         *         
***********************************************************************         
                                                                                
TRN2TACC NTR1  BASE=*,LABEL=*                                                   
         MVC   FULL,0(R1)          FULL=A(COMMERCIAL RECORD)                    
         MVC   TGFULL,4(R1)        TGFULL=A(RECORD TO ADJUST)                   
                                                                                
         USING TACCD,R2                                                         
         LA    R2,ELEMENT                                                       
         XC    ELEMENT,ELEMENT                                                  
         MVI   TACCEL,TACCELQ      INITIALIZE OLD STYLE COMMERCIAL              
         XR    R0,R0               CONTRACT ELEMENT                             
         LA    R3,TACCCON                                                       
                                                                                
***********************************************************************         
                                                                                
         USING TACOD,R4                                                         
         L     R4,FULL                                                          
         MVI   ELCODE,TACOELQ      READ COMMERCIAL DETAILS ELEMENT              
         BRAS  RE,GETEL                                                         
         JE    *+6                                                              
         DC    H'00'                                                            
         LA    R5,TACOCID         R5=A(COMMERCIAL ID)                           
         DROP  R4                                                               
                                                                                
***********************************************************************         
                                                                                
         USING TAMCD,R4                                                         
         L     R4,FULL                                                          
         MVI   ELCODE,TAMCELQ      READ ALL NEW STYLE COMMERCIAL                
         BRAS  RE,GETEL            CONTRACTS                                    
         JNE   T2TACC20                                                         
                                                                                
T2TACC10 MVC   0(L'TACCCON,R3),0(R5)                                            
         TM    TAMCSTAT,TAMCSNEW                                                
         JO    *+10                                                             
         MVC   0(L'TACCCON,R3),TAMCCON                                          
         AHI   R0,1                SAVE THEM INTO OLD STYLE COMMERCIAL          
         LA    R3,L'TACCCON(R3)    CONTRACT ELEMENT                             
                                                                                
         BRAS  RE,NEXTEL                                                        
         BE    T2TACC10                                                         
         DROP  R4                                                               
                                                                                
***********************************************************************         
                                                                                
T2TACC20 MVI   BLOCK,X'FF'                                                      
                                                                                
         USING TATRD,R4                                                         
         L     R4,FULL                                                          
         MVI   ELCODE,TATRELQ      READ ALL NEW STYLE TRACK ASSOICATION         
         BRAS  RE,GETEL            ELEMENTS                                     
         J     *+8                                                              
T2TACC30 BRAS  RE,NEXTEL                                                        
         JNE   T2TACC60                                                         
                                                                                
         LA    RE,BLOCK                                                         
T2TACC40 CLI   0(RE),X'FF'         SAVE EACH UNIQUE INTERNAL COMMERCIAL         
         JE    T2TACC50            NUMBER INTO BLOCK                            
         CLC   TATRCOM,0(RE)                                                    
         JE    T2TACC30                                                         
         LA    RE,L'TATRCOM(RE)                                                 
         J     T2TACC40                                                         
                                                                                
T2TACC50 MVC   0(L'TATRCOM,RE),TATRCOM                                          
         MVI   L'TATRCOM(RE),X'FF'                                              
         J     T2TACC30                                                         
         DROP  R4                                                               
                                                                                
***********************************************************************         
                                                                                
T2TACC60 CLI   BLOCK,X'FF'         IF ANY NEW STYLE TRACK ASSOCIATIONS          
         JE    T2TACC90            EXIST                                        
                                                                                
         L     RE,FULL                                                          
         XC    BLOCK+250(L'TLCOKEY),BLOCK+250                                   
         MVC   BLOCK+250(L'TLCOKEY),0(RE)              SAVE COMM'L KEY          
                                                                                
         LA    R6,BLOCK                                                         
                                                                                
         USING TLCOPD,R1                                                        
T2TACC70 LA    R1,KEY                                                           
         XC    KEY,KEY                                                          
         MVI   TLCOPCD,TLCOCCDQ                                                 
         MVC   TLCOCCOM,0(R6)                                                   
         GOTO1 HIGH                                                             
         CLC   KEY(L'TLCOPKEY),KEYSAVE                                          
         JNE   T2TACC80                                                         
                                                                                
         MVC   DUB(L'AIO),AIO                                                   
         MVC   AIO,FULL                                                         
         GOTO1 GETREC                                                           
         MVC   AIO,DUB                                                          
                                                                                
         USING TACOD,R4                                                         
         L     R4,FULL                                                          
         MVI   ELCODE,TACOELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   T2TACC80                                                         
         MVC   0(L'TACCCON,R3),TACOCID                                          
         AHI   R0,1                                                             
         LA    R3,L'TACCCON(R3)                                                 
         DROP  R4                                                               
                                                                                
T2TACC80 CLI   L'TATRCOM(R6),X'FF'                                              
         JE    T2TACC90                                                         
         LA    R6,L'TATRCOM(R6)                                                 
         J     T2TACC70                                                         
                                                                                
T2TACC90 CLI   BLOCK,X'FF'                                                      
         JE    T2TACCX                                                          
         MVC   KEY,BLOCK+250                                                    
         GOTO1 HIGH                                                             
         CLC   KEY(L'TLCOKEY),KEYSAVE                                           
         JE    *+6                                                              
         DC    H'00'                                                            
         MVC   DUB(L'AIO),AIO                                                   
         MVC   AIO,FULL                                                         
         GOTO1 GETREC                                                           
         MVC   AIO,DUB                                                          
                                                                                
***********************************************************************         
                                                                                
T2TACCX  LTR   R0,R0                                                            
         JZ    NO                                                               
         L     R3,TGFULL                                                        
         GOTO1 HELLO,DMCB,(C'D',=C'TALFIL'),('TACCELQ',(R3)),0                  
                                                                                
         STC   R0,TACCNCON                                                      
         MHI   R0,L'TACCCON                                                     
         AHI   R0,TACCLNQ                                                       
         STC   R0,TACCLEN                                                       
         GOTO1 HELLO,DMCB,(C'P',=C'TALFIL'),(R3),(R2),0                         
         DROP  R2                                                               
                                                                                
         CLI   BLOCK,X'FF'                                                      
         JNE   YES                                                              
         J     NO                                                               
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
