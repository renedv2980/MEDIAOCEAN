*          DATA SET RECYCLCR   AT LEVEL 215 AS OF 07/16/01                      
**CYCCR>                                                                        
*                                                                               
*   THIS ROUTINE BUILDS THE CREDIT   TABLE FOR PREVIOUS TARGET                  
*      BUYLINE                                                                  
*                                                                               
CYCLECR  NMOD1 0,*CYCCR*                                                        
         L     RC,0(R1)            RESET A(WORKSPACE)                           
         XC    CRSPOTS,CRSPOTS     CLEAR TOTAL MISSED COUNT                     
CCRD0010 EQU   *                                                                
         MVC   THISBUY#,RBUYKLIN   SAVE BUYLINE NUMBER                          
         LA    R6,RBUYELEM         SET A(DESCRIPTOR ELT IN BUY)                 
CCRD0020 EQU   *                                                                
         CLI   0(R6),0             END OF RECORD?                               
         BE    CCRD0100            YES - FINISHED WITH CYCLE                    
         CLI   0(R6),X'07'         NO  - OLDCREDIT REFERENCE ELEMENT?           
         BE    CCRD0060            YES                                          
         CLI   0(R6),X'16'         NO  - CREDIT REFERENCE ELEMENT?              
         BE    CCRD0040            YES                                          
CCRD0030 EQU   *                                                                
         ZIC   RF,1(R6)            NO  - BUMP TO NEXT ELT                       
         AR    R6,RF                                                            
         B     CCRD0020            GO BACK FOR NEXT ELT                         
CCRD0040 EQU   *                                                                
         USING RBUYCAEL,R6                                                      
**       OC    FIRSTMGD,FIRSTMGD                                                
**       BNZ   CCRD0044                                                         
*                                                                               
**       GOTO1 DATCON,DMCB,(3,RBUYMGD1),(X'20',FIRSTMGD)                        
**       LTR   RB,RB               INSTRUCTION TO TEST/DUMP                     
***      CLC   RBUYKMLN(2),=X'0D0E'                                             
***      BNE   *+6                                                              
***      DC    H'0'                                                             
*                                                                               
CCRD0044 EQU *                                                                  
         L     R2,ANXTCRTB         SET A(NEXT OPEN SLOT IN TABLE)               
         CLI   RBUYCASP,0          ANY SPOTS IN CREDIT?                         
         BZ    CCRD0030            NO  - DON'T TABLE THIS UP                    
         MVC   0(3,R2),RBUYCASD    MOVE DATE(3)                                 
         MVC   3(1,R2),THISBUY#    MOVE LINE#(1)                                
         MVC   4(1,R2),RBUYCASP    MOVE #SPOTS(1)                               
         ZIC   RF,RBUYCASP         COUNT NUMBER OF SPOTS                        
         L     RE,CRSPOTS          LOAD COUNT OF SPOTS CREDITED                 
         AR    RE,RF                                                            
         ST    RE,CRSPOTS          SAVE COUNT OF SPOTS CREDITED                 
         LA    R2,5(R2)            BUMP NEXT AVAILABLE SLOT                     
         XC    0(5,R2),0(R2)       CLEAR NEXT SLOT                              
         ST    R2,ANXTCRTB         SAVE A(NEXT SLOT)                            
         L     RF,CRCOUNTR         INCREMENT CREDIT   COUNTER                   
         LA    RF,1(RF)                                                         
         ST    RF,CRCOUNTR                                                      
         B     CCRD0030            GO BACK FOR NEXT ELEMENT                     
*                                                                               
         DROP  R6                                                               
*                                                                               
CCRD0060 EQU   *                                                                
         USING RBUYCREL,R6                                                      
*                                                                               
CCRD0064 EQU *                                                                  
         L     R2,ANXTCRTB         SET A(NEXT OPEN SLOT IN TABLE)               
         CLI   RBUYCRSP,0          ANY SPOTS IN CREDIT?                         
         BZ    CCRD0030            NO  - DON'T TABLE THIS UP                    
         MVC   0(3,R2),RBUYCRDT    MOVE DATE(3)                                 
         MVC   3(1,R2),THISBUY#    MOVE LINE#(1)                                
         MVC   4(1,R2),RBUYCRSP    MOVE #SPOTS(1)                               
         ZIC   RF,RBUYCRSP         COUNT NUMBER OF SPOTS                        
         L     RE,CRSPOTS          LOAD COUNT OF SPOTS CREDITED                 
         AR    RE,RF                                                            
         ST    RE,CRSPOTS          SAVE COUNT OF SPOTS CREDITED                 
         LA    R2,5(R2)            BUMP NEXT AVAILABLE SLOT                     
         XC    0(5,R2),0(R2)       CLEAR NEXT SLOT                              
         ST    R2,ANXTCRTB         SAVE A(NEXT SLOT)                            
         L     RF,CRCOUNTR         INCREMENT CREDIT   COUNTER                   
         LA    RF,1(RF)                                                         
         ST    RF,CRCOUNTR                                                      
         B     CCRD0030            GO BACK FOR NEXT ELEMENT                     
*                                                                               
         DROP  R6                                                               
*                                                                               
CCRD0100 EQU   *                                                                
         XIT1                                                                   
THISBUY# DS    XL1                                                              
         DS    0F                                                               
         LTORG                                                                  
         EJECT                                                                  
**CYCMG>                                                                        
