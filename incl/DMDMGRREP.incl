*          DATA SET DMDMGRREP  AT LEVEL 041 AS OF 02/29/00                      
         TITLE 'DATAMGR - INTEREP SPECIAL PROCESSING'                           
*&&REP                                                                          
***********************************************************************         
*- MOD LOG                                                                      
*  -------                                                                      
*  JAN06/98 (BU ) --- SWING TABLES INTO PANBOOK REPMTBL                         
*                                                                               
*                     ***  END TOMBSTONE  ***                                   
***********************************************************************         
         SPACE                                                                  
*                                                                               
* CALLED BEFORE GOING TO FILE                                                   
* REPLACE SUBSIDIARY REP CODE WITH MASTER REP CODE FOR MASTER RECORDS           
* INPUT: DMCBW3 - A(CALLER'S KEY)                                               
*                                                                               
REPMOD1  NMOD1 0,**IREP**                                                       
         LR    R3,R1                                                            
         CLC   REPPMFLG,=C'INIT'                                                
         BE    REPM04                                                           
*                                                                               
         ICM   RF,15,=V(TICTOC)                                                 
         BZ    REPM02                                                           
         GOTO1 (RF),RMDUB,C'SSET'                                               
*                                                                               
REPM02   MVC   RMDUB,=CL8'INTEREP'                                              
         LHI   R0,C'M'                                                          
         ICM   RF,15,=V(SSB)       SEE IF OFFLINE                               
         BZ    REPMLOD             YES                                          
         OC    0(2,RF),0(RF)                                                    
         BZ    REPMLOD             YES                                          
         LHI   R0,C'A'                                                          
*                                                                               
REPMLOD  LA    RF,REPPMTAB                                                      
         GOTO1 =V(LOADER),RMPLST,RMDUB,(RF),((R0),(RF))                         
         MVC   REPPMFLG,=C'INIT'                                                
*                                                                               
         ICM   RF,15,=V(TICTOC)                                                 
         BZ    REPM04                                                           
         GOTO1 (RF),RMDUB,C'RSET'                                               
*                                                                               
REPM04   LR    R1,R3                                                            
         XC    REPWRK(REPWRKL),REPWRK CLEAN OUT REP WORK AREA                   
         L     R5,DMCBW3           A(CALLER'S KEY)                              
         LA    R8,REPPMTAB                                                      
*                                                                               
REPM100  CLC   0(1,R5),RTABID(R8)  MATCH ON KEY ID?                             
         BNE   REPM150                                                          
         ZIC   RF,RTABDISP(R8)                                                  
         AR    RF,R5               RF = A(KEY REP CODE)                         
         CLC   0(2,RF),RTABSUB(R8) CALLER KEY REP -VS- SUBSID REP               
         BNE   REPM150                                                          
         MVC   0(2,RF),RTABMAST(R8)                                             
         ST    R8,REPTABA          SAVE A(REPTABLE ENTRY)                       
         B     REPM1EXT                                                         
*                                                                               
REPM150  LA    R8,RTABELEN(R8)                                                  
         CLI   0(R8),0             EOT?                                         
         BNE   REPM100                                                          
REPM1EXT XIT1                                                                   
*                                                                               
RMDUB    DS    D                                                                
RMPLST   DS    3F                                                               
*                                                                               
         SPACE 2                                                                
* CALL AFTER DIRECTORY ACCESS.                                                  
* RETURN SUBSIDIARY REP CODE TO MASTER KEY (IF REPLACED BY REPMOD1)             
* SPECIAL 'SEQ' CONSIDERATIONS WHEN READING PAST RECORD ID/REP CODE             
* INPUT: DMCBW3 - A(CALLER'S INPUT KEY)                                         
*        DMCBW4 - A(CALLER'S OUTPUT KEY)                                        
*        OLDKEY - INPUT KEY (NOT = CALLER KEY ON SEQ)                           
*                                                                               
REPMOD2  NMOD1 0,**REP2MOD                                                      
         MVI   REPRC,0             ASSUME NO ERROR                              
         ICM   R8,X'F',REPTABA     A(REP TABLE ENTRY)                           
         BZ    REPM2EXT                                                         
         L     R5,DMCBW4           A(KEY RETURNED TO USER)                      
         CLC   0(1,R5),RTABID(R8)                                               
         BNE   REPM250             ID CHANGE                                    
         TM    27(R5),X'08'                                                     
         BZ    REPM210                                                          
REPM205  MVI   REPRC,X'01'         NEED TO READ SEQ                             
         B     REPM2EXT                                                         
*                                                                               
REPM210  ZIC   RF,RTABDISP(R8)     REPLACE MASTER WITH SUBSID                   
         AR    RF,R5                                                            
**TEMP                                                                          
         CLC   0(2,RF),RTABSUB(R8) REJECT IF = SUBSID                           
         BE    REPM205                                                          
**TEMP                                                                          
         CLC   0(2,RF),RTABMAST(R8) SAME MASTER REP CODE?                       
         BE    REPM240                                                          
         BL    REPM2EXT                                                         
         B     REPM250                                                          
*                                                                               
REPM240  MVC   0(2,RF),RTABSUB(R8) OUTPUT KEY                                   
         ZIC   R5,RTABDISP(R8)                                                  
         L     RF,DMCBW3                                                        
         AR    RF,R5                                                            
         MVC   0(2,RF),RTABSUB(R8) INPUT KEY                                    
         B     REPM2EXT                                                         
*                                                                               
* IF REP CODE IS LOW IN KEY (LAST 2 BYTES) JUST RETURN (CURRENT) KEY            
* ELSE RE-POINT INTERNAL KEY TO NEXT PHYSICAL KEY AFTER SUBSIDIARY              
* REP'S LOGICAL POSISTION (AS IF SUBSIDIARY REP CODES WERE ACTUALLY             
* ON THE FILE)                                                                  
*                                                                               
REPM250  ZIC   RF,RTABDISP(R8)                                                  
         LA    R5,OLDKEY                                                        
         AR    RF,R5               A(REP CODE IN PRIOR KEY)                     
*                                                                               
         LA    R0,2(RF)            ZERO REMAINING KEY AFTER REP CODE            
         LA    RE,27(R5)                                                        
         SR    RE,R0               RE = LENGTH REMAINING                        
         BZ    REPM2EXT            (0 MEANS REP CODE IS LAST 2 BYTES)           
         BCTR  RE,0                                                             
         EX    RE,REPM2ZER         ZERO THE KEY                                 
*                                                                               
         LH    RE,RTABSUB(R8)      PUT (SUBSID REP CODE+1) INTO                 
         LA    RE,1(RE)            OLDKEY REP CODE                              
         STCM  RE,3,0(RF)                                                       
         MVI   REPRC,X'FF'         NEED TO READ HI ON RETURN                    
*                                                                               
REPM260  L     RE,DMCBW3           REPLACE USER KEY WITH OLDKEY                 
         MVC   0(27,RE),0(R5)                                                   
*                                                                               
REPM2EXT XIT1                                                                   
         SPACE 1                                                                
REPM2ZER XC    0(0,RF),0(RF)       ZERO REMAINING KEY DATA                      
         SPACE 1                                                                
         LTORG                                                                  
*                                                                               
RTABID   EQU   0                   X'MASTER KEY ID'                             
RTABDISP EQU   1                   REP CODE DISPLACEMENT IN KEY                 
RTABSUB  EQU   2                   SUBSIDIARY REP CODE                          
RTABMAST EQU   4                   MASTER REP CODE                              
RTABELEN EQU   6                   ENTRY LENGTH                                 
*                                                                               
         DS    0D                                                               
REPPMFLG DS    A                                                                
         DS    A                                                                
REPPMTAB DC    10000X'00'                                                       
         DC    C'*EOTEOTEOTEOTEOTEOTEOT*'                                       
*&&                                                                             
