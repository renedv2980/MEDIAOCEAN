*          DATA SET POSTOFF    AT LEVEL 002 AS OF 05/01/02                      
ICSPOST  CSECT                                                                  
         NMOD1 0,ICSPOST                                                        
         SPACE 2                                                                
         LA    RC,SPACEND                                                       
         USING IMRWORK,RC                                                       
         USING BYELEMD,R6                                                       
         USING IELEMD,R7                                                        
         SPACE 3                                                                
*                                                                               
         CLI   ORBSW,0             TEST ANY ORBITS                              
         BE    PS9                 NO                                           
*                                  HAVE ORBITS -                                
*                                  - INVS ARE NOW LINKED TO ORBIT               
*                                     SLAVES -MUST BE LINKED TO MASTERS         
         SR    R5,R5                                                            
         L     R7,AFINV                                                         
         B     *+8                                                              
PS4      DS    0H                                                               
         LA    R7,L'IELEM(R7)                                                   
         C     R7,ALINV                                                         
         BNL   PS9                                                              
         OC    IBY,IBY                                                          
         BZ    PS4                 NOT MATCHER                                  
         SR    R6,R6                                                            
         ICM   R6,7,IBY                                                         
         A     R6,AFBY                                                          
         BCTR  R6,R0               POINT TO MATCHED BUY                         
         TM    BYSTAT,X'08'        TEST ORBIT SLAVE                             
         BZ    PS4                 NO                                           
*                                                                               
         LTR   R5,R5                                                            
         BZ    PS6                                                              
         BAS   RE,CHKMAST                                                       
         BNE   PS6                                                              
PS5      DS    0H                                                               
         LA    RF,1(R5)                                                         
         S     RF,AFBY                                                          
         STCM  RF,7,IBY            MARK INV MATCHED TO MASTER                   
         B     PS4                                                              
*                                                                               
*                                  SEARCH FOR MASTER                            
PS6      DS    0H                                                               
         L     R5,AFBY                                                          
         B     *+8                                                              
PS6B     DS    0H                                                               
         LA    R5,L'BYELEM(R5)                                                  
         C     R5,ALBY                                                          
         BL    *+6                                                              
         DC    H'0'                MASTER MUST BE THERE                         
         BAS   RE,CHKMAST                                                       
         BNE   PS6B                                                             
         B     PS5                                                              
         SPACE 3                                                                
CHKMAST  DS    0H                                                               
         TM    BYSTAT-BYELEM(R5),X'10'                                          
         BNZ   *+8                                                              
         LTR   RE,RE               NOT A MASTER                                 
         BR    RE                                                               
         CLC   BYEST(2),0(R5)                                                   
         BNER  RE                                                               
         CLC   BYORBNO,BYORBNO-BYELEM(R5)                                       
         BNER  RE                                                               
         CLC   BYSPT,BYSPT-BYELEM(R5)                                           
         BR    RE                                                               
         SPACE 3                                                                
PS9      DS    0H                                                               
         SPACE 2                                                                
*                                  INITIALIZE ADDPARS                           
         XC    ADDPARS,ADDPARS                                                  
         USING AFFELEM,R8                                                       
         MVC   ADDPAR1,ADBUY                                                    
         LA    R8,X                                                             
         ST    R8,ADDPAR2                                                       
         L     R2,BUYRPARS+4                                                    
         L     R1,BUYRPARS+8                                                    
         USING BUYRTABD,R2                                                      
         LTR   R1,R1                                                            
         BZ    PSEXT               NO BUYS                                      
         MH    R1,BUYRPARS+14      NO. TIMES LENGTH                             
         A     R1,BUYRPARS+4       PLUS START                                   
         ST    R1,BUYRTX           SET END OF TABEL                             
*                                                                               
         XC    AFFELEM(6),AFFELEM                                               
         MVI   ACODE,X'10'                                                      
         XC    FLMELEM,FLMELEM                                                  
         MVI   FLMCODE,X'12'                                                    
         LA    R0,AFFELEM                                                       
         ST    R0,ADDPAR2                                                       
         B     PNXTREC2                                                         
         SPACE 2                                                                
PNXTREC  EQU   *                                                                
         LA    R2,BUYRTBEL(R2)                                                  
         C     R2,BUYRTX                                                        
         BNL   PSEXT                                                            
*                                                                               
PNXTREC2 DS    0H                                                               
         MVC   KEY+14(4),BUYRDA                                                 
         MVC   AREC,ADBUY                                                       
         GOTO1 GET                                                              
*                                                                               
         L     R5,ADBUY                                                         
         USING BUYREC,R5                                                        
         TM    BDSTAT,X'80'        IF RADIO NPW STYLE POL                       
         BNZ   PNXTREC             SKIP POSTING                                 
         MVI   BYTE3,0             CLEAR REC ALTERED SW                         
         SPACE 2                                                                
*                                  FIND BUYS FOR THIS EST-LIN                   
         L     R6,AFBY                                                          
PS10     C     R6,ALBY                                                          
         BNL   PS50                DONE WITH RECORD                             
         TM    BYSTAT2,BYSTMISQ    SKIP ALL MISSED SPOTS                        
         BNZ   PS11                                                             
         CLI   BYPRD,X'FF'                                                      
         BE    PS11                BYPASS UNALLOCATED SPOTS                     
         TM    BYSTAT,X'08'        SKIP ORBIT SLAVES                            
         BNZ   PS11                                                             
         CLC   BUYKEY+9(2),BYEST                                                
         BE    PS12                                                             
PS11     LA    R6,L'BYELEM(R6)                                                  
         B     PS10                                                             
PS12     LA    R0,1(R6)                                                         
         S     R0,AFBY                                                          
         ST    R0,FULL             FULL = RELATIVE ADDRESS OF BUY               
         MVC   ADDPAR4(2),BYSDT    DATE                                         
         MVI   ADDPAR3+1,0         SPOT REF. NO.                                
         MVI   ALEN,0              CLEAR LENGTH                                 
         SPACE 2                                                                
         SR    R0,R0                                                            
         SR    R1,R1                                                            
         IC    R0,BYUNACH                                                       
         IC    R1,BYNOWK                                                        
         SR    R1,R0                                                            
         STC   R1,BYTE2            BYTE2 = MATCHED SPOTS FOR THIS BUY           
         BNZ   PS13                GET INVOICES                                 
         SPACE 1                                                                
*                                  NO MATCHED SPOTS                             
         CLI   BUYKEY+3,X'FF'                                                   
         BE    PS18                                                             
         B     PS21B                                                            
         SPACE 2                                                                
*                                  FIND INVOICES FOR THIS BUY                   
         SPACE 3                                                                
PS13     L     R7,AFINV                                                         
PS14     C     R7,ALINV                                                         
         BL    *+6                                                              
         DC    H'0'                END OF INVOICES SHOULD NOT OCCUR             
         CLC   FULL+1(3),IBY       IS MATCHING INVOICE                          
         BE    PS16                                                             
PS15     LA    R7,L'IELEM(R7)                                                   
         B     PS14                                                             
PS16     MVC   ATIME,ITIM                                                       
         MVC   ADATE,IDAT          DATE                                         
         MVI   ALEN,6                                                           
         CLI   PSTOPT,C'P'         POST PARTIAL                                 
         BE    PS16B               YES                                          
         CLI   IMIS,0              IS THIS A PARTIAL                            
         BE    PS16B               NO                                           
         MVI   ALEN,0              DELETE AFFID                                 
PS16B    DS    0H                                                               
         CLI   BUYKEY+3,X'FF'                                                   
         BE    PS18                                                             
PS17     IC    R1,ADDPAR3+1        NON-POOL - BUMP REF. NO.                     
         LA    R1,1(R1)                                                         
         STC   R1,ADDPAR3+1                                                     
         B     PS20                                                             
*                                  FOR POOL BYSPT = REF. NO.                    
PS18     MVC   ADDPAR3+1(1),BYSPT                                               
PS20     GOTO1 AADDEL,ADDPARS                                                   
         SPACE 1                                                                
         CLI   ADDPARS,0           TEST REC ALTERED                             
         BE    *+8                 NO                                           
         MVI   BYTE3,X'FF'                                                      
         CLI   ADDPAR3+3,0                                                      
         BE    PS21A                                                            
         BAS   RE,PRTMSG           PRINT BUY ERROR/OVERFLOW MESSAGE             
         B     PNXTREC                                                          
*                                                                               
PS21A    DS    0H                                                               
         CLI   TRAFPOST,C'Y'       OPT TO POST FILMS                            
         BNE   PS21A8                                                           
         CLI   BUYKEY+3,X'FF'      NO FILM POSTING FOR NON-POL                  
         BNE   PS21A8                                                           
*                                                                               
         LA    R0,FLMELEM          POINT TO FILM ELEM                           
         ST    R0,ADDPAR2                                                       
         MVI   FLMLEN,0            NO FILM ELEM                                 
         XC    FLMNUM(4),FLMNUM    CLEAR FILM CODES                             
         CLI   ALEN,0              IF NO AFFID                                  
         BE    PS21A4                                                           
         CLI   IFILM,0             OR NO FILM CODE                              
         BE    PS21A4                                                           
         LA    R3,IFILM                                                         
         LA    R4,FLMNUM                                                        
         BAS   RE,PSFFILM                                                       
         OC    FLMNUM,FLMNUM       IF EITHER FILM UNDEFINED                     
         BZ    PS21A4              DONT POST                                    
         MVI   FLMLEN,5            ONE FILM                                     
         CLI   IFILM2,0                                                         
         BE    PS21A4                                                           
         LA    R3,IFILM2                                                        
         LA    R4,FLMNUM+2                                                      
         BAS   RE,PSFFILM                                                       
         OC    FLMNUM+2,FLMNUM+2   IF EITHER FILM UNDEFINED                     
         BZ    PS21A4              DONT POST                                    
         MVI   FLMLEN,7            TWO FILMS                                    
*                                                                               
PS21A4   DS    0H                                                               
         GOTO1 AADDEL,ADDPARS                                                   
         SPACE 1                                                                
         CLI   ADDPARS,0           TEST REC ALTERED                             
         BE    *+8                 NO                                           
         MVI   BYTE3,X'FF'                                                      
         CLI   ADDPAR3+3,0                                                      
         BE    PS21A6                                                           
         BAS   RE,PRTMSG           PRINT BUY ERROR/OVERFLOW MSG                 
         B     PNXTREC                                                          
*                                                                               
PS21A6   DS    0H                                                               
         LA    R0,AFFELEM          RESTORE TO AFFELEM                           
         ST    R0,ADDPAR2                                                       
*                                                                               
PS21A8   DS    0H                                                               
         CLI   BUYKEY+3,X'FF'                                                   
         BE    PS11                IF POOL, DONE - GO GET ANOTHER BUY           
         CLC   ADDPAR3+1(1),BYTE2  ARE ALL INVOICES FOUND                       
         BL    PS15                GET ANOTHER INVOICE                          
         SPACE 2                                                                
*                                  DELETE ANY REMAINING AFFELEMS                
         SPACE 2                                                                
PS21B    CLI   BYUNACH,0                                                        
         BE    PS11                NONE                                         
         IC    R1,ADDPAR3+1        SET REF. NO. 1 PAST                          
         LA    R1,1(R1)            LAST ELEM I ADDED                            
         STC   R1,ADDPAR3+1        AND LEAVE IT THERE                           
         SR    R0,R0                                                            
         IC    R0,BYUNACH                                                       
         MVI   ALEN,0              SET LENGTH TO ZERO                           
         MVI   FLMLEN,0                                                         
*                                                                               
PS22     GOTO1 AADDEL,ADDPARS                                                   
         SPACE 1                                                                
         CLI   ADDPARS,0           TEST REC ALTERED                             
         BE    *+8                 NO                                           
         MVI   BYTE3,X'FF'                                                      
         CLI   ADDPAR3+3,0                                                      
         BE    *+6                                                              
         DC    H'0'                ADDEL ERROR                                  
*                                                                               
         CLI   TRAFPOST,C'Y'       OPT TO POST FILMS                            
         BNE   PS24                                                             
         CLI   BUYKEY+3,X'FF'      NO FILM POSTING FOR NON-POL                  
         BNE   PS24                                                             
*                                                                               
         LA    R4,FLMELEM          POINT TO FILM ELEM                           
         ST    R4,ADDPAR2                                                       
*                                                                               
         GOTO1 AADDEL,ADDPARS                                                   
*                                                                               
         CLI   ADDPARS,0           TEST REC ALTERED                             
         BE    *+8                 NO                                           
         MVI   BYTE3,X'FF'                                                      
         CLI   ADDPAR3+3,0                                                      
         BE    *+6                                                              
         DC    H'0'                ADDEL ERROR                                  
*                                                                               
         LA    R4,AFFELEM          RESTORE TO AFFELEM                           
         ST    R4,ADDPAR2                                                       
*                                                                               
PS24     BCT   R0,PS22                                                          
         B     PS11                GET ANOTHER BUY                              
PS50     EQU   *                                                                
         CLI   BYTE3,X'FF'         TEST REC ALTERED                             
         BNE   PNXTREC                                                          
         CLI   RCWRITE,C'Y'                                                     
         BNE   PNXTREC                                                          
         XC    DMCB,DMCB                                                        
         GOTO1 PUT                                                              
*                                                                               
         B     PNXTREC                                                          
         SPACE 2                                                                
PSEXT    DS    0H                                                               
         XIT1                                                                   
         SPACE 2                                                                
PSFFILM  DS    0H                                                               
         XC    0(2,R4),0(R4)       CLEAR OUTPUT                                 
         ICM   R0,15,FLMTPARS+8                                                 
         BZR   RE                  NO FILMS                                     
         L     RF,AFLMTAB                                                       
         USING FLMTABD,RF                                                       
*                                                                               
PSFF4    DS    0H                                                               
         CLC   FLMRSET,IRSET       TST FROM RIGHT REC SET                       
         BNE   PSFF10                                                           
         CLC   FLMIDNO,IINVID      TST FROM RIGHT INVOICE                       
         BNE   PSFF10                                                           
         CLC   FLMICOD,0(R3)       INTERNAL FILM NO.                            
         BNE   PSFF10                                                           
*                                                                               
         MVC   0(2,R4),FLMSEQ      SET CMML SEQ                                 
         BR    RE                                                               
*                                                                               
PSFF10   DS    0H                                                               
         LA    RF,FLMTABEL(RF)                                                  
         BCT   R0,PSFF4                                                         
         BR    RE                                                               
         DROP  RF                                                               
         SPACE 3                                                                
*                                  PRINT ERROR/OVERFLOW ERROR                   
PRTMSG   NTR1                                                                   
         SPACE 2                                                                
         MVC   P(1),MED                                                         
         MVC   P+2(3),CLT                                                       
         MVC   P+6(3),PRD                                                       
         CLI   BUYKPRD,X'FF'                                                    
         BNE   *+10                                                             
         MVC   P+6(3),=C'POL'                                                   
         ZIC   R0,BUYKEST                                                       
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  P+10(3),DUB                                                      
         MVC   P+15(9),BIGSTA                                                   
         MVC   P+25(3),=C'LI='                                                  
         ZIC   R0,BUYKEY+10                                                     
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  P+28(2),DUB                                                      
*                                                                               
         CLI   ADDPAR3+2,1         TEST RECORD OVERFLOW                         
         BNE   PRTM06                                                           
*                                                                               
         MVC   P+32(2),=C'**'                                                   
         MVC   P+35(L'OVFMSG1),OVFMSG1                                          
         MVC   P2+35(L'OVFMSG2),OVFMSG2                                         
         MVC   P3+35(L'OVFMSG3),OVFMSG3                                         
         MVC   P4+35(L'OVFMSG4),OVFMSG4                                         
         MVC   P5+35(L'OVFMSG5),OVFMSG5                                         
         MVC   P6+35(L'OVFMSG6),OVFMSG6                                         
         B     PRTM08                                                           
*                                                                               
PRTM06   DS    0H                  RECORD ERROR                                 
         MVC   P+32(2),=C'**'                                                   
         MVC   P+35(L'BERMSG1),BERMSG1                                          
         MVC   P2+35(L'BERMSG2),BERMSG2                                         
         B     PRTM08                                                           
*                                                                               
PRTM08   DS    0H                                                               
         MVI   FORCEHED,C'Y'                                                    
         MVI   SKIPSPEC,C'Y'                                                    
         GOTO1 AIRPRT                                                           
         B     PSEXT                                                            
         SPACE 2                                                                
OVFMSG1  DC    C'BUY RECORD IS TOO LARGE TO HOLD INVOICE DATA.'                 
OVFMSG2  DC    C'THE BUY LINE WILL HAVE TO BE SPLIT INTO TWO'                   
OVFMSG3  DC    C'RECORDS AND THIS MATCH WILL HAVE TO BE RERUN.'                 
OVFMSG4  DC    C'CONTACT DDS IF YOU HAVE ANY QUESTIONS.'                        
OVFMSG5  DC    C'INVOICE DATA HAS NOT BEEN INSERTED INTO THIS'                  
OVFMSG6  DC    C'BUY RECORD.      ---'                                          
         SPACE 2                                                                
BERMSG1  DC    C'BUY RECORD ERROR PREVENTS ADDING OF AFFIDAVIT'                 
BERMSG2  DC    C'DATA. PLEASE CONTACT DDS.'                                     
         SPACE 2                                                                
