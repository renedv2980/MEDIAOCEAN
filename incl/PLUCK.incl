*          DATA SET PLUCK      AT LEVEL 007 AS OF 07/17/75                      
         TITLE 'PLUCK - SELECTS JCL FROM JCLTAPE'                               
         CSECT PLUCK                                                            
         NBASE 0,**PLCK**,=V(REGSAVE)                                           
         SPACE 2                                                                
         OPENR TAPEIN                                                           
         SPACE 2                                                                
         COMRG                                                                  
*                                  UPSI 1 CARD INDICATES OUTPUT TO TAPE         
         MVC   TAPEBYTE,23(R1)                                                  
         TM    TAPEBYTE,X'80'                                                   
         BZ    GETINPUT                                                         
         SPACE 2                                                                
         OPENR TAPEOUT                                                          
         EJECT                                                                  
GETINPUT TM    TAPEBYTE,X'40'      TEST CARD INPUT                              
         BZ    READLOG                                                          
         SPACE 2                                                                
READCARD GOTO1 =V(CARDS),DMCB,CWORK,=C'RE00'                                    
         B     VERIFY                                                           
         SPACE 2                                                                
READLOG  GOTO1 =V(LOGIO),DMCB,1,(LMSG1,MSG1)                                    
*                                                                               
         GOTO1 =V(LOGIO),DMCB,0,(80,CWORK)                                      
*                                                                               
         EJECT                                                                  
VERIFY   CLC   CWORK(3),=C'EOJ'                                                 
         BE    DONE                                                             
         SPACE 2                                                                
         SR    R3,R3                                                            
         LA    R1,CWORK                                                         
CHECK    LA    R2,4                EACH SEQUENCE NUMBER HAS LENGTH OF 4         
CHECK1   CLI   0(R1),C'0'                                                       
         BL    CARDERR                                                          
         CLI   0(R1),C'9'                                                       
         BH    CARDERR                                                          
         LA    R1,1(R1)                                                         
         LA    R3,1(R3)                                                         
         BCT   R2,CHECK1                                                        
         CH    R3,=H'9'                                                         
         BE    SETUP                                                            
         CLI   0(R1),C'-'                                                       
         BNE   CARDERR                                                          
         LA    R1,1(R1)                                                         
         LA    R3,1(R3)                                                         
         B     CHECK                                                            
         EJECT                                                                  
SETUP    MVC   CSTART,CWORK                                                     
         MVC   CEND,CWORK+5                                                     
*                                                                               
SELECT   CLI   FRSTIME,1                                                        
         BNE   *+14                                                             
         MVC   CWORK,CSAVE                                                      
         B     SELECT2                                                          
         MVI   FRSTIME,1                                                        
SELECT1  GET   TAPEIN                                                           
SELECT2 CLC    CSEQ,CSTART                                                      
         BL    SELECT1                                                          
         CLC   CSEQ,CEND                                                        
         BNH   SELECT3                                                          
         MVC   CSAVE,CWORK                                                      
         B     GETINPUT                                                         
*                                                                               
SELECT3  TM    TAPEBYTE,X'80'                                                   
         BO    PUTTAPE                                                          
*                                                                               
         GOTO1 =V(CARDS),DMCB,CWORK,=C'PE00'                                    
         B     SELECT1                                                          
*                                                                               
PUTTAPE  PUT   TAPEOUT                                                          
         B     SELECT1                                                          
         EJECT                                                                  
CARDERR  GOTO1 =V(LOGIO),DMCB,1,(LMSG,MSG)                                      
         GOTO1 =V(LOGIO),DMCB,1,(80,CWORK)                                      
         B     GETINPUT                                                         
         EJECT                                                                  
DONE     CLOSER TAPEIN                                                          
         TM    TAPEBYTE,X'80'                                                   
         BZ    EOJ                                                              
         CLOSER TAPEOUT                                                         
         SPACE 1                                                                
EOJ      EOJ                                                                    
         EJECT                                                                  
TAPEBYTE DC    X'00'               WRITE TO TAPE(1) OR CARDS(0)                 
FRSTIME  DC    X'00'               READING 1ST CARD INDICATOR                   
DMCB     DS    4F                                                               
CSAVE    DS    CL80                                                             
CSTART   DS    CL4                                                              
CEND     DS    CL4                                                              
CWORK    DS    0CL80                                                            
         DS    CL76                                                             
CSEQ     DS    CL4                                                              
MSG      DC    C'*** PLUCK ***  ILLEGAL INPUT, TRY AGAIN'    L--'               
LMSG     DC    AL1(L'MSG)                                                       
MSG1     DC    C'** PLUCK ** ENTER SEQUENCE NOS. (NNNN-NNNN) OR EOJ'            
LMSG1    DC    AL1(L'MSG1)                                                      
         EJECT                                                                  
TAPEIN   DTFMT BLKSIZE=80,IOAREA1=CWORK,EOFADDR=DONE,DEVADDR=SYS001,   C        
               FILABL=NO                                                        
TAPEOUT  DTFMT BLKSIZE=80,IOAREA1=CWORK,TYPEFLE=OUTPUT,DEVADDR=SYS002, C        
               FILABL=NO                                                        
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'007PLUCK     07/17/75'                                      
         END                                                                    
