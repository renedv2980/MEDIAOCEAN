*          DATA SET RECLRFLD   AT LEVEL 001 AS OF 08/10/89                      
*                                                                               
*- RECLRFLD -- CLEAR UNPROTECTED FIELDS TO SPACES & TRANSMIT.                   
*                                                                               
*  (INCLUDE DATA SET)                                                           
*                                                                               
*  INPUT PARAMETERS:  (POINTED TO BY R1)                                        
*        P1 = A(1ST FIELD HEADER TO CLEAR)                                      
*        P2 = A(LAST FIELD HEADER TO CLEAR).                                    
*             0 = CLEAR TO END OF SCREEN                                        
*        P3-P6 MUST BE 0.                                                       
*                                                                               
RECLRFLD NTR1                                                                   
*                                                                               
*- P3 THRU P6 MUST BE 0                                                         
         LA    RF,8(R1)            P3                                           
         LA    R0,4                4 PARAMS TO CHECK                            
RCF020   CLC   =XL4'00',0(RF)                                                   
         BE    *+6                                                              
         DC    H'0'                P3-P6 NOT 0.  CALL ERROR.                    
         LA    RF,4(RF)                                                         
         BCT   R0,RCF020                                                        
*                                                                               
         L     R2,0(R1)            PICK UP 1ST FIELD TO CLEAR                   
         LTR   R2,R2                                                            
         BNZ   RCF050                                                           
         DC    H'0'                START FIELD IS 0.                            
*                                                                               
RCF050   L     R3,4(R1)            PICK UP LAST FIELD TO CLEAR                  
*                                                                               
*- MAIN LOOP                                                                    
*        1. TEST FOR PROTECTED FIELD.  SKIP IF PROTECTED                        
*        2. DETERMINE FIELD LENGTH.  HANDLE EXTENDED HEADERS.                   
*        3. CLEAR THE FIELD                                                     
*        4. SET TRANSMIT BIT                                                    
*        5. FIND NEXT FIELD. STOP IF END OF SCREEN FOUND.                       
*           STOP IF PAST LAST FIELD TO CLEAR.                                   
RCF100   EQU   *                                                                
         TM    1(R2),X'20'         PROTECTED?                                   
         BO    RCF500                                                           
*                                                                               
         ZIC   RF,0(R2)            ENTIRE FIELD LENGTH                          
         LA    R0,8                                                             
         SR    RF,R0               LESS 8 FOR HEADER                            
*                                                                               
         TM    1(R2),X'02'         EXTENDED HEADER?                             
         BNO   RCF120                                                           
         SR    RF,R0               LESS 8 FOR TRAILER                           
*                                                                               
RCF120   BCTR  RF,0                LESS 1 FOR THE EX                            
*                                                                               
         EX    RF,RCF900           CLEAR THE FIELD                              
         FOUT  (R2)                & TRANSMIT                                   
*                                                                               
RCF500   EQU   *                   FIND NEXT FIELD                              
         ZIC   RF,0(R2)                                                         
         AR    R2,RF                                                            
         CLI   0(R2),0                                                          
         BE    RCFEXIT             END OF SCREEN.  STOP.                        
         LTR   R3,R3               USER GAVE STOP ADDRESS?                      
         BZ    RCF100              NO.  GO TO END.                              
*                                                                               
         CLR   R2,R3                                                            
         BNH   RCF100              NOT AT END. DO NEXT.                         
*                                                                               
RCFEXIT  XIT1                                                                   
*                                                                               
RCF900   MVC   8(0,R2),RCFBLANK    BLANK OUT FIELD                              
*                                                                               
RCFBLANK DC    CL80' '             BIGGEST FIELD WE CAN BLANK                   
*                                                                               
********************* END OF INCLUDE DATA SET RECLRFLD ****************         
