*          DATA SET OP154      AT LEVEL 002 AS OF 05/01/02                      
         TITLE 'TAPE OWNER LISTING'                                             
TOL      CSECT                                                                  
R00      EQU   0                                                                
R01      EQU   1                                                                
R02      EQU   2                                                                
R03      EQU   3                                                                
R04      EQU   4                                                                
R05      EQU   5                                                                
R06      EQU   6                                                                
R07      EQU   7                                                                
R08      EQU   8                                                                
R09      EQU   9                                                                
R10      EQU   10                                                               
R11      EQU   11                                                               
R12      EQU   12                                                               
R13      EQU   13                                                               
R14      EQU   14                                                               
R15      EQU   15                                                               
         ENTRY START                                                            
*                                                                               
IN       DTFMT DEVADDR=SYS011,IOAREA1=T1,IOAREA2=T2,FILABL=STD,        X        
               EOFADDR=TEND,RECFORM=FIXBLK,TYPEFLE=INPUT,              X        
               BLKSIZE=800,RECSIZE=80,WORKA=YES                                 
*                                                                               
PRINT    DTFPR BLKSIZE=132,CONTROL=YES,DEVADDR=SYSLST,IOAREA1=P1,      X        
               PRINTOV=YES,RECFORM=FIXUNB,WORKA=YES                             
*                                                                               
T1       DS    800C                                                             
T2       DS    800C                                                             
P1       DS    132C                                                             
*                                                                               
START    BASR  R11,0                                                            
         USING *,R11                                                            
         OPEN  IN                                                               
         COMRG                                                                  
         MVC   DATE(2),0(R01)                                                   
         MVC   DATE+2(2),3(R01)    BUILD DATE                                   
         MVC   DATE+4(2),6(R01)                                                 
         XC    PL(132),PL                                                       
*                                                                               
*                                                                               
         OPEN  IN                                                               
*                                                                               
         BAS   R09,HDL             DO HEADLINE                                  
         LA    R02,SCNT            POINT TO CNTR AREA                           
         BAS   R10,RIN             GET A RECORD                                 
         MVC   0(2,R02),TRUN       SET SYSTEM NUMBER                            
         B     TL2                                                              
*                                                                               
TL1      BAS   R10,RIN                                                          
TL2      CLC   0(2,R02),TRUN       SAME SYSTEM?                                 
         BNE   TL3                 NO                                           
         LH    R14,4(R02)                                                       
         LA    R14,1(R14)          BUMP CNTR                                    
         STH   R14,4(R02)                                                       
         BAS   R10,FPRT            FORMAT PRINT LINE                            
         BAS   R09,PT              PRINT LINE                                   
         BAS   R09,SKIP1                                                        
         LH    R14,LCNT                                                         
         LA    R14,1(R14)          BUMP LINR CNY FOR SKIP                       
         STH   R14,LCNT                                                         
         B     TL1                                                              
*                                                                               
TL3      BAS   R09,SKIP2                                                        
         MVC   PL(5),=C'SAVED'                                                  
         LH    R14,4(R02)                                                       
         BAS   R10,CVD                                                          
         MVC   PL+9(5),CNFD+3                                                   
         BAS   R09,PT              PRINT TOTAL TAPES                            
         LH    R14,6(R02)                                                       
         BAS   R10,CVD                                                          
         MVC   PL+9(5),CNFD+3                                                   
         MVC   PL(5),=C'PURGE'                                                  
         BAS   R09,PT              PRINT PURGE                                  
         CLI   EOF,X'FF'           EOF?                                         
         BE    END                 YES                                          
         LA    R02,8(R02)          POINT TO NEX TCOUNTERS                       
         BAS   R09,HDL             HEADLINE                                     
         MVC   0(2,R02),TRUN       SET NEW SYSTEM                               
         B     TL2                 LOOP                                         
*                                                                               
*                                                                               
TEND     MVI   EOF,X'FF'           SET EOF                                      
         B     TL3                 GO PRINT SUMMARIES                           
*                                                                               
END      CLOSE IN                                                               
         BAS   R09,HDL                                                          
         LA    R02,SCNT                                                         
         MVC   PL+1(L'SHDL),SHDL                                                
         BAS   R09,PT                                                           
         BAS   R09,SKIP1                                                        
*                                                                               
END1     MVC   PL+3(2),0(R02)      SET SYSTEM                                   
         LH    R14,4(R02)                                                       
         BAS   R10,CVD                                                          
         MVC   PL+9(5),CNFD+3      MOVE SAVES                                   
         LH    R14,6(R02)                                                       
         BAS   R10,CVD                                                          
         MVC   PL+17(5),CNFD+3     MOVE PURGES                                  
         BAS   R09,PT              PRINT LINE                                   
         LA    R02,8(R02)          BUMP TO NEXT ENTRY                           
         CLC   0(2,R02),=X'0000'   END OF LIST                                  
         BNE   END1                NO, LOOP                                     
         EOJ                       YES. EOJ                                     
*                                                                               
SCNT     DC    200F'0'                                                          
SHDL     DC    C'SYSTEM  SAVES   PURGE'                                         
EOF      DC    X'00'                                                            
         EJECT                                                                  
*                                                                               
CVD      CVD   R14,PKFLD                                                        
         UNPK  CNFD(8),PKFLD(8)                                                 
         OI    CNFD+7,X'F0'                                                     
         BR    R10                                                              
PKFLD    DS    D                                                                
CNFD     DS    D                                                                
*                                                                               
PT       PUT   PRINT,PL                                                         
         XC    PL(132),PL                                                       
         LH    R14,LCNT                                                         
         LA    R14,1(R14)                                                       
         STH   R14,LCNT                                                         
         CLC   LCNT(2),=H'54'                                                   
         BCR   12,R09              RETURN                                       
HDL      CNTRL PRINT,SK,1          SKIP TO TOP                                  
         MVC   PL+3(4),=C'DATE'                                                 
         MVC   PL+8(2),DATE                                                     
         MVC   PL+11(2),DATE+2                                                  
         MVC   PL+14(2),DATE+4                                                  
         MVI   PL+10,C'/'                                                       
         MVI   PL+13,C'/'                                                       
         MVC   PL+55(25),=C'TAPE LIBRARY USER LISTING'                          
         LH    R14,PCNT                                                         
         LA    R14,1(R14)                                                       
         STH   R14,PCNT                                                         
         BAS   R10,CVD                                                          
         MVC   PL+121(4),=C'PAGE'                                               
         MVC   PL+126(3),CNFD+5                                                 
         PUT   PRINT,PL                                                         
         CNTRL PRINT,SP,2          SKIP 2 LINES                                 
         XC    PL(132),PL                                                       
         MVC   PL(L'PLS1),PLS1     FIRST PART OF HDL                            
         MVC   PL+17(L'PLS2),PLS2  SECOND PART                                  
         PUT   PRINT,PL                                                         
         CNTRL PRINT,SP,2                                                       
         MVC   LCNT(2),=X'0003'                                                 
         XC    PL(132),PL                                                       
         BR    R09                                                              
*                                                                               
LCNT     DC    H'0'                                                             
PCNT     DC    H'0'                                                             
*                                                                               
SKIP1    CNTRL PRINT,SP,1                                                       
         BR    R09                                                              
SKIP2    CNTRL PRINT,SP,2                                                       
         BR    R09                                                              
*                                                                               
RIN      GET   IN,TWRK                                                          
         L     R14,ICNT                                                         
         LA    R14,1(R14)                                                       
         ST    R14,ICNT                                                         
         BR    R10                                                              
*                                                                               
ICNT     DC    F'0'                INPUT CNTR                                   
DATE     DS    6C                                                               
TWRK     DS    0CL80                                                            
         EJECT                                                                  
*                                                                               
TNUM     DS    CL5                 REEL NUMBER                                  
TRUN     DS    CL7                 RUN NUMBER                                   
TDDAT    DS    CL6                 DATE OF DATA                                 
TRDTE    DS    CL6                 RUN DATE                                     
TNON     DS    CL4                 REEL OF REEL                                 
TDES     DS    CL29                DESCRIPTION                                  
TPDTE    DS    CL6                 PURGE DATE                                   
TFLE     DS    CL1                 OUTPUT FILE                                  
TDLACT   DS    CL6                 DATE LAST ACTIVITY                           
TDLIB    DS    CL6                 DATE INTO LIBRARY                            
TDENS    DS    CL4                 DENSITY                                      
*                                                                               
CWRK     DS    0CL80                                                            
CNUM     DS    CL5                 REEL NUMBER                                  
CRUN     DS    CL7                 RUN NUMBER                                   
CDDAT    DS    CL6                 DATE OF DATA                                 
CRDTE    DS    CL6                 RUN DATE                                     
CNON     DS    CL4                 REEL OF REEL                                 
CDES     DS    CL30                DESCRIPTION                                  
CPDTE    DS    CL6                 PURGE DATE                                   
CFLE     DS    CL1                 OUTPUT FILE                                  
CODE     DS    CL1                 TRANSACTION CODE                             
         DS    CL14                SPARE                                        
CNRLS    EQU   CRUN                NUMBER OF REELS                              
CDENS    EQU   CRUN+3              DENSITY                                      
*                                                                               
*                                                                               
PL       DS    132C                                                             
*                                                                               
         EJECT                                                                  
FPRT     LA    R08,PL              POINT TO PRINT LINE                          
         USING PLD,R08                                                          
         CLC   TRUN(5),=C'00000'     SCRATCH"                                   
         BC    8,FPRT1             YES,  NO TAG                                 
         MVC   CNFD+2(4),DATE      MOVE MMDD                                    
         MVC   CNFD(2),DATE+4      MOVE YEAR                                    
         MVC   PKFLD+2(4),TPDTE    MOVE MMDD                                    
         MVC   PKFLD(2),TPDTE+4    MOVE YY                                      
         CLC   PKFLD(6),CNFD       TEST OT TAG TO PURGE                         
         BC    2,FPRT1             NO                                           
         LH    R14,6(R02)                                                       
         LA    R14,1(R14)          BUMP PURGE CNTR                              
         STH   R14,6(R02)                                                       
         MVI   PI,C'*'             FLAG IT                                      
FPRT1    MVC   OWNER,TRUN                                                       
         MVC   REEL,TNUM                                                        
         MVC   FILE,TFLE                                                        
         MVC   ROR(2),TNON                                                      
         MVC   ROR+3(2),TNON+2                                                  
         CLC   ROR(2),=C'  '                                                    
         BE    *+8                                                              
         MVI   ROR+2,C'/'                                                       
         MVC   DESC,TDES                                                        
         MVC   DENS,TDENS                                                       
         LA    R14,RDATE                                                        
         LA    R15,TRDTE                                                        
         BAS   R09,DMVE            MOVE DATE TO PRINT LINE                      
         LA    R14,DDATE                                                        
         LA    R15,TDDAT                                                        
         BAS   R09,DMVE            MOVE DATE TO PRINT LINE                      
         LA    R14,LACT                                                         
         LA    R15,TDLACT                                                       
         BAS   R09,DMVE            MOVE DATE TO PRINT LINE                      
         LA    R14,TDATE                                                        
         LA    R15,TDLIB                                                        
         BAS   R09,DMVE            MOVE DATE TO PRINT LINE                      
         LA    R14,PDATE                                                        
         LA    R15,TPDTE                                                        
         BAS   R09,DMVE            MOVE DATE TO PRINT LINE                      
         BR R10                                                                 
*                                                                               
DMVE     CLI   0(R15),C' '         BLANJ DATE                                   
          BCR   8,R09                                                           
         MVC   0(2,R14),0(R15)                                                  
         MVC   3(2,R14),2(R15)                                                  
         MVC   6(2,R14),4(R15)                                                  
         MVI   2(R14),C'/'                                                      
         MVI   5(R14),C'/'                                                      
         BR    R09                                                              
*                                                                               
PLS1     DC    C'  OWNER    REEL'                                               
PLS2     DC    C'FILE  R/R  RUN DATE   DESCRIPTION                     X        
               PURGE DATE  DATA DATE  LAST ACT             TAPE DATE   X        
               DENS'                                                            
*                                                                               
PLD      DSECT                                                                  
PI       DS    CL1                                                              
         DS    CL1                                                              
OWNER    DS    CL7                                                              
         DS    CL2                                                              
REEL     DS    CL5                                                              
         DS    CL2                                                              
FILE     DS    CL1                                                              
         DS    CL2                                                              
ROR      DS    CL5                                                              
         DS    CL2                                                              
RDATE    DS    CL8                                                              
         DS    CL3                                                              
DESC     DS    CL30                                                             
         DS    CL3                                                              
PDATE    DS    CL8                                                              
         DS    CL3                                                              
DDATE    DS    CL8                                                              
         DS    CL3                                                              
LACT     DS    CL8                                                              
         DS    CL14                                                             
TDATE    DS    CL8                                                              
         DS    CL3                                                              
DENS     DS    CL4                                                              
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002OP154     05/01/02'                                      
         END                                                                    
