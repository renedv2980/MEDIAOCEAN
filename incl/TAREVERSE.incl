*          DATA SET TAREVERSE  AT LEVEL 015 AS OF 07/11/14                      
***********************************************************************         
*        REVERSE ALL AMOUNTS IN INVOICE OR CHECK RECORD               *         
*        INCLUDED BY TAGEN00 AND TALNK1A                              *         
*        ON ENTRY ... R3 = A(INVOICE OR CHECK RECORD)                 *         
***********************************************************************         
                                                                                
         XR    R4,R4                                                            
                                                                                
         USING TLRCD,R3                                                         
         LA    R3,TLRCELEM                                                      
         B     REV20                                                            
         DROP  R3                                                               
                                                                                
REV10    ZIC   RE,1(R3)                                                         
         AR    R3,RE                                                            
                                                                                
REV20    CLI   0(R3),0                                                          
         BE    REV70                                                            
                                                                                
         CLI   0(R3),TAPDELQ       IF THIS IS PAYMENT DETAILS EL.               
         BNE   *+6                                                              
         LR    R2,R3               SAVE ITS ADDRESS                             
         CLI   0(R3),TASDELQ       IF THIS IS SESSION DETAILS EL.               
         BNE   *+6                                                              
         LR    R4,R3               SAVE ITS ADDRESS                             
                                                                                
         LA    RF,REVTAB           RF=A(TABLE)                                  
REV30    CLC   0(1,R3),0(RF)       MATCH ON ELEMENT CODE                        
         BNE   REV65                                                            
                                                                                
         CLI   0(R3),TABDELQ                                                    
         BNE   REV35                                                            
         CLI   1(RF),TABDACOM-TABDD       AGENCY COMMISSION ONLY                
         BNE   REV60                                                            
         CLI   1(RF),TABDSIGN-TABDD       AGENCY COMMISSION ONLY                
         BNE   REV60                                                            
         CLI   1(R3),TABDLN2Q      MAKE SURE ELEMENT HAS IT                     
         BL    REV65                                                            
         B     REV60                                                            
                                                                                
REV35    CLI   0(R3),TAPDELQ                                                    
         BNE   REV40                                                            
         CLI   1(RF),TAPDTXNW-TAPDD    TAXABLE NON WAGES                        
         BE    REV38                                                            
         CLI   1(RF),TAPDNTNW-TAPDD    NON TAXABLE NON WAGES                    
         BNE   REV60                                                            
REV38    CLI   1(R3),TAPDLNQ       MAKE SURE ELEMENT HAS IT                     
         BL    REV65                                                            
         B     REV60                                                            
                                                                                
REV40    CLI   0(R3),TANUELQ                                                    
         BNE   REV45                                                            
         CLI   2(R3),TANUTPAC     P+ AGENCY COMMISSION?                         
         BNE   REV65                                                            
         B     REV60                                                            
                                                                                
REV45    CLI   0(R3),TACWELQ                                                    
         BNE   REV50                                                            
         CLI   1(RF),TACWSFLI-TACWD      FAMILY LEAVE INSURANCE ONLY            
         BNE   *+12                                                             
         CLI   1(R3),TACWLN2Q      HAS TO BE NEW LENGTH FOR FLI                 
         BL    REV65                                                            
                                                                                
         CLI   1(RF),TACWPFTX-TACWD                                             
         BE    *+12                                                             
         CLI   1(RF),TACWPFFI-TACWD                                             
         BNE   REV46                                                            
         CLI   1(R3),TACWLN3Q                                                   
         BL    REV65                                                            
         B     REV60                                                            
                                                                                
REV46    CLI   1(RF),TACWPSTX-TACWD                                             
         BE    REV47                                                            
         CLI   1(RF),TACWPSDI-TACWD                                             
         BE    REV47                                                            
         CLI   1(RF),TACWPSUI-TACWD                                             
         BE    REV47                                                            
         CLI   1(RF),TACWPFLI-TACWD                                             
         BNE   REV60                                                            
REV47    CLI   1(R3),TACWLN4Q                                                   
         BL    REV65                                                            
         B     REV60                                                            
                                                                                
REV50    CLI   0(R3),TAATELQ                                                    
         BNE   REV60                                                            
         CLI   1(RF),TAATCTAX-TAATD                                             
         BE    REV55                                                            
         CLI   1(RF),TAATCPP-TAATD                                              
         BE    REV55                                                            
         CLI   1(RF),TAATCEI-TAATD                                              
         BE    REV55                                                            
         CLI   1(RF),TAATCPIP-TAATD                                             
         BNE   REV60                                                            
REV55    CLI   1(R3),TAATLN2Q                                                   
         BL    REV65                                                            
         B     REV60                                                            
                                                                                
REV60    ZIC   R1,1(RF)            DISPLACEMENT TO FIELD                        
         AR    R1,R3                                                            
         ICM   RE,15,0(R1)         ASSUME 4-BYTE BINARY AMOUNT                  
         BZ    REV65               DON'T BOTHER IF ZERO                         
         LCR   RE,RE               COMPLEMENT AMOUNT                            
         ST    RE,0(R1)            AND RESTORE IT TO ELEMENT                    
                                                                                
REV65    LA    RF,L'REVTAB(RF)     BUMP TO NEXT TABLE ENTRY                     
         CLI   0(RF),X'FF'         END OF TABLE                                 
         BE    REV10               GET NEXT ELEMENT                             
         B     REV30                                                            
                                                                                
REV70    LTR   R3,R4               WAS THERE A SESSION DETAILS EL.              
         BZ    REVX                                                             
         USING TASDD,R3            R3=A(SESSION DETAILS EL.)                    
         USING TAPDD,R2            R2=A(PAYMENT DETAILS EL.)                    
         LA    RF,REVTASD                                                       
REV80    CLC   TAPDUSE,0(RF)       MATCH ON USE TYPE                            
         BNE   REV90                                                            
         ZIC   R1,1(RF)            DISPLACEMENT TO FIELD                        
         AR    R1,R3                                                            
         ICM   RE,15,0(R1)         ASSUME 4-BYTE BINARY AMOUNT                  
         BZ    REV90               DON'T BOTHER IF ZERO                         
         LCR   RE,RE               COMPLEMENT AMOUNT                            
         ST    RE,0(R1)            AND RESTORE IT TO ELEMENT                    
                                                                                
REV90    LA    RF,L'REVTASD(RF)    BUMP TO NEXT TABLE ENTRY                     
         CLI   0(RF),X'FF'         END OF TABLE                                 
         BNE   REV80                                                            
         B     REVX                                                             
                                                                                
***********************************************************************         
*        TABLE OF INVOICE OR CHECK ELEMENTS TO REVERSE                *         
***********************************************************************         
                                                                                
REVTAB   DS    0CL2                        +0=EL CODE, +1=DISP TO FLD           
         DC    AL1(TAPDELQ,TAPDGRS-TAPDD)  PAYMENT DETAILS EL.                  
         DC    AL1(TAPDELQ,TAPDAPPL-TAPDD)                                      
         DC    AL1(TAPDELQ,TAPDGUAR-TAPDD)                                      
         DC    AL1(TAPDELQ,TAPDPAYI-TAPDD)                                      
         DC    AL1(TAPDELQ,TAPDPAYC-TAPDD)                                      
         DC    AL1(TAPDELQ,TAPDREXP-TAPDD)                                      
         DC    AL1(TAPDELQ,TAPDSPNH-TAPDD)                                      
         DC    AL1(TAPDELQ,TAPDMDED-TAPDD)                                      
         DC    AL1(TAPDELQ,TAPDPNH-TAPDD)                                       
         DC    AL1(TAPDELQ,TAPDINR-TAPDD)                                       
         DC    AL1(TAPDELQ,TAPDHNW-TAPDD)                                       
         DC    AL1(TAPDELQ,TAPDDUES-TAPDD)                                      
         DC    AL1(TAPDELQ,TAPDTXNW-TAPDD)                                      
         DC    AL1(TAPDELQ,TAPDNTNW-TAPDD)                                      
         DC    AL1(TAPDELQ,TAPDGCON-TAPDD)                                      
                                                                                
         DC    AL1(TABDELQ,TABDTOT-TABDD)  BILLING DETAILS EL.                  
         DC    AL1(TABDELQ,TABDTAX-TABDD)                                       
         DC    AL1(TABDELQ,TABDHND-TABDD)                                       
         DC    AL1(TABDELQ,TABDHNDC-TABDD)                                      
         DC    AL1(TABDELQ,TABDCSF-TABDD)                                       
         DC    AL1(TABDELQ,TABDFICR-TABDD)                                      
         DC    AL1(TABDELQ,TABDGST-TABDD)                                       
         DC    AL1(TABDELQ,TABDPST-TABDD)                                       
         DC    AL1(TABDELQ,TABDACOM-TABDD)                                      
         DC    AL1(TABDELQ,TABDSIGN-TABDD)                                      
                                                                                
         DC    AL1(TABDELQ2,TABDTOT-TABDD)  EMS/CHLOE BILLING EL.               
         DC    AL1(TABDELQ2,TABDTAX-TABDD)                                      
         DC    AL1(TABDELQ2,TABDHND-TABDD)                                      
         DC    AL1(TABDELQ2,TABDHNDC-TABDD)                                     
         DC    AL1(TABDELQ2,TABDCSF-TABDD)                                      
         DC    AL1(TABDELQ2,TABDFICR-TABDD)                                     
         DC    AL1(TABDELQ2,TABDGST-TABDD)                                      
         DC    AL1(TABDELQ2,TABDPST-TABDD)                                      
         DC    AL1(TABDELQ2,TABDACOM-TABDD)                                     
         DC    AL1(TABDELQ2,TABDSIGN-TABDD)                                     
                                                                                
         DC    AL1(TASDELQ,TASDFEE-TASDD)  SESSION DETAILS EL.                  
         DC    AL1(TASDELQ,TASDHR-TASDD)                                        
                                                                                
         DC    AL1(TACDELQ,TACDEARN-TACDD) CHECK DETAILS EL.                    
         DC    AL1(TACDELQ,TACDNTAX-TACDD)                                      
         DC    AL1(TACDELQ,TACDNET-TACDD)                                       
                                                                                
         DC    AL1(TACXELQ,TACXGST-TACXD)  CHECK EXTRA DETAILS EL.              
         DC    AL1(TACXELQ,TACXPST-TACXD)  CHECK EXTRA DETAILS EL.              
                                                                                
         DC    AL1(TACWELQ,TACWTAX-TACWD)  CHECK WITHHOLDING EL.                
         DC    AL1(TACWELQ,TACWFICA-TACWD)                                      
         DC    AL1(TACWELQ,TACWSUI-TACWD)                                       
         DC    AL1(TACWELQ,TACWSFLI-TACWD) CODE HAS TO CHECK LENGTH             
                                                                                
         DC    AL1(TACWELQ,TACWPFTX-TACWD) CODE HAS TO CHECK LENGTH             
         DC    AL1(TACWELQ,TACWPFFI-TACWD) CODE HAS TO CHECK LENGTH             
         DC    AL1(TACWELQ,TACWPSTX-TACWD) CODE HAS TO CHECK LENGTH             
         DC    AL1(TACWELQ,TACWPSDI-TACWD) CODE HAS TO CHECK LENGTH             
         DC    AL1(TACWELQ,TACWPSUI-TACWD) CODE HAS TO CHECK LENGTH             
         DC    AL1(TACWELQ,TACWPFLI-TACWD) CODE HAS TO CHECK LENGTH             
                                                                                
         DC    AL1(TADWELQ,TADWREC-TADWD)  DUECOMP WITHHOLDING EL.              
                                                                                
         DC    AL1(TALWELQ,TALWREC-TALWD)  LIEN WITHHOLDING EL.                 
                                                                                
         DC    AL1(TAODELQ,TAODAMT-TAODD)  OTHER DEDUCTIONS EL.                 
                                                                                
         DC    AL1(TATUELQ,TATUWAGE-TATUD) TAX UNIT EL.                         
         DC    AL1(TATUELQ,TATUTNWA-TATUD)                                      
         DC    AL1(TATUELQ,TATUNNWA-TATUD)                                      
         DC    AL1(TATUELQ,TATUSTRE-TATUD)                                      
         DC    AL1(TATUELQ,TATUWAAD-TATUD)                                      
         DC    AL1(TATUELQ,TATUTNAD-TATUD)                                      
         DC    AL1(TATUELQ,TATUNNAD-TATUD)                                      
                                                                                
         DC    AL1(TAATELQ,TAATTAX-TAATD)  CANADIAN TAX EL.                     
         DC    AL1(TAATELQ,TAATPP-TAATD)                                        
         DC    AL1(TAATELQ,TAATEI-TAATD)                                        
         DC    AL1(TAATELQ,TAATPIP-TAATD)                                       
         DC    AL1(TAATELQ,TAATCTAX-TAATD)                                      
         DC    AL1(TAATELQ,TAATCPP-TAATD)                                       
         DC    AL1(TAATELQ,TAATCEI-TAATD)                                       
         DC    AL1(TAATELQ,TAATCPIP-TAATD)                                      
                                                                                
         DC    AL1(TAPGELQ,TAPGHNDI-TAPGD)  POSTING DETAILS EL.                 
         DC    AL1(TAPGELQ,TAPGHPRI-TAPGD)                                      
         DC    AL1(TAPGELQ,TAPGHNDC-TAPGD)                                      
         DC    AL1(TAPGELQ,TAPGHPRC-TAPGD)                                      
         DC    AL1(TAPGELQ,TAPGHIAG-TAPGD)                                      
         DC    AL1(TAPGELQ,TAPGHIAP-TAPGD)                                      
         DC    AL1(TAPGELQ,TAPGHCAG-TAPGD)                                      
         DC    AL1(TAPGELQ,TAPGHCAP-TAPGD)                                      
         DC    AL1(TAPGELQ,TAPGFUTA-TAPGD)                                      
         DC    AL1(TAPGELQ,TAPGSUTA-TAPGD)                                      
         DC    AL1(TAPGELQ,TAPGFICA-TAPGD)                                      
         DC    AL1(TAPGELQ,TAPGMED-TAPGD)                                       
         DC    AL1(TAPGELQ,TAPGWCN-TAPGD)                                       
         DC    AL1(TAPGELQ,TAPGWCC-TAPGD)                                       
         DC    AL1(TAPGELQ,TAPGWFEE-TAPGD)                                      
         DC    AL1(TAPGELQ,TAPGCGST-TAPGD)                                      
         DC    AL1(TAPGELQ,TAPGUGST-TAPGD)                                      
                                                                                
         DC    AL1(TANUELQ,TANUMBER-TANUD) P+ AGENCY COMMISSION                 
                                                                                
         DC    AL1(TAXTELQ,TAXTTAXS-TAXTD) EXTENDED TAXES                       
         DC    X'FF'                                                            
                                                                                
REVTASD  DS    0CL2                SPECIAL TABLE FOR SESSION DETAILS EL         
         DC    AL1(UBSS,TASDSPA-TASDD)                                          
         DC    AL1(UBSS,TASDDAA-TASDD)                                          
         DC    AL1(UBSS,TASDOTA-TASDD)                                          
         DC    AL1(UBSS,TASDDTA-TASDD)                                          
         DC    AL1(UBSS,TASDTRA-TASDD)                                          
         DC    AL1(UBSS,TASDPDA-TASDD)                                          
                                                                                
         DC    AL1(UBSM,TASDMSPA-TASDD)                                         
         DC    AL1(UBSM,TASDMHMA-TASDD)                                         
                                                                                
         DC    AL1(UIMS,TASDMSPA-TASDD)                                         
         DC    AL1(UIMS,TASDMHMA-TASDD)                                         
                                                                                
         DC    AL1(UBSR,TASDRSPA-TASDD)                                         
         DC    AL1(UBSR,TASDRHMA-TASDD)                                         
         DC    AL1(UBSR,TASDRTGA-TASDD)                                         
         DC    X'FF'                                                            
