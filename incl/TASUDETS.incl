*          DATA SET TASUDETS   AT LEVEL 007 AS OF 05/17/15                      
***********************************************************************         
*        ROUTINE TO SET PRINTABLE USE DETAILS INTO MYBLOCK            *         
*        ON ENTRY ... P1=A(INVOICE RECORD)                            *         
***********************************************************************         
                                                                                
STUDETS  NTR1  BASE=*,LABEL=*                                                   
         L     RF,0(R1)            R2=A(INVOICE RECORD)                         
         ST    RF,ASTUINVR                                                      
*                                                                               
         USING TLIND,RF                                                         
         LA    R4,TLINELEM                                                      
SUD10    CLI   0(R4),0                                                          
         JNE   *+6                                                              
         DC    H'00'                                                            
         CLI   0(R4),TAPDELQ                                                    
         JE    SUD20                                                            
         ZIC   RF,1(R4)                                                         
         AR    R4,RF                                                            
         J     SUD10                                                            
*                                                                               
         USING TAPDD,R4                                                         
SUD20    MVC   MYBLOCK,SPACES      INITIALIZE DISPLAY BLOCK                     
*                                                                               
         MVI   STUCYCST,0                                                       
         CLC   TAPDCYCS+3(3),=X'A11001'                                         
         JL    *+8                                                              
         MVI   STUCYCST,C'Y'                                                    
*                                                                               
* VALIDATE USE                                                                  
*                                                                               
         USING USETABD,R2                                                       
         L     R2,TGAUSES          R2=A(USE TABLE)                              
SUD30    CLI   0(R2),X'FF'         TEST END OF TABLE                            
         JE    SUDEXIT                                                          
         CLC   USECDE,TAPDUSE      ELSE LOOK UP BY CODE                         
         JE    SUD40                                                            
         LH    RE,USELEN           BUMP TO NEXT USE ENTRY                       
         AR    R2,RE                                                            
         J     SUD30                                                            
*                                                                               
SUD40    MVC   TGUSCDE,USECDE      MOVE USE CODE TO GLOBAL STORAGE              
         MVC   TGUSEQU,USEEQU      EQUATE                                       
         MVC   TGUSMEDS,USEMEDS    VALID MEDIA FOR USE                          
         MVC   TGUSSTAT,USESTAT    USE STATUS                                   
         MVC   TGUSSTA2,USESTAT2   2ND USE STATUS                               
         MVC   TGUSSTA3,USESTAT3   3RD USE STATUS                               
         MVC   TGUSSTA4,USESTAT4   4TH USE STATUS                               
         MVC   TGUSCYCK,USECYCHK   CYCLE CHECKING BITS                          
         MVC   TGUSXCAT,USEEXCAT   CATEGORY EXCEPTIONS                          
         MVC   TGUSXUNI,USEEXUNI   UNION EXCEPTIONS                             
         MVC   TGUSXUNS,USEEXUN1   EXPANDED UNION EXCEPTIONS                    
         MVC   TGUSNAME,SPACES                                                  
         XR    RE,RE                                                            
         IC    RE,USEDSP           DISPLACEMENT TO SUB-ENTRIES                  
         SHI   RE,USENAME-USETABD+1  LESS DISP. TO NAME + 1                     
         EX    RE,*+8              IS L'NAME - 1                                
         J     *+10                                                             
         MVC   TGUSNAME(0),USENAME NAME                                         
*                                                                               
         MVC   MYBLOCK(L'TGUSNAME),TGUSNAME                                     
*                                                                               
* VALIDATE USE TYPE                                                             
*                                                                               
         XC    TGUSTYPD,TGUSTYPD   CLEAR USE TYPE INFO                          
         IC    RE,USEDSP           NOW BUMP TO FIRST SUB-ENTRY                  
         AR    R2,RE                                                            
         USING USESUBD,R2          R2=A(USE TYPE ENTRIES)                       
SUD50    CLC   USETYPE,TAPDTYPE    ELSE LOOK UP BY EQUATE                       
         JE    SUD60                                                            
         IC    RE,USESBLN          BUMP TO NEXT SUB-ENTRY                       
         AR    R2,RE                                                            
         CLI   0(R2),0             TEST END OF SUB-ENTRIES                      
         JNE   SUD50                                                            
*                                                                               
* TEST RADIO GUARANTEES                                                         
*                                                                               
         CLI   TGUSEQU,UGRR        IF USE IS GRR                                
         JNE   SUD70                                                            
*                                                                               
         USING USETABD,R2                                                       
         L     R2,TGAUSES          R2=A(USE TABLE)                              
SUD52    CLI   0(R2),X'FF'         IF AT END OF TABLE, ERROR                    
         JE    SUDEXIT                                                          
         CLC   USEEQU,TAPDTYPE     TEST IF TYPE EXISTS AS USE                   
         JE    SUD54                                                            
         LH    RE,USELEN           BUMP TO NEXT USE ENTRY                       
         AR    R2,RE                                                            
         J     SUD52                                                            
*                                                                               
SUD54    TM    USEMEDS,RADIO       TYPE MUST BE VALID FOR RADIO                 
         JZ    SUD70                                                            
         MVC   TGUSNAME,=CL16'RADIO GRT ('                                      
         MVC   TGUSNAME+11(3),USECDE                                            
         MVI   TGUSNAME+14,C')'                                                 
         J     SUD70                                                            
*                                                                               
         USING USESUBD,R2          R2=A(USE TYPE ENTRIES)                       
SUD60    MVC   TGUSTYP,USETYPE     SAVE USE TYPE EQUATE IN GLOBAL STOR.         
         MVC   TGUSTYMD,USETYMED   VALID MEDIA FOR TYPE                         
         MVC   TGUSWKS,USEWKS      N'WEEKS IN CYCLE (X'80'==>MTHS)              
         MVC   TGUSTYST,USETYST    TYPE STATUS                                  
         MVC   TGUSTYCD,USETYCDE   TYPE CODE                                    
*                                                                               
         IC    RE,USESBLN          L'SUB-ENTRY                                  
         SHI   RE,USETYNME-USESUBD+1 LESS DISP. TO NAME + 1                     
         JM    SUD70               = L'NAME - 1                                 
*                                                                               
         LA    R1,L'TGUSNAME-1     DETERMINE L'STORAGE AVAILABLE                
         LA    RF,TGUSNAME(R1)     FIND END OF USE NAME                         
         CLI   0(RF),C' '                                                       
         JH    *+10                                                             
         BCTR  RF,0                                                             
         BCT   R1,*-10                                                          
*                                                                               
         LA    R0,L'TGUSNAME-3     DETERMINE IF ENOUGH ROOM FOR TYPE            
         SR    R0,R1                                                            
         JM    SUDEXIT                                                          
         CR    RE,R0               IF L'NAME IS GT L'STORAGE                    
         JNH   *+6                                                              
         LR    RE,R0               MOVE ONLY FOR L'STORAGE                      
         EX    RE,*+8                                                           
         J     *+10                                                             
         MVC   2(0,RF),USETYNME    TYPE NAME                                    
*                                                                               
         MVC   MYBLOCK(L'TGUSNAME),TGUSNAME                                     
*                                                                               
SUD70    OC    TAPDAREA(6),TAPDAREA    IF HAVE PRINT AREA AND USE               
         JZ    SUD80                                                            
         MVC   MYBLOCK+17(3),TAPDAREA  DISPLAY IT                               
         MVC   MYBLOCK+21(3),TAPDPUSE                                           
         J     SUD180                                                           
*                                                                               
SUD80    CLI   TGUSEQU,UITN                                                     
         JE    SUD140                                                           
         TM    TGUSTYST,USES                                                    
         JO    SUD160              BRANCH IF USES REQUIRED                      
         TM    TGUSTYST,MAJORS                                                  
         JO    SUD90               BRANCH IF MAJORS REQUIRED                    
         TM    TGUSTYST,UNITS                                                   
         JO    SUD100              BRANCH IF UNITS REQUIRED                     
         TM    TGUSTYST,INSERTS                                                 
         JO    SUD110              BRANCH IF INSERTS REQUIRED                   
         CLI   TGUSEQU,UTAG                                                     
         JE    SUD120              BRANCH IF TAG PAYMENT                        
         CLI   TGUSEQU,UDEM                                                     
         JE    SUD130              BRANCH IF DEM PAYMENT                        
         CLI   TGUSEQU,USNA                                                     
         JE    SUD130              BRANCH IF SNA PAYMENT                        
         CLI   TGUSEQU,UCDM                                                     
         JE    SUD130              BRANCH IF CANDIAN DEMO PAYMENT               
         J     SUD180              ELSE GET OUT                                 
*                                                                               
SUD90    GOTO1 SUDMAJV,DMCB,(X'80',TAPDMAJ)                                     
         JNE   SUD100                                                           
         MVC   MYBLOCK+17(L'TGMACHAR),TGMACHAR                                  
*                                                                               
SUD100   OC    TAPDUNIT,TAPDUNIT IF THERE ARE UNITS                             
         JZ    SUD180                                                           
         LA    RF,MYBLOCK+17+L'TGMACHAR-1 FIND END OF MAJORS                    
         CLI   0(RF),X'40'                                                      
         JH    *+8                                                              
         BCT   RF,*-8                                                           
         EDIT  TAPDUNIT,(5,2(RF)),ALIGN=LEFT NUMBER OF UNITS                    
         J     SUD180                                                           
*                                                                               
SUD110   OC    TAPDINS,TAPDINS   IF THERE ARE INSERTS                           
         JZ    SUD180                                                           
         LA    RF,MYBLOCK+17                                                    
         EDIT  TAPDINS,(5,2(RF)),ALIGN=LEFT NUMBER OF INSERTS                   
         J     SUD180                                                           
*                                                                               
SUD120   OC    TAPDTAGS,TAPDTAGS IF THERE ARE TAGS                              
         JZ    SUD180                                                           
         LA    RF,MYBLOCK+17                                                    
         EDIT  TAPDTAGS,(3,2(RF)),ALIGN=LEFT NUMBER OF TAGS                     
         J     SUD180                                                           
*                                                                               
SUD130   OC    TAPDDEMS,TAPDDEMS IF THERE ARE DEMOS                             
         JZ    SUD180                                                           
         LA    RF,MYBLOCK+17                                                    
         EDIT  TAPDDEMS,(3,2(RF)),ALIGN=LEFT NUMBER OF DEMOS                    
         J     SUD180                                                           
*                                                                               
SUD140   MVC   MYBLOCK+12(4),=C'AUTO'                                           
*                                                                               
         USING TLIND,RF                                                         
         L     RF,ASTUINVR                                                      
         LA    R4,TLINELEM                                                      
SUD142   CLI   0(R4),0                                                          
         JE    SUD180                                                           
         CLI   0(R4),TAUHELQ                                                    
         JE    SUD144                                                           
         ZIC   RF,1(R4)                                                         
         AR    R4,RF                                                            
         J     SUD142                                                           
*                                                                               
         USING TAUHD,R4                                                         
SUD144   LA    R3,MYBLOCK+12        OTHERWISE PRINT OUT USE #'S                 
         EDIT  TAUHIFUS,(3,(R3)),ZERO=NOBLANK,ALIGN=LEFT                        
SUD150   CLI   0(R3),C' '                                                       
         JE    *+12                                                             
         LA    R3,1(R3)                                                         
         J     SUD150                                                           
         MVI   0(R3),C'-'                                                       
         LH    RE,TAUHIFUS                                                      
         AH    RE,TAUHINUS                                                      
         SHI   RE,1                                                             
         EDIT  (RE),(3,1(R3)),ZERO=NOBLANK,ALIGN=LEFT                           
         J     SUD180                                                           
*                                                                               
         USING TLIND,RF                                                         
SUD160   L     RF,ASTUINVR                                                      
         LA    R4,TLINELEM                                                      
SUD162   CLI   0(R4),0                                                          
         JE    SUD180                                                           
         CLI   0(R4),TANDELQ                                                    
         JE    SUD164                                                           
         ZIC   RF,1(R4)                                                         
         AR    R4,RF                                                            
         J     SUD162                                                           
*                                                                               
         USING TANDD,R4                                                         
SUD164   SR    R3,R3                                                            
         ICM   R3,3,TANDSTUS       FIND TOTAL START USE NUMBER                  
         JNZ   *+8                                                              
         AHI   R3,1                + 1 IF 0 FROM CONVERSION                     
         SR    R1,R1                                                            
         ICM   R1,3,TANDSTUL                                                    
         JNZ   *+8                                                              
         AHI   R1,1                                                             
         AR    R3,R1                                                            
         BCTR  R3,0                R3=TOTAL START USE NUMBER                    
         LA    R1,MYBLOCK+21                                                    
         CLI   TGUSEQU,UCLA                                                     
         JNE   SUD170                                                           
         CLI   STUCYCST,C'Y'                                                    
         JNE   SUD170                                                           
         MVC   MYBLOCK+3(12),SPACES                                             
         LA    R1,MYBLOCK+9                                                     
SUD170   EDIT  (R3),(5,(R1)),ALIGN=LEFT                                         
         LA    RF,MYBLOCK+25        FIND END OF START USE NUMBER                
         CLI   0(RF),C' '                                                       
         JH    *+8                                                              
         BCT   RF,*-8                                                           
         MVI   1(RF),C'-'                                                       
         AH    R3,TANDUSES         FIND TOTAL END USE NUMBER                    
         AH    R3,TANDUSEL                                                      
         BCTR  R3,0                                                             
         EDIT  (R3),(5,2(RF)),ALIGN=LEFT                                        
*                                                                               
         OC    TANDUSEL,TANDUSEL                 IF THERE ARE LIFT USES         
         JZ    SUD180                                                           
         MVI   MYBLOCK+10+17,C'L'     SHOW LIFT USES                            
         LA    R1,MYBLOCK+11+17                                                 
         EDIT  (2,TANDSTUL),(5,(R1)),ALIGN=LEFT  LIFT START USE NUMBER          
         LA    RF,MYBLOCK+11+4+17  FIND END OF LIFT START USE NUMBER            
         CLI   0(RF),X'40'                                                      
         JH    *+8                                                              
         BCT   RF,*-8                                                           
         MVI   1(RF),C'-'                                                       
         LH    R3,TANDSTUL         FIND LIFT END USE NUMBER                     
         AH    R3,TANDUSEL                                                      
         BCTR  R3,0                                                             
         EDIT  (R3),(5,2(RF)),ALIGN=LEFT                                        
*                                                                               
SUD180   GOTO1 SQUASHER,DMCB,MYBLOCK,L'MYBLOCK SQUASH IT                        
         J     SUDEXIT                                                          
         DROP  R4,RF                                                            
*                                                                               
SUDYES   SR    RC,RC                                                            
SUDNO    LTR   RC,RC                                                            
SUDEXIT  XIT1                                                                   
*                                                                               
*              ROUTINE TO VALIDATE A MAJOR CODE                                 
*                                                                               
*                                  P1 = A(9-BYTE WORK AREA) OR A(EQU)           
*                                  P1 BYTE 0  X'80' = P1 IS EQUATE              
         USING MAJTABD,R3                                                       
SUDMAJV  NTR1                                                                   
         L     R2,0(R1)            R2 = A(WORK AREA OR EQUATE)                  
         L     R3,TGAMAJS          R3 = A(MAJORS TABLE)                         
*                                                                               
         CLI   TGUSEQU,UFGR        IF FGR USE (ASSUME NEW MAJORS TYPE)          
         JNE   SUDMAJ10                                                         
         TM    0(R1),X'80'         AND TEST FOR EQUATE                          
         JZ    SUDLOCV                                                          
         L     R3,TGALOCS          R3 = A(LOCATIONS TABLE)                      
*                                                                               
SUDMAJ10 CLI   0(R3),X'FF'         IF END OF TABLE                              
         JE    SUDNO               THEN RETURN NEQ                              
*                                                                               
         TM    0(R1),X'80'         IF TEST FOR EQUATE                           
         JZ    SUDMAJ20                                                         
         CLC   MAJEQU,0(R2)        THEN TEST FOR EQUATE                         
         JE    SUDMAJ40                                                         
         J     SUDMAJ30                                                         
*                                                                               
SUDMAJ20 CLC   MAJCHAR,0(R2)       ELSE TEST FOR CHARACTER MAJOR CODE           
         JE    SUDMAJ40                                                         
*                                                                               
SUDMAJ30 LA    R3,MAJNEXT          BUMP R3 TO NEXT MAJOR CODE                   
         J     SUDMAJ10                                                         
*                                                                               
SUDMAJ40 MVC   TGMAEQU,MAJEQU      SAVE EQUATE IN GLOBAL                        
         MVC   TGMACHAR,MAJCHAR    SAVE CHARACTER MAJOR CODE IN GLOBAL          
*                                                                               
         J     SUDYES              RETURN EQ                                    
*                                                                               
*              ROUTINE TO VALIDATE LOCATION BASED ON CHARACTER                  
*                                  R2 = A(9-BYTE LOCATION)                      
         USING MAJTABD,R3                                                       
SUDLOCV  DS    0H                                                               
         L     R2,0(R1)            R2 = A(LOCATION)                             
         CLI   8(R2),C'+'          MAKE SURE DOESN'T END IN A +                 
         JE    SUDNO                                                            
*                                                                               
         MVI   TGBYTE2,0           PRE-CLEAR EQUATE                             
         BAS   RE,SUDMULT          RETURNS R4 = (N'LOCATIONS)                   
         STC   R4,TGBYTE                                                        
*                                                                               
SUDLOCV5 L     R3,TGALOCS          R3=A(LOCATIONS TABLE)                        
         BAS   RE,SUDLOCLN         RETURNS R1=(L'LOCATION)                      
         LTR   R1,R1               PROTECT AGAINST BAD LOCATIONS                
         JZ    SUDNO                                                            
         BCTR  R1,0                SET LENGTH FOR EXECUTED COMPARE              
*                                                                               
SUDLOCV10 CLI  0(R3),X'FF'         TEST END OF TABLE                            
         JE    SUDNO                                                            
         EX    R1,*+8                                                           
         J     *+10                                                             
         CLC   MAJCHAR(0),0(R2)    COMPARE BASED ON INPUT LENGTH                
         JE    *+12                                                             
         LA    R3,MAJNEXT          BUMP R3 TO NEXT MAJOR CODE                   
         J     SUDLOCV10                                                        
*                                                                               
         OC    TGBYTE2,MAJEQU      EVERYTHING OK - SET BIT ON                   
         CLI   TGBYTE,0            IF MULTIPLE LOCATIONS                        
         JE    SUDLOCVY                                                         
         AR    R2,R1               BUMP TO NEXT LOCATION                        
         LA    R2,2(R2)                                                         
         BCT   R4,SUDLOCV5         AND LOOP                                     
*                                                                               
SUDLOCVY GOTO1 SUDMAJV,DMCB,(X'80',TGBYTE2)  SET EQUATE AND CHARACTER           
         JE    *+6                                                              
         DC    H'0'                                                             
         J     SUDYES                                                           
         EJECT                                                                  
*              ROUTINE RETURNS R4 EQUAL TO NUMBER OF LOCATIONS                  
*              FOUND IN CHARACTER EXPRESSION                                    
*                                  R2 = A(LOCATION EXPRESSION)                  
SUDMULT  NTR1                                                                   
         LA    R4,1                PRE-SET R4=(N'LOCATIONS)                     
         LA    R0,L'TGMACHAR                                                    
*                                                                               
SUDMULT5 CLI   0(R2),C'+'          IF SEPERATOR FOUND                           
         JNE   *+8                                                              
         LA    R4,1(R4)            ADD TO LOCATIONS                             
         LA    R2,1(R2)                                                         
         BCT   R0,SUDMULT5                                                      
*                                                                               
         XIT1  REGS=(R4)           RETURN R4                                    
*                                                                               
*              ROUTINE RETURNS R1 EQUAL TO LENGTH OF LOCATION                   
*                                  R2=A(LOCATION)                               
SUDLOCLN NTR1                                                                   
         XR    R1,R1               PRE-CLEAR R1=A(L'LOCATION)                   
         LA    R0,L'TGMACHAR                                                    
*                                                                               
SUDLOC5  CLI   0(R2),C' '          LOOP TILL SPACE OR PLUS SIGN                 
         JE    SUDLOCX                                                          
         CLI   0(R2),C'+'                                                       
         JE    SUDLOCX                                                          
         LA    R1,1(R1)            ADD TO LENGTH OF LOCATION                    
         LA    R2,1(R2)            AND BUMP TO NEXT CHAR IN EXPRESSION          
         BCT   R0,SUDLOC5                                                       
*                                                                               
SUDLOCX  XIT1  REGS=(R1)           RETURN R1                                    
*                                                                               
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
ASTUINVR DS    A                   A(INVOICE RECORD)                            
STUCYCST DS    XL1                                                              
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
