*          DATA SET FRDOLU62   AT LEVEL 018 AS OF 06/11/95                      
         TITLE 'FALCM62- VTAM LINE CONTROL - LU6.2 MAIN SECTION'                
*=================================================================*             
* PROCESS LU6.2 RPL/RPL EXTENSTIONS THAT HAVE BEEN POSTED COMPLETE              
*                                                                               
* THE PROCESSING IS DETERMINED BY THE CONTROL= VALUE RECORDED IN                
* THE RPL EXTENSION BY THE APPC MACRO.  THIS VALUE IS FOUND IN                  
* A TABLE AND THE ASSOCIATED ROUTINE IS CALLED.  AFTER THE RPL                  
* IS PROCESSED CONTROL IS RETURNED TO THE CALLER.  THUS THIS                    
* ROUTINE IS INDEPENDENT OF THE ALLOCATION OF RPLS TO VARIOUS                   
* PURPOSES.                                                                     
*                                                                               
*=================================================================*             
*  FARPL ENTRY IN R7   RPL ITSELF IS IN R8                                      
         USING FARPLD,R7                                                        
         USING IFGRPL,R8                                                        
         SPACE 1                                                                
DOLU62   DS    0H                                                               
         USING *,RF                                                             
         ST    RB,SAVE62                                                        
         ST    RE,SAVE62+4                                                      
         STM   R2,R6,SAVE62WK    SAVE WORK REGS                                 
         BASR  RB,0                                                             
         DROP  RF                                                               
         USING *,RB                                                             
         L     R6,RPLAAREA       GET RPL EXTENSION ADDRESS                      
         USING ISTRPL6X,R6                                                      
         ICM   R3,8,RPL6REQ       GET THE CONTROL= VALUE                        
         SRL   R3,24              MOVE TO LOW ORDER BYTE                        
*                                                                               
         APPCCMD CONTROL=CHECK,RPL=(R8)        OPERATION COMPLETE?              
         LTR   RF,RF             TEST FOR 1ST LEVEL ERROR                       
         BNZ   DOLU62ER                                                         
         LTR   R0,R0             TEST FOR 2ND LEVEL ERROR                       
         BZ    DOLU62B                                                          
DOLU62ER EQU   *                                                                
         CH    R0,HEX0B          DOES RPL EXTENTION HAVE ERROR DATA?            
         BNE   LU62ER1           NO, WEIRD TROUBLE                              
         ICM   R4,12,RPL6RCPR    PRIMARY ERROR REASON                           
         SRL   R4,16             GET INTO LOW ORDER TWO BYTES                   
**                      SENSE IN RPL6SNSI  SEC IN RPL6RCSC                      
         MVC   WTOB40+4(12),=C' AT DOLU62ER'                                    
         STCM  R3,15,WTOB40+16   CONTROL= VALUE                                 
         STCM  R4,15,WTOB40+20   PRI CODE                                       
         STCM  R6,15,WTOB40+24   EXTENSION ADDRESS                              
         WTO   MF=(E,WTOB40)                                                    
         B     XIT62             ABANDOND THIS RPL (FOR NOW)                    
DOLU62B  EQU   *                                                                
         LA    R4,TBL62                                                         
DOLU62C  EX    R3,TBL62CLI        COMPARE R3 WITH WHAT IS IN TBL                
         BE    DOLU62D            WE HAVE A MATCH                               
         LA    R4,TBL62LEN(,R4)   ADVANCE TO NEXT ENTRY                         
         CLI   0(R4),X'FF'        ARE WE AT END OF THE TABLE?                   
         BNE   DOLU62C            NOPE, GO TRY AGAIN                            
         CLI   RPL6REQ,0          IS RPL UNUSED, SO FAR?                        
         BE    XIT62              YES, BYPASS                                   
         DC    H'0'               NO MATCH IN THE TABLE                         
*                                                                               
DOLU62D  L     R2,4(,R4)          GET ROUTINE ADDRESS FROM TABLE                
         MVC   WTOB40+4(12),=C'62 TABLE ==>'                                    
         STCM  R3,15,WTOB40+16     CONTROL= VALUE                               
         STCM  R4,15,WTOB40+20     TABLE ENTRY ADDRESS                          
         STCM  R2,15,WTOB40+24     ROUTINE TO BE ENTERED                        
         MVC   WTOB40+30(16),0(R4) 16 BYTES OF THE TABLE                        
         WTO   MF=(E,WTOB40)                                                    
         BASR  RE,R2              GO TO SELECTED ROUTINE                        
**                                                                              
         MVC   WTOB20+4(12),=C' REQ UNKNOWN'                                    
         WTO   MF=(E,WTOB20)                                                    
         B     XIT62                                                            
*                                                                               
TBL62CLI CLI   0(R4),0          TARGET OF EXECUTE                               
*                                                                               
         DS    0F                                                               
TBL62    DC    AL1(RPL6ALLC),AL3(0),XL4'FF'          ALLOCATE                   
TBL62LEN EQU   *-TBL62                                                          
         DC    AL1(RPL6RSRV),AL3(0),XL4'FF'          RESETRCV                   
         DC    AL1(RPL6DEAL),AL3(0),XL4'FF'          DEALLOCATE                 
         DC    AL1(RPL6OPER),AL3(0),A(LU62OPER)      OPRCNTL                    
         DC    AL1(RPL6RFH5),AL3(0),A(LU62FMH5)      RECVFMH5                   
         DC    AL1(RPL6RCV),AL3(0),A(LU62RECV)       RECEIVE                    
         DC    AL1(RPL6RJCT),AL3(0),XL4'FF'          REJECT                     
         DC    AL1(RPL6SEND),AL3(0),A(LU62SEND)      SEND                       
         DC    AL1(RPL6SETS),AL3(0),XL4'FF'          SETSESS                    
         DC    AL1(0),AL3(0),A(XIT62)     ZERO       BYPASS                     
         DC    X'FF',A(0)                                                       
*                                                                               
         EJECT                                                                  
         NI    FARPLFLG,X'FF'-FARPLBSY    RESET BUSY FLAG                       
*=====================================================================          
*                                                                               
*=====================================================================          
LU62SEND DS    0H                                                               
         APPCCMD CONTROL=SEND,RPL=(R8),CONVID=(R0),                    X        
               OPTCD=ASY,ECB=INTERNAL,                                 X        
               RECLEN=(R6),AREA=(R5)                                            
         LTR   RF,RF                                                            
         EJECT                                                                  
*=====================================================================          
*   A  CONTROL=RECEIVE RPL HAS COMPLETED                                        
*    STEP 1 IS TO HANDLE THE BUFFER THAT HAS BEEN RECEIVED                      
*         2 IS TO THEN ISSUE ANOTHER RECEIVE                                    
*=====================================================================          
LU62RECV DS    0H                                                               
**  RPL EXTENSION IS IN REG 6                                                   
**  RPL ITSELF    IS IN REG 8                                                   
**  FARPL         IS IN REG 7                                                   
         CLI   RPL6REQ,RPL6RECV  MAKE SURE WE'RE HANDLING RECEIVES              
*NOP     BNE                                                                    
**                                                                              
**                               UTL ADDRESS IN DIFFERENT USER FIELD            
         ICM   R3,15,RPL6USR     GET FARPL ADDRESS                              
         CR    R3,R7                                                            
         BE    *+6                                                              
         DC    H'0'              SHOULD NOT OCCUR                               
         L     R3,FARPLUTL       GET THE UTL ADDRESS                            
         USING UTLD,R3                                                          
         MVC   WTOB40+4(12),=C' AT LU62RECV'                                    
         STCM  R3,15,WTOB40+16   UTL ADDRESS                                    
         MVC   WTOB40+25(8),RPL6LU   PARTNER LU NAME                            
         WTO   MF=(E,WTOB40)                                                    
*        MVC   TRTUNNI(1),RPL6RTUN   SAVE THE RETURNED INFO BYTE                
*        MVC   TWRMASK(2),RPL6RCV1   SAVE THE WHAT RECEIVED MASK                
**                                                                              
**    NEED BIG ANALYSIS OF WHAT RECEIVED MASK HERE                              
**                                                                              
         L     R4,RPLRLEN          SEE HOW MUCH DATA CAME IN                    
         L     R5,RPLAREA          GET THE DATA BUFFER ADDRESS                  
         STCM  R4,15,WTOB40+4      LENGTH VALUE                                 
         MVC   WTOB40+8(32),0(R5)  DATA ITSELF                                  
         WTO   MF=(E,WTOB40)                                                    
         L     R4,=V(SYSFAC)                                                    
         USING SYSFACD,R4                                                       
         L     RE,VSSB                                                          
         USING SSBD,RE                                                          
         TM    SSBSTAT1,SSBSEOJ    TEST EOJ PENDING                             
         DROP  R4,RE                                                            
         BO    XIT62               YES, WIND DOWN                               
         NI    FARPLFLG,X'FF'-FARPLBSY    RESET BUSY FLAG                       
         LR    R1,R7               PASS FARPL ADDRESS AS PARM                   
         L     RF,=A(DORECV)                                                    
         BASR  RE,RF               NO, GO DO ANOTHER RECEIVE                    
*                                                                               
**                                                                              
**                                                                              
         B     XIT62                                                            
         EJECT                                                                  
         USING UTLD,R3                                                          
         L     RE,FARPLIOA                                                      
         SH    RE,=H'8'            BACK UP TO HEADER                            
         ST    R3,0(RE)            SET UTL ENTRY ADDRESS                        
*                                                                               
         L     RF,FARPLIOL         BUFFER SIZE                                  
         ST    RF,4(RE)            STORE 0000.LEN                               
*                                                                               
         MVC   TBUFF,FARPLIOA      MOVE BUFFER ADDR TO TRM ENTRY                
         XC    FARPLIOA,FARPLIOA   CLEAR RPL BUFFER ADDRESS                     
         XC    FARPLIOL,FARPLIOL   CLEAR MSG LEN                                
         OC    TPRNT,TPRNT         TEST PRINTER                                 
         BNZ   NXT70L62                                                         
******  ???                                                                     
*=====================================================================*         
* QUEUE INPUT TO PROPER SE                                            *         
*=====================================================================*         
         SPACE 1                                                                
NXT60L62 DS    0H                                                               
NXT70L62 L     R6,TBUFF            SET FLAG TO INDICATE REAL INPUT              
         SH    R6,=H'8'                                                         
         OI    4(R6),X'20'                                                      
*                                                                               
         LA    R3,0(,R3)           CLEAR HIGH ORDER BYTE OF REG                 
*        GOTO1 =V(MSGQIN),P1,(R2),TBUFF,(R3)                                    
*                                                                               
*=====================================================================*         
* BUILD A BUFFER HEADER AT A(BUFFER)-8.                               *         
* BYTES 0-3 = UTL ENTRY ADDRESS                                       *         
*        4  = SPARE                                                   *         
*        5 =  DSP TO STX (SET BY MSGQIN) OR WRITE TYPE (LCWRITE)      *         
*       6-7 = MESSAGE LENGTH                                          *         
*=====================================================================*         
*                                                                               
         EJECT                                                                  
*=====================================================================          
*                                                                               
*   THIS ROUTINE HANDLES THE COMPLETION OF THE SPECIAL FMH5 RECEIVE             
*   MACRO. THE RESULT OF THIS IS THAT A                                         
*   CONVERSATION ID IS ASSIGNED AND THE CONTINUATION MODE OF THE                
*   CONVERSATION IS ESTABLISHED AS CONTINUE SPECIFIC.                           
*                                                                               
*   THE FMH5 ITSELF NEEDS TO BE ANALYZED.                                       
*                                                                               
*   ASSUMING WE LIKE THE FMH5 WE THEN NEED TO SETUP TO DO A SPECIFIC            
*   MODE RECEIVE.  THIS WILL BE DONE USING THE FARPL ASSIGNED IN                
*   THE DOFMH5 PROCESS.                                                         
*=====================================================================          
LU62FMH5 DS    0H                                                               
**  RPL EXTENSION IS IN REG 6                                                   
**  RPL ITSELF    IS IN REG 8                                                   
**  FARPL         IS IN REG 7                                                   
         CLI   RPL6REQ,RPL6RFH5  MAKE SURE WE'RE HANDLING AN FMH5               
**                                                                              
**   MAYBE WE SHOULD DECREMENT THE COUNT HERE.                                  
**                                                                              
**       ANALYSZE THE FMH5 THAT HAS BEEN RECEIVED                               
**       IF WE DON'T LIKE THE FMH5 THEN WHAT? DEALLOCATE, TBD                   
**                                                                              
**                   RPL6LU      PARTNER LU                                     
**                   RPL6MODE    PARTNER'S MODE                                 
         MVC   WTOB20+4(16),=C' AT LU62FMH5    '                                
         WTO   MF=(E,WTOB20)                                                    
         L     R2,=V(SYSFAC)                                                    
         USING SYSFACD,R2                                                       
         L     R3,VUTL                                                          
         DROP  R2                                                               
         LH    R4,0(R3)                                                         
         L     R5,2(R3)                                                         
         LA    R3,6(R3)                                                         
         USING UTLD,R3                                                          
FMH52    CLC   TSYM,RPL6LU         MATCH ON SYMBOLIC NAME(LUID)?                
         BE    FMH54               YES, THEN USE THIS ENTRY                     
         BXLE  R3,R4,FMH52         NO, TRY AGAIN                                
         B     FMH5ERR             NOT FOUND                                    
*                                                                               
FMH54    TM    TSTAT2,TSTATTIP    TEST IF STILL IN PROCESS                      
         ICM   R4,15,RPL6USR      GET THE FARPL ASSIGNED EARLIER                
         ST    R3,(FARPLUTL-FARPLD)(R4) SAVE THE UTL ADDRESS IN FARPL           
         ICM   R2,15,RPL6CNVD     GET ASSIGNED 6.2 CONVERSATION ID              
         ST    R2,TCID            SAVE IT.                                      
*                                 UP TO THIS POINT THIS FIELD HAD THE           
*                                 SESSION ID                                    
*                                                                               
         L     R2,RPLRLEN         HOW MUCH DATA CAME IN?                        
         L     R1,RPLAREA         THIS SHOULD BE WHERE IT IS                    
         USING ISTFM5,R1                                                        
         ICM   R5,8,FM5LENTH      GET LENGTH                                    
         MVC   WTOB40+4(21),0(R1) DISPLAY FMH5 FOR NOW                          
         WTO   MF=(E,WTOB40)      LATER ON PUT IT IN VTT TRACE                  
         DROP  R3                 DONE WITH UTL                                 
*        MVC   TWRMASK(2),RPL6RCV1   SAVE THE WHAT RECEIVED MASK                
*        MVC   TRTUNNI(1),RPL6RTUN   SAVE THE RETURNED INFO BYTE                
**                                                                              
         NI    FARPLFLG,X'FF'-FARPLBSY    RESET BUSY FLAG SO ANOTHER            
*                                       RECVFMH5 CAN BE ISSUED                  
**                                                                              
         LR    R1,R4               GET FARPL TO BE USED FOR RECEIVE             
         L     RF,=A(DORECV)                                                    
         BASR  RE,RF               GO ISSUE A RECEIVE TO GET DATA GOING         
         B     XIT62                                                            
FMH5ERR  MVC   WTOB40+4(15),=C'LU NOT IN UTL***'                                
         MVC   WTOB40+20(8),RPL6LU     HERE'S THE LU NAME                       
         WTO   MF=(E,WTOB40)                                                    
         B     XIT62                                                            
         DROP  R1                                                               
*                                                                               
         APPCCMD CONTROL=REJECT,QUALIFY=CONV,RPL=(R8)                           
         EJECT                                                                  
*=====================================================================          
*                                                                               
*  HANDLE COMPLETION OF CONTROL=OPER REQUESTS                                   
*                                                                               
*=====================================================================          
LU62OPER DS    0H                                                               
**  RPL EXTENSION IS IN REG 6                                                   
**  RPL ITSELF    IS IN REG 8                                                   
**  FARPL         IS IN REG 7                                                   
         CLI   RPL6REQ,RPL6OPER  MAKE SURE WE'RE HANDLING AN OPER               
         BNE   XIT62                                                            
         ICM   R3,15,RPL6USR     IS THIS UTL ADDRESS (WE HOPE)?                 
         USING UTLD,R3                                                          
**                   RPL6LU      PARTNER LU                                     
**                   RPL6MODE    PARTNER'S MODE                                 
         MVC   WTOB20+4(12),=C' AT LU62OPER'                                    
         STCM  R3,15,WTOB20+16   UTL ADDRESS                                    
         MVC   WTOB20+20(4),=C'    '                                            
         WTO   MF=(E,WTOB20)                                                    
         DROP  R3                DONE WITH UTL                                  
         B     XIT62                                                            
         SPACE 4                                                                
XIT62    LM    R2,R6,SAVE62WK    RESTORE WORK REGS                              
         DROP  R6                DONE WITH RPL EXTENSION                        
         L     RE,SAVE62+4       LOAD RETURN ADDRESS                            
         L     RB,SAVE62         RESTORE BASE REG                               
         BR    RE                                                               
LU62ER1  MVC   WTOB20+4(12),=C' AT LU62ER1'                                     
         B     XIT62                                                            
         SPACE 4                                                                
HEX0B    DC    H'11'                                                            
HEX48    DC    H'72'       SESSION TERMINATED                                   
HEX14    DC    H'20'       ABNORMAL DEALLOCATION BY PARTNER                     
HEX2C    DC    H'44'       CONV-ID MOST LIKELY BAD                              
HEX4C    DC    H'76'       RESOURCE FAILURE                                     
HEX60    DC    X'0060'     NO FMH5 AVAILABLE TO BE RECEIVED                     
         LTORG                                                                  
         EJECT                                                                  
DORECV   DS    0H                                                               
         USING *,RF                                                             
         ST    RB,SAVE62B                                                       
         ST    RE,SAVE62B+4                                                     
         STM   R2,R6,SAVE62BW    SAVE WORK REGS                                 
         BASR  RB,0                                                             
         DROP  RF                                                               
         USING *,RB                                                             
*    CALLER PASSES FARPL TO BE USED IN REG 1                                    
*        CONVID IS IN UTL                                                       
         LR    R7,R1               PUT FARPL ADDRESS IN STANDARD REG            
         TM    FARPLFLG,FARPLBSY   TEST IF BUSY FLAG                            
         OI    FARPLFLG,FARPLBSY   SET BUSY FLAG                                
         L     R8,FARPLRPL         POINT TO RPL                                 
         ICM   R2,15,FARPLIOA      TEST BUFFER ASSIGNED                         
         MVC   WTOB20+4(12),=C' AT RECVENT'                                     
         STCM  R7,15,WTOB20+16     FARPL  ADDRESS                               
         STCM  R2,15,WTOB20+20     BUFFER ADDRESS                               
         WTO   MF=(E,WTOB20)                                                    
         LTR   R2,R2                                                            
         BZ    RECVER2                                                          
         LA    R2,6(,R2)           ADVANCE TO LENGTH SECTION OF BUFFER          
         L     R4,FARPLUTL         GET UTL ENTRY POINTER                        
         USING UTLD,R4                                                          
         L     R3,TCID             GET CONVERSATION ID                          
**                                                                              
**                                                                              
         PRINT GEN                                                              
         APPCCMD CONTROL=RECEIVE,RPL=(R8),QUALIFY=SPEC,                X        
               FILL=LL,CONVID=(R3),                                    X        
               OPTCD=ASY,ECB=INTERNAL,CONMODE=SAME,                    X        
               AREA=(R2),AREALEN=82     LCBUFFLN                                
         DROP  R4                                                               
         PRINT NOGEN                                                            
         LTR   RF,RF                                                            
         BNZ   RECVER1                                                          
RECVXIT  LM    R2,R6,SAVE62BW    RESTORE WORK REGS                              
         MVC   WTOB20+4(12),=C' AT RECVEXI'                                     
         WTO   MF=(E,WTOB20)                                                    
         L     RE,SAVE62B+4      LOAD RETURN ADDRESS                            
         L     RB,SAVE62B        RESTORE BASE REG                               
         BR    RE                                                               
RECVER1  MVC   WTOB20+4(12),=C' AT RECVER1'                                     
         WTO   MF=(E,WTOB20)                                                    
         B     RECVXIT                                                          
RECVER2  MVC   WTOB20+4(12),=C' AT RECVER2'                                     
         WTO   MF=(E,WTOB20)                                                    
         B     RECVXIT                                                          
         DROP  RB                                                               
         EJECT                                                                  
DOFMH5   DS    0H                                                               
         USING *,RF                                                             
         ST    RB,SAVE62                                                        
         ST    RE,SAVE62+4                                                      
         STM   R2,R6,SAVE62WK    SAVE WORK REGS                                 
         BASR  RB,0                                                             
         DROP  RF                                                               
         USING *,RB                                                             
         L     R7,=A(FMH5FARP)   GET THE FMH5 FARPL                             
         TM    FARPLFLG,FARPLBSY IS IT ALREADY BUSY?                            
         BO    FMH5XIT           YES, WAIT TILL LATER                           
         OI    FARPLFLG,FARPLBSY SET BUSY FLAG                                  
         L     R8,FARPLRPL       POINT TO RPL                                   
         L     R4,AFMHAREA       READ FMH5 INTO THIS AREA                       
*                                                                               
*    ONCE THE FMH5 COMES IN WE ARE GOING TO DEDICATE AN FARPL/RPL               
*    TO THE CONVERSATION AND THE CONVERSATION WILL BE IN SPECIFIC MODE          
*    AND MOST OF THE TIME A SPECIFIC RECEIVE WILL BE OUTSTANDING FOR IT         
*                                                                               
         L     R7,=A(RECVFARP)   GET THE FIRST RECEIVE FARPL                    
DOFMH5A  TM    FARPLFLG,FARPL62A HAS THIS RPL BEEN ASSIGNED                     
         BNO   DOFMH5B           NO, THEN WE WILL USE IT.                       
         ICM   R7,15,FARPLNXT    YES, GET ADDRESS OF NEXT FARPL                 
         BNZ   DOFMH5A           IF NON-ZERO TRY THIS FARPL OUT                 
         B     FMH5ER2           WE ARE OUT OF FARPL'S                          
DOFMH5B  OI    FARPLFLG,FARPL62A INDICATE THIS RPL HAS BEEN ASSIGNED            
*NOP     LA    R6,FARPLUTL       THE LU62 USER FIELD WILL POINT HERE            
         PRINT GEN                                                              
         APPCCMD CONTROL=RCVFMH5,RPL=(R8),CONMODE=CS,                  X        
               USERFLD=(R7),                                           X        
               OPTCD=ASY,ECB=INTERNAL,AREA=(R4),AREALEN=255                     
         PRINT NOGEN                                                            
         LTR   RF,RF                                                            
         BNZ   FMH5ER1                                                          
         L     RF,=A(QDOWN)      GET SUBROUTINE ADDRESS                         
         L     R1,=A(FMH5CTR)    GET COUNTER ADDRESS                            
         BASR  RE,RF             GO DECREMENT COUNT OF FMH5'S WAITING           
FMH5XIT  LM    R2,R6,SAVE62WK    RESTORE WORK REGS                              
         MVC   WTOB20+4(12),=C' AT FMH5EXI'                                     
         STCM  R7,15,WTOB20+16   SAVE FARPL ADDRESS                             
         WTO   MF=(E,WTOB20)                                                    
         L     RE,SAVE62+4       LOAD RETURN ADDRESS                            
         L     RB,SAVE62         RESTORE BASE REG                               
         BR    RE                                                               
FMH5ER1  MVC   WTOB20+4(12),=C' AT FMH5ER1'                                     
         WTO   MF=(E,WTOB20)                                                    
         B     FMH5XIT                                                          
FMH5ER2  MVC   WTOB20+4(12),=C' AT FMH5ER2'                                     
         WTO   MF=(E,WTOB20)                                                    
         B     FMH5XIT                                                          
         DROP  RB                                                               
DOACTSES DS    0H                                                               
         USING *,RF                                                             
         ST    RB,SAVE62                                                        
         ST    RE,SAVE62+4                                                      
         BASR  RB,0                                                             
         DROP  RF                                                               
         USING *,RB                                                             
         USING UTLD,R3                                                          
         STM   R2,R6,SAVE62WK    SAVE WORK REGS                                 
         L     R7,=A(OPERFARP)     USE ONE AND ONLY ONE 6.2 FARPL               
         OI    FARPLFLG,FARPLBSY   SET BUSY FLAG                                
         L     R5,TCID             GET THE CID FOR THIS SESSION                 
         ICM   R8,15,FARPLRPL         NOW GET THE RPL ADDRESS                   
         MVC   WTOB20+4(16),=C' ACTSESS BEFORE '                                
*NOP     MVC   WTOB20+4(16),=C' ACTSESS BEFORE '                                
*NOP     WTO   MF=(E,WTOB20)                                                    
         L     R1,RPLAAREA                                                      
         USING ISTRPL6X,R1                                                      
         MVI   RPL6QUAL,RPL6ACT    HAVE VTAM ACCEPT THE SESSION                 
         DROP  R1                                                               
         WTO   MF=(E,WTOB20)                                                    
         PRINT GEN                                                              
         APPCCMD CONTROL=OPRCNTL,RPL=(R8),                             X        
               OPTCD=SYN,    ECB=INTERNAL,                             X        
               AREA=0,CONFTXT=NO,  NO OVERRIDES, CLEAR TEXT            X        
               ARG=(R5),           PASS CID OF SESSION TO BE ACCEPTED  X        
               USERFLD=(R3)        SET USER FIELD TO UTL ADDRESS                
         LTR   RF,RF               DID VTAM GET THIS REQUEST OK?                
         BNZ   ACTSESER            YES, TIDY UP AND GO ON TO NEXT ITEM          
         LM    R2,R6,SAVE62WK    RESTORE WORK REGS                              
         L     RE,SAVE62+4       LOAD RETURN ADDRESS                            
         L     RB,SAVE62         RESTORE BASE REG                               
         BR    RE                                                               
         DROP  R3                                                               
ACTSESER MVC   WTOB20+4(20),=C' ACTSESS AFTER ERROR'                            
         WTO   MF=(E,WTOB20)                                                    
         STCM  RF,15,WTOB20+4                                                   
         WTO   MF=(E,WTOB20)                                                    
         L     RE,=A(DOLU62ER)                                                  
         BR    RE                                                               
SAVE62   DS    2F                                                               
SAVE62WK DS    5F                                                               
SAVE62B  DS    2F                                                               
SAVE62BW DS    5F                                                               
AFMHAREA DC    A(FMH5AREA)                                                      
ALCMTRC  DC    A(LCMTRC)              TRACE ROUTINE ADDRESS                     
LU62LIST DC    8F'0'                                                            
WTOB20   WTO   '                    ',ROUTCDE=(11),MF=L                         
WTOB40   WTO   '                                        ',ROUTCDE=(11),X        
               MF=L                                                             
         LTORG                                                                  
