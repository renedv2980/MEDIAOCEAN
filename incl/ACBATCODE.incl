*          DATA SET ACBATCODE  AT LEVEL 004 AS OF 11/20/97                      
GETACC   NTR1  ,                   GET AN ACCOUNT RECORD                        
         MVC   BOHALF1,FVMSGNO                                                  
         MVC   FVMSGNO,=AL2(FVFOK)                                              
         ST    R2,FVADDR                                                        
         GOTO1 AGETACC,BOPARM,KEY,(R6)                                          
         BNE   ERRXIT                                                           
         MVC   FVMSGNO,BOHALF1                                                  
         B     EXIT                                                             
         SPACE 1                                                                
CHECKACC MVC   FVMSGNO,=AL2(AE$INACP)                                           
         TM    ACCTSTAT,ACSBAL                                                  
         BZ    BADACC               NO BALANCE ELEMENT                          
         TM    ACCTSTAT,ACSLOCK                                                 
         BO    BADACC               LOCKED                                      
         BR    RE                                                               
         SPACE 1                                                                
BADACC   MVC   BADACCNT,IOKEYSAV+1                                              
         ST    R2,FVADDR                                                        
         SPACE 1                                                                
ERRXIT   L     RD,BCSVRD           RETURN TO BASE                               
         L     RD,8(RD)            AT POINT OF OVERLAY CALL                     
         B     ERROR                                                            
         SPACE 1                                                                
CHECKOUT MVI   ERRNUM,NOBALAC                                                   
         TM    ACCTSTAT,ACSBAL                                                  
         BZ    BADCHECK             NO BALANCE ELEMENT                          
         MVI   ERRNUM,LOCKDAC                                                   
         TM    ACCTSTAT,ACSLOCK                                                 
         BO    BADCHECK             LOCKED                                      
         MVI   ERRNUM,CLOSDAC                                                   
         TM    ACCTSTAT,ACSCLSE                                                 
         BO    BADCHECK             CLOSED                                      
         BR    RE                                                               
         SPACE 1                                                                
BADCHECK CLI   0(RF),0             RF=FIRST DISPLAY FIELD                       
         BE    BADACC                                                           
         TM    4(RF),X'40'         INPUT PREVIOUSLY                             
         BZ    NEXTWIPE                                                         
         ZIC   R1,0(RF)            LENGTH INCLUDING HEADER                      
         SH    R1,=H'9'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   8(0,RF),SPACES                                                   
         OI    6(RF),X'80'                                                      
NEXTWIPE ZIC   R1,0(RF)                                                         
         AR    RF,R1                                                            
         B     BADCHECK                                                         
         SPACE 1                                                                
PUTDAY   LA    R1,BCFULL                                                        
         XC    0(4,R1),0(R1)       CLEAR DISK ADDRESS                           
         ST    R1,BOPARM+8         SET A(DISK ADDRESS)                          
         BR    RE                                                               
         SPACE 1                                                                
ADTWA1   NTR1  ,                   ADD AN ACCDAY RECORD                         
         MVC   FVMSGNO,=AL2(FVFOK)                                              
         GOTO1 AADACDAY,BOPARM,IOAREA,BOPL61,BOWORK1                            
         CLC   FVMSGNO,=AL2(FVFOK)                                              
         BNE   ERRXIT                                                           
         B     EXIT                                                             
         SPACE 1                                                                
NARRSCAN NTR1  ,                   EXTRACT NARRATIVE FROM TWA                   
         GOTO1 ANARRSCN,BOPARM,(R2),(R3)                                        
         XIT1  REGS=(R6)                                                        
         SPACE 1                                                                
PROFMERG NTR1  ,                   MERGE PROFILE ELEMENTS                       
         GOTO1 APRFMRGE                                                         
         B     EXIT                                                             
         SPACE 1                                                                
ANY      ST    R2,FVADDR                                                        
         CLI   FVILEN-FVIHDR(R2),0                                              
         BNER  RE                                                               
         MVC   FVMSGNO,=AL2(AE$MISIF)                                           
         B     ERROR                                                            
         SPACE 1                                                                
NUMERIC  ST    R2,FVADDR                                                        
         MVC   FVMSGNO,=AL2(AE$NONIF)                                           
         TM    FVIIND-FVIHDR(R2),FVINUM                                         
         BZ    ERROR                                                            
         BR    RE                                                               
         SPACE 1                                                                
GETODAY  NTR1  ,                   GET TODAY'S DATE INTO WORK                   
         GOTO1 VDATCON,BOPARM,(5,0),(0,BOWORK1)                                 
         B     EXIT                                                             
         SPACE 1                                                                
DATECHK  ST    RE,12(RD)           CHECK DATE OF TRANSACTION                    
         MVC   FVMSGNO,=AL2(AE$DOPSP)                                           
         L     RE,0(R1)                                                         
         CLC   BCTDATL,0(RE)                                                    
         BH    DATECHK2                                                         
         CLC   BCTDATH,0(RE)                                                    
         BL    DATECHK2                                                         
         MVC   FVMSGNO,=AL2(FVFOK)                                              
         B     DATECHKX                                                         
DATECHK2 MVI   0(R1),X'FF'         EXIT WITH FF IN PLIST IF BAD                 
DATECHKX L     RE,12(RD)                                                        
         BR    RE                                                               
         EJECT                                                                  
READ     NTR1  ,                   READ A RECORD                                
         MVC   IOKEY,KEY                                                        
         GOTO1 AREAD,AIO1                                                       
         BNE   ERRXIT                                                           
         B     MOVER                                                            
         SPACE 1                                                                
READL    NTR1  ,                   READ AND LOCK A RECORD                       
         MVC   IOKEY,KEY                                                        
         GOTO1 AREADL,AIO1                                                      
         BNE   ERRXIT                                                           
         B     MOVER                                                            
         SPACE 1                                                                
HIGH     NTR1  ,                   READ HIGH FOR A RECORD                       
         MVC   IOKEY,KEY                                                        
         GOTO1 ARDHI,AIO1                                                       
         BNE   ERRXIT                                                           
         CLC   IOKEYSAV,KEY                                                     
         BE    MOVER                                                            
         MVC   FVMSGNO,=AL2(AE$INACC)                                           
         B     ERRXIT                                                           
         SPACE 1                                                                
HIGHL    NTR1  ,                   READ HIGH AND LOCK A RECORD                  
         MVC   IOKEY,KEY                                                        
         GOTO1 ARDHIL,AIO1                                                      
         BNE   ERRXIT                                                           
         CLC   IOKEYSAV,KEY                                                     
         BE    MOVER                                                            
         MVC   FVMSGNO,=AL2(AE$INACC)                                           
         B     ERRXIT                                                           
         SPACE 1                                                                
WRITE    NTR1  ,                   WRITE A RECORD                               
         MVC   IOKEY,KEY                                                        
         L     R0,AIO1             MOVE RECORD INTO IOAREA1                     
         LA    RE,KEY                                                           
         LA    R1,1000                                                          
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         GOTO1 AWRITE,AIO1                                                      
         BNE   ERRXIT                                                           
         B     EXIT                                                             
         SPACE 1                                                                
ADD      NTR1  ,                   ADD A RECORD                                 
         GOTO1 AADD,AIO1                                                        
         BNE   ERRXIT                                                           
         B     EXIT                                                             
         SPACE 1                                                                
SEQ      NTR1  ,                   READ SEQUENTIAL                              
         GOTO1 ASEQ,AIO1                                                        
         BNE   ERRXIT                                                           
         B     MOVER                                                            
         SPACE 1                                                                
SEQL     NTR1  ,                   READ SEQUENTIAL AND LOCK                     
         GOTO1 ASEQL,AIO1                                                       
         BNE   ERRXIT                                                           
         B     MOVER                                                            
         SPACE 1                                                                
MOVER    L     RE,AIO1             MOVE RECORD INTO OVERLAY IOAREA              
         LA    R0,KEY                                                           
         LA    R1,2000                                                          
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         SPACE 1                                                                
EXIT     DS    0H                                                               
ERROR    ST    R2,FVADDR                                                        
CURSIT   XIT1  ,                                                                
