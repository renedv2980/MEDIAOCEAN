*          DATA SET FRCRAMON   AT LEVEL 004 AS OF 05/01/02                      
CRAMON   CSECT                                                                  
CRAMON   AMODE 24                                                               
CRAMON   RMODE 24                                                               
         USING *,R15                                                            
         STM   R14,R12,12(R13)  SAVE REGS IN CALLER SAVE AREA                   
         BASR  R12,0                                                            
         USING *,R12                                                            
         L     R07,0(0,R01)     SAVE INPUT PARM INFO                            
         L     R00,SIZE                                                         
         GETMAIN R,LV=(0)                                                       
         LR    R10,R01                                                          
         USING DATA,R10                                                         
         ST    R13,SAVE+4       SAVE CALLERS SAVE AREA IN MINE                  
         LA    R03,SAVE                                                         
         ST    R03,8(R13)       STORE MY SAVE AREA IN HIS                       
         LR    R13,R03          SET R13 TO MY SAVE AREA                         
         ST    R07,INPARM                                                       
         TIME  DEC              ANSWER IN REG0 PACKED DECIMAL                   
         ST    R00,STARTIME         SAVE TIME                                   
*NOP     B     REBUILD                                                          
         STCM  R01,X'7',STARTIME+4  SAVE DATE                                   
         UNPK  DATETIME(13),STARTIME(7)                                         
         LA    R01,MSG01                                                        
         MVC   MSG01HR(2,R01),DTHR        INSERT HOUR IN MSG                    
         MVC   MSG01MN(2,R01),DTMN        INSERT MIN IN MSG                     
         MVC   MSG01SC(2,R01),DTSC        INSERT SECS IN MSG                    
         MVC   MSG01YR(2,R01),DTYR        INSERT YEAR IN MSG                    
         MVC   MSG01DY(3,R01),DTDAY       INSERT JULIAN DAY IN MSG              
********************************************************************            
*        SHOULD THIS JOB CONTINUE TO RUN OR SHOULD THE             *            
*        JOB BE RECREATED AND SENT BACK TO THE UK                  *            
********************************************************************            
         WTO   MF=(E,MSG01)     INDICATE CRAMON IN CONTROL                      
         L     R05,16           GET CVT ADDRESS                                 
         USING CVTMAP,R05                                                       
         L     R05,CVTSMCA      GET SMCA ADDRESS                                
         DROP  R05                                                              
         MVC   SMFSID,SMCASID-SMCABASE(R05)  GET SYSTEM SMF ID                  
         MVC   PARMCHAR,X'40'   SET TO BLANK                                    
         SR    R08,R08          CLEAR REGISTER                                  
         ICM   R08,X'C',INPARM                                                  
         LTR   R08,R08          IS IT ZERO LENGTH                               
         BZ    TIMECHEK         YES, THEN NO PARM                               
         MVC   PARMCHAR(1),2(R07)                                               
         CLI   PARMCHAR,C'X'    WAS EXECUTE "X" SPECIFIED                       
         BE    RUNJOB                                                           
         CLI   PARMCHAR,C'R'    WAS RESUBMIT "R" SPECIFIED                      
         BE    REBUILD          YES, GO TO REBUILD ROUTINE                      
         CLI   PARMCHAR,C'O'    WAS ASK OPERATOR "O" SPECIFIED                  
         BE    OPCHEK                                                           
TIMECHEK CLC   DTHR,PM6         IS IT 6PM DDS TIME?                             
         BL    OPCHEK           CHECK WITH OPERATOR IF EARLIER                  
         CLC   DTHR,PM9         IS THE HOUR AFTER 9PM DDS TIME                  
         BNH   RUNJOB           NO, JUST RUN THE JOB                            
         CLC   DTHR,AM3         IS THE HOUR 3 AM DDS TIME                       
         BL    REBUILD          IF EARLIER, REBUILD THE JOB                     
*                               OTHERWISE CHECK WITH OPERATOR                   
OPCHEK   EQU   *                                                                
         LA    R08,OPANSER                                                      
         LA    R09,OPECB                                                        
         WTOR  'CRAMON01: IT IS BEFORE 6PM OR AFTER 3AM; SHOULD THIS JOX        
               B RUN',(R08),3,(R09)                                             
         WAIT  ECB=(R09)                                                        
         CLI   OPANSER,C'Y'     WAS THE REPLY YES                               
         BNE   REBUILD          NO, REBUILD THE JOB                             
RUNJOB   WTO   MF=(E,MSG01E)    INDICATE CONTROL GOING TO SPOOF                 
         XCTL  SF=(E,SPOOF)     GO DO SPOOF AND DON'T COME BACK                 
*                                                                               
REBUILD  EQU   *                                                                
         BAS   R14,MAKEJOB      GO REBUILD THE JOB & SUBMIT TO JES              
         L     R13,4(,R13)      GET PTR TO CALLERS SAVE AREA                    
         L     R00,SIZE                                                         
         LR    R01,R10                                                          
      FREEMAIN R,LV=(0),A=(1)                                                   
         L     R14,12(,R13)     PICK UP RETURN ADDRESS                          
         LM    R00,R12,28(R13) RESTORE REGS                                     
         BR    R14             RETURN TO CALLER                                 
         EJECT                                                                  
********************************************************************            
*        THIS SUBROUTINE WILL RECONSTITUTE THE JOB                 *            
*                                                                  *            
********************************************************************            
MAKEJOB  EQU   *                                                                
         ST    R14,MAKE14                                                       
         ST    R13,MAKE13                                                       
         EXTRACT TIOTADDR,FIELDS=TIOT   GET PTR TO TIOT                         
         L     R02,TIOTADDR                                                     
         USING TIOT1,R02                                                        
         MVC   JOBNAME,TIOCNJOB GET THE JOBNAME FROM THE TIOT                   
         DROP  R02                                                              
         LA    R08,OUTRPL      GET THE OUTPUT RPL ADDRESS                       
         USING IFGRPL,R08                                                       
         LA    R04,IOBUFFER    GET ADDRESS OF INPUT OUTPUT AREA                 
         ST    R04,RPLAREA     STORE IT IN RPL                                  
         DROP  R08                                                              
         OPEN  (IRDR)          THIS IS AN ACB                                   
         LA    R04,DJCLIN      GET ADDRESS OF JCLIN DCB                         
         USING IHADCB,R04      MAP THE DCB                                      
         LA    R01,EODADJCL    GET END OF DATA ADDRESS                          
*NOP     STCM  R01,7,DCBEODA   UPDATE DCB WITH IT                               
         OPEN  ((R04),INPUT)   OPEN JCLIN DCB                                   
         TM    DCBOFLGS,DCBOFOPN  DID IT OPEN OK                                
         BO    JCLOPEN         BRANCH IF OPEN                                   
         ABEND 1,DUMP                                                           
JCLOPEN  EQU   *                                                                
*DEBUG   WTO   MF=(E,MSG02A)   INDICATE JCL FILE OPENED                         
READJCL  GET   (R04),IOBUFFER                                                   
         CLI   IOBUFFER,C'$'   IS ESCAPE CHARACTER PRESENT                      
         BNE   PUTJCL                                                           
         CLI   IOBUFFER+1,C'J'    IS IT ESCAPE FOR JOBNAME                      
         BNE   TESTACCT                                                         
         MVC   IOBUFFER(2),=C'//' NEGATE ESCAPE SEQUENCE                        
         MVC   IOBUFFER+2(8),JOBNAME                                            
         B     PUTJCL                                                           
TESTACCT CLI   IOBUFFER+1,C'A'    IS IT ESCAPE FOR ACCOUNT CODE                 
         BNE   PUTJCL                                                           
*  ACCT=                                                                        
         MVC   IOBUFFER(2),=C'//'         NEGATE ESCAPE SEQUENCE                
         MVC   IOBUFFER+9(4),JOBNAME      GET 1ST 4 CHARS OF JOBNAME            
         MVC   IOBUFFER+13(5),=C'L0NY1'   ADD IN FIXED INFO                     
PUTJCL   PUT   RPL=OUTRPL                                                       
         B     READJCL         LOOP UNTIL ALL HAS BEEN READ                     
EODADJCL CLOSE ((R04),)        DONE WITH JCL FILE                               
*DEBUG   WTO   MF=(E,MSG02B)   INDICATE JCL FILE PROCESSED                      
         LA    R04,DSYSIN      GET ADDRESS OF SYSIN DCB                         
         LA    R01,EODADSIN    GET END OF DATA ADDRESS                          
         STCM  R01,7,DCBEODA   UPDATE DCB WITH IT  ??                           
         OPEN  ((R04),INPUT)   OPEN SYSIN DCB                                   
         TM    DCBOFLGS,DCBOFOPN  DID IT OPEN OK                                
         BO    RDSYSIN         BRANCH IF OPEN                                   
         ABEND 2,DUMP                                                           
RDSYSIN  GET   (R04),IOBUFFER                                                   
         PUT   RPL=OUTRPL                                                       
         B     RDSYSIN        LOOP UNTIL ALL HAS BEEN READ                      
EODADSIN EQU   *                                                                
*DEBUG   WTO   MF=(E,MSG02C)   INDICATE SYSIN FILE PROCESSED                    
         CLOSE ((R04),)       DONE WITH SYSIN FILE                              
         ENDREQ RPL=OUTRPL    INDICATE THAT'S ALL FOR THIS JOB                  
         LA    R08,OUTRPL                                                       
         USING IFGRPL,R08                                                       
         L     R07,RPLRBAR    PICK UP THE JOB-ID                                
         LA    R01,MSG03                                                        
         MVC   MSG03NO(8,R01),RPLRBAR                                           
         DROP  R08                                                              
*                                                                               
         CLOSE  (IRDR)                                                          
         WTO   MF=(E,MSG03)    INDICATE JCL FILE PROCESSED                      
         L     R14,MAKE14                                                       
         L     R13,MAKE13                                                       
         BR    R14                                                              
         EJECT                                                                  
DATA     DSECT                                                                  
MAKE14   DS    1F                                                               
MAKE13   DS    1F                                                               
COMPTR   DS    1F                                                               
INPARM   DS    1F                                                               
OPECB    DS    1F        ECB USED FOR WTOR                                      
STARTIME DS    1F        TIME MACRO RESULT                                      
         DS    XL3       DATE                                                   
DATETIME DS    0CL13     UNPACKED TIME & DATE                                   
DTHR     DS    CL2       HOUR                                                   
DTMN     DS    CL2       MIN                                                    
DTSC     DS    CL2       SEC                                                    
         DS    CL2       WHO KNOWS                                              
DTYR     DS    CL2       YEAR                                                   
DTDAY    DS    CL3       DAY JULIAN                                             
PARMCHAR DS    CL1       1 CHARACTER INPUT PARAMETER                            
SMFSID   DS    CL4       SMF'S SYSTEM ID                                        
JOBNAME  DS    CL8       JOB NAME FROM TIOT                                     
OPANSER  DS    CL3       OPERATOR REPLY AREA                                    
IOBUFFER DS    CL90      AREA FOR READING AND WRITING CARD IMAGES               
SAVE     DS    50F                                                              
         SPACE 2                                                                
CRAMON   CSECT                                                                  
SIZE     DC    F'400'    REMEMBER THE DOUBLE WORD ALIGNMENT OF GETMAIN          
PM6      DC    C'12'      WAS 10                                                
PM9      DC    C'15'      WAS 13                                                
AM3      DC    C'21'      WAS 19                                                
DSYSIN   DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GM                                   
DJCLIN   DCB   DDNAME=JCLIN,DSORG=PS,MACRF=GM,EODAD=EODADJCL                    
DLOG     DCB   DDNAME=LOG,RECFM=FBA,LRECL=133,BLKSIZE=1330,MACRF=PM,   X        
               DSORG=PS                                                         
IRDR     ACB   AM=VSAM,MACRF=(ADR,SEQ,OUT),DDNAME=IRDR                          
OUTRPL   RPL ACB=IRDR,OPTCD=(ADR,SEQ,SYN,NUP),RECLEN=80,               X        
               AREALEN=80                                                       
SPOOF    XCTL  EP=SPOOF,SF=L                                                    
TIOTADDR DS    1F                                                               
         SPACE 2                                                                
WTP      EQU   11                                                               
MSG01    WTO   'DDS-NYC CRAFT MONITOR IN CONTROL AT: XX:XX:XX XX.XXX', X        
               ROUTCDE=(2),MF=L                                                 
WTOBASE  EQU   5+36                                                             
MSG01HR  EQU   WTOBASE                                                          
MSG01MN  EQU   WTOBASE+3                                                        
MSG01SC  EQU   WTOBASE+6                                                        
MSG01YR  EQU   WTOBASE+9                                                        
MSG01DY  EQU   WTOBASE+12                                                       
MSG01E   WTO   'TRANSFERRING CONTROL TO SPOOF',                        X        
               ROUTCDE=(2),MF=L                                                 
MSG02A   WTO   'JCL FILE OPENED -- OK',                                X        
               ROUTCDE=(11),MF=L                                                
MSG02B   WTO   'JCL FILE PROCESSED OK',                                X        
               ROUTCDE=(11),MF=L                                                
MSG02C   WTO   'SYSIN FILE PROCESSED OK',                              X        
               ROUTCDE=(11),MF=L                                                
MSG03    WTO   'JOB: XXXXXX    SUBMITTED       ',                      X        
               ROUTCDE=(2),MF=L                                                 
MSG03NO  EQU   5+4                                                              
BRREG    EQU    15                                                              
RETREG   EQU    14                                                              
BASEREG  EQU    12                                                              
SAVEREG  EQU    13                                                              
DATAPTR  EQU    5                                                               
R00      EQU    00                                                              
R01      EQU    01                                                              
R02      EQU    02                                                              
R03      EQU    03                                                              
R04      EQU    04                                                              
R05      EQU    05                                                              
R06      EQU    06                                                              
R07      EQU    07                                                              
R08      EQU    08                                                              
R09      EQU    09                                                              
R10      EQU    10                                                              
R11      EQU    11                                                              
R12      EQU    12                                                              
R13      EQU    13                                                              
R14      EQU    14                                                              
R15      EQU    15                                                              
         EJECT                                                                  
         DCBD  DSORG=PS,DEVD=DA                                                 
         IFGRPL                                                                 
         IEFTIOT1  ,                                                            
         IEESMCA                                                                
         CVT   DSECT=YES                                                        
         IEFAJCTB          GET JCT/ACT                                          
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004FRCRAMON  05/01/02'                                      
         END                                                                    
