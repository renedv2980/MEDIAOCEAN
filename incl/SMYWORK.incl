*          DATA SET SMYWORK    AT LEVEL 009 AS OF 06/18/02                      
*********************  INSERT DATA AFTER   ****************************         
*                                                                               
NUM50B   XC    KEY,KEY                                                          
         MVC   KEY+4(3),=X'FFFFFF'       3X'FF'S IN REC KEY FOR ALL CLI         
         B     NUM50T                                                           
NUM50D   CLI   14(R2),C','               WAS CLIENT OR OFFICE SPECIFIED         
         BNE   ERROR                     (OR COULD BE 'ALL')                    
         OC    15(3,R2),BLANKS                                                  
         CLC   15(3,R2),BLANKS           IF ',BLANKS' ERROR                     
         BE    ERROR                                                            
         CLC   15(3,R2),=C'ALL'          WAS ALL SPECIFIED                      
         BE    NUM50B                                                           
         CLI   15(R2),C'*'               WAS OFFICE SPECIFIED                   
         BNE   NUM50H                                                           
         CLI   5(R2),9                   IF INPUT LONGER THEN THIS IS           
         BH    ERROR                     NOT A VALID OFFICE CODE                
*                                                                               
         LA    R3,SECLOCK          SECURITY LOCKOUT                             
*                                                                               
         OC    6(2,RA),6(RA)     CHK FOR LIMIT ACCESS                           
         BZ    NUM50G              NO LIMIT ACCESS                              
         MVC   TSTOFF,16(R2)      OFFICE CODE TO TSTOFF FOR                     
         BAS   RE,PPCLIVER              LIMIT ACCESS TESTING                    
         BE    NUM50G              OFFICE OK - IN LIST                          
         B     ERROR                                                            
*                                                                               
NUM50G   DS    0H                                                               
         MVC   KEY+4(3),=X'FF4040'       FOR OFFICE CODE IN REC, STORE          
         MVC   KEY+5(1),16(R2)           X'FF' , OFFICE CODE, BLANK             
         B     NUM50T                                                           
*                                                                               
NUM50H   LA    R3,40                     CLIENT NOT ON FILE ERROR MSG           
         XC    KEY,KEY                   READ CLIENT HEADER                     
         MVC   KEY(2),AGYALPHA                                                  
         MVC   KEY+2(1),CMTMD                                                   
         MVI   KEY+3,X'02'                                                      
         MVC   KEY+4(3),15(R2)                                                  
         BAS   RE,HIGH                                                          
         CLC   KEY(7),KEYSAVE                                                   
         BNE   ERROR                                                            
*                                                                               
         LA    R3,SECLOCK          SECURITY LOCKOUT                             
*                                                                               
         OC    6(2,RA),6(RA)     CHK FOR LIMIT ACCESS                           
         BZ    NUM50R              NO LIMIT ACCESS                              
*                                                                               
         BAS   RE,GETREC           GET THE CLIENT RECORD                        
         LA    R4,IOAREA                                                        
         USING PCLTREC,R4                                                       
*                                                                               
         MVC   TSTOFF,PCLTOFF     OFFICE CODE TO TSTOFF                         
*                                                                               
         CLI   TRFAGSW,C'Y'       TRAFFIC AGENCY ID ?                           
         BNE   NUM50P             NO                                            
*                                 SEE IF TRAFFIC OFFICE EXISTS                  
         LA    R5,PCLTREC+33                                                    
         MVI   ELCODE,X'50'       CLT TRAFFIC OFFICE ELEM CODE                  
         BAS   R9,NEXTEL          FOUND ?                                       
         BNE   NUM50P             NO                                            
         MVC   TSTOFF,2(R5)       REPLACE TSTOFF WITH TRAFFIC OFFICE            
NUM50P   BAS   RE,PPCLIVER        LIMIT ACCESS TESTING                          
         BE    NUM50R             OK                                            
         B     ERROR                                                            
*                                                                               
         DROP  R4                                                               
*                                                                               
NUM50R   DS    0H                                                               
         XC    KEY,KEY                                                          
         MVC   KEY+4(3),15(R2)                                                  
NUM50T   MVC   KEY(2),AGYALPHA                                                  
         MVC   KEY+2(1),CMTMD                                                   
         MVI   KEY+3,X'46'        PQUARCREC                                     
         MVC   KEY+7(1),13(R2)    MUST BE 1,2,3 OR 4                            
         B     NUM80                                                            
         EJECT                                                                  
*********************  INSERT DATA BEFORE  ****************************         
