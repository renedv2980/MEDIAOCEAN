*          DATA SET DDPANPAGE  AT LEVEL 006 AS OF 05/01/02                      
         TITLE 'MODULE TO READ TAPE AND OUTPUT PANPRINT CARDS/TAPE'             
PANPAGE  CSECT                                                                  
         NBASE 0,**PAGE**,=V(REGSAVE)                                           
         L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
         MVC   TITLE(21),=C'PANPAGE CONTROL CARDS'                              
         LA    R8,TAPE                                                          
         LA    R9,KHOUT                                                         
         LA    R7,T                                                             
         EJECT                                                                  
*              FIRSTLY READ CARDS AND OUTPUT ADDITIONAL DATA                    
         SPACE 3                                                                
CARD     GOTO1 =V(CARDS),PARA,P,=C'RE00'                                        
         CLC   P(11),=C'OUTPUT=DISK'                                            
         BNE   CARD1                                                            
         OPENR (R9)                                                             
         GOTO1 =V(PRINTER)                                                      
         MVI   OUTPUT,C'D'                                                      
         B     CARD                                                             
         SPACE 2                                                                
CARD1    DS    0H                                                               
         CLC   P(2),=C'/*'                                                      
         BNE   CARD2                                                            
         OPENR (R8)                                                             
         B     READ                                                             
         SPACE 2                                                                
CARD2    MVI   SPACING+3,C'2'                                                   
         BAS   RE,PUT                                                           
         GOTO1 =V(PRINTER)                                                      
         B     CARD                                                             
         EJECT                                                                  
*              NOW READ THE TAPE AND OUTPUT CARDS                               
         SPACE 3                                                                
READ     MVC   TITLE,SPACES                                                     
         MVC   TITLE(22),=C'PANPAGE OUTPUT RECORDS'                             
         ZAP   LINE,=P'75'                                                      
         SPACE 2                                                                
READ2    GET   (R8),(R7)                                                        
         CLI   T,SKIP                                                           
         BNE   READ6                                                            
         CLI   FIRST,C'Y'                                                       
         BE    READ4                                                            
         MVC   P(8),=C'PAGE=END'                                                
         BAS   RE,PUT                                                           
         SPACE 2                                                                
READ4    ZAP   MYLINE,=P'1'                                                     
         MVI   FIRST,C'N'                                                       
         MVC   P(10),=C'PAGE=START'                                             
         BAS   RE,PUT                                                           
         B     READ2                                                            
         SPACE 2                                                                
READ6    CLC   T+1(66),SPACES      ANY LH DATA                                  
         BE    READ8                                                            
         MVI   P,C'L'              GENERATE L CARD                              
         UNPK  P+1(2),MYLINE                                                    
         OI    P+2,X'F0'                                                        
         MVC   P+3(66),T+1                                                      
         BAS   RE,PUT                                                           
         SPACE 2                                                                
READ8    CLC   T+67(66),SPACES     ANY RH DATA                                  
         BE    READ10                                                           
         MVI   P,C'R'              GENERATE R CARD                              
         UNPK  P+1(2),MYLINE                                                    
         OI    P+2,X'F0'                                                        
         MVC   P+3(66),T+67                                                     
         BAS   RE,PUT                                                           
         SPACE 2                                                                
READ10   AP    MYLINE,=P'1'                                                     
         CLI   T,SINGLE                                                         
         BE    READ2                                                            
         AP    MYLINE,=P'1'                                                     
         CLI   T,DOUBLE                                                         
         BE    READ2                                                            
         AP    MYLINE,=P'1'                                                     
         B     READ2                                                            
         EJECT                                                                  
*              END OF FILE ROUTINES                                             
         SPACE 3                                                                
EOF      MVC   P(8),=C'PAGE=END'                                                
         BAS   RE,PUT                                                           
         MVC   P(2),=C'/*'                                                      
         BAS   RE,PUT                                                           
         MVC   P(2),=C'/&&'                                                     
         BAS   RE,PUT                                                           
         MVC   P(18),=C'CLOSE SYSIN,X''00C'''                                   
         BAS   RE,PUT                                                           
         CLOSER (R8)                                                            
         CLI   OUTPUT,C'D'                                                      
         BNE   EOF2                                                             
         CLOSER (R9)                                                            
         SPACE 2                                                                
EOF2     EOJ                                                                    
         EJECT                                                                  
*              ROUTINE TO OUTPUT PANPAGE CARDS/DISK RECORDS                     
         SPACE 3                                                                
PUT      NTR1                                                                   
         OC    P(132),=132X'40'                                                 
         CLI   OUTPUT,C'D'                                                      
         BE    PUT2                                                             
         GOTO1 =V(CARDS),PARA,P,=C'PE00'                                        
         B     PUT4                                                             
         SPACE 2                                                                
PUT2     PUT   (R9),(RA)                                                        
         SPACE 2                                                                
PUT4     MVI   SPACING+3,C'1'                                                   
         GOTO1 =V(PRINTER)                                                      
         XIT1  1                                                                
         EJECT                                                                  
*              DTF FOR OPTIONAL DISK FILE                                       
         SPACE 3                                                                
KHOUT    DTFSD                                                         X        
               BLKSIZE=88,                                             X        
               RECFORM=FIXUNB,                                         X        
               TYPEFLE=OUTPUT,                                         X        
               IOAREA1=DIO,                                            X        
               DEVICE=3340,                                            X        
               WORKA=YES                                                        
         SPACE 2                                                                
DIO      DS    CL88                                                             
         EJECT                                                                  
*              DTF FOR INPUT TAPE FILE                                          
         SPACE 3                                                                
TAPE     DTFMT                                                         X        
               BLKSIZE=133,                                            X        
               RECFORM=FIXUNB,                                         X        
               EOFADDR=EOF,                                            X        
               FILABL=NO,                                              X        
               DEVADDR=SYS020,                                         X        
               IOAREA1=TIO,                                            X        
               WORKA=YES                                                        
         SPACE 2                                                                
TIO      DS    CL133                                                            
         EJECT                                                                  
*              WORK SPACE FOR MODULE                                            
         SPACE 3                                                                
PARA     DS    6F                                                               
T        DS    CL133                                                            
FIRST    DC    C'Y'                                                             
MYLINE   DC    PL2'1'                                                           
OUTPUT   DC    C'C'                                                             
       ++INCLUDE DDDPRINT                                                       
SKIP     EQU   X'8B'                                                            
SINGLE   EQU   X'09'                                                            
DOUBLE   EQU   X'19'                                                            
TREBLE   EQU   X'1B'                                                            
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006DDPANPAGE 05/01/02'                                      
         END                                                                    
