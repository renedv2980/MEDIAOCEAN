*          DATA SET SPFILCOMP  AT LEVEL 016 AS OF 05/01/02                      
         TITLE 'FILE COMPARISON PROGRAM'                                        
FILCOMP  CSECT                                                                  
         NBASE 0,**FILCOMP,=V(REGSAVE),RA                                       
         SPACE 2                                                                
KEYLEN   EQU   13                  ***** USER DEFINED *****                     
         COMRG                                                                  
         MVC   UPSI,23(R1)                                                      
*                                  X'80' = PDUMP                                
*                                  X'40' = PDUMP ONLY 50                        
         TM    UPSI,X'40'                                                       
         BZ    *+10                                                             
         ZAP   MAX,=P'50'                                                       
         OPEN  FIL1,FIL2,FIL3                                                   
         SPACE 1                                                                
         BAS   R9,PRNT                                                          
         BAS   R9,PRNT                                                          
GETBOTH  BAS   R9,GET1                                                          
         BAS   R9,GET2                                                          
COMP     CLC   REC1(KEYLEN),REC2                                                
         BL    PUT1                                                             
         BH    PUT2                                                             
         CLC   REC1(KEYLEN),KEYFF                                               
         BE    END                                                              
         BAS   R9,CMPR                                                          
         BE    GETBOTH                                                          
         PUT   FIL3,REC2-4                                                      
         BAS   R9,GET2                                                          
         MVC   P(04),=C'UNEQ'                                                   
         B     PUT1                                                             
         MVC   STAT1,=C'UNEQUAL '                                               
         MVC   STAT2,=C'UNEQUAL '                                               
         BAS   R9,FMTR1                                                         
         BAS   R9,FMTR2                                                         
         AP    CNT5,=P'1'                                                       
         B     GETBOTH                                                          
         SPACE 3                                                                
PUT1     AP    CNT1,=P'1'                                                       
         TM    UPSI,X'C0'                                                       
         BZ    PUT1A                                                            
         MVC   STAT1,=C'MISSING '                                               
         BAS   R9,FMTR1                                                         
         B     PUT1B                                                            
PUT1A    DS    0H                                                               
         GOTO1 =V(HEXOUT),DMCB,REC1,P+10,KEYLEN,=C'NO'                          
         UNPK  P+4(4),CNT1                                                      
         OI    P+7,X'F0'                                                        
         BAS   R9,PRNT                                                          
PUT1B    DS    0H                                                               
         BAS   R9,GET1                                                          
         B     COMP                                                             
         SPACE 3                                                                
PUT2     AP    CNT2,=P'1'                                                       
         TM    UPSI,X'C0'                                                       
         BZ    PUT2A                                                            
         MVC   STAT2,=C'MISSING '                                               
         BAS   R9,FMTR2                                                         
         B     PUT2B                                                            
PUT2A    DS    0H                                                               
         GOTO1 =V(HEXOUT),DMCB,REC2,P+70,KEYLEN,=C'NO'                          
         UNPK  P+122(4),CNT2                                                    
         OI    P+125,X'F0'                                                      
         BAS   R9,PRNT                                                          
PUT2B    DS    0H                                                               
         BAS   R9,GET2                                                          
         B     COMP                                                             
         SPACE 3                                                                
GET1     CLC   REC1(KEYLEN),KEYFF                                               
         BER   R9                                                               
GET1A    GET   FIL1,REC1-4                                                      
         OC    REC1(KEYLEN-1),REC1                                              
         BNZ   *+12                                                             
         MVI   FS1,1               HEADER                                       
         B     GET1A                                                            
         CLC   REC1(KEYLEN),KEYFF                                               
         BNE   *+10                                                             
         MVI   FS2,1                                                            
         BR    R9                                                               
         AP    CNT3,=P'1'                                                       
         BR    R9                                                               
         SPACE 3                                                                
GET2     CLC   REC2(KEYLEN),KEYFF                                               
         BER   R9                                                               
GET2A    GET   FIL2,REC2-4                                                      
         OC    REC2(KEYLEN-1),REC2                                              
         BNZ   *+12                                                             
         MVI   FS3,1                                                            
         B     GET2A                                                            
         CLC   REC2(KEYLEN),KEYFF                                               
         BNE   *+10                                                             
         MVI   FS4,1                                                            
         BR    R9                                                               
         AP    CNT4,=P'1'                                                       
         BR    R9                                                               
         SPACE 3                                                                
END      LA    R3,CNT1                                                          
         LA    R4,5                                                             
         BAS   R9,PRNT                                                          
END1     MVC   P+25(20),8(R3)                                                   
         OI    7(R3),X'0F'                                                      
         UNPK  P+46(8),0(8,R3)                                                  
         BAS   R9,PRNT                                                          
         LA    R3,28(R3)                                                        
         BCT   R4,END1                                                          
         LA    R3,FS1                                                           
         LA    R4,4                                                             
END2     CLI   0(R3),0                                                          
         BE    END4                                                             
         MVC   P+25(20),1(R3)                                                   
         BAS   R9,PRNT                                                          
END4     LA    R3,21(R3)                                                        
         BCT   R4,END2                                                          
         CLOSE FIL1,FIL2                                                        
         EOJ                                                                    
         SPACE 3                                                                
PRNT     DS    0H                                                               
         GOTO1 =V(PRINT),DMCB,P,=C'BL01'                                        
         SPACE 1                                                                
         MVI   P,C' '                                                           
         MVC   P+1(131),P                                                       
         BR    R9                                                               
         SPACE 3                                                                
CMPR     DS    0H                                                               
         MVC   DUB(2),REC1+KEYLEN                                               
         LH    RF,DUB                                                           
         LA    R1,REC1                                                          
         LA    R2,REC2                                                          
CMPR2    CH    RF,=H'256'                                                       
         BL    CMPR4                                                            
         CLC   0(256,R1),0(R2)                                                  
         BNE   CMPRX                                                            
         LA    R1,256(R1)                                                       
         LA    R2,256(R2)                                                       
         SH    RF,=H'256'                                                       
         B     CMPR2                                                            
CMPR4    LTR   RF,RF                                                            
         BZ    CMPRX                                                            
         BCTR  RF,R0                                                            
         EX    RF,*+8                                                           
         B     CMPRX                                                            
         CLC   0(0,R1),0(R2)                                                    
CMPRX    BR    R9                                                               
         SPACE 3                                                                
FMTR1    DS    0H                                                               
         MVC   DUB(2),REC1+KEYLEN                                               
         LH    R3,DUB                                                           
         LA    R2,REC1-4-20                                                     
         B     FMTR                                                             
FMTR2    DS    0H                                                               
         MVC   DUB(2),REC2+KEYLEN                                               
         LH    R3,DUB                                                           
         LA    R2,REC2-4-20                                                     
FMTR     DS    0H                                                               
         ZAP   DUB,CNT1                                                         
         AP    DUB,CNT2                                                         
         AP    DUB,CNT5                                                         
         CP    DUB,MAX                                                          
         BHR   R9                                                               
         LA    R3,24(R2,R3)                                                     
         PDUMP (R2),(R3)                                                        
         BR    R9                                                               
*                                                                               
         EJECT                                                                  
EOT1     DS    0H                                                               
         CLOSE FIL1                                                             
EOT1B    DS    0H                                                               
         MVC   CONMSG(29),=C'EOF OR EOV ON FILE 1- SYS010?'                     
         BAS   RE,CONPUT                                                        
         BAS   RE,CONGET                                                        
         OC    CONANS,=3C' '                                                    
         CLC   CONANS,=C'EOF'                                                   
         BE    EOT1D                                                            
         CLC   CONANS,=C'EOV'                                                   
         BNE   EOT1B                                                            
         OPEN  FIL1                                                             
         B     GET1                                                             
EOT1D    DS    0H                                                               
         MVC   REC1(KEYLEN),KEYFF                                               
         B     GET1                                                             
         SPACE 3                                                                
EOT2     DS    0H                                                               
         CLOSE FIL2                                                             
EOT2B    DS    0H                                                               
         MVC   CONMSG(29),=C'EOF OR EOV ON FILE 2-SYS011?'                      
         BAS   RE,CONPUT                                                        
         BAS   RE,CONGET                                                        
         OC    CONANS,=3C' '                                                    
         CLC   CONANS,=C'EOF'                                                   
         BE    EOT2D                                                            
         CLC   CONANS,=C'EOV'                                                   
         BNE   EOT2B                                                            
         OPEN  FIL2                                                             
         B     GET2                                                             
EOT2D    DS    0H                                                               
         MVC   REC2(KEYLEN),KEYFF                                               
         B     GET2                                                             
         SPACE 3                                                                
CONPUT   DS    0H                                                               
         LR    R0,RE                                                            
         GOTO1 =V(LOGIO),DMCB,1,(29,CONMSG)                                     
*                                                                               
         LR    RE,R0                                                            
         BR    RE                                                               
         SPACE 3                                                                
CONGET   DS    0H                                                               
         LR    R0,RE                                                            
         GOTO1 =V(LOGIO),DMCB,0,(3,CONANS)                                      
*                                                                               
         LR    RE,R0                                                            
         BR    RE                                                               
         SPACE 3                                                                
DMCB     DC    6F'0'                                                            
P        DC    132C' '                                                          
CONANS   DS    CL3                                                              
CONMSG   DS    CL29                                                             
*                                                                               
CNT1     DC    PL8'0'                                                           
         DC    CL20'MISSING ON FILE 2'                                          
CNT2     DC    PL8'0'                                                           
         DC    CL20'MISSING ON FILE 1'                                          
CNT3     DC    PL8'0'                                                           
         DC    CL20'FILE 1 TOTAL'                                               
CNT4     DC    PL8'0'                                                           
         DC    CL20'FILE 2 TOTAL'                                               
CNT5     DC    PL8'0'                                                           
         DC    CL20'UNEQUAL RECORDS'                                            
FS1      DC    X'00'                                                            
         DC    CL20'HAVE FILE1 HEADER'                                          
FS2      DC    X'00'                                                            
         DC    CL20'HAVE FILE 1 TRAILER'                                        
FS3      DC    X'00'                                                            
         DC    CL20'HAVE FILE 2 HEADER'                                         
FS4      DC    X'00'                                                            
         DC    CL20'HAVE FILE 2 TRAILER'                                        
UPSI     DS    X                                                                
DUB      DS    D                                                                
MAX      DC    PL8'9999'                                                        
KEYFF    DC    100X'FF'            MUST BE AT LEAST KEYLEN LONG                 
         SPACE 3                                                                
         LTORG                                                                  
         EJECT                                                                  
FIL1     DTFMT DEVADDR=SYS010,BLKSIZE=8500,RECSIZE=2100,RECFORM=VARBLK,X        
               IOAREA1=IN1,WORKA=YES,TYPEFLE=INPUT,EOFADDR=EOT1,       X        
               FILABL=NO                                                        
         SPACE 3                                                                
FIL2     DTFMT DEVADDR=SYS011,BLKSIZE=8500,RECSIZE=2100,RECFORM=VARBLK,X        
               IOAREA1=IN2,WORKA=YES,TYPEFLE=INPUT,EOFADDR=EOT2,       X        
               FILABL=NO                                                        
         SPACE 3                                                                
FIL3     DTFMT DEVADDR=SYS012,BLKSIZE=8000,RECFORM=VARBLK,             X        
               IOAREA1=OUT1,WORKA=YES,TYPEFLE=OUTPUT,FILABL=NO                  
         SPACE 3                                                                
         DC    C'**RECORD 1**'                                                  
STAT1    DC    CL8' '                                                           
         DS    F                                                                
REC1     DS    2500C                                                            
         DC    C'**RECORD 2**'                                                  
STAT2    DC    CL8' '                                                           
         DS    F                                                                
REC2     DS    2500C                                                            
IN1      DS    8500C                                                            
IN2      DS    8500C                                                            
OUT1     DS    8500C                                                            
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'016SPFILCOMP 05/01/02'                                      
         END                                                                    
