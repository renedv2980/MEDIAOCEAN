*          DATA SET FRTEST     AT LEVEL 002 AS OF 05/01/02                      
EXCPDRV  CSECT                                                                  
         USING *,R15                                                            
         STM   R14,R12,12(R13)  SAVE REGS IN CALLER SAVE AREA                   
         BASR  R12,0                                                            
         USING *,R12                                                            
         L     R00,SIZE                                                         
         GETMAIN R,LV=(0)                                                       
         LR    R10,R01                                                          
         USING DATA,R10                                                         
         ST    R13,SAVE+4       SAVE CALLERS SAVE AREA IN MINE                  
         LA    R03,SAVE                                                         
         ST    R03,8(R13)       STORE MY SAVE AREA IN HIS                       
         LR    R13,R03          SET R13 TO MY SAVE AREA                         
         EJECT                                                                  
         LA    R02,LOGSNAP      GET DCB ADDRESS FOR SNAP FILE                   
         OPEN  ((2),OUTPUT)                                                     
         TM    48(R02),X'10'    TEST OPEN OK                                    
         BO    EXCP01                                                           
         LH    R03,=XL2'0040'   LOAD ABEND CODE                                 
         B     EXCPABND                                                         
EXCP01   LA    R02,MODLDCB     GET DCB ADDRESS                                  
*NOP     MVC   0(LEXCPDCB,R02),MODLDCB                                          
*NOP     OPEN  ((2)),MF=(E,OPENLST)  OPEN THE DCB                               
         OPEN  ((2),OUTPUT)          OPEN THE DCB                               
         TM    48(R02),X'10'    TEST THE OPEN FLAGS                             
         BO    EXCP02           BRANCH IF OPEN                                  
         LH    R03,=XL2'0041'   LOAD ABEND CODE                                 
         B     EXCPABND                                                         
EXCP02   EQU   *                                                                
         LA    R04,REC1                                                         
         STCM  R04,7,TAPEWRTA   STORE REC ADDRESS                               
         LA    R04,REC2                                                         
         STCM  R04,7,TAPEWR2A   STORE REC ADDRESS                               
         BAS   R14,TAPEEXCP                                                     
         BAS   R14,TAPEEXCP                                                     
         BAS   R14,TAPEEXCP                                                     
         BAS   R14,TAPEEXCP                                                     
         BAS   R14,TAPEEXCP                                                     
ALLDONE  EQU   *                                                                
         LA    R04,EXCPDCB                                                      
         CLOSE ((4))                                                            
EXIT     L     R00,SIZE                                                         
         L     R03,4(,R13)      GET PTR TO CALLERS SAVE AREA                    
         LR    R01,R10                                                          
      FREEMAIN R,LV=(0),A=(1)                                                   
         L     R14,12(,R03)     PICK UP RETURN ADDRESS                          
         LM    R00,R12,20(R03) RESTORE REGS                                     
         BR    R14             RETURN TO CALLER                                 
EXCPABND ABEND (R03),,STEP,USER ABEND                                           
         EJECT                                                                  
***                                                                             
***  IOB CONSTRUCTION, ISSUE EXCP & WAIT ROUTINE FOR TAPE                       
***                                                                             
TAPEEXCP LA    R03,EXCPIOB                                                      
         USING IOB,R03                                                          
         XC    EXCPIOB(32),EXCPIOB  CLEAR OUT IOB (TAPE)                        
         STCM  R02,7,IOBDCBPB   STORE DCB ADDRESS                               
         MVI   IOBFLAG1,X'80'   SET DC/  /UNRELATED/NOAPPENDG                   
*NOP     MVI   IOBFLAG1,X'03'   SET DC/CC/UNRELATED/NOAPPENDG                   
         LA    R04,TAPEWRT      GET TAPE CCW ADDRESS CHAIN                      
         STCM  R04,7,IOBSTRTB   STORE CCW ADDRESS                               
         LA    R04,EXCPECB                                                      
         STCM  R04,7,IOBECBPB   STORE ECB ADDRESS                               
         SR    R05,R05                                                          
         ST    R05,EXCPECB      CLEAR THE ECB                                   
         EXCP  IOBSTDRD                                                         
         WAIT  ECB=EXCPECB                                                      
         CLI   EXCPECB,IOOK     DID I/O COMPLETE OK                             
         BNE   HANDLERR                                                         
         BR    R14                                                              
HANDLERR EQU   *                                                                
         L     R04,IOBSTDRD     GET THE SENSE BYTES                             
         LA    R05,MODLDCB                                                      
         LA    R06,IOBSTRT                                                      
         O     R06,=X'10000000'                                                 
         LA    R07,LOGSNAP      GET THE DCB ADDRESS                             
         SNAP  DCB=(R07),STORAGE=(R05,R06),PDATA=(REGS,JPA)                     
         LTR   R09,R15          SAVE SNAP RETURN CODE                           
         CLI   EXCPECB,X'41'    PERM ERROR COMPLETION?                          
         BE    0(R14)           RETURN                                          
         CLOSE ((7))                                                            
         LH    R03,=XL2'0008'   LOAD ABEND CODE                                 
         L     R08,EXCPECB                                                      
         ABEND (R03),DUMP,STEP,USER ABEND                                       
         EJECT                                                                  
         LTORG                                                                  
         DS    0D                                                               
TAPEWRT  DC    X'01'               FORMAT 0 CCW                                 
TAPEWRTA DC    AL3(0)                                                           
         DC    X'A000'         80  DATA CHAIN                                   
         DC    X'0100'             BYTE COUNT                                   
         DS    0D                                                               
TAPEWRT2 DC    X'01'               THIS CCW IS DATA CHAINED                     
TAPEWR2A DC    AL3(0)              FROM PRIOR CCW                               
         DC    X'2000'             SLI                                          
         DC    X'0380'             BYTE COUNT                                   
         DC   C'DCB*'                                                           
MODLDCB  DCB   DDNAME=EXCPDD,MACRF=(E),REPOS=N,EODAD=ALLDONE,         XC        
               DEVD=TA,DSORG=PS                                                 
LEXCPDCB EQU   *-MODLDCB                                                        
LOGSNAP  DCB   DDNAME=LOGSNAP,RECFM=VBA,LRECL=125,BLKSIZE=882,MACRF=W, X        
               DSORG=PS                                                         
         EJECT                                                                  
         DS    0F                                                               
         DC   C'ECB*'                                                           
EXCPECB  DS    1F                                                               
         DC   C'IOB*'                                                           
EXCPIOB  DS    30F                                                              
IOBSTRT  EQU   *                                                                
         IEZIOB                                                                 
IOBEND   EQU   *                                                                
IOBLNGH  EQU   *-IOBSTRT                                                        
IOBLEN   DC  A(IOBEND-IOBSTRT)                                                  
DATA     DSECT                                                                  
SAVE     DS    50F                                                              
OPENLST  OPEN  (,OUTPUT),MF=L     THE DCB                                       
EXCPDCB  DS    30F                                                              
REC1     DC    CL8'12345678'                                                    
REC2     DC    CL80'***********  80 BYTES OF DATA *************'                
EXCPDRV CSECT                                                                   
SIZE     DC    F'8192'   REMEMBER THE DOUBLE WORD ALIGNMENT OF GETMAIN          
IOOK     EQU   X'7F'                                                            
BRREG    EQU    15                                                              
RETREG   EQU    14                                                              
BASEREG  EQU    12                                                              
SAVEREG  EQU    13                                                              
DATAPTR  EQU    5                                                               
R00      EQU    00                                                              
R01      EQU    01                                                              
R02      EQU    02                                                              
R03      EQU    03                                                              
R04      EQU    04                                                              
R05      EQU    05                                                              
R06      EQU    06                                                              
R07      EQU    07                                                              
R08      EQU    08                                                              
R09      EQU    09                                                              
R10      EQU    10                                                              
R11      EQU    11                                                              
R12      EQU    12                                                              
R13      EQU    13                                                              
R14      EQU    14                                                              
R15      EQU    15                                                              
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002FRTEST    05/01/02'                                      
         END                                                                    
