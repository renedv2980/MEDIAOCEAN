*          DATA SET TATARA     AT LEVEL 005 AS OF 06/03/14                      
***********************************************************************         
*          ROUTINE SAVES NEW BILLING RULES INFORMATION                *         
*          R1 =A(PLACE TO STORE TARA ELEMENT FOR AGENCY OR CLIENT)    *         
***********************************************************************         
                                                                                
SAVENBR  NTR1  BASE=*,LABEL=*                                                   
         L     R1,0(R1)                                                         
         XC    0(TARALNQ,R1),0(R1)   CLEAR BILLING RULES                        
                                                                                
         TM    TBOPTS,TBRERUN        IF JOB IS A RERUN, SEE IF INVOICE          
         JO    SNBR10                HAS BRATES                                 
                                                                                
         TM    TGSYSTAT,TASYSBRT     IF BILLING IS READING BRATES               
         JZ    XIT                                                              
                                                                                
         USING TARAD,R4                                                         
SNBR10   L     R4,AIO                                                           
         MVI   ELCODE,TARAELQ        GET NEW BILLING RULES                      
         BRAS  RE,GETEL                                                         
         JNE   XIT                                                              
         MVC   0(TARALNQ,R1),0(R4)   SAVE TARA ELEMENT                          
         J     XIT                                                              
         DROP  R4                                                               
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        ROUTINE SETS CURRENT BILLING RATES                           *         
*        R1 =A(INVOICE TARA ELEMENT), EXIT IF RATES ALREADY SET       *         
*        IF BOVER, UPDATE FIELDS THAT WERE NOT OVERWRITTEN                      
*        IF NOT, SET WITH AGENCY OR CLIENT RATES                      *         
***********************************************************************         
                                                                                
SETCBR   NTR1  BASE=*,LABEL=*                                                   
         CLI   4(R1),CREDITBR        IF CREDIT INVOICE WITH BRATE,              
         JNE   SCBR05                                                           
         OC    0(TARALNQ,R1),0(R1)   IF INVOICE LEVEL TARA EXISTS,              
         JZ    SCBR40                                                           
         J     XIT                   LEAVE TARA ELEMENT ALONE                   
                                                                                
SCBR05   L     R1,0(R1)              R1 --> INVOICE TARA ELEMENT                
         OC    0(TARALNQ,R1),0(R1)   CHECK IF SET BY INVOICE RATES              
         JZ    SCBR40                                                           
                                                                                
         L     R2,ACLITARA                                                      
         OC    0(TARALNQ,R2),0(R2)   DO WE HAVE CLIENT RATES?                   
         JNZ   SCBR10                                                           
         L     R2,AAGYTARA                                                      
         OC    0(TARALNQ,R2),0(R2)   DO WE HAVE AGENCY RATES?                   
         JZ    XIT                                                              
SCBR10   BRAS  RE,UPDBOVR            UPDATE BOVER WITH AGY/CLI BRATE            
         BRAS  RE,CPYAGYB            COPY FIELDS FROM AGENCY BRATE              
         J     XIT                                                              
                                                                                
SCBR40   L     R2,ACLITARA                                                      
         OC    0(TARALNQ,R2),0(R2)   CHECK IF SET BY CLIENT RATES               
         JZ    SCBR50                                                           
         MVC   0(TARALNQ,R1),0(R2)   SAVE RATES AS CURRENT RATES                
                                                                                
         BRAS  RE,CPYAGYB            COPY FIELDS FROM AGENCY BRATE              
         J     XIT                                                              
                                                                                
SCBR50   L     R2,AAGYTARA                                                      
         OC    0(TARALNQ,R2),0(R2)   CHECK IF SET BY AGENCY RATES               
         JZ    XIT                                                              
         MVC   0(TARALNQ,R1),0(R2)   SAVE RATES AS CURRENT RATES                
         J     XIT                                                              
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
CREDITBR EQU   X'80'                 BRATE CREDIT INVOICE                       
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        ROUTINE UPDATES BOVER FIELDS THAT WERE NOT FILLED IN         *         
*        WITH PERCENTS ON CLIENT OR AGENCY BRATE RECORD               *         
*        R1 =A(INVOICE TARA ELEMENT), R2=A(CLIENT OR AGENCY TARA ELEM)*         
***********************************************************************         
UPDBOVR  NTR1  BASE=*,LABEL=*                                                   
                                                                                
INVTARA  USING TARAD,R1                                                         
AGCTARA  USING TARAD,R2                                                         
                                                                                
         MVC   INVTARA.TARATYPE,AGCTARA.TARATYPE  BILL TYPE ON AGY/CLI          
         MVC   INVTARA.TARASTA1,AGCTARA.TARASTA1  WAGE BREAK                    
         MVC   INVTARA.TARAGRUL,AGCTARA.TARAGRUL  APPL GRT RULE                 
                                                                                
         TM    INVTARA.TARASTA2,TARASSHD    BASIC HAND IN DOLLARS?              
         JNZ   *+16                                                             
         MVC   INVTARA.TARAHFB,AGCTARA.TARAHFB   SESSION BASIC                  
         MVC   INVTARA.TARARFB,AGCTARA.TARARFB   REUSE BASIC                    
                                                                                
         TM    INVTARA.TARASTA2,TARASSCD    CAN HAND IN DOLLARS?                
         JNZ   *+16                                                             
         MVC   INVTARA.TARAHFBC,AGCTARA.TARAHFBC  SESSION CAN                   
         MVC   INVTARA.TARARFBC,AGCTARA.TARARFBC  REUSE CAN                     
                                                                                
         TM    INVTARA.TARASTA2,TARASSPD    PREM HAND IN DOLLARS?               
         JNZ   *+16                                                             
         MVC   INVTARA.TARAHFP,AGCTARA.TARAHFP   SESSION PREM                   
         MVC   INVTARA.TARARFP,AGCTARA.TARARFP   REUSE PREM                     
                                                                                
         TM    INVTARA.TARASTA2,TARASFUD    FUTA IN DOLLARS?                    
         JNZ   *+10                                                             
         MVC   INVTARA.TARATFU,AGCTARA.TARATFU                                  
                                                                                
         TM    INVTARA.TARASTA2,TARASSUD    SUTA IN DOLLARS?                    
         JNZ   *+10                                                             
         MVC   INVTARA.TARATSU,AGCTARA.TARATSU                                  
                                                                                
         TM    INVTARA.TARASTA2,TARASFID    FICA IN DOLLARS?                    
         JNZ   *+10                                                             
         MVC   INVTARA.TARATFI,AGCTARA.TARATFI                                  
                                                                                
         TM    INVTARA.TARASTA2,TARASMED    MEDICARE IN DOLLARS?                
         JNZ   *+10                                                             
         MVC   INVTARA.TARATM,AGCTARA.TARATM                                    
                                                                                
         TM    INVTARA.TARASTA2,TARASWCD    WC IN DOLLARS?                      
         JNZ   *+10                                                             
         MVC   INVTARA.TARATWC,AGCTARA.TARATWC                                  
         J     XIT                                                              
                                                                                
         DROP  INVTARA,AGCTARA                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        ROUTINE COPIES AGENCY LEVEL FIELDS FROM AGENCY BRATE                   
*        ONTO CURRENT BRATE OR BOVER (EXCEPT FOR CREDIT INVOICES)               
*        R1 =A(CURRENT TARA ELEMENT)                                            
*        THIS ROUTINE OVERWRITES AGY PREM STATUS FROM UPDBOVR ROUTINE           
***********************************************************************         
CPYAGYB  NTR1  BASE=*,LABEL=*                                                   
                                                                                
CURTARA  USING TARAD,R1                                                         
AGYTARA  USING TARAD,R3                                                         
                                                                                
         L     R3,AAGYTARA           COPY FIELDS FROM AGENCY BRATE              
         MVC   CURTARA.TARASGNS,AGYTARA.TARASGNS     SIG FEE RULE               
         MVC   CURTARA.TARACOMM,AGYTARA.TARACOMM     AGY COMM RULE              
         MVC   CURTARA.TARAGCAP,AGYTARA.TARAGCAP     GRT CAP                    
         MVC   CURTARA.TARAAACO,AGYTARA.TARAAACO     AGY COMM RATE              
         MVC   CURTARA.TARAASFE,AGYTARA.TARAASFE     SIG FEE RATE               
         MVC   CURTARA.TARAASCA,AGYTARA.TARAASCA     SIG FEE CAP                
                                                                                
         TM    AGYTARA.TARASTA1,TARASPRE    IS AGY PREM SERV?                   
         JZ    CAGY10                                                           
         OI    CURTARA.TARASTA1,TARASPRE    YES, TURN ON PREMIUM STAT           
         J     XIT                                                              
                                                                                
CAGY10   NI    CURTARA.TARASTA1,X'FF'-TARASPRE   BASIC, TURN OFF PREM           
         J     XIT                                                              
                                                                                
         DROP  CURTARA,AGYTARA                                                  
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        IF BOVER EXISTS, SET STATUS FOR HANDLING OR TAX OVERRIDES    *         
***********************************************************************         
         USING TARAD,R1                                                         
SETBVST  NTR1  BASE=*,LABEL=*                                                   
         MVI   BOVRSTAT,0          RESET BOVER STATUS 1                         
         L     R1,0(R1)                                                         
         OC    0(TARALNQ,R1),0(R1) DO WE HAVE A TARA ELEMENT?                   
         JZ    XIT                                                              
                                                                                
         TM    TARASTA2,TARASSHD   IS HANDLING OVERRIDDEN?                      
         BZ    *+8                                                              
         OI    BOVRSTAT,BOVRHNDB   BASIC HANDLING OVERRIDE                      
                                                                                
         TM    TARASTA2,TARASSCD   IS CAN HAND OVERRIDDEN?                      
         BZ    *+8                                                              
         OI    BOVRSTAT,BOVRHNDC   CANADIAN HANDLING OVERRIDE                   
                                                                                
         TM    TARASTA2,TARASSPD   IS PREM HAND OVERRIDDEN?                     
         BZ    *+8                                                              
         OI    BOVRSTAT,BOVRHNDP   PREM HANDLING OVERRIDE                       
                                                                                
*                                  ANY PAYROLL TAXES OVERRIDDEN?                
         TM    TBIPST2,TAPDPEUR    IGNORE IF EUROS                              
         JNZ   XIT                                                              
         TM    TARASTA2,TARASFUD+TARASSUD+TARASFID+TARASMED+TARASWCD            
         JZ    XIT                                                              
         OI    BOVRSTAT,BOVRTAX    TAX OVERRIDE                                 
         J     XIT                                                              
                                                                                
         DROP  R1                                                               
         LTORG                                                                  
         EJECT                                                                  
