*          DATA SET POST45     AT LEVEL 002 AS OF 05/01/02                      
*--------------------------------------------------------------                 
*        BUILD DESCRIPTION ELEMENT                                              
*--------------------------------------------------------------                 
*                                                                               
         LA    R8,IOAREA+2                                                      
         USING DLDESCD,R8                                                       
         MVI   DLDSEL,X'64'                                                     
         MVC   DLDSREF,SAVEDOC                                                  
         MVC   DLDSDATE,DATE3                                                   
         MVI   DLDSSBRF,0                                                       
         MVI   DLDSSTAT,0                                                       
         OI    DLDSSTAT,X'08'                                                   
         XC    DLDSNARR,DLDSNARR                                                
         MVC   DLDSNARR(120),SAVENARR                                           
         LH    R6,LEN              LENGTH OF NARRATIVE STRING                   
         LA    R5,DLDSNARR                                                      
         SR    R5,R8               LENGTH OF ELEMENT-NARRATIVE                  
         AR    R5,R6                                                            
         STH   R5,HALF                                                          
         MVC   DLDSLEN,HALF+1                                                   
         EJECT                                                                  
*--------------------------------------------------------------                 
*        BUILD POSTING ELEMENTS                                                 
*--------------------------------------------------------------                 
*                                                                               
         AR    R8,R5               BUMP TO NEXT ELEMENT                         
         USING DLPOSTD,R8                                                       
         MVI   DLPSLEN,X'71'                                                    
         MVI   DLPSTYPE,0                                                       
         ZAP   DLPSAMNT,POSTCASH                                                
         MVC   DLPSANAL,WRKCODE                                                 
         CLI   POSTDC,C'C'                                                      
         BE    ATR250                                                           
         MVC   DLPSDBAC(51),POSTACC        NUMBER AND NAME                      
         MVC   DLPSCRAC(51),POSTCAC        NUMBER AND NAME                      
         MVI   DLPSEL,X'69'          SINGLE DEBIT                               
         B     ATR260                                                           
*                                                                               
ATR250   MVC   DLPSCRAC(51),POSTACC        NUMBER AND NAME                      
         MVC   DLPSDBAC(51),POSTCAC        NUMBER AND NAME                      
         MVI   DLPSEL,X'6A'          SINGLE CREDIT                              
         EJECT                                                                  
*                                                                               
ATR260   CLC   POSTACC+1(2),=C'SE' IF ACCOUNT IS S/E                            
         BE    ATR260A                                                          
         CLC   POSTACC+1(2),=C'GP' OR G/P                                       
         BNE   ATR260B                                                          
*                                                                               
ATR260A  CLI   POSTDC,C'C'         AND IT'S A CREDIT, THEN                      
         BNE   ATR260B                                                          
         MP    POSTCASH,=P'-1'     ANALYSIS POSTINGS S/B REVERSED SIGN          
*                                                                               
ATR260B  CLI   STFSW,C'Y'                                                       
         BNE   ATR262                                                           
         LR    RF,R8                                                            
         ZIC   R3,DLPSLEN                                                       
         AR    R8,R3                                                            
         BCTR  R3,R0                                                            
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R8),0(RF)       SECOND ELEMENT                               
         LA    R3,1(R3)                                                         
         MVI   DLPSEL,X'69'        DEBIT STAFF                                  
         MVC   DLPSDBAC,STAFFNUM                                                
         MVC   DLPSDBNM,STAFFNAM                                                
         MVC   DLPSCRNM,CRPSNAME                                                
         ZAP   DLPSAMNT,POSTCASH                                                
         OI    DLPSTYPE,X'80'      SUBSIDIARY FROM HERE                         
         MVI   DLPSCRAC,C'*'       CONTRA IS *EXPENSE-CLIENT                    
         L     R5,SAVER2                                                        
         ZIC   R1,5(R5)            LENGTH OF ACCOUNT                            
         SH    R1,=H'3'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   DLPSCRAC+1(0),10(R5)                                             
         CLI   TENO,X'F0'                                                       
         BL    ATR261                                                           
         PACK  DUB,TENO                                                         
         CVB   R4,DUB                                                           
         MVC   DLPSCRAC+1(14),SPACES                                            
         L     R2,SAVER2                                                        
         MVI   ERRNUM,2                                                         
         LA    R6,1(R1)                                                         
         SR    R6,R4                                                            
         BM    ERROR                                                            
         LA    R3,10(R6,R2)                                                     
         BCTR  R4,0                                                             
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   DLPSCRAC+1(0),0(R3)                                              
         LR    R1,R4                                                            
*                                                                               
ATR261   STC   R1,BYTE                                                          
         LA    RF,DLPSCRAC+2(R1)                                                
         MVI   0(RF),C'-'                                                       
         LA    R1,DLPSCRAC+14                                                   
         SR    R1,RF                                                            
         BCTR  R1,R0                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   1(0,RF),CRPSNUM+3                                                
*                                                                               
         LR    RF,R8                                                            
         ZIC   R3,DLPSLEN                                                       
         AR    R8,R3                                                            
         BCTR  R3,R0                                                            
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R8),0(RF)                                                    
         LA    R3,1(R3)                                                         
         MVI   DLPSEL,X'6A'        CREDIT CLIENT                                
         MVC   DLPSDBAC,DLPSCRAC                                                
         MVC   DLPSCRAC,CRPSNUM                                                 
         IC    R1,BYTE                                                          
         LA    R1,1(R1)                                                         
         LA    RF,DLPSDBAC+1(R1)                                                
         LA    R1,DLPSDBAC+14                                                   
         SR    R1,RF                                                            
         BCTR  R1,R0                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   1(0,RF),STAFFNUM+3  CONTRA IS *EXPENSE-STAFF                     
*                                                                               
ATR262   DS    0H                                                               
         CLI   DEPSW,C'Y'                                                       
         BNE   ATR270                                                           
         ZIC   R3,DLPSLEN                                                       
         LR    RF,R8                                                            
         AR    R8,R3                                                            
         BCTR  R3,R0                                                            
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R8),0(RF)       THIRD ELEMENT                                
         LA    R3,1(R3)                                                         
         MVC   DLPSDBAC,DEPNUM                                                  
         MVC   DLPSDBNM,DEPNAME                                                 
         MVC   DLPSCRAC,CRDSNUM                                                 
         MVC   DLPSCRNM,CRDSNAME                                                
         ZAP   DLPSAMNT,POSTCASH                                                
         MVI   DLPSEL,X'68'                                                     
         OI    DLPSTYPE,X'80'      IN CASE WE MISSED IT ON ELEMENT 2            
*                                                                               
ATR270   DS    0H                                                               
         CLI   COSTBYTE,C' '                                                    
         BE    ATR280                                                           
         ZIC   R3,DLPSLEN                                                       
         LR    RF,R8                                                            
         AR    R8,R3                                                            
         BCTR  R3,R0                                                            
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R8),0(RF)                                                    
         MVI   DLPSEL,X'69'        DEBIT DEPT C/A CLIENT                        
         MVC   DLPSCRAC,COSTNUM                                                 
         MVC   DLPSCRNM,COSTNAME                                                
         MVC   DLPSDBAC,CRCNUM                                                  
         MVC   DLPSDBNM,CRCNAME                                                 
         ZAP   DLPSAMNT,POSTCASH                                                
*                                                                               
ATR274   OI    DLPSTYPE,X'80'                                                   
         LA    R3,1(R3)            BACK TO DLPSLEN AGAIN                        
         LR    RF,R8                                                            
         AR    R8,R3                                                            
         BCTR  R3,0                                                             
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R8),0(RF)                                                    
         MVI   DLPSEL,X'6A'        CREDIT CLIENT                                
         MVC   DLPSDBAC,CR13NUM                                                 
         MVC   DLPSDBNM,CR13NAME                                                
*                                                                               
ATR280   ZIC   R3,DLPSLEN                                                       
         AR    R8,R3                                                            
         MVI   0(R8),0             END OF RECORD                                
         LA    R4,IOAREA-1                                                      
         SR    R8,R4                                                            
         STH   R8,HALF                                                          
         MVC   IOAREA(2),HALF      FINAL LENGTH                                 
         BAS   RE,PUTDAY           ADD RECORDS TO TRANSACTION FILE              
