*          DATA SET TAPCSTLCK  AT LEVEL 005 AS OF 11/14/11                      
*======================================================================         
* !!!!!! CHANGES REQUIRE RELINKING TAGENBB AND TAGENBC                          
*======================================================================         
*                                                                               
*======================================================================         
*              CHECK IF MODEL/CAT IN COMML ALREADY                              
*======================================================================         
         USING TLCAPD,R4                                                        
CHKMODL  NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         MVC   SVKEY,KEY           SAVE KEY FOR RESTORE LATER                   
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         MVI   TLCAPCD,TLCACCDQ    LOOK UP BY SS#                               
         MVC   TLCACSSN,TGSSN      SS#                                          
         MVC   TLCAACOM,TGCOM      INTERNAL COMML #                             
         MVC   TLCACCAT,TGCAT      CATEGORY                                     
         GOTO1 HIGH                                                             
*                                                                               
         CLC   KEY(TLCAASEQ-TLCAPD),KEYSAVE     WAS IN COMML BEFORE?            
         BE    CHKMODLN                                                         
*                                                                               
         CR    RB,RB                                                            
         B     *+6                                                              
CHKMODLN LTR   RB,RB                                                            
         MVC   KEY,SVKEY           RESTORE KEY                                  
         XIT1                                                                   
         DROP  R4                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*======================================================================         
*              SET NEXT CAST SEQUENCE NUMBER IN CAST KEY                        
*======================================================================         
                                                                                
STNXTSEQ NTR1  BASE=*,LABEL=*                                                   
         MVC   SVKEY,KEY                                                        
*                                                                               
         MVC   AIO,AIO2            READ COMMERCIAL                              
         GOTO1 RECVAL,DMCB,TLCOCCDQ,(X'A4',0)                                   
         JE    *+6                                                              
         DC    H'00'                                                            
*                                                                               
         USING TANUD,R4                                                         
         MVI   ELCODE,TANUELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TANUTSEQ))                                     
         JE    *+6                                                              
         DC    H'00'                                                            
         L     R4,TGELEM           GET NEXT CAST SEQUENCE NUMBER                
*                                                                               
         USING TLCAD,R3                                                         
         LA    R3,KEY              SET NEXT CAST SEQUENCE NUMBER                
         MVC   KEY,SVKEY                                                        
         MVC   TLCASEQ,TANUNXTC    IN CAST RECORD KEY                           
         DROP  R3,R4                                                            
*                                                                               
         MVC   AIO,AIO1                                                         
         J     XIT                                                              
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*======================================================================         
*              UPDATE COMMERCIAL WITH NEXT CAST SEQUENCE NUMBER                 
*======================================================================         
                                                                                
UPNXTSEQ NTR1  BASE=*,LABEL=*                                                   
         GOTO1 RECVAL,DMCB,TLCOCCDQ,(X'B4',0)                                   
         JE    *+6                                                              
         DC    H'00'               READ COMMERCIAL FOR UPDATE                   
*                                                                               
         USING TANUD,R4                                                         
         MVI   ELCODE,TANUELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TANUTSEQ))                                     
         JE    *+6                                                              
         DC    H'00'                                                            
         L     R4,TGELEM           GET NEXT CAST SEQUENCE NUMBER                
         ZICM  R0,TANUNXTC,2       BUMP NEXT CAST SEQUENCE NUMBER               
         AHI   R0,1                                                             
         STCM  R0,3,TANUNXTC                                                    
         GOTO1 PUTREC              AND UPDATE COMMERCIAL RECORD                 
         J     XIT                                                              
         DROP  R4                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
