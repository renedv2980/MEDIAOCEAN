*          DATA SET TAREP1BE   AT LEVEL 079 AS OF 03/12/15                      
         SPACE 2                                                                
*              VALIDATE RECORD                                                  
         SPACE 1                                                                
VREC     NTR1                                                                   
         SPACE 1                                                                
         CLI   RECNUM,NYDISK       FOR NEW YORK DISK                            
         BE    VRNT                DIFFERENT SCREEN                             
         LA    R2,SPLPERH          PERIOD                                       
         GOTO1 ANY                 (REQUIRED)                                   
         ST    R2,APERH                                                         
         GOTO1 VALPERD                                                          
         MVI   TIQDTYPE,TIQDCHK    (FILTER ON CHECK DATE)                       
         SPACE 1                                                                
         LA    R2,SPLEMPH          EMPLOYER                                     
         CLI   RECNUM,UCTAPES      IF UCTAPES                                   
         BE    VREC2                                                            
         CLI   RECNUM,UCDISK       OR IF UCDISK - INPUT REQUIRED                
         BE    VREC2                                                            
         CLI   5(R2),0             (OPTIONAL)                                   
         BE    VREC4                                                            
VREC2    GOTO1 ANY                                                              
         MVC   TIFEMP,WORK                                                      
         GOTO1 RECVAL,DMCB,TLEMCDQ,(R2),0                                       
         SPACE 1                                                                
VREC4    LA    R2,SPLUNITH         TAX UNIT                                     
         CLI   RECNUM,UCDISK       IF UCDISK - INPUT REQUIRED                   
         BE    *+12                                                             
         CLI   5(R2),0             ELSE, OPTIONAL                               
         BE    VREC6                                                            
         GOTO1 ANY                                                              
         MVC   TIFUNIT,WORK        SET UNIT FOR SYSIO                           
         CLI   RECNUM,UCDISK       IF UCDISK                                    
         BNE   *+12                                                             
         BAS   RE,CHKUNIT          CHECK VALID UNIT                             
         BNE   FLDINV                                                           
         GOTO1 TAXVAL,DMCB,(3,WORK)                                             
         BNE   FLDINV                                                           
         SPACE 1                                                                
VREC6    LA    R2,SPLOPTH          VALIDATE OPTIONS                             
         BAS   RE,VOPTS                                                         
*&&DO                                                                           
         CLC   TIFUNIT,=C'FL '                                                  
         BNE   XIT                                                              
         CLI   PROCOPT,0                                                        
         BE    MISOPT                                                           
*&&                                                                             
         B     XIT                                                              
         SPACE 2                                                                
VRNT     LA    R2,SNTPERH          PERIOD                                       
         GOTO1 ANY                 (REQUIRED)                                   
         ST    R2,APERH                                                         
         GOTO1 VALPERD                                                          
         MVI   TIQDTYPE,TIQDCHK    (FILTER ON CHECK DATE)                       
*                                                                               
         LA    R2,SNTEMPH          EMPLOYER                                     
         GOTO1 ANY                 REQUIRED                                     
         MVC   TIFEMP,WORK                                                      
         GOTO1 RECVAL,DMCB,TLEMCDQ,(R2),0                                       
*                                                                               
         MVC   TIFUNIT,=C'NY '                                                  
         GOTO1 TAXVAL,DMCB,(3,TIFUNIT)                                          
*                                                                               
         LA    R2,SNTOPTH          VALIDATE OPTIONS                             
         BAS   RE,VOPTS                                                         
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO CHECK UNIT VALID FOR UCDISK                           
*                                                                               
CHKUNIT  NTR1                                                                   
         LA    RE,UCDSKTAB         RE=A(VALID UNITS TO DISK)                    
CHKUNIT5 CLI   0(RE),X'FF'                                                      
         BE    FLDINV                                                           
         CLC   TIFUNIT,0(RE)                                                    
         BNE   CHKUNIT8                                                         
         CLC   =CL3'ALL',3(RE)                                                  
         BE    YES                                                              
         CLC   TIFEMP,3(RE)                                                     
         BE    YES                                                              
CHKUNIT8 LA    RE,L'UCDSKTAB(RE)                                                
         B     CHKUNIT5                                                         
         SPACE 2                                                                
*              VALIDATE OPTIONS                                                 
         SPACE 1                                                                
VOPTS    NTR1                                                                   
         ZAP   TRALIMIT,=P'0'                                                   
         ZAP   RECLIMIT,=P'99999999'                                            
         ZAP   REPLIMIT,=P'99999999'                                            
         ZAP   MINVALUE,=P'0'                                                   
         MVI   READOPT,C'D'        DEFAULT TO READ DISK                         
         MVI   DOLLOPT,C'N'                NO DOLLARS                           
         MVI   GREYOPT,C'N'                NO GREY RECORDS                      
         MVI   SSSUI,C'N'                  NO SSSUI                             
         MVI   EXCESS,C'N'                 NO EXCESS                            
         MVI   OMNTHS,C'N'                 NO MONTH DETAIL                      
         MVI   STSTAT,0                    NO STATE FOR FLORIDA FORMAT          
***      MVI   PROCOPT,0                   PROCESS OPTION                       
*                                                                               
         CLI   5(R2),0                                                          
         BE    XIT                                                              
         GOTO1 SCANNER,DMCB,(R2),(10,BLOCK),0                                   
         ZIC   R0,4(R1)                                                         
         LA    R4,BLOCK                                                         
         LTR   R0,R0                                                            
         BZ    BADOPT                                                           
         SPACE 1                                                                
OPT2     CLC   12(5,R4),=C'TRACE'  TRACE OPTION                                 
         BNE   OPT4                                                             
         MVI   TRACOPT,C'Y'                                                     
         L     R1,8(R4)            (CHECK NUMERIC)                              
         LTR   R1,R1                                                            
         BZ    OPTEND                                                           
         CVD   R1,DUB                                                           
         ZAP   TRALIMIT,DUB                                                     
         B     OPTEND                                                           
         SPACE 1                                                                
OPT4     CLC   12(5,R4),=C'LIMIT'  LIMIT OPTION                                 
         BNE   OPT5                                                             
         L     R1,8(R4)            (CHECK NUMERIC)                              
         LTR   R1,R1                                                            
         BZ    BADOPT                                                           
         CVD   R1,DUB                                                           
         ZAP   RECLIMIT,DUB                                                     
         B     OPTEND                                                           
         SPACE 1                                                                
OPT5     CLC   12(6,R4),=C'REPORT' REPORT LIMIT                                 
         BNE   OPT6                                                             
         L     R1,8(R4)            (CHECK NUMERIC)                              
         LTR   R1,R1                                                            
         BZ    BADOPT                                                           
         CVD   R1,DUB                                                           
         ZAP   REPLIMIT,DUB                                                     
         B     OPTEND                                                           
         SPACE 1                                                                
OPT6     CLC   12(3,R4),=C'SSN'    TESTING OPTION 1 SS#                         
         BNE   OPT8                                                             
         MVC   TIFSSN,22(R4)       (CHECK NUMERIC)                              
         B     OPTEND                                                           
         SPACE 1                                                                
OPT8     CLC   12(6,R4),=C'AGENCY' AGENCY OPTION                                
         BNE   OPT8A                                                            
         CLI   OTHBRK,C'Y'                                                      
         BE    BADOPT                                                           
         MVC   TIFAGY,22(R4)                                                    
         B     OPTEND                                                           
         SPACE 1                                                                
OPT8A    CLC   12(6,R4),=C'OTHBRK'  AGENCY OTHERS BREAKDOWN                     
         BNE   OPT9                                                             
         CLI   1(R4),0                                                          
         BE    BADOPT                                                           
         OC    TIFAGY,TIFAGY        NOT VALID WITH AGENCY OPTION                
         BZ    BADOPT                                                           
         MVC   TIFAGY,22(R4)                                                    
         CLI   RECNUM,AGYPROF       ONLY VALID FOR AGY PROFITABILITY            
         BNE   BADOPT                                                           
         CLI   ACTEQU,DOWN          ONLY VALID FOR DOWNLOAD                     
         BNE   BADOPT                                                           
         MVI   OTHBRK,C'Y'                                                      
         B     OPTEND                                                           
         SPACE 1                                                                
OPT9     CLC   12(6,R4),=C'OFFICE' OFFICE OPTION                                
         BNE   OPT10                                                            
         MVC   TIFOFF,22(R4)                                                    
         B     OPTEND                                                           
         SPACE 1                                                                
OPT10    CLC   12(4,R4),=C'READ'   READ OPTION                                  
         BNE   OPT12                                                            
         MVC   READOPT,22(R4)                                                   
         B     OPTEND                                                           
         SPACE 1                                                                
OPT12    CLC   12(6,R4),=C'DOLLAR' DOLLAR OPTION                                
         BNE   OPT14                                                            
         MVC   DOLLOPT,22(R4)                                                   
         B     OPTEND                                                           
         SPACE 1                                                                
OPT14    CLC   12(4,R4),=C'GREY'   OPTION TO READ GREY RECORDS                  
         BNE   OPT16                                                            
         MVC   GREYOPT,22(R4)                                                   
         B     OPTEND                                                           
         SPACE 1                                                                
OPT16    CLC   12(3,R4),=C'MIN'    MINIMUM OPTION                               
         BNE   OPT18                                                            
         ZIC   R5,1(R4)                                                         
         LTR   R5,R5                                                            
         BZ    BADOPT                                                           
         GOTO1 CASHVAL,DMCB,(X'80',22(R4)),(R5)                                 
         CLI   DMCB,X'FF'                                                       
         BE    BADOPT                                                           
         MVC   MINVALUE,DMCB+4                                                  
         B     OPTEND                                                           
         SPACE 1                                                                
OPT18    DS    0H                                                               
         CLC   12(5,R4),=C'UTRAC'  TRACE UC RECORDS OPTION                      
         BNE   OPT20                                                            
         MVI   UTRACE,C'Y'                                                      
         B     OPTEND                                                           
         SPACE 1                                                                
OPT20    DS    0H                                                               
         CLC   12(5,R4),=C'SSSUI'  PRINT PERFORMERS SUI                         
         BNE   OPT22                                                            
         MVI   SSSUI,C'Y'                                                       
         B     OPTEND                                                           
*                                                                               
OPT22    DS    0H                                                               
*&&DO                                                                           
         CLC   12(5,R4),=C'EXEMP'  PRINT EXEMPTIONS FOR PERFORMER               
         BNE   OPT30                                                            
         MVI   EXEMP,C'Y'                                                       
         B     OPTEND                                                           
*&&                                                                             
OPT30    DS    0H                                                               
         CLC   12(4,R4),=C'TAPE'    WRITE TAPE                                  
         BNE   OPT40                                                            
         CLI   ACTEQU,DOWN         IF DOWNLOADING, THIS ISN'T VALID             
         BE    BADOPT                                                           
         MVI   WRITAPE,C'N'                                                     
         B     OPTEND                                                           
         SPACE 1                                                                
OPT40    DS    0H                                                               
         CLC   12(5,R4),=C'AMEND'   NY TAPE AMENDED DATA                        
         BNE   OPT50                                                            
         MVI   AMENDTAP,C'Y'                                                    
         B     OPTEND                                                           
         SPACE 1                                                                
OPT50    DS    0H                                                               
         CLC   12(6,R4),=C'EXCESS'  PRINT EXCESS WAGES                          
         BNE   OPT60                                                            
         MVI   EXCESS,C'Y'                                                      
         B     OPTEND                                                           
*                                                                               
OPT60    DS    0H                                                               
         CLC   12(6,R4),=C'MONTHS'  PRINT MONTHS IN DETAIL                      
         BNE   OPT70                                                            
         MVI   OMNTHS,C'Y'                                                      
         B     OPTEND                                                           
*                                                                               
OPT70    DS    0H                                                               
         CLC   12(8,R4),=C'FLFORMAT' USE FLORIDA FORMAT FOR TN                  
         BNE   OPT80                                                            
         CLI   RECNUM,UCTAPES        MUST BE UCTAPES                            
         BE    OPT75                                                            
         CLI   RECNUM,UCDISK         OR UCDISK                                  
         BNE   BADOPT                                                           
OPT75    CLC   TIFUNIT,=C'TN '       TENNESSEE ONLY                             
         BNE   BADOPT                                                           
         OI    STSTAT,STSTATTN       TN WANTS FLORIDA FORMAT                    
         B     OPTEND                                                           
*&&DO                                                                           
         CLC   12(4,R4),=C'TEST'    TEST RUN                                    
         BNE   OPT80                                                            
         CLC   TIFUNIT,=C'FL '      FLORIDA ONLY                                
         BNE   BADOPT                                                           
         MVI   PROCOPT,C'T'         PROCESS TYPE = TEST                         
         CLI   22(R4),C'Y'          TEST RUN = Y                                
         BE    OPTEND                                                           
         MVI   PROCOPT,C'P'         PROCESS TYPE = PRODUCTION                   
         CLI   22(R4),C'N'          TEST RUN = N                                
         BE    OPTEND                                                           
         B     BADOPT                                                           
*&&                                                                             
*                                                                               
OPT80    DS    0H                                                               
         CLC   12(7,R4),=C'MONTHLY'  MONTHLY OPTION                             
         BNE   OPT90                                                            
         CLC   TIFUNIT,=C'IL '       ILLINOIS  ONLY                             
         BNE   BADOPT                                                           
         CLI   RECNUM,UCTAPES        MUST BE UCTAPES                            
         BE    OPT85                                                            
         CLI   RECNUM,UCDISK         OR UCDISK                                  
         BNE   BADOPT                                                           
OPT85    OI    STSTAT,STSTATIL                                                  
         B     OPTEND                                                           
*                                                                               
OPT90    DS    0H                                                               
         CLC   12(4,R4),=C'WAGE'  WAGE OPTION                                   
         BNE   *+12                                                             
         OI    STSTAT,STSTATM1                                                  
         B     OPT94                                                            
         CLC   12(4,R4),=C'WITH'  WITHHOLDING OPTION                            
         BNE   OPT100                                                           
         OI    STSTAT,STSTATM2                                                  
OPT94    CLC   TIFUNIT,=C'ME '       MAINE ONLY                                 
         BNE   BADOPT                                                           
         B     OPTEND                                                           
OPT100   DS    0H                                                               
         SPACE 1                                                                
MISOPT   MVC   CONHEAD(L'MOPTERR),MOPTERR                                       
         B     BADXIT                                                           
*                                                                               
BADOPT   MVC   CONHEAD(L'OPTERR),OPTERR                                         
         SPACE 1                                                                
BADXIT   GOTO1 ERREX2                                                           
         SPACE 1                                                                
OPTERR   DC    C'** ERROR ** INVALID OPTION'                                    
MOPTERR  DC    C'** ERROR ** MISSING TEST OPTION FOR FL'                        
         SPACE 1                                                                
OPTEND   LA    R4,32(R4)                                                        
         BCT   R0,OPT2                                                          
         CLI   SSSUI,C'Y'          CAN'T HAVE BOTH SSSUI AND EXCESS             
         BNE   *+12                                                             
         CLI   EXCESS,C'Y'                                                      
         BE    BADOPT                                                           
         B     XIT                                                              
         DROP  R6                                                               
         SPACE 1                                                                
FLDINV   MVI   ERROR,INVALID                                                    
         GOTO1 ERREX                                                            
*                                                                               
DOWN     EQU   18                                                               
*              UNITS THAT CAN GO TO DISK                                        
*                                                                               
UCDSKTAB DS    0CL(L'TIFUNIT+L'TIFEMP)                                          
         DC    C'AK ',C'ALL'                                                    
         DC    C'AL ',C'ALL'                                                    
         DC    C'CA ',C'ALL'                                                    
         DC    C'CO ',C'ALL'                                                    
         DC    C'CT ',C'ALL'                                                    
         DC    C'DC ',C'ALL'                                                    
         DC    C'FL ',C'ALL'                                                    
         DC    C'GA ',C'ALL'                                                    
         DC    C'HI ',C'ALL'                                                    
         DC    C'ID ',C'ALL'                                                    
         DC    C'IL ',C'ALL'                                                    
         DC    C'KS ',C'ALL'                                                    
         DC    C'KY ',C'ALL'                                                    
         DC    C'LA ',C'ALL'                                                    
         DC    C'MA ',C'ALL'                                                    
         DC    C'MD ',C'ALL'                                                    
         DC    C'ME ',C'ALL'                                                    
         DC    C'MI ',C'ALL'                                                    
         DC    C'MN ',C'ALL'                                                    
         DC    C'MS ',C'ALL'                                                    
         DC    C'NC ',C'ALL'                                                    
         DC    C'ND ',C'ALL'                                                    
         DC    C'NE ',C'ALL'                                                    
         DC    C'NJ ',C'ALL'                                                    
         DC    C'NM ',C'ALL'                                                    
         DC    C'NY ',C'PG '                                                    
         DC    C'OH ',C'ALL'                                                    
         DC    C'OK ',C'ALL'                                                    
         DC    C'OR ',C'ALL'                                                    
         DC    C'PA ',C'ALL'                                                    
         DC    C'PR ',C'ALL'                                                    
         DC    C'RI ',C'ALL'                                                    
         DC    C'SC ',C'ALL'                                                    
         DC    C'TN ',C'ALL'                                                    
         DC    C'TX ',C'ALL'                                                    
         DC    C'UT ',C'ALL'                                                    
         DC    C'WA ',C'ALL'                                                    
         DC    C'VA ',C'ALL'                                                    
         DC    C'WI ',C'ALL'                                                    
         DC    C'WV ',C'ALL'                                                    
         DC    X'FF'                                                            
         EJECT                                                                  
