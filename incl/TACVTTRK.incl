*          DATA SET TACVTTRK   AT LEVEL 001 AS OF 11/12/10                      
***********************************************************************         
*        ROUTINE DETERMINES IF ALL MUSIC CONTRACT TRACKS              *         
*        NOW CONFORM TO NEW RULES AND, IF SO, TURNS ON STATUS         *         
*        IN ALL OF THE TRACK ELEMENTS                                 *         
*        ON ENTRY ,,, P1 BYTE 0 = COMMERCIAL TYPE                     *         
***********************************************************************         
                                                                                
CVTTRK   NTR1                                                                   
         CLI   0(R1),CTYMUS        IF COMMERCIAL TYPE IS MUSIC ...              
         JNE   NO                                                               
                                                                                
***********************************************************************         
                                                                                
         XC    TGDUB,TGDUB         PREPARE TO SAVE TRACKS                       
                                                                                
         USING TAMCD,R4                                                         
         L     R4,AIO                                                           
         MVI   ELCODE,TAMCELQ      GET ALL MUSIC CONTRACT ELEMENTS              
         BRAS  RE,GETEL                                                         
         JNE   NO                                                               
                                                                                
CTRK10   CLI   TAMCTRK,0           IF TRACK FIELD IS NOT POPULATED              
         JE    NO                  COMMERCIAL DOES NOT CONFORM                  
                                                                                
         LA    R2,TGDUB                                                         
CTRK20   CLI   0(R2),0                                                          
         JE    CTRK30                                                           
         CLC   TAMCTRK,0(R2)       IF TRACK IS DUPLICATED                       
         JE    NO                  COMMERCIAL DOES NOT CONFORM                  
         LA    R2,1(R2)                                                         
         J     CTRK20                                                           
                                                                                
CTRK30   MVC   0(1,R2),TAMCTRK                                                  
                                                                                
         BRAS  RE,NEXTEL                                                        
         JE    CTRK10                                                           
         DROP  R4                                                               
                                                                                
***********************************************************************         
                                                                                
CTRK40   MVI   TGBYTE,0            INIT TO COMML DOES NOT CONFORM               
                                                                                
         MVC   TGFULL,AIO          SAVE COMMERCIAL I/O AREA                     
         MVC   AIO,AIO3            AND SET AIO FOR CAST READS                   
                                                                                
         L     RE,AIO3                                                          
         MVI   0(RE),0                                                          
                                                                                
         USING TLCAD,R3                                                         
         LA    R3,KEY                                                           
         XC    KEY,KEY                                                          
         MVI   TLCACD,TLCACDQ      READ ALL CAST KEYS                           
         MVC   TLCACOM,TGCOM                                                    
         GOTO1 HIGH                                                             
         J     CTRK60                                                           
CTRK50   GOTO1 SEQ                                                              
CTRK60   CLC   KEY(TLCASORT-TLCAD),KEYSAVE                                      
         JNE   CTRK100                                                          
         TM    TLCASORT,X'80'      IF NON-MUSICIAN FOUND                        
         JZ    CTRK110             COMM'L DOES NOT CONFORM                      
         DROP  R3                                                               
                                                                                
         GOTO1 GETREC              GET CAST RECORD                              
                                                                                
         MVI   ELCODE,TAFNELQ      IF NO TRACK ASSOCIATIONS, COMM'L             
         GOTO1 GETL,DMCB,(1,=AL1(TAFNTTRK))        DOES NOT CONFORM             
         JNE   CTRK110                                                          
                                                                                
         USING TAFND,R4                                                         
         L     R4,TGELEM                                                        
         CLI   TAFNNAME,C'*'                                                    
         JE    CTRK50                                                           
                                                                                
         ZIC   R0,TAFNLEN                                                       
         SHI   R0,TAFNLNQ                                                       
         LA    R1,TAFNNAME                                                      
         DROP  R4                                                               
                                                                                
CTRK70   LA    R2,TGDUB                                                         
                                                                                
CTRK80   CLC   0(1,R1),0(R2)                                                    
         JE    CTRK90                                                           
         CLI   1(R2),0             IF CAST'S TRACK DOES NOT EXIST               
         JE    CTRK110             ON COMMERCIAL, DOES NOT CONFORM              
         LA    R2,1(R2)                                                         
         J     CTRK80                                                           
                                                                                
CTRK90   LA    R1,1(R1)                                                         
         BCT   R0,CTRK70                                                        
         J     CTRK50                                                           
                                                                                
CTRK100  MVI  TGBYTE,1             SET COMMERCIAL CONFORMS                      
                                                                                
***********************************************************************         
                                                                                
CTRK110  L     RE,AIO3                                                          
         CLI   0(RE),TLCACDQ       IF A CAST RECORD WAS READ                    
         JNE   CTRK120                                                          
         L     RE,TGFULL           RE-GET COMMERCIAL RECORD                     
         XC    KEY,KEY             TO AVOID PUTREC DRAMA                        
         MVC   KEY(L'TLDRKEY),0(RE)                                             
         GOTO1 HIGH                                                             
         CLC   KEY(L'TLDRKEY),KEYSAVE                                           
         JE    *+6                                                              
         DC    H'00'                                                            
         MVI   RDUPDATE,C'Y'                                                    
         GOTO1 GETREC                                                           
                                                                                
CTRK120  MVC   AIO,TGFULL          EITHER WAY, RESTORE AIO AREA                 
                                                                                
***********************************************************************         
                                                                                
CTRK130  CLI   TGBYTE,1            IF COMMERCIAL CONFORMS TO NEW                
         JNE   NO                  MUSIC RULES ...                              
                                                                                
         USING TAMCD,R4                                                         
         L     R4,AIO                                                           
         MVI   ELCODE,TAMCELQ      GET ALL MUSIC CONTRACT ELEMENTS              
         BRAS  RE,GETEL                                                         
         JE    *+6                                                              
         DC    H'00'                                                            
CTRK140  OI    TAMCSTAT,TAMCSNEW   AND MARK THEM ALL AS CONFORMING              
         BRAS  RE,NEXTEL           TO NEW MUSIC RULES                           
         JE    CTRK140                                                          
         J     YES                                                              
         DROP  R4                                                               
