*          DATA SET PGENEROLP  AT LEVEL 024 AS OF 05/01/02                      
         SPACE 3                                                                
*                  INITIALISATION CODE                                          
         SPACE 3                                                                
INITL    LR    R4,RC               SET UP TO CLEAR WORK SPACE                   
         LR    R5,RD                                                            
         SR    R5,R4                                                            
         LR    R0,RE                                                            
         BAS   RE,CLEARWRK                                                      
         LM    R2,R4,0(R1)                                                      
         PACK  AGYNUM,0(1,R1)      AGENCY NUMBER                                
         LH    R5,0(R2)                                                         
         AR    R5,R3                                                            
         ST    R5,FRSTFLD .        A(FIRST INPUT FIELD HEADER)                  
         LH    R5,2(R2)                                                         
         AR    R5,R3                                                            
         ST    R5,LASTFLD .        A(LAST INPUT FIELD)                          
         MVC   NUMFLD,4(R2) .      NUMBER OF FIELDS                             
         ST    R3,VTWA .           A(TWA)                                       
         MVC   VDATAMGR(36),0(R4)  FACILITY LIST                                
         LR    RA,R3                                                            
         MVC   TERMNAL,0(RA) .     TERMINAL NUMBER                              
         MVC   AGYALPHA,14(RA)     ALPHA AGENCY CODE                            
         LA    R3,64(R3)                                                        
         ST    R3,ERRAREA          PRESET ERRAREA TO A(FIRST HEADER)            
         MVI   DMINBTS,X'C0'       PRESET DATAMGR CONTROL BITS                  
         MVI   DMOUTBTS,X'FD'      PRESET DATAMGR ERROR CHECK BITS              
*                             INITIALIZE OTHER FIELDS                           
         MVC   DMREAD(80),DMGRCONS                                              
         LA    RF,PPWORKD                                                       
         ST    RF,PPWORKC                                                       
         LA    RF,PPFILED                                                       
         ST    RF,PPFILEC                                                       
         L     RF,=V(REPORT)                                                    
         AR    RF,RB                                                            
         ST    RF,REPORT                                                        
         L     RF,=V(PRINT)                                                     
         AR    RF,RB                                                            
         ST    RF,PRINT                                                         
*                                                                               
         COMRG                                                                  
*                                                                               
         MVC   RCDATE,0(R1)                                                     
         ZAP   RCSPECNO,=P'1'                                                   
         MVI   RCSPACNG,1                                                       
         MVI   RCALLAGY,C'N'                                                    
*                                                                               
         MVI   LINE,99                                                          
         MVI   MAXLINES,45                                                      
         MVI   SPACING,1                                                        
         MVC   FORCEHED(5),=5C'N'                                               
         MVC   PAGE,=H'1'                                                       
         MVC   SUBPAGE,PAGE                                                     
*                                                                               
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
*                                                                               
         MVC   MONTHS,MTHCONS                                                   
         MVC   DAYTABL,DAYCONS                                                  
*                                                                               
         LR    RE,R0                                                            
         BR    RE                                                               
         SPACE 2                                                                
DMGRCONS DC    C'DMREADDMRDHIDMRSEQDMADD DMWRTGETRECPUTRECADDREC'               
         DC    C'PRTFILE PRTDIR  PUBFILE  PUBDIR  '                             
MTHCONS  DC    C'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'                          
DAYCONS  DC    C'MONTUEWEDTHUFRISATSUN'                                         
         SPACE 2                                                                
CLEARWRK LTR   R5,R5               CLEAR STORAGE TO ZEROS                       
         BCR   8,RE                                                             
         CH    R5,=H'250'                                                       
         BNH   CLEAREST                                                         
         XC    0(250,R4),0(R4)                                                  
         LA    R4,250(R4)                                                       
         SH    R5,=H'250'                                                       
         B     CLEARWRK                                                         
         SPACE 2                                                                
CLEAREST BCTR  R5,R0                                                            
         EX    R5,VARCLEAR                                                      
         BR    RE                                                               
         SPACE 2                                                                
VARCLEAR XC    0(0,R4),0(R4)                                                    
         EJECT                                                                  
*                  FARMABLE CODE                                                
         SPACE 3                                                                
ANY      CLI   5(R2),0                                                          
         BNE   ANY2                                                             
         LA    R3,1                                                             
         B     ERROR                                                            
         SPACE 2                                                                
ANY2     TM    4(R2),X'10' .       IS IT VALID NUMERIC                          
         BCR   8,RE .              IF APPLICABLE                                
         LA    R3,3                                                             
         B     ERROR                                                            
         SPACE 2                                                                
PACK     SR    R1,R1                                                            
         SR    R0,R0                                                            
         ZAP   DUB,=P'0'                                                        
         IC    R1,5(R2)                                                         
         LTR   R1,R1                                                            
         BCR   8,RE                EXIT ON ZERO LENGTH                          
         TM    4(R2),X'08'                                                      
         BCR   8,RE                        OR NON NUMERIC                       
         BCTR  R1,R0                                                            
         EX    R1,VARPACK                                                       
         CVB   R0,DUB                                                           
         BR    RE                                                               
         SPACE 2                                                                
VARPACK  PACK  DUB,8(0,R2)                                                      
         SPACE 2                                                                
MOVE     MVI   WORK,C' '                                                        
         MVC   WORK+1(L'WORK-1),WORK                                            
         SR    R1,R1                                                            
         IC    R1,5(R2)                                                         
         LTR   R1,R1                                                            
         BCR   8,RE                EXIT ON ZERO LENGTH                          
         BCTR  R1,R0                                                            
         EX    R1,VARMOVE                                                       
         BR    RE                                                               
         SPACE 2                                                                
VARMOVE  MVC   WORK(0),8(R2)                                                    
         EJECT                                                                  
*                  COMMUNICATION WITH DATA MANAGER (DIRECTORY)                  
         SPACE 3                                                                
READ     LA    RF,DMREAD                                                        
         MVC   KEYSAVE,KEY                                                      
         B     DIRCTRY                                                          
         SPACE 2                                                                
SEQ      LA    RF,DMRSEQ                                                        
         B     DIRCTRY                                                          
         SPACE 2                                                                
HIGH     LA    RF,DMRDHI                                                        
         MVC   KEYSAVE,KEY                                                      
         B     DIRCTRY                                                          
         SPACE 2                                                                
ADD      LA    RF,DMADD                                                         
         B     DIRCTRY                                                          
         SPACE 2                                                                
WRITE    LA    RF,DMWRT                                                         
         SPACE 2                                                                
DIRCTRY  NTR1                                                                   
         SPACE 2                                                                
         ST    RF,DMCB                                                          
         MVC   DMCB(1),DMINBTS                                                  
         GOTO1 DATAMGR,DMCB,,PRTDIR,KEY,KEY                                     
*                                                                               
         B     DMCHECK                                                          
         EJECT                                                                  
*                  COMMUNICATION WITH DATA MANAGER (FILE)                       
         SPACE 3                                                                
*                                                                               
GETPRT   LA    RF,GETREC                                                        
         B     FILE                                                             
         SPACE 2                                                                
PUTPRT   LA    RF,PUTREC                                                        
         B     FILE                                                             
         SPACE 2                                                                
ADDPRT   LA    RF,ADDREC                                                        
         B     FILE                                                             
         SPACE 2                                                                
FILE     NTR                                                                    
         SPACE 2                                                                
         LA    R2,KEY+27                                                        
         CLI   0(RF),C'A'                                                       
         BNE   *+8                                                              
         LA    R2,KEY                                                           
         ST    RF,DMCB                                                          
         MVC   DMCB(1),DMINBTS                                                  
         GOTO1 DATAMGR,DMCB,,PRTFILE,(R2),AREC,DMWORK                           
*                                                                               
         B     DMCHECK                                                          
         EJECT                                                                  
*                  COMMUNICATION WITH DATA MANAGER (PUBDIR)                     
         SPACE 3                                                                
READPUB  LA    RF,DMREAD                                                        
         MVC   KEYSAVE,KEY                                                      
         B     PUBDIRY                                                          
         SPACE 2                                                                
SEQPUB   LA    RF,DMRSEQ                                                        
         B     PUBDIRY                                                          
         SPACE 2                                                                
HIGHPUB  LA    RF,DMRDHI                                                        
         MVC   KEYSAVE,KEY                                                      
         B     PUBDIRY                                                          
         SPACE 2                                                                
WRITEPUB LA    RF,DMWRT                                                         
         B     PUBDIRY                                                          
         SPACE 2                                                                
ADPUB    LA    RF,DMADD                                                         
         B     PUBDIRY                                                          
         SPACE 2                                                                
PUBDIRY  NTR                                                                    
         SPACE 2                                                                
         ST    RF,DMCB                                                          
         MVC   DMCB(1),DMINBTS                                                  
         GOTO1 DATAMGR,DMCB,,PUBDIR,KEY,KEY                                     
         B     DMCHECK                                                          
         EJECT                                                                  
*                  COMMUNICATION WITH DATA MANAGER (PUBFILE)                    
         SPACE 3                                                                
GETPUB   LA    RF,GETREC                                                        
         B     PUBFIL                                                           
         SPACE 2                                                                
PUTPUB   LA    RF,PUTREC                                                        
         B     PUBFIL                                                           
         SPACE 2                                                                
ADDPUB   LA    RF,ADDREC                                                        
         B     PUBFIL                                                           
         SPACE 2                                                                
PUBFIL   NTR                                                                    
         LA    R2,KEY+27                                                        
         CLI   0(RF),C'A'                                                       
         BNE   *+8                                                              
         LA    R2,KEY                                                           
         ST    RF,DMCB                                                          
         MVC   DMCB(1),DMINBTS                                                  
         GOTO1 DATAMGR,DMCB,,PUBFILE,AREC,DMWORK                                
         B     DMCHECK                                                          
         EJECT                                                                  
*                  DATA MANAGER ERRORS AND EXIT                                 
         SPACE 3                                                                
DMCHECK  MVC   BYTE,DMCB+8                                                      
         NC    BYTE,DMOUTBTS                                                    
         BNZ   DMERRS                                                           
         XIT                                                                    
         SPACE 2                                                                
DMERRS   L     RD,4(RD) .          UNWIND WITHOUT XIT                           
         LM    RE,RC,12(RD)                                                     
         SR    R3,R3 .             LET GETMSG SORT IT OUT                       
         B     ERROR                                                            
         EJECT                                                                  
*                  EXITS FROM PROGRAM                                           
         SPACE 3                                                                
LOCK     OI    6(R2),X'02'         LOCK SCREEN                                  
         SPACE 2                                                                
ERROR    L     R4,ERRAREA                                                       
         MVI   ERRAREA,X'FF'                                                    
         MVC   DMCB+20(4),VDATAMGR                                              
         MVC   DMCB+20(1),TERMNAL                                               
         GOTO1 VGETMSG,DMCB+12,((R3),8(R4)),(4,DMCB)                            
         SPACE 2                                                                
EXIT     OI    6(R2),OI1C .        INSERT CURSOR                                
         L     R4,ERRAREA                                                       
         FOUT  (R4)                                                             
EXXMOD   XMOD1 1                                                                
         EJECT                                                                  
