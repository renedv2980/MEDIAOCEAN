*          DATA SET REMERG16   AT LEVEL 039 AS OF 12/27/01                      
CRDIT16S NMOD1 CREDITDX-CREDITD,*CREDIT*,CLEAR=YES                              
***********************************************************************         
* CHANGE LOG:                                                         *         
*   OCT04/00 (BU ) --- BUILD GRID ADDING IN X'56' MAKEGOOD ELEMENTS   *         
*                                                                     *         
*   OCT09/00 (BU ) --- OPTIONAL SKIP OF CREDIT ADD-BACK IF P2 = NOCR  *         
*                                                                     *         
*                                                                     *         
*                                                                     *         
*                                                                     *         
***********************************************************************         
                                                                                
*                                                                               
         LR    R5,RC               SET UP GRID SPACE                            
         USING CREDITD,R5                                                       
         L     RC,0(R1)            RESET A(WORKSPACE)                           
         L     R8,4(R1)                                                         
*                                                                               
         BAS   RE,BLDGRID          LOAD ALL 03 ELTS INTO GRID                   
*                                                                               
         BAS   RE,ADDMKGDS         ADD MAKEGOODS BACK INTO GRID                 
*                                                                               
*   TEST DUMP                                                                   
***      LA    R3,WORK2                                                         
***      DC    H'0'                                                             
*   END TEST DUMP                                                               
*                                                                               
         CLC   =C'NOCR',0(R8)      SKIP CREDIT ADDBACK?                         
         BE    CRDI0020            YES                                          
         BAS   RE,ADDGRID          ADD CREDIT SPOTS BACK INTO GRID              
*                                                                               
***      BNZ   CRDI0200            NO CREDITS:  EXIT                            
*                                                                               
*   ALWAYS REBUILD X'03' ELEMENT FROM GRID                                      
*                                                                               
CRDI0020 EQU   *                                                                
         BAS   RE,BLDBUY                                                        
*                                                                               
CRDI0200 EQU   *                                                                
         XIT1                                                                   
         EJECT                                                                  
*                                                                               
***********************************************************************         
* CONSTRUCT A GRID BASE ON X'03' ELEMENTS IN THE BUY RECORD EACH                
* WEEK CONSIST OF 2 1-BYTE ENTRIES WITH THE FIRST ENTRY THE NUMBER              
* OF SPOTS FOR THAT WEEK, AND THE SECOND BYTE FOR VARIOUS FLAGS                 
* (ZERO OVERRIDE, ALTERNATE, ETC)                                               
* THE START OF THE GRID IS THE FIRST START DATE OF THE BUY                      
***********************************************************************         
BLDGRID  NTR1                                                                   
*                                                                               
         LA    R6,RBUYREC                                                       
         MVI   ELCODE,X'03'                                                     
         BAS   RE,GETEL                                                         
         BNE   ERROR                                                            
         USING RBUYDTEL,R6                                                      
*                                                                               
         GOTO1 DATCON,DMCB,(3,RBUYDTST),(5,CRSTDT)                              
         LA    R3,WORK2                                                         
         XC    WORK2,WORK2         CLEAR WORK AREA                              
*                                                                               
BGRD0060 DS    0H                                                               
         CLI   RBUYDTWK,0          ANY VALUE IN # WEEKS?                        
         BNZ   BGRD0070            YES                                          
         LA    R1,1                NO - SET TO 1                                
         B     BGRD0080                                                         
BGRD0070 EQU   *                                                                
         ZIC   R1,RBUYDTWK                                                      
*                                                                               
BGRD0080 DS    0H                                                               
         MVC   0(1,R3),RBUYDTNW                                                 
*                                                                               
         CLI   RBUYDTNW,0          FORCE ZERO OVERRIDE AS DEFAULT               
         BNE   *+8                                                              
         OI    1(R3),X'01'                                                      
*                                                                               
         TM    RBUYDTIN,X'01'      NPW OVERRIDE??                               
         BZ    *+8                                                              
         OI    1(R3),X'01'                                                      
         LA    R3,2(R3)            BUMP TO NEXT WEEK                            
         TM    RBUYDTIN,X'40'      ALTERNATE WEEKS??                            
         BZ    *+8                                                              
         LA    R3,2(R3)            YES, BUMP ONE MORE                           
         BCT   R1,BGRD0080         PROCESS FOR SPECIFIED NUMBER OF WKS          
*                                                                               
         BAS   RE,NEXTEL                                                        
         BNE   BGRD0400                                                         
*                                                                               
         MVI   CRSTDT+8,C'-'                                                    
         GOTO1 DATCON,DMCB,(3,RBUYDTST),(5,CRSTDT2)                             
         L     RE,ACOMFACS         CALL COMFACS FOR ADDRESS OF PERVAL           
         USING COMFACSD,RE                                                      
         L     RF,CPERVAL                                                       
         DROP  RE                                                               
*                                                                               
         GOTO1 (RF),DMCB,(17,CRSTDT),WORK2+120                                  
         CLI   DMCB+4,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
WKD      USING PERVALD,WORK2+120                                                
         ZICM  R1,WKD.PVALNWKS,2   # WEEKS ROUNDED UP                           
         BCTR  R1,0                ADJUST FOR ROUNDING                          
         DROP  WKD                                                              
*                                                                               
         LA    R3,WORK2                                                         
         SLL   R1,1                MULTIPLY BY 2                                
         AR    R3,R1               BUMP TO START OF NEXT X'03' ELEMENT          
         B     BGRD0060            IN THE GRID                                  
*                                                                               
BGRD0400 DS    0H                                                               
         XIT1                                                                   
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
* REINSERT SPOTS DETAILED IN 16 ELEMENTS.  FIND START DATE, NUMBER OF           
* WEEKS, AND THE NUMBER OF SPOTS/WEEK.                                          
*                                                                               
* USES IO4 FOR SCANNER WORK AREA.                                               
* USES IO4+1000 TO BUILD CREDIT AUDIT TRAIL ELEMENTS                            
***********************************************************************         
ADDGRID  NTR1                                                                   
         LA    R6,RBUYREC                                                       
         MVI   ELCODE,X'16'                                                     
         GOTO1 GETEL               RETRIEVE FIRST CREDIT AUDIT                  
         BNE   ADDG0240            NO 16 ELEMENT                                
CATD     USING RBUYCAEL,R6         USE X'16' CREDIT AUDIT TRAIL DSECT           
*                                                                               
*                                                                               
ADDG0020 EQU   *                                                                
         XC    WORK,WORK                                                        
         MVC   WORK(8),CRSTDT      LOAD START DATE                              
         MVI   WORK+8,C'-'         INSERT SEPARATOR                             
         GOTO1 DATCON,DMCB,(3,CATD.RBUYCASD),(5,WORK+9)                         
         MVC   CRNUMWK,CATD.RBUYCANW                                            
*                                  INSERT # OF WEEKS:  LOOP CONTROL             
         L     RE,ACOMFACS         CALL COMFACS FOR ADDRESS OF PERVAL           
         USING COMFACSD,RE                                                      
         L     RF,CPERVAL                                                       
         DROP  RE                                                               
         GOTO1 (RF),DMCB,(17,WORK),WORK2+120                                    
*                                  PERVAL THE DATE RANGE                        
         CLI   DMCB+4,0                                                         
         BE    ADDG0040                                                         
         DC    H'0'                MUST BE VALID DATES                          
*                                                                               
*                                                                               
WKD      USING PERVALD,WORK2+120                                                
ADDG0040 DS    0H                                                               
         ZIC   R0,CRNUMWK          SET NUMBER OF WEEKS                          
         ZICM  R1,WKD.PVALNWKS,2   # WEEKS ROUNDED UP                           
*                                  NUMBER OF WEEKS: LOOP CONTROL                
         BCTR  R1,0                CELL DISPLACEMENT                            
         DROP  WKD                                                              
         LA    R2,WORK2            SET A(GRID)                                  
         SLL   R1,1                DOUBLE CELL DISPLACEMENT FOR OFFSET          
         AR    R2,R1               SET DISPLACEMENT OF 1ST CELL                 
ADDG0050 EQU   *                                                                
         CLC   RBUYNW,0(R2)        BUY # SPOTS = GRID CELL?                     
         BE    ADDG0060            YES - DON'T READD                            
         ZIC   RF,0(R2)            GET NUMBER OF SPOTS/WK IN GRID CELL          
         ZIC   RE,CATD.RBUYCASP    GET NUMBER OF SPOTS TO REINSERT              
         AR    RF,RE               ADD SPOTS BACK                               
         STC   RF,0(R2)                                                         
         CLC   RBUYNW,0(R2)        BUY # SPOTS = GRID CELL?                     
         BNE   ADDG0060            NO                                           
         NI    1(R2),X'FF'-X'01'   YES - TURN OFF OVERRIDE                      
ADDG0060 EQU   *                                                                
         LA    R2,2(R2)            BUMP TO NEXT CELL                            
         BCT   R0,ADDG0050         PROCESS EACH WEEK                            
         MVI   ELCODE,X'16'                                                     
         GOTO1 NEXTEL              RETRIEVE NEXT  CREDIT AUDIT                  
         BE    ADDG0020            FOUND - PROCESS IT                           
*                                                                               
         SR    R0,R0               SET CC ZERO                                  
         B     ADDG0260                                                         
ADDG0240 DS    0H                                                               
         LTR   RB,RB               SET CC NOT ZERO                              
ADDG0260 DS    0H                                                               
         XIT1                                                                   
*                                                                               
         DROP  CATD                                                             
         EJECT                                                                  
***********************************************************************         
* REINSERT SPOTS DETAILED IN 56 ELEMENTS.  USE ELEMENT START DATE AS            
*   A SINGLE START-END DATE, PROCESS FOR A # OF MISSED SPOTS                    
*                                                                               
* USES IO4 FOR SCANNER WORK AREA.                                               
* USES IO4+1000 TO BUILD CREDIT AUDIT TRAIL ELEMENTS                            
***********************************************************************         
ADDMKGDS NTR1                                                                   
         LA    R6,RBUYREC                                                       
         MVI   ELCODE,X'56'                                                     
         GOTO1 GETEL               RETRIEVE FIRST MAKEGOOD ELEMENT              
         BNE   ADDM0240            NO 56 ELEMENT                                
CATM     USING RBYMGSEL,R6         USE X'56' MAKEGOOD ELEMENT                   
*                                                                               
*                                                                               
ADDM0020 EQU   *                                                                
         XC    WORK,WORK                                                        
         MVC   WORK(8),CRSTDT      LOAD START DATE                              
         MVI   WORK+8,C'-'         INSERT SEPARATOR                             
         GOTO1 DATCON,DMCB,(3,CATM.RBYMGSDT),(5,WORK+9)                         
*                                  MAKEGOOD MISSED DATE                         
         MVI   CRNUMWK,1           INSERT NUMBER OF WEEKS:                      
*                                     ALWAYS A SINGLE WEEK                      
         L     RE,ACOMFACS         CALL COMFACS FOR ADDRESS OF PERVAL           
         USING COMFACSD,RE                                                      
         L     RF,CPERVAL                                                       
         DROP  RE                                                               
         GOTO1 (RF),DMCB,(17,WORK),WORK2+120                                    
*                                  PERVAL THE DATE RANGE                        
         CLI   DMCB+4,0                                                         
         BE    ADDM0040                                                         
         DC    H'0'                MUST BE VALID DATES                          
*                                                                               
*                                                                               
WKMKGD   USING PERVALD,WORK2+120                                                
ADDM0040 DS    0H                                                               
         ZIC   R0,CRNUMWK          SET NUMBER OF WEEKS                          
         ZICM  R1,WKMKGD.PVALNWKS,2 # WEEKS ROUNDED UP                          
*                                  NUMBER OF WEEKS: LOOP CONTROL                
         BCTR  R1,0                CELL DISPLACEMENT                            
         DROP  WKMKGD                                                           
         LA    R2,WORK2            SET A(GRID)                                  
         SLL   R1,1                DOUBLE CELL DISPLACEMENT FOR OFFSET          
         AR    R2,R1               SET DISPLACEMENT OF 1ST CELL                 
ADDM0050 EQU   *                                                                
*                                                                               
*   THE FOLLOWING TEST DOESN'T PERMIT ADDING MAKEGOODS BACK TO BUYS             
*        WHERE ADDITIONAL SPOTS HAVE BEEN PLUSSED IN.  FOR INSTANCE,            
*        THE ORIGINAL LINE WAS PURCHASED AS TWO SPOTS, BUT WEEK 1               
*        WAS OVERRIDDEN AS 3 SPOTS.  ONE OF THE THREE WAS THEN                  
*        MADE GOOD.  THE MADEGOOD SPOT WOULD NOT BE ADDED BACK IN,              
*        RESULTING IN AN EC ERROR.                                              
*                                                                               
**       CLC   RBUYNW,0(R2)        BUY # SPOTS = GRID CELL?                     
**       BE    ADDM0060            YES - DON'T READD                            
         ZIC   RF,0(R2)            GET NUMBER OF SPOTS/WK IN GRID CELL          
         ZIC   RE,CATM.RBYMGSSP    GET NUMBER OF SPOTS TO REINSERT              
         AR    RF,RE               ADD SPOTS BACK                               
         STC   RF,0(R2)                                                         
         CLC   RBUYNW,0(R2)        BUY # SPOTS = GRID CELL?                     
         BNE   ADDM0060            NO                                           
         NI    1(R2),X'FF'-X'01'   YES - TURN OFF OVERRIDE                      
ADDM0060 EQU   *                                                                
         LA    R2,2(R2)            BUMP TO NEXT CELL                            
         BCT   R0,ADDM0050         PROCESS EACH WEEK                            
         MVI   ELCODE,X'56'                                                     
         GOTO1 NEXTEL              RETRIEVE NEXT  MAKEGOOD ELEMENT              
         BE    ADDM0020            FOUND - PROCESS IT                           
*                                                                               
         SR    R0,R0               SET CC ZERO                                  
         B     ADDM0260                                                         
ADDM0240 DS    0H                                                               
         LTR   RB,RB               SET CC NOT ZERO                              
ADDM0260 DS    0H                                                               
         XIT1                                                                   
*                                                                               
         DROP  CATM                                                             
         EJECT                                                                  
***********************************************************************         
* REBUILD X'03' ELEMENT FROM BUY GRID                                           
***********************************************************************         
BLDBUY   NTR1                                                                   
         LA    R6,RBUYREC                                                       
         MVI   ELCODE,X'02'                                                     
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
BBUY03   DS    0H                  POINT TO LAST X'02' ELEMENT TO GET           
         LR    R2,R6               END DAY                                      
         BAS   RE,NEXTEL                                                        
         BE    BBUY03                                                           
         LR    R6,R2                                                            
         USING RBUYDYEL,R6                                                      
         MVI   CRENDDAY,0     GET END DAY                                       
         MVN   CRENDDAY,RBUYDYIN                                                
         DROP  R6                                                               
*                                                                               
         LA    R6,RBUYREC                                                       
         MVI   ELCODE,X'03'                                                     
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING RBUYDTEL,R6         SAVE OFF BUY START DATE                      
         GOTO1 DATCON,DMCB,(3,RBUYDTST),(0,CRSTDT)                              
         DROP  R6                                                               
*                                                                               
* DELETE OLD X'03'S                                                             
*                                                                               
         GOTO1 VDELELEM,DMCB,(X'03',RBUYREC)                                    
*                                                                               
         LA    R6,WORK2                                                         
*                                                                               
         SR    R7,R7                                                            
*                                                                               
BBUY05   DS    0H                                                               
         LA    R4,1                                                             
         XC    CRBUYEL,CRBUYEL                                                  
WKD      USING RBUYDTEL,CRBUYEL                                                 
*                                                                               
         MVI   WKD.RBUYDTCD,X'03'                                               
         MVI   WKD.RBUYDTLN,11                                                  
*                                                                               
BBUY08   DS    0H                                                               
         CLI   0(R6),0                                                          
         BNE   BBUY10                                                           
         TM    1(R6),X'01'         OVERRIDE??                                   
         BZ    BBUY40                                                           
*                                                                               
BBUY10   DS    0H                                                               
         OC    WKD.RBUYDTST,WKD.RBUYDTST                                        
         BNZ   BBUY20              NEED TO FIND START DATE                      
         LTR   R7,R7                                                            
         BNZ   BBUY15                                                           
         MVC   WKD.RBUYDTST,CRSTDT                                              
         MVC   CRSTDT2,CRSTDT                                                   
         GOTO1 DATCON,DMCB,(0,CRSTDT),(3,WKD.RBUYDTST)                          
         B     BBUY20                                                           
*                                                                               
BBUY15   DS    0H                  SUBSEQUENT X'03'S NEED TO COMPUTE            
         LR    R2,R7               START DATES RELATIVE TO INITIAL              
         MHI   R2,7                FLIGHT START FOR THIS BUY                    
         GOTO1 ADDAY,DMCB,CRSTDT,CRSTDT2,(R2)                                   
         GOTO1 DATCON,DMCB,(0,CRSTDT2),(3,WKD.RBUYDTST)                         
*                                                                               
BBUY20   DS    0H                                                               
         CLC   0(1,R6),2(R6)                                                    
         BNE   BBUY23                                                           
         CLI   0(R6),0             INCASE ALL SPOTS CREDITED OUT FOR            
         BNE   BBUY21              THIS WEEK, CHECK IF ZERO SPOT                
         TM    3(R6),X'01'         OVERRIDE FLAG IS SET                         
         BZ    BBUY23                                                           
*                                                                               
BBUY21   DS    0H                                                               
         LA    R6,2(R6)                                                         
         LA    R4,1(R4)                                                         
         LA    R7,1(R7)                                                         
         CH    R7,=H'53'                                                        
         BL    BBUY08              BUMP TO NEXT CELL                            
*                                                                               
BBUY23   DS    0H                                                               
         MVC   WKD.RBUYDTNW,0(R6)                                               
         STC   R4,WKD.RBUYDTWK                                                  
*** CHECK IF OVERRIDE OR ALTERNATE WEEKS                                        
         OI    WKD.RBUYDTIN,X'80'  DEFAULT IS WEEKLY                            
         TM    1(R6),X'01'         OVERRIDE??                                   
         BZ    *+8                                                              
         OI    WKD.RBUYDTIN,X'01'                                               
*** CHECK IF OVERRIDE OR ALTERNATE WEEKS                                        
*** CHECK IF OVERRIDE OR ALTERNATE WEEKS                                        
         CLC   RBUYNW,0(R6)                                                     
         BE    BBUY25                                                           
         OI    WKD.RBUYDTIN,X'01'                                               
***********                                                                     
***********                                                                     
*                                                                               
* CALCULATE END DATE                                                            
*                                                                               
BBUY25   DS    0H                                                               
         ZIC   R2,WKD.RBUYDTWK                                                  
         MHI   R2,7                                                             
         GOTO1 ADDAY,DMCB,CRSTDT2,CRSTDT2,(R2)                                  
*                                                                               
         GOTO1 GETDAY,DMCB,CRSTDT2,FULL                                         
         ZIC   RE,CRENDDAY                                                      
         ZIC   RF,DMCB                                                          
         SR    RE,RF                                                            
         BZ    BBUY35                                                           
         BP    BBUY30                                                           
*                                                                               
* HANDLE OUT OF WEEK ROTATIONS                                                  
*                                                                               
         LR    R2,RE                                                            
         B     BBUY38                                                           
*                                                                               
BBUY30   DS    0H                                                               
         LA    R2,7                                                             
         LNR   R2,R2                                                            
         AR    R2,RE                                                            
         B     BBUY38                                                           
*                                                                               
BBUY35   DS    0H                  SAME START END DAY, BACK UP A WEEK           
         LA    R2,7                                                             
         LNR   R2,R2                                                            
*                                                                               
BBUY38   DS    0H                                                               
         GOTO1 ADDAY,DMCB,CRSTDT2,CRSTDT2,(R2)                                  
         GOTO1 DATCON,DMCB,(0,CRSTDT2),(3,WKD.RBUYDTED)                         
*                                                                               
***********                                                                     
***********                                                                     
         MVC   DMCB2(24),DMCB      SAVE SINCE TRASHED BY HELLO...               
         GOTO1 VADDELEM,DMCB,RBUYREC,WKD.RBUYDTEL                               
         MVC   DMCB(24),DMCB2      RESTORE                                      
*                                                                               
BBUY40   DS    0H                                                               
         LA    R6,2(R6)                                                         
         LA    R7,1(R7)                                                         
         CH    R7,=H'53'                                                        
         BL    BBUY05              BUMP TO NEXT CELL                            
*                                                                               
BBUYX    DS    0H                                                               
         XIT1                                                                   
         DROP  WKD                                                              
         EJECT                                                                  
***********************************************************************         
* PARSE INPUT STRING TO FIND NUMBER OF SPOTS TO BE CREDITED                     
***********************************************************************         
GETCRSPT NTR1                                                                   
         L     R2,0(R1)                                                         
*                                                                               
         SR    R4,R4                                                            
GCRS10   DS    0H                                                               
         LA    R1,0(R4,R2)                                                      
         CLI   0(R1),C'('                                                       
         BE    GCRS20                                                           
         LA    R4,1(R4)                                                         
         CH    R4,=H'20'                                                        
         BL    GCRS10                                                           
         LA    R3,808                                                           
         B     GCRSERR                                                          
*                                                                               
GCRS20   DS    0H                                                               
         MVI   0(R1),C' '          INSERT SEPARATOR                             
         XC    CRNUMSPT,CRNUMSPT                                                
*                                                                               
* GET SPOTS CREDITED - FORMAT IS JAN15(4) JAN15-JAN26(5) JAN5-E(6)              
*                           OR JAN15-8W(3)                                      
         LA    R3,NPWERR                                                        
         LA    R4,1(R4,R2)                                                      
         LR    RF,R4                                                            
         SR    R1,R1                                                            
*                                                                               
* CHECK NUMBER PER WEEK                                                         
*                                                                               
GCRS30   CLI   0(R4),C'0'                                                       
         BL    GCRSERR                                                          
         CLI   0(R4),C'9'                                                       
         BH    GCRSERR                                                          
*                                                                               
         LA    R4,1(R4)                                                         
         LA    R1,1(R1)                                                         
         CH    R1,=H'3'                                                         
         BH    GCRSERR                                                          
         CLI   0(R4),C')'                                                       
         BNE   GCRS30                                                           
*                                                                               
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         PACK  DUB,0(0,RF)                                                      
         CVB   R1,DUB                                                           
         CH    R1,=H'255'                                                       
         BH    GCRSERR                                                          
*                                                                               
         LTR   R1,R1                                                            
         BZ    GCRSERR                                                          
         STC   R1,CRNUMSPT    NUMBER OF SPOTS TO BE CREDITED                    
*                                                                               
         LA    R3,816              MUST BE SEPARATED BY A COMMA                 
         CLI   1(R4),C' '          THERE MUST NOT BE ANY INPUT AFTER            
         BE    GCRSEXIT            THE NUMBER OF SPOTS                          
*                                                                               
GCRSERR  DS    0H                                                               
         DC    H'0'                CAN'T PROCESS - DUMP                         
         B     ERROR                                                            
GCRSEXIT DS    0H                                                               
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
