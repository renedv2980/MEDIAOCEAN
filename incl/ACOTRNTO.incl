*          DATA SET ACOTRNTO   AT LEVEL 009 AS OF 03/16/17                      
***********************************************************************         
* IS PROFILE OUTPUT TYPE = TORONTO?                                   *         
*    P1 = AIOAREA                                                     *         
*    P2 = A(COMPANY USERID NUMBER)                                    *         
* SET MCREPTS = X'01' (DEFAULT)                                       *         
* CHECK IF OUTPUT TYPE FOR PROFILE RECORD IS TORONTO                  *         
* SET MCREPTS = C'F'  (TORONTO ONLY)                                  *         
***********************************************************************         
OUTTRNTO NTR1  BASE=*,LABEL=*                                                   
         L     R5,ADMASTC                                                       
         USING MASTD,R5                                                         
         MVI   MCREPTS,X'01'       SET DEFAULT VALUES                           
         L     R6,MCBXAREA                                                      
*&&DO                                                                           
         L     RE,MCVREMOT                                                      
         ST    RE,REMOTEC                                                       
                                                                                
         USING REMOTED,RE                                                       
REMOT2   USING REMOTED,RF                                                       
         L     RE,MCVREMOT                                                      
         L     RF,MCVREMO2                                                      
         XC    0(REMOTEDL,RF),0(RF)                                             
         MVC   REMOT2.REMOTADM,REMOTADM                                         
         MVC   REMOT2.REMOTAOP,REMOTAOP                                         
*&&                                                                             
         USING CTPREC,R2           R2=A(PROFILE RECORD)                         
         L     R2,0(R1)                                                         
         L     R3,4(R1)                                                         
         XR    R4,R4                                                            
                                                                                
         XC    CTPKEY,CTPKEY                                                    
         MVI   CTPKTYP,CTPKTYPQ                                                 
         MVI   CTPKSYS,C'A'                                                     
         MVC   CTPKPROG,RCPROG                                                  
         MVC   CTPKORIG,0(R3)      READ WITH ORIGIN ID                          
         GOTO1 DATAMGR,DMCB,DMREAD,=C'CTFILE ',CTPREC,CTPREC                    
         JE    OTRNT10                                                          
OTRNT03  AHI   R4,1                                                             
                                                                                
         CHI   R4,1                                                             
         JH    OTRNT05                                                          
         USING CTIREC,R2           R2=A(ID RECORD)                              
         XC    CTIKEY,CTIKEY       CAN'T FIND, READ IDI RECORD                  
         MVI   CTIKTYP,CTIKTYPQ                                                 
         MVC   CTIKNUM,0(R3)       READ WITH ORIGIN ID                          
         GOTO1 DATAMGR,DMCB,DMREAD,=C'CTFILE ',CTIREC,CTIREC                    
         JE    OTRNT10                                                          
         AHI   R4,1                                                             
                                                                                
OTRNT05  CHI   R4,2                                                             
         JH    OTRNTXIT                                                         
         USING CTPREC,R2           R2=A(PROFILE RECORD)                         
         XC    CTPKEY,CTPKEY       CAN'T FIND, READ DEFAULT PROFILE             
         MVI   CTPKTYP,CTPKTYPQ                                                 
         MVI   CTPKSYS,C'A'                                                     
         MVC   CTPKPROG,RCPROG                                                  
         GOTO1 DATAMGR,DMCB,DMREAD,=C'CTFILE ',CTPREC,CTPREC                    
         JNE   OTRNTXIT                                                         
                                                                                
         USING CTOCOD,R1                                                        
OTRNT10  LA    R1,CTPDATA-CTPREC(R2)                                            
         XR    R0,R0                                                            
OTRNT20  CLI   CTOCOEL,0           TEST E-O-R                                   
         JE    OTRNT03             YES, BUT MAY BE IN OTHER RECORDS             
         CLI   CTOCOEL,CTOCOELQ    X'42'                                        
         JE    OTRNT30                                                          
         CLI   CTOCOEL,X'43'       X'43'                                        
         JE    OTRNT30                                                          
         IC    R0,1(R1)                                                         
         AR    R1,R0                                                            
         J     OTRNT20                                                          
OTRNT30  CLC   =CL3'TOR',CTOCODE   FOR TORONTO                                  
         JNE   OTRNTXIT                                                         
         MVI   MCREPTS,C'F'                                                     
*&&DO                                                                           
         CLC   =CL6'TORBTH',CTOCODE                                             
         JNE   OTRNT50                                                          
         L     RE,MCVREMOT                                                      
         L     RF,MCVREMO2                                                      
         MVC   0(REMOTEDL,RF),0(RE)                                             
         L     RE,MCVREMO2                                                      
         ST    RE,REMOTEC                                                       
                                                                                
         MVC   MCREMOT2,MCREMOTE                                                
         MVC   MCREMP2K,MCREMPQK                                                
         MVC   MCREMP2A,MCREMPQA                                                
         MVC   MCREPP2I,MCREPPQI                                                
*&&                                                                             
         USING BOXD,R6             R6=A(BOX AREA)                               
BXAREA2  USING BOXD,R8                                                          
OTRNT50  L     R8,MCBXARE2                                                      
                                                                                
         LR    RE,R6                        RE=A(DATA TO MOVE FROM)             
         LR    R0,R8                        R0=A(DATA TO MOVE TO)               
         LHI   R1,BOXAPAT-BOXD              R1 AND RF = LENGTH TO MOVE          
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
                                                                                
         LA    RE,BOXWIDTH                  RE=A(DATA TO MOVE FROM)             
         LA    R0,BXAREA2.BOXWIDTH          R0=A(DATA TO MOVE TO)               
         LHI   R1,BOXPGDEF-BOXWIDTH         R1 AND RF = LENGTH TO MOVE          
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
                                                                                
         L     R6,MCBXARE2                                                      
                                                                                
OTRNTXIT ST    R6,ADBXAREA                                                      
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
