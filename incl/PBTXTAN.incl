*          DATA SET PBTXTAN    AT LEVEL 053 AS OF 05/01/02                      
TXTAN    CSECT                                                                  
*                                       SAVE REGISTERS - SETUP LINKAGE          
*                                                                               
         USING *,15                                                             
         SAVE  (14,12),,*                                                       
         DROP  15                                                               
START    DC    0Y(0)                                                            
         BASR  12,0                                                             
         USING *,12                                                             
         ST    13,SAVEAREA+4                                                    
         LA    13,SAVEAREA                                                      
         PRINT NOGEN                                                            
         EJECT                                                                  
*                                                                               
*        EQUATE REGISTERS                                                       
*                                                                               
R0       EQU   0                                                                
R1       EQU   1                                                                
R2       EQU   2                                                                
R3       EQU   3                                                                
R4       EQU   4                                                                
R5       EQU   5                                                                
R6       EQU   6                                                                
R7       EQU   7                                                                
R8       EQU   8                                                                
R9       EQU   9                                                                
R10      EQU   10                                                               
R11      EQU   11                                                               
R12      EQU   12                                                               
R13      EQU   13                                                               
R14      EQU   14                                                               
R15      EQU   15                                                               
         EJECT                                                                  
*                                       OPEN FILES                              
         OPEN  (INFIL,(INPUT),PRINT,(OUTPUT))                                   
*                                                                               
*        SETUP CONTROL AND PRINT A HEADER                                       
*                                                                               
         SR    R9,R9               INIT LINE COUNTER                            
         LA    R8,LININ            SAVE ADDRESS OF LININ                        
         PUT   PRINT,HEAD1                                                      
         PUT   PRINT,HEAD2                                                      
         PUT   PRINT,BLINE                                                      
         A     R9,=F'3'            INCR. LINE COUNTER                           
         EJECT                                                                  
*                                                                               
*        READ A RECORD LINE FROM INPUT FILE FOR ANALYSIS                        
*                                                                               
READIN   DC    0Y(0)                                                            
         MVI   FLG,C'N'            SET PRINT CONTROL FLAG                       
         LR    R6,R8                                                            
         GET   INFIL,LININ                                                      
         MVC   PRTLIN1(80),LININ       MOVE LINE CONTENTS TO PRINTAREA          
*                                                                               
*        SCAN THE LINE FOR UNDEFINED CHARACTERS                                 
*                                                                               
*                                                                               
SCANIT   LA    R5,81               INIT SCAN LENGTH                             
         EX    R5,TRCHAR                                                        
         CLM   R2,1,CHTBL          LOOK FOR LININ END                           
         BE    CHECKPRT                                                         
*                                                                               
*        SET FLAG FOR DETECTION OF UNDEFINED CHARACTER                          
*                                                                               
         MVI   FLG,C'Y'                                                         
*                                                                               
*        SCAN MAINTENANCE                                                       
*                                                                               
         LR    R4,R1               SAVE ADDRESS OF ARG. CHAR.                   
         SR    R4,R8               ARG. CHAR. COL. NUM  (0-79)                  
         LA    R6,1(R1)            SCAN START RESET                             
*                                                                               
*        TURN NIBBLES INTO PRINTABLE HEX                                        
*                                                                               
         SRDL  R2,4                HOLD RIGHT NIBBLE                            
         STC   R2,NIBL1            SAVE LEFT NIBBLE                             
         SRL   R2,4                DROP LEFT                                    
         SLDL  R2,4                GET RIGHT                                    
         STC   R2,NIBL2            SAVE RIGHT                                   
         TR    NIBL1,NIBTBL        CONVERT LEFT                                 
         TR    NIBL2,NIBTBL        CONVERT RIGHT                                
         EJECT                                                                  
*                                                                               
*        PUT HEX CHARS INTO OVER/UNDER PRINT LINES                              
*                                                                               
         LA    R7,PRTLIN2          START OF PRINT ARRAY                         
         AR    R7,R4               CHAR PPN ON OVER LINE                        
         MVC   0(1,R7),NIBL1       MOVE FIRST CHAR                              
         A     R7,=F'133'          ADJUST ARRAY POINT TO UNDER LINE             
         MVC   0(1,R7),NIBL2       MOVE SECOND CHAR                             
         B     SCANIT                                                           
CHECKPRT DC    0Y(0)                                                            
*                                                                               
*        IF NO UNDEFINED CHARACTER FOUND, DONT PRINT                            
*                                                                               
         CLI   FLG,C'N'                                                         
         BE    READIN                                                           
PRINTIT  DC    0Y(0)                                                            
*                                                                               
*        OVERFLOW THE PAGE IF NECESSARY                                         
*                                                                               
         C     R9,=F'53'           OVERFLOW YET...                              
         BNH   DETAIL              NO...PRINT DETAIL                            
*                                                                               
*        PRINT THE HEADER                                                       
*                                                                               
         PUT   PRINT,HEAD1                                                      
         PUT   PRINT,HEAD2                                                      
         PUT   PRINT,BLINE                                                      
         SR    R9,R9               INIT. LINE COUNTER                           
         A     R9,=F'3'            INCR. LINE COUNTER                           
*                                                                               
*        PRINT THE DETAIL                                                       
*                                                                               
DETAIL   PUT   PRINT,P1                                                         
         PUT   PRINT,P2                                                         
         PUT   PRINT,P3                                                         
         LA    R9,3(R9)            INCR. THE LINE COUNTER                       
*                                                                               
*        CLEAR OVER/UNDER PRINT LINES                                           
*                                                                               
         MVC   PRTLIN2(213),P2                                                  
         B     READIN                                                           
         EJECT                                                                  
*                                                                               
*        END OF JOB ROUTINE                                                     
*                                                                               
EOJRT    DC    0Y(0)                                                            
         CLOSE (INFIL,,PRINT)                                                   
         L     13,SAVEAREA+4                                                    
         PRINT GEN                                                              
         RETURN (14,12),T,RC=0                                                  
         EJECT                                                                  
         PRINT NOGEN                                                            
*                                                                               
*                                                                               
*                                                                               
TRCHAR   TRT   0(0,R6),CHTBL       EXECUTABLE INSTRUCTION FOR SCAN              
*                                                                               
*                                                                               
*                                                                               
*        TABLES WORKAREAS AND CONSTANTS                                         
*                                                                               
SAVEAREA DS    18F                                                              
*                                                                               
*                                                                               
CHTBL    DS    0Y(0)                                                            
         DC    X'C10102030405060708090A0B0C0D0E0F'                              
         DC    X'101112131415161718191A1B1C1D1E1F'                              
         DC    X'202122232425262728292A2B2C2D2E2F'                              
         DC    X'303132333435363738393A3B3C3D3E3F'                              
         DC    X'004142434445464748494A004C4D4E4F'                              
         DC    X'505152535455565758595A5B5C5D5E5F'                              
         DC    X'606162636465666768696A006C6D6E6F'                              
         DC    X'70717273747576777879007B7C7D7E7F'                              
         DC    X'800000000000000000008A8B8C8D8E8F'                              
         DC    X'900000000000000000009A9B9C9D9E9F'                              
         DC    X'A0A10000000000000000AAABACADAEAF'                              
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'                              
         DC    X'C0000000000000000000CACBCCCDCECF'                              
         DC    X'D0000000000000000000DADBDCDDDEDF'                              
         DC    X'E0E10000000000000000EAEBECEDEEEF'                              
         DC    X'00000000000000000000FAFBFCFDFEFF'                              
*                                                                               
NIBTBL   DS    0Y(0)                                                            
         DC    C'0123456789ABCDEF'                                              
*                                                                               
NIBL1    DS    CL1                                                              
NIBL2    DS    CL1                                                              
         EJECT                                                                  
*                                                                               
FLG      DS    CL1                 PRINT CONTROL FLAG                           
*                                                                               
BLINE    DC    133C' '                                                          
*                                                                               
HEAD1    DC    C'1'                                                             
         DC    C'         1'                                                    
         DC    C'    1    2'                                                    
         DC    C'    2    3'                                                    
         DC    C'    3    4'                                                    
         DC    C'    4    5'                                                    
         DC    C'    5    6'                                                    
         DC    C'    6    7'                                                    
         DC    C'    7    8'                                                    
         DC    52C' '                                                           
*                                                                               
HEAD2    DC    C' '                                                             
         DC    C'....5....0'                                                    
         DC    C'....5....0'                                                    
         DC    C'....5....0'                                                    
         DC    C'....5....0'                                                    
         DC    C'....5....0'                                                    
         DC    C'....5....0'                                                    
         DC    C'....5....0'                                                    
         DC    C'....5....0'                                                    
         DC    52C' '                                                           
*                                                                               
LININ    DS    CL80                                                             
         DC    X'00'                                                            
*                                                                               
P1       DC    C' '                                                             
PRTLIN1  DC    132C' '                                                          
P2       DC    C' '                                                             
PRTLIN2  DC    132C' '                                                          
P3       DC    C' '                                                             
PRTLIN3  DC    132C' '                                                          
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
INFIL    DCB   DSORG=PS,                                               C        
               MACRF=(GM),                                             C        
               DDNAME=INPUT,                                           C        
               EODAD=EOJRT                                                      
*                                                                               
PRINT    DCB   DSORG=PS,                                               C        
               MACRF=(PM),                                             C        
               DDNAME=PRINT,                                           C        
               LRECL=133,                                              C        
               RECFM=FBA                                                        
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'053PBTXTAN   05/01/02'                                      
         END   TXTAN                                                            
