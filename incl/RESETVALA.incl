*          DATA SET RESETVALA  AT LEVEL 019 AS OF 10/25/02                      
*                                                                               
*  REP ROUTINE TO SCAN THE NEWMON TABLE, AND RESET 'INVOICE $ PRESENT'          
*        BITS FOR CLOSED MONTHS.  THIS ROUTINE IS CALLED OPTIONALLY             
*        BASED ON REP RECORD PROFILE FLAG 11.  THIS VERSION OF THE              
*        ROUTINE IS CALLED IF A SPECIAL NRRGON OPTION IS REQUESTED,             
*        WHICH REQUIRES THE RECALIBRATION OF COMPARISON DATES.                  
*                                                                               
*                                                                               
*                                                                               
*  SETVALU3:  SETS INVOICE DATA BIT ON WHERE APPROPRIATE FOR MONTHS             
*        NOT AFTER CLOSED DATE                                                  
*        PRIOR YEAR/2 YRS PRIOR WILL BE PACED 1/2 YEARS BEFORE THE              
*        CURRENT CLOSE DATE.                                                    
**********************************************************************          
*  HISTORY OF CHANGES                                                *          
**********************************************************************          
*  JUN02/97 (BU ) --- INCORPORATE AS-AT DATE CHECK TO ENSURE NO      *          
*                     FUTURE DATES APPLIED                           *          
*                                                                    *          
*  AUG19/97 (BU ) --- ADDITIONAL CHECKS FOR INVOICE DATA AFTER       *          
*                     ASAT DATE FOR MONTH                            *          
*                                                                    *          
*  NOV17/97 (BU ) --- P4 FLAG:  NEXT YEAR OFFSET APPLIED: ADJUST     *          
*                     CLOSE DATE ARRAY DATES                         *          
*                                                                    *          
*  JAN07/98 (BU ) --- CORRECT PRIOR PERIOD ACTUAL CALCULATION        *          
*                                                                    *          
*  APR16/98 (BU ) --- UPDATE FOR SPECIAL NRGON OPTION (ALTERNATE     *          
*                     DATE SCHEME): NRRGFLAG=Y                       *          
*                                                                    *          
*                                                                    *          
*                                                                    *          
**********************************************************************          
*                                                                               
*                                                                               
*                                                                               
SETVALU3 NMOD1 0,*SVALU3*                                                       
         L     RC,0(R1)            RESET A(WORKSPACE)                           
         L     R2,4(R1)            RESET A(NEWMON TABLE)                        
         L     R7,8(R1)            RESET A(QWMONFOR: AS-AT DATES)               
         L     R6,12(R1)           RESET A(QWCURST3: PERIOD DATES)              
*                                                                               
         GOTO1 DATCON,DMCB,(5,WORK),(3,TODAY2DT)                                
*                                  SET SYSTEM DATE                              
         L     RE,ADCONLST                                                      
         USING ADCONSD,RE                                                       
         L     RF,VSPNDCTL                                                      
         LA    RF,8(RF)            SKIP FLAGS                                   
*                                                                               
         DROP  RE                                                               
*                                                                               
         LA    R4,2                STATION RECORD TYPE                          
         SLA   R4,2                SET DISPLACEMENT                             
         AR    R4,RF               ADD A(SPACEND CNTRL)                         
*                                     = A(1ST STATION ENTRY IN TABLE)           
         L     R4,0(R4)            LOAD ADDRESS                                 
SEVX0020 EQU   *                                                                
         CLI   0(R4),X'02'         STATION RECORD?                              
         BNE   SEVX0360            NO  - STATION NOT FOUND                      
         CLC   RCONKREP,2(R4)      SAME REP?                                    
         BNE   SEVX0040                                                         
         CLC   RCONKSTA,4(R4)      SAME STATION?                                
         BE    SEVX0060                                                         
SEVX0040 EQU   *                                                                
         ZIC   R5,1(R4)            BUMP TO NEXT ENTRY                           
         AR    R4,R5                                                            
         B     SEVX0020            GO BACK FOR NEXT                             
SEVX0060 EQU   *                                                                
         GOTO1 DATCON,DMCB,(2,(R7)),(3,SCLASAT2)                                
         MVC   SCLAAYR,SCLASAT2    SAVE YEAR OF CURRENT ASAT DATE               
         CLI   SCLASAT2+1,3        AS AT DATE'S MONTH MARCH OR LATER?           
         BNL   SEVX0080            YES                                          
         ZIC   RF,SCLASAT2+1       NO  - ADJUST MONTH, AND YEAR                 
         LA    RF,12(RF)           ADD 12 TO MONTH                              
         SH    RF,=H'2'            BACK UP TWO MONTHS                           
         STC   RF,SCLASAT2+1       PUT MONTH BACK                               
         ZIC   RF,SCLASAT2         BACK YEAR UP 1                               
         BCTR  RF,0                                                             
         STC   RF,SCLASAT2         PUT YEAR BACK                                
         B     SEVX0100                                                         
SEVX0080 EQU   *                                                                
         ZIC   RF,SCLASAT2+1       ADJUST MONTH BACK 2 MONTHS                   
         SH    RF,=H'2'            BACK UP TWO MONTHS                           
         STC   RF,SCLASAT2+1       PUT MONTH BACK                               
SEVX0100 EQU   *                                                                
         XC    SCLSDATE(6),SCLSDATE SET ALL DATES TO ZERO                       
         MVC   SCLSDATE,68(R4)     SET STATION CLOSE DATE                       
         CLC   SCLASAT2(2),SCLSDATE                                             
*                                  'ADJUSTED' CLOSE VS ACTL STN CLOSE           
         BNL   SEVX0120            USE STATION'S ACTUAL CLOSE                   
         MVC   SCLSDATE(2),SCLASAT2                                             
*                                  CALC'D EARLIER:  USE IT                      
SEVX0120 EQU   *                                                                
*                                                                               
*   TEST                                                                        
*        MVC   P+1(08),=C'CLOSES: '                                             
*        MVC   P+10(2),0(R7)       INSERT AS-AT DATE                            
*        MVC   P+14(3),SCLASAT2                                                 
*        MVC   P+20(2),68(R4)                                                   
*        MVC   P+26(2),SCLSDATE                                                 
*        GOTO1 REPORT                                                           
*   TEST END                                                                    
*                                                                               
         MVC   SCLSPRIR,SCLSDATE   CALCULATE PRIOR YEARS' DATES                 
         MVC   SCLSPRI2,SCLSDATE                                                
         OC    SCLSDATE,SCLSDATE   ANY DATE CLOSED?                             
         BZ    SEVX0140            NO  - LEAVE ALL AS ZERO                      
         ZIC   RF,SCLSDATE         GET CURRENT CLOSE DATE                       
         CLC   SCLAAYR,TODAY2DT    CURRENT AS-AT DATE IN NEXT YEAR?             
         BH    SEVX0130            YES - DON'T BACK UP FOR PRIOR YEAR           
         CLC   TODAY2DT(1),0(R6)   NO  - CURRENT DATA IN NEXT YEAR?             
         BL    SEVX0130            YES - DON'T BACK UP FOR PRIOR YEAR           
         BCTR  RF,0                NO  - BACK UP 1 YEAR                         
SEVX0130 EQU   *                                                                
         STC   RF,SCLSPRIR         SAVE PRIOR YEAR                              
         BCTR  RF,0                BACK UP 2YR PRIOR 1 YEAR                     
         STC   RF,SCLSPRI2         SET 2YR PRIOR 1 MORE YEAR BACK               
SEVX0140 EQU   *                                                                
*                                                                               
*   TEST                                                                        
*        MVC   P+1(06),=C'DATES:'                                               
*        MVC   P+10(2),SCLSDATE                                                 
*        MVC   P+20(2),SCLSPRIR                                                 
*        MVC   P+30(2),SCLSPRI2                                                 
*        MVC   P+40(3),TODAY2DT                                                 
*        MVC   P+50(3),0(R6)                                                    
*        GOTO1 REPORT                                                           
*   TEST END                                                                    
*                                                                               
*                                                                               
*   TABLE RESETTING.  EACH SLOT IN THE TABLE WILL BE INSPECTED TO               
*        DETERMINE IF AUTOMATIC INVENTORY SETTING IS TO BE DONE.                
*        IN EACH OF THE INSTANCES WHERE INVOICE DATA SUPERCEDES                 
*        ESTIMATE DATA, THE BUCKETS WILL BE CHECKED IN THIS MANNER:             
*        1.  IF THE DATE (CURRENT/PRIOR/2YR PRIOR) IS GREATER                   
*            THAN THE STATION'S CLOSE DATE, NO ACTION WILL BE TAKEN.            
*        2.  IF DATE IS LESS THAN/EQUAL TO CLOSE DATE, THE ESTIMATE             
*            DOLLAR BUCKET WILL BE CHECKED.  IF ZERO, NO ACTION WILL            
*            BE TAKEN.  THIS WILL ENSURE THAT BUCKETS OUTSIDE THE               
*            FLIGHT DATES OF THE ORDER ARE NOT MISTAKENLY SET, AND              
*            WILL ALWAYS PROVIDE THE BEST DOLLAR FIGURE IN ALL CASES            
*            WITH NO RESETTING.                                                 
*        3.  IF ESTIMATE DOLLAR BUCKET IS NON-ZERO, THE APPROPRIATE             
*            INVOICE BIT IN THE FLAG BYTE WILL BE SET.  IF ALREADY              
*            SET BECAUSE INVOICE DOLLARS ARE PRESENT, THIS WILL HAVE            
*            NO EFFECT.  IF NOT ALREADY SET, THIS WILL HAVE THE EFFECT          
*            OF INDICATING $0 INVOICE TO ANYTHING EXAMINING THE BUCKET          
*            AT POST TIME.                                                      
*                                                                               
         L     R4,ANEWMON          SET A(NEW MONTH TABLE)                       
SEVX0160 EQU   *                                                                
*                                                                               
*   TEST 2RE:                                                                   
*        MVC   P+1(4),=C'PRE:'                                                  
*        GOTO1 HEXOUT,DMCB,RCONKCON,P+6,4,=C'TOG'                               
*        GOTO1 HEXOUT,DMCB,SCLSDATE,P+20,2,=C'TOG'                              
*        MVC   P+30(90),0(R4)                                                   
*        GOTO1 REPORT                                                           
*   TEST PRE: END                                                               
*                                                                               
         OC    0(6,R4),0(R4)       ANY ENTRY IN TABLE SLOT?                     
         BZ    SEVX0360            NO  - FINISHED                               
         LR    R3,R4               SET A(BUCKETS WITHIN SLOT)                   
         LA    R3,BUCKDISP(R3)     BUMP TO BUCKETS                              
         CLC   CURDATE(2,R4),SCLSDATE                                           
*                                  CURRENT MONTH CLOSED?                        
         BH    SEVX0180            NO  - NO FURTHER CHECKS                      
         OC    TOTORD(4,R3),TOTORD(R3)                                          
*                                  YES - TOTAL ORDERED ZERO?                    
         BZ    SEVX0180            YES - LEAVE AS IS                            
         TM    FLAG6(R4),X'20'     NO  - INV$ AFTER ASAT DATE?                  
         BO    SEVX0180            YES - DON'T SET 'INVOICE $$ PRESENT'         
         OI    FLAG6(R4),X'01'     NO  - TURN ON 'INVOICE $$ PRESENT'           
SEVX0180 EQU   *                                                                
         CLC   PRIDATE(2,R4),SCLSPRIR                                           
*                                  PRIOR   MONTH CLOSED?                        
         BH    SEVX0200            NO  - NO FURTHER CHECKS                      
         OC    PRASATOR(4,R3),PRASATOR(R3)                                      
*                                  YES - PRIOR AS AT ORDERED ZERO?              
         BZ    SEVX0200            YES - LEAVE AS IS                            
         TM    FLAG6(R4),X'40'     NO  - INV$ AFTER ASAT DATE?                  
         BO    SEVX0200            YES - DON'T SET 'INVOICE $$ PRESENT'         
         OI    FLAG6(R4),X'04'     NO  - TURN ON 'INVOICE $$ PRESENT'           
SEVX0200 EQU   *                                                                
*                                                                               
*   BECAUSE THIS FIELD IS BEING USED FOR ACTUALS IN PRIOR YEAR,  IT             
*      IS CHECKING AGAINST THE CLOSED DATE IN THE **CURRENT*** YEAR!            
*                                                                               
         CLC   PRIDATE(2,R4),SCLSDATE                                           
*                                  PRIOR   MONTH CLOSED?                        
         BH    SEVX0220            NO  - NO FURTHER CHECKS                      
         OC    PRTOTORD(4,R3),PRTOTORD(R3)                                      
*                                  YES - PRIOR TOTAL ORDERED ZERO?              
         BZ    SEVX0220            YES - LEAVE AS IS                            
         TM    FLAG6(R4),X'40'     NO  - INV$ AFTER ASAT DATE?                  
         BO    SEVX0220            YES - DON'T SET 'INVOICE $$ PRESENT'         
         OI    FLAG6(R4),X'02'     NO  - TURN ON 'INVOICE $$ PRESENT'           
SEVX0220 EQU   *                                                                
*                                                                               
*   TEST POST:                                                                  
*        MVC   P+1(13),=C'PRI2DATE TEST'                                        
*        GOTO1 REPORT                                                           
*        GOTO1 HEXOUT,DMCB,RCONKCON,P+6,4,=C'TOG'                               
*        GOTO1 HEXOUT,DMCB,PRI2DATE(R4),P+16,2,=C'TOG'                          
*        GOTO1 HEXOUT,DMCB,SCLSPRI2,P+22,2,=C'TOG'                              
*        MVC   P+30(4),P2ASATOR(R3)                                             
*        MVC   P+38(22),=C'NO  INVOICE AFTER ASAT'                              
*        TM    FLAG6(R4),X'80'     INV$ AFTER ASAT?                             
*        BNO   TEST0110            NO                                           
*        MVC   P+38(22),=C'YES INVOICE AFTER ASAT'                              
TEST0110 EQU   *                                                                
*        GOTO1 REPORT                                                           
*        GOTO1 REPORT                                                           
*   TEST POST: END                                                              
*                                                                               
*                                                                               
         CLC   PRI2DATE(2,R4),SCLSPRI2                                          
*                                  2YR PRIOR   MONTH CLOSED?                    
         BH    SEVX0240            NO  - NO FURTHER CHECKS                      
         OC    P2ASATOR(4,R3),P2ASATOR(R3)                                      
*                                  YES - 2YR PRIOR AS AT ORDERED ZERO?          
         BZ    SEVX0240            YES - LEAVE AS IS                            
         TM    FLAG6(R4),X'80'     NO  - INV$ AFTER ASAT DATE?                  
         BO    SEVX0240            YES - DON'T SET 'INVOICE $$ PRESENT'         
         OI    FLAG6(R4),X'10'     NO  - TURN ON 'INVOICE $$ PRESENT'           
SEVX0240 EQU   *                                                                
*                                                                               
*   BECAUSE THIS FIELD IS BEING USED FOR ACTUALS 2 YEARS PRIOR, IT              
*      IS CHECKING AGAINST THE CLOSED DATE IN THE ***PRIOR*** YEAR!             
*                                                                               
         CLC   PRI2DATE(2,R4),SCLSPRIR                                          
*                                  2YR PRIOR   MONTH CLOSED?                    
         BH    SEVX0260            NO  - NO FURTHER CHECKS                      
         OC    P2TOTORD(4,R3),P2TOTORD(R3)                                      
*                                  YES - 2YR PRIOR TOTAL ORDERED ZERO?          
         BZ    SEVX0260            YES - LEAVE AS IS                            
         TM    FLAG6(R4),X'80'     NO  - INV$ AFTER ASAT DATE?                  
         BO    SEVX0260            YES - DON'T SET 'INVOICE $$ PRESENT'         
         OI    FLAG6(R4),X'08'     NO  - TURN ON 'INVOICE $$ PRESENT'           
SEVX0260 EQU   *                                                                
*                                                                               
*   'NEXT YEAR' IS BEING USED FOR A SPECIAL NRRGON OPTION, WHICH,               
*        THRU DATE MANIPULATION, PUTS 'LAST YEAR'S' FIGURES, AS OF AN           
*        ALTERNATE AS-AT DATE, IN THE 'NEXT YEAR' SLOTS                         
*        FURTHER, THIS IS CONSIDERED 'ACTUALS', BEING 'AS OF' THE               
*        CURRENT YEAR, SO IT IS COMPARED AGAINST THE CURRENT YEAR               
*        CLOSE DATE.                                                            
*                                                                               
         CLC   NEXTDATE(2,R4),SCLSDATE                                          
*                                  'NEXT YEAR' = 'LAST YEAR',BUT                
*                                     USE CURRENT CLOSE DATE                    
*                                                                               
         BH    SEVX0280            NO  - NO FURTHER CHECKS                      
         OC    NXASATOR(4,R3),NXASATOR(R3)                                      
*                                  YES - NEXT YR AS AT ORDERED ZERO?            
         BZ    SEVX0280            YES - LEAVE AS IS                            
         TM    FLAG7(R4),X'40'     NO  - INV$ AFTER ASAT DATE?                  
         BO    SEVX0280            YES - DON'T SET 'INVOICE $$ PRESENT'         
         OI    FLAG7(R4),X'20'     NO  - TURN ON 'INVOICE $$ PRESENT'           
SEVX0280 EQU   *                                                                
*                                                                               
*   BECAUSE THIS FIELD IS BEING USED FOR ACTUALS IN 'NEXT' YEAR,                
*      WHICH IS REALLY THE PRIOR YEAR IN THIS INSTANCE, IT                      
*      IS CHECKING AGAINST THE CLOSED DATE IN THE **CURRENT** YEAR!             
*                                                                               
         CLC   NEXTDATE(2,R4),SCLSDATE                                          
*                                  PRIOR MONTH CLOSED?                          
         BH    SEVX0300            NO  - NO FURTHER CHECKS                      
         OC    NEXTOTOR(4,R3),NEXTOTOR(R3)                                      
*                                  YES - NEXT YR TOTAL ORDERED ZERO?            
         BZ    SEVX0300            YES - LEAVE AS IS                            
         TM    FLAG7(R4),X'40'     NO  - INV$ AFTER ASAT DATE?                  
         BO    SEVX0300            YES - DON'T SET 'INVOICE $$ PRESENT'         
         OI    FLAG7(R4),X'80'     NO  - TURN ON 'INVOICE $$ PRESENT'           
SEVX0300 EQU   *                                                                
*                                                                               
*   TEST POST:                                                                  
*        MVC   P+1(5),=C'POST:'                                                 
*        GOTO1 HEXOUT,DMCB,RCONKCON,P+6,4,=C'TOG'                               
*        GOTO1 HEXOUT,DMCB,SCLSDATE,P+20,2,=C'TOG'                              
*        MVC   P+30(90),0(R4)                                                   
*        GOTO1 REPORT                                                           
*   TEST POST: END                                                              
*                                                                               
         LA    R4,NEXTBUCK(R4)     BUMP TO NEXT BUCKET                          
         B     SEVX0160            GO BACK FOR NEXT BUCKET                      
SEVX0360 EQU   *                                                                
         XIT1                                                                   
*                                                                               
SCLSDATE DS    CL2                 STATION CLOSE DATE                           
SCLSPRIR DS    CL2                 STATION CLOSE DATE: PRIOR YEAR               
SCLSPRI2 DS    CL2                 STATION CLOSE DATE: 2YR PRIOR                
SCLASAT2 DS    CL3                 AS-AT YEAR/MONTH DATES                       
TODAY2DT DS    CL3                 TODAY'S DATE                                 
SCLAAYR DS     CL1                 CURRENT AS-AT YEAR                           
*                                                                               
