*          DATA SET TAUPDOAP   AT LEVEL 001 AS OF 10/28/10                      
***********************************************************************         
*        ROUTINE ENSURES OVERSCALE AMOUNT/PERCENTAGE FIELD            *         
*        REMAINS IN SYNC WITH COMMERCIAL TYPE AND MEDIA               *         
*        ON ENTRY ... P1 BYTE 0 = OVERSCALE AMOUNT/PCT ELEMENT CODE   *         
*                     P1        = A(CAST RECORD)                      *         
*                     TGCTEQU   = A(COMMERCIAL TYPE)                  *         
***********************************************************************         
                                                                                
UPDOAP   NTR1                                                                   
         L     R4,0(R1)                                                         
         ST    R4,ACASTREC         R4=A(CAST RECORD)                            
                                                                                
         USING TAOPD,R4                                                         
         MVC   ELCODE,0(R1)        IF OVERSCALE AMOUNT/PERCENTAGE               
         BRAS  RE,GETEL            FIELD IS PRESENT                             
         JNE   XIT                                                              
         MVC   OLENGTH,TAOPLEN     SAVE ORIGINAL LENGTH                         
                                                                                
         ZIC   R0,TAOPNUM          R0 = # SUB ELEMENTS                          
         LA    R2,TAOPSBEL         R2 = A(FIRST SUB ELEMENT)                    
         DROP  R4                                                               
                                                                                
         USING TAOPD,R3                                                         
         LA    R3,BLOCK                                                         
         XC    BLOCK(255),BLOCK    INITIALIZE NEW OVERSCALE AMOUNT/             
         MVC   TAOPEL,ELCODE       PERCENTAGE ELEMENT                           
         MVI   TAOPLEN,TAOPLNQ                                                  
         LA    R4,TAOPSBEL                                                      
                                                                                
UOAP10   CLC   0(3,R2),=CL3' '     IF USE CODE IS ALL                           
         JE    UOAP30                                                           
         CLC   0(3,R2),=C'ARE'     OR ARE                                       
         JE    UOAP30              THEN IT REMAINS VALID                        
                                                                                
         GOTO1 USEVAL,DMCB,(X'40',0(R2)),0                                      
         JE    *+6                                                              
         DC    H'00'               OTHERWISE, SET USE VARIABLES                 
                                                                                
         CLI   TGUSMEDS,ALL        ENSURE USE IS VALID FOR MEDIA                
         JE    UOAP20                                                           
         MVC   BYTE,TGUSMEDS                                                    
         NC    BYTE,TGMEEQU                                                     
         JZ    UOAP40                                                           
                                                                                
UOAP20   GOTO1 VALTYP,DMCB,TGCTEQU ENSURE USE IS VALID FOR COML TYPE            
         JNE   UOAP40                                                           
                                                                                
UOAP30   ZIC   RE,TAOPLEN          IF USE TYPE IS VALID FOR THIS                
         AHI   RE,L'TAOPSBEL       MEDIA NAD TYPE                               
         STC   RE,TAOPLEN                                                       
         ZIC   RE,TAOPNUM          ADD IT TO THE NEW ELEMENT                    
         AHI   RE,1                                                             
         STC   RE,TAOPNUM                                                       
         MVC   0(L'TAOPSBEL,R4),0(R2)                                           
         LA    R4,L'TAOPSBEL(R4)                                                
                                                                                
UOAP40   LA    R2,L'TAOPSBEL(R2)   BUMP TO NEXT USE IN ORIGINAL ELEMENT         
         BCT   R0,UOAP10           REPEAT UNTIL NO MORE SUB-ELEMENTS            
                                                                                
         CLC   TAOPLEN,OLENGTH     IF ELEMENT IS CHANGING                       
         JE    XIT                                                              
         DROP  R3                                                               
                                                                                
         USING TAOPD,R4                                                         
         L     R4,ACASTREC                                                      
         BRAS  RE,GETEL            SET THE ORIGINAL ELEMENT FOR DELETE          
         JE    *+6                                                              
         DC    H'00'                                                            
         MVI   0(R4),X'FF'                                                      
         DROP  R4                                                               
                                                                                
         CLI   BLOCK+TAOPLEN-TAOPD,TAOPLNQ                                      
         JE    XIT                 AND IF ELEMENT SHOULD STILL EXIST            
         MVC   ELEMENT,BLOCK       ADD IT NOW                                   
         GOTO1 ADDELEM                                                          
         J     XIT                                                              
         EJECT                                                                  
