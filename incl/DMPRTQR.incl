*          DATA SET DMPRTQR    AT LEVEL 002 AS OF 12/04/00                      
* LOCK/UNLOCK PRTQUE DISK FILE                                                  
*                                                                               
PQLOCK   LA    R0,C'E'             SET ENQUEUE VALUE                            
         B     *+8                                                              
PQUNLK   LA    R0,C'D'             SET DEQUEUE VALUE                            
         ST    RE,CISAVRE                                                       
         CLI   CFPQID,C'A'         SET PRTQ ID IF NOT DEFINED                   
         BNL   *+10                                                             
         MVC   CFPQID,=C'PRTQU'                                                 
         L     RF,CIENQDEQ                                                      
         GOTO1 (RF),CIP1,((R0),CFPQID)                                          
         L     RE,CISAVRE                                                       
         BR    RE                                                               
         SPACE 1                                                                
* INITIALISE INDEX SEARCH LOOP     SETS R5=A(FIRST ENTRY)                       
*                                                                               
CXLOOPI  XC    CXPAGE,CXPAGE                                                    
         LH    R5,CICINDX                                                       
         B     *+14                                                             
CXLOOPJ  MVC   CXPAGE,CJPAGE                                                    
         LH    R5,CJENTRY                                                       
         STH   R5,CXENTRY                                                       
         MH    R5,CINDXLN                                                       
         LA    R5,CXREC(R5)                                                     
         BR    RE                                                               
         SPACE 1                                                                
* TERMINATE INDEX SEARCH LOOP      NEXT ENTRY RETURN - 0(RE)                    
*                                  PAGE FAULT RETURN - 4(RE)                    
*                                  END OF NDX RETURN - 8(RE)                    
CXLOOPX  LH    RF,CXENTRY                                                       
         LA    RF,1(RF)                                                         
         STH   RF,CXENTRY                                                       
         CH    RF,CIENTRYS                                                      
         BNL   CXLOOPX1                                                         
         AH    R5,CINDXLN                                                       
         CLC   0(2,R5),=X'FFFF'                                                 
         BNER  RE                                                               
         B     8(RE)               END OF INDEX                                 
CXLOOPX1 XC    CXENTRY,CXENTRY                                                  
         LH    RF,CXPAGE                                                        
         LA    RF,1(RF)                                                         
         STH   RF,CXPAGE                                                        
         LA    R5,CXREC                                                         
         B     4(RE)               END OF PAGE                                  
         EJECT                                                                  
* INDEX PAGE/ENTRY TO CONTROL INTERVAL DISK ADDR                                
*                                                                               
GETCAD   LH    RF,CXPAGE           CAD=(CXP*ENT+CXE)*TRKS + 1                   
         CLC   CXPAGE(4),CJPAGE                                                 
         BNL   GETCAD1                                                          
         MH    RF,CIENTRYS                                                      
         AH    RF,CXENTRY                                                       
         MH    RF,CITRKS                                                        
         LA    RF,1(RF)                                                         
         LH    R0,CITRKS                                                        
         B     GETCAD2                                                          
GETCAD1  SH    RF,CJPAGE           CI IS IN PART 2 OF INDEX                     
         MH    RF,CIENTRYS                                                      
         AH    RF,CXENTRY                                                       
         SH    RF,CJENTRY                                                       
         MH    RF,CJTRKS                                                        
         SR    R0,R0                                                            
         ICM   R0,3,CJSTTRK                                                     
         AR    RF,R0                                                            
         LH    R0,CJTRKS                                                        
GETCAD2  SLL   RF,16                                                            
         ST    RF,CIADDR                                                        
         MVI   CIADDR+2,1                                                       
         SRL   RF,16                                                            
         AR    RF,R0                                                            
         BCTR  RF,0                                                             
         SLL   RF,16                                                            
         ST    RF,CIADDRX                                                       
         MVC   CIADDRX+2(1),CIHIREC+1                                           
         BR    RE                                                               
         SPACE 1                                                                
* INDEX PAGE/ENTRY TO INDEX DISK ADDR                                           
*                                                                               
GETXAD   ST    RE,CISAVRE          XADT=CXP/TRKS + 1                            
         SR    RE,RE               XADB=REM + 1                                 
         LH    RF,CXPAGE                                                        
         LH    R0,CIHIREC                                                       
         DR    RE,R0                                                            
         LA    RE,1(RE)                                                         
         LA    RF,1(RF)                                                         
         STH   RF,CXADDR                                                        
         STC   RE,CXADDR+2                                                      
         MVI   CXADDR+3,0                                                       
         L     RE,CISAVRE                                                       
         BR    RE                                                               
         SPACE 1                                                                
* CONTROL INTERVAL DISK ADDR TO INDEX PAGE/ENTRY                                
*                                                                               
GETXPE   ST    RE,CISAVRE          CXP=(CAD-1)/(CXE*TRKS)                       
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         ICM   RF,3,CIADDR                                                      
         CLM   RF,3,CJSTTRK                                                     
         BNL   GETXPE1                                                          
         BCTR  RF,0                                                             
         LH    R0,CITRKS                                                        
         MH    R0,CIENTRYS                                                      
         DR    RE,R0                                                            
         STH   RF,CXPAGE                                                        
         SRDL  RE,32                                                            
         LH    R0,CITRKS                                                        
         DR    RE,R0                                                            
         STH   RF,CXENTRY                                                       
         B     GETXPEX                                                          
GETXPE1  SR    R0,R0               CI IS IN PART 2 OF INDEX                     
         ICM   R0,3,CJSTTRK                                                     
         SR    RF,R0                                                            
         LH    R0,CJTRKS                                                        
         MH    R0,CIENTRYS                                                      
         DR    RE,R0                                                            
         AH    RF,CJPAGE                                                        
         STH   RF,CXPAGE                                                        
         SRDL  RE,32                                                            
         LH    R0,CJTRKS                                                        
         DR    RE,R0                                                            
         AH    RF,CJENTRY                                                       
         CH    RF,CIENTRYS                                                      
         BL    *+20                                                             
         SH    RF,CIENTRYS         ADJUST FOR PART 2 INDEX OFFSET               
         LH    RE,CXPAGE                                                        
         LA    RE,1(RE)                                                         
         STH   RE,CXPAGE                                                        
         STH   RF,CXENTRY                                                       
GETXPEX  L     RE,CISAVRE                                                       
         BR    RE                                                               
         SPACE 1                                                                
* REPORT SEQUENCE NUMBER TO INDEX PAGE ENTRY                                    
*                                                                               
RSNXPE   ST    RE,CISAVRE          CXP=RSN/ENT                                  
         SR    RE,RE               CXE=REM                                      
         SR    RF,RF                                                            
         ICM   RF,3,CIRSN                                                       
         LH    R0,CIENTRYS                                                      
         DR    RE,R0                                                            
         STH   RF,CXPAGE                                                        
         STH   RE,CXENTRY                                                       
         L     RE,CISAVRE                                                       
         BR    RE                                                               
         SPACE 1                                                                
* INDEX PAGE ENTRY TO REPORT SEQUENCE NUMBER                                    
*                                                                               
XADRSN   LH    RF,CXPAGE           RSN=(CXP*ENT)+CXE                            
         MH    RF,CIENTRYS                                                      
         AH    RF,CXENTRY                                                       
         STH   RF,CIRSN                                                         
         BR    RE                                                               
