*          DATA SET RECYCLMGS  AT LEVEL 201 AS OF 01/17/00                      
**CYCMG>                                                                        
*                                                                               
*   THIS ROUTINE BUILDS THE MAKEGOOD TABLE FOR PREVIOUS TARGET                  
*      BUYLINE                                                                  
*                                                                               
CYCLEMKG NMOD1 0,*CYCM*                                                         
         L     RC,0(R1)            RESET A(WORKSPACE)                           
         MVC   LASTMGMS,RBUYKMLN                                                
         MVC   FIRSTMG,RBUYKLIN                                                 
         XC    FIRSTMGD,FIRSTMGD                                                
         MVC   KEY,EXTRAKEY        REREAD THIS BUY                              
         GOTO1 VHIGH                                                            
         CLC   KEY(27),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                HUH?                                         
CMKG0010 EQU   *                                                                
         LA    R6,RBUYELEM         SET A(DESCRIPTOR ELT IN BUY)                 
CMKG0020 EQU   *                                                                
         CLI   0(R6),0             END OF RECORD?                               
         BE    CMKG0100            YES - FINISHED WITH CYCLE                    
         CLI   0(R6),X'05'         NO  - M/G REFERENCE ELEMENT?                 
         BE    CMKG0040            YES                                          
CMKG0030 EQU   *                                                                
         ZIC   RF,1(R6)            NO  - BUMP TO NEXT ELT                       
         AR    R6,RF                                                            
         B     CMKG0020            GO BACK FOR NEXT ELT                         
CMKG0040 EQU   *                                                                
         OC    FIRSTMGD,FIRSTMGD                                                
         BNZ   CMKG0044                                                         
*                                                                               
         GOTO1 DATCON,DMCB,(3,RBUYMGD1),(20,FIRSTMGD)                           
*                                                                               
CMKG0044 EQU *                                                                  
         L     R2,ANXTMGTB         SET A(NEXT OPEN SLOT IN TABLE)               
         USING RBUYMGEL,R6                                                      
         CLI   RBUYMGSP,0          ANY SPOTS IN MAKEGOOD?                       
         BZ    CMKG0030            NO  - DON'T TABLE THIS UP                    
         MVC   0(3,R2),RBUYMGD1    MOVE DATE(3)                                 
         MVC   3(1,R2),RBUYMGLI    MOVE LINE#(1)                                
         MVC   4(1,R2),RBUYMGSP    MOVE #SPOTS(1)                               
         LA    R2,5(R2)            BUMP NEXT AVAILABLE SLOT                     
         ST    R2,ANXTMGTB         SAVE A(NEXT SLOT)                            
         L     RF,MGCOUNTR         INCREMENT MAKEGOOD COUNTER                   
         LA    RF,1(RF)                                                         
         ST    RF,MGCOUNTR                                                      
         B     CMKG0030            GO BACK FOR NEXT ELEMENT                     
*                                                                               
         DROP  R6                                                               
*                                                                               
CMKG0100 EQU   *                                                                
         GOTO1 VSEQ                                                             
*                                                                               
         CLC   KEY(RBUYKLIN-RBUYKEY),KEYSAVE    SAME MASTER LINE?               
         BNE   CMKG0120                         YES                             
         GOTO1 VGETREC,DMCB,RBUYREC                                             
         B     CMKG0010                         YES                             
*                                                                               
CMKG0120 EQU   *                                                                
         MVC   KEY(27),EXTRAKEY        REREAD THIS BUY                          
         GOTO1 VHIGH                                                            
         CLC   KEY(27),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                HUH?                                         
         GOTO1 VGETREC,DMCB,RBUYREC                                             
*                                  RELOAD THE BUY RECORD                        
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
**CYCMG>                                                                        
