*          DATA SET TATRNSAGT  AT LEVEL 001 AS OF 03/09/10                      
***********************************************************************         
*        ROUTINE TO TRANSLATE AGENT CODE                              *         
*        INCLUDED BY TASYSVAL, TALDCPTR, TALNK01,                     *         
*                    TASYSIOB AND TARCVAGT                            *         
*        ON ENTRY ... P1 BYTE 0 = X'80' CHARACTER TO NUMERIC          *         
*                                 X'40' NUMERIC TO CHARACTER          *         
*                     P1        = A(INCOMING AGENT CODE)              *         
*                     P2        = A(TRANSLATED AGENT CODE)            *         
***********************************************************************         
                                                                                
         LA    R5,APRETAB          R5 = A(AGENT PREFIX TABLE)                   
                                                                                
***********************************************************************         
                                                                                
         TM    0(R1),X'80'         IF TRANSLATING CHARACTER AGENT CODE          
         JZ    A2C10               TO NUMERIC                                   
         L     R2,0(R1)            R2=A(AGENT CODE - CHARACTER VALUE)           
         L     R3,4(R1)            R3=A(OUTPUT FIELD - NUMERIC VALUE)           
                                                                                
         GOTO1 NUMCHECK,DMCB,1(R2) ENSURE CHARACTER 2 IS NUMERIC                
         JNE   NO                                                               
         GOTO1 NUMCHECK,DMCB,2(R2) ENSURE CHARACTER 3 IS NUMERIC                
         JNE   NO                                                               
         GOTO1 NUMCHECK,DMCB,3(R2) ENSURE CHARACTER 4 IS NUMERIC                
         JNE   NO                                                               
                                                                                
         GOTO1 NUMCHECK,DMCB,0(R2) IF FIRST CHARACTER IS ALSO NUMERIC           
         JNE   A2N10                                                            
         PACK  DUB,0(4,R2)                                                      
         CVB   R1,DUB                                                           
         STCM  R1,3,0(R3)          CONVERT WHOLE CODE TO NUMERIC                
         J     YES                                                              
                                                                                
A2N10    CLC   0(1,R2),8(R5)       ELSE ENSURE THAT FIRST CHARACTER             
         JE    A2N20               IS A VALID AGENT PREFIX                      
         LA    R5,12(R5)                                                        
         CLI   0(R5),X'FF'                                                      
         JNE   A2N10                                                            
         J     NO                                                               
                                                                                
A2N20    MVI   DMCB,C'0'                                                        
         MVC   DMCB+1(3),1(R2)                                                  
         PACK  DUB,DMCB(4)                                                      
         CVB   R1,DUB                                                           
         A     R1,0(R5)            AND ADD PREFIX-REPRESENTED NUMBER            
         STCM  R1,3,0(R3)          TO TRAILING 3 DIGITS                         
         J     YES                                                              
                                                                                
***********************************************************************         
                                                                                
A2C10    TM    0(R1),X'40'         IF TRANSLATING NUMERIC AGENT CODE            
         JZ    XIT                 TO CHARACTER                                 
         L     RE,0(R1)                                                         
         OC    0(2,RE),0(RE)                                                    
         JZ    XIT                                                              
         ZICM  R2,0(RE),2          R2=A(AGENT CODE - NUMERIC VALUE)             
         L     R3,4(R1)            R3=A(OUTPUT FIELD - CHARACTER VALUE)         
                                                                                
         C     R2,0(R5)            IF AGENT NUMBER CAN BE DISPLAYED             
         JNL   A2C20               AS ALL NUMERIC, NO CONVERSION REQ'D          
         EDIT  (R2),(4,0(R3)),FILL=0,ZERO=NOBLANK                               
         J     XIT                                                              
                                                                                
A2C20    C     R2,4(R5)            FIND APPROPRIATE LETTER PREFIX               
         JNH   A2C30                                                            
         LA    R5,12(R5)                                                        
         CLI   0(R5),X'FF'                                                      
         JNE   A2C20                                                            
         DC    H'00'                                                            
                                                                                
A2C30    MVC   0(1,R3),8(R5)       AND TACK ON TRAILING 3 DIGITS                
         EDIT  (R2),(3,1(R3)),FILL=0,ZERO=NOBLANK                               
         J     XIT                                                              
                                                                                
***********************************************************************         
*        ROUTINE VALIDATES A BYTE AS A NUMERIC CHARACTER              *         
*        ON ENTRY ... P1 = A(CHARACTER VALUE)                         *         
***********************************************************************         
                                                                                
NUMCHECK NTR1                                                                   
         L     R1,0(R1)                                                         
         CLI   0(R1),C'0'                                                       
         JL    NO                                                               
         CLI   0(R1),C'9'                                                       
         JNH   YES                                                              
         J     NO                                                               
                                                                                
***********************************************************************         
*        AGENT PREFIX TABLE FOR TRANSLATION                           *         
***********************************************************************         
                                                                                
APRETAB  DC    0F                                                               
         DC    F'10000',F'10999',C'A'                                           
         DC    F'11000',F'11999',C'B'                                           
         DC    F'12000',F'12999',C'C'                                           
         DC    F'13000',F'13999',C'D'                                           
         DC    F'14000',F'14999',C'E'                                           
         DC    F'15000',F'15999',C'F'                                           
         DC    F'16000',F'16999',C'G'                                           
         DC    F'17000',F'17999',C'H'                                           
         DC    F'18000',F'18999',C'I'                                           
         DC    F'19000',F'19999',C'J'                                           
         DC    F'20000',F'20999',C'K'                                           
         DC    F'21000',F'21999',C'L'                                           
         DC    F'22000',F'22999',C'M'                                           
         DC    F'23000',F'23999',C'N'                                           
         DC    F'24000',F'24999',C'O'                                           
         DC    F'25000',F'25999',C'P'                                           
         DC    F'26000',F'26999',C'Q'                                           
         DC    F'27000',F'27999',C'R'                                           
         DC    F'28000',F'28999',C'S'                                           
         DC    F'29000',F'29999',C'T'                                           
         DC    F'30000',F'30999',C'U'                                           
         DC    F'31000',F'31999',C'V'                                           
         DC    F'32000',F'32999',C'W'                                           
         DC    F'33000',F'33999',C'X'                                           
         DC    F'34000',F'34999',C'Y'                                           
         DC    F'35000',F'35999',C'Z'                                           
         DC    X'FF'                                                            
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
