*          DATA SET POSTON     AT LEVEL 002 AS OF 05/01/02                      
ICSPOST  CSECT                                                                  
         NMOD1 0,ICSPOST                                                        
         SPACE 2                                                                
         L     RC,0(R1)                                                         
         USING GENOLD,RC                                                        
         USING BYELEMD,R6                                                       
         USING IELEMD,R7                                                        
         USING BUYRECD,R9                                                       
         USING T215FFD,RA                                                       
         SPACE 2                                                                
*                                  INITIALIZE ADDPARS                           
         XC    ADDPARS,ADDPARS                                                  
         LA    R9,IOAREA                                                        
         ST    R9,ADDPAR1                                                       
         LA    R0,BUYDSKS          DISK ADDRESSES                               
         ST    R0,DSKPOINT                                                      
         XC    AFEL(6),AFEL                                                     
         MVI   AFCODE,X'10'                                                     
         XC    FCMEL(7),FCMEL                                                   
         MVI   FCMCOD,X'12'                                                     
         XC    RESPEL(6),RESPEL                                                 
         MVI   RESPCOD,X'17'                                                    
         LA    R0,AFEL                                                          
         ST    R0,ADDPAR2                                                       
         EJECT                                                                  
         SPACE 3                                                                
NXTREC   L     R2,DSKPOINT                                                      
         OC    0(4,R2),0(R2)                                                    
         BZ    PSEXT                                                            
         LA    R0,BUYDSKSX                                                      
         CR    R2,R0                                                            
         BNL   PSEXT                                                            
         SPACE 2                                                                
         MVC   KEY+14(4),0(R2)                                                  
         BAS   RE,CLRIO                                                         
         BAS   RE,GETREC                                                        
         LA    R2,4(R2)                                                         
         ST    R2,DSKPOINT                                                      
*                                                                               
         TM    BDSTAT,X'80'        SKIP ANY RADIO NPW POL BUYS                  
         BNZ   NXTREC                                                           
*                                                                               
         MVI   BYTE3,0             SET REC NOT ALTERED                          
         SPACE 2                                                                
*                                  FIND BUYS FOR THIS EST-LIN                   
         L     R6,AFBY                                                          
PS10     C     R6,ALBY                                                          
         BNL   PS50                DONE WITH RECORD                             
         CLI   BYPRD,X'FF'                                                      
         BE    PS11                BYPASS UNALLOCATED                           
         CLC   BUYKEY+9(2),BYEST                                                
         BE    PS12                                                             
PS11     LA    R6,L'BYELEM(R6)                                                  
         B     PS10                                                             
PS12     LA    R0,1(R6)                                                         
         S     R0,AFBY                                                          
         STH   R0,HALF             HALF = RELATIVE ADDRESS OF BUY               
         MVC   ADDPAR4(2),BYSDT    DATE                                         
         MVI   ADDPAR3+1,0         SPOT REF. NO.                                
         MVI   AFLEN,0              CLEAR LENGTH                                
         SPACE 2                                                                
         SR    R0,R0                                                            
         SR    R1,R1                                                            
         IC    R0,BYUNACH                                                       
         IC    R1,BYNOWK                                                        
         SR    R1,R0                                                            
         STC   R1,BYTE2            BYTE2 = MATCHED SPOTS FOR THIS BUY           
         BNZ   PS13                GET INVOICES                                 
         SPACE 1                                                                
*                                  NO MATCHED SPOTS                             
         CLI   BUYKEY+3,X'FF'                                                   
         BE    PS18                                                             
         B     PS21B                                                            
         EJECT                                                                  
*                                  FIND INVOICES FOR THIS BUY                   
         SPACE 3                                                                
PS13     L     R7,AFINV                                                         
PS14     C     R7,ALINV                                                         
         BL    *+6                                                              
         DC    H'0'                END OF INVOICES SHOULD NOT OCCUR             
         CLC   HALF,IBY            IS MATCHING INVOICE                          
         BE    PS16                                                             
PS15     LA    R7,L'IELEM(R7)                                                   
         B     PS14                                                             
PS16     MVC   AFTIME,ITIM                                                      
         SR    R1,R1                                                            
         ICM   R1,3,AFTIME                                                      
         SR    R0,R0                                                            
         D     R0,=F'60'                                                        
         MH    R1,=H'100'                                                       
         AR    R0,R1                                                            
         CH    R0,=H'2400'                                                      
         BNH   *+8                                                              
         SH    R0,=H'2400'                                                      
         STCM  R0,3,AFTIME                                                      
*                                                                               
         MVC   AFDATE,IDAT          DATE                                        
         MVI   AFLEN,6                                                          
         CLI   BUYKEY+3,X'FF'                                                   
         BE    PS18                                                             
PS17     IC    R1,ADDPAR3+1        NON-POOL - BUMP REF. NO.                     
         LA    R1,1(R1)                                                         
         STC   R1,ADDPAR3+1                                                     
         B     PS20                                                             
*                                  FOR POOL BYSPT = REF. NO.                    
PS18     MVC   ADDPAR3+1(1),BYSPT                                               
PS20     GOTO1 VADDEL,ADDPARS                                                   
         SPACE 1                                                                
         CLI   ADDPARS,0           TEST REC ALTERED                             
         BE    *+8                 NO                                           
         MVI   BYTE3,X'FF'                                                      
         CLI   ADDPAR3+3,0                                                      
         BE    PS21A                                                            
         CLI   ADDPAR3+2,1         REC OVERFLOW                                 
         BE    *+6                                                              
         DC    H'0'                ADDEL ERROR                                  
         LA    R3,OVFERR                                                        
         B     ERROR                                                            
*                                                                               
PS21A    DS    0H                                                               
         CLI   TRAFPST,C'Y'      OPTION TO POST FILMS                           
         BNE   PS21A8                                                           
         CLI   BUYKEY+3,X'FF'      NO FILM POSTING FOR NON-POL                  
         BNE   PS21A8                                                           
*                                                                               
         LA    R0,FCMEL            POINT TO FILM ELEM                           
         ST    R0,ADDPAR2                                                       
         MVI   FCMLEN,0            NO FILM ELEM                                 
         XC    FCMFLM1(4),FCMFLM1  CLEAR FILM CODES                             
         CLI   AFLEN,0             IF NO AFFID                                  
         BE    PS21A4                                                           
         CLI   IFILM,0             OR NO FILM CODE                              
         BE    PS21A4                                                           
         LA    R3,IFILM                                                         
         LA    R4,FCMFLM1                                                       
         BAS   RE,PSFFILM                                                       
         OC    FCMFLM1,FCMFLM1     IF EITHER FILM UNDEFINED                     
         BZ    PS21A4              DONT POST                                    
         MVI   FCMLEN,5            ONE FILM                                     
         CLI   IFILM2,0                                                         
         BE    PS21A4                                                           
         LA    R3,IFILM2                                                        
         LA    R4,FCMFLM2                                                       
         BAS   RE,PSFFILM                                                       
         OC    FCMFLM2,FCMFLM2     IF EITHER FILM UNDEFINED                     
         BZ    PS21A4              DONT POST                                    
         MVI   FCMLEN,7            TWO FILMS                                    
*                                                                               
PS21A4   DS    0H                                                               
         GOTO1 VADDEL,ADDPARS                                                   
         SPACE 1                                                                
         CLI   ADDPARS,0           TEST REC ALTERED                             
         BE    *+8                 NO                                           
         MVI   BYTE3,X'FF'                                                      
         CLI   ADDPAR3+3,0                                                      
         BE    PS21A6                                                           
         CLI   ADDPAR3+2,1         REC OVERFLOW                                 
         BE    *+6                                                              
         DC    H'0'                ADDEL ERROR                                  
         LA    R3,OVFERR                                                        
         B     ERROR                                                            
*                                                                               
PS21A6   DS    0H                                                               
         LA    R0,AFEL             RESTORE TO AFFELEM                           
         ST    R0,ADDPAR2                                                       
*                                                                               
PS21A8   DS    0H                                                               
*                                                                               
PSRES    DS    0H                  RESPONSE COUNT CODE                          
         CLI   RESPSW,C'Y'         ONLY IF RESPONSE REQ                         
         BNE   PS21A9                                                           
         CLI   BUYKEY+3,X'FF'      ONLY FOR POOL                                
         BC    0,PS21A9            **TEMP NOOP**                                
*                                                                               
         LA    R0,RESPEL           POINT TO RESPONSE ELEM                       
         ST    R0,ADDPAR2                                                       
         MVI   RESPLEN,0           NO ELEM                                      
         XC    RESPDPT(4),RESPDPT  CLEAR ELEM                                   
         CLI   AFLEN,0             IF NO AFFID                                  
         BE    PSRES4                                                           
         OC    ICOST,ICOST         OR NO RESPONSE COUNT                         
         BZ    PSRES4                                                           
         MVC   RESPNUM,ICOST+1     SET RESP COUNT                               
         MVI   RESPLEN,6           ELEM LENGTH                                  
*                                                                               
PSRES4   DS    0H                                                               
         GOTO1 VADDEL,ADDPARS                                                   
         SPACE 1                                                                
         CLI   ADDPARS,0           TEST REC ALTERED                             
         BE    *+8                 NO                                           
         MVI   BYTE3,X'FF'                                                      
         CLI   ADDPAR3+3,0                                                      
         BE    PSRES6                                                           
         CLI   ADDPAR3+2,1         REC OVERFLOW                                 
         BE    *+6                                                              
         DC    H'0'                ADDEL ERROR                                  
         LA    R3,OVFERR                                                        
         B     ERROR                                                            
*                                                                               
PSRES6   DS    0H                                                               
         LA    R0,AFEL             RESTORE TO AFFELEM                           
         ST    R0,ADDPAR2                                                       
*                                                                               
*                                                                               
PS21A9   DS    0H                                                               
         CLI   BUYKEY+3,X'FF'                                                   
         BE    PS11                IF POOL, DONE - GO GET ANOTHER BUY           
         CLC   ADDPAR3+1(1),BYTE2  ARE ALL INVOICES FOUND                       
         BL    PS15                GET ANOTHER INVOICE                          
         SPACE 2                                                                
*                                  DELETE ANY REMAINING AFFELEMS                
         SPACE 2                                                                
PS21B    CLI   BYUNACH,0                                                        
         BE    PS11                NONE                                         
         IC    R1,ADDPAR3+1        SET REF. NO. 1 PAST                          
         LA    R1,1(R1)            LAST ELEM I ADDED                            
         STC   R1,ADDPAR3+1        AND LEAVE IT THERE                           
         SR    R0,R0                                                            
         IC    R0,BYUNACH                                                       
         MVI   AFLEN,0              SET LENGTH TO ZERO                          
         MVI   FCMLEN,0                                                         
         MVI   RESPLEN,0                                                        
PS22     GOTO1 VADDEL,ADDPARS                                                   
         SPACE 1                                                                
         CLI   ADDPARS,0           TEST REC ALTERED                             
         BE    *+8                 NO                                           
         MVI   BYTE3,X'FF'                                                      
         CLI   ADDPAR3+3,0                                                      
         BE    *+6                                                              
         DC    H'0'                ADDEL ERROR                                  
*                                                                               
         CLI   TRAFPST,C'Y'        OPTION TO POST FILMS                         
         BNE   PS23                                                             
         CLI   BUYKEY+3,X'FF'      NO FILM POSTING FOR NON-POL                  
         BNE   PS23                                                             
*                                                                               
         LA    R4,FCMEL            POINT TO FILM ELEM                           
         ST    R4,ADDPAR2                                                       
*                                                                               
         GOTO1 VADDEL,ADDPARS                                                   
         SPACE 1                                                                
         CLI   ADDPARS,0           TEST REC ALTERED                             
         BE    *+8                 NO                                           
         MVI   BYTE3,X'FF'                                                      
         CLI   ADDPAR3+3,0                                                      
         BE    *+6                                                              
         DC    H'0'                ADDEL ERROR                                  
*                                                                               
         LA    R4,AFEL             RESTORE TO AFFELEM                           
         ST    R4,ADDPAR2                                                       
*                                                                               
PS23     DS    0H                                                               
         CLI   RESPSW,C'Y'         RESPONSE COUNT REQ?                          
         BNE   PS24                                                             
         CLI   BUYKEY+3,X'FF'      ONLY IF POOL                                 
         BC    0,PS24              ***TEMP NOOP                                 
*                                                                               
         LA    R4,RESPEL           POINT TO RESP ELEM                           
         ST    R4,ADDPAR2                                                       
*                                                                               
         GOTO1 VADDEL,ADDPARS                                                   
         SPACE 1                                                                
         CLI   ADDPARS,0           TEST REC ALTERED                             
         BE    *+8                 NO                                           
         MVI   BYTE3,X'FF'                                                      
         CLI   ADDPAR3+3,0                                                      
         BE    *+6                                                              
         DC    H'0'                ADDEL ERROR                                  
*                                                                               
         LA    R4,AFEL             RESTORE TO AFFELEM                           
         ST    R4,ADDPAR2                                                       
PS24     BCT   R0,PS22                                                          
         B     PS11                GET ANOTHER BUY                              
PS50     EQU   *                                                                
         CLI   BYTE3,X'FF'         TEST REC ALTERED                             
         BNE   NXTREC                                                           
         BAS   RE,PUTREC                                                        
         B     NXTREC                                                           
         SPACE 2                                                                
PSEXT    XIT1                                                                   
         SPACE 2                                                                
PSFFILM  DS    0H                                                               
         XC    0(2,R4),0(R4)       CLEAR OUTPUT                                 
         ICM   R0,15,FLMTPARS+8                                                 
         BZR   RE                  NO FILMS                                     
         L     RF,AFLMTAB                                                       
         USING FLMTABD,RF                                                       
*                                                                               
PSFF4    DS    0H                                                               
         CLC   FLMRSET,IRSET       TST FROM RIGHT REC SET                       
         BNE   PSFF10                                                           
         CLC   FLMIDNO,IINVID      TST FROM RIGHT INVOICE                       
         BNE   PSFF10                                                           
         CLC   FLMICOD,0(R3)       INTERNAL FILM NO.                            
         BNE   PSFF10                                                           
*                                                                               
         MVC   0(2,R4),FLMSEQ      SET CMML SEQ                                 
         BR    RE                                                               
*                                                                               
PSFF10   DS    0H                                                               
         LA    RF,FLMTABEL(RF)                                                  
         BCT   R0,PSFF4                                                         
         BR    RE                                                               
         DROP  RF                                                               
         SPACE 3                                                                
         LTORG                                                                  
         EJECT                                                                  
*                                  FORMAT DEMOS                                 
         EJECT                                                                  
*                   CLEAR IOAREA                                                
         SPACE 3                                                                
CLRIO    LA    R0,8                                                             
         LA    R1,IOAREA                                                        
         XC    0(250,R1),0(R1)                                                  
         LA    R1,250(R1)                                                       
         BCT   R0,*-10                                                          
         BR    RE                                                               
