*          DATA SET TAUNITEST  AT LEVEL 001 AS OF 09/29/03                      
*              ROUTINE TO TEST 4-BYTE UNION AREA AGAINST EQUATES                
*                                  P1=A(4-BYTE BLOCK OF UNION BYTES             
*                                       TO TEST - TGUNEQUS, TGCAUNIS            
*                                       OR TGUSXUNS)                            
*                                     BYTE 0 X'80'=TEST THIS BLOCK              
*                                                  AGAINST A BLOCK OF           
*                                                  STORAGE INSTEAD OF           
*                                                  STRAIGHT EQUATES             
*                                  P2=UNION EQUATES TO TEST FIRST               
*                                     BYTE FOR                                  
*                                     OR, IF BYTE 0 OF P1 IS X'80',             
*                                     A(4-BYTE BLOCK OF UNION BYTES             
*                                       TO TEST AGAINST 4-BYTE BLOCK            
*                                       OF UNION BYTES SPECIFIED BY             
*                                       P1)                                     
*                                  P3=UNION EQUATES TO TEST SECOND              
*                                     BYTE FOR                                  
*                                     IF BYTE 0 OF P1 IS X'80' THIS             
*                                     PARAMETER SHOULD BE EMPTY                 
*                                  P4=UNION EQUATES TO TEST THIRD               
*                                     BYTE FOR                                  
*                                     IF BYTE 0 OF P1 IS X'80' THIS             
*                                     PARAMETER SHOULD BE EMPTY                 
*                                  P5=UNION EQUATES TO TEST FOURTH              
*                                     BYTE FOR                                  
*                                     IF BYTE 0 OF P1 IS X'80' THIS             
*                                     PARAMETER SHOULD BE EMPTY                 
*              RETURN CONDITION CODE                                            
         SPACE 1                                                                
         DS    0H                                                               
         TM    0(R1),X'80'         IF TESTING TWO 4-BYTE UNION                  
         BZ    UT10                BLOCKS AGAINST ONE ANOTHER                   
         L     RE,4(R1)                                                         
         XC    4(16,R1),4(R1)                                                   
         MVC   7(1,R1),0(RE)       SET P2 = FIRST EQUATE                        
         MVC   11(1,R1),1(RE)          P3 = SECOND EQUATE                       
         MVC   15(1,R1),2(RE)          P4 = THIRD EQUATE                        
         MVC   19(1,R1),3(RE)          P5 = FOURTH EQUATE                       
         SPACE                                                                  
UT10     L     R2,0(R1)            R2=A(4-BYTE BLOCK OF UNION BYTES)            
         LA    R3,4(R1)            R3=A(EQUATES TO TEST FOR)                    
         SPACE                                                                  
         MVI   0(R1),X'FF'         0(R1)=BYTE FOR EVENTUAL TEST                 
         SPACE                                                                  
         CLI   3(R3),0             BEGIN FIRST BYTE TEST                        
         BE    UT20                                                             
         SPACE 1                                                                
UT15     ZIC   RE,3(R3)                                                         
         EX    RE,*+8              TEST FIRST UNION BYTE AGAINST                
         B     *+8                 FIRST EQUATE                                 
         TM    0(R2),0                                                          
         BO    UT20                                                             
         BNZ   *+8                                                              
         NI    0(R1),X'FF'-X'80'   IF ANY BYTES ON                              
         NI    0(R1),X'FF'-X'40'   IF NO BYTES ON                               
         SPACE                                                                  
UT20     DS    0H                                                               
         LA    R2,1(R2)            BEGIN SECOND BYTE TEST                       
         LA    R3,4(R3)                                                         
         SPACE                                                                  
         TM    0(R1),X'C0'                                                      
         BZ    UT25                                                             
         CLI   3(R3),0                                                          
         BE    UT30                                                             
         SPACE                                                                  
UT25     ZIC   RE,3(R3)                                                         
         EX    RE,*+8              TEST SECOND UNION BYTE AGAINST               
         B     *+8                 SECOND EQUATE                                
         TM    0(R2),0                                                          
         BO    UT30                                                             
         BNZ   *+8                                                              
         NI    0(R1),X'FF'-X'20'   IF ANY BYTES ON                              
         NI    0(R1),X'FF'-X'10'   IF NO BYTES ON                               
         SPACE                                                                  
         TM    0(R1),X'10'                                                      
         BO    UT30                                                             
         LR    RE,R2                                                            
         LR    RF,R3                                                            
         AHI   RE,-4                                                            
         AHI   RF,-4                                                            
         CLI   0(RE),0                                                          
         BNE   UT30                                                             
         CLI   3(RF),0                                                          
         BNE   UT30                                                             
         NI    0(R1),X'FF'-X'C0'                                                
         SPACE                                                                  
UT30     DS    0H                                                               
         LA    R2,1(R2)            BEGIN THIRD BYTE TEST                        
         LA    R3,4(R3)                                                         
         SPACE                                                                  
         TM    0(R1),X'30'                                                      
         BZ    UT35                                                             
         CLI   3(R3),0                                                          
         BE    UT40                                                             
         SPACE                                                                  
UT35     ZIC   RE,3(R3)                                                         
         EX    RE,*+8              TEST THIRD UNION BYTE AGAINST                
         B     *+8                 THIRD EQUATE                                 
         TM    0(R2),0                                                          
         BO    UT40                                                             
         BNZ   *+8                                                              
         NI    0(R1),X'FF'-X'08'   IF ANY BYTES ON                              
         NI    0(R1),X'FF'-X'04'   IF NO BYTES ON                               
         SPACE                                                                  
         TM    0(R1),X'04'                                                      
         BO    UT40                                                             
         LR    RE,R2                                                            
         LR    RF,R3                                                            
         AHI   RE,-4                                                            
         AHI   RF,-4                                                            
         CLI   0(RE),0                                                          
         BNE   UT36                                                             
         CLI   3(RF),0                                                          
         BNE   UT36                                                             
         NI    0(R1),X'FF'-X'30'                                                
UT36     AHI   RE,-4                                                            
         AHI   RF,-4                                                            
         CLI   0(RE),0                                                          
         BNE   UT40                                                             
         CLI   3(RF),0                                                          
         BNE   UT40                                                             
         NI    0(R1),X'FF'-X'C0'                                                
         SPACE                                                                  
UT40     DS    0H                                                               
         LA    R2,1(R2)            BEGIN FOURTH BYTE TEST                       
         LA    R3,4(R3)                                                         
         SPACE                                                                  
         TM    0(R1),X'0C'                                                      
         BZ    UT45                                                             
         CLI   3(R3),0                                                          
         BE    UT50                                                             
         SPACE                                                                  
UT45     ZIC   RE,3(R3)                                                         
         EX    RE,*+8              TEST THIRD UNION BYTE AGAINST                
         B     *+8                 THIRD EQUATE                                 
         TM    0(R2),0                                                          
         BO    UT50                                                             
         BNZ   *+8                                                              
         NI    0(R1),X'FF'-X'02'   IF ANY BYTES ON                              
         NI    0(R1),X'FF'-X'01'   IF NO BYTES ON                               
         SPACE                                                                  
         TM    0(R1),X'01'                                                      
         BO    UT50                                                             
         LR    RE,R2                                                            
         LR    RF,R3                                                            
         AHI   RE,-4                                                            
         AHI   RF,-4                                                            
         CLI   0(RE),0                                                          
         BNE   UT46                                                             
         CLI   3(RF),0                                                          
         BNE   UT46                                                             
         NI    0(R1),X'FF'-X'0C'                                                
UT46     AHI   RE,-4                                                            
         AHI   RF,-4                                                            
         CLI   0(RE),0                                                          
         BNE   UT47                                                             
         CLI   3(RF),0                                                          
         BNE   UT47                                                             
         NI    0(R1),X'FF'-X'30'                                                
UT47     AHI   RE,-4                                                            
         AHI   RF,-4                                                            
         CLI   0(RE),0                                                          
         BNE   UT50                                                             
         CLI   3(RF),0                                                          
         BNE   UT50                                                             
         NI    0(R1),X'FF'-X'C0'                                                
         SPACE                                                                  
UT50     TM    0(R1),X'FF'         SET CONDITION CODE                           
