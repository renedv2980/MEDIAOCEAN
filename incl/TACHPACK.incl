*          DATA SET TACHPACK   AT LEVEL 013 AS OF 08/13/10                      
*=====================================================================          
* PACKS 12 PRINTABLE CHARS, EXCLUDING CHARS  []{}~                              
* INTO 10 BYTE BINARY (USED FOR TALENT ESTIMATE'S HYPO COMMERCIAL)              
*                                                                               
* R1 ==> C'P',AL3(12 CHAR CODE)                                                 
*        A(10 BYTE OUTPUT PACKED)                                               
*                                                                               
*        C'U',AL3(10 BYTE OUTPUT PACKED)                                        
*        A(12 CHAR OUTPUT CODE)                                                 
*                                                                               
* PROGRAM RETURNS CC NEQ IF CODE INVALID                                        
*=====================================================================          
                                                                                
CHPACK   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         SR    R2,R2                                                            
         ICM   R2,7,1(R1)          R2=A(INPUT)                                  
         SR    R3,R3                                                            
         ICM   R3,7,5(R1)          R3=A(OUTPUT)                                 
*                                                                               
         CLI   0(R1),C'P'                                                       
         BE    CHPCK                                                            
         CLI   0(R1),C'U'                                                       
         BE    CHUPCK                                                           
         DC    H'0'                                                             
*                                                                               
CHPACKY  CR    RB,RB               CC=EQ                                        
         B     *+6                                                              
CHPACKN  LTR   RB,RB               CC=NEQ                                       
CHPACKX  XIT1                                                                   
         EJECT                                                                  
*=====================================================================          
* CONVERT 12 BYTE CHARACTER TO 10 BYTE PACKED FORMAT                            
*=====================================================================          
CHPCK    DS    0H                                                               
         MVC   C12CODE,0(R2)                                                    
         XC    0(L'B10CODE,R3),0(R3)                                            
         TR    C12CODE,PACKTAB     TRANSLATE CHARACTERS                         
*                                                                               
         LA    R1,C12CODE          TEST ALL CHARACTERS ARE VALID                
         LHI   R0,L'C12CODE                                                     
         CLI   0(R1),CHPKIVAL      IF CHARACTER INVALID                         
         BE    CHPACKN             CC=NEQ                                       
         AHI   R1,1                                                             
         BCT   R0,*-12                                                          
*                                                                               
         LA    R5,C12CODE          POINT TO TRANLATED CHARS                     
         LA    R2,B10CODE          POINT TO PACKED CHARS                        
         LA    R0,3                                                             
CHPCK10  XR    R1,R1                                                            
         XR    RE,RE                                                            
         IC    RE,0(R5)            GET TRANSLATED 1ST CHARACTER                 
         OR    R1,RE                                                            
         SLA   R1,6                                                             
         IC    RE,1(R5)            GET TRANSLATED 2ND CHARACTER                 
         OR    R1,RE                                                            
         SLA   R1,6                                                             
         IC    RE,2(R5)            GET TRANSLATED 3RD CHARACTER                 
         OR    R1,RE                                                            
         SLA   R1,6                                                             
         IC    RE,3(R5)            GET TRANSLATED 4TH CHARACTER                 
         OR    R1,RE                                                            
*                                                                               
         STCM  R1,7,0(R2)          SAVE IT                                      
         AHI   R2,3                                                             
         AHI   R5,4                                                             
         BCT   R0,CHPCK10          LOOP FOR NEXT 4 CHARS                        
*                                                                               
         MVI   B10CODE+9,0         CLEAR OUT LAST CHARACTER                     
         MVC   0(L'B10CODE,R3),B10CODE                                          
         B     CHPACKY                                                          
         EJECT                                                                  
*=====================================================================          
* CONVERT 10 BYTE PACKED TO 12 CHARACTER                                        
*=====================================================================          
CHUPCK   DS    0H                                                               
         MVC   B10CODE,0(R2)                                                    
         XR    R1,R1                                                            
*                                                                               
         LA    R5,C12CODE          POINT TO TRANLATED CHARS                     
         LA    R2,B10CODE          POINT TO PACKED CHARS                        
         LA    R0,3                                                             
         XR    R1,R1                                                            
         LA    RE,UNPKTAB                                                       
*                                                                               
CHUPCK10 ICM   R1,7,0(R2)          R1 HOLDS CONVERTED 4 CHARS                   
         LR    RF,R1                                                            
         N     RF,=X'00FC0000'     MASK OUT                                     
         SRA   RF,18                                                            
         AR    RF,RE                                                            
         MVC   0(1,R5),0(RF)       MOVE IN UNPACKED CHAR                        
*                                                                               
         LR    RF,R1                                                            
         N     RF,=X'0003F000'     MASK OUT                                     
         SRA   RF,12                                                            
         AR    RF,RE                                                            
         MVC   1(1,R5),0(RF)       MOVE IN UNPACKED CHAR                        
*                                                                               
         LR    RF,R1                                                            
         N     RF,=X'00000FC0'     MASK OUT                                     
         SRA   RF,6                                                             
         AR    RF,RE                                                            
         MVC   2(1,R5),0(RF)       MOVE IN UNPACKED CHAR                        
*                                                                               
         LR    RF,R1                                                            
         N     RF,=X'0000003F'     MASK OUT                                     
         AR    RF,RE                                                            
         MVC   3(1,R5),0(RF)       MOVE IN UNPACKED CHAR                        
*                                                                               
         AHI   R2,3                                                             
         AHI   R5,4                                                             
         BCT   R0,CHUPCK10         LOOP FOR NEXT 4 CHARS                        
*                                                                               
         MVC   0(L'C12CODE,R3),C12CODE                                          
         B     CHPACKY                                                          
         EJECT                                                                  
*=====================================================================          
         LTORG                                                                  
C12CODE  DS    CL12                                                             
B10CODE  DS    XL10                                                             
CHPKIVAL EQU   X'FF'                                                            
*                                                                               
CHRLIST  DS    0C                  EXCLUDING CHARS  [\]{}~                      
PACKTAB  DC    256AL1(CHPKIVAL)                                                 
         ORG   PACKTAB+C' '                                                     
         DC    AL1(00)                                                          
         ORG   PACKTAB+C'A'                                                     
         DC    AL1(01,02,03,04,05,06,07,08,09)                                  
         ORG   PACKTAB+C'J'                                                     
         DC    AL1(10,11,12,13,14,15,16,17,18)                                  
         ORG   PACKTAB+C'S'                                                     
         DC    AL1(19,20,21,22,23,24,25,26)                                     
         ORG   PACKTAB+C'0'                                                     
         DC    AL1(27,28,29,30,31,32,33,34,35,36)                               
         ORG   PACKTAB+C'!'                                                     
         DC    AL1(37)                                                          
         ORG   PACKTAB+C'"'                                                     
         DC    AL1(38)                                                          
         ORG   PACKTAB+C'#'                                                     
         DC    AL1(39)                                                          
         ORG   PACKTAB+C'$'                                                     
         DC    AL1(40)                                                          
         ORG   PACKTAB+C'%'                                                     
         DC    AL1(41)                                                          
         ORG   PACKTAB+C'&&'                                                    
         DC    AL1(42)                                                          
         ORG   PACKTAB+X'7D'           '                                        
         DC    AL1(43)                                                          
         ORG   PACKTAB+C'('                                                     
         DC    AL1(44)                                                          
         ORG   PACKTAB+C')'                                                     
         DC    AL1(45)                                                          
         ORG   PACKTAB+C'*'                                                     
         DC    AL1(46)                                                          
         ORG   PACKTAB+C'+'                                                     
         DC    AL1(47)                                                          
         ORG   PACKTAB+C','                                                     
         DC    AL1(48)                                                          
         ORG   PACKTAB+C'-'                                                     
         DC    AL1(49)                                                          
         ORG   PACKTAB+C'.'                                                     
         DC    AL1(50)                                                          
         ORG   PACKTAB+C'/'                                                     
         DC    AL1(51)                                                          
         ORG   PACKTAB+C':'                                                     
         DC    AL1(52)                                                          
         ORG   PACKTAB+X'5E'           SEMICOLON                                
         DC    AL1(53)                                                          
         ORG   PACKTAB+C'<'                                                     
         DC    AL1(54)                                                          
         ORG   PACKTAB+C'='                                                     
         DC    AL1(55)                                                          
         ORG   PACKTAB+C'>'                                                     
         DC    AL1(56)                                                          
         ORG   PACKTAB+C'?'                                                     
         DC    AL1(57)                                                          
         ORG   PACKTAB+C'@'                                                     
         DC    AL1(58)                                                          
         ORG   PACKTAB+C'^'                                                     
         DC    AL1(59)                                                          
         ORG   PACKTAB+C'_'                                                     
         DC    AL1(60)                                                          
         ORG   PACKTAB+C'`'                                                     
         DC    AL1(61)                                                          
         ORG   PACKTAB+C'|'                                                     
         DC    AL1(62)                                                          
         ORG   PACKTAB+C'\'                                                     
         DC    AL1(63)                                                          
         ORG                                                                    
UNPKTAB  DS    0C                                                               
         DC    C' ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789'                         
         DC    C'!"#$%&&'                                                       
         DC    X'7D'               '                                            
         DC    C'()*+,-./:'                                                     
         DC    X'5E'               SEMICOLON                                    
         DC    C'<=>?@^_`|\'                                                    
         DS    0C                                                               
         EJECT                                                                  
