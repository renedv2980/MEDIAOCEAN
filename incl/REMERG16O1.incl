*          DATA SET REMERG16O1 AT LEVEL 002 AS OF 05/01/02                      
**CRDT16                                                                        
*                                                                               
*   CRDIT16S:  CYCLE THROUGH RECORD'S X'16' ELEMENTS, AND RECON-                
*        STRUCT THE ORIGINAL BUYLINE WITH THE CREDIT SPOTS                      
*        REINSERTED.  THIS IS THE VERSION WHICH WILL BE PASSED TO               
*        THE BIAS TRAFFIC SYSTEM.                                               
*                                                                               
CRDIT16S NMOD1 0,*CRDT*                                                         
         L     RC,0(R1)            RESET A(WORKSPACE)                           
CRDT0010 EQU   *                                                                
*                                                                               
*   BEGIN AT START OF BUY RECORD EACH TIME TO FIND X'16' ELEMENT.               
*        SAVING THE ELEMENT ADDRESS IS NG, BECAUSE THE PRECEDING                
*        ELEMENTS ARE COMPRESSED OUT, CHANGING THE RECORD'S                     
*        ELEMENT ADDRESSING.                                                    
*                                                                               
         LA    R6,RBUYELEM         A(1ST ELEMENT OF TARGET BUY)                 
CRDT0020 EQU   *                                                                
         CLI   0(R6),0             END OF RECORD?                               
         BE    CRDT0200            YES - FINISHED                               
         CLI   0(R6),X'16'         CREDIT SPLITOUT BUY MISSED ELT?              
         BE    CRDT0060            YES - PROCESS                                
CRDT0040 EQU   *                                                                
         ZIC   RF,1(R6)            NO  - BUMP TO NEXT ELEMENT                   
         AR    R6,RF                                                            
         B     CRDT0020            GO BACK FOR NEXT                             
CRDT0060 EQU   *                                                                
         MVI   0(R6),X'17'         TURN OFF SPLITOUT ELT SO NEXT LOOP           
*                                     DOESN'T REPROCESS IT                      
         USING RBUYCAEL,R6         CREDIT SPLITOUT MISSED SPOT ELT              
         LA    R2,RBUYELEM         A(1ST ELEMENT OF TARGET BUY)                 
CRDT0080 EQU   *                                                                
         ZIC   RF,1(R2)            BUMP TO NEXT ELEMENT                         
         AR    R2,RF                                                            
         CLI   0(R2),0             END OF RECORD?                               
         BE    CRDT0010            YES - FINISHED                               
         CLI   0(R2),3             EFFECTIVE DATE ELEMENT?                      
         BNE   CRDT0080            NO  - GO BACK FOR NEXT                       
*                                                                               
         USING RBUYDTEL,R2         BUY EFFECTIVE DATE ELEMENT                   
*                                                                               
         OC    RBUYCAED,RBUYCAED   ANY END DATE IN CREDIT?                      
         BZ    CRDT0120            NO  - USE START DATE FOR COMPARE             
         CLC   RBUYCAED,RBUYDTST   YES - MISSED RANGE VS EFF START DATE         
         BL    CRDT0080            LOW - GO BACK FOR NEXT 03 ELEMENT            
         CLC   RBUYCASD,RBUYDTED   YES - MISSED RANGE VS EFF START DATE         
         BL    CRDT0080            LOW - GO BACK FOR NEXT 03 ELEMENT            
CRDT0120 EQU   *                                                                
         CLC   RBUYCASD,RBUYDTST   MISSED DATE VS EFF START DATE                
         BL    CRDT0080            LOW - GO BACK FOR NEXT 03 ELEMENT            
         CLC   RBUYCASD,RBUYDTED   GE: MISSED DATE VS EFF END   DATE            
         BH    CRDT0080            HIGH - GO BACK FOR NEXT                      
*                                  MISSED DATE WITHIN ST/END DATES              
CRDT0140 EQU   *                                                                
         ZIC   RF,RBUYCASP         # SPOTS MISSED                               
         ZIC   RE,RBUYDTNW         # SPOTS IN X'03' ELT                         
         AR    RF,RE               ADD MISSED SPOTS BACK IN                     
         STC   RF,RBUYDTNW         REPLACE IN X'03' ELT                         
         CLC   RBUYDTNW,RBUYNW                                                  
*                                  03 ELT SPOTS = BUYLINE SPOTS?                
         BNE   CRDT0010            NO  - TRY NEXT X'16' ELT                     
         NI    RBUYDTIN,X'FF'-X'01'                                             
*                                  YES - TURN OFF NPW OVERRIDE FLAG             
         MVC   SAVCOMDT,RBUYDTST   SAVE START DATE OF ELT                       
*                                     INTO WHICH SPOT REINSERTED                
         BAS   RE,COMPRES2         COMPRESS X'03' ELTS, IF POSSIBLE             
         B     CRDT0010            TRY NEXT X'16' ELT                           
CRDT0200 EQU   *                                                                
         XIT1                                                                   
         DROP  R6                                                               
         EJECT                                                                  
*                                                                               
*   COMPRES2:  SPOTS MISSED HAVE BEEN REINSERTED INTO THE TARGET                
*        BUY.  THIS ELEMENT WILL BE COMPARED WITH THE ELEMENT BEFORE            
*        IT AND THE ELEMENT AFTER IT TO SEE IF THEY MAY BE COMBINED             
*        INTO A SINGLE ELEMENT REPRESENTING THE ENTIRE PERIOD.                  
*        R2  -->  X'03' ELEMENT INTO WHICH SPOT HAS BEEN REINSERTED.            
*                                                                               
*                                                                               
COMPRES2 NTR1                                                                   
         ZIC   RF,1(R2)            LENGTH OF X'03' ELT                          
*                                     (IN CASE IT CHANGES)                      
         LR    R3,R2               A(MAKEGOOD 03 ELT)                           
         SR    R3,RF               BACK UP TO PREVIOUS ELEMENT                  
         LA    R6,RBUYELEM         VERIFY IT'S REALLY AN ELEMENT                
CMRS0020 EQU   *                                                                
         CR    R6,R3               ADDR OF PREVIOUS ELEMENT?                    
         BE    CMRS0040            YES - TEST FOR X'03' ELEMENT                 
         BH    CMRS0100            PAST ADDRESS: NO PREV ELEMENT                
         ZIC   RF,1(R6)            BUMP TO NEXT ELEMENT                         
         AR    R6,RF                                                            
         B     CMRS0020            GO BACK FOR NEXT ELEMENT                     
CMRS0040 EQU   *                                                                
         CLI   0(R6),X'03'         X'03' ELEMENT?                               
         BNE   CMRS0100            NO  - CHECK AFTER MAKEGOOD 03 ELT            
         GOTO1 DATETST2,DMCB,0     YES - CHECK ITS DATE AND                     
*                                     MRG2E IF ALL CONDITIONS MET               
CMRS0100 EQU   *                   CHECK FOLLOWING ELEMENT                      
         LR    R6,R2                                                            
         ZIC   RF,1(R6)            BUMP TO NEXT ELEMENT                         
         AR    R6,RF                                                            
         CLI   0(R6),X'03'         X'03' ELEMENT?                               
         BNE   CMRS0200            NO  - CLEAN UP THE RECORD                    
*                                     AND EXIT                                  
         GOTO1 DATETST2,DMCB,1     YES - CHECK ITS DATE AND                     
*                                     MRG2E IF ALL CONDITIONS MET               
CMRS0200 EQU   *                                                                
         GOTO1 VDELELEM,DMCB,(X'FF',RBUYREC)                                    
*                                  DELETE MRG2ED X'03' ELEMENT(S)               
         XIT1                                                                   
         EJECT                                                                  
*                                                                               
*   DATETST2:  COMPARE FOR CONTINUOUS DATES (SINGLE OR ALTERNATING              
*        WEEKS), SAME NUMBER OF SPOTS.  0(R1) = 0 INDICATES PREVIOUS            
*        X'03' BEING CONSIDERED, 0(R1) = 1 INDICATES FOLLOWING.                 
*        R2 ---> MAKEGOOD X'03' ELEMENT                                         
*        R6 ---> ELEMENT WHICH MAY BE MRG2ED                                    
*                                                                               
DATETST2 NTR1                                                                   
         L     R3,0(R1)            PREVIOUS/FOLLOWING INDICATOR                 
         LA    R4,RBUYDTST-RBUYDTEL(R6)                                         
*                                  A(START DATE IN ELT TO BE MRG2ED)            
         GOTO1 DATCON,DMCB,(3,(R4)),(0,SDATSPLT)                                
*                                  CONVERT IT TO EBCDIC                         
         GOTO1 DATCON,DMCB,(3,SAVCOMDT),(0,EDATSPLT)                            
*                                  CONVERT MG 03 STARTDATE TO EBCDIC            
         LA    R5,7                SET WEEK INTERVAL TO 7 DAYS                  
         LTR   R3,R3               PREVIOUS WEEK/FOLLOWING WEEK?                
         BNZ   DTST0010            NOT ZERO = FOLLOWING WEEK, WHICH             
*                                     WILL ALWAYS BE 1 OR 2 WEEKS               
*                                     AFTER MG X'03' ELEMENT                    
*                                                                               
*                                  PRIOR ELEMENT START DATE MUST BE             
*                                     CALC'ED USING NUMBER OF WEEKS             
         ZIC   RF,RBUYDTWK-RBUYDTEL(R6)                                         
         STH   RF,HALF                                                          
         MH    R5,HALF             CALCULATE # DAYS DIFFERENCE                  
*                                                                               
DTST0010 EQU   *                                                                
         TM    RBUYDTIN,X'40'      ALTERNATING WEEK?                            
         BNO   DTST0020            NO                                           
         SLL   R5,1                YES - DOUBLE THE NUMBER OF DAYS              
DTST0020 EQU   *                                                                
         LTR   R3,R3               PREVIOUS WEEK/FOLLOWING WEEK?                
         BNZ   DTST0040            NOT ZERO = FOLLOWING WEEK                    
         LNR   R5,R5               PREVIOUS WEEK - BACK IT UP                   
DTST0040 EQU   *                                                                
         GOTO1 ADDAY,DMCB,EDATSPLT,EDATSPLT+8,(R5)                              
*                                  CALCULATE PREV/FOLLOWING DATE FROM           
*                                     MAKEGOOD X'03' ELT DATE                   
         CLC   EDATSPLT+8(6),SDATSPLT                                           
*                                  SAME DATES?                                  
         BNE   DTST0200            NO  - DON'T COMPRESS                         
         CLC   RBUYDTNW,RBUYDTNW-RBUYDTEL(R6)                                   
*                                  YES - SAME NUMBER OF SPOTS?                  
         BNE   DTST0200            NO  - DON'T COMPRESS                         
*                                  YES - COMPRESS THE ELEMENTS                  
         LTR   R3,R3               PREVIOUS WEEK/FOLLOWING WEEK?                
         BNZ   DTST0060            NOT ZERO = FOLLOWING WEEK                    
*                                  PREVIOUS WEEK: RESET START WEEK              
         MVC   RBUYDTST,RBUYDTST-RBUYDTEL(R6)                                   
*                                  MOVE START WEEK FROM PREVIOUS                
         B     DTST0080                                                         
DTST0060 EQU   *                                                                
         MVC   RBUYDTED,RBUYDTED-RBUYDTEL(R6)                                   
DTST0080 EQU   *                                                                
         ZIC   RF,RBUYDTWK         NUMBER OF WEEKS                              
         ZIC   RE,RBUYDTWK-RBUYDTEL(R6)                                         
         AR    RF,RE               INCREASE NUMBER OF WEEKS BACK                
         STC   RF,RBUYDTWK         PUT IT BACK                                  
         MVI   0(R6),X'FF'         SET ELEMENT FOR DELETION                     
DTST0200 EQU   *                                                                
         XIT1                                                                   
         DROP  R2                                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
