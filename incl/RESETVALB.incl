*          DATA SET RESETVALB  AT LEVEL 003 AS OF 04/20/98                      
*                                                                               
*  REP ROUTINE TO SCAN THE NEWMON TABLE, AND RESET 'INVOICE $ PRESENT'          
*        BITS FOR CLOSED MONTHS.  THIS ROUTINE IS CALLED OPTIONALLY             
*        BASED ON REP RECORD PROFILE FLAG 11.                                   
*                                                                               
*                                                                               
*  SETVALU2:  SETS INVOICE DATA BIT ON WHERE APPROPRIATE FOR MONTHS             
*        NOT AFTER CLOSED DATE                                                  
*        PRIOR YEAR/2 YRS PRIOR WILL BE PACED 1/2 YEARS BEFORE THE              
*        CURRENT CLOSE DATE.                                                    
**********************************************************************          
*  HISTORY OF CHANGES                                                *          
**********************************************************************          
*  JUN02/97 (BU ) --- INCORPORATE AS-AT DATE CHECK TO ENSURE NO      *          
*                     FUTURE DATES APPLIED                           *          
*                                                                    *          
*  AUG19/97 (BU ) --- ADDITIONAL CHECKS FOR INVOICE DATA AFTER       *          
*                     ASAT DATE FOR MONTH                            *          
*                                                                    *          
*  NOV17/97 (BU ) --- P4 FLAG:  NEXT YEAR OFFSET APPLIED: ADJUST     *          
*                     CLOSE DATE ARRAY DATES                         *          
*                                                                    *          
*  JAN07/98 (BU ) --- CORRECT PRIOR PERIOD ACTUAL CALCULATION        *          
*                                                                    *          
*                                                                    *          
**********************************************************************          
*                                                                               
*                                                                               
*                                                                               
SETVALU2 NMOD1 0,*SVALU2*                                                       
         L     RC,0(R1)            RESET A(WORKSPACE)                           
         L     R2,4(R1)            RESET A(NEWMON TABLE)                        
         L     R7,8(R1)            RESET A(QWMONFOR: AS-AT DATES)               
         L     R6,12(R1)           RESET A(QWCURST3: PERIOD DATES)              
*                                                                               
         GOTO1 DATCON,DMCB,(5,WORK),(3,TODAYSDT)                                
*                                  SET SYSTEM DATE                              
         L     RE,ADCONLST                                                      
         USING ADCONSD,RE                                                       
         L     RF,VSPNDCTL                                                      
         LA    RF,8(RF)            SKIP FLAGS                                   
*                                                                               
         DROP  RE                                                               
*                                                                               
         LA    R4,2                STATION RECORD TYPE                          
         SLA   R4,2                SET DISPLACEMENT                             
         AR    R4,RF               ADD A(SPACEND CNTRL)                         
*                                     = A(1ST STATION ENTRY IN TABLE)           
         L     R4,0(R4)            LOAD ADDRESS                                 
SEVA0020 EQU   *                                                                
         CLI   0(R4),X'02'         STATION RECORD?                              
         BNE   SEVA0280            NO  - STATION NOT FOUND                      
         CLC   RCONKREP,2(R4)      SAME REP?                                    
         BNE   SEVA0040                                                         
         CLC   RCONKSTA,4(R4)      SAME STATION?                                
         BE    SEVA0060                                                         
SEVA0040 EQU   *                                                                
         ZIC   R5,1(R4)            BUMP TO NEXT ENTRY                           
         AR    R4,R5                                                            
         B     SEVA0020            GO BACK FOR NEXT                             
SEVA0060 EQU   *                                                                
         GOTO1 DATCON,DMCB,(2,(R7)),(3,SCLASATD)                                
         MVC   SCLASATY,SCLASATD   SAVE YEAR OF CURRENT ASAT DATE               
         CLI   SCLASATD+1,3        AS AT DATE'S MONTH MARCH OR LATER?           
         BNL   SEVA0080            YES                                          
         ZIC   RF,SCLASATD+1       NO  - ADJUST MONTH, AND YEAR                 
         LA    RF,12(RF)           ADD 12 TO MONTH                              
         SH    RF,=H'2'            BACK UP TWO MONTHS                           
         STC   RF,SCLASATD+1       PUT MONTH BACK                               
         ZIC   RF,SCLASATD         BACK YEAR UP 1                               
         BCTR  RF,0                                                             
         STC   RF,SCLASATD         PUT YEAR BACK                                
         B     SEVA0100                                                         
SEVA0080 EQU   *                                                                
         ZIC   RF,SCLASATD+1       ADJUST MONTH BACK 2 MONTHS                   
         SH    RF,=H'2'            BACK UP TWO MONTHS                           
         STC   RF,SCLASATD+1       PUT MONTH BACK                               
SEVA0100 EQU   *                                                                
         XC    SCLOSDTE(6),SCLOSDTE SET ALL DATES TO ZERO                       
         MVC   SCLOSDTE,68(R4)     SET STATION CLOSE DATE                       
         CLC   SCLASATD(2),SCLOSDTE                                             
*                                  'ADJUSTED' CLOSE VS ACTL STN CLOSE           
         BNL   SEVA0120            USE STATION'S ACTUAL CLOSE                   
         MVC   SCLOSDTE(2),SCLASATD                                             
*                                  CALC'D EARLIER:  USE IT                      
SEVA0120 EQU   *                                                                
*                                                                               
*   TEST                                                                        
         MVC   P+1(08),=C'CLOSES: '                                             
         MVC   P+10(2),0(R7)       INSERT AS-AT DATE                            
         MVC   P+14(3),SCLASATD                                                 
         MVC   P+20(2),68(R4)                                                   
         MVC   P+26(2),SCLOSDTE                                                 
         GOTO1 REPORT                                                           
*   TEST END                                                                    
*                                                                               
         MVC   SCLOSPRI,SCLOSDTE   CALCULATE PRIOR YEARS' DATES                 
         MVC   SCLOS2PR,SCLOSDTE                                                
         OC    SCLOSDTE,SCLOSDTE   ANY DATE CLOSED?                             
         BZ    SEVA0140            NO  - LEAVE ALL AS ZERO                      
         ZIC   RF,SCLOSDTE         GET CURRENT CLOSE DATE                       
         CLC   SCLASATY,TODAYSDT   CURRENT AS-AT DATE IN NEXT YEAR?             
         BH    SEVA0130            YES - DON'T BACK UP FOR PRIOR YEAR           
         CLC   TODAYSDT(1),0(R6)   NO  - CURRENT DATA IN NEXT YEAR?             
         BL    SEVA0130            YES - DON'T BACK UP FOR PRIOR YEAR           
         BCTR  RF,0                NO  - BACK UP 1 YEAR                         
SEVA0130 EQU   *                                                                
         STC   RF,SCLOSPRI         SAVE PRIOR YEAR                              
         BCTR  RF,0                BACK UP 2YR PRIOR 1 YEAR                     
         STC   RF,SCLOS2PR         SET 2YR PRIOR 1 MORE YEAR BACK               
SEVA0140 EQU   *                                                                
*                                                                               
*   TEST                                                                        
         MVC   P+1(06),=C'DATES:'                                               
         MVC   P+10(2),SCLOSDTE                                                 
         MVC   P+20(2),SCLOSPRI                                                 
         MVC   P+30(2),SCLOS2PR                                                 
         MVC   P+40(3),TODAYSDT                                                 
         MVC   P+50(3),0(R6)                                                    
         GOTO1 REPORT                                                           
*   TEST END                                                                    
*                                                                               
*                                                                               
*   TABLE RESETTING.  EACH SLOT IN THE TABLE WILL BE INSPECTED TO               
*        DETERMINE IF AUTOMATIC INVENTORY SETTING IS TO BE DONE.                
*        IN EACH OF THE INSTANCES WHERE INVOICE DATA SUPERCEDES                 
*        ESTIMATE DATA, THE BUCKETS WILL BE CHECKED IN THIS MANNER:             
*        1.  IF THE DATE (CURRENT/PRIOR/2YR PRIOR) IS GREATER                   
*            THAN THE STATION'S CLOSE DATE, NO ACTION WILL BE TAKEN.            
*        2.  IF DATE IS LESS THAN/EQUAL TO CLOSE DATE, THE ESTIMATE             
*            DOLLAR BUCKET WILL BE CHECKED.  IF ZERO, NO ACTION WILL            
*            BE TAKEN.  THIS WILL ENSURE THAT BUCKETS OUTSIDE THE               
*            FLIGHT DATES OF THE ORDER ARE NOT MISTAKENLY SET, AND              
*            WILL ALWAYS PROVIDE THE BEST DOLLAR FIGURE IN ALL CASES            
*            WITH NO RESETTING.                                                 
*        3.  IF ESTIMATE DOLLAR BUCKET IS NON-ZERO, THE APPROPRIATE             
*            INVOICE BIT IN THE FLAG BYTE WILL BE SET.  IF ALREADY              
*            SET BECAUSE INVOICE DOLLARS ARE PRESENT, THIS WILL HAVE            
*            NO EFFECT.  IF NOT ALREADY SET, THIS WILL HAVE THE EFFECT          
*            OF INDICATING $0 INVOICE TO ANYTHING EXAMINING THE BUCKET          
*            AT POST TIME.                                                      
*                                                                               
         L     R4,ANEWMON          SET A(NEW MONTH TABLE)                       
SEVA0160 EQU   *                                                                
*                                                                               
*   TEST PRE:                                                                   
         MVC   P+1(4),=C'PRE:'                                                  
         GOTO1 HEXOUT,DMCB,RCONKCON,P+6,4,=C'TOG'                               
         GOTO1 HEXOUT,DMCB,SCLOSDTE,P+20,2,=C'TOG'                              
         MVC   P+30(60),0(R4)                                                   
         GOTO1 REPORT                                                           
*   TEST PRE: END                                                               
*                                                                               
         OC    0(6,R4),0(R4)       ANY ENTRY IN TABLE SLOT?                     
         BZ    SEVA0280            NO  - FINISHED                               
         LR    R3,R4               SET A(BUCKETS WITHIN SLOT)                   
         LA    R3,BUCKDISP(R3)     BUMP TO BUCKETS                              
         CLC   CURDATE(2,R4),SCLOSDTE                                           
*                                  CURRENT MONTH CLOSED?                        
         BH    SEVA0180            NO  - NO FURTHER CHECKS                      
         OC    TOTORD(4,R3),TOTORD(R3)                                          
*                                  YES - TOTAL ORDERED ZERO?                    
         BZ    SEVA0180            YES - LEAVE AS IS                            
         TM    FLAG6(R4),X'20'     NO  - INV$ AFTER ASAT DATE?                  
         BO    SEVA0180            YES - DON'T SET 'INVOICE $$ PRESENT'         
         OI    FLAG6(R4),X'01'     NO  - TURN ON 'INVOICE $$ PRESENT'           
SEVA0180 EQU   *                                                                
         CLC   PRIDATE(2,R4),SCLOSPRI                                           
*                                  PRIOR   MONTH CLOSED?                        
         BH    SEVA0200            NO  - NO FURTHER CHECKS                      
         OC    PRASATOR(4,R3),PRASATOR(R3)                                      
*                                  YES - PRIOR AS AT ORDERED ZERO?              
         BZ    SEVA0200            YES - LEAVE AS IS                            
         TM    FLAG6(R4),X'40'     NO  - INV$ AFTER ASAT DATE?                  
         BO    SEVA0200            YES - DON'T SET 'INVOICE $$ PRESENT'         
         OI    FLAG6(R4),X'04'     NO  - TURN ON 'INVOICE $$ PRESENT'           
SEVA0200 EQU   *                                                                
*                                                                               
*   BECAUSE THIS FIELD IS BEING USED FOR ACTUALS IN PRIOR YEAR, IT              
*      IS CHECKING AGAINST THE CLOSED DATE IN THE **CURRENT*** YEAR!            
*                                                                               
         CLC   PRIDATE(2,R4),SCLOSDTE                                           
*                                  PRIOR   MONTH CLOSED?                        
         BH    SEVA0220            NO  - NO FURTHER CHECKS                      
         OC    PRTOTORD(4,R3),PRTOTORD(R3)                                      
*                                  YES - PRIOR TOTAL ORDERED ZERO?              
         BZ    SEVA0220            YES - LEAVE AS IS                            
         TM    FLAG6(R4),X'40'     NO  - INV$ AFTER ASAT DATE?                  
         BO    SEVA0220            YES - DON'T SET 'INVOICE $$ PRESENT'         
         OI    FLAG6(R4),X'02'     NO  - TURN ON 'INVOICE $$ PRESENT'           
SEVA0220 EQU   *                                                                
         CLC   PRI2DATE(2,R4),SCLOS2PR                                          
*                                  2YR PRIOR   MONTH CLOSED?                    
         BH    SEVA0240            NO  - NO FURTHER CHECKS                      
         OC    P2ASATOR(4,R3),P2ASATOR(R3)                                      
*                                  YES - 2YR PRIOR AS AT ORDERED ZERO?          
         BZ    SEVA0240            YES - LEAVE AS IS                            
         TM    FLAG6(R4),X'80'     NO  - INV$ AFTER ASAT DATE?                  
         BO    SEVA0240            YES - DON'T SET 'INVOICE $$ PRESENT'         
         OI    FLAG6(R4),X'10'     NO  - TURN ON 'INVOICE $$ PRESENT'           
SEVA0240 EQU   *                                                                
*                                                                               
*   BECAUSE THIS FIELD IS BEING USED FOR ACTUALS 2 YEARS PRIOR, IT              
*      IS CHECKING AGAINST THE CLOSED DATE IN THE ***PRIOR*** YEAR!             
*                                                                               
         CLC   PRI2DATE(2,R4),SCLOSPRI                                          
*                                  2YR PRIOR   MONTH CLOSED?                    
         BH    SEVA0260            NO  - NO FURTHER CHECKS                      
         OC    P2TOTORD(4,R3),P2TOTORD(R3)                                      
*                                  YES - 2YR PRIOR TOTAL ORDERED ZERO?          
         BZ    SEVA0260            YES - LEAVE AS IS                            
         TM    FLAG6(R4),X'80'     NO  - INV$ AFTER ASAT DATE?                  
         BO    SEVA0260            YES - DON'T SET 'INVOICE $$ PRESENT'         
         OI    FLAG6(R4),X'08'     NO  - TURN ON 'INVOICE $$ PRESENT'           
SEVA0260 EQU   *                                                                
*                                                                               
*   TEST POST:                                                                  
         MVC   P+1(5),=C'POST:'                                                 
         GOTO1 HEXOUT,DMCB,RCONKCON,P+6,4,=C'TOG'                               
         GOTO1 HEXOUT,DMCB,SCLOSDTE,P+20,2,=C'TOG'                              
         MVC   P+30(60),0(R4)                                                   
         GOTO1 REPORT                                                           
*   TEST POST: END                                                              
*                                                                               
         LA    R4,NEXTBUCK(R4)     BUMP TO NEXT BUCKET                          
         B     SEVA0160            GO BACK FOR NEXT BUCKET                      
SEVA0280 EQU   *                                                                
         XIT1                                                                   
*                                                                               
SCLOSDTE DS    CL2                 STATION CLOSE DATE                           
SCLOSPRI DS    CL2                 STATION CLOSE DATE: PRIOR YEAR               
SCLOS2PR DS    CL2                 STATION CLOSE DATE: 2YR PRIOR                
SCLASATD DS    CL3                 AS-AT YEAR/MONTH DATES                       
TODAYSDT DS    CL3                 TODAY'S DATE                                 
SCLASATY DS    CL1                 CURRENT AS-AT YEAR                           
*                                                                               
