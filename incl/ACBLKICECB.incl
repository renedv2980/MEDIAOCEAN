*          DATA SET ACBLKICECB AT LEVEL 166 AS OF 10/27/20                      
       ++INCLUDE IEBUPDTHDR                                                     
                                                                                
**** SUPP=N IS FOR DEBUGGING ONLY. USE THIS WHEN NO SUPPORT DATA IS             
****     AVAILABLE, SO THAT ACC TRANSACTIONS WILL BE PROCESSED THAT             
****     WOULD OTHERWISE ONLY GO TO THE EXCEPTION FILE.                         
* SET DEFAULT TO SUPP=Y. CAN BE OVERRIDDEN IN THE JCL TO SUPP=N.                
*&&      SET   SUPP=Y                                                           
                                                                                
***********************************************************************         
                                                                                
       SAP MEDIA CLIENT BILLING EXPORT: XML FILE GENERATION                     
       BC_AR.xsd                                                                
IMPORTANT NOTE: this ICETOOL application sets RC=4 if there are no data         
records in the generated XML file. We do this by using the NULLOFL              
option. The JCL downstream from this ICETOOL step tests for RC=4, and           
does not transmit the XML file if this step completes with a non-zero           
return code. Therefore, if any COUNT operators are added to this                
program which may set the return code to a non-zero value, then the             
JCL may need to be changed!                                                     
                                                                                
***********************************************************************         
./ ADD NAME=SYMNAMES   ** SYMBOLICS USED IN CONTROL CARDS                       
*                                                                               
PIPE,C'|'                          DELIMITER                                    
TAB,X'05'                          DELIMITER                                    
*                                                                               
F1_ONLY,C'1'                       JOIN RESULT INDICATORS (AS PER IBM)          
F2_ONLY,C'2'                                                                    
BOTH_F1_AND_F2,C'B'                                                             
*                                                                               
* Parsed record variables (for the ACC extract dataset)                         
*                                                                               
P_AXG$RTYP,%105                    RECORD TYPE                                  
P_AXG$TTYP,%106                    TRANSACTION TYPE                             
P_AXG$ACCD,%107                    ACCOUNT CODE                                 
P_AXG$OFFC,%109                    OFFICE CODE                                  
P_AXG$TRDT,%110                    TRANSACTION DATE                             
P_AXG$SYS,%113                     SYSTEM CODE                                  
P_AXG$MED,%114                     MEDIA                                        
P_AXG$CLT,%115                     CLIENT                                       
P_AXG$PRD,%116                     CLIENT/PRODUCT                               
P_AXG$EST,%117                     ESTIMATE                                     
P_AXG$DR,%118                      DEBIT                                        
P_AXG$ACDT,%122                    ACTIVITY DATE                                
P_AXG$MOS,%124                     MOS                                          
P_AXG$BINC,%128                    BILLING INCOME (COMMISSION)                  
P_AXG$DUDT,%135                    DUE DATE                                     
P_AXG$INV,%138                     INVOICE NUMBER (LONG)                        
P_AXG$NARR,%153                    NARRATIVE                                    
P_AXG$CURR,%154                    SAP CURRENCY (USD/CND)                       
P_AXG$STYP,%155                    SAP TRANACTION TYPE                          
P_AXG$NETW,%156                    SAP NETWORK                                  
P_AXG$PID,%164                     PID                                          
P_AXG$ITMN,%165                    ITEM NO                                      
P_AXG$VNDC,%166                    VENDOR CODE                                  
P_AXG$VNDN,%167                    VENDOR NAME                                  
P_AXG$GRS,%168                     GROSS                                        
P_AXG$NET,%169                     NET AMOUNT                                   
P_AXG$TAXA,%170                    Tax Amount                                   
P_AXG$TAXC,%171                    TAx Code                                     
P_AXG$INSO,%172                    INSERTION ORDER NUMBER                       
P_AXG$CMPG,%173                    CAMPAIGN                                     
P_AXG$AGY,%174                     AGENCY                                       
P_AXG$MEDA,%175                    MEDIA AUTHORIZATION NUMBER                   
P_AXG$ESTU,%176                    ESTIMATE UNIQUE NUMBER                       
P_AXG$BUYU,%177                    BUY UNIQUE NUBMER                            
P_AXG$VOUC,%178                    VOUCHER NUMBER                               
P_AXG$VOID,%179                    VOID FLAG                                    
P_AXG$MKTC,%180                    MARKET CODE                                  
P_AXG$MKTN,%181                    MARKET NAME                                  
P_AXG$NSPT,%182                    NUMBER OF SPOTS                              
P_AXG$PONM,%183                    PURCHASE ORDER NUMBER                        
P_AXG$CLNM,%184                    CLIENT NAME                                  
P_AXG$PRNM,%185                    PRODUCT NAME                                 
P_AXG$ESNM,%186                    ESTIMATE NAME                                
P_AXG$SBMT,%187                    SUB-MEDIA TYPE NAME                          
P_AXG$SBMN,%188                    SUB-MEDIA NAME                               
P_AXG$PUD1,%189                    PRODUCT UDEF 1                               
P_AXG$PUD2,%190                    PRODUCT UDEF 2                               
P_AXG$EUD1,%191                    ESTIMATE UDEF 1                              
P_AXG$EUD2,%192                    ESTIMATE UDEF 2                              
P_AXG$CPYI,%193                    COMPANY ID (USERID)                          
P_AXG$LINI,%194                    COMPANY ID (USERID)                          
P_AXG$INSD,%195                    INSERTION DATE                               
P_AXG$SPAC,%196                    SPACE DESCRIPTION                            
P_AXG$AXN,%197                     ACTION                                       
P_AXG$NETH,%198                    NET FOR HEADER                               
P_AXG$COMH,%199                    COMMISSION FOR HEADER                        
P_AXG$TAXH,%200                    TAX FOR HEADER                               
*                                                                               
* Open AR file symbols                                                          
*                                                                               
OPEN_ACC2,1,12,CH                                                               
OPEN_SMC2,*,2,CH                                                                
OPEN_OFC2,*,2,CH                                                                
OPEN_BDTE2,*,6,CH                                                               
OPEN_REF2,*,6,CH                                                                
OPEN_SEQ2,*,3,ZD                                                                
OPEN_BAL2,*,14,CH                                                               
*                                                                               
*                                                                               
./ ADD NAME=TOOLIN     ** CONTROL CARDS FOR ICETOOL                             
*                                                                               
* Parse the ACC extract file, keeping only Spot and Print transactions.         
COPY FROM(XTRACTIN) USING(PARS)                                                 
*                                                                               
* Discard all transactions with errors, producing an FB dataset of              
* transactions to be joined with vendor data. This is the "detail               
* work file".                                                                   
COPY FROM(ALLRECS) TO(GOODTRNS) USING(COPG)                                     
*                                                                               
* Copy GOODTRNS to another dataset, so we can join it with itself.              
COPY FROM(GOODTRNS) TO(TEMP1)                                                   
*                                                                               
* Seed the invoice gross $ into the detail work file.                           
COPY JKFROM TO(JOINVEND) USING(JONH)                                            
*                                                                               
* Sort the detail work file by invoice number.                                  
SORT FROM(JOINVEND) TO(SRTITEMS) USING(SRTI)                                    
*                                                                               
* Copy FINAL to a tab-delimited file we can import into Excel (for              
*  debugging).                                                                  
COPY FROM(JOINVEND) TO(JOINTAB) USING(JTAB)                                     
*                                                                               
* Generate an XML output file. This file is syntactically correct, but          
* contains the more verbose syntax for empty fields.                            
COPY FROM(SRTITEMS) USING(COPX)                                                 
*                                                                               
* Prettify the XML output file by shortening tags with empty fields.            
COPY FROM(TEMPXML) TO(XMLOUT) USING(CPTX)                                       
                                                                                
                                                                                
./ ADD NAME=PARSCNTL                                                            
*                                                                               
 INCLUDE COND=(5,5,CH,EQ,C'05081',OR,5,5,CH,EQ,C'05088',OR,                     
               5,5,CH,EQ,C'07141',OR,5,5,CH,EQ,C'06536')                        
*                                                                               
 INREC PARSE=(%=(ENDBEFR=PIPE,REPEAT=5),                                        
           P_AXG$RTYP=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$RTYP),                       
           P_AXG$TTYP=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$TTYP),                       
           P_AXG$ACCD=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$ACCD), for debugging         
           %=(ENDBEFR=PIPE),                                                    
           P_AXG$OFFC=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$OFFC),                       
           P_AXG$TRDT=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$TRDT),                       
           %=(ENDBEFR=PIPE,REPEAT=2),                                           
           P_AXG$SYS=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$SYS),                         
           P_AXG$MED=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$MED),                         
           P_AXG$CLT=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$CLT),                         
           P_AXG$PRD=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$PRD),  CLIENT/PRODUCT         
           P_AXG$EST=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$EST),                         
           P_AXG$DR=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$DR),                           
           %=(ENDBEFR=PIPE,REPEAT=2),                                           
           P_AXG$ACDT=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$ACDT),                       
           %=(ENDBEFR=PIPE),                                                    
           P_AXG$MOS=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$MOS),                         
           %=(ENDBEFR=PIPE),                                                    
           P_AXG$BINC=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$BINC),                       
           %=(ENDBEFR=PIPE,REPEAT=6),                                           
           P_AXG$DUDT=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$DUDT),                       
           P_AXG$INV=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$INV),                         
           %=(ENDBEFR=PIPE,REPEAT=12),                                          
           P_AXG$NARR=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$NARR),                       
           P_AXG$CURR=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$CURR),                       
           P_AXG$STYP=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$STYP),                       
           P_AXG$NETW=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$NETW),                       
           %=(ENDBEFR=PIPE,REPEAT=7),                                           
           P_AXG$PID=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$PID),                         
           P_AXG$ITMN=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$ITMN),                       
           P_AXG$VNDC=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$VNDC),                       
           P_AXG$VNDN=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$VNDN),                       
           P_AXG$GRS=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$GRS),                         
           P_AXG$NET=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$NET),                         
           P_AXG$TAXA=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$TAXA),                       
           P_AXG$TAXC=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$TAXC),                       
           P_AXG$INSO=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$INSO),                       
           P_AXG$CMPG=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$CMPG),                       
           P_AXG$AGY=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$AGY),                         
           P_AXG$MEDA=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$MEDA),                       
           P_AXG$ESTU=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$ESTU),                       
           P_AXG$BUYU=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$BUYU),                       
           P_AXG$VOUC=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$VOUC),                       
           P_AXG$VOID=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$VOID),                       
           P_AXG$MKTC=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$MKTC),                       
           P_AXG$MKTN=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$MKTN),                       
           P_AXG$NSPT=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$NSPT),                       
           P_AXG$PONM=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$PONM),                       
           P_AXG$CLNM=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$CLNM),                       
           P_AXG$PRNM=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$PRNM),                       
           P_AXG$ESNM=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$ESNM),                       
           P_AXG$SBMT=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$SBMT),                       
           P_AXG$SBMN=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$SBMN),                       
           P_AXG$PUD1=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$PUD1),                       
           P_AXG$PUD2=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$PUD2),                       
           P_AXG$EUD1=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$EUD1),                       
           P_AXG$EUD2=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$EUD2),                       
           P_AXG$CPYI=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$CPYI),                       
           P_AXG$LINI=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$LINI),                       
           P_AXG$INSD=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$INSD),                       
           P_AXG$SPAC=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$SPAC),                       
           %=(ENDBEFR=PIPE),                                                    
           P_AXG$NETH=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$NETH),                       
           P_AXG$COMH=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$COMH),                       
           P_AXG$TAXH=(ENDBEFR=PIPE,FIXLEN=LEN_AXG$TAXH)),                      
    BUILD=(AXG$LEN,                                                             
           AXG$RTYP:P_AXG$RTYP,                                                 
           AXG$TTYP:P_AXG$TTYP,                                                 
           AXG$ACCD:P_AXG$ACCD,                                                 
           AXG$OFFC:P_AXG$OFFC,                                                 
           AXG$TRDT:P_AXG$TRDT,                                                 
           AXG$SYS:P_AXG$SYS,                                                   
           AXG$MED:P_AXG$MED,                                                   
           AXG$CLT:P_AXG$CLT,                                                   
           AXG$PRD:P_AXG$PRD,                                                   
           AXG$EST:P_AXG$EST,                                                   
           AXG$DR:P_AXG$DR,SFF,TO=ZD,                                           
           AXG$ACDT:P_AXG$ACDT,                                                 
           AXG$MOS:P_AXG$MOS,                                                   
           AXG$BINC:P_AXG$BINC,SFF,TO=ZD,                                       
           AXG$DUDT:P_AXG$DUDT,                                                 
           AXG$INV:P_AXG$INV,                                                   
           AXG$NARR:P_AXG$NARR,                                                 
           AXG$CURR:P_AXG$CURR,                                                 
           AXG$STYP:P_AXG$STYP,                                                 
           AXG$NETW:P_AXG$NETW,                                                 
           AXG$PID:P_AXG$PID,                                                   
           AXG$ITMN:P_AXG$ITMN,                                                 
           AXG$VNDC:P_AXG$VNDC,                                                 
           AXG$VNDN:P_AXG$VNDN,                                                 
           AXG$GRS:P_AXG$GRS,SFF,TO=ZD,                                         
           AXG$NET:P_AXG$NET,SFF,TO=ZD,                                         
           AXG$TAXA:P_AXG$TAXA,SFF,TO=ZD,                                       
           AXG$TAXC:P_AXG$TAXC,                                                 
           AXG$INSO:P_AXG$INSO,                                                 
           AXG$CMPG:P_AXG$CMPG,                                                 
           AXG$AGY:P_AXG$AGY,                                                   
           AXG$MEDA:P_AXG$MEDA,                                                 
           AXG$ESTU:P_AXG$ESTU,                                                 
           AXG$BUYU:P_AXG$BUYU,                                                 
           AXG$VOUC:P_AXG$VOUC,                                                 
           AXG$VOID:P_AXG$VOID,                                                 
           AXG$MKTC:P_AXG$MKTC,                                                 
           AXG$MKTN:P_AXG$MKTN,                                                 
           AXG$NSPT:P_AXG$NSPT,                                                 
           AXG$PONM:P_AXG$PONM,                                                 
           AXG$CLNM:P_AXG$CLNM,                                                 
           AXG$PRNM:P_AXG$PRNM,                                                 
           AXG$ESNM:P_AXG$ESNM,                                                 
           AXG$SBMT:P_AXG$SBMT,                                                 
           AXG$SBMN:P_AXG$SBMN,                                                 
           AXG$PUD1:P_AXG$PUD1,                                                 
           AXG$PUD2:P_AXG$PUD2,                                                 
           AXG$EUD1:P_AXG$EUD1,                                                 
           AXG$EUD2:P_AXG$EUD2,                                                 
           AXG$CPYI:P_AXG$CPYI,                                                 
           AXG$LINI:P_AXG$LINI,                                                 
           AXG$INSD:P_AXG$INSD,                                                 
           AXG$SPAC:P_AXG$SPAC,                                                 
           AXG$NETH:P_AXG$NETH,SFF,TO=ZD,                                       
           AXG$COMH:P_AXG$COMH,SFF,TO=ZD,                                       
           AXG$TAXH:P_AXG$TAXH,SFF,TO=ZD,                                       
           A_ACC_SEQNUM:SEQNUM,LEN_A_ACC_SEQNUM,PD)                             
*                                                                               
 OUTFIL FNAMES=ALLRECS,                                                         
*                                                                               
* Include all system receivables that are type 9                                
   INCLUDE=(AXG$RTYP,EQ,C'RCV',AND,AXG$TTYP,EQ,C'009'),                         
   VTOF,                                                                        
** Start building in column 5, to account for the absence of the RDW !          
   BUILD=(5:AXG$RECORD,              *** start in column 5                      
          A_ACC_SEQNUM:A_ACC_SEQNUM)                                            
                                                                                
                                                                                
./ ADD NAME=COPGCNTL                                                            
*                                                                               
* Discard all records which were kept around only so that they could            
* be written to the exception file.                                             
*                                                                               
 OUTFIL VTOF,BUILD=(D_AXG$SYS:AXG$SYS,                                          
                    D_AXG$MED:AXG$MED,                                          
                    D_AXG$CLT:AXG$CLT,                                          
                    D_AXG$PRD:AXG$PRD,                                          
                    D_AXG$EST:AXG$EST,                                          
                    D_AXG$INV:AXG$INV,                                          
                    D_AXG$AGY:AXG$AGY,                                          
                    D_AXG$ACCD:AXG$ACCD,                                        
                    D_AXG$OFFC:AXG$OFFC,                                        
                    D_AXG$TRDT:AXG$TRDT,                                        
                    D_AXG$DR:AXG$DR,                                            
                    D_AXG$ACDT:AXG$ACDT,                                        
                    D_AXG$MOS:AXG$MOS,                                          
                    D_AXG$BINC:AXG$BINC,                                        
                    D_AXG$DUDT:AXG$DUDT,                                        
                    D_AXG$NARR:AXG$NARR,                                        
                    D_AXG$CURR:AXG$CURR,                                        
                    D_AXG$STYP:AXG$STYP,                                        
                    D_AXG$NETW:AXG$NETW,                                        
                    D_AXG$PID:AXG$PID,                                          
                    D_AXG$ITMN:AXG$ITMN,                                        
                    D_AXG$VNDC:AXG$VNDC,                                        
                    D_AXG$VNDN:AXG$VNDN,                                        
                    D_AXG$GRS:AXG$GRS,                                          
                    D_AXG$NETA:AXG$NET,                                         
                    D_AXG$TAXA:AXG$TAXA,                                        
                    D_AXG$TAXC:AXG$TAXC,                                        
                    D_AXG$INSO:AXG$INSO,                                        
                    D_AXG$CMPG:AXG$CMPG,                                        
                    D_AXG$MEDA:AXG$MEDA,                                        
                    D_AXG$ESTU:AXG$ESTU,                                        
                    D_AXG$BUYU:AXG$BUYU,                                        
                    D_AXG$VOUC:AXG$VOUC,                                        
                    D_AXG$VOID:AXG$VOID,                                        
                    D_AXG$MKTC:AXG$MKTC,                                        
                    D_AXG$MKTN:AXG$MKTN,                                        
                    D_AXG$NSPT:AXG$NSPT,                                        
                    D_AXG$PONM:AXG$PONM,                                        
                    D_AXG$CLNM:AXG$CLNM,                                        
                    D_AXG$PRNM:AXG$PRNM,                                        
                    D_AXG$ESNM:AXG$ESNM,                                        
                    D_AXG$SBMT:AXG$SBMT,                                        
                    D_AXG$SBMN:AXG$SBMN,                                        
                    D_AXG$PUD1:AXG$PUD1,                                        
                    D_AXG$PUD2:AXG$PUD2,                                        
                    D_AXG$EUD1:AXG$EUD1,                                        
                    D_AXG$EUD2:AXG$EUD2,                                        
                    D_AXG$CPYI:AXG$CPYI,                                        
                    D_AXG$LINI:AXG$LINI,                                        
                    D_AXG$INSD:AXG$INSD,                                        
                    D_AXG$SPAC:AXG$SPAC,                                        
                    D_AXG$NETH:AXG$NETH,                                        
                    D_AXG$COMH:AXG$COMH,                                        
                    D_AXG$TAXH:AXG$TAXH)                                        
                                                                                
                                                                                
./ ADD NAME=JONHCNTL                                                            
*                                                                               
* Produce gross $ totals across months-of-service for each invoice.             
*                                                                               
 JOINKEYS F1=GOODTRNS,                                                          
             FIELDS=(D_AXG$SYS,A,                                               
                     D_AXG$MED,A,                                               
                     D_AXG$CLT,A,                                               
                     D_AXG$PRD,A,                                               
                     D_AXG$EST,A,                                               
                     D_AXG$TDYY,A,                                              
                     D_AXG$TDMM,A,                                              
                     D_AXG$TDDD,A,                                              
                     D_AXG$INV,A)                                               
 JOINKEYS F2=TEMP1,                                                             
             TASKID=T1,          <<<< SEE T1F2CNTL                              
             FIELDS=(D_AXG$SYS,A,                                               
                     D_AXG$MED,A,                                               
                     D_AXG$CLT,A,                                               
                     D_AXG$PRD,A,                                               
                     D_AXG$EST,A,                                               
                     D_AXG$TDYY,A,                                              
                     D_AXG$TDMM,A,                                              
                     D_AXG$TDDD,A,                                              
                     D_AXG$INV,A)                                               
 REFORMAT FIELDS=(F2:D_AXG$GRS,     <===== COMPUTED GROSS (FROM TOTALS)         
                     D_AXG$NETH,    <===== COMPUTED NET   (FROM TOTALS)         
                     D_AXG$COMH,    <===== COMPUTED COMM  (FROM TOTALS)         
                     D_AXG$TAXH,    <===== COMPUTED TAX   (FROM TOTALS)         
                  F1:D_JOINED_DATA)                                             
*                                                                               
* Build our work record from the reformatted fields.                            
 INREC BUILD=(D_TOTGRS:REFH_D_TOTGRS,                                           
              D_TOTNET:REFH_D_TOTNET,                                           
              D_TOTCOM:REFH_D_TOTCOM,                                           
              D_TOTTAX:REFH_D_TOTTAX,                                           
              D_JOINED_DATA:REFH_D_JOINED_DATA)                                 
                                                                                
                                                                                
./ ADD NAME=T1F2CNTL                                                            
*                                                                               
* This collapses F2 *after* the sort (where the sort key is defined             
* by the F2 JOINKEYS fields above), retaining only the first duplicate.         
* The gross $ (i.e., the SUM) winds up in the totalled record.                  
*                                                                               
 SUM FIELDS=(D_AXG$GRS,                                                         
             D_AXG$NETH,                                                        
             D_AXG$COMH,                                                        
             D_AXG$TAXH)                                                        
                                                                                
                                                                                
./ ADD NAME=SRTICNTL                                                            
*                                                                               
* Sort the detail records into the sequence that we want them to appear         
* in the XML file, and fill in all other available (and relevant) data          
* fields. Assign the Item_Number value to each remaining detail line.           
* The Item_Number is restarted at value 1 at the start of each Header           
* (System, Media, Client, Product, Estimate, Invoice#).                         
*                                                                               
 INREC IFTHEN=(WHEN=INIT,OVERLAY=(D_AXG$STYP:C'INV'))                           
*                                                                               
 SORT FIELDS=(D_AXG$SYS,A,                                                      
              D_AXG$MED,A,                                                      
              D_AXG$CLT,A,                                                      
              D_AXG$PRD,A,                                                      
              D_AXG$EST,A,                                                      
              D_AXG$TDYY,A,                                                     
              D_AXG$TDMM,A,                                                     
              D_AXG$TDDD,A,                                                     
              D_AXG$INV,A,                                                      
              D_SORT_SEQUENCE,A),                                               
      EQUALS                               *** makes debugging easier!          
*                                                                               
 OUTREC IFTHEN=(WHEN=INIT,                 *** initialization                   
         OVERLAY=(X_AXG$SYS:D_AXG$SYS,                                          
                  X_AXG$MED:D_AXG$MED,                                          
                  X_AXG$CLT:D_AXG$CLT,                                          
                  X_AXG$PRD:D_AXG$PRD,                                          
                  X_AXG$EST:D_AXG$EST,                                          
                  X_AXG$TDYY:D_AXG$TDYY,                                        
                  X_AXG$TDMM:D_AXG$TDMM,                                        
                  X_AXG$TDDD:D_AXG$TDDD,                                        
                  X_AXG$INV:D_AXG$INV))                                         
*                                                                               
 OUTFIL OVERLAY=(D_ITEM_NUMBER:SEQNUM,LEN_D_ITEM_NUMBER,ZD,                     
                   RESTART=(D_ITEM_#_KEY))                                      
                                                                                
                                                                                
./ ADD NAME=JTABCNTL                                                            
*                                                                               
* Produce a tab-delimited file for debugging (this can easily be                
* imported into Excel).                                                         
*                                                                               
 OUTFIL REMOVECC,                                                               
        HEADER1=('SEP=',TAB,/,         *** SEP= only works in Excel!!!          
                 'M',TAB,                                                       
                 'CLT',TAB,                                                     
                 'PRD',TAB,                                                     
                 'EST',TAB,                                                     
                 'INV#',TAB,                                                    
                 'AY',TAB,                                                      
                 'ACCOUNT',TAB,                                                 
                 'OF',TAB,                                                      
                 'TRANDATE',TAB,                                                
                 'DEBIT',TAB,                                                   
                 'ACT DATE',TAB,                                                
                 'MOS',TAB,                                                     
                 'COMMISSION',TAB,                                              
                 'DUE DATE',TAB,                                                
                 'ITEM#',TAB,                                                   
                 'VENDOR',TAB,                                                  
                 'GROSS',TAB,                                                   
                 'NET AMOUNT',TAB,                                              
                 'TAX AMOUNT',TAB,                                              
                 'TC',TAB,                                                      
                 'INSERTION ORDER',TAB,                                         
                 'CAMPAIGN',TAB,                                                
                 'MEDIA AUTH',TAB,                                              
                 'EUN',TAB,                                                     
                 'BUY UNIQUE NO',TAB,                                           
                 'VOUCHER',TAB,                                                 
                 'V',TAB,                                                       
                 'INSD',TAB,                                                    
                 'SPACE',TAB),                                                  
*                                                                               
       BUILD=(D_AXG$MED,TAB,                                                    
              D_AXG$CLT,TAB,                                                    
              D_AXG$PRD_PRD,TAB,                                                
              D_AXG$EST,TAB,                                                    
              D_AXG$INV,TAB,                                                    
              D_AXG$MOS,TAB,                                                    
              D_AXG$AGY,TAB,                                                    
              D_AXG$ACCD,TAB,                                                   
              D_AXG$OFFC,TAB,                                                   
              D_AXG$TRDT,TAB,                                                   
              D_AXG$DR,TAB,                                                     
              D_AXG$ACDT,TAB,                                                   
              D_AXG$BINC,TAB,                                                   
              D_AXG$DUDT,TAB,                                                   
              D_AXG$ITMN,TAB,                                                   
              D_AXG$VNDC,TAB,                                                   
              D_AXG$GRS,TAB,                                                    
              D_AXG$NETA,TAB,                                                   
              D_AXG$TAXA,TAB,                                                   
              D_AXG$TAXC,TAB,                                                   
              D_AXG$INSO,TAB,                                                   
              D_AXG$CMPG,TAB,                                                   
              D_AXG$MEDA,TAB,                                                   
              D_AXG$ESTU,TAB,                                                   
              D_AXG$BUYU,TAB,                                                   
              D_AXG$VOUC,TAB,                                                   
              D_AXG$VOID,TAB,                                                   
              D_AXG$INSD,TAB,                                                   
              D_AXG$SPAC,TAB)                                                   
                                                                                
                                                                                
./ ADD NAME=COPXCNTL                                                            
*                                                                               
* Generate the almost-final XML dataset.                                        
*                                                                               
* This OUTREC is used to format the Invoice Header field lines in XML           
* format. This is necessary because the formatting features are not             
* available under HEADER3.                                                      
*                                                                               
 OUTREC OVERLAY=(X_MEDIA_SYSTEM:                                                
        C'<Media_System>MEDIAOCEAN_US</Media_System>',                          
     X_AGY_ALPHA:D_AXG$AGY,JFY=(SHIFT=LEFT,LEAD=C'<Agency_Alpha_Code>',         
      TRAIL=C'</Agency_Alpha_Code>',LENGTH=60),                                 
     X_VOUCHER:D_AXG$VOUC,JFY=(SHIFT=LEFT,LEAD=C'<Voucher_No>',                 
      TRAIL=C'</Voucher_No>',LENGTH=60),                                        
     X_DUE_DATE:D_AXG$DUDT,JFY=(SHIFT=LEFT,LEAD=C'<Due_Date>',                  
      TRAIL=C'</Due_Date>',LENGTH=60),                                          
     X_CUSTOMER:D_AXG$CLT,JFY=(SHIFT=LEFT,                                      
      LEAD=C'<Customer>',TRAIL=C'</Customer>',LENGTH=60),                       
     X_INVOICE_NO:D_AXG$INV,JFY=(SHIFT=LEFT,                                    
      LEAD=C'<Invoice_No>',TRAIL=C'</Invoice_No>',LENGTH=60),                   
     X_COMPANY_CODE:D_AXG$OFFC,JFY=(SHIFT=LEFT,                                 
      LEAD=C'<Company_Code>',TRAIL=C'</Company_Code>',LENGTH=60),               
     X_TOTGRS:D_TOTGRS,EDIT=(SIIIIIIIIIIIT.TT),SIGNS=(,-),                      
     X_TOTNET:D_TOTNET,EDIT=(SIIIIIIIIIIIT.TT),SIGNS=(,-),                      
     X_TOTCOM:D_TOTCOM,EDIT=(SIIIIIIIIIIIT.TT),SIGNS=(,-),                      
     X_TOTTAX:D_TOTTAX,EDIT=(SIIIIIIIIIIIT.TT),SIGNS=(,-),                      
     X_GRS:D_AXG$GRS,EDIT=(SIIIIIIIIIIIT.TT),SIGNS=(,-),                        
     X_NETA:D_AXG$NETA,EDIT=(SIIIIIIIIIIIT.TT),SIGNS=(,-),                      
     X_COMM:D_AXG$BINC,EDIT=(SIIIIIIIIIIIT.TT),SIGNS=(,-),                      
     X_TAXA:D_AXG$TAXA,EDIT=(SIIIIIIIIIIIT.TT),SIGNS=(,-),                      
     X_NETH:D_AXG$NETH,EDIT=(SIIIIIIIIIIIT.TT),SIGNS=(,-),                      
     X_COMH:D_AXG$COMH,EDIT=(SIIIIIIIIIIIT.TT),SIGNS=(,-),                      
     X_TAXH:D_AXG$TAXH,EDIT=(SIIIIIIIIIIIT.TT),SIGNS=(,-),                      
     X_TOTGRSAMT:X_TOTGRS,JFY=(SHIFT=LEFT,                                      
      LEAD=C'<Gross_Amount>',TRAIL=C'</Gross_Amount>',LENGTH=60),               
     X_TOTNETAMT:X_TOTNET,JFY=(SHIFT=LEFT,                                      
      LEAD=C'<Net_Amount>',TRAIL=C'</Net_Amount>',LENGTH=60),                   
     X_TOTCOMAMT:X_TOTCOM,JFY=(SHIFT=LEFT,                                      
      LEAD=C'<Commission_Amount>',                                              
      TRAIL=C'</Commission_Amount>',LENGTH=60),                                 
     X_TOTTAXAMT:X_TOTTAX,JFY=(SHIFT=LEFT,                                      
      LEAD=C'<Tax_Amount>',TRAIL=C'</Tax_Amount>',LENGTH=60),                   
     X_TRNSTYP:D_AXG$STYP,JFY=(SHIFT=LEFT,                                      
      LEAD=C'<Trans_Type>',TRAIL=C'</Trans_Type>',LENGTH=30),                   
     X_INVOICE_DATE:D_AXG$TRDT,JFY=(SHIFT=LEFT,                                 
      LEAD=C'<Invoice_Date>',TRAIL=C'</Invoice_Date>',LENGTH=60),               
     X_POSTING_DATE:D_AXG$TRDT,JFY=(SHIFT=LEFT,                                 
      LEAD=C'<Posting_Date>',TRAIL=C'</Posting_Date>',LENGTH=60),               
     X_MEDIA_CLT:D_AXG$CLT,JFY=(SHIFT=LEFT,                                     
      LEAD=C'<Media_Client>',TRAIL=C'</Media_Client>',LENGTH=60),               
     X_CLNAM:D_AXG$CLNM,JFY=(SHIFT=LEFT,                                        
      LEAD=C'<Media_Client_Name>',                                              
      TRAIL=C'</Media_Client_Name>',LENGTH=80),                                 
     X_MEDIA_PRD:D_AXG$PRD_PRD,JFY=(SHIFT=LEFT,                                 
      LEAD=C'<Media_Product>',TRAIL=C'</Media_Product>',LENGTH=60),             
     X_CURRENCY:D_AXG$CURR,JFY=(SHIFT=LEFT,                                     
      LEAD=C'<Currency>',TRAIL=C'</Currency>',LENGTH=60),                       
     X_PRNAM:D_AXG$PRNM,JFY=(SHIFT=LEFT,                                        
      LEAD=C'<Media_Product_Name>',                                             
      TRAIL=C'</Media_Product_Name>',LENGTH=80),                                
     X_INSD:D_AXG$INSD,JFY=(SHIFT=LEFT,                                         
      LEAD=C'<Insertion_Date>',                                                 
      TRAIL=C'</Insertion_Date>',LENGTH=80),                                    
     X_SPAC:D_AXG$SPAC,JFY=(SHIFT=LEFT,                                         
      LEAD=C'<Space_Description>',                                              
      TRAIL=C'</Space_Description>',LENGTH=80),                                 
     X_MEDIA_CMPG:D_AXG$CMPG,JFY=(SHIFT=LEFT,                                   
      LEAD=C'<Media_Campaign>',TRAIL=C'</Media_Campaign>',LENGTH=60),           
     X_BILLING_PRD:D_AXG$PRD_PRD,JFY=(SHIFT=LEFT,                               
      LEAD=C'<Billing_Product>',TRAIL=C'</Billing_Product>',LENGTH=60),         
     X_PONUM:D_AXG$PONM,JFY=(SHIFT=LEFT,                                        
      LEAD=C'<Purchase_Order_Number>',                                          
      TRAIL=C'</Purchase_Order_Number>',LENGTH=80),                             
     X_PUD1:D_AXG$PUD1,JFY=(SHIFT=LEFT,                                         
      LEAD=C'<UDEF_Product_1>',TRAIL=C'</UDEF_Product_1>',LENGTH=80),           
     X_PUD2:D_AXG$PUD2,JFY=(SHIFT=LEFT,                                         
      LEAD=C'<UDEF_Product_2>',TRAIL=C'</UDEF_Product_2>',LENGTH=80),           
     X_EUD1:D_AXG$EUD1,JFY=(SHIFT=LEFT,                                         
      LEAD=C'<UDEF_Estimate_1>',TRAIL=C'</UDEF_Estimate_1>',LENGTH=80),         
     X_EUD2:D_AXG$EUD2,JFY=(SHIFT=LEFT,                                         
      LEAD=C'<UDEF_Estimate_2>',TRAIL=C'</UDEF_Estimate_2>',LENGTH=80),         
     X_LOCATION:D_AXG$OFFC,JFY=(SHIFT=LEFT,                                     
      LEAD=C'<Location>',TRAIL=C'</Location>',LENGTH=60),                       
     X_LOCCOD:D_AXG$CPYI,JFY=(SHIFT=LEFT,                                       
      LEAD=C'<Location_Code>',TRAIL=C'</Location_Code>',LENGTH=80))             
*                                                                               
 OUTFIL FNAMES=TEMPXML,LINES=80,                                                
        REMOVECC,                                                               
        NULLOFL=RC4,          *** SET RC=4 IF THERE ARE NO DATA RECORDS         
 BUILD=(05:C'<InvoiceItem>',/,                                                  
     07:D_ITEM_NUMBER,JFY=(SHIFT=LEFT,                                          
        LEAD=C'<Item_No>',TRAIL=C'</Item_No>',LENGTH=74),/,                     
     07:C'<GL_Account/>'/,        *** REQUIRED BY XSD SCHEMA                    
*    07:X_GRS,JFY=(SHIFT=LEFT,                                                  
*       LEAD=C'<Grs_Amount>',TRAIL=C'</Grs_Amount>',LENGTH=74),/,               
     07:X_NETA,JFY=(SHIFT=LEFT,                                                 
        LEAD=C'<Net_Amount>',TRAIL=C'</Net_Amount>',LENGTH=74),/,               
*    07:X_NETH,JFY=(SHIFT=LEFT,                                                 
*       LEAD=C'<Net_Header>',TRAIL=C'</Net_Header>',LENGTH=74),/,               
     07:X_COMM,JFY=(SHIFT=LEFT,LEAD=C'<Commission_Amount>',                     
        TRAIL=C'</Commission_Amount>',LENGTH=74),/,                             
*    07:X_COMH,JFY=(SHIFT=LEFT,LEAD=C'<Commission_Header>',                     
*       TRAIL=C'</Commission_Header>',LENGTH=74),/,                             
     07:D_AXG$TAXC,JFY=(SHIFT=LEFT,                                             
        LEAD=C'<Tax_Code>',TRAIL=C'</Tax_Code>',LENGTH=74),/,                   
     07:X_TAXA,JFY=(SHIFT=LEFT,                                                 
        LEAD=C'<Tax_Amount>',TRAIL=C'</Tax_Amount>',LENGTH=74),/,               
*    07:X_TAXH,JFY=(SHIFT=LEFT,                                                 
*       LEAD=C'<Tax_Header>',TRAIL=C'</Tax_Header>',LENGTH=74),/,               
     07:D_AXG$VNDC,JFY=(SHIFT=LEFT,                                             
        LEAD=C'<Network>',TRAIL=C'</Network>',LENGTH=74),/,                     
     07:D_AXG$VNDN,JFY=(SHIFT=LEFT,                                             
        LEAD=C'<Vendor>',TRAIL=C'</Vendor>',LENGTH=74),/,                       
     07:D_AXG$MOS,JFY=(SHIFT=LEFT,                                              
        LEAD=C'<MOS>',TRAIL=C'</MOS>',LENGTH=74),/,                             
     07:D_AXG$SYS,JFY=(SHIFT=LEFT,                                              
        LEAD=C'<Media_Type>',TRAIL=C'</Media_Type>',LENGTH=74),/,               
     07:D_AXG$MED,JFY=(SHIFT=LEFT,                                              
        LEAD=C'<Submedia>',TRAIL=C'</Submedia>',LENGTH=74),/,                   
     07:D_AXG$SBMT,JFY=(SHIFT=LEFT,                                             
        LEAD=C'<Submedia_Name>',                                                
        TRAIL=C'</Submedia_Name>',LENGTH=74),/,                                 
     07:D_AXG$SBMN,JFY=(SHIFT=LEFT,                                             
        LEAD=C'<Submedia_Type>',                                                
        TRAIL=C'</Submedia_Type>',LENGTH=74),/,                                 
     07:C'<Item_Text/>'/,                                                       
     07:C'<Booking_Curr_Amt/>'/,                                                
     07:C'<Booking_Exch_Rate/>'/,                                               
     07:C'<Booking_Curr_Code/>'/,                                               
     07:D_AXG$CMPG,JFY=(SHIFT=LEFT,                                             
        LEAD=C'<Media_Campaign>',TRAIL=C'</Media_Campaign>',LENGTH=74),         
        /,                                                                      
     07:D_AXG$MKTC,JFY=(SHIFT=LEFT,                                             
        LEAD=C'<Market_Code>',TRAIL=C'</Market_Code>',LENGTH=74),/,             
     07:D_AXG$MKTN,JFY=(SHIFT=LEFT,                                             
        LEAD=C'<Market_Name>',TRAIL=C'</Market_Name>',LENGTH=74),/,             
     07:D_AXG$EST,JFY=(SHIFT=LEFT,                                              
        LEAD=C'<Estimate_No>',TRAIL=C'</Estimate_No>',LENGTH=74),/,             
     07:D_AXG$ESNM,JFY=(SHIFT=LEFT,                                             
        LEAD=C'<Estimate_No_Name>',                                             
        TRAIL=C'</Estimate_No_Name>',LENGTH=74),/,                              
     07:D_AXG$INSO,JFY=(SHIFT=LEFT,                                             
        LEAD=C'<Insertion_Order>',                                              
        TRAIL=C'</Insertion_Order>',LENGTH=74),/,                               
     07:X_INSD,/,                                                               
     07:X_SPAC,/,                                                               
     07:D_AXG$INSD,JFY=(SHIFT=LEFT,                                             
        LEAD=C'<Insertion_Date>',                                               
        TRAIL=C'</Insertion_Date>',LENGTH=74),/,                                
     07:D_AXG$SPAC,JFY=(SHIFT=LEFT,                                             
        LEAD=C'<Space_Description>',                                            
        TRAIL=C'</Space_Description>',LENGTH=74),/,                             
     07:D_AXG$NSPT,JFY=(SHIFT=LEFT,                                             
        LEAD=C'<Number_of_Spots>',                                              
        TRAIL=C'</Number_of_Spots>',LENGTH=74),/,                               
     07:C'<Local_Currency/>'/,                                                  
     07:D_AXG$PRD_PRD,JFY=(SHIFT=LEFT,                                          
        LEAD=C'<Product>',TRAIL=C'</Product>',LENGTH=74),/,                     
     07:D_AXG$PRNM,JFY=(SHIFT=LEFT,                                             
        LEAD=C'<Product_Name>',TRAIL=C'</Product_Name>',LENGTH=74),/,           
     07:C'<Brand/>'/,                                                           
     07:D_AXG$MEDA,JFY=(SHIFT=LEFT,LEAD=C'<Media_Authorization>',               
        TRAIL=C'</Media_Authorization>',LENGTH=74),/,                           
     07:D_AXG$ESTU,JFY=(SHIFT=LEFT,LEAD=C'<Estimate_Unique_Num>',               
        TRAIL=C'</Estimate_Unique_Num>',LENGTH=60),/,                           
     07:D_AXG$BUYU,JFY=(SHIFT=LEFT,LEAD=C'<Buy_Unique_Num>',                    
        TRAIL=C'</Buy_Unique_Num>',LENGTH=100),/,                               
     05:C'</InvoiceItem>',80:X),                                                
 HEADER1=('<?xml version="1.0" encoding="UTF-8" ?>',/,                          
          '<n1:MediaClientBilling xmlns:',                                      
          'n1="urn:publicis.com:0055:PUBLICIS_I_PUR_MEDIA1.0">'),               
 SECTIONS=(D_AXG$SYS,                                                           
           D_AXG$MED,                                                           
           D_AXG$CLT,                                                           
           D_AXG$PRD_PRD,                                                       
           D_AXG$EST,                                                           
           D_AXG$INV,                                                           
       HEADER3=(03:'<InvoiceHeader>',/,                                         
                05:X_MEDIA_SYSTEM,/,                                            
                05:X_AGY_ALPHA,/,                                               
                05:X_VOUCHER,/,                                                 
                05:X_CUSTOMER,/,                                                
                05:X_INVOICE_NO,/,                                              
                05:X_COMPANY_CODE,/,                                            
                05:X_TOTGRSAMT,/,                                               
                05:X_TOTNETAMT,/,                                               
                05:X_TOTCOMAMT,/,                                               
                05:X_TOTTAXAMT,/,                                               
                05:X_CURRENCY,/,                                                
                05:'<Exchange_Rate/>',/,                                        
                05:X_INVOICE_DATE,/,                                            
                05:X_DUE_DATE,/,                                                
                05:X_TRNSTYP,/,                                                 
                05:'<Alternate_Payer/>',/,                                      
                05:'<Partner_Bank_Type/>',/,                                    
                05:X_MEDIA_CLT,/,                                               
                05:X_CLNAM,/,                                                   
                05:X_MEDIA_PRD,/,                                               
                05:X_PRNAM,/,                                                   
                05:X_MEDIA_CMPG,/,                                              
                05:'<ASBOF_BASBOF/>',/,                                         
                05:'<Campaign_Ref/>',/,                                         
                05:'<Discount/>',/,                                             
                05:'<Accrual/>',/,                                              
                05:'<Header_Text/>',/,                                          
                05:X_POSTING_DATE,/,                                            
                05:X_BILLING_PRD,/,                                             
                05:'<User_Id/>',/,                                              
                05:X_PONUM,/,                                                   
                05:X_PUD1,/,                                                    
                05:X_PUD2,/,                                                    
                05:X_EUD1,/,                                                    
                05:X_EUD2,/,                                                    
                05:X_LOCATION,/,                                                
                05:X_LOCCOD),                                                   
       TRAILER3=(03:'</InvoiceHeader>')),                                       
 TRAILER1=('</n1:MediaClientBilling>')                                          
                                                                                
                                                                                
./ ADD NAME=CPTXCNTL                                                            
*                                                                               
* Generate the final XML dataset. Simplify all XML tags that contain            
* empty fields.                                                                 
*                                                                               
 OUTFIL FINDREP=(INOUT=(C'<MOS></MOS>',C'<MOS/>',                               
      C'<Media_System></Media_System>',C'<Media_System/>',                      
      C'<Invoice_No></Invoice_No>',C'<Invoice_No/>',                            
      C'<Company_Code></Company_Code>',C'<Company_Code/>',                      
      C'<Gross_Amount></Gross_Amount>',C'<Gross_Amount/>',                      
      C'<Currency></Currency>',C'<Currency/>',                                  
      C'<Voucher></Voucher>',C'<Voucher/>',                                     
      C'<Exchange_Rate></Exchange_Rate>',C'<Exchange_Rate/>',                   
      C'<Invoice_Date></Invoice_Date>',C'<Invoice_Date/>',                      
      C'<Due_Date></Due_Date>',C'<Due_Date/>',                                  
      C'<Trans_Type></Trans_Type>',C'<Trans_Type/>',                            
      C'<Alternate_Payer></Alternate_Payer>',C'<Alternate_Payer/>',             
      C'<Media_Client></Media_Client>',C'<Media_Client/>',                      
      C'<Media_Product></Media_Product>',C'<Media_Product/>',                   
      C'<ASBOF_BASBOF></ASBOF_BASBOF>',C'<ASBOF_BASBOF/>',                      
      C'<Campaign_Ref></Campaign_Ref>',C'<Campaign_Ref/>',                      
      C'<Discount></Discount>',C'<Discount/>',                                  
      C'<Accrual></Accrual>',C'<Accrual/>',                                     
      C'<Mandat_Flag></Mandat_Flag>',C'<Mandat_Flag/>',                         
      C'<Header_Text></Header_Text>',C'<Header_Text/>',                         
      C'<Posting_Date></Posting_Date>',C'<Posting_Date/>',                      
      C'<Billing_Product></Billing_Product>',C'<Billing_Product/>',             
      C'<User_Id></User_Id>',C'<User_Id/>',                                     
      C'<Item_No></Item_No>',C'<Item_No/>',                                     
      C'<Net_Amount></Net_Amount>',C'<Net_Amount/>',                            
      C'<Commission_Amount></Commission_Amount>',                               
      C'<Commission_Amount/>',                                                  
      C'<Tax_Code></Tax_Code>',C'<Tax_Code/>',                                  
      C'<Tax_Amount></Tax_Amount>',C'<Tax_Amount/>',                            
      C'<Vendor></Vendor>',C'<Vendor/>',                                        
      C'<MOS></MOS>',C'<MOS/>',                                                 
      C'<Media_Type></Media_Type>',C'<Media_Type/>',                            
      C'<Submedia></Submedia>',C'<Submedia/>',                                  
      C'<Item_Text></Item_Text>',C'<Item_Text/>',                               
      C'<Media_Campaign></Media_Campaign>',C'<Media_Campaign/>',                
      C'<Trans_Type></Trans_Type>',C'<Trans_Type/>',                            
      C'<Network></Network>',C'<Network/>',                                     
      C'<Estimate_No></Estimate_No>',C'<Estimate_No/>',                         
      C'<Estimate_No_Name></Estimate_No_Name>',                                 
      C'<Estimate_No_Name/>',                                                   
      C'<Insertion_Order></Insertion_Order>',C'<Insertion_Order/>',             
      C'<Number_of_Spots></Number_of_Spots>',C'<Number_of_Spots/>',             
      C'<Product></Product>',C'<Product/>',                                     
      C'<Location></Location>',C'<Location/>',                                  
      C'<Media_Authorization></Media_Authorization>',                           
      C'<Media_Authorization/>',                                                
      C'<Market_Code></Market_Code>',C'<Market_Code/>',                         
      C'<Market_Name></Market_Name>',C'<Market_Name/>',                         
      C'<Estimate_Unique_Num></Estimate_Unique_Num>',                           
      C'<Estimate_Unique_Num/>',                                                
      C'<Purchase_Order_Number></Purchase_Order_Number>',                       
      C'<Purchase_Order_Number/>',                                              
      C'<Media_Client_Name></Media_Client_Name>',                               
      C'<Media_Client_Name/>',                                                  
      C'<Media_Product_Name></Media_Product_Name>',                             
      C'<Media_Product_Name/>',                                                 
      C'<Product_Name></Product_Name>',C'<Product_Name/>',                      
      C'<UDEF_Product_1></UDEF_Product_1>',C'<UDEF_Product_1/>',                
      C'<UDEF_Product_2></UDEF_Product_2>',C'<UDEF_Product_2/>',                
      C'<UDEF_Estimate_1></UDEF_Estimate_1>',C'<UDEF_Estimate_1/>',             
      C'<UDEF_Estimate_2></UDEF_Estimate_2>',C'<UDEF_Estimate_2/>',             
      C'<Media_Estimate_Name></Media_Estimate_Name>',                           
      C'<Media_Estimate_Name/>',                                                
      C'<Buy_Unique_Num></Buy_Unique_Num>',C'<Buy_Unique_Num/>'))               
                                                                                
