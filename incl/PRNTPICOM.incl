*          DATA SET PRNTPICOM  AT LEVEL 002 AS OF 05/01/02                      
         TITLE 'COLLECT 68 ELEMENTS               '                             
PP12468  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,PP12468                                                        
         SPACE 2                                                                
** CALLING PGM      WORD 1   BYTE 0   ELEMENT TYPE                              
*                               1-3   ADDRESS OF BUY RECORD                     
*                        2   BYTE 0   WIDTH OF COMMENT TO BE PRINTED            
*                                     MAX 80 BYTES                              
*                               1-3                                             
******                                                                          
** ON RETURN         WORD 1   BYTE 0  NUMBER OF LINES                           
*                                1-3  ADDRESS OF TABLE OF COMMENT               
*                         2   BYTE 0                                            
*                                                                               
*                                                                               
*                                                                               
******                                                                          
         USING PPWORKD,RA                                                       
         USING PP12WRKD,R8                                                      
         USING PPFILED,RC,R9                                                    
         LR    RC,R9           INITIALIZE RC                                    
         S     RC,=F'1000'                                                      
         SPACE 2                                                                
         MVC   PARMS(8),0(R1)                                                   
         ST    R1,SAVER1        SAVE POINTER                                    
         LA    R4,BIGTABLE                                                      
         XC    0(35,R4),0(R4)                                                   
         ST    R4,ANXTCOM       ADDRESS OF NEXT COMMENT                         
         CLI   PARMS+4,X'80'                                                    
         BNH   *+8              MUST BE 80 OR LESS                              
         MVI   PARMS+4,X'80'                                                    
*                                                                               
*                                                                               
BC2      DS    0H                                                               
         LA    R2,PBUYREC+33                                                    
         MVI   ELCODE,X'68'                                                     
*                                                                               
BC4      BAS   RE,NXTEL                                                         
         BNE   BC20                                                             
         CLI   1(R2),2                                                          
         BE    BC4                                                              
*                                                                               
*    CAN GET TO BC5 FROM BC22                                                   
*                                                                               
BC5      MVI   THISBUY+34,C'C'     CAN'T USE DITTOS                             
         CLC   2(4,R2),=C'COM='                                                 
         BE    BC8                 STANDARD COMMENT                             
         L     R4,ANXTCOM                                                       
         XC    0(35,R4),0(R4)                                                   
         ZIC   R5,1(R2)                                                         
         SH    R5,=H'3'                                                         
         EX    R5,*+8                                                           
         B     *+10                                                             
*                                                                               
         MVC   0(0,R4),2(R2)                                                    
*                                                                               
         LA    R4,35(R4)                                                        
         ST    R4,ANXTCOM                                                       
         B     BC4                                                              
*                                                                               
*                                                                               
BC8      DS    0H                                                               
         MVC   SVELCOD,ELCODE      SAVE ELCODE                                  
         ST    R2,SAVER2           SAVE BUYREC'S R2                             
         MVC   SVBUYKEY,KEY                                                     
         XC    KEY,KEY                                                          
         MVC   KEY(3),QAGENCY                                                   
         MVI   KEY+3,X'40'                                                      
         ZIC   R5,1(R2)                                                         
         SH    R5,=H'6'                                                         
         LTR   R5,R5                                                            
         BZ    BC8X                NO NUMBER                                    
         MVC   WORK(10),SPACES                                                  
         LA    R7,WORK+6                                                        
         SR    R7,R5                                                            
         BCTR  R5,0                                                             
         EX    R5,*+8                                                           
         B     *+10                                                             
*                                                                               
         MVC   0(0,R7),6(R2)                                                    
*                                                                               
         MVC   KEY+4(6),WORK                                                    
         GOTO1 DATAMGR,DMCB,DMREAD,PRTDIR,KEY,KEY                               
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                STND COMMENT NOT FOUND                       
         GOTO1 (RF),(R1),GETREC,PRTFILE,KEY+27,ACOMREC,DMWORK                   
         L     R2,ACOMREC                                                       
         LA    R2,33(R2)                                                        
         MVI   ELCODE,X'40'                                                     
         CLI   0(R2),X'40'                                                      
         BE    BC8D                                                             
*                                                                               
BC8C     BAS   RE,NXTEL                                                         
         BNE   BC8X                                                             
*                                                                               
BC8D     DS    0H                                                               
         CLC   2(5,R2),=C'SHIP='                                                
         BE    BC8C                                                             
         CLC   2(6,R2),=C'LABEL='                                               
         BE    BC8C                                                             
         CLC   2(4,R2),=C'MAT='                                                 
         BE    BC8C                                                             
         CLC   2(3,R2),=C'RC='                                                  
         BE    BC8C                                                             
         LA    R4,2(R2)                                                         
         LR    R6,R4                                                            
         ZIC   R5,1(R2)                                                         
         SH    R5,=H'3'                                                         
         BM    BC8C                                                             
*                                                                               
         CLI   0(R4),C'+'                                                       
         BNE   *+8                                                              
         LA    R6,2(R6)                                                         
         SR    RF,RF                                                            
         CLC   0(3,R6),=C'DC='     ONLY DC= COMMNTS                             
         BNE   *+8                 ON CANCELS                                   
         LA    RF,1                                                             
         CLI   TSCHGSW-STATDIF,C'D'                                             
         BNE   *+6                                                              
         BCTR  RF,R0                                                            
         LTR   RF,RF                                                            
         BNZ   BC8C                                                             
*                                                                               
         CLI   0(R4),C'+'                                                       
         BNE   BC8F                                                             
         CLI   1(R4),C'1'          ONLY PACK IF NUMERIC                         
         BL    BC8F                ELSE TREAT AS REGULAR LINE                   
         PACK  DUB,1(1,R4)                                                      
         CVB   R7,DUB                                                           
         L     R3,ANXTCOM                                                       
BC8E     XC    0(35,R3),0(R3)      CAN'T USE SPACES                             
         LA    R3,35(R3)                                                        
         BCT   R7,BC8E                                                          
         ST    R3,ANXTCOM                                                       
         LA    R4,2(R4)                                                         
         SH    R5,=H'2'                                                         
*                                                                               
BC8F     DS    0H                                                               
         LTR   R5,R5                                                            
         BM    BC8C                                                             
         MVC   SAVEP,SPACES                                                     
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   SAVEP(0),0(R4)                                                   
         L     R3,ANXTCOM                                                       
         MVC   0(35,R3),SAVEP                                                   
         LA    R3,35(R3)                                                        
         CLC   SAVEP+35(35),SPACES                                              
         BE    BC8H                                                             
         SH    R3,=H'35'           BACK UP R3                                   
         LA    R5,1(R5)            ADJUST R5 FOR CHOPPER                        
         L     RF,ACHOPPER                                                      
         GOTO1 (RF),DMCB,((R5),SAVEP),(35,0(R3)),2                              
         OC    DMCB+8(4),DMCB+8                                                 
         BNE   *+6                                                              
         DC    H'0'                CHOPPER ERROR                                
         LA    R3,35(R3)                                                        
         CLC   0(35,R3),SPACES     SEE IF I USED 2 LINES                        
         BE    BC8H                NO                                           
         LA    R3,35(R3)           YES                                          
BC8H     ST    R3,ANXTCOM                                                       
         MVC   SAVEP,SPACES                                                     
         B     BC8C                                                             
*                                                                               
BC8X     MVC   ELCODE,SVELCOD      RESTORE ELCODE                               
         MVC   KEY,SVBUYKEY                                                     
         L     R2,SAVER2           RESTORE BUYREC'S R2                          
         B     BC4                                                              
*                                                                               
BC20     CLI   ELCODE,X'68'      SEE IF I JUST LOOKED FOR POSITION INS          
         BE    BCX               YES - THEN DONE                                
         LA    R2,PBUYREC+33                                                    
         MVI   ELCODE,X'68'                                                     
*                                                                               
BC22     BAS   RE,NXTEL                                                         
         BNE   BCX                 NONE FOUND                                   
         CLI   1(R2),2             SKIP IF NO COMMENT                           
         BE    BC22                                                             
         L     R3,ANXTCOM                                                       
         XC    0(35,R3),0(R3)      SKIP A LINE                                  
         LA    R3,35(R3)                                                        
         XC    0(35,R3),0(R3)                                                   
         MVC   0(29,R3),=C'*** POSITION INSTRUCTIONS ***'                       
         LA    R3,35(R3)                                                        
         ST    R3,ANXTCOM                                                       
         B     BC5                                                              
*                                                                               
BCX      L     R3,ANXTCOM                                                       
         MVI   0(R3),X'FF'         SET END OF TABLE                             
         L     R3,ACOMTAB                                                       
         ST    R3,ANXTCOM          INITIALIZE ANXTCOM TO FIRST                  
         XIT1                                                                   
*                                                                               
NXTEL    DS    0H                                                               
         SR    R0,R0                                                            
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         CLI   0(R2),0                                                          
         BE    NXTELX                                                           
         CLC   ELCODE,0(R2)                                                     
         BER   RE                                                               
         B     NXTEL+2                                                          
NXTELX   LTR   R2,R2                                                            
         BR    RE                                                               
         SPACE 3                                                                
SVBUYKEY DS    CL32                                                             
SAVER2   DS    F                                                                
R1SAVE   DS    F                                                                
ANXTCOM  DS    A                                                                
SVELCOD  DS    CL1                                                              
*                                                                               
         SPACE 3                                                                
         EJECT                                                                  
         LTORG                                                                  
BIGTABLE DS    600C                                                             
         SPACE 3                                                                
*                                                                               
*                                                                               
*                                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002PRNTPICOM 05/01/02'                                      
         END                                                                    
