*          DATA SET TATRNTR    AT LEVEL 002 AS OF 04/19/12                      
***********************************************************************         
*        ROUTINE BUILDS TRADITIONAL VERSION ELEMENT (TAFNTVER)        *         
*        BASED ON TATRELQ ELEMENTS                                    *         
*        ON ENTRY ... AIO = A(CAST RECORD)                            *         
***********************************************************************         
                                                                                
TRNTATR  NTR1  BASE=*,LABEL=*                                                   
         USING TATRD,R4                                                         
         L     R4,AIO                                                           
         MVI   ELCODE,TATRELQ    IF PERFORMER HAS ANY TRACK                     
         BRAS  RE,GETEL          ASSOCIATIONS ...                               
         JNE   NO                                                               
                                                                                
         USING TATRTABD,R2                                                      
         LA    R2,BLOCK          CLEAR BLOCK                                    
         LHI   R3,GLOBKEY-BLOCK                                                 
         XR    RE,RE                                                            
         XR    RF,RF                                                            
         MVCL  R2,RE                                                            
                                                                                
         LA    R2,BLOCK                                                         
                                                                                
TTATR10  MVC   TTCOM,TATRCOM     SAVE ALL TRACK ASSOCIATIONS                    
         MVC   TTTRK,TATRTRK     INTO TABLE                                     
         LA    R2,TTLNQ(R2)                                                     
                                                                                
         BRAS  RE,NEXTEL                                                        
         JE    TTATR10                                                          
         MVI   0(R2),X'FF'                                                      
         DROP  R2,R4                                                            
                                                                                
         USING TLCAD,R4                                                         
         L     R4,AIO                                                           
         L     R0,TLCACOM                                                       
         DROP  R4                                                               
                                                                                
         USING TATRTABD,R2                                                      
         LA    R2,BLOCK          FOR EACH TRACK IN THE TABLE ...                
         MVC   TGBYTE3,DMINBTS                                                  
         NI    DMINBTS,X'F7'                                                    
                                                                                
         USING TLCOPD,R3                                                        
         LA    R3,KEY                                                           
TTATR20  XC    KEY,KEY                                                          
         MVI   TLCOPCD,TLCOTCDQ                                                 
         MVC   TLCOTMCO,TTCOM                                                   
         MVC   TLCOTTRK,TTTRK                                                   
         STCM  R0,15,TLCOTCOM                                                   
         GOTO1 HIGH                                                             
         J     TTATR40                                                          
TTATR30  GOTO1 SEQ                                                              
TTATR40  CLC   KEY(TLCOTVER-TLCOPD),KEYSAVE                                     
         JNE   TTATR50                                                          
                                                                                
         LA    RE,BLOCK+TABLELNQ                                                
         ZIC   RF,TLCOTVER                                                      
         SHI   RF,1                                                             
         AR    RE,RF                                                            
         MVI   0(RE),C'X'                                                       
         J     TTATR30                                                          
                                                                                
TTATR50  LA    R2,TTLNQ(R2)                                                     
         CLI   0(R2),X'FF'                                                      
         JNE   TTATR20                                                          
         DROP  R2,R3                                                            
                                                                                
         MVC   DMINBTS,TGBYTE3                                                  
                                                                                
         USING TAFND,R4                                                         
         LA    R4,ELEMENT                                                       
         XC    ELEMENT,ELEMENT                                                  
         MVI   TAFNEL,TAFNELQ                                                   
         LHI   R0,TAFNLNQ                                                       
         MVI   TAFNTYPE,TAFNTVER                                                
         LA    R2,TAFNNAME                                                      
                                                                                
         LHI   RE,1                                                             
         LA    RF,BLOCK+TABLELNQ                                                
         MVI   250(RF),X'FF'                                                    
                                                                                
TTATR60  CLI   0(RF),C'X'                                                       
         JNE   TTATR70                                                          
         STC   RE,0(R2)                                                         
         LA    R2,1(R2)                                                         
         AHI   R0,1                                                             
                                                                                
TTATR70  AHI   RE,1                                                             
         LA    RF,1(RF)                                                         
         CLI   0(RF),X'FF'                                                      
         JNE   TTATR60                                                          
                                                                                
         CHI   R0,TAFNLNQ                                                       
         JE    NO                                                               
         STC   R0,TAFNLEN                                                       
         GOTO1 ADDELEM                                                          
         J     YES                                                              
         DROP  R4                                                               
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
TABLELNQ EQU   (7*TTLNQ)+1                                                      
                                                                                
         LTORG                                                                  
         EJECT                                                                  
