*          DATA SET TACVAV     AT LEVEL 001 AS OF 09/18/13                      
***********************************************************************         
*        ROUTINE ASSERTS ALL PROVIDED VALUES (COMMON BETWEEN          *         
*        ASSETS AND VERSIONS) ARE VALID                               *         
***********************************************************************         
                                                                                
ACVAV    NTR1                                                                   
         MVI   BYTE1,D#COTYP       VALIDATE COMMERCIAL TYPE                     
         GOTOR (#VALFLD,AVALFLD),DMCB,('VFCOTY',RQCOTYP)                        
         JNE   AVINV                                                            
         CLI   RQCOTYP,CTYMUS      MUSIC IS NOT ALLOWED AT THIS                 
         JE    AVINV               POINT IN TIME                                
                                                                                
         MVI   BYTE1,D#COMED       VALIDATE MEDIA                               
         GOTOR (#VALFLD,ACHKFLD),DMCB,('VFMED',RQCOMED)                         
         JNE   AVINV                                                            
                                                                                
         CLC   RQCOFST,=C'OT'                                                   
         JE    ACVAV10                                                          
         MVI   BYTE1,D#COFST       VALIDATE FILM STATE                          
         GOTOR (#VALFLD,ACHKFLD),DMCB,('VFSOP',RQCOFST)                         
         JNE   AVINV                                                            
                                                                                
ACVAV10  CLC   RQCORST,=C'OT'                                                   
         JE    ACVAV20                                                          
         MVI   BYTE1,D#CORST       VALIDATE RECORDING STATE                     
         GOTOR (#VALFLD,ACHKFLD),DMCB,('VFSOP',RQCORST)                         
         JNE   AVINV                                                            
                                                                                
ACVAV20  CLC   RQCOMST,=C'OT'                                                   
         JE    ACVAV30                                                          
         MVI   BYTE1,D#COMST       VALIDATE MUSIC STATE                         
         GOTOR (#VALFLD,ACHKFLD),DMCB,('VFSOP',RQCOMST)                         
         JNE   AVINV                                                            
                                                                                
ACVAV30  BAS   RE,AVEDIT           VALIDATE EDIT TYPE YEAR AND TYPE             
         JNE   AVINV                                                            
                                                                                
         BAS   RE,AVTRK            VALIDATE TRACKS                              
         JNE   AVINV                                                            
                                                                                
         BAS   RE,AVMUC            VALIDATE MUSIC CODES                         
         JNE   AVINV                                                            
                                                                                
         MVI   BYTE1,D#COALU                                                    
         CLI   RQCOALU,9           VALIDATE ALLOWABLE USES                      
         JH    AVINV                                                            
                                                                                
         MVI   BYTE1,D#CORMU                                                    
         CLI   RQCORMU,9           VALIDATE REMAINING USES                      
         JH    AVINV                                                            
                                                                                
         MVI   BYTE1,D#COCDO       VALIDATE CANADIAN DOLLARS?                   
         GOTOR (#VALFLD,AVALFLD),DMCB,('VFYORN',RQCOCDO)                        
         JNE   AVINV                                                            
         MVI   BYTE1,D#COCRT       VALIDATE CANADIAN RATES?                     
         GOTOR (#VALFLD,AVALFLD),DMCB,('VFYORN',RQCOCRT)                        
         JNE   AVINV                                                            
         J     XIT                                                              
                                                                                
***********************************************************************         
*        ROUTINE ASSERTS PROVIDED EDIT TYPE YEAR AND TYPE ARE VALID   *         
***********************************************************************         
                                                                                
AVEDIT   NTR1                                                                   
         OC    RQCOEYR,RQCOEYR     IF EDIT TYPE YEAR IS PROVIDED                
         JE    YES                                                              
                                                                                
         MVI   BYTE1,D#COEYR                                                    
         CLC   RQCOEYR,=C'1900'    ASSERT THAT EDIT TYPE YEAR IS                
         JL    NO                  BETWEEN 1900                                 
         CLC   RQCOEYR,=C'2027'    AND 2027                                     
         JH    NO                  THEN TRANSLATE IT                            
         MVC   TEMP(L'RQCOEYR),RQCOEYR                                          
         MVC   TEMP+L'RQCOEYR(4),=C'0101'                                       
         GOTO1 VDATCON,DMCB,(9,TEMP),(1,TEMP2)                                  
         MVC   TRANEYR,TEMP2                                                    
         XI    TRANEYR,X'FF'                                                    
                                                                                
         USING TGTABLES,RE                                                      
         L     RE,VSYSTAB                                                       
         L     RF,TGAEDTYP                                                      
         DROP  RE                                                               
                                                                                
         AR    RF,RE                                                            
         LR    R2,RF                                                            
                                                                                
AE10     CLC   RQCOEYR,0(RF)       ASSERT THAT IT IS IS VALID                   
         JE    AE20                                                             
         CLI   3(RF),X'FF'                                                      
         JE    NO                                                               
         LA    RF,3(RF)                                                         
         J     AE10                                                             
                                                                                
AE20     ZICM  R1,1(RF),2                                                       
         AR    R2,R1                                                            
                                                                                
         MVI   BYTE1,D#COETY                                                    
                                                                                
AE30     CLC   RQCOETY,0(R2)       AND ASSERT THAT PROVIDED EDIT TYPE           
         JE    YES                 IS VALID FOR THAT YEAR                       
         CLI   L'RQCOETY(R2),X'FF'                                              
         JE    NO                                                               
         LA    R2,L'RQCOETY(R2)                                                 
         J     AE30                                                             
                                                                                
***********************************************************************         
*        ROUTINE ASSERTS PROVIDED TRACKS ARE VALID                    *         
***********************************************************************         
                                                                                
AVTRK    NTR1                                                                   
         OC    RQCOA1O(5),RQCOA1O  IF AFM CONTRACT TRACK 1 IS PROVIDED          
         JZ    AT10                ASSERT THAT IT IS NOT DUPLICATED             
         CLC   RQCOA1O(5),RQCOA2O                                               
         JE    AT2INV                                                           
         CLC   RQCOA1O(5),RQCOA3O                                               
         JE    AT3INV                                                           
         CLC   RQCOA1O(5),RQCOA4O                                               
         JE    AT4INV                                                           
         CLC   RQCOA1O(5),RQCOA5O                                               
         JE    AT5INV                                                           
         CLC   RQCOA1O(5),RQCOA6O                                               
         JE    AT6INV                                                           
         CLC   RQCOA1O(5),RQCOA7O                                               
         JE    AT7INV                                                           
                                                                                
AT10     OC    RQCOA2O(5),RQCOA2O  IF AFM CONTRACT TRACK 2 IS PROVIDED          
         JZ    AT20                ASSERT THAT IT IS NOT DUPLICATED             
         CLC   RQCOA2O(5),RQCOA3O                                               
         JE    AT3INV                                                           
         CLC   RQCOA2O(5),RQCOA4O                                               
         JE    AT4INV                                                           
         CLC   RQCOA2O(5),RQCOA5O                                               
         JE    AT5INV                                                           
         CLC   RQCOA2O(5),RQCOA6O                                               
         JE    AT6INV                                                           
         CLC   RQCOA2O(5),RQCOA7O                                               
         JE    AT7INV                                                           
                                                                                
AT20     OC    RQCOA3O(5),RQCOA3O  IF AFM CONTRACT TRACK 3 IS PROVIDED          
         JZ    AT30                ASSERT THAT IT IS NOT DUPLICATED             
         CLC   RQCOA3O(5),RQCOA4O                                               
         JE    AT4INV                                                           
         CLC   RQCOA3O(5),RQCOA5O                                               
         JE    AT5INV                                                           
         CLC   RQCOA3O(5),RQCOA6O                                               
         JE    AT6INV                                                           
         CLC   RQCOA3O(5),RQCOA7O                                               
         JE    AT7INV                                                           
                                                                                
AT30     OC    RQCOA4O(5),RQCOA4O  IF AFM CONTRACT TRACK 4 IS PROVIDED          
         JZ    AT40                ASSERT THAT IT IS NOT DUPLICATED             
         CLC   RQCOA4O(5),RQCOA5O                                               
         JE    AT5INV                                                           
         CLC   RQCOA4O(5),RQCOA6O                                               
         JE    AT6INV                                                           
         CLC   RQCOA4O(5),RQCOA7O                                               
         JE    AT7INV                                                           
                                                                                
AT40     OC    RQCOA5O(5),RQCOA5O  IF AFM CONTRACT TRACK 5 IS PROVIDED          
         JZ    AT50                ASSERT THAT IT IS NOT DUPLICATED             
         CLC   RQCOA5O(5),RQCOA6O                                               
         JE    AT6INV                                                           
         CLC   RQCOA5O(5),RQCOA7O                                               
         JE    AT7INV                                                           
                                                                                
AT50     OC    RQCOA6O(5),RQCOA6O  IF AFM CONTRACT TRACK 6 IS PROVIDED          
         JZ    YES                 ASSERT THAT IT IS NOT DUPLICATED             
         CLC   RQCOA6O(5),RQCOA7O                                               
         JE    AT7INV                                                           
         J     YES                                                              
                                                                                
AT2INV   MVI   BYTE1,D#COA2T                                                    
         J     NO                                                               
                                                                                
AT3INV   MVI   BYTE1,D#COA3T                                                    
         J     NO                                                               
                                                                                
AT4INV   MVI   BYTE1,D#COA4T                                                    
         J     NO                                                               
                                                                                
AT5INV   MVI   BYTE1,D#COA5T                                                    
         J     NO                                                               
                                                                                
AT6INV   MVI   BYTE1,D#COA6T                                                    
         J     NO                                                               
                                                                                
AT7INV   MVI   BYTE1,D#COA7T                                                    
         J     NO                                                               
                                                                                
***********************************************************************         
*        ROUTINE ASSERTS PROVIDED MUSIC CODES ARE VALID               *         
***********************************************************************         
                                                                                
AVMUC    NTR1                                                                   
         OC    RQCOMU1,RQCOMU1     IF MUSIC CODE 1 IS PROVIDED                  
         JZ    AM10                ASSERT THAT IT IS NOT DUPLICATED             
         CLC   RQCOMU1,RQCOMU2                                                  
         JE    AM2INV                                                           
         CLC   RQCOMU1,RQCOMU3                                                  
         JE    AM3INV                                                           
         CLC   RQCOMU1,RQCOMU4                                                  
         JE    AM4INV                                                           
                                                                                
AM10     OC    RQCOMU2,RQCOMU2     IF MUSIC CODE 2 IS PROVIDED                  
         JZ    AM20                ASSERT THAT IT IS NOT DUPLICATED             
         CLC   RQCOMU1,RQCOMU3                                                  
         JE    AM3INV                                                           
         CLC   RQCOMU1,RQCOMU4                                                  
         JE    AM4INV                                                           
                                                                                
AM20     OC    RQCOMU3,RQCOMU3     IF MUSIC CODE 3 IS PROVIDED                  
         JZ    YES                 ASSERT THAT IT IS NOT DUPLICATED             
         CLC   RQCOMU1,RQCOMU4                                                  
         JE    AM4INV                                                           
         J     YES                                                              
                                                                                
AM2INV   MVI   BYTE1,D#COMU2                                                    
         J     NO                                                               
                                                                                
AM3INV   MVI   BYTE1,D#COMU3                                                    
         J     NO                                                               
                                                                                
AM4INV   MVI   BYTE1,D#COMU4                                                    
         J     NO                                                               
