*          DATA SET TAADDEAR   AT LEVEL 001 AS OF 06/05/15                      
***********************************************************************         
*        ROUTINE ADDS ERROR DETAILS TO ERROR TABLE AND RECORDS        *         
*        SEQUENCE NUMBER OF THE CAST MEMBER THAT THE ERROR APPLIES TO *         
*        ON ENTRY ... P1 = A(CERNO ERROR ENTRY)                       *         
*                     P2 = A(NON-CERNO ERROR ENTRY)                   *         
*                     P3 = A(CAST ENTRY IN WEB RESPONSE DETAILS AREA) *         
***********************************************************************         
                                                                                
ADDEAP   NTR1  BASE=*,LABEL=*                                                   
         USING ERRENTD,R2                                                       
         L     R2,0(R1)            IF PAYMENT COMING FROM CERNO                 
         TM    TGFASTAT,TGCRNOPY   IMMEDIATELY JUMP AHEAD TO ADDING             
         JO    AEAP30              ERROR TO THE TABLE                           
                                                                                
         L     R2,4(R1)            R2 = A(NON-CERNO ERROR ENTRY)                
                                                                                
         USING WEBRESD,R3                                                       
         L     R3,8(R1)            SET "PERFORMER NOT ELIGIBLE FOR              
         OI    WRSSTAT,WRSSNERE    PAYMENT - REASON IDENTIFIED" STATUS          
                                                                                
         XR    R0,R0                                                            
                                                                                
         USING EATENTD,R4                                                       
         L     R4,TGAEATAB         FIND FIRST EMPTY SLOT IN "ERROR              
AEAP10   CLI   0(R4),X'FF'         APPLIES TO ..." TABLE                        
         JE    AEAP20                                                           
         CLC   EANUMB,EENUMB                                                    
         JNE   *+8                                                              
         AHI   R0,1                                                             
         LA    R4,EALNQ(R4)                                                     
         J     AEAP10                                                           
                                                                                
AEAP20   MVI   EASTAT,EASCASQ                                                   
         MVC   EANUMB,EENUMB       ADD ERROR NUMBER                             
         MVC   EAAPPSTO,WRSSEQ     AND CAST SEQUENCE NUMBER TO                  
         MVI   EALNQ(R4),X'FF'     "ERROR APPLIES TO" TABLE                     
         DROP  R3,R4                                                            
                                                                                
         LTR   R0,R0               IF THIS IS THE FIRST TIME ERROR              
         JNZ   XIT                 HAS BEEN ENCOUNTERED, ADD IT TO              
AEAP30   GOTO1 ADDERROR,DMCB,ERRENTD                   ERROR TABLE              
         J     XIT                                                              
         DROP  R2                                                               
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        ROUTINE ADDS ERROR DETAILS TO ERROR TABLE AND RECORDS        *         
*        UNIQUE IDENTIFIER OF THE RECORD THAT THE ERROR APPLIES TO    *         
*        ON ENTRY ... P1 = A(ERROR ENTRY)                             *         
*                     P2 = A(UNIQUE IDENTIFIER OF RECORD)             *         
***********************************************************************         
                                                                                
ADDEAR   NTR1  BASE=*,LABEL=*                                                   
         USING ERRENTD,R2                                                       
         L     R2,0(R1)            R2=A(ERROR ENTRY)                            
         L     R3,4(R1)            R3=A(UNIQUE IDENTIFIER OF RECORD)            
                                                                                
         XR    R0,R0                                                            
                                                                                
         USING EATENTD,R4                                                       
         L     R4,TGAEATAB         FIND FIRST EMPTY SLOT IN "ERROR              
AEAR10   CLI   0(R4),X'FF'         APPLIES TO ..." TABLE                        
         JE    AEAR20                                                           
         CLC   EANUMB,EENUMB                                                    
         JNE   *+8                                                              
         AHI   R0,1                                                             
         LA    R4,EALNQ(R4)                                                     
         J     AEAR10                                                           
                                                                                
AEAR20   MVI   EASTAT,EASUQID                                                   
         MVC   EANUMB,EENUMB       ADD ERROR NUMBER                             
         XC    EAAPPSTO,EAAPPSTO                                                
         MVC   EAAPPSTO+1(1),0(R3) AND UNIQUE IDENTIFIER TO                     
         MVI   EALNQ(R4),X'FF'     "ERROR APPLIES TO" TABLE                     
         DROP  R4                                                               
                                                                                
         LTR   R0,R0               IF THIS IS THE FIRST TIME ERROR              
         JNZ   XIT                 HAS BEEN ENCOUNTERED, ADD IT TO              
         GOTO1 ADDERROR,DMCB,ERRENTD                   ERROR TABLE              
         J     XIT                                                              
         DROP  R2                                                               
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
