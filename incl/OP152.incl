*          DATA SET OP152      AT LEVEL 002 AS OF 05/01/02                      
         TITLE 'TAPE LIBRARY MASTER LISTING'                                    
TLML     START                                                                  
R00      EQU   0                                                                
R01      EQU   1                                                                
R02      EQU   2                                                                
R03      EQU   3                                                                
R04      EQU   4                                                                
R05      EQU   5                                                                
R06      EQU   6                                                                
R07      EQU   7                                                                
R08      EQU   8                                                                
R09      EQU   9                                                                
R10      EQU   10                                                               
R11      EQU   11                                                               
R12      EQU   12                                                               
R13      EQU   13                                                               
R14      EQU   14                                                               
R15      EQU   15                                                               
         ENTRY START                                                            
*                                                                               
IN       DTFMT DEVADDR=SYS011,IOAREA1=T1,IOAREA2=T2,FILABL=STD,        X        
               EOFADDR=TEND,RECFORM=FIXBLK,TYPEFLE=INPUT,              X        
               BLKSIZE=800,RECSIZE=80,WORKA=YES                                 
*                                                                               
PRINT    DTFPR BLKSIZE=132,CONTROL=YES,DEVADDR=SYSLST,IOAREA1=P1,      X        
               PRINTOV=YES,RECFORM=FIXUNB,WORKA=YES                             
*                                                                               
T1       DS    800C                                                             
T2       DS    800C                                                             
P1       DS    132C                                                             
*                                                                               
START    BASR  R11,0                                                            
         USING *,R11                                                            
         OPEN  IN                                                               
         COMRG                                                                  
         MVC   DATE(2),0(R01)                                                   
         MVC   DATE+2(2),3(R01)    BUILD DATE                                   
         MVC   DATE+4(2),6(R01)                                                 
*                                                                               
         XC    PL(132),PL                                                       
         BAS   R09,HDL            DO HEADLINES                                  
         LA    R02,CNTRS           POINT TO RECORD COUNTERS                     
         LA    R03,PURGES          POINY TO PURGE CNTRS                         
TL       BAS   R10,RIN             READ INPUT                                   
*                                                                               
         CLC   TNUM(1),LAST        TEST FOR BREAK                               
         BNE   TL3                 YES                                          
*                                                                               
TLA      CLC   TRUN(7),=C'0000000'                                              
         BE    TL1                 YES                                          
         L     R14,4(R02)          NO. BUMP SAVED CNTR                          
         LA    R14,1(R14)                                                       
         ST    R14,4(R02)                                                       
         B     TL2                                                              
TL1      L     R14,0(R02)          BUMP SCRATCH COUNTER                         
         LA    R14,1(R14)                                                       
         ST    R14,0(R02)                                                       
TL2      BAS   R10,FPRT            FORMAT PRINT LINE                            
         BAS   R09,PT              PRINT IT                                     
         BAS   R09,SKIP1                                                        
         LH    R14,LCNT                                                         
         LA    R14,1(R14)          BUMP LINE CNT FOR SKIP                       
         STH   R14,LCNT                                                         
         B     TL                                                               
*                                                                               
TL3      BAS   R09,SKIP2           SKIP 2 LINES                                 
         L     R14,0(R02)                                                       
         BAS   R10,CVD                                                          
         MVC   PL+9(5),CNFD+3                                                   
         MVC   PL(7),=C'SCRATCH'                                                
         BAS   R09,PT              PRINT SCRATCH TOTAL                          
         MVC   PL(5),=C'SAVED'                                                  
         L     R14,4(R02)                                                       
         BAS   R10,CVD                                                          
         MVC   PL+9(5),CNFD+3                                                   
         BAS   R09,PT              PRINT SAVED TOTAL                            
         L     R14,0(R02)          PRINT TOTAL                                  
         A     R14,4(R02)                                                       
         BAS   R10,CVD                                                          
         MVC   PL+9(5),CNFD+3                                                   
         MVC   PL(5),=C'TOTAL'                                                  
         BAS   R09,PT                                                           
         BAS   R09,SKIP1                                                        
         L     R14,0(R03)          PRINT PUGE                                   
         BAS   R10,CVD                                                          
         MVC   PL+9(5),CNFD+3                                                   
         MVC   PL(5),=C'PURGE'                                                  
         BAS   R09,PT                                                           
*                                                                               
         MVC   LAST(1),TNUM        SET NEW TAPE ALPHA                           
         LA    R02,8(R02)          BUMP TO NEXT COUNTERS                        
         LA    R03,4(R03)          BUMP TO NEXT PURGE CNTR                      
         BAS   R09,HDL             NEW HEADLINE                                 
         CLI   EOF,X'FF'           EOF                                          
         BNE   TLA                 NO.                                          
*                                                                               
* END OF FILE                                                                   
*                                                                               
         CLOSE IN                                                               
         BAS   R09,SKIP2           SKUP 2 LINES                                 
         LA    R02,CNTRS           POINT TO COUNTERS                            
*                                                                               
         MVC   PL+9(L'LHDL),LHDL   MOVE TAPE LENGTH HDL                         
         BAS   R09,PT                                                           
         MVC   PL(7),=C'SCRATCH'                                                
         L     R14,0(R02)          MOVE SCRATCH SUB TOTALS                      
         BAS   R10,CVD                                                          
         MVC   PL+9(5),CNFD+3                                                   
         L     R14,8(R02)                                                       
         BAS   R10,CVD                                                          
         MVC   PL+17(5),CNFD+3                                                  
         L     R14,16(R02)                                                      
         BAS   R10,CVD                                                          
         MVC   PL+25(5),CNFD+3                                                  
         L     R14,24(R02)                                                      
         BAS   R10,CVD                                                          
         MVC   PL+33(5),CNFD+3                                                  
         A     R14,0(R02)          ADD ALL SCRATCHES                            
         A     R14,8(R02)                                                       
         A     R14,16(R02)                                                      
         BAS   R10,CVD                                                          
         MVC   PL+41(5),CNFD+3                                                  
         BAS   R09,PT              PRINT SCRATCH LINE                           
*                                                                               
         MVC   PL(5),=C'SAVED'                                                  
         L     R14,4(R02)                                                       
         BAS   R10,CVD                                                          
         MVC   PL+9(5),CNFD+3                                                   
         L     R14,12(R02)                                                      
         BAS   R10,CVD                                                          
         MVC   PL+17(5),CNFD+3                                                  
         L     R14,20(R02)                                                      
         BAS   R10,CVD                                                          
         MVC   PL+25(5),CNFD+3                                                  
         L     R14,28(R02)                                                      
         BAS   R10,CVD                                                          
         MVC   PL+33(5),CNFD+3                                                  
         A     R14,4(R02)          SUM SAVED TAPES                              
         A     R14,12(R02)                                                      
         A     R14,20(R02)                                                      
         BAS   R10,CVD                                                          
         MVC   PL+41(5),CNFD+3                                                  
         BAS   R09,PT              PRINT SAVED LINE                             
*                                                                               
         MVC   PL+1(6),=C'TOTALS'  DO TOTAL LINE                                
         L     R14,0(R02)                                                       
         A     R14,4(R02)                                                       
         BAS   R10,CVD                                                          
         MVC   PL+9(5),CNFD+3                                                   
         L     R14,8(R02)                                                       
         A     R14,12(R02)                                                      
         BAS   R10,CVD                                                          
         MVC   PL+17(5),CNFD+3                                                  
         L     R14,16(R02)                                                      
         A     R14,20(R02)                                                      
         BAS   R10,CVD                                                          
         MVC   PL+25(5),CNFD+3                                                  
         L     R14,24(R02)                                                      
         A     R14,28(R02)                                                      
         BAS   R10,CVD                                                          
         MVC   PL+33(5),CNFD+3                                                  
         A     R14,0(R02)          SUM ALL SUB TOTALS                           
         A     R14,4(R02)                                                       
         A     R14,8(R02)                                                       
         A     R14,12(R02)                                                      
         A     R14,16(R02)                                                      
         A     R14,20(R02)                                                      
         BAS   R10,CVD                                                          
         MVC   PL+41(5),CNFD+3                                                  
         BAS   R09,PT              PRINT TOTAL LINE                             
         BAS   R09,SKIP1                                                        
         MVC   PL+1(5),=C'PURGE'                                                
         LA    R03,PURGES                                                       
         LA    R01,4                                                            
         LA    R15,PL+9                                                         
         SR    R09,R09                                                          
PU1      L     R14,0(R03)                                                       
         AR    R09,R14                                                          
         BAS   R10,CVD                                                          
         MVC   0(5,R15),CNFD+3                                                  
         LA    R03,4(R03)                                                       
         LA    R15,8(R15)                                                       
         BCT   R01,PU1                                                          
         LR    R14,R09                                                          
         BAS   R10,CVD                                                          
         MVC   PL+41(5),CNFD+3                                                  
         BAS   R09,PT                                                           
*                                                                               
         EOJ                                                                    
*                                                                               
*                                                                               
TEND     MVI   EOF,X'FF'           SET EOF                                      
         B     TL3                 GO DO LAST TOTALS                            
*                                                                               
LHDL     DC    C'2400    1200     600    DISC    TOTAL'                         
*                                                                               
CNTRS    DC    F'0'                A                                            
         DC    F'0'                                                             
         DC    F'0'                B                                            
         DC    F'0'                                                             
         DC    F'0'                C                                            
         DC    F'0'                                                             
         DC    F'0'                D                                            
         DC    F'0'                                                             
PURGES   DC    F'0'                                                             
         DC    F'0'                                                             
         DC    F'0'                                                             
         DC    F'0'                                                             
*                                                                               
LAST     DC    C'A'                                                             
EOF      DC    X'00'                                                            
         EJECT                                                                  
*                                                                               
CVD      CVD   R14,PKFLD                                                        
         UNPK  CNFD(8),PKFLD(8)                                                 
         OI    CNFD+7,X'F0'                                                     
         BR    R10                                                              
PKFLD    DS    D                                                                
CNFD     DS    D                                                                
*                                                                               
PT       PUT   PRINT,PL                                                         
         XC    PL(132),PL                                                       
         LH    R14,LCNT                                                         
         LA    R14,1(R14)                                                       
         STH   R14,LCNT                                                         
         CLC   LCNT(2),=H'54'                                                   
         BCR   12,R09              RETURN                                       
HDL      CNTRL PRINT,SK,1          SKIP TO TOP                                  
         MVC   PL+3(4),=C'DATE'                                                 
         MVC   PL+8(2),DATE                                                     
         MVC   PL+11(2),DATE+2                                                  
         MVC   PL+14(2),DATE+4                                                  
         MVI   PL+10,C'/'                                                       
         MVI   PL+13,C'/'                                                       
         MVC   PL+55(27),=C'TAPE LIBRARY MASTER LISTING'                        
         LH    R14,PCNT                                                         
         LA    R14,1(R14)                                                       
         STH   R14,PCNT                                                         
         BAS   R10,CVD                                                          
         MVC   PL+121(4),=C'PAGE'                                               
         MVC   PL+126(3),CNFD+5                                                 
         PUT   PRINT,PL                                                         
         CNTRL PRINT,SP,2          SKIP 2 LINES                                 
         XC    PL(132),PL                                                       
         MVC   PL(L'PLS1),PLS1     FIRST PART OF HDL                            
         MVC   PL+17(L'PLS2),PLS2  SECOND PART                                  
         PUT   PRINT,PL                                                         
         CNTRL PRINT,SP,2                                                       
         MVC   LCNT(2),=X'0003'                                                 
         XC    PL(132),PL                                                       
         BR    R09                                                              
*                                                                               
LCNT     DC    H'0'                                                             
PCNT     DC    H'0'                                                             
*                                                                               
SKIP1    CNTRL PRINT,SP,1                                                       
         BR    R09                                                              
SKIP2    CNTRL PRINT,SP,2                                                       
         BR    R09                                                              
*                                                                               
RIN      GET   IN,TWRK                                                          
         L     R14,ICNT                                                         
         LA    R14,1(R14)                                                       
         ST    R14,ICNT                                                         
         BR    R10                                                              
*                                                                               
ICNT     DC    F'0'                INPUT CNTR                                   
DATE     DS    6C                                                               
TWRK     DS    0CL80                                                            
         EJECT                                                                  
*                                                                               
TNUM     DS    CL5                 REEL NUMBER                                  
TRUN     DS    CL7                 RUN NUMBER                                   
TDDAT    DS    CL6                 DATE OF DATA                                 
TRDTE    DS    CL6                 RUN DATE                                     
TNON     DS    CL4                 REEL OF REEL                                 
TDES     DS    CL29                                                             
TPDTE    DS    CL6                 PURGE DATE                                   
TFLE     DS    CL1                 OUTPUT FILE                                  
TDLACT   DS    CL6                 DATE LAST ACTIVITY                           
TDLIB    DS    CL6                 DATE INTO LIBRARY                            
TDENS    DS    CL4                 DENSITY                                      
*                                                                               
CWRK     DS    0CL80                                                            
CNUM     DS    CL5                 REEL NUMBER                                  
CRUN     DS    CL7                 RUN NUMBER                                   
CDDAT    DS    CL6                 DATE OF DATA                                 
CRDTE    DS    CL6                 RUN DATE                                     
CNON     DS    CL4                 REEL OF REEL                                 
CDES     DS    CL30                DESCRIPTION                                  
CPDTE    DS    CL6                 PURGE DATE                                   
CFLE     DS    CL1                 OUTPUT FILE                                  
CODE     DS    CL1                 TRANSACTION CODE                             
         DS    CL14                SPARE                                        
CNRLS    EQU   CRUN                NUMBER OF REELS                              
CDENS    EQU   CRUN+3              DENSITY                                      
*                                                                               
*                                                                               
PL       DS    132C                                                             
*                                                                               
         EJECT                                                                  
FPRT     LA    R08,PL              POINT TO PRINT LINE                          
         USING PLD,R08                                                          
         CLC   TRUN(5),=C'00000'   SCRATCH                                      
         BC    8,FPRT1             YES, NO TAG                                  
         MVC   CNFD+2(4),DATE      MOVE MMDD                                    
         MVC   CNFD(2),DATE+4      MOVE YEAR                                    
         MVC   PKFLD+2(4),TPDTE    MOVE MMDD                                    
         MVC   PKFLD(2),TPDTE+4    MOVE YY                                      
         CLC   PKFLD(6),CNFD       TEST TO TAG AS PURGE                         
         BC    2,FPRT1             NO                                           
         L     R14,0(R03)                                                       
         LA    R14,1(R14)          BUM  PURGE CNTR                              
         ST    R14,0(R03)                                                       
         MVI   PI,C'*'             FLAG IT                                      
FPRT1    MVC   OWNER,TRUN                                                       
         MVC   REEL,TNUM                                                        
         MVC   FILE,TFLE                                                        
         MVC   ROR(2),TNON                                                      
         MVC   ROR+3(2),TNON+2                                                  
         CLC   ROR(2),=C'  '                                                    
         BE    *+8                                                              
         MVI   ROR+2,C'/'                                                       
         MVC   DESC(29),TDES                                                    
         MVC   DENS,TDENS                                                       
         LA    R14,RDATE                                                        
         LA    R15,TRDTE                                                        
         BAS   R09,DMVE            MOVE DATE TO PRINT LINE                      
         LA    R14,DDATE                                                        
         LA    R14,TDDAT                                                        
         BAS   R09,DMVE            MOVE DATE TO PRINT LINE                      
         LA    R14,LACT                                                         
         LA    R15,TDLACT                                                       
         BAS   R09,DMVE            MOVE DATE TO PRINT LINE                      
         LA    R14,TDATE                                                        
         LA    R15,TDLIB                                                        
         BAS   R09,DMVE            MOVE DATE TO PRINT LINE                      
         LA    R14,PDATE                                                        
         LA    R15,TPDTE                                                        
         BAS   R09,DMVE            MOVE DATE TO PRINT LINE                      
         BR R10                                                                 
*                                                                               
DMVE     CLI   0(R15),C' '         BALNK                                        
         BCR   8,R09                                                            
         MVC   0(2,R14),0(R15)                                                  
         MVC   3(2,R14),2(R15)                                                  
         MVC   6(2,R14),4(R15)                                                  
         MVI   2(R14),C'/'                                                      
         MVI   5(R14),C'/'                                                      
         BR    R09                                                              
*                                                                               
PLS1     DC    C'  REEL    OWNER'                                               
PLS2     DC    C'FILE  R/R  RUN DATE   DESCRIPTION                     X        
               PURGE DATE  DATA DATE  LAST ACT             TAPE DATE   X        
               DENS'                                                            
*                                                                               
PLD      DSECT                                                                  
PI       DS    CL1                                                              
         DS    CL1                                                              
REEL     DS    CL5                                                              
         DS    CL2                                                              
OWNER    DS    CL7                                                              
         DS    CL2                                                              
FILE     DS    CL1                                                              
         DS    CL2                                                              
ROR      DS    CL5                                                              
         DS    CL2                                                              
RDATE    DS    CL8                                                              
         DS    CL3                                                              
DESC     DS    CL30                                                             
         DS    CL3                                                              
PDATE    DS    CL8                                                              
         DS    CL3                                                              
DDATE    DS    CL8                                                              
         DS    CL3                                                              
LACT     DS    CL8                                                              
         DS    CL14                                                             
TDATE    DS    CL8                                                              
         DS    CL3                                                              
DENS     DS    CL4                                                              
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002OP152     05/01/02'                                      
         END                                                                    
