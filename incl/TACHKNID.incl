*          DATA SET TACHKNID   AT LEVEL 044 AS OF 02/20/09                      
*                                                                               
*              INCLUDED BY TAGEND4, TAGEND6, AND TAREP41                        
*                                  NTRY  - AIO SET                              
*                                  XIT   - IF CC EQU NEWCOM,NEWCTYPE            
*                                                TAL COMML REC IN AIO           
*                                          IF CC NEQU - NO MATCH FOUND          
         SPACE 3                                                                
CHKNID   NTR1                                                                   
*                                                                               
         XC    NEWCOM,NEWCOM       CLEAR TALENT INTERNAL COMML NUMBER           
         MVI   NEWVER,0            CLEAR TALENT COMML VERSION LETTER            
         MVI   NEWCTYPE,0          CLEAR TALENT COMMERCIAL TYPE                 
*                                                                               
         CLC   TGADID,SPACES       IF WE HAVE AD-ID                             
         BNH   CHKNID5             SET GLOBAL CID AS AD-ID                      
         MVC   TGCID(L'TGADID),TGADID                                           
*                                                                               
         BAS   RE,CHKLMAT          CHECK MATCHED VIA LIFT ID                    
         BE    XIT                                                              
*                                                                               
         BAS   RE,CHKVMAT          CHECK MATCH ON VERSION                       
         BE    XIT                                                              
*                                                                               
         BAS   RE,CHKEMAT          CHECK EXACT MATCH                            
         BE    XIT                                                              
*                                                                               
         BAS   RE,CHKAMAT          CHECK MATCHED VIA ALIAS RECORD               
         B     XIT                 ( ALIAS MATCH OR ALIAS(LIFT) MATCH )         
*                                                                               
CHKNID5  MVC   TGCID,SPACES        IF WE DON'T HAVE AD-ID                       
         MVC   TGCID(L'TGNID),TGNID SET GLOBAL CID AS NID                       
         MVC   TGADID,TGCID                                                     
*                                                                               
         BAS   RE,CHKLMAT          CHECK MATCHED VIA LIFT ID                    
         BE    XIT                                                              
*                                                                               
         BAS   RE,CHKVMAT          CHECK MATCH ON VERSION                       
         BE    XIT                                                              
*                                                                               
         BAS   RE,CHKEMAT          CHECK EXACT MATCH                            
         BE    XIT                                                              
*                                                                               
         BAS   RE,CHKAMAT          CHECK MATCHED VIA ALIAS RECORD               
         B     XIT                 (ALIAS MATCH OR ALIAS(LIFT) MATCH)           
         EJECT                                                                  
*              ROUTINE TO CHECK NWK ID = TALENT LIFT ID                         
*                                  XIT - IF CC EQU  NEWCOM=INT COMM #           
*                                                   NEWCTYPE='L'                
         SPACE                                                                  
CHKLMAT  NTR1                                                                   
         XC    KEY,KEY                                                          
         LA    R2,KEY                                                           
         USING TLCOPD,R2                                                        
*                                                                               
         MVI   TLCOPCD,TLCOKCDQ                                                 
         MVC   TLCOKAGY,TGAGY                                                   
         MVC   TLCOKAKA,SPACES                                                  
         MVC   TLCOKAKA(L'TGCID),TGCID     TRY COMM'L ID AS AKA                 
         GOTO1 HIGH                                                             
         CLC   KEY(TLCOKCID-TLCOPD),KEYSAVE                                     
         BNE   NO                                                               
*                                                                               
         GOTO1 GETREC                                                           
         L     R2,AIO                                                           
         USING TLCOD,R2                                                         
         MVC   NEWCOM,TLCOCOM      SET TALENT INTERNAL COMM #                   
         MVI   NEWCTYPE,TANXTLFT   SET MATCHED VIA LIFT ID                      
         B     YES                                                              
         DROP  R2                                                               
         SPACE 2                                                                
*              ROUTINE TO CHECK NWK ID = TALENT ID                              
*                                  XIT - IF CC EQU  NEWCOM=INT COMM #           
*                                                   NEWCTYPE='E'                
         SPACE                                                                  
CHKEMAT  NTR1                                                                   
         GOTO1 RECVAL,DMCB,TLCOICDQ,(X'A0',0)                                   
         BNE   NO                                                               
         LA    R2,KEY                                                           
         USING TLCOPD,R2                                                        
*                                                                               
         MVC   NEWCOM,TLCOICOM     SET TALENT INTERNAL COMM #                   
         MVI   NEWCTYPE,TANXTTAL   SET EXACT MATCH ON COMMERCIAL                
*                                                                               
         B     YES                                                              
         DROP  R2                                                               
         EJECT                                                                  
*              ROUTINE TO CHECK NWK ID IS AN ALIAS TALENT ID                    
*                                  XIT - IF CC EQU  NEWCOM=INT COMM #           
*                                                   NEWCTYPE='A'/'F'            
         SPACE                                                                  
CHKAMAT  NTR1                                                                   
         GOTO1 RECVAL,DMCB,TLAKCDQ,0                                            
         BNE   NO                                                               
*                                                                               
         LA    R2,KEY                                                           
         USING TLAKD,R2                                                         
         MVC   NEWCOM,TLAKCOM      SET NEW TALENT INTERNAL COMM #               
         MVI   NEWCTYPE,TANXTAKA   SET MATCHED VIA ALIAS RECORD                 
         TM    KEY+TLDRSTAT-TLDRD,TLAKSLFT   IF ALIAS RECORD BY LIFT ID         
         BZ    *+8                                                              
         MVI   NEWCTYPE,TANXTALF             SET MATCHED VIA ALIAS LIFT         
*                                                                               
         GOTO1 RECVAL,DMCB,TLCOCCDQ,(X'A0',NEWCOM)                              
         BNE   NO                                                               
         L     R2,AIO                                                           
         USING TLCOD,R2                                                         
         CLC   TLCOAGY,TGAGY       IF AGENCY CHANGED                            
         BNE   NO                  NO MATCH FOUND                               
         B     YES                                                              
         DROP  R2                                                               
         EJECT                                                                  
*              ROUTINE TO CHECK NWK ID = TALENT VERSION ID                      
*                                  XIT - IF CC EQU  NEWCOM=INT COMM #           
*                                                   NEWVER=VER LETTER           
*                                                                               
*                                  MATCH BY VERSION NEWCTYPE='V'                
*                                  MATCH BY VALIAS  NEWCTYPE='W'                
         SPACE                                                                  
CHKVMAT  NTR1                                                                   
         GOTO1 RECVAL,DMCB,TLAKCDQ,0                                            
         B     CVM20                                                            
CVM10    GOTO1 SEQ                                                              
CVM20    CLC   KEY(TLAKVER-TLAKD),KEYSAVE                                       
         BNE   CVM50                                                            
*                                                                               
         USING TLAKD,R2                                                         
         LA    R2,KEY              FIRST CHECK ALIAS RECORDS                    
         CLI   TLAKVER,0           ONLY CONSIDER ALIASES TO VERSIONS            
         BE    CVM50                                                            
*                                                                               
         GOTO1 GETREC                                                           
*                                                                               
         MVI   ELCODE,TACMELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TACMTYPX))                                     
         BNE   CVM10                                                            
*                                                                               
         USING TACMD,RE                                                         
         L     RE,TGELEM                                                        
         B     CVM40                                                            
*                                                                               
CVM30    ZIC   R0,1(RE)                                                         
         AR    RE,R0                                                            
         CLI   0(RE),TACMELQ                                                    
         BNE   CVM10                                                            
         CLI   TACMTYPE,TACMTYPX                                                
         BNE   CVM10                                                            
*                                                                               
CVM40    CLC   TGCID,TACMCOMM                                                   
         BNE   CVM30                                                            
         DROP  RE                                                               
*                                                                               
         MVC   NEWVER,TLAKVER      SET VERSION LETTER                           
*                                                                               
         MVC   TGCOM,TLAKCOM       ENSURE COMM'L STILL EXISTS                   
         GOTO1 RECVAL,DMCB,TLCOCCDQ,(X'A0',0)                                   
         BNE   CVM50                                                            
         DROP  R2                                                               
*                                                                               
         USING TLCOD,R2                                                         
         L     R2,AIO                                                           
         CLC   TLCOAGY,TGAGY       ENSURE COMM'L AGENCY STILL                   
         BNE   CVM50               MATCHES ALIAS AGENCY                         
         DROP  R2                                                               
*                                                                               
         MVI   NEWCTYPE,TANXTALV   SET MATCHED VIA ALIAS VERSION                
*                                                                               
CVM50    GOTO1 RECVAL,DMCB,(X'20',TLCOICDQ),(X'A4',0)                           
         BNE   CVMNO                                                            
         CLC   KEY(TLCOICID+L'TLCOICID-TLCOPD),KEYSAVE                          
         BNE   CVMNO                                                            
         LA    R2,KEY                                                           
         USING TLCOPD,R2                                                        
         CLI   TLCOIVER,0                                                       
         BNE   CVM60                                                            
         GOTO1 SEQ                                                              
         CLC   KEY(TLCOICID+L'TLCOICID-TLCOPD),KEYSAVE                          
         BNE   CVMNO                                                            
         CLI   TLCOIVER,0                                                       
         BE    CVMNO                                                            
*                                                                               
CVM60    MVC   NEWCOM,TLCOICOM     SET TALENT INTERNAL COMM #                   
*                                                                               
         CLI   NEWCTYPE,TANXTALV   IF NOT MATCHED BY ALIAS TO VERSION           
         BE    CVM70                                                            
         MVC   NEWVER,TLCOIVER     SET VERSION LETTER                           
         MVI   NEWCTYPE,TANXTVER   SET MATCH ON VERSION ID                      
*                                                                               
         USING TLCOD,RE                                                         
CVM70    L     RE,AIO                                                           
         CLI   TLCOVER,TLCOV026                                                 
         BE    YES                                                              
         MVC   TGCOM,TLCOICOM                                                   
         GOTO1 RECVAL,DMCB,TLCOCCDQ,(X'A4',0)                                   
         BE    YES                                                              
         DC    H'00'                                                            
         DROP  R2,RE                                                            
*                                                                               
CVMNO    MVI   NEWVER,0                                                         
         B     NO                                                               
         EJECT                                                                  
