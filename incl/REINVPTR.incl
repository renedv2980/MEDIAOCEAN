*          DATA SET REINVPTR   AT LEVEL 020 AS OF 11/03/11                      
*              CREATE NEW PASSIVE POINTER                                       
         SPACE 1                                                                
*              PARAM 1   BYTES 1-3 A(INVENTORY RECORD)                          
*              PARAM 2   BYTES 1-3 A(200 BYTE OUTPUT AREA)                      
         SPACE 1                                                                
         DROP  R4                                                               
         DROP  R2                                                               
         USING RINVREC,R2                                                       
         USING RIDPKEY,R4                                                       
INVPTR   NTR1                                                                   
         L     R2,0(R1)                                                         
         L     R4,4(R1)                                                         
         XC    0(200,R4),0(R4)                                                  
         LA    R6,6                                                             
         LA    R3,RINVDP                                                        
         SPACE 1                                                                
INVPTR1  DS    0H                                                               
         CLI   RINVKTYP,X'12'      OLD X'12' INVENTORY REC ? (06/09)            
         BE    OLDPTR1             YES - PRODUCE X'92' PASSIVE                  
         MVI   RIDPKTYP,X'E2'                                                   
         MVC   RIDPKREP,RINVKREP                                                
         MVC   RIDPKSTA,RINVKSTA                                                
         MVC   RIDPKDPT,0(R3)                                                   
         MVC   RIDPKINV,RINVKINV                                                
         MVC   RIDPKSTD,RINVKSTD                                                
*                                                                               
         CLI   RINVKINV+3,0        OLD INV FORMAT                               
         BNE   INVPTR40            NO HANDLE NEW FORMAT                         
*                                                                               
         LA    R1,DAYCOD                                                        
INVPTR5  CLI   0(R1),X'FF'                                                      
         BE    INVPTR10                                                         
         CLC   RIDPKDPT,0(R1)      FIND DAYPART IN TABLE                        
         BE    *+12                                                             
         LA    R1,1(R1)                                                         
         B     INVPTR5                                                          
         MVC   RIDPKDAY,RINVKDAY   MOVE DAY CODE,                               
         MVC   RIDPKQTR,RINVKQTR   QUARTER HOUR,                                
         MVC   RIDPKLEN,RINVKLEN   AND PROGRAM LENGTH TO KEY                    
         SPACE                                                                  
INVPTR10 LA    R1,EFFDAT                                                        
INVPTR15 CLI   0(R1),X'FF'                                                      
         BE    INVPTR90                                                         
         CLC   RIDPKDPT,0(R1)      IF DAYPART IN TABLE                          
         BE    *+12                                                             
         LA    R1,1(R1)                                                         
         B     INVPTR15                                                         
         MVC   RIDPKDTE,RINVPEFF   MOVE EFFECTIVE DATE,                         
         MVC   RIDPKQTR,RINVKQTR   QUARTER HOUR,                                
         MVC   RIDPKLEN,RINVKLEN   AND PROGRAM LENGTH TO KEY                    
         B     INVPTR90                                                         
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
*           * * *   PRODUCE X'92' PASSIVE   * * *                     *         
*   X'12' INVENTORY REC HAS DIFFERENT KEY STRUCTURE FROM NEW X'62'    *         
*    ALL KEY DISPLACEMENTS N(R2) ARE FROM THE OLD X'12' FORMAT        *         
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
*                                                                               
OLDPTR1  DS    0H                                                               
         MVI   RIDPKTYP,X'92'                                                   
         MVC   RIDPKREP,10(R2)     RINVKREP                                     
         MVC   RIDPKSTA,12(R2)     RINVKSTA                                     
         MVC   RIDPKDPT,0(R3)                                                   
         MVC   RIDPKINV,17(R2)     RINVKINV                                     
         MVC   RIDPKSTD,21(R2)     RINVKSTD                                     
*                                                                               
         CLI   20(R2),0            OLD INV FORMAT (RINVKINV+3)                  
         BNE   INVPTR40            NO HANDLE NEW FORMAT                         
*                                                                               
         LA    R1,DAYCOD                                                        
OLDPTR5  CLI   0(R1),X'FF'                                                      
         BE    OLDPTR10                                                         
         CLC   RIDPKDPT,0(R1)      FIND DAYPART IN TABLE                        
         BE    *+12                                                             
         LA    R1,1(R1)                                                         
         B     OLDPTR5                                                          
         MVC   RIDPKDAY,19(R2)     MOVE DAY CODE, (RINVKDAY)                    
         MVC   RIDPKQTR,17(R2)     QUARTER HOUR,  (RINVKQTR)                    
         MVC   RIDPKLEN,20(R2)     AND PROGRAM LENGTH (RINVKLEN) TO KEY         
         SPACE                                                                  
OLDPTR10 LA    R1,EFFDAT                                                        
OLDPTR15 CLI   0(R1),X'FF'                                                      
         BE    INVPTR90                                                         
         CLC   RIDPKDPT,0(R1)      IF DAYPART IN TABLE                          
         BE    *+12                                                             
         LA    R1,1(R1)                                                         
         B     OLDPTR15                                                         
         MVC   RIDPKDTE,RINVPEFF   MOVE EFFECTIVE DATE,                         
         MVC   RIDPKQTR,17(R2)     QUARTER HOUR, (RINVKQTR)                     
         MVC   RIDPKLEN,20(R2)     AND PROGRAM LENGTH (RINVKLEN) TO KEY         
         B     INVPTR90                                                         
*                                                                               
*   HANDLE NEW INVENTORY RECORDS  (BOTH X'12' AND X'62' RECORD TYPES)           
*                                                                               
INVPTR40 DS    0H                                                               
         TM    RINVPEFF+9,X'80'    USER DEFINED INVENTORY                       
         BO    INVPTR90                                                         
*                                                                               
*  FOR COMPUTER ASSIGNED INVENTORY NUMBERS, FILL IN                             
*  THE DAY,TIME,QUARTER HOUR KEY FIELDS.                                        
*                                                                               
         MVC   RIDPKDAY,RINVPEFF+7   MOVE DAY CODE,                             
         MVC   RIDPKQTR,RINVPEFF+6   QUARTER HOUR,                              
         MVC   RIDPKLEN,RINVPEFF+8   AND PROGRAM LENGTH TO KEY                  
*                                                                               
         LA    RE,EFFDAT           SPECIAL DAYPARTS                             
INVPTR50 CLI   0(RE),X'FF'                                                      
         BE    INVPTR90                                                         
         CLC   0(1,R3),0(RE)                                                    
         BE    INVPTR60                                                         
         LA    RE,1(RE)                                                         
         B     INVPTR50                                                         
*                                                                               
INVPTR60 XC    RIDPKDAY,RIDPKDAY                                                
         MVC   RIDPKDTE,RINVPEFF                                                
         SPACE                                                                  
*                                                                               
INVPTR90 DS    0H                                                               
*                                                                               
         BRAS  RE,PSVCHK           ELIMINATE DUPLICATE PASSIVES                 
*                                                                               
         LA    R3,1(R3)            NEXT DAYPART CODE                            
         CLI   0(R3),X'40'                                                      
         BNH   INVPTX                                                           
******   LA    R4,32(R4)                                                        
         BCT   R6,INVPTR1          DO NEXT POINTER                              
         SPACE 1                                                                
INVPTX   XIT1                                                                   
         SPACE 1                                                                
         DROP  R2                                                               
         DROP  R4                                                               
         SPACE 1                                                                
*  THESE DAYPARTS GET A DAY CODE, QUARTER HOUR, AND PROGRAM LENGTH              
DAYCOD   DC    C'MDKNPOUXYWZ',X'FF'                                             
         SPACE 1                                                                
*  THESE DAYPARTS GET EFFECTIVE DATE, QUARTER HOUR, AND PROGRAM LENGTH          
EFFDAT   DC    C'VSJ',X'FF'                                                     
         SPACE 1                                                                
*  THESE DAYPARTS ONLY GET INVENTORY NUMBER AND START DATE                      
*       ERATLF - THEY ARE THE FRINGE "SUB-DAYPARTS"                             
* (W-WEEKEND IS NOT TREATED AS FRINGE FOR PASSIVE POINTERS, BUT                 
*    IS GROUPED WITH FRINGE EVERYWHERE ELSE)                                    
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
*        ELIMINATE DUPLICATE PASSIVES                                 *         
*                                                                     *         
***********************************************************************         
         DS    0D                                                               
PSVCHK   NTR1  LABEL=*                                                          
*                                                                               
         LA    R4,0(R4)            CLEAR HOB                                    
*                                                                               
         L     RF,SAVER1           POINT TO START OF PASSIVE POINTERS           
         L     RF,4(RF)                                                         
         LA    RF,0(RF)            CLEAR HOB                                    
*                                                                               
         CR    RF,R4               DONE IF LAST ENTRY REACHED                   
         BE    PSVCHK10                                                         
         CLC   0(32,RF),0(R4)      LOOK FOR DUPLICATE                           
         BE    *+12                   DUPE FOUND                                
         LA    RF,32(RF)              BUMP TO NEXT PASSIVE                      
         B     *-20                                                             
*                                  DUPLICATE PASSIVE                            
         B     PSVCHK20                                                         
*                                                                               
PSVCHK10 DS    0H                  NO DUPLICATE                                 
*                                                                               
         LA    R4,32(R4)                                                        
*                                                                               
PSVCHK20 DS    0H                                                               
         XC    0(32,R4),0(R4)         CLEAR LAST POINTER                        
*                                                                               
PSVCHKX  DS    0H                                                               
         XIT1  REGS=(R4)                                                        
*                                                                               
         LTORG                                                                  
*                                                                               
