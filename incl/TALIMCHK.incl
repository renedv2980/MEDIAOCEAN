*          DATA SET TALIMCHK   AT LEVEL 001 AS OF 11/26/12                      
***********************************************************************         
*        ROUTINE TO CHECK AGENCY AND CLIENT LIMIT ACCESS              *         
*        INCLUDED BY TAGEN29 AND TAGEN5C                              *         
*        ON ENTRY ... AIO = A(CHECK RECORD)                           *         
***********************************************************************         
                                                                                
LIMITCHK NTR1  BASE=*,LABEL=*                                                   
         LHI   R2,1                                                             
                                                                                
         USING TLCKD,R4                                                         
         L     R4,AIO                                                           
         MVC   CHKAGY,TLCKAGY      INITIALIZE CHECK'S AGENCY                    
         XC    CHKCLI,CHKCLI       AND CLIENT                                   
         DROP  R4                                                               
                                                                                
         USING TAOID,R4                                                         
         MVI   ELCODE,TAOIELQ      SAVE CHECK'S AGENCY                          
         BRAS  RE,GETEL                                                         
         JNE   LIMCHK10                                                         
         MVC   CHKAGY,TAOIAGY                                                   
         DROP  R4                                                               
                                                                                
         USING TAPDD,R4                                                         
LIMCHK10 L     R4,AIO              CHECK CLIENT LIMIT ACCESS                    
         MVI   ELCODE,TAPDELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   LIMCHK20                                                         
         MVC   CHKCLI,TAPDCLI                                                   
         DROP  R4                                                               
                                                                                
         USING FAWSSVRD,R1                                                      
LIMCHK20 LA    R1,LIMBLK                                                        
         MVC   FAWSTOKN(3),=C'STF'                                              
         STC   R2,FAWSTOKN+3                                                    
         MVI   FAWSACTN,FAWSARST   RECALL STAFF2 INFORMATION VIA WWSVR          
         XC    FAWSLEN,FAWSLEN                                                  
         MVC   FAWSADR,TGAS2ACC                                                 
         GOTO1 WSSVR,(R1)                                                       
         CLI   FAWSRTN,0           IF NOT FOUND, STAFF HAS NO ACCESS            
         JNE   NO                                                               
         DROP  R1                                                               
                                                                                
         AHI   R2,1                                                             
                                                                                
         USING TAVAD,R1                                                         
         L     R1,TGAS2ACC                                                      
         OC    TAVAAGY,TAVAAGY     IF STAFF HAS NO AGENCY LIMITS,               
         JZ    YES                 STAFF HAS ACCESS TO ALL RECORDS              
                                                                                
LIMCHK30 CLI   0(R1),0             RECALL NEXT RECORD FROM WSSVR                
         JE    LIMCHK20                                                         
                                                                                
         CLC   CHKAGY,TAVAAGY      IF AGENCY IS FOUND IN STAFF LIMITS           
         JNE   LIMCHK50                                                         
                                                                                
         CLI   TAVALEN,TAVALNQ     IF NO CLIENT LIMITS ARE DEFINED              
         JE    YES                 ACCESS IS GRANTED                            
                                                                                
         ZIC   RE,TAVALEN                                                       
         SHI   RE,TAVALNQ                                                       
         LA    RF,TAVACLI                                                       
LIMCHK40 CLC   CHKCLI,0(RF)        IF CLIENT IS FOUND IN STAFF LIMITS           
         JE    YES                 ACCESS IS GRANTED                            
         LA    RF,L'TAVACLI(RF)                                                 
         SHI   RE,L'TAVACLI                                                     
         LTR   RE,RE                                                            
         JNZ   LIMCHK40                                                         
                                                                                
LIMCHK50 ZIC   RE,TAVALEN          BUMP TO NEXT VALID AGENCY/CLIENT             
         AR    R1,RE               ELEMENT                                      
         J     LIMCHK30                                                         
         DROP  R1                                                               
                                                                                
CHKAGY   DS    CL6                 CHECK'S AGENCY                               
CHKCLI   DS    CL6                 CHECK'S CLIENT                               
LIMBLK   DS    XL100               AGENCY/CLIENT LIMIT WORK BLOCK               
                                                                                
         LTORG                                                                  
         EJECT                                                                  
