*          DATA SET PPPRINT    AT LEVEL 002 AS OF 05/01/02                      
         TITLE 'STANDARD PRINTING MODULE FOR COBOL'                             
         SPACE 3                                                                
PRINT    CSECT                                                                  
         STM   14,12,12(13)                                                     
         BASR  11,0                                                             
         USING *,11                                                             
         L     2,4(1)              ADDRESS CONTROL BYTES                        
         L     3,0(1)                                                           
         CLI   FLAG,C'Y'                                                        
         BC    7,PRINT2                                                         
         MVI   FLAG,C'N'                                                        
         OPEN  PRDTF                                                            
         SPACE 3                                                                
PRINT2   EQU   *                                                                
         MVC   DUMDUM+1(132),0(3)                                               
         LA     3,DUMDUM                                                        
         PACK  PRIDUB,2(2,2)                                                    
         CVB   4,PRIDUB                                                         
         CLI   0(2),C'B'                                                        
         BC    7,PRINT7                                                         
         EJECT                                                                  
*                       CONTROL OF WRITING BEFORE SPACING                       
         SPACE 3                                                                
         CLI   1(2),C'C'           CHANNEL CONTROL                              
         BC    7,PRINT3                                                         
         LA    4,BCTAB(4)                                                       
         MVC   0(1,3),0(4)                                                      
         B     PRIPUT                                                           
         SPACE 2                                                                
PRINT3   CH    4,=H'3'             LESS THAN 4 LINES                            
         BC    2,PRINT4                                                         
         LA    4,BLTAB(4)                                                       
         MVC   0(1,3),0(4)                                                      
         B     PRIPUT                                                           
         SPACE 2                                                                
PRINT4   MVI   0(3),X'19'          WRITE BEFORE 3                               
         SPACE 2                                                                
PRINT5   PUT   PRDTF,(3)           LOOP FOR MORE THAN 3 LINES                   
         SH    4,=H'3'                                                          
         CH    4,=H'3'                                                          
         BC    2,PRINT6                                                         
         LA    4,SLTAB(4)                                                       
         MVC   0(1,3),0(4)                                                      
         B     PRIPUT                                                           
         SPACE 3                                                                
PRINT6   MVI   0(3),X'1B'                                                       
         B     PRINT5                                                           
         EJECT                                                                  
*                        CONTROL OF WRITING AFTER SPACING                       
         SPACE 3                                                                
PRINT7   CLI   1(2),C'C'                                                        
         BC    7,PRINT9                                                         
         LA    4,SCTAB(4)                                                       
         MVC   0(1,3),0(4)                                                      
         SPACE 2                                                                
PRINT8   PUT   PRDTF,(3)           SPACE CONTROL                                
PRINT8A  MVI   0(3),X'01'                                                       
         B     PRIPUT              NOW GO AND WRITE WITHOUT ADVANCING           
         SPACE 2                                                                
PRINT9   CH    4,=H'3'                                                          
         BC    2,PRINT10                                                        
         LTR   4,4                 CHECK FOR AFTER 0                            
         BC    8,PRINT8A                                                        
         LA    4,SLTAB(4)                                                       
         MVC   0(1,3),0(4)                                                      
         B     PRINT8                                                           
         SPACE 2                                                                
PRINT10  MVI   0(3),X'1B'          LOOP FOR SPACING 3 LINES                     
         PUT   PRDTF,(3)           SPACE DOWN 3 LINES                           
         SH    4,=H'3'                                                          
         B     PRINT9                                                           
         EJECT                                                                  
*                        FINAL PUT AND RETURN TO USER                           
         SPACE 2                                                                
PRIPUT   PUT   PRDTF,(3)                                                        
         LM    14,12,12(13)                                                     
         BR    14                                                               
         SPACE 2                                                                
FLAG     DC    C'Y'                                                             
PRIDUB   DS    D                                                                
         SPACE 2                                                                
BLTAB    DC    X'01091119'         WRITE BEFORE LINES 0-3                       
BCTAB    DC    X'89899199A1A9B1'                CHANNELS 0-6                    
         DC    X'B9C1C9D1D9E1'                  CHANNELS 7-12                   
         SPACE 2                                                                
SLTAB    DC    X'010B131B'                SPACE LINES 0-3                       
SCTAB    DC    X'8B8B939BA3ABB3'        SKIP TO CHANNELS 0-6                    
         DC    X'BBC3CBD3DBE3'                  CHANNELS 7-12                   
         SPACE 3                                                                
PRDTF    DTFPR                                                         X        
               DEVADDR=SYSLST,                                         X        
               IOAREA1=DUMDUM,                                         X        
               BLKSIZE=133,                                            X        
               CTLCHR=YES,                                             X        
               DEVICE=1403                                                      
DUMDUM    DS   CL133                                                            
         LTORG                                                                  
