*          DATA SET REMERG16O2 AT LEVEL 001 AS OF 01/05/00                      
CRDIT16S NMOD1 CREDITDX-CREDITD,*CREDIT*,CLEAR=YES                              
                                                                                
*                                                                               
         LR    R5,RC               SET UP GRID SPACE                            
         USING CREDITD,R5                                                       
         L     RC,0(R1)            RESET A(WORKSPACE)                           
*                                                                               
         BAS   RE,BLDGRID          LOAD ALL 03 ELTS INTO GRID                   
*                                                                               
* REMOVE SPOTS FROM GRID                                                        
*                                                                               
         BAS   RE,REMGRID                                                       
*                                                                               
* REBUILD X'03' ELEMENT FROM GRID                                               
*                                                                               
         BAS   RE,BLDBUY                                                        
*                                                                               
         XIT1                                                                   
         EJECT                                                                  
*                                                                               
***********************************************************************         
* CONSTRUCT A GRID BASE ON X'03' ELEMENTS IN THE BUY RECORD EACH                
* WEEK CONSIST OF 2 1-BYTE ENTRIES WITH THE FIRST ENTRY THE NUMBER              
* OF SPOTS FOR THAT WEEK, AND THE SECOND BYTE FOR VARIOUS FLAGS                 
* (ZERO OVERRIDE, ALTERNATE, ETC)                                               
* THE START OF THE GRID IS THE FIRST START DATE OF THE BUY                      
***********************************************************************         
BLDGRID  NTR1                                                                   
*                                                                               
         LA    R6,RBUYREC                                                       
         MVI   ELCODE,X'03'                                                     
         BAS   RE,GETEL                                                         
         BNE   ERROR                                                            
         USING RBUYDTEL,R6                                                      
*                                                                               
         GOTO1 DATCON,DMCB,(3,RBUYDTST),(5,CRSTDT)                              
         LA    R3,WORK2                                                         
         XC    WORK2,WORK2         CLEAR WORK AREA                              
*                                                                               
BGRD0060 DS    0H                                                               
         ZIC   R1,RBUYDTWK                                                      
*                                                                               
BGRD0080 DS    0H                                                               
         MVC   0(1,R3),RBUYDTNW                                                 
         TM    RBUYDTIN,X'01'      NPW OVERRIDE??                               
         BZ    *+8                                                              
         OI    1(R3),X'01'                                                      
         LA    R3,2(R3)            BUMP TO NEXT WEEK                            
         TM    RBUYDTIN,X'40'      ALTERNATE WEEKS??                            
         BZ    *+8                                                              
         LA    R3,2(R3)            YES, BUMP ONE MORE                           
         BCT   R1,BGRD0080         PROCESS FOR SPECIFIED NUMBER OF WKS          
*                                                                               
         BAS   RE,NEXTEL                                                        
         BNE   BGRD0400                                                         
*                                                                               
         MVI   CRSTDT+8,C'-'                                                    
         GOTO1 DATCON,DMCB,(3,RBUYDTST),(5,CRSTDT2)                             
         L     RE,ACOMFACS         CALL COMFACS FOR ADDRESS OF PERVAL           
         USING COMFACSD,RE                                                      
         L     RF,CPERVAL                                                       
         DROP  RE                                                               
*                                                                               
         GOTO1 (RF),DMCB,(17,CRSTDT),WORK2+120                                  
         CLI   DMCB+4,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
WKD      USING PERVALD,WORK2+120                                                
         ZICM  R1,WKD.PVALNWKS,2   # WEEKS ROUNDED UP                           
         BCTR  R1,0                ADJUST FOR ROUNDING                          
         DROP  WKD                                                              
*                                                                               
         LA    R3,WORK2                                                         
         SLL   R1,1                MULTIPLY BY 2                                
         AR    R3,R1               BUMP TO START OF NEXT X'03' ELEMENT          
         B     BGRD0060            IN THE GRID                                  
*                                                                               
BGRD0400 DS    0H                                                               
         XIT1                                                                   
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
* REINSERT SPOTS DETAILED IN 16 ELEMENTS.  FIND START DATE, NUMBER OF           
* WEEKS, AND THE NUMBER OF SPOTS/WEEK.                                          
*                                                                               
* USES IO4 FOR SCANNER WORK AREA.                                               
* USES IO4+1000 TO BUILD CREDIT AUDIT TRAIL ELEMENTS                            
***********************************************************************         
REMGRID  NTR1                                                                   
         L     RE,AIO4                                                          
         ICM   RF,15,=AL4(CONLENQ)                                              
         XCEF                                                                   
*                                                                               
         LA    R3,803                                                           
         GOTO1 SCANNER,DMCB,WORK3,AIO4,C',=,-'                                  
         CLI   DMCB+4,0                                                         
         BE    ERROR                                                            
*                                                                               
         L     R7,AIO4                                                          
         LA    R6,1000(R7)                                                      
CATD     USING RBUYCAEL,R6         USE X'16' CREDIT AUDIT TRAIL DSECT           
*                                  PROCESS START DATE                           
RGRID5   DS    0H                                                               
         LA    RE,12(R7)                                                        
         CLC   =C'CR=',12(R7)      SKIP CREDIT KEYWORD                          
         BNE   *+8                                                              
         LA    RE,15(R7)                                                        
         ST    RE,DMCB                                                          
         MVI   DMCB,1                                                           
         GOTO1 DATVAL,DMCB,,CRSTDT2                                             
*                                                                               
         MVC   DUB(4),ACOMFACS                                                  
         MVC   DUB+4(2),REPALPHA                                                
         GOTO1 (RFKFLT,VREPFACS),DMCB,RCONREC,TMPDATE,0,DUB                     
         MVC   CRSTDT2(2),TMPDATE     K START YEAR                              
         CLC   CRSTDT2+2(4),TMPDATE+2   BUY MMDD V K MMDD                       
         BNL   RGRID8                                                           
         CLC   CRSTDT2(2),TMPDATE2  START AND END YEARS SAME?                   
         BE    RGRIDERR                                                         
         MVC   CRSTDT2(2),TMPDATE2   USE K END YEAR                             
*                                                                               
RGRID8   DS    0H                                                               
         GOTO1 DATCON,DMCB,CRSTDT2,(3,USRSDATE)                                 
         GOTO1 DATCON,DMCB,CRSTDT2,(5,CRSTDT2)                                  
*                                                                               
         CLI   1(R7),0                                                          
         BNE   RGRID10             ANY END DATE??                               
*                                  GET NUMBER OF SPOTS TO BE CREDITED           
*                                                                               
* INPUT DATE IS IN THE FORM MMMDD(NNN) IE JAN7(3)                               
*                                                                               
         GOTO1 GETCRSPT,DMCB,12(R7)                                             
         XC    USREDATE,USREDATE                                                
         MVI   CRNUMWK,1                                                        
         B     RGRID200                                                         
*                                                                               
* CHECK WHICH FORMAT END DATE IS IN:                                            
*   MMMDD-MMMDD(NNN)                                                            
*   MMMDD-NNNW(NNN)                                                             
*   MMMDD-E(NNN)                                                                
*                                                                               
RGRID10  DS    0H                  CHECK FOR END WEEKS OPTION                   
         XC    CRFLAG,CRFLAG       CLEAR FLAGS                                  
         CLI   22(R7),C'E'         FORMAT IS MMMDD-E(NNN)                       
         BNE   RGRID50                                                          
*        CLI   23(R7),C'A'         CHECK FOR ALTERNATING WEEKS                  
*        BNE   *+8                                                              
*        OI    CRFLAG,CRFALTQ                                                   
*                                                                               
         MVC   DUB(4),ACOMFACS                                                  
         MVC   DUB+4(2),REPALPHA                                                
         GOTO1 (RFKFLT,VREPFACS),DMCB,RCONREC,WORK,0,DUB                        
         GOTO1 DATCON,DMCB,(0,WORK+6),(5,CRSTDT3)                               
         B     RGRID90                                                          
*                                                                               
RGRID50  DS    0H                                                               
         LA    R4,1                                                             
         CLI   23(R7),C'W'         WEEKS IND?                                   
         BE    RGRID60                                                          
         LA    R4,2                                                             
         CLI   24(R7),C'W'                                                      
         BNE   RGRID80                                                          
*                                                                               
RGRID60  DS    0H                                                               
         LA    R1,23(R4,R7)                                                     
*        CLI   0(R1),C'A'          CHECK FOR ALTERNATING WEEKS                  
*        BNE   *+8                                                              
*        OI    CRFLAG,CRFALTQ                                                   
*                                                                               
         LR    R1,R4                                                            
         LA    R2,22(R7)                                                        
*                                                                               
RGRID70  CLI   0(R2),C'0'          NUMERIC?                                     
         BL    RGRIDERR                                                         
         CLI   0(R2),C'9'                                                       
         BH    RGRIDERR                                                         
         LA    R2,1(R2)                                                         
         BCT   R4,RGRID70                                                       
*                                  NUMERIC WEEKS ENTERED                        
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                PACK WEEKS                                   
         PACK  DUB,22(0,R7)                                                     
         CVB   R4,DUB                                                           
         LTR   R4,R4                                                            
         BZ    RGRIDERR                                                         
         STC   R4,CRNUMWK                                                       
         B     RGRID90                                                          
*                                                                               
RGRID80  DS    0H                  FORMAT IS MMMDD-MMMDD(NN)                    
         GOTO1 DATVAL,DMCB,(1,22(R7)),CRSTDT3                                   
******                                                                          
         MVC   DUB(4),ACOMFACS                                                  
         MVC   DUB+4(2),REPALPHA                                                
         GOTO1 (RFKFLT,VREPFACS),DMCB,RCONREC,TMPDATE,0,DUB                     
         MVC   CRSTDT3(2),TMPDATE     K START YEAR                              
         CLC   CRSTDT3+2(4),TMPDATE+2   BUY MMDD V K MMDD                       
         BNL   RGRID85                                                          
         CLC   CRSTDT3(2),TMPDATE2  START AND END YEARS SAME?                   
         BE    RGRIDERR                                                         
         MVC   CRSTDT3(2),TMPDATE2   USE K END YEAR                             
RGRID85  DS    0H                  SAVE END DATE                                
******                                                                          
         GOTO1 DATCON,DMCB,(0,CRSTDT3),(3,USREDATE)                             
         GOTO1 DATCON,DMCB,(0,CRSTDT3),(5,CRSTDT3)                              
*                                  GET NUMBER OF SPOTS TO BE CREDITED           
RGRID90  DS    0H                                                               
         GOTO1 GETCRSPT,DMCB,22(R7)                                             
*                                                                               
RGRID100 DS    0H                  GET NUMBER OF WEEKS                          
         OC    CRNUMWK,CRNUMWK                                                  
         BNZ   RGRID200                                                         
*                                                                               
         L     RE,ACOMFACS         CALL COMFACS FOR ADDRESS OF PERVAL           
         USING COMFACSD,RE                                                      
         L     RF,CPERVAL                                                       
         DROP  RE                                                               
*                                                                               
         MVI   CRSTDT2+8,C'-'                                                   
         GOTO1 (RF),DMCB,(17,CRSTDT2),WORK2+120                                 
         CLI   DMCB+4,0                                                         
         BE    RGRID150                                                         
         LA    R3,474              MUST FALL WITHIN EXISTING DATES              
         B     RGRIDERR                                                         
*                                                                               
*                                                                               
WKD      USING PERVALD,WORK2+120                                                
RGRID150 DS    0H                                                               
         ZICM  R1,WKD.PVALNWKS,2   # WEEKS ROUNDED UP                           
         MVC   CRNUMWK,WKD.PVALNWKS+1                                           
         BCTR  R1,0                ADJUST FOR ROUNDING                          
         DROP  WKD                                                              
*                                                                               
RGRID200 DS    0H                  REMOVE SPOTS FROM GRID                       
         L     RE,ACOMFACS         CALL COMFACS FOR ADDRESS OF PERVAL           
         USING COMFACSD,RE                                                      
         L     RF,CPERVAL                                                       
         DROP  RE                                                               
*                                                                               
         MVI   CRSTDT+8,C'-'                                                    
         GOTO1 (RF),DMCB,(17,CRSTDT),WORK2+120                                  
         CLI   DMCB+4,0                                                         
         BE    RGRID220                                                         
         LA    R3,474              MUST FALL WITHIN EXISTING DATES              
         B     RGRIDERR                                                         
*                                                                               
WKD      USING PERVALD,WORK2+120                                                
RGRID220 DS    0H                                                               
         ZICM  R4,WKD.PVALNWKS,2   # WEEKS ROUNDED UP                           
         BCTR  R4,0                ADJUST FOR ROUNDING                          
         DROP  WKD                                                              
*                                                                               
RGRID300 DS    0H                                                               
*                                                                               
*******                                                                         
* BUILD AUDIT TRAIL ELEMENT                                                     
*******                                                                         
         MVI   CATD.RBUYCACD,RBUYCACQ                                           
         MVI   CATD.RBUYCALN,RBUYCALQ                                           
         LR    R2,R4                                                            
         MHI   R2,7                                                             
         GOTO1 DATVAL,DMCB,CRSTDT,TMPDATE                                       
         GOTO1 ADDAY,DMCB,TMPDATE,TMPDATE,(R2)                                  
*        GOTO1 DATCON,DMCB,(0,TMPDATE),(3,CATD.RBUYCASD)                        
         MVC   CATD.RBUYCASD,USRSDATE                                           
         MVC   CATD.RBUYCAED,USREDATE                                           
         MVC   CATD.RBUYCANW,CRNUMWK                                            
         MVC   CATD.RBUYCASP,CRNUMSPT                                           
         GOTO1 DATCON,DMCB,(5,0),(2,CATD.RBUYCACT)                              
         TM    CRFLAG,CRFALTQ                                                   
         BZ    *+8                                                              
         OI    CATD.RBUYCAFL,X'80' FLAG ALTERNATING                             
         LA    R6,RBUYCALQ(R6)     BUMP TO NEXT SLOT                            
*                                                                               
         LA    R2,WORK2                                                         
         SLL   R4,1                MULTIPLY BY 2                                
         AR    R2,R4               BUMP TO STARTING CELL FOR THIS DATE          
         ZIC   R4,CRNUMWK     NUMBER OF WEEKS TO PROCESS                        
         LA    R3,811              NO SPOTS LEFT TO BE CREDITED                 
*                                                                               
RGRID310 DS    0H                                                               
         CLC   CRNUMSPT,0(R2)                                                   
         BH    RGRIDERR                                                         
         ZIC   RE,0(R2)                                                         
         ZIC   RF,CRNUMSPT                                                      
         SR    RE,RF                                                            
         STC   RE,0(R2)                                                         
         LTR   RE,RE                                                            
         BNZ   *+8                                                              
         OI    1(R2),X'01'         IF ALL CREDITED OUT, FLAG OVERRIDE           
*                                  SO WE'LL REBUILD THIS DATE WITH (0)          
         LA    R2,2(R2)                                                         
         TM    CRFLAG,CRFALTQ                                                   
         BZ    *+8                 ALTERNATING WEEKS BUMPS ONE MORE             
         LA    R2,2(R2)                                                         
         BCT   R4,RGRID310                                                      
*                                                                               
         LA    R7,32(R7)           NEXT SET OF DATES                            
         CLI   0(R7),0                                                          
         BNE   RGRID5                                                           
*                                                                               
RGRIDX   DS    0H                                                               
         XIT1                                                                   
*                                                                               
RGRIDERR DS    0H                                                               
         DC    H'0'                CAN'T PROCESS - DUMP                         
         B     ERROR                                                            
         DROP  CATD                                                             
         EJECT                                                                  
***********************************************************************         
* REBUILD X'03' ELEMENT FROM BUY GRID                                           
***********************************************************************         
BLDBUY   NTR1                                                                   
         LA    R6,RBUYREC                                                       
         MVI   ELCODE,X'02'                                                     
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
BBUY03   DS    0H                  POINT TO LAST X'02' ELEMENT TO GET           
         LR    R2,R6               END DAY                                      
         BAS   RE,NEXTEL                                                        
         BE    BBUY03                                                           
         LR    R6,R2                                                            
         USING RBUYDYEL,R6                                                      
         MVI   CRENDDAY,0     GET END DAY                                       
         MVN   CRENDDAY,RBUYDYIN                                                
         DROP  R6                                                               
*                                                                               
         LA    R6,RBUYREC                                                       
         MVI   ELCODE,X'03'                                                     
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING RBUYDTEL,R6         SAVE OFF BUY START DATE                      
         GOTO1 DATCON,DMCB,(3,RBUYDTST),(0,CRSTDT)                              
         DROP  R6                                                               
*                                                                               
* DELETE OLD X'03'S                                                             
*                                                                               
         GOTO1 VDELELEM,DMCB,(X'03',RBUYREC)                                    
*                                                                               
         LA    R6,WORK2                                                         
*                                                                               
         SR    R7,R7                                                            
*                                                                               
BBUY05   DS    0H                                                               
         LA    R4,1                                                             
         XC    CRBUYEL,CRBUYEL                                                  
WKD      USING RBUYDTEL,CRBUYEL                                                 
*                                                                               
         MVI   WKD.RBUYDTCD,X'03'                                               
         MVI   WKD.RBUYDTLN,11                                                  
*                                                                               
BBUY08   DS    0H                                                               
         CLI   0(R6),0                                                          
         BNE   BBUY10                                                           
         TM    1(R6),X'01'         OVERRIDE??                                   
         BZ    BBUY40                                                           
*                                                                               
BBUY10   DS    0H                                                               
         OC    WKD.RBUYDTST,WKD.RBUYDTST                                        
         BNZ   BBUY20              NEED TO FIND START DATE                      
         LTR   R7,R7                                                            
         BNZ   BBUY15                                                           
         MVC   WKD.RBUYDTST,CRSTDT                                              
         MVC   CRSTDT2,CRSTDT                                                   
         GOTO1 DATCON,DMCB,(0,CRSTDT),(3,WKD.RBUYDTST)                          
         B     BBUY20                                                           
*                                                                               
BBUY15   DS    0H                  SUBSEQUENT X'03'S NEED TO COMPUTE            
         LR    R2,R7               START DATES RELATIVE TO INITIAL              
         MHI   R2,7                FLIGHT START FOR THIS BUY                    
         GOTO1 ADDAY,DMCB,CRSTDT,CRSTDT2,(R2)                                   
         GOTO1 DATCON,DMCB,(0,CRSTDT2),(3,WKD.RBUYDTST)                         
*                                                                               
BBUY20   DS    0H                                                               
         CLC   0(1,R6),2(R6)                                                    
         BNE   BBUY23                                                           
         CLI   0(R6),0             INCASE ALL SPOTS CREDITED OUT FOR            
         BNE   BBUY21              THIS WEEK, CHECK IF ZERO SPOT                
         TM    3(R6),X'01'         OVERRIDE FLAG IS SET                         
         BZ    BBUY23                                                           
*                                                                               
BBUY21   DS    0H                                                               
         LA    R6,2(R6)                                                         
         LA    R4,1(R4)                                                         
         LA    R7,1(R7)                                                         
         CH    R7,=H'53'                                                        
         BL    BBUY08              BUMP TO NEXT CELL                            
*                                                                               
BBUY23   DS    0H                                                               
         MVC   WKD.RBUYDTNW,0(R6)                                               
         STC   R4,WKD.RBUYDTWK                                                  
*** CHECK IF OVERRIDE OR ALTERNATE WEEKS                                        
         OI    WKD.RBUYDTIN,X'80'  DEFAULT IS WEEKLY                            
         TM    1(R6),X'01'         OVERRIDE??                                   
         BZ    *+8                                                              
         OI    WKD.RBUYDTIN,X'01'                                               
*** CHECK IF OVERRIDE OR ALTERNATE WEEKS                                        
*** CHECK IF OVERRIDE OR ALTERNATE WEEKS                                        
         CLC   RBUYNW,0(R6)                                                     
         BE    BBUY25                                                           
         OI    WKD.RBUYDTIN,X'01'                                               
***********                                                                     
***********                                                                     
*                                                                               
* CALCULATE END DATE                                                            
*                                                                               
BBUY25   DS    0H                                                               
         ZIC   R2,WKD.RBUYDTWK                                                  
         MHI   R2,7                                                             
         GOTO1 ADDAY,DMCB,CRSTDT2,CRSTDT2,(R2)                                  
*                                                                               
         GOTO1 GETDAY,DMCB,CRSTDT2,FULL                                         
         ZIC   RE,CRENDDAY                                                      
         ZIC   RF,DMCB                                                          
         SR    RE,RF                                                            
         BZ    BBUY35                                                           
         BP    BBUY30                                                           
*                                                                               
* HANDLE OUT OF WEEK ROTATIONS                                                  
*                                                                               
         LR    R2,RE                                                            
         B     BBUY38                                                           
*                                                                               
BBUY30   DS    0H                                                               
         LA    R2,7                                                             
         LNR   R2,R2                                                            
         AR    R2,RE                                                            
         B     BBUY38                                                           
*                                                                               
BBUY35   DS    0H                  SAME START END DAY, BACK UP A WEEK           
         LA    R2,7                                                             
         LNR   R2,R2                                                            
*                                                                               
BBUY38   DS    0H                                                               
         GOTO1 ADDAY,DMCB,CRSTDT2,CRSTDT2,(R2)                                  
         GOTO1 DATCON,DMCB,(0,CRSTDT2),(3,WKD.RBUYDTED)                         
*                                                                               
***********                                                                     
***********                                                                     
         MVC   DMCB2(24),DMCB      SAVE SINCE TRASHED BY HELLO...               
         GOTO1 VADDELEM,DMCB,RBUYREC,WKD.RBUYDTEL                               
         MVC   DMCB(24),DMCB2      RESTORE                                      
*                                                                               
BBUY40   DS    0H                                                               
         LA    R6,2(R6)                                                         
         LA    R7,1(R7)                                                         
         CH    R7,=H'53'                                                        
         BL    BBUY05              BUMP TO NEXT CELL                            
*                                                                               
BBUYX    DS    0H                                                               
         XIT1                                                                   
         DROP  WKD                                                              
         EJECT                                                                  
***********************************************************************         
* PARSE INPUT STRING TO FIND NUMBER OF SPOTS TO BE CREDITED                     
***********************************************************************         
GETCRSPT NTR1                                                                   
         L     R2,0(R1)                                                         
*                                                                               
         SR    R4,R4                                                            
GCRS10   DS    0H                                                               
         LA    R1,0(R4,R2)                                                      
         CLI   0(R1),C'('                                                       
         BE    GCRS20                                                           
         LA    R4,1(R4)                                                         
         CH    R4,=H'20'                                                        
         BL    GCRS10                                                           
         LA    R3,808                                                           
         B     GCRSERR                                                          
*                                                                               
GCRS20   DS    0H                                                               
         MVI   0(R1),C' '          INSERT SEPARATOR                             
         XC    CRNUMSPT,CRNUMSPT                                                
*                                                                               
* GET SPOTS CREDITED - FORMAT IS JAN15(4) JAN15-JAN26(5) JAN5-E(6)              
*                           OR JAN15-8W(3)                                      
         LA    R3,NPWERR                                                        
         LA    R4,1(R4,R2)                                                      
         LR    RF,R4                                                            
         SR    R1,R1                                                            
*                                                                               
* CHECK NUMBER PER WEEK                                                         
*                                                                               
GCRS30   CLI   0(R4),C'0'                                                       
         BL    GCRSERR                                                          
         CLI   0(R4),C'9'                                                       
         BH    GCRSERR                                                          
*                                                                               
         LA    R4,1(R4)                                                         
         LA    R1,1(R1)                                                         
         CH    R1,=H'3'                                                         
         BH    GCRSERR                                                          
         CLI   0(R4),C')'                                                       
         BNE   GCRS30                                                           
*                                                                               
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         PACK  DUB,0(0,RF)                                                      
         CVB   R1,DUB                                                           
         CH    R1,=H'255'                                                       
         BH    GCRSERR                                                          
*                                                                               
         LTR   R1,R1                                                            
         BZ    GCRSERR                                                          
         STC   R1,CRNUMSPT    NUMBER OF SPOTS TO BE CREDITED                    
*                                                                               
         LA    R3,816              MUST BE SEPARATED BY A COMMA                 
         CLI   1(R4),C' '          THERE MUST NOT BE ANY INPUT AFTER            
         BE    GCRSEXIT            THE NUMBER OF SPOTS                          
*                                                                               
GCRSERR  DS    0H                                                               
         DC    H'0'                CAN'T PROCESS - DUMP                         
         B     ERROR                                                            
GCRSEXIT DS    0H                                                               
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
