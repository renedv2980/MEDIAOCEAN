*          DATA SET RESPL256   AT LEVEL 037 AS OF 05/01/02                      
*              ROUTINE TO INITIALIZE PAD VALUES                                 
         SPACE 3                                                                
PADINIT  NTR1                                                                   
         XC    PADLINES,PADLINES                                                
         MVC   PADMAX,=H'100'      ASSUME MAX OF 100                            
         MVC   PADWORST,=X'FFFF'                                                
         LA    R2,RNKMANYH                                                      
         CLI   5(R2),0                                                          
         BE    XIT                                                              
         TM    4(R2),X'08'         UNLESS NUMBER IS SPECIFIED                   
         BNO   XIT                                                              
         ZIC   R3,5(R2)                                                         
         BCTR  R3,0                                                             
         EX    R3,*+8                                                           
         B     *+10                                                             
         PACK  DUB,8(0,R2)                                                      
         CVB   R0,DUB                                                           
         CH    R0,=H'100'                                                       
         BH    XIT                                                              
         STH   R0,PADMAX                                                        
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO GET HUT FACTOR                                        
         SPACE 3                                                                
GETHUT   NTR1                                                                   
         MVC   HUTKEYSV,KEYSAVE                                                 
         USING NPGELEM,R6                                                       
         LA    R4,KEY                                                           
         USING CTHREC,R4                                                        
         XC    CTHKEY,CTHKEY                                                    
         MVI   CTHTYP,C'H'                                                      
         MVC   CTHDAY,NPGDAY                                                    
         MVC   CTHTIME,NPGTIME                                                  
         MVC   CTHYEAR,BOOK+1                                                   
         MOVE  (HEAD1,1000),IO     SAVE PROGRAM RECORD                          
         MVI   DMFILE,C'C'                                                      
         GOTO1 HIGH                                                             
         CLC   KEY(25),KEYSAVE                                                  
         BE    GETHUT2                                                          
         MVC   KEY(25),KEYSAVE                                                  
         ZIC   R1,BOOK+1                                                        
         BCTR  R1,0                                                             
         STC   R1,CTHYEAR                                                       
         GOTO1 HIGH                                                             
         CLC   KEY(25),KEYSAVE                                                  
         BE    GETHUT2                                                          
         MVC   HUT,=H'1000'                                                     
         B     GETHUT4                                                          
         SPACE 2                                                                
GETHUT2  LA    R6,IO                                                            
         MVI   ELCODE,X'90'                                                     
         BAS   RE,GETEL                                                         
         BAS   RE,DIGHUT                                                        
         SPACE 2                                                                
GETHUT4  MVI   DMFILE,C'S'                                                      
         MVC   KEYSAVE,HUTKEYSV                                                 
         MVC   DATADISP,=H'24'                                                  
         MOVE  (IO,1000),HEAD1                                                  
         MVI   HEAD1,C' '                                                       
         MOVE  (HEAD1+1,999),HEAD1                                              
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO DIG OUT HUT FACTOR                                    
         SPACE 3                                                                
DIGHUT   NTR1                                                                   
         USING CTHUTD,R6                                                        
         ZIC   R2,BOOK+2                                                        
         ZIC   R3,BOOK+5                                                        
         LTR   R3,R3                                                            
         BNZ   *+6                                                              
         LR    R3,R2                                                            
         SR    R1,R1                                                            
         SR    R4,R4                                                            
         SPACE 2                                                                
DIGHUT2  LR    R5,R2                                                            
         BCTR  R5,0                                                             
         SLL   R5,1                                                             
         LA    R5,CTHUTLST(R5)                                                  
         AH    R1,0(R5)                                                         
         LA    R4,1(R4)                                                         
         CR    R2,R3                                                            
         BNL   DIGHUT4                                                          
         LA    R2,1(R2)                                                         
         B     DIGHUT2                                                          
         SPACE 2                                                                
DIGHUT4  SR    R0,R0                                                            
         SLDA  R0,1                                                             
         DR    R0,R4                                                            
         AH    R1,=H'1'                                                         
         SRA   R1,1                                                             
         STH   R1,HUT                                                           
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO BUILD A PAD RECORD                                    
         SPACE 3                                                                
FILL     NTR1                                                                   
         XC    WORK,WORK           BUILD A LINE IN WORK                         
         LA    R9,WORK                                                          
         USING PADD,R9                                                          
         CLI   DMSOURCE,C'P'                                                    
         BNE   *+12                                                             
         BAS   RE,PROGFILL                                                      
         B     FILLB                                                            
         L     R4,ADBLOCK          POINT TO USER DBLOCK                         
         USING DBLOCKD,R4                                                       
         MVC   PADSTAT,DBSELSTA    STATION                                      
         GOTO1 DEFINE,DMCB,=C'PURE',DBLOCK,OUTAREA                              
         MVC   PADINV(2),OUTAREA   PURE NUMBER                                  
         GOTO1 (RF),(R1),=C'PROGRAM',DBLOCK,OUTAREA                             
         MVC   PADPROG,OUTAREA     PROGRAM NAME                                 
         SPACE 2                                                                
FILLB    ZIC   R1,NUMDEMS                                                       
         MH    R1,=H'3'                                                         
         LA    R1,DEMOS(R1)                                                     
         MVI   0(R1),X'FF'                                                      
         LA    R2,DEMOS            POINT R2 AT DEMO LIST                        
         LA    R3,PADDEM1                                                       
         MVC   SAVEBK,DBACTBK      PROTECT DBACTBK                              
         GOTO1 DEMOUT,DMCB,(C'L',(R2)),DBLOCK,BLOCK+256                         
         CLI   DBERROR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   DBACTBK,SAVEBK      RESTORE IT                                   
         LA    R5,BLOCK+256        NOW POINT R5 TO VALUES                       
         ZIC   R4,NUMDEMS                                                       
         SPACE 2                                                                
FILL2A   TM    0(R2),X'20'                                                      
         BO    FILL2B                                                           
         MVC   0(2,R3),2(R5)                                                    
         CLI   SORN,C'N'                                                        
         BNE   FILL3                                                            
         CLI   1(R2),C'R'                                                       
         BE    FILL3                                                            
         CLI   1(R2),C'S'                                                       
         BE    FILL3                                                            
         CLI   1(R2),C'V'                                                       
         BE    FILL3                                                            
         CLI   1(R2),C'X'                                                       
         BE    FILL3                                                            
         L     R1,0(R5)                                                         
         AH    R1,=H'2'                                                         
         SRL   R1,2                SHIFT FOR HALF-WORD MAX                      
         STH   R1,0(R3)                                                         
         B     FILL3                                                            
         SPACE 2                                                                
FILL2B   OC    COST,COST           HANDLE CPP/CPM FACILITY                      
         BZ    FILL3                                                            
         L     R1,COST                                                          
         OC    0(4,R5),0(R5)                                                    
         BZ    FILL3                                                            
         LA    R0,200              CPM IS IN CENTS                              
         CLI   1(R2),C'T'                                                       
         BE    *+8                                                              
         LA    R0,20               CPP WILL BE CARRIED IN $                     
         MR    R0,R0                                                            
         D     R0,0(R5)                                                         
         AH    1,=H'1'                                                          
         SRA   R1,1                                                             
         LCR   R1,R1               CARRY THEM COMPLEMENTED                      
         STH   R1,0(R3)                                                         
         SPACE 2                                                                
FILL3    LA    R2,3(R2)            NEXT DEMO                                    
         LA    R3,3(R3)            NEXT PAD AREA                                
         LA    R5,4(R5)            NEXT DEMO VALUE                              
         BCT   R4,FILL2A                                                        
         EJECT                                                                  
*              NOW SEE IF IT QUALIFIES                                          
         SPACE 3                                                                
FILL4    CLC   PADLINES,PADMAX     IS PAD FULL                                  
         BE    FILL6                                                            
         CLC   PADDEM1,PADWORST    NO                                           
         BH    *+10                                                             
         MVC   PADWORST,PADDEM1    UPDATE WORST SO FAR                          
         LH    R1,PADLINES                                                      
         LA    R1,1(R1)                                                         
         STH   R1,PADLINES         UPDATE LINES SO FAR                          
         B     FILL8                                                            
         SPACE 2                                                                
FILL6    CLC   PADDEM1,PADWORST    TABLE IS FULL                                
         BL    XIT                 CHECK IF THIS IS WORSE THAN WORST            
         SPACE 2                                                                
FILL8    LH    R2,PADLINES         FIND N'TH POSITION                           
         BCTR  R2,0                                                             
         MH    R2,=H'40'                                                        
         LA    R2,PAD(R2)                                                       
         MVC   0(40,R2),WORK       AND PUT IN THIS ITEM                         
         CLC   PADLINES,PADMAX                                                  
         BL    XIT                                                              
         LH    R2,PADLINES                                                      
         GOTO1 XSORT,PARAS,(1,PAD),(R2),40,2,22                                 
         BCTR  R2,0                                                             
         MH    R2,=H'40'                                                        
         LA    R2,PAD(R2)                                                       
         MVC   PADWORST,22(R2)                                                  
         B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
*              FILL PAD FROM PROGRAM RECORD                                     
         SPACE 3                                                                
PROGFILL NTR1                                                                   
         LA    R9,WORK                                                          
         USING PADD,R9                                                          
         LA    R6,IO                                                            
         MVI   ELCODE,X'92'                                                     
         BAS   RE,GETEL                                                         
         USING NPGELEM,R6                                                       
         MVC   PADSTAT,0(R2)                                                    
         MVC   DUB,NPGTIME                                                      
         LH    R1,DUB              CONVERT START TIME TO 1/4 HOUR               
         SR    R0,R0                                                            
         D     R0,=F'100'                                                       
         CH    R1,=H'5'                                                         
         BH    *+8                                                              
         LA    R1,24(R1)                                                        
         SH    R1,=H'6'                                                         
         CH    R1,=H'20'                                                        
         BNH   *+8                                                              
         LA    R1,20                                                            
         SLL   R1,2                                                             
         LTR   R0,R0                                                            
         BZ    *+8                                                              
         LA    R1,2(R1)                                                         
         STC   R1,PADINV                                                        
         IC    R1,NPGRDAY                                                       
         SLL   R1,4                                                             
         STC   R1,PADINV+1                                                      
         MVC   PADPROG,NPGNAME                                                  
         OC    SPECDATE,SPECDATE                                                
         BZ    XIT                                                              
         MVC   PADPROG,SPACES                                                   
         LA    R4,IO                                                            
         USING NPGRECD,R4                                                       
         MVC   PADPROG(6),NPGKPROG                                              
         MVC   PADPROG+7(2),=C'ON'                                              
         GOTO1 DATCON,DMCB,(2,SPECDATE),(4,PADPROG+10)                          
         GOTO1 SQUASHER,DMCB,PADPROG,16                                         
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO RANK THE TABLE                                        
         SPACE 3                                                                
RANK     NTR1                                                                   
         USING PADD,R9                                                          
         LA    R9,PAD                                                           
         ZIC   R3,NUMDEMS                                                       
         BCTR  R3,0                                                             
         MH    R3,=H'3'                                                         
         LA    R2,PADDEM1                                                       
         SR    R2,R9               DISPLACEMENT TO FIRST DEMO                   
         AR    R2,R3               DISPLACEMENT TO LAST                         
         LA    R3,DEMOS(R3)        R3=DEMO                                      
         ZIC   R4,NUMDEMS                                                       
         LH    R5,PADLINES                                                      
         SPACE 2                                                                
RANK2    GOTO1 XSORT,PARAS,(1,PAD),(R5),40,2,(R2)                               
         BAS   RE,RANK6                                                         
         SPACE 2                                                                
RANK4    SH    R2,=H'3'                                                         
         SH    R3,=H'3'                                                         
         BCT   R4,RANK2                                                         
         B     XIT                                                              
         SPACE 2                                                                
*                                  ROUTINE TO PUT IN RANK NUMBERS               
RANK6    NTR1                                                                   
         LA    R2,PAD(R2)          R2=A(COLUMN)                                 
         LA    R3,1                R3=PLACE NUMBER                              
         LA    R4,1                R4=ACTUAL NUMBER                             
*                                  R5=N'LINES                                   
         SR    R6,R6               R6=CURRENT VALUE                             
         SPACE 2                                                                
RANK8    STC   R4,2(R2)                                                         
         CH    R6,0(R2)            IS THIS WORSE THAN PREVIOUS                  
         BE    RANK10                                                           
         LR    R3,R4               THEN SET PLACE TO ACTUAL                     
         LH    R6,0(R2)            AND SAVE THIS VALUE                          
         B     RANK12                                                           
         SPACE 2                                                                
RANK10   STC   R3,2(R2)            TIE - USE PREVIOUS PLACE                     
         SPACE 2                                                                
RANK12   LA    R2,40(R2)                                                        
         LA    R4,1(R4)                                                         
         BCT   R5,RANK8                                                         
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO PRINT A REPORT FROM PAD                               
         SPACE 3                                                                
SPLAT    NTR1                                                                   
         LA    R9,PAD                                                           
         USING PADD,R9                                                          
         LH    R3,PADLINES                                                      
         SPACE 2                                                                
SPLAT2   LA    R2,10               PRINT IN BLOCKS OF 10                        
         CR    R3,R2               OR LESS                                      
         BH    *+6                                                              
         LR    R2,R3                                                            
         LA    R1,1(R2)                                                         
         STC   R1,ALLOWLIN         ALLOW FOR 1 MORE LINE                        
         SPACE 2                                                                
SPLAT4   MVI   SPACING,1                                                        
         BAS   RE,FORMAT                                                        
         GOTO1 SPOOL,PARAS,(R8)                                                 
         XC    0(40,R9),0(R9)                                                   
         BCTR  R3,0                                                             
         LA    R9,40(R9)                                                        
         MVI   ALLOWLIN,0                                                       
         BCT   R2,SPLAT4                                                        
         GOTO1 SPOOL,PARAS,(R8)                                                 
         CH    R3,=H'0'                                                         
         BNE   SPLAT2                                                           
         B     XIT                                                              
         SPACE 2                                                                
*                                  FILL A PRINT LINE                            
FORMAT   NTR1                                                                   
         MVC   P+1(4),PADSTAT      STATION                                      
         MVC   P+8(16),PADPROG     PROGRAM                                      
*                                                                               
         GOTO1 INVEDIT,PARAS,PADINV,WORK                                        
         MVC   P+25(3),WORK        DAY                                          
         MVC   P+29(6),WORK+3      TIME                                         
         LA    R2,PADDEM1          SET UP TO EDIT DEMOS                         
         LA    R3,P+36                                                          
         LA    R4,DEMOS                                                         
         ZIC   R5,NUMDEMS                                                       
         SPACE 2                                                                
FORMAT2  TM    0(R4),X'20'         CPP/CPM EDITING                              
         BNO   FORMAT3                                                          
         LH    R1,0(R2)                                                         
         LCR   R1,R1               (UN-COMPLEMENT)                              
         ST    R1,SAVCPM                                                        
         EDIT  (R1),(6,DMCB),FLOAT=$                                            
         MVC   0(5,R3),DMCB+1                                                   
         CLI   1(R4),C'T'                                                       
         BNE   FORMAT6                                                          
         L     R1,SAVCPM                                                        
         EDIT  (R1),(8,DMCB),2,FLOAT=$                                          
         L     R1,SAVCPM                                                        
         MVC   0(5,R3),DMCB                                                     
         CH    R1,=H'9999'                                                      
         BH    FORMAT6                                                          
         MVC   0(5,R3),DMCB+3                                                   
         B     FORMAT6                                                          
         SPACE 2                                                                
FORMAT3  CLI   1(R4),C'S'                                                       
         BE    FORMAT4                                                          
         CLI   1(R4),C'R'                                                       
         BE    FORMAT4                                                          
         CLI   1(R4),C'V'                                                       
         BE    FORMAT4                                                          
         CLI   1(R4),C'C'                                                       
         BE    FORMAT4                                                          
         CLI   1(R4),C'X'                                                       
         BE    FORMAT4                                                          
         EDIT  (2,0(R2)),(5,0(R3))                                              
         CLI   SORN,C'N'                                                        
         BNE   FORMAT6                                                          
         XC    DUB,DUB                                                          
         MVC   DUB+2(2),0(R2)                                                   
         L     R1,DUB                                                           
         SLL   R1,2                                                             
         EDIT  (R1),(5,0(R3))                                                   
         B     FORMAT6                                                          
         SPACE 2                                                                
FORMAT4  EDIT  (2,0(R2)),(5,0(R3)),1                                            
         SPACE 2                                                                
FORMAT6  EDIT  (1,2(R2)),(3,6(R3)),ALIGN=LEFT                                   
         SPACE 2                                                                
FORMAT8  LA    R2,3(R2)                                                         
         LA    R3,11(R3)                                                        
         LA    R4,3(R4)                                                         
         BCT   R5,FORMAT2                                                       
         B     XIT                                                              
         EJECT                                                                  
*              HEAD HOOK                                                        
         SPACE 3                                                                
         USING *,RF                                                             
         DS    0H                                                               
HOOK     NTR1                                                                   
         L     RE,4(RD)                                                         
         CLC   0(4,RE),=C'SPUL'                                                 
         BE    *+12                                                             
         L     RE,4(RE)                                                         
         B     *-14                                                             
         LM    RE,RC,12(RE)                                                     
         DROP  RF                                                               
         MVC   H4+10(6),RNKSTAT                                                 
         MVC   H4+17(24),SAVEMKT                                                
         CLC   RCPROG(2),=C'NT'                                                 
         BNE   *+10                                                             
         MVC   H4(42),SPACES                                                    
         MVC   H4+58(3),RNKSRCE                                                 
         MVC   H4+62(12),RNKBOOK                                                
         LA    R2,DEMOS                                                         
         LA    R3,H7+36                                                         
         ZIC   R5,NUMDEMS                                                       
         SPACE 2                                                                
HOOK2    GOTO1 DEMCON,PARAS,(0,(R2)),(7,WORK),DEMOTAB                           
         MVC   1(7,R3),WORK                                                     
         MVC   132(5,R3),WORK+7                                                 
         MVC   138(3,R3),=C'RNK'                                                
         LA    R2,3(R2)                                                         
         LA    R3,11(R3)                                                        
         BCT   R5,HOOK2                                                         
         SPACE 2                                                                
XIT      XIT1                                                                   
         SPACE 2                                                                
         GETEL (R6),DATADISP,ELCODE                                             
BUMP     ZIC   R0,0(R2)                                                         
         AR    R2,R0                                                            
         BR    RE                                                               
         SPACE 1                                                                
NETWEEK  DS    A                   DUMMIES                                      
NETUNIV  DS    A                                                                
         EJECT                                                                  
*              DSECT TO COVER RECORDS IN PAD                                    
         SPACE 3                                                                
PADD     DSECT                                                                  
PADREC   DS    0CL40                                                            
PADSTAT  DS    CL4                                                              
PADPROG  DS    CL16                                                             
PADINV   DS    CL2                                                              
PADDEM1  DS    CL2                                                              
PADRNK1  DS    CL1                                                              
PADDEM2  DS    CL2                                                              
PADRNK2  DS    CL1                                                              
PADDEM3  DS    CL2                                                              
PADRNK3  DS    CL1                                                              
PADDEM4  DS    CL2                                                              
PADRNK4  DS    CL1                                                              
PADDEM5  DS    CL2                                                              
PADRNK5  DS    CL1                                                              
PADDEM6  DS    CL2                                                              
PADRNK6  DS    CL1                                                              
         PRINT OFF                                                              
DBLOCKD  DSECT                                                                  
       ++INCLUDE DEDBLOCK                                                       
       ++INCLUDE RESPLWORKD                                                     
       ++INCLUDE DDSPOOLD                                                       
       ++INCLUDE RESPLFFD                                                       
         ORG   CONTAGH                                                          
