*          DATA SET PPSECURITY AT LEVEL 009 AS OF 05/01/02                      
***************************************************************                 
*                                                             *                 
**                                                           **                 
***                                                         ***                 
****  DDS SECURITY                                         ****                 
****     PASSED TO PGM                                     ****                 
****           PARM 1 ADDRESS OF TWA                       ****                 
****                2 ADDRESS OF CLIENT HEADER             ****                 
****                3 ADDRESS OF VCOMFACS OR VDATAMGR      ****                 
***       ON RETURN CONDITION CODE = IS OK -- NE IS ERROR   ***                 
*                                                            **                 
**                                                            *                 
***************************************************************                 
         SPACE 2                                                                
SECURITY NTR1                                                                   
         L     RA,0(R1)                                                         
         LA    R4,6(RA)                                                         
         L     R5,8(R1)                                                         
         OC    0(2,R4),0(R4)     ANY RESTRICTIONS                               
         BZ    SECURT4           NO - GETOUT                                    
*                                                                   L03         
         CLI   0(R4),C'$'        OFFICE CLIENT LIST                 L03         
         BNE   SECURTA                                              L03         
         BAS   RE,PPCLIVER         CHECK OFFICE SECURITY            L03         
         BE    SECURT4                                              L03         
         BNE   SECURTX             ERROR                            L03         
SECURTA  DS    0H                                                   L03         
*                                                                   L03         
         CLI   0(R4),C'*'          OFFICE RESTRICTION                           
         BE    SECURT3                                                          
         CLC   0(3,R4),PCLTKCLT    CLIENT RESTRICTION                           
         BE    SECURT4                                                          
         B     SECURTX             ERROR                                        
SECURT3  DS    0H                                                               
         CLI   PCLTOFF+2,C'*'      BYPASS TEST                                  
         BE    SECURT4                                                          
         CLC   1(1,R4),PCLTOFF     SAME OFFICE                                  
         BNE   SECURTX                                                          
SECURT4  CR    RE,RE                                                            
SECURTXT XIT1                                                                   
SECURTX  LTR   RE,RE                                                            
         B     SECURTXT                                                         
*                                                                               
*       *************************                                               
******  TEST OFFICE LIST SECURITY  ******                                       
*       *************************                                               
         SPACE 2                                                                
*                  / **************************\                                
PPCLIVER NTR1 ***** NOTE- I/O AREA M/B IN AREC ******                           
*                  \ **************************/                                
         SPACE 2                                                                
         XC    DMCB(12),DMCB                                                    
         MVC   DMCB+4(4),=X'D9000A38'  GET OFFICER ADDRESS                      
         GOTO1 VCALLOV,DMCB                                                     
         CLI   4(R1),255                                                        
         BNE   *+6                                                              
         DC    H'0'                                                             
         XC    XWORKX,XWORKX                                                    
         LA    R1,XWORKX                                                        
         USING OFFICED,R1                                                       
         MVI   OFCSYS,C'P'                                                      
         MVC   OFCAUTH,6(RA)                                                    
         MVC   OFCAGY,AGYALPHA                                                  
         MVC   OFCOFC,PCLTOFF                                                   
         DROP  R1                                                               
         L     RF,DMCB                                                          
         PRINT GEN                                                              
         GOTO1  (RF),DMCB,XWORKX,(R5)                                           
         PRINT NOGEN                                                            
         CLI   0(R1),0                                                          
         XIT1                                                                   
***                                                                             
XWORKX   DS    CL16                                                             
*******************************************************                         
*******************************************************                         
