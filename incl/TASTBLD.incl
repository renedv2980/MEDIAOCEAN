*          DATA SET TASTBLD    AT LEVEL 002 AS OF 04/08/14                      
***********************************************************************         
*        INCLUDED BY TAGENFC AND TAGEN14                              *         
***********************************************************************         
                                                                                
***********************************************************************         
*        ROUTINE TO BUILD INITIAL AGENCY/CLIENT TABLE                 *         
*        ON ENTRY ... AIO=A(PRIMARY STAFF RECORD)                     *         
***********************************************************************         
                                                                                
BLDTBL   NTR1                                                                   
         MVI   BS2LSSEQ,1          INITIALIZE WSSVR BLOCK COUNTER               
*                                                                               
         L     R3,ACTBL            R3=A(INITIAL AGY/CLI TABLE AREA)             
         BAS   RE,CLRTBL           CLEAR INITIAL AGY/CLI TABLE AREA             
         B     BTBL20                                                           
*                                                                               
         USING TLSTD,RE                                                         
BTBL10   GOTO1 SEQ                 READ ALL RECORDS FOR THIS STAFF              
         LA    RE,KEY                                                           
         CLC   KEY(TLSTSSEQ-TLSTD),SVKEY                                        
         BNE   BTBL70                                                           
         GOTO1 GETREC                                                           
         DROP  RE                                                               
*                                                                               
         USING TAVAD,R4                                                         
BTBL20   L     R4,AIO              READ ALL AGENCY/CLIENT ELEMENTS              
         MVI   ELCODE,TAVAELQ                                                   
         BAS   RE,GETEL                                                         
         B     *+8                                                              
BTBL30   BAS   RE,NEXTEL                                                        
         BNE   BTBL10                                                           
*                                                                               
         XR    RE,RE                                                            
         ZIC   RF,TAVALEN          RF=NUMBER OF CLIENTS IN ELEMENT              
         SHI   RF,TAVALNQ                                                       
         LTR   RF,RF                                                            
         BNZ   BTBL40                                                           
         LHI   RF,TAVALNQ                                                       
BTBL40   D     RE,=A(L'TAVACLI)                                                 
*                                                                               
         LA    R5,TAVACLI          RE=A(CURRENT CLIENT IN ELEMENT)              
*                                                                               
BTBL50   MVC   0(ACCLI-ACTBLD,R3),TAVASTAT                                      
         CLI   TAVALEN,TAVALNQ                                                  
         BE    *+10                                                             
         MVC   ACCLI-ACTBLD(L'ACCLI,R3),0(R5)                                   
         LA    R3,ACLNQ(R3)                                                     
         MVI   0(R3),X'FF'         ADD CURRENT CLIENT TO TABLE                  
*                                                                               
         C     R3,ACTBLX           IF THIS CLIENT IS THE LAST                   
         BL    BTBL60              TO FIT                                       
         BAS   RE,SAVTBL           SAVE INITIAL TABLE                           
         L     R3,ACTBL                                                         
         BAS   RE,CLRTBL           THEN CLEAR IT                                
*                                                                               
BTBL60   MVI   0(R3),X'FF'                                                      
*                                                                               
         LA    R5,L'TAVACLI(R5)                                                 
         BCT   RF,BTBL50           BUMP TO NEXT CLIENT IN ELEMENT               
         B     BTBL30                                                           
         DROP  R4                                                               
*                                                                               
BTBL70   BAS   RE,SAVTBL           SAVE INITIAL TABLE                           
         B     XIT                                                              
*                                                                               
***********************************************************************         
*        ROUTINE TO CLEAR INITIAL AGENCY/CLIENT TABLE AREA            *         
*        ON ENTRY ... R3=A(INITIAL AGENCY/CLIENT TABLE AREA)          *         
***********************************************************************         
                                                                                
CLRTBL   NTR1                                                                   
         LR    RE,R3                                                            
CTBL10   XC    0(250,RE),0(RE)                                                  
         LA    RE,250(RE)                                                       
         C     RE,ACTBLX                                                        
         BL    CTBL10                                                           
         MVI   0(R3),X'FF'                                                      
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
*        ROUTINE TO SAVE INITIAL AGY/CLI TABLE INTO WSSVR BLOCK       *         
***********************************************************************         
                                                                                
SAVTBL   NTR1                                                                   
         USING FAWSSVRD,R1                                                      
         LA    R1,BS2LWBLK                                                      
         MVC   FAWSTOKN(3),=CL3'INI'                                            
         MVC   FAWSTOKN+3(1),BS2LSSEQ                                           
         MVI   FAWSACTN,FAWSASVE                                                
         LHI   R0,ACTBLNQ                                                       
         STH   R0,FAWSLEN          SAVE INITIAL AGY/CLI TABLE TABLE             
         MVC   FAWSADR,ACTBL                                                    
         GOTO1 WSSVR,(R1)                                                       
         CLI   FAWSRTN,0                                                        
         BE    *+6                                                              
         DC    H'00'                                                            
         DROP  R1                                                               
*                                                                               
         ZIC   RE,BS2LSSEQ         BUMP WSSVR BLOCK COUNTER                     
         AHI   RE,1                                                             
         STC   RE,BS2LSSEQ                                                      
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
*        ROUTINE TO RESTORE INITIAL AGY/CLI TABLE FROM WSSVR          *         
***********************************************************************         
                                                                                
GETTBL   NTR1                                                                   
         USING FAWSSVRD,R1                                                      
         LA    R1,BS2LWBLK                                                      
         MVC   FAWSTOKN(3),=C'INI'                                              
         MVC   FAWSTOKN+3(1),BS2LSSEQ                                           
         MVI   FAWSACTN,FAWSARST   RECALL INITIAL AGENCY/CLIENT TABLE           
         XC    FAWSLEN,FAWSLEN     VIA WSSVR                                    
         MVC   FAWSADR,ACTBL                                                    
         GOTO1 WSSVR,(R1)                                                       
         CLI   FAWSRTN,0                                                        
         BE    *+6                                                              
         DC    H'00'                                                            
         DROP  R1                                                               
*                                                                               
         ZIC   RE,BS2LSSEQ         BUMP WSSVR BLOCK COUNTER                     
         AHI   RE,1                                                             
         STC   RE,BS2LSSEQ                                                      
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
*        ROUTINE TO CHANGE INITIAL AGY/CLI TABLE FROM WSSVR           *         
***********************************************************************         
                                                                                
CHGTBL   NTR1                                                                   
         ZIC   RE,BS2LSSEQ         BUMP WSSVR BLOCK COUNTER                     
         SHI   RE,1                                                             
*                                                                               
         USING FAWSSVRD,R1                                                      
         LA    R1,BS2LWBLK                                                      
         MVC   FAWSTOKN(3),=CL3'INI'                                            
         STC   RE,FAWSTOKN+3                                                    
         MVI   FAWSACTN,FAWSASVE                                                
         LHI   R0,ACTBLNQ                                                       
         STH   R0,FAWSLEN          CHANGE INITIAL AGY/CLI TABLE TABLE           
         MVC   FAWSADR,ACTBL                                                    
         GOTO1 WSSVR,(R1)                                                       
         CLI   FAWSRTN,0                                                        
         BE    XIT                                                              
         DC    H'00'                                                            
         DROP  R1                                                               
         EJECT                                                                  
***********************************************************************         
*        ROUTINE TO CONTROL BUILDING/UPDATING STAFF RECORDS           *         
***********************************************************************         
                                                                                
UPDRECS  NTR1                                                                   
         BAS   RE,UPDCLGR                    UPDATE CLIENT GROUP LIMIT          
         BAS   RE,BLDRECS                    BUILD AND PUT STAFF RECS           
         B     XIT                                                              
*                                                                               
***********************************************************************         
*        UPDATE CLIENT GROUP LIMIT FIELD                              *         
***********************************************************************         
                                                                                
UPDCLGR  NTR1                                                                   
         USING TASTD,R4                                                         
         L     R4,AIO           GET STAFF ELEMENT                               
         MVI   ELCODE,TASTELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'00'                                                            
         MVC   TASTCLG,TGCLG    UPDATE CLIENT GROUP FIELD                       
*                                                                               
         OC    TASTCLG,TASTCLG                                                  
         BZ    XIT                                                              
         OC    TASTCLG,SPACES                                                   
         B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
*        ROUTINE TO BUILD AND PUT STAFF RECORDS                       *         
***********************************************************************         
                                                                                
BLDRECS  NTR1                                                                   
         MVI   ELCODE,TAVAELQ    DELETE ALL EXISTING AGENCY/                    
         GOTO1 REMELEM           CLIENT LIMIT ELEMENTS FROM RECORD              
*                                                                               
         GOTO1 ACTVIN,DMCB,0     ADD ACTIVITY ELEMENT                           
*                                                                               
         MVI   RECSTAT,PUTTINGR  SET TO PUT FIRST RECORD                        
*                                                                               
         MVI   BS2LSSEQ,1        RECALL INITIAL AGENCY/CLIENT TABLE             
         BAS   RE,GETTBL         VIA WSSVR                                      
*                                                                               
         USING ACTBLD,R2                                                        
         L     R2,ACTBL          R2=A(INTIAL TABLE)                             
         L     R3,AADDTBL        R3=A(ADDITIONS TABLE)                          
*                                                                               
         USING TAVAD,R4                                                         
BREC10   LA    R4,MYELEM         R4=A(TAVA ELEMENT TO BUILD)                    
         XC    MYELEM,MYELEM                                                    
         MVI   TAVAEL,TAVAELQ    INITIALIZE AGENCY/CLIENT LIMIT ELEMENT         
         MVI   TAVALEN,TAVALNQ                                                  
         MVI   TAVASTAT,TAVANDEF                                                
*                                                                               
         LA    R5,TAVACLI        R5=A(WHERE TO SAVE NXT CLI IN ELEMENT)         
*                                                                               
BREC20   C     R2,ACTBLX         IF REACHED LAST ENTRY IN WSSVR                 
         BL    BREC30            BLOCK, GO GET CONTINUATION OF THE              
         BAS   RE,GETTBL         INITIAL TABLE                                  
         L     R2,ACTBL                                                         
         B     BREC20                                                           
*                                                                               
BREC30   OC    ACTBLD(ACLNQ),ACTBLD                                             
         BNZ   BREC40                                                           
         LA    R2,ACLNQ(R2)      IF AT DELETED ENTRY FROM INTIAL TABLE          
         B     BREC20            BUMP TO NEXT INTIAL ENTRY                      
*                                                                               
BREC40   OC    0(ACLNQ,R3),0(R3)                                                
         BNZ   BREC50                                                           
         LA    R3,ACLNQ(R3)      IF AT DELETED ENTRY FROM ADDITIONS             
         B     BREC40            TABLE, BUMP TO NEXT ADDITIONS ENTRY            
*                                                                               
BREC50   CLI   0(R2),X'FF'       IF AT THE END OF BOTH TABLES                   
         BNE   BREC60            GO ADD THE FINAL AGY/CLI ELEMENT               
         CLI   0(R3),X'FF'                                                      
         BE    BREC130                                                          
*                                                                               
         USING TLRCD,RF                                                         
BREC60   L     RF,AIO                                                           
         CLC   TLRCLEN,=H'1500'  IF RECORD SIZE IS ALREADY OVER 1500            
         BH    BREC140           GO ADD/PUT THE RECORD                          
         DROP  RF                                                               
*                                                                               
         CLI   0(R2),X'FF'       IF AT END OF INTIAL TABLE                      
         BE    BREC80            GO ADD FROM ADDITIONS TABLE                    
*                                                                               
         CLI   0(R3),X'FF'       IF AT END OF ADDITIONS TABLE                   
         BE    BREC70            GO ADD ENTRY FROM INITAL TABLE                 
*                                                                               
         CLC   0(ACLNQ,R2),0(R3) COMPARE ENTRIES FROM INITIAL AND               
         BH    BREC80            ADDITIONS - ADD THE ALPHA LOWER                
*                                                                               
BREC70   LR    R1,R2             SET TO ADD FROM INTIAL TABLE                   
         LA    R2,ACLNQ(R2)      AND BUMP TO NEXT INITIAL ENTRY                 
         B     BREC90                                                           
*                                                                               
BREC80   LR    R1,R3             SET TO ADD FROM ADDITIONS TABLE                
         LA    R3,ACLNQ(R3)      AND BUMP TO NEXT ADDITIONS ENTRY               
*                                                                               
BREC90   OC    TAVAAGY,TAVAAGY   IF ELEMENT BUILDING IN PROGRESS                
         BZ    BREC100                                                          
         CLC   TAVAAGY,ACAGY-ACTBLD(R1)                                         
         BE    BREC100           BUT CURRENT AGENCY DOES NOT MATCH              
         MVC   ELEMENT,MYELEM    THE ELEMENT, GO ADD THE ELEMENT                
         GOTO1 ADDELEM                                                          
         XC    MYELEM,MYELEM     AND INITIALIZE THE NEW ELEMENT                 
         MVI   TAVAEL,TAVAELQ                                                   
         MVI   TAVALEN,TAVALNQ                                                  
         MVI   TAVASTAT,TAVANDEF                                                
         LA    R5,TAVACLI                                                       
*                                                                               
BREC100  CLI   TAVASTAT,0        ADD STATUS AND AGENCY TO ELEMENT               
         BE    BREC110                                                          
         MVC   TAVASTAT,ACSTAT-ACTBLD(R1)                                       
BREC110  MVC   TAVAAGY,ACAGY-ACTBLD(R1)                                         
*                                                                               
         OC    ACCLI-ACTBLD(L'ACCLI,R1),ACCLI-ACTBLD(R1)                        
         BZ    BREC20                                                           
         MVC   0(L'TAVACLI,R5),ACCLI-ACTBLD(R1)                                 
         ZIC   RE,TAVALEN                                                       
         AHI   RE,L'TAVACLI     IF NEW CLIENT IS BEING ADDED                    
         STC   RE,TAVALEN       ADD CLIENT AND BUMP UP ELEMENT LENGTH           
*                                                                               
         CLI   TAVALEN,255      IF ELEMENT IS NOW AT MAXIMUM LENGTH             
         BL    BREC120                                                          
         MVC   ELEMENT,MYELEM   ADD ELEMENT TO RECORD                           
         GOTO1 ADDELEM                                                          
         B     BREC10           AND REINITIALIZE THE ELEMENT                    
*                                                                               
BREC120  LA    R5,L'ACCLI(R5)   BUMP TO SPOT IN ELEMENT FOR THE                 
         B     BREC20           NEXT CLIENT                                     
         DROP  R2                                                               
*                                                                               
BREC130  OC    TAVAAGY,TAVAAGY   WHEN END OF BOTH TABLES IS REACHED             
         BZ    BREC140                                                          
         MVC   ELEMENT,MYELEM    ADD THE FINAL ELEMENT                          
         GOTO1 ADDELEM                                                          
         DROP  R4                                                               
*                                                                               
BREC140  L     R4,AIO                                                           
         MVC   LASTKEY,0(R4)     SAVE KEY OF CURRENT STAFF RECORD               
*                                                                               
         CLI   TLSTSSEQ-TLSTD(R4),0          PRIMARY STAFF RECORD               
         BNE   BREC150                       MUST CONTAIN AT LEAST              
         MVI   ELCODE,TAVAELQ                ONE LIMITED AGENCY/                
         BAS   RE,GETEL                      CLIENT IF STAFF IS A               
         BE    BREC150                       CLIENT                             
         CLI   TGSTEQU,TASTTYPC                                                 
         BE    ERRALOA                                                          
         CLI   TGSTEQU,TASTTYPD                                                 
         BE    ERRALOA                                                          
         CLI   TGSTEQU,TASTTYPF                                                 
         BE    ERRALOA                                                          
*                                                                               
BREC150  CLI   RECSTAT,PUTTINGR              PUT BACK RECORD                    
         BNE   BREC160                                                          
         GOTO1 PUTREC                                                           
BREC160  CLI   RECSTAT,ADDINGR               OR ADD IT                          
         BNE   BREC170                                                          
         GOTO1 ADDREC                                                           
BREC170  OC    SVSTPTR,SVSTPTR                                                  
         BZ    BREC180                                                          
         GOTO1 ADDPTRS,DMCB,SVSTPTR          AND UPDATE POINTERS                
*                                                                               
BREC180  XC    SVSTPTR,SVSTPTR                                                  
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(L'LASTKEY),LASTKEY        GET THE NEXT KEY                   
         GOTO1 HIGH                                                             
         CLC   KEY(L'LASTKEY),LASTKEY                                           
         BE    *+6                                                              
         DC    H'00'                                                            
BREC190  GOTO1 SEQ                                                              
*                                                                               
         LA    R4,KEY                                                           
         CLC   KEY(TLSTSSEQ-TLSTD),LASTKEY   IF NEXT SEQUENCE NUMBER            
         BNE   BREC200                       EXIST                              
         MVI   RECSTAT,PUTTINGR              SET TO PUT IT                      
         MVI   RDUPDATE,C'Y'                                                    
         GOTO1 GETREC                                                           
*                                                                               
         MVI   ELCODE,TAVAELQ                DELETE EXISTING                    
         GOTO1 REMELEM                       ELEMENTS FROM RECORD               
*                                                                               
         LA    R4,MYELEM                                                        
         CLI   0(R2),X'FF'                   IF MORE AGYS/CLIS                  
         BNE   BREC20                        GO ADD THEM                        
         CLI   0(R3),X'FF'                                                      
         BNE   BREC20                                                           
         GOTO1 PUTREC                        IF NOT, PUT THIS RECORD            
         B     BREC190                                                          
*                                                                               
BREC200  CLI   0(R2),X'FF'                   IF NO MORE SEQUENCE #'S            
         BNE   BREC210                       AND NO MORE AGY/CLIS               
         CLI   0(R3),X'FF'                   DONE                               
         BE    XIT                                                              
*                                                                               
         USING TLSTD,R4                                                         
BREC210  L     R4,AIO                        IF NEXT SEQUENCE NUMBER            
         MVC   0(L'TLSTKEY,R4),LASTKEY       DOES NOT ALREADY EXIST             
         ZIC   RE,TLSTSSEQ                   INITIALIZE RECORD                  
         AHI   RE,1                          AND WE'LL HAVE TO ADD IT           
         STC   RE,TLSTSSEQ                                                      
         MVC   TLSTLEN,=X'0041'                                                 
         XC    TLSTELEM(100),TLSTELEM                                           
         MVI   RECSTAT,ADDINGR                                                  
         LA    R4,MYELEM                                                        
         B     BREC20                                                           
         DROP  R4                                                               
         EJECT                                                                  
