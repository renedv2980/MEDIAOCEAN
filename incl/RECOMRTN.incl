*          DATA SET RECOMRTN   AT LEVEL 012 AS OF 05/01/02                      
*                                                                               
********************************************************************            
*                                                                  *            
*        RECOMRTN --- REP ROUTINES TO GET AND CALCULATE A          *            
*                       COMMISSION RATE FROM THE NEW COMMISSION    *            
*                       RECORDS                                    *            
*                                                                  *            
* ---------------------------------------------------------------- *            
* UPDATE HISTORY:                                                  *            
*                                                                  *            
* JUL20/89 (MRR) --- INITIAL RELEASE                               *            
*                                                                  *            
* AUG04/89 (MRR) --- FIX DIVIDE                                    *            
*                    ADD P5, DIVIDE BY 10000 OR 1000000            *            
*                                                                  *            
* AUG11/89 (MRR) --- ADD LOGIC TO BUILD THE RATE TABLE TO EMULATE  *            
*                     THE OLD COMMISSION REPORT.  THIS WILL BE FOR *            
*                     BLAIR AND TORBET RADIO FOR A SHORT WHILE.    *            
*                                                                  *            
* NOV07/89 (MRR) --- FIX DATE CHARACTER TO BINARY BUG              *            
*                                                                  *            
* JAN07/90 (MRR) --- REMOVE 'TO' TORBET FROM OLD COMMISION REP     *            
*                     LIST                                         *            
*                                                                  *            
* 02/08/90  PJS  --- GETRATE - CHANGE TO LOAD NEW RATE WHEN        *            
*                    EFFECTIVE DATE EQUALS CURRENT PERIOD          *            
*                    (IE: USE RATE EFFECTIVE JAN WHEN DOING JAN)   *            
*                                                                  *            
* FEB28/90 (MRR) --- BLDSTA PROCESSING FOR BLAIR FIX               *            
*                                                                  *            
* JAN28/92 (MRR) --- SUPPORT ERROR CONDITION FOR NO RATE RECORDS   *            
*                                                                  *            
* MAR02/92 (MRR) --- COMM RATE RETURNED TO THE WRONG ADDR          *            
*                                                                  *            
* JAN21/93 (BU ) --- KILL 'OLD/NEW REP' CONCEPT BY REMOVING 'BL'   *            
*                    FROM LIST IN 'BSTAREP' ROUTINE.               *            
*                                                                  *            
* MAY18/93 (BU ) --- IF QOPTION2 = 'Y' CALCULATE $0 COMMISSION TO  *            
*                    PERMIT BILLING DOLLARS TO BE ACCUMULATED.     *            
*                                                                  *            
* JUL17/97 (BU ) --- DISPLAY STATION WHEN 'ERROR CALCULATING'      *            
*                    IS ENCOUNTERED.                               *            
*                                                                  *            
*                                                                  *            
*                                                                  *            
*                    **  END TOMBSTONE  **                         *            
********************************************************************            
*                                                                               
*                                                                               
*        DOCOMM --- CALCULATE THE COMMISSION ON THE CURRENT GROSS               
*                    AMOUNT                                                     
*                                                                               
*        P1     =    A(MONTABLE)                                                
*        P2     =    (GROSS)                                                    
*        P3     =    A(COMMISSION ACCUMULATOR)                                  
*        P4     =    A(COMMISSION RATE)                                         
*        P5     =    (RESULT PRECISION FLAG, 0=DOLLARS, 1=CENTS)                
*        P6     =    ZERO$$ FLAG                                                
*                                                                               
* ----------------------------------------------------------------              
*                                                                               
* RETURN:            1ST BYTE OF P1, 0 = NO ERROR                               
*                                    1 = NO RATES ON FILE                       
*                                    2 = ZERO RATE DETECTED                     
*                                    3 = OTHER ERROR                            
*                                                                               
********************************************************************            
*                                                                               
DOCOMM   NTR1                                                                   
*                                                                               
         LR    R7,R1               SAVE PASSED PARAM ADDR                       
         L     R2,0(R1)            P1:  R2 POINTS TO MONTABLE                   
         SR    R4,R4               R4/R5 HAS THE GROSS AMOUNT                   
         L     R5,4(R1)            P2:                                          
         L     R3,8(R1)            P3:  POINTS TO COMM ACCUM                    
         L     R6,12(R1)           P4:  R6 POINTS TO COMMISSION RATE            
         XC    0(4,R6),0(R6)       INIT COMM RATE TO ZERO                       
         MVC   DOLFLAG,19(R1)      P5:  DOLFLAG HAS DIVIDE AMOUNT FLAG          
         MVC   ZERO$FLG,23(R1)     P6:  INCLUDE/EXCLUDE $0 COMM BLNG            
*                                                                               
         LTR   R5,R5               NO GROSS, NO COMM                            
         BZ    DCOMGOOD                                                         
*                                                                               
         GOTO1 GETRATE,DMCB,(R2)                                                
         CLI   NUMRATES,0          ANY RATES?                                   
         BNE   DCOM05A             YES                                          
         XC    DMCB(4),DMCB        NO  - SET COMM RATE TO $00                   
         CLI   ZERO$FLG,C'Y'       INCLUDE $00 COMMISSION BILLING?              
         BE    DCOMGOOD            YES                                          
         LA    R2,1                                                             
         B     DCOMBAD                                                          
DCOM05A  EQU   *                                                                
         OC    DMCB(4),DMCB        ANY COMM RATE?                               
         BNZ   DCOM05B             YES                                          
         CLI   ZERO$FLG,C'Y'       INCLUDE $00 COMMISSION BILLING?              
         BE    DCOMGOOD            YES                                          
         LA    R2,2                                                             
         B     DCOMBAD                                                          
DCOM05B  EQU   *                                                                
         MVC   0(4,R6),DMCB        RETURN COMMISSION RATE                       
*                                                                               
         ZICM  R6,DMCB,4           LOAD R6 WITH COMM RATE                       
         MR    R4,R6               R4/R5 HAS GROSS*RATE                         
         SLDA  R4,1                NOW DOUBLE IT                                
         CLI   DOLFLAG,0                                                        
         BNE   DCOM10                                                           
         D     R4,=F'10000'        DIVIDE TO SHIFT FOUR DECIMAL PLACES          
         B     DCOM20                                                           
DCOM10   EQU   *                                                                
         D     R4,=F'1000000'                                                   
DCOM20   EQU   *                                                                
         LTR   R5,R5               RESULT NEG                                   
         BM    *+8                                                              
         AH    R5,=H'1'                                                         
         SRA   R5,1                                                             
*                                                                               
         A     R5,0(R3)            ADD CURRENT COMMISSION                       
         ST    R5,0(R3)            STORE TOTAL                                  
*                                                                               
*        DOCOMM EXIT                                                            
*                                                                               
DCOMGOOD EQU   *                                                                
         MVI   0(R7),0             SET GOOD                                     
         SR    R0,R0                                                            
         B     RCOMEXIT                                                         
DCOMBAD  EQU   *                                                                
         GOTO1 REPORT                                                           
*                          0         1         2         3         4            
*                          0.2.4.6.8.0.2.4.6.8.0.2.4.6.8.0.2.4.6.8.0            
*                                                                               
         MVC   P+00(20),=C'>>> STATION XXXX-X: '                                
         MVC   P+21(41),=C'>>> ERROR IN CALCULATING A COMMISSION <<<'           
         MVC   P+12(4),RCONKSTA                                                 
         MVC   P+17(1),RCONKSTA+4                                               
         GOTO1 REPORT                                                           
         STC   R2,0(R7)            RETURN ERROR CODE                            
         LA    R0,1                                                             
         B     RCOMEXIT                                                         
         EJECT                                                                  
*                                                                               
*        GETRATE --- GET A COMMISSION RATE FOR THE CURRENT CONTRACT             
*                     MONTH/YEAR---RATE RETURNED IN P1                          
*                                                                               
*        P1    =     A(YYMM PERIOD)                                             
*                                                                               
*        CURRENT CONTRACT IN RCONREC                                            
*                                                                               
*        P2 AND P3 USED TO HOLD BINARY VERSION OF CHARACTER PERIOD              
*                                                                               
*                                                                               
GETRATE  NTR1                                                                   
*                                                                               
         L     R2,0(R1)            R2 POINTS TO PERIOD                          
         LA    R3,8(R1)                                                         
         GOTO1 DATCON,(R1),(0,(R2)),(3,(R3))                                    
         L     R2,8(R1)                                                         
         SRL   R2,16                                                            
         STH   R2,4(R1)                                                         
         LA    R2,4(R1)                                                         
         XC    0(4,R1),0(R1)       INIT RETURN RATE VALUE TO ZERO               
*                                                                               
         ZICM  R0,NUMRATES,1                                                    
         BZ    GRATGOOD                                                         
*                                                                               
         L     R3,ASTATAB            R3 POINTS TO STATION COMM TABLE            
         MVC   0(4,R1),STABRATE(R3)  SET INITIAL RATE                           
*                                                                               
*        GET GENERIC RATE FIRST (NON OFFICE/ADV/TYPE)                           
*                                                                               
GRAT10   EQU   *                                                                
         OC    0(STABDATE,R3),0(R3)         PAST GENERIC RATES?                 
         BNZ   GRAT50                       YES                                 
         OC    STABDATE(6,R3),STABDATE(R3)  AT END OF TABLE?                    
         BZ    GRATGOOD                     YES                                 
GRAT20   EQU   *                                                                
         CLC   0(2,R2),STABDATE(R3)  CURRENT PERD VS EFFECT DATE                
         BL    GRAT30                 PAST ACCEPTABLE DATE                      
*                                                                               
         MVC   0(4,R1),STABRATE(R3)                                             
GRAT30   EQU   *                                                                
         LA    R3,STABLEN(R3)                                                   
         B     GRAT10                                                           
*                                                                               
*        LOOK FOR A SPECIFIC RATE                                               
*                                                                               
GRAT50   EQU   *                                                                
         CLC   STABOFF(2,R3),RCONKOFF      MATCH ON OFFICE                      
         BNE   GRAT90                      NO, GET NEXT                         
         OC    STABADV(4,R3),STABADV(R3)   DO WE HAVE AN ADV CODE               
         BZ    GRAT70                      NO, TAKE THIS RATE                   
*                                                                               
         CLC   RCONKADV(4),STABADV(R3)     MATCH ON ADV                         
         BNE   GRAT90                      NO, GET NEXT                         
         OC    STABTYP(1,R3),STABTYP(R3)   DO WE HAVE A TYPE CODE               
         BZ    GRAT70                      NO, TAKE THIS RATE                   
*                                                                               
         CLC   RCONTYPE(1),STABTYP(R3)                                          
         BNE   GRAT90                                                           
*                                                                               
GRAT70   EQU   *                                                                
         CLC   0(2,R2),STABDATE(R3)                                             
         BL    GRAT90                                                           
GRAT80   EQU   *                                                                
         MVC   0(4,R1),STABRATE(R3)                                             
GRAT90   EQU   *                                                                
         LA    R3,STABLEN(R3)                                                   
         OC    STABDATE(6,R3),STABDATE(R3)  AT END OF TABLE?                    
         BZ    GRATGOOD                     YES                                 
         B     GRAT50                                                           
GRAT100  EQU   *                                                                
*                                                                               
*        GETRATE EXIT                                                           
*                                                                               
GRATGOOD EQU   *                                                                
         SR    R0,R0                                                            
         B     RCOMEXIT                                                         
         EJECT                                                                  
*                                                                               
*        BLDSTA --- BUILD STATION COMMISSION TABLE                              
*                                                                               
BLDSTA   NTR1                                                                   
*                                                                               
*        SET ADDRESSES TO STATION TABLE AND IO AREA                             
*                                                                               
         L     R0,=V(IOBUFF)                                                    
         A     R0,RELO                                                          
         ST    R0,AIOBUFF                                                       
*                                                                               
         L     R0,=V(STATAB)                                                    
         A     R0,RELO                                                          
         ST    R0,ASTATAB                                                       
*                                                                               
         BAS   RE,BSTAINIT                                                      
         L     R2,ASTATAB                                                       
*                                                                               
*        SET I/O AREA AND POINTERS AND READ OFFICE RECORDS TO BUILD             
*        THE STATION COMMISSION TABLE                                           
*                                                                               
         L     R3,AIOBUFF                                                       
         L     R4,AIOAREA                                                       
         ST    R3,AIOAREA                                                       
         USING RCOMREC,R3                                                       
*                                                                               
         SR    R5,R5               USE R5 AS A COUNTER                          
         LA    R6,STABNUM          LOAD FOR MAX COMAPRE                         
         MVC   KEYSTA(27),KEY      SAVE CURRENT KEY                             
*                                                                               
         XC    RCOMREC(32),RCOMREC                                              
         MVI   RCOMKEY,X'29'                                                    
         MVC   RCOMKREP(2),QREP                                                 
         MVC   RCOMKSTA(5),RCONKSTA                                             
         MVC   KEY(27),RCOMKEY                                                  
         GOTO1 HIGH                                                             
         B     BSTA60                                                           
*                                                                               
BSTA50   EQU   *                                                                
         GOTO1 SEQ                                                              
BSTA60   EQU   *                                                                
         CLI   KEY,X'29'           STILL COMMISSION KEYS?                       
         BNE   BSTA100             NO-ALL DONE                                  
         CLC   KEY+11(2),QREP      STILL MY REP                                 
         BNE   BSTA100             NO-ALL DONE                                  
         CLC   KEY+13(5),RCONKSTA  STILL CURRENT STATION                        
         BNE   BSTA100             NO-ALL DONE                                  
         BAS   RE,BSTAREP          OLD REP?                                     
         BZ    BSTA70              NO, SKIP FURTHER TEST                        
         OC    KEY+18(7),KEY+18    OLD REP RECORDS MUST BE 'GENERIC'            
         BZ    BSTA70                                                           
         CLC   KEY+18(7),SPACES                                                 
         BNE   BSTA100                                                          
BSTA70   EQU   *                                                                
         GOTO1 GREC                GET THE RECORD                               
         MVC   STABOFF(2,R2),RCOMKOFF                                           
         MVC   STABADV(4,R2),RCOMKADV                                           
         MVC   STABTYP(1,R2),RCOMKTPE                                           
         MVC   STABDATE(2,R2),RCOMKDAT                                          
         MVC   STABRATE(4,R2),RCOMRAT1                                          
         LA    R2,STABLEN(R2)      POINT TO THE NEXT ENTRY                      
         LA    R5,1(R5)            BUMP COUNT                                   
         CR    R5,R6               TABLE FULL?                                  
         BL    BSTA50              NO-CHECK FOR MORE                            
         DC    H'0'                YES - BLOW IT OUT, SO TABLE                  
*                                     CAN BE EXPANDED!!                         
*                                  SEE RECOMTAB PANBOOK                         
*                                                                               
BSTA100  EQU   *                                                                
         STC   R5,NUMRATES         SAVE NUM OF ENTRIES                          
         MVC   0(2,R2),=X'0000'    END OF TABLE MARKER                          
         ST    R4,AIOAREA          RESET IO BUFFER                              
         MVC   KEY(27),KEYSTA      RESTORE READING ORDER                        
         GOTO1 HIGH                                                             
*                                                                               
         BAS   RE,BSTAREP          OLD REP?                                     
         BZ    BSTAGOOD            NO, ALL DONE                                 
         LTR   R5,R5               ANY RATES?                                   
         BZ    BSTAGOOD            NO, NOTHING TO PROCESS                       
*                                                                               
         MVC   CHARYM(4),QEND                                                   
         MVC   CHARYM+4(2),=C'01'                                               
         GOTO1 DATCON,DMCB,(0,CHARYM),(3,QENDBIN)                               
         MVC   CHARYM(4),QSTART                                                 
         MVC   CHARYM+4(2),=C'01'                                               
         GOTO1 DATCON,DMCB,(0,CHARYM),(3,QSTRBIN)                               
*                                                                               
         GOTO1 BSTAMON,DMCB,QENDBIN,QEND1BIN                                    
         MVI   QEND1BIN+2,X'01'                                                 
         GOTO1 BSTAMON,DMCB,QEND1BIN,QEND2BIN                                   
         MVI   QEND2BIN+2,X'01'                                                 
*                                                                               
         GOTO1 DATCON,DMCB,(3,QENDBIN),(0,CHARYM)                               
         GOTO1 GETRATE,DMCB,CHARYM                                              
         MVC   OLDRATE3(4),0(R1)                                                
         GOTO1 DATCON,DMCB,(3,QEND1BIN),(0,CHARYM)                              
         GOTO1 GETRATE,DMCB,CHARYM                                              
         MVC   OLDRATE2(4),0(R1)                                                
         GOTO1 DATCON,DMCB,(3,QEND2BIN),(0,CHARYM)                              
         GOTO1 GETRATE,DMCB,CHARYM                                              
         MVC   OLDRATE1(4),0(R1)                                                
*                                                                               
         BAS   RE,BSTAINIT                                                      
         L     R2,ASTATAB                                                       
         MVC   STABDATE(2,R2),QSTRBIN                                           
         MVC   STABRATE(4,R2),OLDRATE1                                          
         LA    R2,STABLEN(R2)                                                   
         MVC   STABDATE(2,R2),QEND1BIN                                          
         MVC   STABRATE(4,R2),OLDRATE2                                          
         LA    R2,STABLEN(R2)                                                   
         MVC   STABDATE(2,R2),QENDBIN                                           
         MVC   STABRATE(4,R2),OLDRATE3                                          
         MVI   NUMRATES,3                                                       
*                                                                               
BSTAGOOD EQU   *                                                                
         SR    R0,R0                                                            
         B     RCOMEXIT                                                         
         DROP  R3                                                               
         EJECT                                                                  
*                                                                               
*        BSTAINIT --- INIT THE STATION RATE TABLE                               
*                                                                               
BSTAINIT NTR1                                                                   
*                                                                               
*        CLEAR THE STATION COMMISSION TABLE                                     
*                                                                               
         L     R1,ASTATAB                                                       
         LA    R3,STABNUM                                                       
BINT10   EQU   *                                                                
         LA    R4,STABLEN                                                       
         EX    R4,BINT15                                                        
         B     BINT20                                                           
BINT15   XC    0(0,R1),0(R1)                                                    
BINT20   EQU   *                                                                
         LA    R1,STABLEN(R1)                                                   
         BCT   R3,BINT10                                                        
*                                                                               
BINTGOOD EQU   *                                                                
         SR    R0,R0                                                            
         B     RCOMEXIT                                                         
         EJECT                                                                  
*                                                                               
*        BSTAREP --- CHECK FOR AN OLD REP FOR BUILD STATION TABLE               
*                                                                               
BSTAREP  NTR1                                                                   
*                                                                               
         LA    R1,BREPLIST                                                      
BREP10   EQU   *                                                                
         CLI   0(R1),0                                                          
         BE    BREPGOOD            AT END OF LIST, REP NOT IN LIST              
         CLC   0(2,R1),QREP                                                     
         BE    BREPBAD             REP IN THE LIST, IT'S 'OLD'                  
         LA    R1,2(R1)                                                         
         B     BREP10                                                           
*                                                                               
BREPGOOD EQU   *                   ZERO RETURN IS A 'NEW' REP                   
         SR    R0,R0                                                            
         B     RCOMEXIT                                                         
BREPBAD  EQU   *                   NON-ZERO IS AN 'OLD' REP                     
         LA    R0,1                                                             
         B     RCOMEXIT                                                         
*                                                                               
BREPLIST EQU   *                                                                
******>> DC    CL2'BL'             BLAIR REMOVED:  LIST IS EMPTY.               
         DC    X'0000'                                                          
         EJECT                                                                  
*                                                                               
*        BSTAMON --- TAKE BINARY YM INPUT AND SUBTRACT 1 MONTH                  
*                                                                               
*        P1  =   A(INPUT)                                                       
*        P2  =   A(OUTPUT)                                                      
*                                                                               
BSTAMON  NTR1                                                                   
*                                                                               
         L     R2,0(R1)            GET INPUT ADDR                               
         L     R3,4(R1)            GET OUPUT ADDR                               
         MVC   0(2,R3),0(R2)       SET OUTPUT                                   
*                                                                               
         CLI   1(R3),X'01'         JANUARY?                                     
         BE    BMON10              YES, CHANGE YEAR, ET AL                      
         ZIC   R2,1(R3)                                                         
         BCTR  R2,0                NO, JUST SUBTRACT A MONTH                    
         STC   R2,1(R3)                                                         
         B     BMON20                                                           
BMON10   EQU   *                                                                
         MVI   1(R3),12            MAKE JANUARY INTO DECEMBER                   
         ZIC   R2,0(R3)                                                         
         BCTR  R2,0                SUBTRACT A YEAR                              
         STC   R2,0(R3)                                                         
BMON20   EQU   *                                                                
*                                                                               
*        BSTAMON EXIT                                                           
*                                                                               
BMONGOOD EQU   *                                                                
         SR    R0,R0                                                            
         B     RCOMEXIT                                                         
         EJECT                                                                  
*                                                                               
*        RECOMRTN EXIT                                                          
*                                                                               
         SPACE 3                                                                
RCOMEXIT EQU   *                                                                
         LTR   R0,R0                                                            
         XIT1                                                                   
*                                                                               
*        RECOMRTN LOCAL VARIABLES                                               
*                                                                               
ASTATAB  DS    F                   ADDRESS OF THE STATION TABLE                 
AIOBUFF  DS    F                                                                
AIOAREA  DS    F                                                                
NUMRATES DS    CL1                                                              
KEYSTA   DS    CL32                                                             
         ORG   KEYSTA                                                           
OLDRATE1 DS    CL4                                                              
OLDRATE2 DS    CL4                                                              
OLDRATE3 DS    CL4                                                              
QENDBIN  DS    CL3                                                              
QEND1BIN DS    CL3                                                              
QEND2BIN DS    CL3                                                              
QSTRBIN  DS    CL3                                                              
CHARYM   DS    CL4                                                              
         DS    CL2                 DAY HOLDER FOR YEAR/MONTH                    
         DS    CL2                                                              
DOLFLAG  DS    CL1                                                              
ZERO$FLG DS    CL1                                                              
         DS    CL4                 SPARE                                        
         DS    0H                                                               
