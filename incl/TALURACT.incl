*          DATA SET TALURACT   AT LEVEL 003 AS OF 07/29/15                      
*---------------------------------------------------------------------          
*              ROUTINE TO LOOKUP REC/ACT SECURITY BITS                          
*---------------------------------------------------------------------          
LKUPRACT NTR1  BASE=*,LABEL=*                                                   
         MVC   TALSECMK,=X'00000000'       NO SEC BITS                          
*                                                                               
         CLI   TGTALNUM,1                  IF TAL1, LEAVE                       
         JE    LKUPXIT                                                          
*                                                                               
         L     RF,TGAGRACT         $GEN RECACT                                  
         CLI   SYSPHASE+2,2                                                     
         BE    *+8                                                              
         L     RF,TGARRACT         $REP RECACT                                  
         AHI   RF,16               SKIP 16 BYTES TO FIRST RECORD                
*                                                                               
         OC    CONREC,=CL8' '                                                   
         OC    CONACT,=CL8' '                                                   
*                                                                               
         XR    R4,R4                                                            
         IC    R4,CONRECH+5                                                     
         BCTR  R4,0                                                             
*                                                                               
LKUP100  CLI   0(RF),1             MAKE SURE STILL RECORD 1/4                   
         JE    LKUP105                                                          
         CLI   0(RF),4                                                          
         JNE   LKUP120                                                          
LKUP105  EX    R4,*+8                                                           
         J     *+10                                                             
         CLC   1(0,RF),CONREC                                                   
         JE    LKUP110                                                          
         AHI   RF,16               BUMP TO NEXT RECORD                          
         J     LKUP100                                                          
LKUP110  MVC   CONREC,1(RF)        MOVE IN FULL RECORD                          
         MVI   CONRECH+5,8                                                      
*                                                                               
LKUP115  AHI   RF,16               SKIP TO FIND FIRST ACTION                    
         CLI   0(RF),2             ACTION RECORD?                               
         BNE   LKUP115             NO, KEEP LOOKING                             
*                                                                               
LKUP120  XR    R4,R4                                                            
         IC    R4,CONACTH+5                                                     
         BCTR  R4,0                                                             
*                                                                               
         CLI   0(RF),2             MAKE SURE STILL ACTION                       
         JNE   LKUP200                                                          
         EX    R4,*+8                                                           
         J     *+10                                                             
         CLC   1(0,RF),CONACT                                                   
         JE    LKUP130                                                          
         AHI   RF,16               BUMP TO NEXT RECORD                          
         J     LKUP120                                                          
LKUP130  MVC   CONACT,1(RF)        MOVE IN FULL ACTION                          
         MVI   CONACTH+5,8                                                      
*                                                                               
         USING TLSED,R2                                                         
LKUP200  XC    KEY,KEY                                                          
         LA    R2,KEY                                                           
         MVI   TLSECD,TLSECDQ      SECURITY KEYS                                
         MVC   TLSEPROG,SYSPHASE+2                                              
         XR    R4,R4                                                            
         IC    R4,CONRECH+5        RECORD                                       
         LTR   R4,R4                                                            
         JZ    LKUP230                                                          
         BCTR  R4,0                                                             
         EX    R4,LKUP500          EX MVC RECORD                                
         OC    TLSEREC,LKUPBLK                                                  
*                                                                               
LKUP230  XR    R6,R6                                                            
         IC    R6,CONACTH+5        ACTION                                       
         LTR   R6,R6                                                            
         JZ    LKUP250                                                          
         BCTR  R6,0                                                             
         EX    R6,LKUP700          EX MVC ACTION                                
         OC    TLSEACT,LKUPBLK                                                  
                                                                                
LKUP250  GOTO1 HIGH                                                             
*                                                                               
         CLC   KEY(TLSEREC-TLSED),KEYSAVE                                       
         JNE   LKUPXIT                                                          
         EX    R4,LKUP600          EX CLC RECORD                                
         JNE   LKUP300                                                          
         EX    R6,LKUP800          EX CLC ACTION                                
         JNE   LKUP400             RECORD MATCH BUT NOT ACTION                  
         J     LKUP900                                                          
*                                                                               
LKUP300  LA    R2,KEY              ALL RECORD WITH SPECIFIC ACTION              
         MVC   KEY,KEYSAVE                                                      
         XC    TLSEREC,TLSEREC                                                  
         GOTO1 HIGH                                                             
         EX    R4,LKUP800          EX CLC ACTION                                
         JNE   LKUPXIT                                                          
         J     LKUP900                                                          
*                                                                               
LKUP400  LA    R2,KEY              SPECIFIC RECORD WITH ALL ACTION              
         MVC   KEY,KEYSAVE                                                      
         XC    TLSEACT,TLSEACT                                                  
         GOTO1 HIGH                                                             
         EX    R4,LKUP600          EX CLC RECORD                                
         JNE   LKUPXIT                                                          
         J     LKUP900                                                          
*                                                                               
LKUP500  MVC   TLSEREC(0),CONREC   GET RECORD FIRST                             
LKUP600  CLC   TLSEREC(0),CONREC   CHECK RECORD MATCHES                         
LKUP700  MVC   TLSEACT(0),CONACT   GET RECORD/ACTION                            
LKUP800  CLC   TLSEACT(0),CONACT   CHECK ACTION MATCHES                         
*                                                                               
LKUPBLK  DC    CL8' '                                                           
*                                                                               
         USING TASED,R3                                                         
LKUP900  GOTO1 GETREC              GET THE SECURITY RECORD                      
*                                                                               
         L     R3,AIO                                                           
         MVI   ELCODE,TASEELQ      SECURITY ELEMENT                             
         BRAS  RE,GETEL                                                         
         JNE   LKUPXIT                                                          
         MVC   TALSECMK,TASEMASK   SAVE THE SECURITY MASK                       
LKUPXIT  J     XIT                                                              
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
