*          DATA SET ACREPB502S AT LEVEL 035 AS OF 05/01/02                      
*PHASE ACB502A,+0                                                               
*INCLUDE ACCEDIT                                                                
*INCLUDE SQUASHER                                                               
*INCLUDE CENTER                                                                 
*INCLUDE HELLO                                                                  
*INCLUDE HELEN                                                                  
*INCLUDE CHOPCON                                                                
*INCLUDE PRINT                                                                  
*INCLUDE PRNTBL                                                                 
*INCLUDE PERVAL                                                                 
*INCLUDE ADDAY                                                                  
         TITLE 'WORKER/BUDGET REPORT'                                           
ACB502   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,**ACB5**,R9,R7,RR=R5                                           
         L     RA,0(R1)                                                         
         USING ACWORKD,RA          RA=A(GLOBAL W/S)                             
         LA    RC,SPACEND                                                       
         USING ACB502D,RC          RC=A(SAVE W/S)                               
         L     R8,VBIGPRNT                                                      
         USING BIGPRNTD,R8         R8=ADDRESSES WIDE PRINT                      
*                                                                               
***********************************************************************         
*  ACREPB502 UPDATES BUDGET RECORDS FROM WORKER FILE                  *         
*                                                                     *         
* QOPT1 = W - UPDATES BUDGETS FROM THE WORKER FILE GENERATED BY       *         
*             $MAD UPLOAD (NOT REQUESTABLE ON LINE)                   *         
*                                                                     *         
* QOPT2    - D=DRAFT (NO UPDATES MADE), L OR BLANK=LIVE RUN           *         
*                                                                     *         
* QOPT6    - W= PRINTS ALL UPDATED RECORDS - FOR DEBUGGING            *         
***********************************************************************         
         EJECT                                                                  
         CLI   MODE,RUNFRST        RUN FIRST                                    
         BE    RUNF                                                             
         CLI   MODE,REQFRST        REQUEST FIRST                                
         BE    REQF                                                             
*                                                                               
EXIT     XMOD1 1                                                                
*                                                                               
XYES     SR    RC,RC               EXIT CC=EQUAL                                
XNO      LTR   RC,RC               EXIT CC=NOT EAUAL                            
XIT      XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
*              RUN FIRST                                              *         
***********************************************************************         
*                                                                               
RUNF     DS    0H                                                               
         MVC   VTYPES(VTYPLNQ),ADCONS                                           
*                                                                               
         L     R2,=A(BOXRC)        SET UP BOX ROUTINE                           
         ST    RC,0(R2)                                                         
         L     R2,VEXTRAS                                                       
         USING RUNXTRAD,R2                                                      
         L     R2,ADMASTD                                                       
         USING MASTD,R2                                                         
         L     R2,MCBXAREA                                                      
         ST    R2,ADBOX                                                         
         L     R2,=A(BXHOOK)                                                    
         ST    R2,HEADHOOK                                                      
         B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
*              REQUEST FIRST                                          *         
***********************************************************************         
*                                                                               
REQF     DS    0H                                                               
         GOTO1 DATCON,DMCB,(4,RCDATE),(1,TODAYP)                                
         GOTO1 DATCON,DMCB,(4,RCDATE),(0,TODAYC)                                
*                                                                               
         GOTO1 ADSORTER,DMCB,SORTCARD,RECCARD,0                                 
*                                                                               
         XC    SVACCT,SVACCT                                                    
         XC    SVUNTLDG,SVUNTLDG                                                
         XC    SVCONTRA,SVCONTRA                                                
         XC    LASTBKEY,LASTBKEY                                                
*                                                                               
         USING ACCOMPD,R4                                                       
REQF10   L     R4,ADCMPEL                                                       
         MVC   CMPABBR,ACMPABBR    COMPANY ABBREVIATION                         
         MVC   CMPNAME,SPACES                                                   
         L     R4,ADCMPNAM                                                      
         ZIC   R1,1(R4)                                                         
         SH    R1,=H'3'                                                         
         EX    R1,*+4                                                           
         MVC   CMPNAME(0),2(R4)    COMPANY NAME                                 
         DROP  R4                                                               
*                                                                               
         CLI   QOPT1,C'W'          WORKER/BUDGET UPLOAD REPORT                  
         BNE   XIT                 EVERYTHING DONE IN REQ FIRST                 
         BAS   RE,WBUD                                                          
         B     XIT                                                              
*                                                                               
         EJECT                                                                  
***********************************************************************         
* WORKER FILE/ BUDGET UPLOAD AND REPORT   (QOPT1=W)                   *         
***********************************************************************         
*                                                                               
WBUD     NTR1                                                                   
*                                                                               
         MVI   RCSUBPRG,0                                                       
         ZAP   NEWTOT,=P'0'                                                     
         ZAP   PREVTOT,=P'0'                                                    
*                                                                               
*  READ WORKER FILE                                                             
*                                                                               
         L     R0,AWRKB            WORK FILE BUFFER                             
         L     R1,=A(WRKBEND-WRKB)     LENGTH                                   
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LA    R3,3                                                             
*                                                                               
WBUD10   XC    WKID,WKID           MAKE INDEX CALL TO WORKER FILES              
         LA    R6,WKID                                                          
         USING UKRECD,R6                                                        
         MVC   UKUSRID,ORIGINUM    USER ID NUMBER                               
         MVC   UKSYSPRG,=C'ABU'    SYSTEM/PROGRAM                               
         MVI   UKSUBPRG,X'00'                                                   
         MVC   UKDAY,TODAYP+2      DAY NUMBER                                   
         MVI   UKCLASS,C'B'        CLASS                                        
         SR    R6,R6                                                            
         XC    SVWRKSEQ,SVWRKSEQ                                                
         MVC   SVWKID,WKID                                                      
         BAS   RE,WRKINDX          GET INDEX RECORD                             
         BE    WBUD10NX                                                         
         GOTO1 =V(ADDAY),DMCB,(C'D',TODAYC),TODAYC,F'-1'                        
         GOTO1 DATCON,DMCB,(0,TODAYC),(1,TODAYP)                                
         BCT   R3,WBUD10                                                        
         DC    H'0'                                                             
         DROP  R6                                                               
*                                                                               
WBUD10NX L     R6,AWRKREC          R6 MUST HAVE A(WORKER REC)                   
         BAS   RE,WRKREAD          GET WORKER RECORD                            
         BNE   WBUD50GT            NO MORE                                      
*                                                                               
         LA    R4,4(R6)            SKIP WORKER REC BYTES                        
         ST    R4,SVADDR                                                        
         USING BUDRECD,R4          POINTS TO BUDGET REC IN WORKER REC           
*                                                                               
* VALIDATE ACCOUNTS AND MAKE SORTER RECORD                                      
*                                                                               
         LA    R0,SREC             CLEAR AREA FOR SORT RECORD                   
         L     R1,=A(SREND-SRECD)    LENGTH                                     
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LA    R2,SREC                                                          
         USING SRECD,R2            R2 IS POINTER TO SORT RECORD                 
*                                                                               
         LA    R5,KEY2                                                          
         USING ACTRECD,R5          VALIDATE ACCOUNT IN BUDGET REC               
         MVC   ACTKEY,SPACES                                                    
         MVC   ACTKCULA,BUDKCPY                                                 
*                                                                               
         BAS   RE,VALACCNT         READS INTO AIO2                              
         BE    WBUD30                                                           
*                                                                               
         MVI   SRSTAT,C'X'         ALL ERRORS GO OUT WITH 'X'                   
         OI    SRERROR,SRERRACC    ERROR=ACCOUNT INVALID                        
         B     WBUD35                                                           
*                                                                               
WBUD30   MVI   ELCODE,X'20'        GET NAME                                     
         L     R4,AIO2                                                          
         BAS   RE,GETEL                                                         
         BNE   WBUD35                                                           
         BAS   RE,GETNAME          PUTS NAME IN WORK                            
         MVC   SRACCNM,WORK                                                     
         L     R4,SVADDR           RESET R4 TO WORKER REC'S BUD REC             
*                                                                               
WBUD35   CLC   BUDKCCPY(15),SPACES   DOESN'T HAVE TO HAVE CONTRA ACCNT          
         BE    WBUD45                                                           
         MVC   ACTKEY,SPACES       VALIDATE CONTRA ACCNT IN BUDGET REC          
         MVC   ACTKCULA,BUDKCCPY                                                
         DROP  R5                                                               
*                                                                               
         BAS   RE,VALACCNT         READS INTO AIO2                              
         BE    WBUD40                                                           
*                                                                               
         MVI   SRSTAT,C'X'         ALL ERRORS GO OUT WITH 'X'                   
         OI    SRERROR,SRERRCON    ERROR=CONTRA ACCOUNT INVALID                 
         B     WBUD45                                                           
*                                                                               
WBUD40   MVI   ELCODE,X'20'        GET NAME                                     
         L     R4,AIO2                                                          
         BAS   RE,GETEL                                                         
         BNE   WBUD45                                                           
         BAS   RE,GETNAME                                                       
         MVC   SRCONNM,WORK                                                     
         L     R4,SVADDR           RESET R4 TO WRKER BUD REC                    
*                                                                               
WBUD45   DS    0H                  FILL IN REST OF SORT REC AND PUT             
         MVC   SRBUDNUM,BUDKBUDN      BUDGET NUMBER                             
         MVC   SRBTYPE,BUDKBCKT       BUCKET TYPE                               
         LH    R1,BUDRLEN          MAX LEN OF BUDREC IN WRK REC=194             
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   SRBUDREC(0),BUDKEY     ENTIRE BUDGET RECORD                      
*                                                                               
         GOTO1 ADSORTER,DMCB,=C'PUT',SREC                                       
         B     WBUD10NX            GET NEXT WORKER REC                          
         DROP  R4                                                               
*                                                                               
*  GET RECORD BACK FROM SORTER/ UPDATE BUDGET RECS/ PRINT REPORT                
*                                                                               
WBUD50GT GOTO1 ADSORTER,DMCB,=C'GET'                                            
         ICM   R2,15,4(R1)                                                      
         BZ    WBUDX               ALL DONE                                     
         MVC   SREC,0(R2)          TO SEE IN DUMP                               
*                                                                               
         CLC   SVBUDNUM,SRBUDNUM   PAGE BREAK ON BUDGET NUMBER                  
         BNE   WBUD53                                                           
         CLC   SVBTYPE,SRBTYPE     PAGE BREAK ON BUCKET TYPE                    
         BE    WBUD55                                                           
*                                                                               
WBUD53   MVI   FORCEHED,C'Y'                                                    
*                                                                               
         MVC   SVBUDNUM,SRBUDNUM                                                
         MVC   SVBTYPE,SRBTYPE                                                  
         EDIT  (B2,SRBUDNUM),(6,BUDNUM),ALIGN=LEFT                              
*                                                                               
         MVC   BUDNO,SRBUDNUM                                                   
         MVC   BUDBTYPE,SRBTYPE    BUCKET TYPE                                  
         MVC   AIO,AIO1            INTO AIO1                                    
         BAS   RE,GETBUDNM         READ FOR BUDGET TYPE NAME                    
*                                                                               
WBUD55   CLC   SRSTAT,SVSTAT       NEW PAGE FOR DIFFERENT STATUS                
         BE    *+8                                                              
         MVI   FORCEHED,C'Y'                                                    
         MVC   SVSTAT,SRSTAT                                                    
*                                                                               
         CLI   SRSTAT,C'X'         ERROR REPORT?                                
         BE    WBUD100                                                          
*                                                                               
         MVC   KEY2,SRBUDREC       DOES BUDGET REC EXIST?                       
         MVC   AIO,AIO1                                                         
         BAS   RE,READUP           READ FOR UPDATE                              
         L     R4,AIO1                                                          
         CLC   KEY2,0(R4)                                                       
         BNE   WBUD60                                                           
*                                                                               
         BAS   RE,BUDUP            UPDATE BUDGET RECORD AND REPORT              
         B     WBUD50GT            GET NEXT SORT REC                            
*                                                                               
WBUD60   BAS   RE,BUDADD           ADD NEW BUDGET RECORD AND REPORT             
         B     WBUD50GT            GET NEXT SORT REC                            
*                                                                               
WBUD100  BAS   RE,BUDERR           PRINT ERROR REPORT                           
         B     WBUD50GT            GET NEXT SORT REC                            
*                                                                               
WBUDX    MVI   FCRDACC,C'N'        SKIP MONACC'S READS IN OTHER MODES           
         MVI   FCRDTRNS,C'N'                                                    
         MVI   FCRDHIST,C'N'                                                    
*                                                                               
         GOTO1 ADSORTER,DMCB,=C'END'                                            
         BAS   RE,WRKKEEP                                                       
         B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
*        UPDATES EXISTING BUDGET RECORD AND WRITE OUT                 *         
*        INPUT                     AIO1 HAS EXISTING BUD REC          *         
*                                  R2 = SORT RECORD                   *         
***********************************************************************         
*                                                                               
BUDUP    NTR1                                                                   
         USING SRECD,R2            R2 IS POINTER TO SORT RECORD                 
         L     R4,AIO1                                                          
         USING BUDRECD,R4          R4 IS POINTER TO EXISTING BUDREC             
*                                                                               
         TM    BUDRSTA,X'80'       MARKED FOR DELETION                          
         BNO   BU10                                                             
*                                                                               
         L     R0,AIO1             CLEAR IO                                     
         LA    R1,MAXACC           MAX LENGTH                                   
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     R3,AIO1             IF WAS MARKED FOR DELETION                   
         LA    R4,SRBUDREC         MOVE NEW BUDREC INTO AIO                     
         LH    R1,BUDRLEN          MAX LEN OF BUDREC IN WRK REC=194             
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),BUDKEY                                                   
*                                                                               
         BAS   RE,WRPTADD         W REPORT FOR NEW RECORD IN AIO1               
         BAS   RE,WRITE           WRITE OUT NEW REC                             
         B     XIT                                                              
*                                                                               
BU10     DS    0H                                                               
*                                                                               
         LA    R3,XP                                                            
         USING PLINED,R3           PRINT LINE                                   
*                                                                               
         LA    R4,SRBUDREC         PRINT ACCOUNT NUMBERS AND NAMES              
         MVC   PLINACCT,BUDKUNT                                                 
         MVC   PLINANME,SRACCNM                                                 
         MVC   PLINCON,BUDKCUNT                                                 
         MVC   PLINCNME,SRCONNM                                                 
*                                                                               
         MVI   ELCODE,BAMELQ       BUMP THRU NEW REC AND COMPARE TO OLD         
         USING BAMELD,R4           BUDGET AMOUNT ELEMENT (X'1D')                
         BAS   RE,GETEL                                                         
         BE    BU30                                                             
         B     BU100                                                            
*                                                                               
BU20NX   L     R4,SVADDR           MARKER TO LAST ELEMENT                       
         BAS   RE,NEXTEL                                                        
         BNE   BU100                                                            
*                                                                               
BU30     ST    R4,SVADDR           SAVE MARKER TO ELEMENT                       
         MVC   YYMM,BAMMNTH        SAVE MONTH                                   
         MVC   NEWAMT,BAMBUDG      AND AMOUNT                                   
*                                                                               
         XC    ELEM,ELEM                                                        
         ZIC   R1,BAMLN            MOVE ELEMENT TO ELEM                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   ELEM(0),0(R4)                                                    
*                                                                               
         L     R4,AIO1             LOOK FOR DATE IN EXISTING RECORD             
         BAS   RE,GETEL                                                         
         BE    BU50                                                             
         B     BU60                                                             
*                                                                               
BU40NX   BAS   RE,NEXTEL                                                        
         BNE   BU60                                                             
*                                                                               
BU50     CLC   BAMMNTH,YYMM        SAME YEAR AND MONTH?                         
         BNE   BU40NX                                                           
         MVC   PREVAMT,BAMBUDG                                                  
         MVC   BAMBUDG,NEWAMT      REPLACE WITH NEW AMOUNT                      
         MVC   PRSTAT,=C'CHANGED'                                               
         BAS   RE,WRPTLINE         REPORT ON UPDATED ELEM                       
         B     BU20NX              GET NEXT NEW ELEMENT                         
*                                                                               
BU60     DS    0H                  NO MATCH FOUND=ADD ELEMENT                   
         BAS   RE,ADDELEM                                                       
         BE    BU70                                                             
         MVC   PRSTAT,=C'ERROR  '                                               
         B     BU72                                                             
*                                                                               
BU70     MVC   PRSTAT,=C'ADDED  '                                               
BU72     BAS   RE,WRPTLINE         REPORT ON UPDATED ELEM                       
         B     BU20NX              GET NEXT NEW ELEMENT                         
*                                                                               
BU100    DS    0H                  NO MORE NEW ELEMENTS                         
         BAS   RE,WRPTTOT          PRINT TOTALS FOR REC                         
         BAS   RE,WRITE            WRITE OUT NEW REC                            
         GOTO1 ACREPORT                                                         
         B     XIT                                                              
*                                                                               
         DROP  R2,R3,R4                                                         
         EJECT                                                                  
***********************************************************************         
*        ADDS A NEW BUDGET RECORD                                     *         
*        INPUT                     R2 = SORT RECORD                   *         
***********************************************************************         
*                                                                               
BUDADD   NTR1                                                                   
*                                                                               
         USING SRECD,R2            R2 IS POINTER TO SORT RECORD                 
         USING BUDRECD,R4          R4 IS POINTER TO BUDGET RECORD               
*                                                                               
         L     R0,AIO1             CLEAR IO                                     
         LA    R1,MAXACC           MAX LENGTH                                   
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     R3,AIO1                                                          
         LA    R4,SRBUDREC         MOVE NEW BUDREC INTO AIO                     
         LH    R1,BUDRLEN          MAX LEN OF BUDREC IN WRK REC=194             
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),BUDKEY                                                   
         BAS   RE,WRPTADD         W REPORT FOR NEW RECORD IN AIO1               
*                                                                               
         MVC   AIO,AIO1                                                         
         BAS   RE,ADD             WRITE OUT NEW REC                             
*                                                                               
         B     XIT                                                              
         DROP  R2,R4                                                            
         EJECT                                                                  
***********************************************************************         
*        FORMATS ERROR REPORT LINE                                    *         
*        INPUT                     R2 = SORT RECORD                   *         
***********************************************************************         
*                                                                               
BUDERR   NTR1                                                                   
*                                                                               
*        LA    R2,SREC                                                          
         USING SRECD,R2            R2 IS POINTER TO SORT RECORD                 
         LA    R4,SRBUDREC                                                      
         USING BUDRECD,R4          R4 IS POINTER TO BUDGET RECORD               
*                                                                               
         MVI   RCSUBPRG,1                                                       
*                                                                               
         LA    R3,XP                                                            
         USING PLINED,R3           PRINT LINE                                   
*                                                                               
         MVC   PLINACCT,BUDKUNT    ACCOUNT NUMBER                               
         CLC   BUDKCUNT,SPACES                                                  
         BE    *+10                                                             
         MVC   PLINCON,BUDKCUNT    CONTRA ACCOUNT NUMBER IF ONE                 
*                                                                               
         TM    SRERROR,SRERRACC+SRERRCON                                        
         BNO   BE10                                                             
         MVC   PLINERR,NOACCCON    BOTH ACCOUNTS DON'T EXIST                    
         B     BEX                                                              
*                                                                               
BE10     TM    SRERROR,SRERRACC                                                 
         BNO   BE20                                                             
         MVC   PLINERR,NOACC       ACCOUNT DOESN'T EXIST                        
         MVC   PLINCNME,SRCONNM    CAN STILL SHOW CONTRA ACCNT NAME             
         B     BEX                                                              
*                                                                               
BE20     TM    SRERROR,SRERRCON                                                 
         BNO   BE30                                                             
         MVC   PLINERR,NOCON       CONTRA ACCOUNT DOESN'T EXIST                 
         MVC   PLINANME,SRACCNM    CAN STILL SHOW ACCOUNT NAME                  
         B     BEX                                                              
*                                                                               
BE30     DC    H'0'                                                             
*                                                                               
BEX      DS    0H                                                               
         GOTO1 ACREPORT                                                         
         B     XIT                                                              
         DROP  R2,R3,R4                                                         
         EJECT                                                                  
***********************************************************************         
*        REPORT FOR ADDINNG A NEW BUDGET RECORD                       *         
*        INPUT                     AIO1 HAS NEW RECORD                *         
*                                  R2 POINTS TO SORT REC              *         
***********************************************************************         
*                                                                               
WRPTADD  NTR1                                                                   
*                                                                               
         USING SRECD,R2            R2 IS POINTER TO SORT RECORD                 
         L     R4,AIO1                                                          
         USING BUDRECD,R4          R4 IS POINTER TO BUDGET RECORD               
         LA    R3,XP                                                            
         USING PLINED,R3           PRINT LINE                                   
*                                                                               
         MVC   PLINACCT,BUDKUNT    PRINT ACCOUNT NUMBERS AND NAMES              
         MVC   PLINANME,SRACCNM                                                 
         MVC   PLINCON,BUDKCUNT                                                 
         MVC   PLINCNME,SRCONNM                                                 
*                                                                               
         MVI   ELCODE,BAMELQ       BUDGET AMOUNT ELEMENT (X'1D')                
         USING BAMELD,R4                                                        
         BAS   RE,GETEL                                                         
         BE    WA20                                                             
         B     WA30                                                             
*                                                                               
WA10NX   BAS   RE,NEXTEL                                                        
         BNE   WA30                                                             
*                                                                               
WA20     MVC   YYMM,BAMMNTH        SAVE MONTH                                   
         MVC   NEWAMT,BAMBUDG      AND AMOUNT                                   
         MVC   PRSTAT,=C'ADDED  '                                               
         BAS   RE,WRPTLINE         GO PRINT LINE                                
         B     WA10NX                                                           
*                                                                               
WA30     BAS   RE,WRPTTOT          PRINT TOTALS                                 
         GOTO1 ACREPORT                                                         
         B     XIT                                                              
         DROP  R2,R3,R4                                                         
*                                                                               
         EJECT                                                                  
***********************************************************************         
*        FORMATS A LINE FOR THE W REPORT                              *         
*        INPUT                                                        *         
***********************************************************************         
*                                                                               
WRPTLINE NTR1                                                                   
*                                                                               
         LA    R3,XP                                                            
         USING PLINED,R3           PRINT LINE                                   
*                                                                               
         MVI   DD,X'01'                                                         
         GOTO1 DATCON,DMCB,(1,YYMM),(6,PLINDATE)                                
*                                                                               
         EDIT  (P8,NEWAMT),(14,PLINAMT),2,ALIGN=RIGHT,MINUS=YES                 
         AP    NEWTOT,NEWAMT                                                    
*                                                                               
         MVC   PLINSTAT,PRSTAT     ADDED OR CHANGED                             
         CLC   PRSTAT,=C'CHANGED'                                               
         BNE   WL20                                                             
*                                                                               
         EDIT  (P8,PREVAMT),(14,PLINPREV),2,ALIGN=RIGHT,MINUS=YES               
         AP    PREVTOT,PREVAMT                                                  
         B     WLX                                                              
*                                                                               
WL20     DS    0H                  CHECK FOR REC TOO LARGE ERROR                
         CLC   PRSTAT,=C'ERROR  '                                               
         BNE   WLX                                                              
         LA    R3,XPSECOND                                                      
         MVC   PLINACCT,=C'* REC TOO LARGE - NO UPDATES MADE *'                 
*                                                                               
WLX      DS    0H                                                               
         GOTO1 ACREPORT                                                         
*                                                                               
         B     XIT                                                              
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
*        FORMATS TOTALS FOR CHANGES TO ONE BUDGET REC                 *         
*        INPUT                                                                  
***********************************************************************         
*                                                                               
WRPTTOT  NTR1                                                                   
*                                                                               
         LA    R3,XP                                                            
         USING PLINED,R3           PRINT LINE                                   
*                                                                               
         MVC   PLINAMT,=C'--------------'                                       
         CP    PREVTOT,=P'0'                                                    
         BE    *+10                                                             
         MVC   PLINPREV,=C'--------------'                                      
*                                                                               
         GOTO1 ACREPORT                                                         
*                                                                               
         EDIT  (P8,NEWTOT),(14,PLINAMT),2,ALIGN=RIGHT,MINUS=YES                 
*                                                                               
         CP    PREVTOT,=P'0'                                                    
         BE    WTX                                                              
*                                                                               
         EDIT  (P8,PREVTOT),(14,PLINPREV),2,ALIGN=RIGHT,MINUS=YES               
*                                                                               
WTX      GOTO1 ACREPORT                                                         
         ZAP   NEWTOT,=P'0'        RESET TOTALS                                 
         ZAP   PREVTOT,=P'0'                                                    
*                                                                               
         B     XIT                                                              
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
*        READS BUDGET TYPE RECORD AND GETS NAME AND CODE              *         
*        INPUT                     BUDNO=BUDGET NUMBER TO READ        *         
*        OUTPUT                    BUDNAME=NAME,BUDNUM=NUMBER,BUDCODE *         
***********************************************************************         
*                                                                               
GETBUDNM NTR1                                                                   
*                                                                               
         MVC   BUDNAME,SPACES                                                   
         MVC   BUDCODE,SPACES                                                   
*        MVI   BUDBTYPE,X'00'                                                   
*                                                                               
         LA    R4,KEY2                                                          
         USING BUDRECD,R4                                                       
         XC    KEY2,KEY2                                                        
         MVI   BUDKTYP,BUDKTYPQ                                                 
         MVC   BUDKCPY,QCOMPANY                                                 
         MVI   BUDKIND,X'00'                                                    
         MVC   BUDKNO1,BUDNO                                                    
         MVC   SVKEY,KEY2                                                       
         BAS   RE,HIGH                                                          
         L     R4,AIO                                                           
         CLC   SVKEY(5),0(R4)      SAME UP TO BUDGET NUMBER                     
         BNE   XNO                                                              
         MVC   BUDCODE,BUDKCOD     SAVE CODE                                    
         CLC   BUDKBTYP,BUDBTYPE   MAKE SURE SAME BUCKET TYPE                   
         BE    GB20                                                             
*                                                                               
         LA    R4,KEY2                                                          
         MVC   KEY2,SVKEY                                                       
         XC    BUDKNO1,BUDKNO1     READ BY CODE TO GET RIGHT BUCKET             
         MVC   BUDKCOD,BUDCODE                                                  
         MVC   BUDKNO2,BUDNO                                                    
         MVC   BUDKBTYP,BUDBTYPE                                                
         MVC   SVKEY,KEY2                                                       
         BAS   RE,HIGH                                                          
         L     R4,AIO                                                           
         CLC   SVKEY,0(R4)      SAME UP TO BUDGET NUMBER                        
         BNE   XNO                                                              
*                                                                               
GB20     MVC   BUDCODE,BUDKCOD     SAVE CODE                                    
*                                                                               
         MVI   ELCODE,X'20'        NAME ELEMENT                                 
         BAS   RE,GETEL                                                         
         BNE   XYES                                                             
         BAS   RE,GETNAME                                                       
         MVC   BUDNAME,WORK                                                     
*                                                                               
         B     XYES                                                             
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
*        VALIDATES THAT AN ACCOUNT EXISTS                             *         
*        INPUT                     KEY2 = KEY                         *         
*        OUTPUT                    CC=YES IF RECORD FOUND, NO IF NOT  *         
***********************************************************************         
*                                                                               
VALACCNT NTR1                                                                   
*                                                                               
         MVC   AIO,AIO2            USE AIO2                                     
         BAS   RE,READ                                                          
         MVC   AIO,AIO1                                                         
         L     R4,AIO2                                                          
         CLC   KEY2,0(R4)                                                       
         BE    XYES                                                             
         B     XNO                                                              
         EJECT                                                                  
*                                                                               
***********************************************************************         
*        WORKER INTERFACE                                             *         
***********************************************************************         
*                                                                               
WRKINDX  MVC   WKCMD,WKINDX        INDEX                                        
         B     WRKR                                                             
*                                                                               
WRKREAD  MVC   WKCMD,WKREAD        READ                                         
         B     WRKR                                                             
*                                                                               
WRKKEEP  MVC   WKCMD,WKKEEP        KEEP                                         
         B     WRKR                                                             
*                                                                               
WRKRTN   MVC   WKCMD,WKRTN         RETAIN                                       
         B     WRKR                                                             
*                                                                               
         USING UKRECD,R5                                                        
WRKR     NTR1  ,                                                                
         GOTO1 WORKER,DMCB,WKCMD,AWRKB,WKID,(R6)                                
         CLI   8(R1),0                                                          
         BNE   XNO                                                              
         CLC   WKCMD,WKINDX                                                     
         BNE   XYES                                                             
         LA    R5,WKID                                                          
         TM    UKSTAT,X'08'                                                     
         BZ    XYES                NO, OK TO EXIT                               
         B     XNO                                                              
*                                                                               
         EJECT                                                                  
***********************************************************************         
*              DATAMGR INTERFACE                                      *         
***********************************************************************         
*                                                                               
READ     NTR1                                                                   
         GOTO1 DATAMGR,DMCB,DMREAD,ACCFIL,KEY2,AIO                              
         CLI   8(R1),0                                                          
         BE    XYES                                                             
         B     XNO                                                              
*                                                                               
READ2    GOTO1 DATAMGR,DMCB,DMREAD,ACCFIL,KEY2,AIO                              
*                                                                               
READ10   CLI   QOPT6,C'W'                                                       
         BNE   READX                                                            
         MVC   XP(15),=C'READ FOR UPDATE'                                       
         GOTO1 ACREPORT                                                         
         L     R2,AIO                                                           
         USING BUDRECD,R2                                                       
         SR    R3,R3                                                            
         LH    R3,BUDRLEN                                                       
         GOTO1 REPORTER,DMCB,(R2),(R3)                                          
         GOTO1 ACREPORT                                                         
         DROP  R2                                                               
*                                                                               
READX    CLI   8(R1),0                                                          
         BE    XYES                                                             
         B     XNO                                                              
*                                                                               
READUP   NTR1                        READ FOR UPDATE/PASS DELETED RECS          
*                                                                               
         CLI   QOPT2,C'L'        DOLIVE RUN?                                    
         BNE   READ2                                                            
         CLI   RCWRITE,C'N'      DON'T UPDATE                                   
         BE    READ2                                                            
         GOTO1 DATAMGR,DMCB,(X'88',DMREAD),ACCFIL,KEY2,AIO                      
         B     READ10                                                           
*                                                                               
HIGH     NTR1                                                                   
*                                                                               
         GOTO1 DATAMGR,DMCB,DMRDHI,ACCFIL,KEY2,AIO                              
         B     XIT                                                              
*                                                                               
SEQ      NTR1                                                                   
         GOTO1 DATAMGR,DMCB,DMRSEQ,ACCFIL,AIO,AIO                               
         B     XIT                                                              
*                                                                               
WRITE    NTR1                                                                   
         CLI   QOPT6,C'W'                                                       
         BNE   WRITE10                                                          
         MVC   XP(5),=C'WRITE'                                                  
         GOTO1 ACREPORT                                                         
         L     R2,AIO                                                           
         USING BUDRECD,R2                                                       
         SR    R3,R3                                                            
         LH    R3,BUDRLEN                                                       
         GOTO1 REPORTER,DMCB,(R2),(R3)                                          
         GOTO1 ACREPORT                                                         
         DROP  R2                                                               
*                                                                               
WRITE10  CLI   QOPT2,C'L'          LIVE RUN                                     
         BNE   XIT                                                              
         CLI   RCWRITE,C'N'        DON'T WRITE TO FILE                          
         BE    XIT                                                              
         L     R2,AIO                                                           
         GOTO1 DATAMGR,DMCB,DMWRT,ACCFIL,(R2),(R2)                              
         CLI   8(R1),0                                                          
         BE    XIT                                                              
         DC    H'0'                                                             
*                                                                               
ADD      NTR1                                                                   
         CLI   QOPT6,C'W'                                                       
         BNE   ADD10                                                            
         MVC   XP(5),=C'ADD  '                                                  
         GOTO1 ACREPORT                                                         
         L     R2,AIO                                                           
         USING BUDRECD,R2                                                       
         SR    R3,R3                                                            
         LH    R3,BUDRLEN                                                       
         GOTO1 REPORTER,DMCB,(R2),(R3)                                          
         GOTO1 ACREPORT                                                         
         DROP  R2                                                               
*                                                                               
ADD10    CLI   QOPT2,C'L'          LIVE RUN                                     
         BNE   XIT                                                              
         CLI   RCWRITE,C'N'        DON'T WRITE TO FILE                          
         BE    XIT                                                              
         L     R2,AIO                                                           
         GOTO1 DATAMGR,DMCB,DMADD,ACCFIL,(R2),(R2)                              
         CLI   8(R1),0                                                          
         BE    XIT                                                              
         DC    H'0'                                                             
*                                                                               
ADDELEM  NTR1                                                                   
         CLI   ELEM,0                                                           
         BE    XIT                                                              
         GOTO1 HELLO,DMCB,(C'P',=C'ACCOUNT '),AIO,ELEM,0                        
         CLI   DMCB+12,0                                                        
         BE    XYES                                                             
         CLI   DMCB+12,5                                                        
         BE    XNO                                                              
         DC    H'0'                OTHER ERRORS UNACCEPTABLE                    
*                                                                               
REMELEM  NTR1                                                                   
         L     R4,AIO                                                           
         XC    ELEM,ELEM                                                        
         BAS   RE,GETEL                                                         
         BNE   XIT                                                              
         ZIC   R1,1(R4)                                                         
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   ELEM(0),0(R4)                                                    
         GOTO1 HELLO,DMCB,(C'D',=C'ACCOUNT'),(ELCODE,AIO),0                     
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
*              EXTRACT NAME FROM X'20' ELEMENT                        *         
***********************************************************************         
*                                                                               
GETNAME  NTR1                                                                   
         MVC   WORK,SPACES                                                      
         OR    R4,R4                                                            
         BZ    XIT                                                              
*                                                                               
         USING NAMELD,R4           X'20' NAME ELEMENT                           
         ZIC   R1,NAMLN                                                         
         SH    R1,=Y(NAMLN1Q+1)                                                 
         BM    XIT                                                              
         EX    R1,*+4                                                           
         MVC   WORK(0),NAMEREC                                                  
         B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* THIS ROUTINE DUMPS WHATEVER IS IN DMCB                                        
*                                                                               
* ON ENTRY:    P1                  A(SOURCE)                                    
*              P2                  LENGTH OF SOURCE                             
***********************************************************************         
REPORTER NTR1                                                                   
         L     R5,DMCB                                                          
         L     R4,DMCB+4                                                        
*                                                                               
RPTR10   GOTO1 HEXOUT,DMCB,(R5),WORK,32                                         
*                                                                               
         MVC   XP(8),WORK          HEX BREAKDOWN                                
         MVC   XP+9(8),WORK+8                                                   
         MVC   XP+18(8),WORK+16                                                 
         MVC   XP+27(8),WORK+24                                                 
         MVI   XP+35,C'!'                                                       
         MVC   XP+36(8),WORK+32                                                 
         MVC   XP+45(8),WORK+40                                                 
         MVC   XP+54(8),WORK+48                                                 
         MVC   XP+63(8),WORK+56                                                 
*                                                                               
         MVC   XP+72(32),0(R5)                                                  
*                                                                               
         GOTO1 ACREPORT                                                         
*                                                                               
         LA    R5,32(R5)                                                        
         SH    R4,=H'32'                                                        
         BP    RPTR10                                                           
*                                                                               
RPTRX    B     XIT                                                              
*                                                                               
***********************************************************************         
*              EXTERNAL ADDRESS LIST                                  *         
***********************************************************************         
*                                                                               
ADCONS   DS    0F                                                               
         DC    V(PRNTBL)                                                        
         DC    V(SQUASHER)                                                      
         DC    V(CENTER)                                                        
         DC    V(HELLO)                                                         
         DC    V(HELEN)                                                         
         DC    V(CHOPCON)                                                       
         DC    V(PERVAL)                                                        
         DC    A(IO1)                                                           
         DC    A(IO2)                                                           
         DC    A(WRKREC)                                                        
         DC    A(WRKB)                                                          
         EJECT                                                                  
***********************************************************************         
*              LITERALS                                               *         
***********************************************************************         
*                                                                               
EFFS     DC    12X'FF'                                                          
*                                                                               
         GETEL R4,DATADISP,ELCODE                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*              MISC STUFF                                             *         
***********************************************************************         
*                                                                               
SORTCARD DC    CL80'SORT FIELDS=(1,43,A),FORMAT=BI,WORK=1'                      
RECCARD  DC    CL80'RECORD TYPE=F,LENGTH=385'                                   
*                                                                               
WKINDX   DC    CL8'INDEX'                                                       
WKREAD   DC    CL8'READ'                                                        
WKKEEP   DC    CL8'KEEP'                                                        
WKRTN    DC    CL8'RETAIN'                                                      
WKOPEN   DC    CL8'OPEN'                                                        
WKADD    DC    CL8'ADD'                                                         
WKCLSE   DC    CL8'CLOSE'                                                       
WKCMD    DC    CL8' '                                                           
*                                                                               
ACCFIL   DC    C'ACCOUNT '                                                      
ACCDIR   DC    C'ACCDIR  '                                                      
*                                                                               
NOACCCON DC    CL60'ACCOUNT AND CONTRA ACCOUNT DO NOT EXIST'                    
NOACC    DC    CL60'ACCOUNT DOES NOT EXIST'                                     
NOCON    DC    CL60'CONTRA ACCOUNT DOES NOT EXIST'                              
*                                                                               
         EJECT                                                                  
***********************************************************************         
*              BOX HOOK                                               *         
***********************************************************************         
*                                                                               
BXHOOK   DS    0D                                                               
         NMOD1 0,*BHOOK                                                         
*                                                                               
         L     RC,BOXRC            RESTORE REG C                                
*                                                                               
         MVC   XHEAD2+12(L'CMPABBR),CMPABBR                                     
         MVC   XHEAD2+21(L'CMPNAME),CMPNAME                                     
         MVC   XHEAD4(11),=C'BUDGET TYPE'                                       
*                                                                               
         MVC   SQUASHLN,SPACES                                                  
         MVC   SQUASHLN(L'BUDCODE),BUDCODE                                      
         MVC   SQUASHLN+12(L'BUDTYPE),BUDBTYPE                                  
*                                                                               
         OC    SQUASHLN,SPACES                                                  
         GOTO1 SQUASHER,DMCB,SQUASHLN,50                                        
         MVC   XHEAD4+15(12),SQUASHLN                                           
         MVC   XHEAD4+27(L'BUDNAME),BUDNAME                                     
*                                                                               
         USING BOXD,R4                                                          
         L     R4,ADBOX                                                         
         MVC   BOXCOLS(165),XSPACES                                             
         MVC   BOXROWS,XSPACES                                                  
         MVI   BOXROWS+6,C'T'                                                   
         MVI   BOXROWS+9,C'M'                                                   
         MVI   BOXCOLS,C'L'                                                     
*                                                                               
         CLI   RCSUBPRG,0          WORKER UPLOAD                                
         BNE   BX100                                                            
         MVI   BOXCOLS+45,C'C'                                                  
         MVI   BOXCOLS+90,C'C'                                                  
         MVI   BOXCOLS+99,C'C'                                                  
         MVI   BOXCOLS+116,C'C'                                                 
         MVI   BOXCOLS+126,C'C'                                                 
         MVI   BOXCOLS+143,C'R'                                                 
         B     BXIT                                                             
*                                                                               
BX100    CLI   RCSUBPRG,1          WORKER UPLOAD ERROR REPORT                   
         BNE   BXIT                                                             
         MVI   BOXCOLS+45,C'C'                                                  
         MVI   BOXCOLS+90,C'C'                                                  
         MVI   BOXCOLS+135,C'R'                                                 
         B     BXIT                                                             
*                                                                               
BXIT     MVI   BOXYORN,C'Y'                                                     
         MVI   BOXWT,1                                                          
         MVI   BOXINIT,0                                                        
         MVI   BOXBLANK,C'N'                                                    
         XMOD1 1                                                                
BOXRC    DC    A(0)                                                             
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*              BUFFERS                                                *         
***********************************************************************         
*                                                                               
IO1      DS    0D                  IOAREA #1                                    
         DS    2000C                                                            
MAXACC   EQU   *-IO1                                                            
*                                                                               
IO2      DS    0D                  IOAREA #2                                    
         DS    2000C                                                            
*                                                                               
WRKREC   DS    0D                  WORKER RECORD                                
         DS    2000C                                                            
*                                                                               
WRKB     DS    0D                  WORKER BUFFER AREA                           
WRKBLN   DS    CL5000                                                           
WRKBEND  DS    0C                                                               
*                                                                               
         EJECT                                                                  
***********************************************************************         
*              SORT RECORD DSECT                                      *         
***********************************************************************         
*                                                                               
SRECD    DSECT                     FOR QOPT1=W WORKER/BUDGET UPDATE             
SRSTAT   DS    CL1                 X'00'=NO ERROR, C'X'=ERROR                   
SRBUDNUM DS    CL2                 BUDGET NUMBER                                
SRBTYPE  DS    CL1                 BUCKET TYPE                                  
SRERROR  DS    XL1                                                              
SRERRACC EQU   X'80'               ACCOUNT DOESN'T EXIST                        
SRERRCON EQU   X'40'               CONTRA ACCOUNT DOESN'T EXIST                 
SRERRNO  EQU   X'00'               NO ERRORS                                    
SRSPARE  DS    CL2                 SPARE                                        
SRBUDREC DS    CL194               MAX LEN BUD REC                              
SRACCNM  DS    CL25                ACCOUNT NAME                                 
SRCONNM  DS    CL25                CONTRA ACCOUNT NAME                          
SREND    DS    C                                                                
SRECLNQ  EQU   *-SRECD                                                          
         EJECT                                                                  
***********************************************************************         
*              PRINT LINE DSECT                                       *         
***********************************************************************         
*                                                                               
PLINED   DSECT                                                                  
PLIN     DS    0C                                                               
         DS    CL2                                                              
PLINACCT DS    CL14                ACCOUNT  NUMBER                              
         DS    CL3                                                              
PLINANME DS    CL25                ACCOUNT NAME                                 
         DS    CL3                                                              
PLINCON  DS    CL14                CONTRA ACCOUNT  NUMBER                       
         DS    CL3                                                              
PLINCNME DS    CL25                CONTRA ACCOUNT NAME                          
         DS    CL3                                                              
PLINDATE DS    CL6                 DATE YYMM                                    
         DS    CL3                                                              
PLINAMT  DS    CL14                NEW AMOUNT                                   
         DS    CL3                                                              
PLINSTAT DS    CL7                 STATUS (ADD/CHANGE)                          
         DS    CL3                                                              
PLINPREV DS    CL14                PREVIOUS AMOUNT                              
         DS    CL3                                                              
PLINEND  DS    0C                                                               
         ORG   PLINDATE                                                         
PLINERR  DS    CL60                                                             
*                                                                               
         EJECT                                                                  
***********************************************************************         
*              WORKING STORAGE                                        *         
***********************************************************************         
*                                                                               
ACB502D  DSECT                                                                  
VTYPES   DS    0A                  EXTERNAL ADDRESSES                           
PRNTBL   DS    A                   PRNTBL                                       
SQUASHER DS    A                   SQUASHER                                     
CENTER   DS    A                   CENTER                                       
HELLO    DS    A                   HELLO                                        
HELEN    DS    A                   HELEN                                        
CHOPCON  DS    A                   CHOPPER                                      
PERVAL   DS    A                   PERVAL                                       
AIO1     DS    A                   IO AREA #1 (2000 BYTES)                      
AIO2     DS    A                   IO AREA #2 (2000 BYTES)                      
AWRKREC  DS    A                   WORKER RECORD                                
AWRKB    DS    A                   A(WORKER BUFFER)                             
VTYPLNQ  EQU   *-VTYPES                                                         
ADBOX    DS    A                   ADDRESS OF BOX ROUTINE                       
*                                                                               
STDATE   DS    PL3                 START DATE                                   
ENDATE   DS    PL3                 END DATE                                     
CMPABBR  DS    CL7                 COMPANY ABBREVIATION                         
CMPNAME  DS    CL36                COMPANY NAME                                 
*                                                                               
TYPE     DS    XL1                                                              
RCCOLUMN DS    XL1                                                              
ELCODE   DS    XL1                                                              
COMMAND  DS    CL8                 USED IN DATAMGR CALL                         
TODAYP   DS    PL3                 TODAY'S DATE PACKED                          
TODAYC   DS    CL6                 TODAY'S DATE PACKED                          
*                                                                               
AIO      DS    A                   IO AREA                                      
KEY2     DS    CL42                                                             
WRK2     DS    CL120                                                            
ELEM     DS    CL50                                                             
*                                                                               
SVADDR   DS    A                   SAVED ADDRESS                                
SVWRKSEQ DS    XL2                 SAVED WORKER SEQ NUMBER                      
SVBUDNUM DS    XL2                 SAVED BUDGET NUMBER                          
SVBTYPE  DS    XL1                 SAVED BUCKET TYPE                            
SVSTAT   DS    CL1                 SAVED STATUS                                 
SVACT    DS    CL12                                                             
SVCONTRA DS    CL15                SAVED CONTRA UL/ACCT                         
NEWACT   DS    CL12                                                             
SVKEY    DS    CL42                                                             
LASTBKEY DS    CL42                LAST BUDGET KEY FROM SORTER                  
YYMM     DS    PL2                 DATE PACKED                                  
DD       DS    PL1                                                              
NEWAMT   DS    PL8                 NEW AMOUNT                                   
PREVAMT  DS    PL8                 PREVIOUS AMOUNT                              
NEWTOT   DS    PL8                 NEW TOTAL                                    
PREVTOT  DS    PL8                 PREV TOTAL                                   
PRSTAT   DS    CL7                 STATUS ADDED/CHANGED                         
UNITNAME DS    CL36                UNIT NAME                                    
SVUNITNM DS    CL36                                                             
SVLEDGNM DS    CL36                                                             
LEDGNAME DS    CL36                LEDGER NAME                                  
SVUNTLDG DS    CL2                 SAVED UNIT AND LEDGER                        
NEWUL    DS    CL2                 NEW UNIT AND LEDGER                          
SVACCT   DS    CL12                SAVED ACCOUNT                                
BUDNAME  DS    CL36                BUDGET NAME                                  
BUDNUM   DS    CL6                 BUDGET NUMBER                                
BUDCODE  DS    CL10                BUDGET CODE                                  
BUDBTYPE DS    CL1                 BUDGET BUCKET TYPE                           
BUDNO    DS    XL2                 BUDGET NUMBER                                
ERRMSG   DS    CL36                                                             
*                                                                               
WKID     DS    CL(L'UKINDEX)       INDEX ID                                     
SVWKID   DS    CL(L'UKINDEX)       INDEX ID                                     
*                                                                               
*                                                                               
SQUASHLN DS    CL50                LINE TO BE SQUASHED                          
PVALBLK  DS    CL100               PERVAL BLOCK                                 
*                                                                               
SREC     DS    CL(SRECLNQ)         SORT RECORD                                  
SRECDONE DS    0H                                                               
*                                                                               
         EJECT                                                                  
***********************************************************************         
*              OTHER INCLUDES                                         *         
***********************************************************************         
*                                                                               
* DDREPXTRAD                                                                    
* ACBIGPRNTD                                                                    
* DDBOXEQUS                                                                     
* DDBIGBOX                                                                      
* DDMASTD                                                                       
* DDLOGOD                                                                       
* ACREPWORKD                                                                    
* ACGENMODES                                                                    
* ACGENBOTH                                                                     
* ACGENFILE                                                                     
* DMWRKRK                                                                       
* DMWRKRD                                                                       
* DDPERVALD                                                                     
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE DDREPXTRAD                                                     
       ++INCLUDE ACBIGPRNTD                                                     
       ++INCLUDE DDBOXEQUS                                                      
       ++INCLUDE DDBIGBOX                                                       
       ++INCLUDE DDMASTD                                                        
       ++INCLUDE DDLOGOD                                                        
       ++INCLUDE ACREPWORKD                                                     
       ++INCLUDE ACGENMODES                                                     
       ++INCLUDE ACGENBOTH                                                      
       ++INCLUDE ACGENFILE                                                      
       ++INCLUDE DMWRKRK                                                        
       ++INCLUDE DMWRKRD                                                        
       ++INCLUDE DDPERVALD                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'035ACREPB502S05/01/02'                                      
         END                                                                    
