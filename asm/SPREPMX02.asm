*          DATA SET SPREPMX02  AT LEVEL 139 AS OF 06/26/19                      
*PHASE SPMX02B                                                                  
*INCLUDE MXPRNT                                                                 
*INCLUDE MXPOST                                                                 
*INCLUDE MXCOMMON                                                               
*INCLUDE MXTABLE                                                                
*INCLUDE CONVMOS                                                                
*INCLUDE SORTER                                                                 
*INCLUDE DDUCOM                                                                 
*INCLUDE SPFMTINO                                                               
*INCLUDE BMONVAL                                                                
*INCLUDE PERVAL                                                                 
*INCLUDE PRINT                                                                  
*INCLUDE PRNTBL                                                                 
         SPACE                                                                  
*-------------------------------------------------------------*                 
* NOTE ABOUT REVERSAL OPTIONS--                               *                 
*                                                             *                 
* QOPT1 = R    AUTOMATIC GENERATED BY SPREQ00                 *                 
* QOPT3 = R,I  FORCED - WITH DATE IN QIORDT                   *                 
* QOPT4 = M    DDS ONLY (MARK) OPTION                         *                 
*              MARKS BILLS TRANSFERED & SETS TRANSFER DATE =  *                 
*              TODAY (QOPT3 AND QIORDT MUST BE SET)           *                 
* QOPT4 = U    DDS ONLY (UNPOST) OPTION                       *                 
*              READS DELETED BILL RECORDS AND UNPOSTS         *                 
*                                                             *                 
* NOTE SPECIAL RUN WE HAVE TO RUN DURING THE DAY              *                 
* NEW WAY:                                                    *                 
* USE CONTROL CARDS: RERUN=YES, DATE=MM/DD/YY, WRITE=NO,      *                 
* POSTING=YES (PGM WILL SET REQUEST CARD OLD WAY)             *                 
* OLD WAY:                                                    *                 
* QOPT1 = D    IF BILLING DIES AT NIGHT - NEXT MORNING        *                 
* &QTRADT SET  RUN WITH THIS OPTION - WILL READ ALL TRANSFERRED                 
* &POST=YES    BILLS WITH GIVEN DATE - AND REGENERATE POSTINGS*                 
* & WRITE=NO   THEN WHEN WRKR FILE GENERATED, HAVE DATA CONTROL                 
*              RUN '99' DUMMY SORT MERGE - CHECK IF ALL O.K   *                 
*-------------------------------------------------------------*                 
*---------------------------------------------------------------------*         
* USER    JIRA       DATE                  CHANGE LOG                 *         
* ---- ----------  -------- ------------------------------------------*         
* SMUR  SPEC-33293 03/05/19 ESTIMATE PO NUMBER FOR MX (19.2)          *         
* SMUR  SPEC-9487  03/24/17 CLIENT FILTER FOR CURRENCY                *         
*---------------------------------------------------------------------*         
         TITLE 'BILLING TRANSFER - ADDS POSTINGS TO WRKFIL'                     
SPMX02   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,SPMX02,R9,R8                                                   
*                                                                               
         L     RA,0(R1)                                                         
         LA    R7,4095(RA)                                                      
         LA    R7,1(R7)                                                         
         USING SPWORKD,RA,R7      GLOBAL WORKING STORAGE                        
         LA    RC,SPACEND                                                       
         LA    R6,4095(RC)                                                      
         LA    R6,1(R6)                                                         
         USING SPMXWRKD,RC,R6     LOCAL WORKING STORAGE                         
         L     RF,UTL                                                           
         MVC   SAVSYS,4(RF)       SAVE SPOT SYSTEM NUMBER                       
*                                                                               
*   FIRST'S                                                                     
*                                                                               
         CLI   MODE,RUNFRST       RUN FIRST                                     
         BE    RUNF                                                             
         CLI   MODE,REQFRST       REQUEST FIRST                                 
         BE    REQF                                                             
         CLI   MODE,OFCFRST       OFFICE FIRST                                  
         BE    OFCF                                                             
         CLI   MODE,CLTFRST       CLIENT FIRST                                  
         BE    CLTF                                                             
         CLI   MODE,PRDFRST       PRODUCT FIRST                                 
         BE    PRDF                                                             
         CLI   MODE,ESTFRST       ESTIMATE FIRST                                
         BE    ESTF                                                             
*                                                                               
*   LAST'S                                                                      
*                                                                               
         CLI   MODE,OFCLAST       OFFICE LAST                                   
         BE    OFCL                                                             
         CLI   MODE,CLTLAST       CLIENT LAST                                   
         BE    CLTL                                                             
         CLI   MODE,PRDLAST       PRODUCT LAST                                  
         BE    PRDL                                                             
         CLI   MODE,REQLAST       REQUEST LAST                                  
         BE    REQL                                                             
         CLI   MODE,RUNLAST       RUN LAST                                      
         BE    RUNL                                                             
*                                                                               
EXIT     L     RF,UTL                                                           
         MVC   4(1,RF),SAVSYS     ALWAYS RESET SYSTEM NUMBER                    
         XIT1                                                                   
*                                                                               
XITNO    LTR   RB,RB              GENERAL XIT - W/CC CODE NOT EQUAL             
         B     XIT                                                              
*                                                                               
XITYES   CR    RB,RB              GENERAL XIT - W/CC CODE EQUAL                 
         B     XIT                                                              
*                                                                               
XIT      XIT1                                                                   
         EJECT                                                                  
*-------------------------------------------------------------------*           
* RUN FIRST - SET UP WORKER FILE ID, OPEN ACC FILES & READ COMP REC *           
*-------------------------------------------------------------------*           
*                                                                               
RUNF     SR    R3,R3               SET UP ENTRIES TO MXCOMMON ROUTINES          
         LA    R4,COMTAB                                                        
         LA    R5,NCOMTAB                                                       
RUNF10   MVC   0(L'COMTAB,R4),=V(MXCOMMON)                                      
         STC   R3,0(R4)                                                         
         AHI   R3,1                                                             
         AHI   R4,L'COMTAB                                                      
         BCT   R5,RUNF10                                                        
*                                                                               
         L     RE,=V(WKFKEYS)                                                   
         SHI   RE,1                                                             
         MVC   MAX#WKF,0(RE)                                                    
         L     RF,=V(POSTNGSX)                                                  
         L     RE,=V(POSTINGS)                                                  
         SR    RF,RE              LENGTH OF POSTINGS AREA                       
         SR    RE,RE                                                            
         LA    R1,ACPRTNL2        LENGTH OF EACH ENTRY                          
         DR    RE,R1                                                            
         STC   RF,PSTNUM2         NUMBER OF ENTRIES                             
         L     RF,=V(IOAREAX)                                                   
         L     RE,=V(IOAREA)                                                    
         SR    RF,RE              LENGTH OF IOAREA                              
         SR    RE,RE                                                            
         LA    R1,ACPRTNL         LENGTH OF EACH ENTRY                          
         DR    RE,R1                                                            
         STC   RF,PSTNUMA         NUMBER OF ENTRIES                             
         MVC   AMYAREA,=V(MYAREA)                                               
         MVC   AMYAREA2,=V(MYAREA2)                                             
         MVC   AMYAREA3,=V(MYAREA3)                                             
         MVC   AAORBIL1,=V(AORBIL1)                                             
         MVC   AAORBIL2,=V(AORBIL2)                                             
         MVC   AAORBIL3,=V(AORBIL3)                                             
         MVC   AMXBLK,=V(MXBLK)                                                 
*                                                                               
         L     R1,=A(PRVTAB)                                                    
         ST    R1,APRVTAB         SAVE A(PRVTAB)                                
*                                                                               
         L     RF,ADCONLST                                                      
         USING SPADCONS,RF                                                      
         MVC   AOFFICER,VOFFICER   SAVE OFFICER'S ADDRESS                       
         DROP  RF                                                               
*                                                                               
         USING MASTD,R3                                                         
         L     R3,VMASTC                                                        
         OC    ACPOSTER,ACPOSTER                                                
         BNZ   RUNF20                                                           
*                                                                               
         MVC   MCDUB,=CL8'T62210'                                               
         GOTO1 MCVLOADM,DMCB,0                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   ACPOSTER,DMCB+4                                                  
*                                                                               
RUNF20   CLI   MCPOSTNG,NO        IF POSTING=NO IN MASTER                       
         BNE   *+8                                                              
         OI    MXOPT,MXNOPOST     THEN SET POST=NO INTERNALLY                   
         CLI   MCWRITE,NO         IF WRITE=NO IN MASTER                         
         BNE   *+8                                                              
         OI    MXOPT,MXNOWRIT     THEN SET WRITE=NO INTERNALLY                  
         MVC   UPSI,MCUPSI                                                      
         MVI   SYS,C'S'                                                         
         CLI   MCNETPAK,YES                                                     
         BNE   *+8                                                              
         MVI   SYS,C'N'                                                         
         DROP  R3                                                               
*                                                                               
         BAS   RE,CHKMEDIA        READ MEDIA MX CONTROL PROFILE                 
         MVC   ACCTSE2,ACCTSE                                                   
         MVC   ACCTCD2,ACCTCD                                                   
         MVC   TEMPSE,ACCTSE      SE NUMBER                                     
         MVC   TEMPCD,ACCTCD      COMPANY CODE                                  
         BAS   RE,SETFILE         SET ACCOUNTING FILE                           
         MVC   ACCTOID1,TEMPOID   SAVE ORIGIN ID                                
         MVC   ACCTOID2,TEMPOID                                                 
         BAS   RE,CHKSPLT         RESOLVE VAULE FOR MPRKALPH                    
         LA    R1,SRTKEY-SRTKEYD+1                                              
         CVD   R1,DUB                                                           
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  SORTCARD+13(3),DUB                                               
         LA    R1,SRTKEYLQ-(SRTKEY-SRTKEYD)                                     
         CVD   R1,DUB                                                           
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  SORTCARD+17(3),DUB                                               
         GOTO1 =V(SORTER),DMCB,SORTCARD,RECCARD                                 
*                                                                               
         XC    SVPROF,SVPROF      READ ACCOUNT MX CONTROL PROFILE               
         XC    WORK,WORK                                                        
         MVI   WORK,C'A'-X'40'    ACCOUNTING SYSTEM                             
         MVC   WORK+2(2),=C'MX'   PROFILE NAME                                  
         MVC   WORK+4(1),ACCTCD   NATIVE COMPANY CODE                           
         MVC   WORK+12(2),RCAGENCY                                              
         GOTO1 GETPROF,DMCB,WORK,SVPROF,DATAMGR                                 
RUNFX    B     EXIT                                                             
         EJECT                                                                  
*---------------------------------------------------------------------*         
* CHKMEDIA -                                                          *         
*     (A) READ SPOT MX MEDIA CONTROL PROFILE                          *         
*     (B) GET POSSIBLE OVERRIDE ALPHA ID FROM PROFILE, SVPROF2        *         
*         THIS ALPHA IS WHERE TRANSFER RECORDS ARE SETUP ON ACC       *         
*     (C) IF NO OVERRIDE THEN DEFAULT TO REQUEST ALPHA (ALREADY SET)  *         
*         ELSE GET SE AND HEX COMPANY VALUE TO READ ACC FILE          *         
*                                                                     *         
*---------------------------------------------------------------------*         
CHKMEDIA NTR1                     CHECKS IF 3 MEDIA FILE -> 1 ACC FILE          
         XC    SVPROF2,SVPROF2                                                  
         XC    WORK,WORK                                                        
         MVC   WORK(4),=C'S0MX'   SPOT MX CONTROL PROFILE                       
         MVC   WORK+4(2),RCAGENCY AGENCY ID                                     
         GOTO1 GETPROF,DMCB,WORK,SVPROF2,DATAMGR                                
         OC    SVPROF2(2),SVPROF2                                               
         BZ    CHKMEDX                                                          
         CLC   SVPROF2(2),=C'00'   CHARACTER ZEROS                              
         BNE   *+14                                                             
         XC    SVPROF2(2),SVPROF2  SAME AS BINARY ZEROS                         
         B     CHKMEDX                                                          
*                                                                               
         XC    AKEY,AKEY                                                        
         LA    RE,AKEY                                                          
         USING CT5REC,RE                                                        
         MVI   CT5KTYP,CT5KTYPQ   C'5'                                          
         MVC   CT5KALPH,SVPROF2   ALPHA USER ID                                 
         DROP  RE                                                               
         L     R2,AMYAREA                                                       
         USING CT5REC,R2                                                        
         GOTO1 DATAMGR,DMCB,=C'DMREAD  ',=C'CTFILE ',AKEY,(R2),0                
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'               WHAT SHOULD I DO???                           
         SR    R0,R0                                                            
         LA    R1,CT5DATA                                                       
         USING CTSYSD,R1                                                        
CHKMED5  CLI   0(R1),0                                                          
         BNE   *+6                                                              
         DC    H'0'               WHAT SHOULD I DO????                          
         CLI   0(R1),CTSYSELQ     X'21' - SYSTEM ELEMENT                        
         BNE   CHKMED8                                                          
         CLI   CTSYSNUM,6         ACC SYSTEM                                    
         BE    CHKMED10                                                         
CHKMED8  ICM   R0,1,1(R1)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R1,R0                                                            
         B     CHKMED5                                                          
*                                                                               
CHKMED10 MVC   ACCTSE,CTSYSSE     SE NUM                                        
         MVC   ACCTCD,CTSYSAGB    COMPANY CODE                                  
CHKMEDX  B     EXIT                                                             
         DROP  R2,R1                                                            
         EJECT                                                                  
*---------------------------------------------------*                           
* SETFILE - OPEN ACC FILES & SET COMP RECORD VALUES *                           
*      NTRY - TEMPSE,TEMPCD                         *                           
*---------------------------------------------------*                           
*                                                                               
SETFILE  NTR1                                                                   
         L     RF,UTL                                                           
         MVC   4(1,RF),TEMPSE     SET SE NUMBER FOR FILE                        
         L     R2,ADBUY           USE AS WORK AREA                              
         LA    R3,FILELIST                                                      
         MVI   EMULATE,C'N'                                                     
         GOTO1 DATAMGR,DMCB,=C'DTFADD',=C'ACCOUNT'                              
         L     RE,12(R1)          A(ACCOUNT DCB)                                
         TM    ISFTYPE-ISDTF(RE),ISFTEMU                                        
         BNO   SETFIL10                                                         
         LA    R3,EMULIST         FILE IS EMULATED                              
         MVI   EMULATE,C'Y'                                                     
SETFIL10 GOTO1 DATAMGR,DMCB,=C'OPEN',=C'ACCFIL ',(R3),(R2)                      
*                                 READ AND SET COMPANY RECORD VALUES            
         LA    R2,AKEY            READ COMPANY RECORD                           
         USING CPYRECD,R2                                                       
         MVC   CPYKEY,SPACES                                                    
         MVC   CPYKCPY,TEMPCD     COMPANY CODE                                  
         L     R2,AMYAREA                                                       
         ST    R2,AREC                                                          
         MVI   RDSE,2             READ FROM OTHER ACC FILE(IF APPL)             
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMREAD'                               
         BE    *+6                                                              
         DC    H'0'               *** NEEDS TO BE CHANGED                       
         LA    R1,ACCORFST                                                      
         AR    R2,R1                                                            
         USING CPYELD,R2                                                        
         SR    R0,R0                                                            
*                                                                               
SETFIL20 CLI   0(R2),0            END OF RECORD                                 
         BNE   *+6                                                              
         DC    H'0'               *** MUST BE                                   
         CLI   0(R2),CPYELQ       MUST HAVE X'10' ELEMENT                       
         BE    SETFIL30                                                         
         ICM   R0,1,1(R2)         LENGTH OF ELEMENT                             
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R2,R0              BUMP TO NEXT ELEMENT                          
         B     SETFIL20                                                         
*                                                                               
SETFIL30 MVC   SVPROD,CPYPROD     SAVE UNIT/LEDGER FOR CLT/PRODUCT              
         MVI   SVMI,C'Y'          MI RECORDS USED                               
         TM    CPYSTAT4,CPYSMINT                                                
         BO    *+8                                                              
         MVI   SVMI,C'N'                                                        
         MVI   SVNEWOF,C'Y'       NEW OFFICE CODES USED                         
         TM    CPYSTAT4,CPYSOFF2                                                
         BO    *+8                                                              
         MVI   SVNEWOF,C'N'                                                     
         MVI   SVCOST,C'Y'        USE COSTING,BILL,REV                          
         TM    CPYSTAT1,CPYSCOST                                                
         BO    *+8                                                              
         MVI   SVCOST,C'N'                                                      
         MVC   TEMPOID,CPYUID     ORIGIN ID                                     
*                                                                               
         CLI   CPYLN,CPYLN3Q      MUST BE AT LEAST THIS BIG                     
         BL    SETFILX                                                          
         TM    CPYSTAT9,CPYMBSOD  SAME NIGHT POSTINGS?                          
         BZ    SETFILX                                                          
         OI    MXOPT,MXMBSOB                                                    
*                                                                               
SETFILX  B     XIT                                                              
         DROP  R2                                                               
         SPACE 1                                                                
FILELIST DC    CL8'NACCFIL '                                                    
         DC    CL8'NCTFILE '                                                    
         DC    CL10'X'                                                          
         SPACE                                                                  
EMULIST  DC    CL8'NACCDIR '                                                    
         DC    CL8'NACCMST '                                                    
         DC    CL8'NACCARC '                                                    
         DC    CL8'NCTFILE '                                                    
         DC    CL10'X'                                                          
         EJECT                                                                  
*-------------------------------------------*                                   
* REQUEST FIRST - SET INITIAL VALUES        *                                   
*-------------------------------------------*                                   
*                                                                               
REQF     L     R2,VMASTC                                                        
         USING MASTD,R2                                                         
         CLI   MCRERUN,C'Y'        IF RERUN = Y                                 
         BNE   REQF2                                                            
         MVI   QOPT1,C'D'          SET DIED OPTION AND TRANSFER DATE            
         GOTO1 DATCON,DMCB,(4,MCDATE),(0,QTRADT)                                
         DROP  R2                                                               
*                                                                               
REQF2    BAS   RE,CHKSPLT         CHECK ID ON SPLIT MEDIA FILES                 
         BAS   RE,ANYREC          CHECK ANY POSTING RECORDS?                    
         BNE   REQFEXIT                                                         
*                                                                               
         L     RF,ADAGY           SET COUNTRY                                   
         MVC   CNTRY,AGYPROF+7-AGYHDR(RF)                                       
         MVC   MYMED,QMED         SET MEDIA                                     
         CLI   SYS,C'N'                                                         
         BNE   *+10                                                             
         MVC   MYMED,QOPT2        SET REAL NETWORK  MEDIA                       
*                                 STILL NEED TO DO, GRANT TOO                   
         XC    B1XPROF,B1XPROF    READ B1X PROFILE                              
         XC    WORK,WORK                                                        
         MVC   WORK(4),=C'SB1X'                                                 
         NI    WORK,X'BF'                                                       
         MVC   WORK+4(2),AGY                                                    
         MVC   WORK+6(1),MED                                                    
         GOTO1 GETPROF,DMCB,WORK,B1XPROF,DATAMGR                                
*                                                                               
         MVI   RQNOAML,C'Y'       SUPPRESS FILCON ACTV MKT LOGIC                
         CLC   QEST,SPACES        IF NO ESTIMATE NUMBER                         
         BNE   REQF5                                                            
         MVC   QEST,=C'ALL'       GET ALL ESTIMATES                             
         MVC   QSTART,=X'F8F0F0F1F0F1'  ALL DATES                               
         MVC   QEND,=X'FFFFF1F2F3F1'                                            
*                                                                               
REQF5    XC    RERUNDT,RERUNDT                                                  
         CLI   QOPT1,C'R'         IF REVERSAL                                   
         BE    REQF10                                                           
         CLI   QOPT1,C'D'         IF QOPT1=D                                    
         BNE   REQF20                                                           
         MVC   RERUNDT,QTRADT     SPECIAL FOR WORKER FILE DATE                  
*                                                                               
REQF10   XC    REQTDT,REQTDT                                                    
         CLC   QTRADT,SPACES      SET TRANSFER DATE - IF GIVEN                  
         BNH   REQF20                                                           
         GOTO1 DATCON,DMCB,(0,QTRADT),(2,REQTDT)                                
*                                                                               
REQF20   L     RE,=A(ACCAREA)     SAVED AREA FOR POSTER                         
         ICM   RF,15,=AL4(ACCAREAX-ACCAREA)                                     
         XCEF                                                                   
         MVI   RCSUBPRG,10                                                      
         LA    R2,PRTBLOCK                                                      
         USING MXPRNTD,R2                                                       
         LA    RE,PNUMTOTS                                                      
         LA    R1,PMXSAVE                                                       
REQF30   ZAP   0(L'PMXSAVE,R1),=P'0'                                            
         AHI   R1,L'PMXSAVE                                                     
         BCT   RE,REQF30                                                        
*                                 SET MXPRNT BLOCK FOR INITIALIZATION           
         GOTO1 =A(SETPRT),DMCB,(RC)                                             
         MVI   PMODE,PMINIT                                                     
         GOTO1 =V(MXPRNT),DMCB,(R2)                                             
REQFX    B     EXIT                                                             
         DROP  R2                                                               
         SPACE                                                                  
REQFEXIT L     RF,UTL             SKIP TO NEXT REQUEST                          
         MVC   4(1,RF),SAVSYS                                                   
         GOTO1 AENDREQ                                                          
         EJECT                                                                  
*---------------------------------------------------*                           
* CHKSPLT - CHECK IF AGENCY IS ON SPLIT MEDIA FILES *                           
*---------------------------------------------------*                           
*                                                                               
CHKSPLT  NTR1                                                                   
         XC    SALPH,SALPH                                                      
         OC    SVPROF2(2),SVPROF2                                               
         BZ    CHKSPX             NO SPLIT MEDIA FILES                          
         XC    AKEY,AKEY          READ POST RULE RECORD                         
         LA    R2,AKEY                                                          
         USING MPRRECD,R2                                                       
         MVI   MPRKTYP,MPRKTYPQ   X'2F'                                         
         MVI   MPRKSUB,MPRKSUBQ   X'01'                                         
         MVC   MPRKCPY,ACCTCD     NATIVE COMPANY CODE                           
         MVC   MPRKALPH,QAGY      AGENCY ALPHA                                  
         MVC   MPRKSYS,SYS        SYSTEM                                        
         MVI   MPRKPRO,MPRKREG    REGULAR                                       
         L     R2,AMYAREA                                                       
         ST    R2,AREC                                                          
         USING MPRRECD,R2                                                       
         MVI   RDSE,1             READ FROM NATIVE ACC FILE                     
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMREAD'                               
         CLC   0(MPRKMED-MPRKTYP,R2),AKEY                                       
         BNE   CHKSPX                                                           
         SR    R0,R0                                                            
         LA    R1,ACCORFST                                                      
         AR    R2,R1                                                            
         USING MTPELD,R2                                                        
CHKSP10  CLI   0(R2),0                                                          
         BE    CHKSPX                                                           
         CLI   0(R2),MTPELQ                                                     
         BNE   CHKSP15                                                          
         CLI   MTPFNUM,MTPFDMED     DDS MEDIA FILE?                             
         BE    CHKSP20                                                          
CHKSP15  ICM   R0,1,1(R2)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R2,R0                                                            
         B     CHKSP10                                                          
*                                                                               
CHKSP20  MVC   SALPH,QAGY         SPLIT MEDIA FILE AGY ID                       
CHKSPX   B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*---------------------------------------------------*                           
* ANYREC - SEE IF AT LEAST SYSTEM LVL RECORD EXISTS *                           
*---------------------------------------------------*                           
*                                                                               
ANYREC   NTR1                                                                   
         CLI   QOPT1,C'R'                                                       
         BNE   ANYREC5                                                          
         CLI   QOPT4,C'M'                                                       
         BE    ANYRECX                                                          
ANYREC5  XC    AKEY,AKEY          READ POST RULE RECORD                         
         LA    R2,AKEY                                                          
         USING MPRRECD,R2                                                       
         MVI   MPRKTYP,MPRKTYPQ   X'2F'                                         
         MVI   MPRKSUB,MPRKSUBQ   X'01'                                         
         MVC   MPRKCPY,ACCTCD     NATIVE COMPANY CODE                           
         MVC   MPRKALPH,SALPH     AGY FOR MEDIA SPLIT FILES                     
         MVC   MPRKSYS,SYS        SYSTEM                                        
         L     R2,AMYAREA                                                       
         ST    R2,AREC                                                          
         USING MPRRECD,R2                                                       
         MVI   RDSE,1             READ FROM NATIVE ACC FILE                     
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMREAD'                               
         CLC   0(MPRKMED-MPRKTYP,R2),AKEY                                       
         BE    ANYRECX                                                          
         DROP  R2                                                               
*                                                                               
         LA    R2,PRTBLOCK                                                      
         USING MXPRNTD,R2                                                       
         GOTO1 =A(SETPRT),DMCB,(RC)                                             
         MVI   PMODE,PMMSG        PRINT MSG                                     
         OI    PMSG,PMNOREC       NO POSTING RECORDS                            
         MVC   PMSGTYPE,MSGTYPE   LEVEL WHICH PROF/MAINT APPLIES                
         GOTO1 =V(MXPRNT),DMCB,(R2)                                             
         LTR   RB,RB                                                            
ANYRECX  XIT1                     CC CODE SET                                   
         DROP  R2                                                               
         EJECT                                                                  
*-------------------------------------------*                                   
* OFFICE FIRST - EJECT PAGE                 *                                   
*-------------------------------------------*                                   
*                                                                               
OFCF     MVI   FORCEHED,C'Y'      FORCE TO A NEW PAGE                           
         B     EXIT                                                             
         SPACE 2                                                                
*-------------------------------------------*                                   
* CLIENT FIRST - CLEARS MYCLT FOR ESTF MODE *                                   
*-------------------------------------------*                                   
*                                                                               
CLTF     MVI   FORCEHED,C'Y'      FORCE TO A NEW PAGE                           
         MVI   ANYBILL,C'N'       NO BILLS PROCESSED FOR THIS CLIENT            
         XC    MYCLTB,MYCLTB      INDICATE NEW CLIENT                           
         B     EXIT                                                             
         SPACE 2                                                                
*-------------------------------------------*                                   
* PRODUCT FIRST - CLEARS MYPRD FOR ESTF MODE*                                   
*-------------------------------------------*                                   
*                                                                               
PRDF     XC    MYPRD,MYPRD        INDICATE NEW PRODUCT                          
         B     EXIT                                                             
         EJECT                                                                  
*---------------------------------------------------------------*               
* ESTIMATE FIRST - FOR EACH ESTIMATE- READS THROUGH BILL RECORDS*               
*---------------------------------------------------------------*               
ESTF     L     R2,ADEST           ESTIMATE                                      
         USING ESTHDRD,R2                                                       
         MVC   SVEDESC,EDESC      SAVE ESTIMATE DESCRIPTION                     
         OC    SVEDESC,SPACES                                                   
         MVC   SVEUSER1,EUSER1    SAVE USER FIELDS                              
         MVC   SVEUSER2,EUSER2                                                  
         MVC   SVEPONUM,EPONUM     PO NUMBER                                    
         OC    SVEPONUM,SPACES                                                  
*                                                                               
*        XC    SVUCMPLN,SVUCMPLN                                                
         XC    SVUCMELN,SVUCMELN                                                
         XC    SVUCMMLN,SVUCMMLN                                                
*                                                                               
*        MVC   SVUCMDP,SPACES                                                   
         MVC   SVUCMDE,SPACES                                                   
         MVC   SVUCMDM,SPACES                                                   
*                                                                               
*        MVC   SVUCMP,SPACES                                                    
         MVC   SVUCME,SPACES                                                    
         MVC   SVUCMM,SPACES                                                    
         MVC   SESTKEY,0(R2)      SAVE ESTIMATE KEY                             
*                                                                               
*                                                                               
ESTF2E   XC    KEY,KEY            ---LOOK FOR BILL RECORDS                      
         LA    R5,KEY                                                           
         USING BILLRECD,R5                                                      
         MVC   BKEYAM,BAGYMD      AGENCY/MEDIA                                  
         MVC   BKEYCLT,BCLT       CLIENT                                        
         MVC   BKEYPRD,PRD        CURRENT PRODUCT                               
         MVC   BKEYEST,EKEYEST                                                  
         MVI   BKEYINV+1,X'01'                                                  
         MVC   KEYSAVE,KEY                                                      
         LA    R1,DMCB                                                          
         LA    RE,DMRDHI                                                        
         ST    RE,0(R1)                                                         
         CLI   QOPT4,C'U'         IF REVERSAL TO UNPOST                         
         BNE   *+8                                                              
         MVI   0(R1),X'08'        PASS BACK DELETED RECORDS                     
         GOTO1 DATAMGR,DMCB,,SPTDIR,KEY,KEY                                     
         B     ESTF5                                                            
*                                                                               
ESTF3    BAS   RE,GETSME          RE-RESTABLISH READ SEQUENCE                   
ESTF4    BAS   RE,GETNXT          GET NEXT RECORD                               
ESTF5    CLC   KEY(8),SESTKEY     CHECK SAME AGYMED/CLT/PRD/EST                 
         BNE   ESTFX              NOPE                                          
         OC    BKEYYSRV(5),BKEYYSRV                                             
         BZ    ESTF4                                                            
*                                                                               
* IMPLEMENT THIS CODE WHEN YOU NEED TO RE-RUN THE MX'S DURING THE DAY           
* BREAKING OUT THE REQUESTS BY CLIENT B/C IT'S TOO BIG.                         
*                                                                               
* TEST                                                                          
*        CLC   CLT,=C'OS'          PROCESS CLIENTS LOWER THAN OS                
*        BNL   ESTF4                                                            
*        CLC   CLT,=C'OS'                                                       
*        BL    ESTF4               PROCESS CLIENTS OS AND HIGHER                
* TEST                                                                          
         CLI   QOPT1,C'D'                                                       
         BE    ESTF6              TAKE ONLY TRANSFERRED                         
         CLI   QOPT1,C'R'         REVERSE OPTION?                               
         BNE   ESTF7                                                            
         CLI   QOPT4,C'M'         MARK BILLS?                                   
         BE    ESTF7              YES WANT UNTRANSFERRED BILLS                  
ESTF6    TM    KEY+13,X'01'       WANT TRANSFERRED BILLS ONLY                   
         BNO   ESTF4                                                            
         CLI   QOPT4,C'U'         AND UNPOST OPTION?                            
         BNE   ESTF10                                                           
         TM    KEY+13,X'80'       WANT DELETED BILLS ONLY                       
         BNO   ESTF4                                                            
         B     ESTF10                                                           
*                                                                               
ESTF7    TM    KEY+13,X'01'       REGULAR REQUEST                               
         BNO   ESTF10             WANT UNTRANSFERRED BILLS ONLY                 
         B     ESTF4                                                            
         EJECT                                                                  
ESTF10   L     R5,ADBILL          --- FILTER BILL RECORDS                       
         ST    R5,AREC                                                          
         MVI   REVSTAT,C'N'                                                     
         MVC   SKEY,KEY           SAVE KEY                                      
         BAS   RE,GETRC           GET THE RECORD                                
         BAS   RE,FILTBIL         FILTER BILL & SETS SOME INFO                  
         BNE   ESTF4                                                            
*                                                                               
         USING BILLREC,R5                                                       
*                                                                               
         LA    RE,UCOMBLK         NOW READ NEW UCOM USER DEF FIELDS             
         LA    RF,L'UCOMBLK       FOR ESTIMATE WITHIN EFFECTIVE DATES           
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         LA    R3,UCOMBLK                                                       
         USING DDUCOMD,R3                                                       
         MVC   UCACOMF,ACOMFACS    A(COMFACS)                                   
         MVC   UCSYS,SYS           SYSTEM                                       
         MVC   UCPROG,=C'MX'       CALLING PGM(THIS MEANS CHK MBI BIT)          
         MVC   UCSAM,BAGYMD        HEX AGENCY/MEDIA                             
         MVC   UCSCLT,BCLT         CLIENT                                       
         MVC   UCPRD,PRD           PRODUCT                                      
         MVC   UCSEST,EKEYEST      ESTIMATE                                     
         MVC   UCMOS,BKEYYSRV      MOS                                          
         OI    UCOPT,UCOEST+UCOEFF RETURN ESTIMATE UCOMS W/I EFF RANGE          
         GOTO1 =V(DDUCOM),UCOMBLK                                               
         CLI   UCERROR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         TM    UCDATA,UCDNOCLT+UCDNOEST     NO CLIENT/ESTIMAT FOUND?            
         BNZ   ESTF10A                                                          
         MVC   SVUCMELN,UCELENS    LENGTHS OF 4 ESTIMATE UCOM FIELDS            
         L     RF,UCETTLS                                                       
         MVC   SVUCME1D(L'SVUCMDE),0(RF) ESTIMATE UCOMS DESCRIPTIONS            
         L     RF,UCEDATA                                                       
         MVC   SVUCME1(L'SVUCME),0(RF) ESTIMATE UCOMS                           
         OC    SVUCME1D(L'SVUCMDE),SPACES                                       
         OC    SVUCME1(L'SVUCME),SPACES                                         
         DROP  R2                                                               
*                                                                               
ESTF10A  CLC   BLMKT,SPACES        DO WE HAVE A MARKET?                         
         BE    ESTF11              NO, SKIP THE UCOM LOOKUP                     
         CLI   SYS,C'N'            FOR NET-MARKET LEVEL UCOM'S NOT              
         BE    ESTF11              NOT VALID SO SKIP EVEN IF THERE              
*                                  IS A MARKET.                                 
         XC    WORK,WORK           SKIP ANY THAT ARE NON-NUMERIC                
         MVZ   WORK(L'BLMKT),BLMKT                                              
         CLC   WORK(L'BLMKT),=C'0000'                                           
         BNE   ESTF11                                                           
         MVN   WORK(L'BLMKT),BLMKT                                              
         CLC   WORK(L'BLMKT),=C'0000'                                           
         BNH   ESTF11                                                           
         CLC   WORK(L'BLMKT),=C'9999'                                           
         BH    ESTF11                                                           
         LA    RE,UCOMBLK         NOW READ NEW UCOM USER DEF FIELDS             
         LA    RF,L'UCOMBLK                                                     
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         LA    R3,UCOMBLK                                                       
         USING DDUCOMD,R3                                                       
         MVC   UCACOMF,ACOMFACS    A(COMFACS)                                   
         MVC   UCSYS,SYS           SYSTEM                                       
         MVC   UCPROG,=C'MX'       CALLING PGM(THIS MEANS CHK MBI BIT)          
         MVC   UCSAM,BAGYMD        HEX AGENCY/MEDIA                             
         MVC   UCSCLT,BKEYCLT      CLIENT                                       
         MVC   UCPRD,BKEYPRD       PRODUCT                                      
         MVC   UCSEST,BKEYEST      ESTIMATE                                     
         PACK  DUB,BLMKT                                                        
         CVB   R0,DUB                                                           
         STH   R0,HALF                                                          
         MVC   UCMKT,HALF          MARKET                                       
         OI    UCOPT,UCOMKT        RETURN MARKET UCOMS                          
         GOTO1 =V(DDUCOM),UCOMBLK                                               
         CLI   UCERROR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         TM    UCDATA,UCDNOCLT+UCDNOMKT     NO CLIENT/MARKET FOUND?             
         BNZ   ESTF11                                                           
         MVC   SVUCMMLN,UCMLENS      LENGTHS OF 4 MARKET UCOM FIELDS            
         L     RF,UCMTTLS                                                       
         MVC   SVUCMM1D(L'SVUCMDM),0(RF)   MARKET UCOMS DESCRIPTIONS            
         L     RF,UCMDATA                                                       
         MVC   SVUCMM1(L'SVUCMM),0(RF)   MARKET UCOMS                           
         OC    SVUCMM1D(L'SVUCMDM),SPACES                                       
         OC    SVUCMM1(L'SVUCMM),SPACES                                         
*                                                                               
ESTF11   CLC   MYCLTB,BKEYCLT     NEW CLIENT IN BILL RECORD?                    
         BE    ESTF12                                                           
         MVC   MYCLTB,BKEYCLT     YES - SET NEW CLIENT VALUES                   
         XC    MYPRD,MYPRD                                                      
         BAS   RE,NEWCLT                                                        
         BAS   RE,RDB1PROF         READ B1 PROFILE                              
*                                                                               
ESTF12   CLC   MYPRD,BKEYPRD      NEW PRODUCT IN BILL RECORD?                   
         BE    ESTF14                                                           
         MVC   MYPRD,BKEYPRD      YES - SET NEW PRODUCT VALUES                  
         BAS   RE,NEWPRD                                                        
         BAS   RE,RDPROF                                                        
         GOTO1 =A(CHKPROF),DMCB,(RC)                                            
         CLI   MAKEPOST,C'N'                                                    
         BNE   *+12                                                             
         MVI   MODE,PRDLAST       SKIP  TO NEXT PRD (THANX MEL,TIM)             
         B     EXIT                                                             
         GOTO1 =A(CHKPGR),DMCB,(RC)                                             
         B     ESTF15                                                           
*                                                                               
ESTF14   CLC   SMTYPBIL,MTYPEBIL   IF BILL TYPE CHANGED                         
         BE    ESTF15                                                           
         BAS   RE,RDPROF           RE-READ PROFILES                             
*                                                                               
ESTF15   BRAS  RE,GETUSER          GET USER FIELDS FROM MASTER PRODUCT          
         MVC   SVBLFLT,BLFLT      SAVE WB FLIGHT CODE                           
         MVC   SVCNET,BCLDNET     SAVE CALCULATE NET FOR RCV AND INC            
         ZAP   DUB,BNET2P         ASSIGNED NET                                  
         SR    RF,RF                                                            
         ICM   RF,15,BCLDNET      TRADE CREDIT (ON REGM BILLS)                  
         CVD   RF,DUB2                                                          
         SP    DUB,DUB2           ADD TO GET MIDAS EXCHANGE AMT                 
         ZAP   SVBNET,DUB         AND STORE FOR MXPOST                          
         CLI   QOPT4,C'M'         DDS ONLY OPTION - JUST MARK ALL BILLS         
         BNE   ESTF16                                                           
         BAS   RE,MARK            CHECK BILL MATCHES MARK REQUIREMENTS          
         BNE   ESTF3              NO                                            
         BAS   RE,WRITBIL                                                       
         B     ESTF3                                                            
*                                                                               
ESTF16   CLI   QOPT1,C'R'         REVERSE OPTION?                               
         BNE   ESTF18                                                           
         BAS   RE,REVERSE         REVERSE THE BILL                              
         BNE   ESTF5                                                            
*                                                                               
ESTF18   MVI   CONERR,0           CHECK ERRORS WITH CONTRAS?                    
         MVI   ANYBILL,C'Y'       BILL PROCESSED FOR THIS CLIENT                
         MVI   PREVINC,C'N'       DIDN'T POST TO MEDIA INCOME ACC               
         MVI   PREVAINC,C'N'      DIDN'T POST TO AOR INCOME ACC                 
         MVI   PSTAOR,C'Y'        NO PROB WITH AOR POSTINGS                     
         XC    REVDATE,REVDATE                                                  
         XC    AORERRS,AORERRS                                                  
         MVC   TRANSDT,BQDATE      DEFAULT MOA DATE                             
         GOTO1 MXPROF,DMCB,MTPFIOFC,PROFWORK                                    
         MVC   SVACOF2,PROFWORK                                                 
         OC    SVACOF2,SPACES                                                   
         CLC   =C'REGM',MTYPEBIL                                                
         BNE   ESTF18K                                                          
*                                                                               
         GOTO1 MXPROF,DMCB,MTPFMOFF,PROFWORK     GROUPM MIDAS TRADE OFF         
         MVC   SVACOF2,PROFWORK                                                 
         OC    SVACOF2,SPACES                                                   
         B     ESTF19                                                           
*                                                                               
ESTF18K  CLC   =C'REGX',MTYPEBIL                                                
         BE    *+14                                                             
         CLC   =C'AORX',MTYPEBIL                                                
         BNE   ESTF19                                                           
*                                                                               
         GOTO1 MXPROF,DMCB,MTPFOFF,PROFWORK      TRADE BILLING OFFICE           
         MVC   SVACOF2,PROFWORK                                                 
         OC    SVACOF2,SPACES                                                   
*        GOTO1 MXPROF,DMCB,MTPFSPCT,PROFWORK     GROUPM MIDAS SPLIT %           
*        MVC   ACPTMPCT,PROFWORK                                                
ESTF19   GOTO1 SETMOA,DMCB,TRANSDT,ADDAY                                        
*                                                                               
         CLI   SVPROF+7,C'Y'         HONOR MOA LOCK RULES?                      
         BNE   ESTF30                                                           
         XC    WORK,WORK                                                        
         GOTO1 DATCON,DMCB,(0,TRANSDT),(6,WORK)                                 
         L     RF,UTL                SET ACCOUNTING SE NUMBER                   
         MVC   4(1,RF),ACCTSE2                                                  
         USING COMFACSD,R1                                                      
         L     R1,ACOMFACS         R1=A(COMFACS)                                
         OC    CPERVAL,CPERVAL     SPONSER DOESN'T FILL IN PERVAL               
         BNZ   *+10                SO NEED TO DO IT                             
         MVC   CPERVAL,=V(PERVAL)                                               
ESTF20   GOTO1 =V(BMONVAL),DMCB,(6,WORK),(9,ACOMFACS),(0,WORK+20),     +        
               (ACCTCD2,0)                                                      
         LA    R1,WORK+20                                                       
         USING BMONVALD,R1                                                      
         CLI   BMOERR,BMOEOKQ      EVERYTHING OK?                               
         BE    ESTF30                                                           
         TM    BMOERR,BMOELOKQ     MOA LOCKED?                                  
         BZ    ESTF25                                                           
         OC    BMOLCKP,BMOLCKP     LAST LOCKED MOA                              
         BZ    ESTF25                                                           
         XC    WORK(10),WORK                                                    
         MVC   WORK(L'BMOLCKP),BMOLCKP                                          
         MVI   WORK+2,X'01'                                                     
         GOTO1 DATCON,DMCB,(1,WORK),(0,WORK+3)                                  
         GOTO1 ADDAY,DMCB,WORK+3,WORK+3,31                                      
         MVC   TRANSDT,WORK+3                                                   
         B     ESTF30                                                           
*                                                                               
ESTF25   TM    BMOERR,BMOERNGQ     DATE OF OUF RANGE?                           
         BZ    ESTF30                                                           
         OC    BMOMOSP,BMOMOSP                                                  
         BZ    ESTF30                                                           
         XC    WORK(10),WORK                                                    
         MVC   WORK(L'BMOMOSP),BMOMOSP                                          
         MVI   WORK+2,X'01'                                                     
         GOTO1 DATCON,DMCB,(1,WORK),(0,WORK+3)                                  
         GOTO1 ADDAY,DMCB,WORK+3,WORK+3,31                                      
         GOTO1 DATCON,DMCB,(0,WORK+3),(6,WORK)                                  
         B     ESTF20             VALIDATE AGAIN USING NEW MONTH                
*                                                                               
ESTF30   L     RF,UTL                                                           
         MVC   4(1,RF),SAVSYS     RE-SET TO SPOT SYSTEM NUMBER                  
         BAS   RE,SETRET          SET OVERRIDE RETAIL CLI/PRD                   
         CLC   =C'AOR',TYPEBIL                                                  
         BE    ESTF40                                                           
         GOTO1 =A(RDPOST),DMCB,(RC)                                             
         GOTO1 =A(POSTCALL),DMCB,(RC)                                           
         BAS   RE,BILLPOST                                                      
         B     ESTF3                                                            
*                                                                               
ESTF40   ST    R2,SVADDR         SAVE ADDRESS OF ESTIMATE REC                   
         ST    R5,SVADDR2                                                       
         MVC   TEMPINV,BINVNO    RE-SET INVOICE NUMBER                          
         SR    RF,RF                                                            
         XC    BINVL,BINVL                                                      
         CLC   BINVMED,SPACES     ONLY STORE LONG INVOICE IF THE RULES          
         BNH   *+8                ARE IN THE BILL HEADER                        
         LA    RF,BINVL                                                         
         GOTO1 =A(SETINV),DMCB,(RC),BINVOICE,(RF)                               
         GOTO1 =A(DOAOR),DMCB,(RC)                                              
         OC    AORERRS,AORERRS                                                  
         BZ    ESTF50                                                           
         TM    BILSTAT,BSTSNETQ                                                 
         BO    ESTF50                                                           
         MVI   PSTAOR,C'N'        ERROR WITH POSTINGS IN AOR SET                
ESTF50   MVI   ABILCNT,1          NUMBER OF AOR BILLS IN SET                    
         LA    R2,AAORBIL1        FIRST A(OF POSTINGS)                          
         LA    R3,AORERRS                                                       
         USING AORERD,R3                                                        
ESTF60   BAS   RE,SETERR          SET CURRENT ERROR MSGS                        
         BAS   RE,SETPST          SET V(POSTINGS) AND RESET PST BLOCK           
         BAS   RE,BILLPOST                                                      
         BAS   RE,GETSME          RE-ESTABLISH READ SEQUENCE                    
         LA    R2,4(R2)           PT TO NEXT ADDRESS OF POSTINGS                
ESTF70   LA    R3,AORERRL(R3)                                                   
         SR    R1,R1                                                            
         IC    R1,ABILCNT                                                       
         CLM   R1,1,NUMABIL                                                     
         BE    ESTF80                                                           
         LA    R1,1(R1)                                                         
         STC   R1,ABILCNT                                                       
         MVI   PREVINC,C'N'       RESET THESE POSTING FLAGS                     
         MVI   PREVAINC,C'N'                                                    
         BAS   RE,GETNXT          GET NEXT BILL IN AOR SET                      
         XC    SKEY,SKEY                                                        
         MVC   SKEY(L'BKEY),KEY                                                 
         MVC   AREC,ADBILL                                                      
         BAS   RE,GETRC           GET THE RECORD                                
         TM    BILSTAT,X'20'                                                    
         BO    ESTF70                                                           
         MVC   TEMPDATE,BDATE                                                   
         MVC   TEMPINV,BINVNO                                                   
         ST    R5,SVADDR2                                                       
         XC    BINVL,BINVL                                                      
         SR    RF,RF                                                            
         CLC   BINVMED,SPACES     ONLY STORE LONG INVOICE IF THE RULES          
         BNH   *+8                ARE IN THE BILL HEADER                        
         LA    RF,BINVL                                                         
         GOTO1 =A(SETINV),DMCB,(RC),BINVOICE,(RF)                               
         B     ESTF60                                                           
*                                                                               
ESTF80   L     R2,SVADDR           RESTORE ESTIMATE REC                         
         B     ESTF4                                                            
*                                                                               
ESTFX    MVI   MODE,ESTLAST       GET NEXT ESTIMATE                             
         B     EXIT                                                             
         EJECT                                                                  
GETNXT   NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         LA    R1,DMCB                                                          
         LA    RE,DMRSEQ                                                        
         ST    RE,0(R1)                                                         
         CLI   QOPT4,C'U'         IF REVERSAL TO UNPOST                         
         BNE   *+8                                                              
         MVI   0(R1),X'08'        PASS BACK DELETED RECORDS                     
         GOTO1 DATAMGR,DMCB,,SPTDIR,KEY,KEY                                     
         B     EXIT                                                             
         SPACE                                                                  
* GETSME - RE-READ SAVED KEY                                                    
*                                                                               
GETSME   NTR1                                                                   
         L     RF,UTL                                                           
         MVC   4(1,RF),SAVSYS     SPOT SYSTEM NUMBER                            
         XC    KEY,KEY                                                          
         MVC   KEY(L'BKEY),SKEY   RE-ESTABLISH READ SEQUENCE                    
         LA    R1,DMCB                                                          
         LA    RE,DMREAD                                                        
         ST    RE,0(R1)                                                         
         CLI   QOPT4,C'U'         IF REVERSAL TO UNPOST                         
         BNE   *+8                                                              
         MVI   0(R1),X'08'        PASS BACK DELETED RECORDS                     
         GOTO1 DATAMGR,DMCB,,SPTDIR,KEY,KEY                                     
         CLC   KEY(13),SKEY                                                     
         BE    *+6                                                              
         DC    H'0'                                                             
         B     EXIT                                                             
         SPACE                                                                  
* GETSME2 - RE-READ CURRENT KEY                                                 
*                                                                               
GETSME2  NTR1                                                                   
         L     RF,UTL                                                           
         MVC   4(1,RF),SAVSYS     SPOT SYSTEM NUMBER                            
         LA    R1,DMCB                                                          
         LA    RE,DMREAD                                                        
         ST    RE,0(R1)                                                         
         CLI   QOPT4,C'U'         IF REVERSAL TO UNPOST                         
         BNE   *+8                                                              
         MVI   0(R1),X'08'        PASS BACK DELETED RECORDS                     
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,,SPTDIR,KEY,KEY                                     
         CLC   KEY(L'BKEY),KEYSAVE                                              
         BE    *+6                                                              
         DC    H'0'                                                             
         B     EXIT                                                             
         SPACE                                                                  
GETRC    NTR1                                                                   
         GOTO1 DATAMGR,DMCB,GETREC,SPTFILE,KEY+14,AREC,DMWORK                   
         B     EXIT                                                             
         EJECT                                                                  
*------------------------------------------------------------*                  
* FILTBIL - IF BILL PASSES SOME REQUIREMENTS - SET SOME INFO *                  
*------------------------------------------------------------*                  
*                                                                               
FILTBIL  NTR1                                                                   
         CLI   QOPT1,C' '         REGULAR REQUEST                               
         BNE   FILTB5                                                           
         OC    BILPOST,BILPOST    WANT UNTRANS BILLS ONLY                       
         BNZ   XITNO                                                            
         B     FILTB10                                                          
*                                                                               
FILTB5   CLI   QOPT1,C'D'                                                       
         BNE   FILTB8                                                           
         CLC   BILPOST,REQTDT     MATCH ON DATE                                 
         BNE   XITNO                                                            
         B     FILTB10                                                          
*                                                                               
FILTB8   CLI   QOPT1,C'R'                                                       
         BNE   FILTB10                                                          
         MVI   REVSTAT,C'Y'       SET REVERSE IN PROGRESS                       
         MVC   REVDATE,BILPOST    SET DATE OF ORIGINAL TRANSFER                 
*                                                                               
FILTB10  GOTO1 CLRAREA,DMCB,V(IOAREA),V(IOAREAX)                                
         BRAS  RE,SETTYPE         SET BILL TYPE                                 
         MVC   SVBTNUM,BTYPE      B4, B5, ETC.                                  
         XC    SVFLAG,SVFLAG                                                    
         TM    BILSTAT2,BSTC2Q    IS IT COST2 BILL?                             
         BNO   *+8                                                              
         OI    SVFLAG,COS2FLG     SET COST2 FLAG                                
         MVC   RUNDT,BDATE        SET RUN DATE                                  
         MVC   TEMPINV,BINVNO                                                   
         MVC   TEMPDATE,BDATE     GET REAL INVOICE NUMBER                       
         ST    R5,SVADDR2                                                       
         XC    BINVL,BINVL                                                      
         SR    RF,RF                                                            
         CLC   BINVMED,SPACES     ONLY STORE LONG INVOICE IF THE RULES          
         BNH   *+8                ARE IN THE BILL HEADER                        
         LA    RF,BINVL                                                         
         GOTO1 =A(SETINV),DMCB,(RC),BINVOICE,(RF)                               
         GOTO1 =A(SETVATO),DMCB,(RC),AREC   SAVE OUTPUT GST/PST CODES           
         BAS   RE,CHKRET          CHECK IF SPECIAL UNTRANS RETAIL               
         BNE   XITNO                                                            
         BAS   RE,CHKNET          CHECK NETWORK FILTER                          
         BNE   XITNO                                                            
*                                                                               
         CLI   QOPT1,C'R'         CHECK REQUIR FOR REG REV & UNPOST             
         BNE   FILTB20                                                          
         CLI   QOPT4,C'M'                                                       
         BE    FILTB20                                                          
         BAS   RE,CHKREV                                                        
         BNE   XITNO                                                            
*                                                                               
FILTB20  TM    BILSTAT,X'20'                                                    
         BO    XITNO                                                            
         B     XITYES                                                           
         EJECT                                                                  
*----------------------------------------------------------*                    
* CHKRET  - CHECKS FOR SPECIAL UNTRANSFERABLE RETAIL BILLS *                    
*----------------------------------------------------------*                    
*                                                                               
CHKRET   NTR1                                                                   
         CLC   =C'RET',TYPEBIL    FOR RETAIL BILLS ONLY                         
         BNE   XITYES                                                           
         CLI   BRETAIL,X'81'      SKIP ALL CORP CONTROL                         
         BE    XITNO              IF ALL ZERO AMOUNTS                           
         ZAP   BGRSP,BGRSP                                                      
         BNZ   XITYES                                                           
         ZAP   BNETP,BNETP                                                      
         BNZ   XITYES                                                           
         ZAP   BACTP,BACTP                                                      
         BNZ   XITYES                                                           
         OC    BVATAMT,BVATAMT                                                  
         BZ    XITNO              ALL AMOUNTS ARE ZERO                          
         B     XITYES             BILL OKAY TO TRANSFER SO FAR                  
         SPACE                                                                  
*----------------------------------------------------------*                    
* CHKNET  - IF NETWORK BILL - CHECKS FOR MEDIA MATCH       *                    
*----------------------------------------------------------*                    
*                                                                               
CHKNET   NTR1                                                                   
         CLI   SYS,C'N'           IF NETWORK                                    
         BNE   XITYES                                                           
         CLI   BLMED,C' '         IF NO STA FILTER-CONSIDER IT NETWORK          
         BNE   CHKNET5                                                          
         CLI   MYMED,C'N'                                                       
         BE    XITYES                                                           
         B     XITNO                                                            
CHKNET5  CLC   BLMED,MYMED        MAKE SURE SAME MEDIA FILTER                   
         BNE   XITNO                                                            
         B     XITYES             BILL OKAY SO FAR                              
         EJECT                                                                  
*-------------------------------------------*                                   
* SETRET - SET OVERRIDE RETAIL CLI/PRD      *                                   
*-------------------------------------------*                                   
*                                                                               
SETRET   NTR1                                                                   
         MVC   OVRCLT,SPACES                                                    
         MVC   OVRPRD,SPACES                                                    
         CLC   =C'RET',TYPEBIL    FOR CORP ONLY RETAIL BILLS                    
         BNE   SETRETX                                                          
         CLI   BRETAIL,X'01'                                                    
         BE    SETRET10                                                         
         CLI   BRETAIL,X'41'                                                    
         BNE   SETRETX                                                          
SETRET10 GOTO1 MXPROF,DMCB,MTPFCOV,PROFWORK                                     
         CLC   PROFWORK,SPACES                                                  
         BE    SETRET15                                                         
         MVC   OVRCLT,PROFWORK    OVERRIDE CLIENT                               
         OC    OVRCLT,SPACES                                                    
SETRET15 GOTO1 MXPROF,DMCB,MTPFPOV,PROFWORK                                     
         CLC   PROFWORK,SPACES                                                  
         BE    SETRETX                                                          
         MVC   OVRPRD,PROFWORK                                                  
         OC    OVRPRD,SPACES                                                    
SETRETX  B     XIT                                                              
         EJECT                                                                  
*-----------------------------------------------------*                         
* SETERR - SETS CURRENT ERROR MSG FOR BILL IN AOR SET *                         
*-----------------------------------------------------*                         
*                                                                               
SETERR   NTR1                                                                   
         LA    R1,ACPBLK                                                        
         USING ACPOSTD,R1                                                       
         MVC   ACPERR,AORERR                                                    
         MVC   ACPERRC,AORERRC                                                  
         MVC   CONERR,AORCERR                                                   
         ZIC   RE,ABILCNT                                                       
         CH    RE,=H'1'           IF MAIN BILL -                                
         BNE   SETERRX                                                          
         LA    R3,AORERRL(R3)     ADD TRUE AOR ERRORS                           
         OC    ACPERR,AORERR                                                    
         OC    ACPERRC,AORERRC                                                  
         CLI   AORCERR,1                                                        
         BNE   SETERRX                                                          
         MVI   CONERR,1                                                         
SETERRX  B     EXIT                                                             
         DROP  R1,R3                                                            
         SPACE                                                                  
*-----------------------------------------------------------*                   
* SETPST - FOR AOR BILLS MOVE SAVED POSTINGS TO V(POSTINGS) *                   
*          AND RESET PST VALUES (PROVINCE AND TAX CODE)     *                   
*-----------------------------------------------------------*                   
*                                                                               
SETPST   NTR1                                                                   
         GOTO1 CLRAREA,DMCB,V(POSTINGS),V(POSTNGSX)                             
         ICM   RE,15,0(R2)                                                      
         L     RF,=V(POSTINGS)                                                  
         ZIC   R1,PSTNUM2                                                       
*                                                                               
SETPST5  MVC   0(ACPRTNL2,RF),0(RE)                                             
         LA    RE,ACPRTNL2(RE)                                                  
         LA    RF,ACPRTNL2(RF)                                                  
         BCT   R1,SETPST5                                                       
*                                                                               
         GOTO1 =A(SETVATO),DMCB,(RC),ADBILL                                     
         B     EXIT                                                             
         EJECT                                                                  
                                                                                
***********************************************************************         
* NEWCLT - FOR EACH NEW BILL REC CLIENT                                         
*          SET OFFICE GROUPS,ACC FILE,ETC                                       
***********************************************************************         
NEWCLT   NTR1                                                                   
*                                                                               
         L     RE,ADCLT                                                         
         USING CLTHDRD,RE                                                       
         MVC   SVCOFF,COFFICE     MEDIA OFFICE                                  
*                                                                               
         MVI   SVUSR,0                                                          
         TM    CPU1FLG1,CFLGMXQ    TEST TRANSFER P1                             
         BZ    *+8                                                              
         OI    SVUSR,SVPU1                                                      
         TM    CPU2FLG1,CFLGMXQ    TEST TRANSFER P2                             
         BZ    *+8                                                              
         OI    SVUSR,SVPU2                                                      
         TM    CEU1FLG1,CFLGMXQ    TEST TRANSFER E1                             
         BZ    *+8                                                              
         OI    SVUSR,SVEU1                                                      
         TM    CEU2FLG1,CFLGMXQ    TEST TRANSFER E2                             
         BZ    *+8                                                              
         OI    SVUSR,SVEU2                                                      
         MVC   SVPDESC1,CPU1       USER FIELD DESCRIPTIONS                      
         MVC   SVPDESC2,CPU2                                                    
         MVC   SVEDESC1,CEU1                                                    
         MVC   SVEDESC2,CEU2                                                    
         DROP  RE                                                               
*                                                                               
         XC    SVOFFG,SVOFFG      CLEAR OLD OFFICE GROUP                        
         CLI   SVPROF,C'Y'        USE OFFICE GROUPS?                            
         BNE   NEWCLT50                                                         
                                                                                
         LA    R3,GRPTBL                                                        
NEWCLT10 CLI   0(R3),X'FF'        END OF OFFICE GROUP (LIST) TABLE?             
         BE    NEWCLT50                                                         
*                                                                               
         XC    DUB,DUB                                                          
         LA    R1,DUB                                                           
         USING OFFICED,R1                                                       
         MVI   OFCSYS,C'S'        SPOT SYSTEM                                   
         MVI   OFCAUTH,C'$'       OFFICE GROUP (LIST) IDENTIFIER                
         MVC   OFCAUTH+1(1),0(R3) OFFICE GROUP (LIST) CODE                      
         MVC   HALF,OFCAUTH                                                     
         MVC   OFCOFC,SVCOFF      MEDIA OFFICE CODE                             
         MVC   OFCAGY,QAGY                                                      
         DROP  R1                                                               
*                                                                               
         GOTO1 AOFFICER,DMCB,DUB,ACOMFACS                                       
         CLI   0(R1),0                                                          
         BE    NEWCLT20                                                         
         LA    R3,1(,R3)          TRY NEXT OFFICE GROUP                         
         B     NEWCLT10                                                         
*                                                                               
NEWCLT20 MVC   SVOFFG,HALF        SAVE NEW OFFICE GROUP                         
*                                                                               
* NOW CHECK IF WHAT ACC FILE? PROFILE IS SET UP.  IF SO CHANGE TO NEW           
* ACCFILE.  IF NOT CHECK IF ACCTCD2=ACCTCD BECAUSE MAY HAVE BEEN SET            
* AS AN OVERRIDE FOR A PREVIOUS CLIENT. IF NOT= THAN FORCE IN DEFAULT           
* AGENCY TO PROFILE IN ORDER TO CHANGE THE ACCFILE BACK TO DEFAULT.             
*                                                                               
NEWCLT50 DS    0H                                                               
         BAS   RE,RDPROF                                                        
         GOTO1 MXPROF,DMCB,MTPFDACC,PROFWORK                                    
         BAS   RE,SETACC          SET CURRENT ACC POWER CODE                    
         OC    PROFWORK,PROFWORK  SPECIAL ACCFILE TO READ FROM?                 
         BNZ   NEWCLT60           YES                                           
         CLC   ACCTCD2,ACCTCD                                                   
         BE    NEWCLT70                                                         
         MVC   PROFWORK(2),ACCALPH                                              
NEWCLT60 BAS   RE,CHGFILE          CHANGE TO NEW ACCFILE                        
NEWCLT70 CLC   ACCALPH,RCAGENCY    SAME AGENCY?                                 
         BE    *+8                 YES: CONTINUE                                
         BRAS  RE,SMFOX            NO:  KEEP A RECORD OF IT                     
         GOTO1 =A(RDCLT),DMCB,(RC) READ ACC CLT RECORD  & SET VALUES            
         B     EXIT                                                             
*                                                                               
GRPTBL   DC    C'ABCDEFGHIJKLMNOPQRSTUVWXYZ'  OFFICE GROUPS (LISTS)             
         DC    C'123456789'                   ..                                
         DC    C'<>#@'                        SPECIAL CHARACTERS                
         DC    X'41424344464748494C'          NEW 2 CHAR LIST'S 1 BYTE          
         DC    X'51525354565758595B5D5E5F'    HEX VALUE                         
         DC    X'61626364666768696B6C6D6F'    ..                                
         DC    X'71727374767778797B7C'        ..                                
         DC    X'81828384868788898B8C8D8E'    ..                                
         DC    X'91929394969798999B9C9D9E'    ..                                
         DC    X'A1A2A3A4A6A7A8A9ABACADAE'    ..                                
         DC    X'B1B2B3B4B6B7B8B9BBBCBDBE'    ..                                
         DC    X'CBCCCDCE'                    ..                                
         DC    X'DBDCDDDE'                    ..                                
         DC    X'EBECEDEE'                    ..                                
         DC    X'FBFCFDFE'                    ..                                
         DC    X'FF'                          END OF TABLE                      
                                                                                
*-------------------------------------------*                                   
* CHGFILE - SETS VALUES FOR OTHER ACCFILE   *                                   
*-------------------------------------------*                                   
*                                                                               
CHGFILE  NTR1                                                                   
         XC    AKEY,AKEY                                                        
         LA    RE,AKEY                                                          
         USING CT5REC,RE                                                        
         MVI   CT5KTYP,CT5KTYPQ   C'5'                                          
         MVC   CT5KALPH,PROFWORK  ALPHA USER ID                                 
         DROP  RE                                                               
         L     R2,ADBUY                                                         
         USING CT5REC,R2                                                        
         GOTO1 DATAMGR,DMCB,=C'DMREAD  ',=C'CTFILE ',AKEY,(R2),0                
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'               WHAT SHOULD I DO???                           
         SR    R0,R0                                                            
         LA    R1,CT5DATA                                                       
         USING CTSYSD,R1                                                        
CHGFIL5  CLI   0(R1),0                                                          
         BNE   *+6                                                              
         DC    H'0'               WHAT SHOULD I DO????                          
         CLI   0(R1),CTSYSELQ     X'21' - SYSTEM ELEMENT                        
         BNE   CHGFIL8                                                          
         CLI   CTSYSNUM,6         ACC SYSTEM                                    
         BE    CHGFIL10                                                         
CHGFIL8  ICM   R0,1,1(R1)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R1,R0                                                            
         B     CHGFIL5                                                          
*                                                                               
CHGFIL10 MVC   ACCTSE2,CTSYSSE    SE NUM                                        
         MVC   TEMPSE,CTSYSSE                                                   
         MVC   ACCTCD2,CTSYSAGB   COMPANY CODE                                  
         MVC   TEMPCD,ACCTCD2                                                   
         BAS   RE,SETFILE         OPEN UP FILES & SET VALUES                    
         MVC   ACCTOID2,TEMPOID                                                 
*                                                                               
         B     XIT                                                              
         DROP  R1,R2                                                            
         EJECT                                                                  
*---------------------------------------------*                                 
* SETACC - SETS CURRENT ALPHA ID              *                                 
*---------------------------------------------*                                 
*                                                                               
SETACC   NTR1                                                                   
         XC    ACCALPH,ACCALPH                                                  
         OC    SVPROF2(2),SVPROF2 IF GIVEN OVERRIDE ACC CONNECTION              
         BZ    SETACC5                                                          
         MVC   ACCALPH,SVPROF2    IN CONTROL PROFILE - USE IT                   
         B     SETACC10                                                         
*                                                                               
SETACC5  OC    PROFWORK,PROFWORK  IF GIVEN OVERRIDE ACC FILE IN                 
         BZ    SETACC8                                                          
         MVC   ACCALPH,PROFWORK   MY PROF MAINT RECORDS - USE THAT              
         B     SETACC10                                                         
*                                                                               
SETACC8  MVC   ACCALPH,RCAGENCY    NONE OF ABOVE - USE CUR ACC CONNECT          
SETACC10 OC    ACCALPH,ACCALPH                                                  
         BNZ   *+6                                                              
         DC    H'0'                                                             
SETACCX  B     EXIT                                                             
         EJECT                                                                  
*-----------------------------------------*                                     
* NEWPRD - FOR EACH NEW BILL REC PRODUCT  *                                     
*          READ ACC PRD                   *                                     
*-----------------------------------------*                                     
*                                                                               
NEWPRD   NTR1                                                                   
         L     RE,ADPRD            RE=A(SPOT/NET PRODUCT RECORD)                
         USING PRDHDRD,RE                                                       
*                                                                               
         XC    SVUCMPLN,SVUCMPLN                                                
         MVC   SVUCMDP,SPACES                                                   
         MVC   SVUCMP,SPACES                                                    
*                                                                               
         MVC   SVPUSER1,PUSER1     SAVE PRODUCT USER FIELD1                     
         OC    SVPUSER1,SPACES                                                  
         MVC   SVPUSER2,PUSER2     SAVE PRODUCT USER FIELD2                     
         OC    SVPUSER2,SPACES                                                  
         DROP  RE                                                               
*                                                                               
         LA    RE,UCOMBLK         NOW READ NEW UCOM USER DEF FIELDS             
         LA    RF,L'UCOMBLK                                                     
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         LA    R3,UCOMBLK                                                       
         USING DDUCOMD,R3                                                       
         MVC   UCACOMF,ACOMFACS    A(COMFACS)                                   
         MVC   UCSYS,SYS           SYSTEM                                       
         MVC   UCPROG,=C'MX'       CALLING PGM(THIS MEANS CHK MBI BIT)          
         MVC   UCSAM,BAGYMD        HEX AGENCY/MEDIA                             
         MVC   UCSCLT,BCLT         CLIENT                                       
         MVC   UCPRD,PRD           PRODUCT                                      
         OI    UCOPT,UCOPRD        RETURN PRODUCT UCOMS                         
         GOTO1 =V(DDUCOM),UCOMBLK                                               
         CLI   UCERROR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         TM    UCDATA,UCDNOCLT+UCDNOPRD     NO CLIENT/PRODUCT FOUND?            
         BNZ   NEWPRD05                                                         
         MVC   SVUCMPLN,UCPLENS    LENGTHS OF 4 PRODUCT UCOM FIELDS             
         L     RF,UCPTTLS                                                       
         MVC   SVUCMP1D(L'SVUCMDP),0(RF) PRODUCT UCOMS DESCRIPTIONS             
         L     RF,UCPDATA                                                       
         MVC   SVUCMP1(L'SVUCMP),0(RF) PRODUCT UCOMS                            
         OC    SVUCMP1D(L'SVUCMDP),SPACES                                       
         OC    SVUCMP1(L'SVUCMP),SPACES                                         
*                                                                               
* NOW CHECK IF WHAT ACC FILE? PROFILE IS SET UP.  IF SO CHANGE TO NEW           
* ACCFILE.  IF NOT CHECK IF ACCTCD2=ACCTCD BECAUSE MAY HAVE BEEN SET            
* AS AN OVERRIDE FOR A PREVIOUS CLIENT. IF NOT= THAN FORCE IN DEFAULT           
* AGENCY TO PROFILE IN ORDER TO CHANGE THE ACCFILE BACK TO DEFAULT.             
*                                                                               
NEWPRD05 DS    0H                                                               
         BAS   RE,RDPROF                                                        
         GOTO1 MXPROF,DMCB,MTPFDACC,PROFWORK                                    
         BAS   RE,SETACC          SET CURRENT ACC POWER CODE                    
         OC    PROFWORK,PROFWORK  SPECIAL ACCFILE TO READ FROM?                 
         BNZ   NEWPRD10           YES                                           
         CLC   ACCTCD2,ACCTCD                                                   
         BE    NEWPRD20                                                         
         MVC   PROFWORK(2),ACCALPH                                              
NEWPRD10 BAS   RE,CHGFILE          CHANGE TO NEW ACCFILE                        
NEWPRD20 CLC   ACCALPH,RCAGENCY    SAME AGENCY?                                 
         BE    *+8                 YES: CONTINUE                                
         BRAS  RE,SMFOX            NO:  KEEP A RECORD OF THAT                   
         GOTO1 =A(RDPRD),DMCB,(RC) READ ACC PRD REC & SET VALUES                
         B     EXIT                                                             
*                                                                               
*--------------------------------------------------------*                      
* MARK - CHECKS BILL BY BILL TYPE AND (R,I) DATE TO SEE  *                      
*        IF BILL SHOULD BE MARKED TRANSFERRED            *                      
*    XIT  - CC NOT EQUAL IF BILL DOESN'T QUALIFY         *                      
*--------------------------------------------------------*                      
*                                                                               
MARK     NTR1                                                                   
         MVI   PSTAOR,C'Y'        NO PROB WITH AOR POSTINGS                     
         CLC   =C'ALL',QBTYPE     PARTICULAR BILL TYPE REQUESTED?               
         BE    *+14                                                             
         CLC   QBTYPE,TYPEBIL     BILL TYPES MUST MATCH                         
         BNE   XITNO              NO - SKIP TO NEXT BILL                        
*                                                                               
         LA    RF,BQDATE          TRANSFERRED THAT HAVE A DATE                  
         CLI   QOPT3,C'I'                                                       
         BE    *+8                                                              
         LA    RF,BDATE                                                         
         CLC   0(6,RF),QIORDT     UPTO AND INCLUDING DATE                       
         BH    XITNO              REQUESTED                                     
         CLC   =C'AOR',TYPEBIL                                                  
         BNE   MARK10                                                           
         GOTOR GETRUE,DMCB,ADBILL                                               
MARK10   BAS   RE,PRTBIL          PRINT BILL TO BE MARKED                       
         B     XITYES                                                           
         EJECT                                                                  
*----------------------------------------------------------*                    
* CHKREV  - CHECKS BILL BY BILL TYPE, (R,I) DATE, TRANSFER *                    
*           DATE OR BILL NUMS TO SEE IF BILL SHOULD BE REV *                    
*    XIT -  BILL SHOULDN'T BE REVERSED - DOESN'T MEET      *                    
*           REQUIREMENTS                                   *                    
*----------------------------------------------------------*                    
*                                                                               
CHKREV   NTR1                                                                   
         CLC   =C'ALL',QBTYPE     PARTICULAR BILL TYPE REQUESTED?               
         BE    *+14                                                             
         CLC   QBTYPE,TYPEBIL     BILL TYPES MUST MATCH                         
         BNE   XITNO              NO - SKIP TO NEXT BILL                        
*                                                                               
         CLC   QTRADT,SPACES      PARTICULAR TRANSFER DATE REQUESTED?           
         BE    *+14                                                             
         CLC   BILPOST,REQTDT     TRANSFER DATE MATCHES?                        
         BNE   XITNO              NO - SKIP TO NEXT BILL                        
*                                                                               
         CLI   QOPT3,C' '         MATCH ON DATE REQUESTED?                      
         BE    CHKREV20           NO -                                          
         LA    RF,BQDATE                                                        
         CLI   QOPT3,C'I'         FILTER ON INVOICE DATE                        
         BE    *+8                                                              
         LA    RF,BDATE           MUST BE RUN DATE                              
         CLC   0(6,RF),QIORDT                                                   
         BNE   XITNO              MUST MATCH ON SPECIF DATE REQUEST             
*                                                                               
CHKREV20 CLC   QBILL1,SPACES      FILTERING ON INVOICE NUMBERS?                 
         BE    CHKREV40           NO                                            
*                                                                               
* SPFMTINO ONLY PACKS THE LAST FOUR DIGITS OF THE INVOICE NUMBER                
* SO CALL HEXIN TO PACK THE FIRST TWO DIGITS                                    
*                                                                               
* WORK CONTAINS PACKED QBILL1                                                   
         XC    WORK,WORK                                                        
         GOTO1 HEXIN,DMCB,QBILL1,WORK,2                                         
         GOTO1 =V(SPFMTINO),DMCB,0,(C'P',QBILL1+2),0,0                          
         L     RF,DMCB+4                                                        
         MVC   WORK+1(2),0(RF)                                                  
*                                                                               
* WORK+3 CONTAINS PACKED BINVOICE                                               
         GOTO1 HEXIN,DMCB,BINVOICE,WORK+3,2                                     
         GOTO1 =V(SPFMTINO),DMCB,0,(C'P',BINVOICE+2),0,0                        
         L     RF,DMCB+4                                                        
         MVC   WORK+4(2),0(RF)                                                  
*                                                                               
         CLC   QBILL2,SPACES                                                    
         BNE   CHKREV30                                                         
         CLC   WORK(3),WORK+3     COMPARE QBILL1 TO BINVOICE                    
         BNE   XITNO                                                            
         B     CHKREV35                                                         
*                                                                               
* WORK+6 CONTAINS PACKED QBILL2                                                 
CHKREV30 GOTO1 HEXIN,DMCB,QBILL2,WORK+6,2                                       
         GOTO1 =V(SPFMTINO),DMCB,0,(C'P',QBILL2+2),0,0                          
         L     RF,DMCB+4                                                        
         MVC   WORK+7(2),0(RF)                                                  
*                                                                               
         CLC   WORK+3(3),WORK      COMPARE BINVOICE TO QBILL1                   
         BL    XITNO                                                            
         BE    CHKREV35                                                         
         CLC   WORK+3(3),WORK+6    COMPARE BINVOICE TO QBILL3                   
         BH    XITNO                                                            
         B     CHKREV40                                                         
CHKREV35 CLC   =C'AOR',TYPEBIL    FIRST BILL NUMBER                             
         BNE   XITYES             OKAY TO REVERSE                               
         TM    BILSTAT,X'10'      IF AOR - MUST BE FIRST IN SET                 
         BNO   XITNO                                                            
         TM    BILSTAT,BSTSADJQ                                                 
         BO    XITNO                                                            
         B     XITYES                                                           
*                                                                               
CHKREV40 TM    BILSTAT,X'20'      IF TRUE AOR BILL                              
         BO    XITNO              SKIP IT - ALREADY PROCESSED                   
         B     XITYES                                                           
         EJECT                                                                  
*----------------------------------------------------------*                    
* REVERSE - TRIES TO REVERSE A BILL                        *                    
*    XIT -  IF BILL DIDN'T REVERSE CORRECTLY OR UNPOST OPT *                    
*           CC SET NOT EQUAL                               *                    
*----------------------------------------------------------*                    
*                                                                               
REVERSE  NTR1                                                                   
         CLC   =C'AOR',TYPEBIL                                                  
         BE    REV50                                                            
         XC    AORERRS,AORERRS                                                  
         MVI   TEST,C'N'                                                        
         GOTO1 =A(UNDO),DMCB,(RC)                                               
         GOTO1 =A(PRTREV),DMCB,(RC) PRINT THE REVERSAL                          
         BAS   RE,GETSME          RE-ESTABLISH SEQUENCE                         
         MVC   AREC,ADBILL                                                      
         BAS   RE,GETRC                                                         
         CLI   QOPT4,C'U'         IF REVERSAL UNPOST - SET CC <> 0              
         BE    REV20                                                            
         CLI   UNERR,0                                                          
         BE    XIT                NO PROBLEM UNDOING - TRANSFER BILL            
REV20    BAS   RE,GETNXT          PROBLEM - SKIP TRANSFER OF  THIS BILL         
         LTR   RB,RB                                                            
         B     XIT                                                              
*                                                                               
REV50    MVI   TEST,C'Y'                                                        
         LA    R3,AORERRS                                                       
         XC    AORERRS,AORERRS                                                  
         GOTOR GETRUE,DMCB,ADBILL                                               
         BAS   RE,DOREV           FIRST TIME PASS - TEST                        
*                                                                               
         MVI   TEST,C'N'          TRANSFER SECOND TIME AROUND                   
         OC    AORERRS,AORERRS                                                  
         BZ    *+8                                                              
         MVI   TEST,C'B'          ERRORS FOUND -DON'T TRANS 2ND TIME            
*                                                                               
         BAS   RE,GETSME          GET FIRST BILL IN AOR SET AGAIN               
         GOTO1 DATAMGR,DMCB,GETREC,SPTFILE,KEY+14,AREC,DMWORK                   
         BAS   RE,DOREV           SECOND PASS -TRANSFER/PRINT                   
*                                                                               
         CLI   TEST,C'B'          CAN'T RETANS OR UNPOST(WHICH DOESN'T          
         BE    REV70              RETRANS)                                      
         CLI   QOPT4,C'U'         IF REVERSAL UNPOST - SET CC <> 0              
         BNE   REV80                                                            
REV70    MVC   KEY,NEXTKEY        -SKIP TO NEXT BILL RECORD TO PROCESS          
         BAS   RE,GETSME2                                                       
         LTR   RB,RB                                                            
         B     XIT                                                              
*                                 IF RE-TRANSFER THEN START AT                  
REV80    BAS   RE,GETSME          FIRST BILL IN AOR SET                         
         GOTO1 DATAMGR,DMCB,GETREC,SPTFILE,KEY+14,AREC,DMWORK                   
         CR    RB,RB                                                            
         B     XIT                                                              
         EJECT                                                                  
DOREV    NTR1                                                                   
         USING AORERD,R3                                                        
*                                                                               
DOREV10  MVC   TEMPINV,BINVNO     SET 'REAL' INVOICE NUMBER                     
         MVC   TEMPDATE,BDATE                                                   
         ST    R5,SVADDR2                                                       
         XC    BINVL,BINVL                                                      
         SR    RF,RF                                                            
         CLC   BINVMED,SPACES     ONLY STORE LONG INVOICE IF THE RULES          
         BNH   *+8                ARE IN THE BILL HEADER                        
         LA    RF,BINVL                                                         
         GOTO1 =A(SETINV),DMCB,(RC),BINVOICE,(RF)                               
         GOTO1 =A(UNDO),DMCB,(RC)                                               
         MVC   AREC,ADBILL                                                      
         CLI   TEST,C'Y'                                                        
         BE    DOREV20                                                          
         GOTO1 =A(PRTREV),DMCB,(RC) PRINT REVERSAL FOR TEST=N & B               
         B     DOREV30                                                          
*                                                                               
DOREV20  MVC   AORERR,UNERR                                                     
         LA    R3,AORERRL(R3)                                                   
*                                                                               
DOREV30  L     RE,ADBILL                                                        
         XC    KEY,KEY                                                          
         MVC   KEY(L'BKEY),0(RE)                                                
         BAS   RE,GETSME2         RE-READ CURRENT KEY                           
DOREV40  BAS   RE,GETNXT                                                        
         CLC   KEY(BKEYINV-BKEY),SKEY                                           
         BNE   DOREV60            END OF SET                                    
         GOTO1 DATAMGR,DMCB,GETREC,SPTFILE,KEY+14,AREC,DMWORK                   
         CLC   BDATE,RUNDT                                                      
         BNE   DOREV60            END OF SET                                    
         TM    BILSTAT,X'20'                                                    
         BO    DOREV40                                                          
         TM    BILSTAT,X'10'                                                    
         BNO   DOREV60                                                          
         TM    BILSTAT,BSTSADJQ                                                 
         BNO   DOREV60                                                          
         B     DOREV10                                                          
*                                                                               
DOREV60  MVC   NEXTKEY,KEY        SAVE NEXT NEW KEY FOR PROCESSING              
DOREVX   B     XIT                                                              
         EJECT                                                                  
*--------------------------------------------------------------*                
* BILLPOST - MAKE POSTINGS/ADDS POST DETAIL / MARKS BILL RECORD*                
*--------------------------------------------------------------*                
*                                                                               
BILLPOST NTR1                                                                   
         GOTO1 =A(CONTRAS),DMCB,(RC),V(POSTINGS)                                
         BAS   RE,PRTBIL           PRINT BILL DETAILS                           
         BAS   RE,CHKERRS          IF NO ERRORS                                 
         BNE   BILLPX              (ERRORS - DON'T POST BILL)                   
*                                                                               
         BAS   RE,PSTHDR           INITIALIZE POST DETAIL RECORD                
         GOTO1 =A(SETMXBLK),DMCB,(RC) SET BLOCK FOR MXPOST CALL                 
         L     R3,AMXBLK           R3=A(PARAMETER BLOCK FOR MXPOST)             
         USING MXPOSTD,R3                                                       
         ZIC   R2,PSTNUM2          R2=(M'NUM OF POSTING ROWS)                   
         L     R4,=V(POSTINGS)     R4=A(POSTING RETURNED FROM ACPOSTER)         
         USING ACPRTND,R4                                                       
*                                                                               
BILLP30  OC    0(ACPRTNL2,R4),0(R4) TEST END OF POSTINGS                        
         BZ    BILLP70                                                          
         GOTO1 CLRAREA,DMCB,V(REC),V(RECX)                                      
         MVC   MXACCOFF,SVACOFF    SET SJ CLI/PRD ACC OFFICE                    
         CLI   ACPBTYP,MBTTINTI    IF INTERNAL POSTING                          
         BL    BILLP50                                                          
         CLI   ACPBTYP,MBTTINTR                                                 
         BH    BILLP50                                                          
         CLC   SVACOF2,SPACES      AND DIFFERENT ACC OFFICE SPECIFIED           
         BNH   BILLP50                                                          
         MVC   MXACCOFF,SVACOF2    USE IT                                       
BILLP50  CLI   ACPBTYP,MBTTOINC    IF INTERNAL POSTING                          
         BL    BILLP60                                                          
         CLI   ACPBTYP,MBTTIRCV                                                 
         BH    BILLP60                                                          
         CLC   SVACOF2,SPACES      AND DIFFERENT ACC OFFICE SPECIFIED           
         BNH   BILLP60                                                          
         MVC   MXACCOFF,SVACOF2    USE IT                                       
*                                                                               
BILLP60  MVC   TEMPROW,0(R4)       SET CURRENT POSTING ROW                      
         BAS   RE,STCONTRA         SET CONTRA & CONTRAN                         
         LA    RE,TEMPROW                                                       
         ST    RE,MXROW            PASS ADDRESS TO MXPOST                       
         MVC   MXCONTRA,CONTRA     PASS CONTRA ACCOUNT                          
         MVC   MXCONTRN,CONTRAN    AND CONTRA ACCOUNT NAME                      
         BAS   RE,STMKTN           SET MARKET NAME                              
         GOTO1 =V(MXPOST),DMCB,(R3) CALL MXPOST FOR POSTING FOR ACC             
         MVI   SEQONE,NO           NOT REVERSAL RECORD                          
         MVI   DETL,NO             NOT A DETAIL RECORD                          
         GOTO1 =A(PUTSORT),DMCB,(RC),V(REC)                                     
         BAS   RE,PSTELE           ADD ELEM TO POSTING DETAIL RECORD            
*                                                                               
BILLP70  LA    R4,ACPRTNL2(R4)     NEXT POSTING RETURNED                        
         BCT   R2,BILLP30          DECREMENT NUMBER OF POSTINGS LEFT            
*                                                                               
         BAS   RE,ADDTL            ADD POST DETAIL(S) CREATED                   
         BAS   RE,WRITBIL          MARK BILL RECORD                             
BILLPX   B     EXIT                                                             
         DROP  R3,R4                                                            
         EJECT                                                                  
*--------------------------------*                                              
* PRTBIL  - PRINT BILL DETAILS   *                                              
*--------------------------------*                                              
*                                                                               
PRTBIL   NTR1                                                                   
         LA    R2,PRTBLOCK                                                      
         USING MXPRNTD,R2                                                       
         GOTO1 =A(SETPRT),DMCB,(RC)                                             
         MVI   PMODE,PMBILL       PRINT BILL INFO                               
         GOTO1 =V(MXPRNT),DMCB,(R2)                                             
         B     XIT                                                              
         DROP  R2                                                               
         SPACE 2                                                                
*-----------------------------------------------------------*                   
* CHKERR - CHECKS FLAGS SET DURING POSTINGS                 *                   
*-----------------------------------------------------------*                   
*                                                                               
CHKERRS  NTR1                                                                   
         LA    R1,ACPBLK                                                        
         USING ACPOSTD,R1                                                       
         CLI   ACPERR,0           ANY ERRORS WITH ACCOUNTS?                     
         BNE   XITNO              YES - GO TO NEXT BILL                         
         CLI   ACPERRC,0                                                        
         BNE   XITNO                                                            
         DROP  R1                                                               
         CLI   CONERR,0           ANY ERRORS WITH CONTRA ACCOUNTS?              
         BNE   XITNO              YES - GO TO NEXT BILL                         
         CLI   PSTAOR,C'Y'                                                      
         BNE   XITNO                                                            
         B     XITYES                                                           
         EJECT                                                                  
*              STCONTRA - ROUTINE TO SET CONTRA AND CONTRAN                     
*                                                                               
STCONTRA NTR1                                                                   
         LA    R3,TEMPROW          R3=A(CURRENT POSTING ROW)                    
         USING ACPRTND,R3                                                       
         ZIC   RE,PSTNUM2          RE=(M'NUMBER OF ENTRIES IN TABLE)            
         L     R1,=V(CONTBL)       R1=A(CONTRA ACCOUNT TABLE)                   
         USING CONTBLD,R1                                                       
STCNTR2  CLC   CONTYPE,ACPBTYP     MATCH ON CURRENT ROW                         
         BNE   *+14                                                             
         CLC   CONTYPE2,ACPWHC                                                  
         BE    STCNTR5                                                          
         LA    R1,CONTBLL(R1)      BUMP TO NEXT ENTRY IN TABLE                  
         BCT   RE,STCNTR2          LOOP                                         
         DC    H'0'                MUST BE IN TABLE                             
*                                                                               
STCNTR5  MVC   CONTRA,CONACC       SET CONTRA ACCOUNT                           
         OC    CONTRA+1(14),SPACES                                              
         MVC   CONTRAN,CONNAME     AND CONTRA ACCOUNT NAME                      
         OC    CONTRAN,SPACES                                                   
         B     XIT                                                              
         DROP  R1,R3                                                            
         EJECT                                                                  
*-----------------------------------------------------------*                   
*              STMKTN   - ROUTINE TO SET MARKET NAME                            
*-----------------------------------------------------------*                   
*                                                                               
STMKTN   NTR1                                                                   
*                                                                               
* REMOVED THIS CHECK FOR RETAIL BILLS BECAUASE SPOT/NET BILL HEADERS            
* ARE NOW BIG ENOUGH TO HOLD BOTH MARKET NAME AND RETAIL CODE SO THEY           
* NO LONGER SHARE THE SAME SPOT ON THE BILL HEADER RECORD                       
*        CLC   =C'RET',TYPEBIL     NOT RETAIL BILLS                             
*        BE    XIT                                                              
         L     R2,AMXBLK           R2=A(MXPOST BLOCK)                           
         USING MXPOSTD,R2                                                       
         L     R5,MXBILL           A(BILL RECORD)                               
         USING BILLRECD,R5                                                      
         MVC   MXMRKTN,SPACES      CLEAR NAME AREA IN MXPOST BLK                
         MVC   WORK(L'BLMKT),BLMKT    MARKET CODE                               
         OC    WORK(L'BLMKT),SPACES                                             
         LA    R0,L'BLMKT                                                       
         LA    RF,WORK                                                          
         CLI   0(RF),X'40'                                                      
         BH    STMKT02                                                          
         LA    RF,1(RF)                                                         
         BCT   R0,*-12                                                          
         B     XIT                                                              
*                                                                               
STMKT02  XC    KEY,KEY                                                          
         MVI   KEY,C'0'                                                         
         MVC   KEY+1(16),KEY                                                    
         LA    R3,KEY                                                           
         USING MKTRECD,R3          MARKET RECORD ON STATION FILE                
         MVI   MKTKTYPE,MKTKTYPQ                                                
         MVC   MKTKMED,MXMED                                                    
         MVC   MKTKAGY,QAGY                                                     
         MVC   MKTKMKT,WORK        MARKET CODE                                  
         MVC   KEYSAVE,KEY                                                      
         GOTO1 READMKT                                                          
         L     R3,ADMARKET                                                      
         CLC   MKTREC(MKTKEYLQ),KEYSAVE                                         
         BNE   XIT                                                              
*                                                                               
         MVC   MXMRKTN,MKTNAME     MARKET NAME TO MXPOST BLK                    
         OC    MXMRKTN,MKTNAME                                                  
         B     XIT                                                              
         DROP  R2,R3                                                            
         EJECT                                                                  
*-------------------------------------------*                                   
* ADDTL -ADD POST DETAIL RECORD(S) CREATED  *                                   
*-------------------------------------------*                                   
*                                                                               
ADDTL    NTR1                                                                   
         L     R1,=V(PSTREC)                                                    
         LR    R2,R1                                                            
         LA    R2,4(R2)                                                         
         USING MPDRECD,R2                                                       
         SR    RE,RE                                                            
         ICM   RE,3,0(R1)         LENGTH OF WORKER RECORD                       
         SH    RE,=H'4'           WORKER RECORD HEADER                          
         STCM  RE,3,MPDRLEN       SET RECORD LENGTH                             
         MVI   SEQONE,NO          NOT REVERSAL RECORD                           
         MVI   DETL,YES           DETAIL RECORD                                 
         GOTO1 =A(PUTSORT),DMCB,(RC),V(PSTREC)                                  
*                                                                               
         TM    MPDRSTA,X'01'      SECOND POST DETAIL RECORD?                    
         BNO   ADDTLX             NO                                            
         L     R1,=V(PSTREC2)                                                   
         LR    R2,R1                                                            
         LA    R2,4(R2)                                                         
         SR    RE,RE                                                            
         ICM   RE,3,0(R1)                                                       
         SH    RE,=H'4'                                                         
         STCM  RE,3,MPDRLEN                                                     
         MVI   SEQONE,NO          NOT REVERSAL RECORD                           
         MVI   DETL,YES           DETAIL RECORD                                 
         GOTO1 =A(PUTSORT),DMCB,(RC),V(PSTREC2)                                 
*                                                                               
ADDTLX   B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*---------------------------------------------------------------*               
* WRITBIL - MARKS BILL RECORD TRANSFERRED & SETS DATE IN BILL   *               
*---------------------------------------------------------------*               
WRITBIL  NTR1                                                                   
         L     R5,ADBILL                                                        
         ST    R5,AREC                                                          
         MVI   TRUEAOR,NO                                                       
         BAS   RE,GETSME          RE-ESTABLISH READ SEQUENCE                    
         SR    R3,R3                                                            
         IC    R3,NUMABIL          # OF AOR BILLS                               
*                                                                               
WRITB2   TM    MXOPT,MXNOWRIT     IF WRITE=NO -                                 
         BO    WRITB05            DON'T MARK BILL RECORDS                       
         OI    KEY+13,X'01'       MARK DIRECTORY KEY TRANS                      
         GOTO1 DATAMGR,DMCB,DMWRT,SPTDIR,KEY,KEY                                
         CLI   DM3,0                                                            
         BE    *+6                                                              
         DC    H'0'                                                             
WRITB05  GOTO1 DATAMGR,DMCB,GETREC,SPTFILE,KEY+14,AREC,DMWORK                   
         CLI   TRUEAOR,YES        1ST TIME PASS ?                               
         BNE   WRITB20                                                          
         CLC   RUNDT,BDATE        NO - LOOKING FOR TRUE AOR BILL                
         BE    *+6                                                              
         DC    H'0'                                                             
         TM    BILSTAT,BSTTAORQ   BILL MUST BE TRUE AOR                         
         BNO   WRITB40                                                          
         MVI   TRUEAOR,C'M'       FOUND IT - MARK REC & DIRECTORY               
         B     WRITB30                                                          
*                                                                               
WRITB20  TM    BILSTAT,BSTCAORQ   1ST TIME THROUGH - SET FLG IF NECESS          
         BNO   WRITB30                                                          
         TM    BILSTAT,BSTSADJQ                                                 
         BO    WRITB30                                                          
         TM    BILSTAT,BSTSNETQ                                                 
         BO    WRITB30                                                          
         MVI   TRUEAOR,C'Y'       MAIN CLT BILL SO MUST FIND TRUE AOR           
*                                                                               
WRITB30  TM    MXOPT,MXNOWRIT     IF WRITE=NO -                                 
         BO    WRITB40            DON'T MARK BILL RECORDS                       
         OI    BCNTRL,X'01'       MARK RECORD TRANSFERRED                       
         MVC   BILPOST,TODAYP     AND SET TODAYS DATE                           
         GOTO1 DATAMGR,DMCB,PUTREC,SPTFILE,KEY+14,AREC,DMWORK                   
WRITB40  CLI   TRUEAOR,C'Y'       IF CLIENT AOR BILL - ALSO MARK TRUE           
         BNE   WRITB50                                                          
         CLI   QOPT4,C'M'          DDS ONLY, JUST MARK BILLS                    
         BE    WRITB60                                                          
         BAS   RE,GETNXT                                                        
         B     WRITB70                                                          
WRITB50  CLI   TRUEAOR,C'M'                                                     
         BNE   WRITBX                                                           
         TM    MXOPT,MXNOWRIT     IF WRITE=NO -                                 
         BO    WRITBX             DON'T MARK BILL RECORDS                       
         OI    KEY+13,X'01'       MARK DIRECTORY KEY TRANS                      
         GOTO1 DATAMGR,DMCB,DMWRT,SPTDIR,KEY,KEY                                
         CLI   DM3,0                                                            
         BE    *+6                                                              
         DC    H'0'                                                             
WRITB60  BAS   RE,GETNXT                                                        
WRITB70  BRCT  R3,WRITB05                                                       
WRITBX   B     XIT                                                              
         EJECT                                                                  
*---------------------------------------------------*                           
* RDPROF - CALLS ACPOSTER TO READ PROF MAINT RECORDS*                           
*---------------------------------------------------*                           
*                                                                               
RDPROF   NTR1                                                                   
         GOTO1 CLRAREA,DMCB,V(PROFREC),V(PROFRECX)                              
         GOTO1 =A(SETBLK),DMCB,(RC) SET UP BASIC ACPOSTER CALL                  
         LA    R2,ACPBLK                                                        
         USING ACPOSTD,R2                                                       
         MVI   ACPTYPE,1          PROF MAINT RECORD                             
         MVC   ACPPOST,=V(PROFREC) A(PROFILES RETURNED)                         
         MVI   ACPTYPE2,MPRKREG   READ FOR REGULAR BILLING                      
         CLC   =C'RET',TYPEBIL    IF RETAIL BILL                                
         BNE   *+8                                                              
         MVI   ACPTYPE2,MPRKRTL   READ FOR REG + RET BILLING                    
         CLC   =C'BPC',TYPEBIL    IF RETAIL BILL                                
         BNE   RDPRF02                                                          
         MVI   ACPTYPE2,MPRKRLMG  READ FOR REG + RLPOST BILLNG                  
         B     RDPRF10                                                          
RDPRF02  CLC   =C'REGM',MTYPEBIL                                                
         BNE   *+8                                                              
         MVI   ACPTYPE2,MPRKMTRA  READ FOR REG + MPOST BILLING                  
         CLC   =C'REGX',MTYPEBIL                                                
         BE    RDPRF05                                                          
         CLC   =C'ADJX',MTYPEBIL                                                
         BE    RDPRF05                                                          
         CLC   =C'AORX',MTYPEBIL  AORXM/AORXA/AORXJ                             
         BNE   RDPRF10                                                          
RDPRF05  MVI   ACPTYPE2,MPRKTRAD  READ FOR REG + TRADE BILLING                  
RDPRF10  GOTO1 =A(POSTCALL),DMCB,(RC)    CALL TO ACPOSTER                       
RDPROFX  B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*----------------------------------------------------------------*              
* PSTHDR  - START SETUP OF POST DETAIL REC TO FOR BILL TRANSFER  *              
*----------------------------------------------------------------*              
PSTHDR   NTR1                                                                   
         GOTO1 CLRAREA,DMCB,V(PSTREC),V(PSTRECX)                                
         L     R1,=V(PSTREC)                                                    
         LR    R2,R1                                                            
         LA    R2,4(R2)           PT TO ACTUAL RECORD                           
         USING MPDRECD,R2                                                       
         LA    RE,ACCORFST                                                      
         LA    RE,4(RE)           INCLUDE WORKER FILE RECORD HEADER             
         LA    RE,1(RE)           + 1 00 FOR PHYSICAL END OF RECORD             
         STCM  RE,3,0(R1)         STARTING LENGTH OF WORKER RECORD              
         MVI   MPDKTYP,MPDKTYPQ   X'2F'                                         
         MVI   MPDKSUB,MPDKSUBQ   X'00'                                         
         MVC   MPDKCPY,ACCTCD     NATIVE COMPANY CODE                           
         MVC   MPDKALPH,SALPH     AGY ALPHA FOR MEDIA SPLIT FILES               
         MVC   MPDKSYS,SYS        SYSTEM                                        
         MVC   MPDKMED,MYMED      MEDIA                                         
         MVC   MPDKCLI,CLT        CLIENT                                        
         MVC   MPDKPRD,PRD        PRODUCT                                       
         ZIC   RE,BKEYEST                                                       
         CVD   RE,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  MPDKEST,DUB        ESTIMATE                                      
*                                                                               
         MVC   MPDKPER,BDATE      BILL MONTH(YYMM)-WAS USING BKEYMBIL           
*                                                                               
         XC    WORK,WORK          GET SERVICE MONTH                             
         MVC   WORK(2),BKEYYSRV                                                 
         MVI   WORK+2,X'01'       FUDGE DAY                                     
         GOTO1 DATCON,DMCB,(3,WORK),(0,WORK+3)                                  
         MVC   MPDKMOS,WORK+3     MONTH OF SERVICE (YYMM)                       
*                                                                               
         MVI   MPDKINV,C'0'                                                     
         MVC   MPDKINV+1(4),BINVOICE+2 INV NUMBER                               
         LA    R1,ACCORFST        DISP TO FIRST ELEM                            
         AR    R2,R1                                                            
         STCM  R2,15,NXTPSTE      A(ADD NEXT ELEMENT)                           
         MVI   DTLREC,0           INDICATE ONE DETAIL REC -SO FAR               
         B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*------------------------------------------------*                              
* PSTELE  - FOR EACH ACCOUNT RETURNED FROM       *                              
*           ACPOSTER (& RECORD ADDED TO WRKFILE) *                              
*           --- AN ELEMENT NEEDS TO ADDED TO     *                              
*           THE POST DETAIL RECORD               *                              
* NOTE : IF POST DETAIL RECORD BECOMES TOO LARGE *                              
*        ROUTINE WILL SPLIT TO A NEW DETAIL REC  *                              
* CAUTION : MAX OF TWO DTL RECORDS ALLOWED       *                              
*------------------------------------------------*                              
*                                                                               
PSTELE   NTR1                                                                   
         LA    RE,MBTPDLNQ        LENGTH OF ELEMENT TO ADD                      
         SR    R1,R1                                                            
         CLI   DTLREC,0           ON 1ST RECORD?                                
         BE    PSTELE5            YES- GO CHECK LENGTH                          
         L     R2,=V(PSTREC2)                                                   
         ICM   R1,3,0(R2)         GET LENGTH OF RECORD #2                       
         AR    R1,RE              ADD NEW ELE TO LENGTH                         
         STCM  R1,3,0(R2)         AND GO ADD IT TO RECORD                       
         B     PSTELE10                                                         
*                                                                               
PSTELE5  L     R2,=V(PSTREC)      YES                                           
         ICM   R1,3,0(R2)                                                       
         AR    R1,RE              ADD ELEMENT TO BE ADDED                       
         C     R1,=F'1000'        RECORD OVER 1000 BYTES?                       
         BH    PSTELE8            YES - CREATE 2ND RECORD                       
         STCM  R1,3,0(R2)         NO - UPDATE LENGTH AND GO ADD IT              
         B     PSTELE10                                                         
*                                                                               
PSTELE8  BAS   RE,DOPDTL2         NEED TO CREATE A 2ND POST DTL REC             
*                                                                               
PSTELE10 LA    R1,TEMPROW         CURRENT ACCOUNT INFO                          
         USING ACPRTND,R1                                                       
         XC    ELEM,ELEM                                                        
         LA    R2,ELEM                                                          
         USING MBTELD,R2                                                        
         MVI   MBTEL,MBTELQ       X'2E'                                         
         MVI   MBTLLN,MBTPDLNQ    LENGTH                                        
         MVC   MBTTYP,ACPBTYP     POSTING TYPE                                  
         MVC   MBTCHNG,ACPCHDT    LAST CHANGE DATE                              
         MVC   MBTULA(14),ACPACC  UNIT/LEDGER/ACCOUNT                           
         OC    MBTULA(14),SPACES                                                
         MVC   MBTOFFC,SVACOFF    ACCOUNTING OFFICE                             
         CLI   ACPBTYP,MBTTINTI   IF INTERNAL POSTING                           
         BL    PSTELE14                                                         
         CLI   ACPBTYP,MBTTINTR                                                 
         BH    PSTELE14                                                         
         B     PSTELE15                                                         
PSTELE14 CLI   ACPBTYP,MBTTOINC   IF OTHER INCOME POSTING                       
         BE    PSTELE15                                                         
         CLI   ACPBTYP,MBTTOBIL   IF OTHER BILLING POSTING                      
         BE    PSTELE15                                                         
         CLI   ACPBTYP,MBTTOREV   IF OTHER REVENUE POSTING                      
         BE    PSTELE15                                                         
         CLI   ACPBTYP,MBTTIRCV   IF INTERCOMPANY RECIEVABLE                    
         BNE   PSTELE16                                                         
PSTELE15 CLC   SVACOF2,SPACES     AND DIFFERENT ACC OFFICE SPECIFIED            
         BNH   PSTELE16                                                         
         MVC   MBTOFFC,SVACOF2    USE IT                                        
PSTELE16 TM    ACPSTAT,ACPDEB     DEBIT?                                        
         BNO   *+8                                                              
         OI    MBTSTAT,MBTSDR     STATUS BYTE (DEBIT OR CREDIT)                 
         CLI   ACPBIL2,2                                                        
         BNE   *+8                                                              
         OI    MBTSTAT,MBTSAOR    INDICATE TRUE AOR BILL                        
         MVC   MBTCNTRA,CONTRA+1  CONTRA ACCOUNT                                
         OC    MBTCNTRA,SPACES                                                  
         MVC   MBTPOST,ACPAMT2    POSTING AMOUNT                                
         MVC   MBTMEMO,ACPMEMO2   MEMO AMOUNT                                   
         DROP  R2,R1                                                            
*                                                                               
         ICM   R1,15,NXTPSTE      POSITION TO INSERT NEXT ELEMENT               
         LA    RE,MBTPDLNQ        ELEMENT LENGTH                                
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R1),ELEM       MOVE ELEMENT INTO RECORD AREA                 
         LA    RE,1(RE)           RESTORE ELEMENT LENGTH                        
         AR    R1,RE              PT TO NXT AVAILABLE SPOT IN                   
         STCM  R1,15,NXTPSTE      RECORD TO ADD NEW ELEMENT                     
         MVI   0(R1),0                                                          
         B     XIT                                                              
         EJECT                                                                  
*------------------------------------------------------*                        
* DOPDTL2 - FIRST POST DETAIL RECORD GETTING TOO LARGE *                        
*           MUST SPLIT INTO TWO RECORDS                *                        
*------------------------------------------------------*                        
*                                                                               
DOPDTL2  NTR1                                                                   
         L     R3,=V(PSTREC2)     SECOND POST DETAIL RECORD                     
         L     R2,=V(PSTREC)      FIRST POST DETAIL RECORD                      
         LA    R1,ACCORFST        KEY PART OF RECORD                            
         LA    R1,4(R1)           + HEADER                                      
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),0(R2)      COPY HEADER + KEY PART OF REC                 
         LA    R1,1(R1)           RESTORE LENGTH MOVED                          
         AR    R3,R1                                                            
         STCM  R3,15,NXTPSTE      SET POSITION TO ADD 1ST ELEMENT               
*                                                                               
         L     R3,=V(PSTREC2)                                                   
         LA    R1,ACCORFST        + KEY PART OF RECORD                          
         LA    R1,4(R1)           HEADER                                        
         LA    R1,1(R1)           + 1 X'00' FOR PHYSICAL END OF REC             
         LA    RE,MBTPDLNQ        + FIRST ELEMENT                               
         AR    R1,RE                                                            
         STCM  R1,3,0(R3)         LENGTH OF WORKER REC SO FAR                   
*                                                                               
         MVI   DTLREC,X'01'       INDICATE A 2ND DTL RECORD                     
         L     R3,=V(PSTREC2)                                                   
         LA    R3,4(R3)                                                         
         USING MPDRECD,R3                                                       
         MVI   MPDKSEQ,X'01'      GIVE 2ND REC A SUB LINE                       
         DROP  R3                                                               
*                                                                               
         L     R3,=V(PSTREC)                                                    
         LA    R3,4(R3)                                                         
         USING MPDRECD,R3                                                       
         OI    MPDRSTA,X'01'      INDICATE SECOND RECORD TO FOLLOW              
         B     XIT                                                              
         DROP  R3                                                               
         EJECT                                                                  
*-------------------------------------------------*                             
* READ B1 PROFILE FOR LOWEST LEVEL TO SEE IF      *                             
* SYSTEM/MEDIA CODE HAS BEEN REDEFINED            *                             
* HIERARCHY FROM HIGHEST TO LOWEST:               *                             
* 1. AGENCY LEVEL                                 *                             
* 2. MEDIA LEVEL                                  *                             
* 3. MEDIA "ALL" OFFICE LEVEL                     *                             
* 4. OFFICE LEVEL                                 *                             
*-------------------------------------------------*                             
*                                                                               
RDB1PROF NTR1                                                                   
         XC    SVRSYS,SVRSYS                                                    
         XC    SVRMED,SVRMED                                                    
         XC    B1PROF,B1PROF                                                    
         XC    WORK,WORK                                                        
*                                                                               
         MVI   WORK,C'S'                                                        
         MVC   WORK+2(2),=C'B1'                                                 
         MVC   WORK+4(2),RCAGENCY  FILL IN AGENCY                               
         MVC   WORK+6(1),MYMED     FILL IN MEDIA                                
         MVC   WORK+7(3),CLT       FILL IN CLIENT                               
         MVI   WORK+10,C'*'     FILL IN OFFICE                                  
         MVC   WORK+11(1),SVCOFF                                                
         GOTO1 GETPROF,DMCB,WORK,B1PROF,DATAMGR                                 
         OC    B1PROF,B1PROF                                                    
         BZ    RDB1X               NO B1 PROFILES SET UP                        
         CLC   B1PROF+2(2),=C'**'  IF BOTH DEFINED AS "**" THEN...              
         BE    RDB1X               ...USE DEFAULT (ST,SR,SX, ETC.)              
         MVC   SVRSYS,SYS          MOVE IN DEFAULT SYSTEM                       
         CLI   B1PROF+2,C'*'                                                    
         BE    *+10                                                             
         MVC   SVRSYS,B1PROF+2     MOVE IN REDEFIEND SYSTEM                     
         MVC   SVRMED,MYMED        MOVE IN DEFAULT MEDIA                        
         CLI   B1PROF+3,C'*'                                                    
         BE    RDB1X                                                            
         MVC   SVRMED,B1PROF+3     MOVE IN REDEFINED MEDIA                      
*                                                                               
RDB1X    B     XIT                                                              
         EJECT                                                                  
*-------------------------------------------------*                             
* MEDIA OFF LAST-CALL MXPRNT TO GET OFFICE TOTALS *                             
*-------------------------------------------------*                             
*                                                                               
OFCL     CLI   ANYBILL,C'Y'       BILL PROCESSED FOR THIS CLIENT?               
         BNE   OFCLX                                                            
         LA    R2,PRTBLOCK                                                      
         USING MXPRNTD,R2                                                       
         GOTO1 =A(SETPRT),DMCB,(RC)                                             
         MVI   PMODE,PMTOTAL      PRINT OUT TOTAL LINE                          
         MVI   PTOTTYPE,PTOTMOF   MEDIA OFFICE TOTAL                            
         GOTO1 =V(MXPRNT),DMCB,(R2)                                             
OFCLX    B     EXIT                                                             
         DROP  R2                                                               
         SPACE                                                                  
*-------------------------------------------------*                             
* CLIENT LAST  - CALL MXPRNT TO GET CLIENT TOTALS *                             
*-------------------------------------------------*                             
*                                                                               
CLTL     CLI   ANYBILL,C'Y'       BILL PROCESSED FOR THIS CLIENT?               
         BNE   CLTLX                                                            
         LA    R2,PRTBLOCK                                                      
         USING MXPRNTD,R2                                                       
         GOTO1 =A(SETPRT),DMCB,(RC)                                             
         MVI   PMODE,PMTOTAL      PRINT OUT TOTAL LINE                          
         MVI   PTOTTYPE,PTOTCLT   CLIENT TOTAL                                  
         GOTO1 =V(MXPRNT),DMCB,(R2)                                             
CLTLX    B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
*----------------------------------------------*                                
* PRODUCT LAST - CALL MXPRNT FOR PRODUCT TOTAL *                                
*----------------------------------------------*                                
*                                                                               
PRDL     CLI   ANYBILL,C'Y'       BILL PROCESSED FOR THIS CLIENT?               
         BNE   PRDLX                                                            
         LA    R2,PRTBLOCK                                                      
         USING MXPRNTD,R2                                                       
         GOTO1 =A(SETPRT),DMCB,(RC)                                             
         MVI   PMODE,PMTOTAL      PRINT OUT TOTAL LINE                          
         MVI   PTOTTYPE,PTOTPRD   PRODUCT TOTAL                                 
         GOTO1 =V(MXPRNT),DMCB,(R2)                                             
PRDLX    B     EXIT                                                             
         DROP  R2                                                               
         SPACE                                                                  
*----------------------------------------------------------------*              
* REQ LAST -  PRINT MEDIA TOTALS,BILLS IN ERROR AND THE SUMMARIES*              
*----------------------------------------------------------------*              
*                                                                               
REQL     LA    R2,PRTBLOCK                                                      
         USING MXPRNTD,R2                                                       
         GOTO1 =A(SETPRT),DMCB,(RC)                                             
         MVI   PMODE,PMTOTAL      PRINT OUT MEDIA TOTAL LINE                    
         MVI   PTOTTYPE,PTOTMED                                                 
         GOTO1 =V(MXPRNT),DMCB,(R2)                                             
*                                 PRINT OUT SUMMARIES                           
         GOTO1 =A(SETPRT),DMCB,(RC)                                             
         MVI   PMODE,PMRECAP                                                    
         GOTO1 =V(MXPRNT),DMCB,(R2)                                             
*                                 PRINT OUT BILLS IN ERROR                      
         GOTO1 =A(SETPRT),DMCB,(RC)                                             
         MVI   PMODE,PMBILERR                                                   
         GOTO1 =V(MXPRNT),DMCB,(R2)                                             
         B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
*-------------------------------------------*                                   
* RUN LAST -  WRITE TRAILER RECORD OUT AND  *                                   
*             CLOSE UP WORKER FILE & REPORT *                                   
*-------------------------------------------*                                   
*                                                                               
RUNL     ZAP   RECCNT,=P'0'       RESET COUNTERS                                
         ZAP   TOTCASH,=P'0'                                                    
         ZAP   DEBS,=P'0'                                                       
         ZAP   CREDS,=P'0'                                                      
         XC    ACCOID,ACCOID                                                    
         XC    PRVINV,PRVINV                                                    
         L     RE,=A(WRKRBUFF)                                                  
         ST    RE,WFILE                                                         
         GOTO1 =V(SORTER),DMCB,=C'GET'                                          
         ICM   R3,15,4(R1)                                                      
         BZ    RUNL60             NO SORT RECORDS                               
*                                                                               
         USING SRTKEYD,R3                                                       
RUNL10   TM    UPSI,UPSISOUT                                                    
         BZ    RUNL12                                                           
         LA    R0,SRTKEYLQ+L'ACTKEY                                             
         GOTOR =V(PRNTBL),DMCB,=C'GET',(R3),C'DUMP',(R0),=C'1D'                 
                                                                                
RUNL12   CLI   FORCECWK,YES        CLOSE WORKER FILE                            
         BNE   RUNL26              NO, THINGS AS USUAL                          
         CLC   ACCOID,SRTOID      MATCH ON ORIGINAL ID                          
         BNE   RUNL28              THIS WAS GOING TO CLOSE FILE ANYWAY          
         CLC   DEBS,CREDS          DEBITS EQUAL CREDITS ?                       
         BNE   RUNL26              CAN'T BREAK                                  
         CLC   SRTINV,PRVINV                                                    
         BE    RUNL26              PUT LIKE INVOICES IN SAME WF                 
         B     RUNL28              OKAY TO CLOSE NOW                            
*                                                                               
RUNL26   CLC   ACCOID,SRTOID      MATCH ON ORIGIN ID, WORKER FILE ID            
         BE    RUNL30                                                           
                                                                                
RUNL28   OC    ACCOID,ACCOID      FIRST TIME?                                   
         BZ    *+8                YES                                           
         BAS   RE,CLSWRK          NO -CLOSE PREVIOUS WORKER FILE                
         MVC   ACCOID,SRTOID      SET NUMERIC ORIGIN ID                         
         BAS   RE,SETID           SET WORKER FILE ID                            
                                                                                
RUNL30   BAS   RE,ADDWRK          ADD WORKER FILE RECORD                        
         MVC   PRVINV,SRTINV                                                    
         GOTO1 =V(SORTER),DMCB,=C'GET'                                          
         ICM   R3,15,4(R1)                                                      
         BNZ   RUNL10                                                           
         BAS   RE,CLSWRK          CLOSE WORKER FILE                             
*                                                                               
RUNL60   BAS   RE,CONTROL         INTERAL CONTROLS                              
         GOTO1 =V(SORTER),DMCB,=C'END'                                          
RUNLX    B     EXIT                                                             
         EJECT                                                                  
*---------------------------------------*                                       
* SETID - SETS ID FOR NEW WORKER FILE   *                                       
*---------------------------------------*                                       
*                                                                               
SETID    NTR1                                                                   
         MVI   FORCECWK,NO         DON'T FORCE CLOSE OF WORKER FILE             
         LA    R2,WID                                                           
         XC    WID,WID                                                          
         USING UKRECD,R2          SET ID(INDEX) OF WORKER FILE                  
         MVC   UKUSRID,ACCOID                                                   
         MVI   UKFLAG,X'01'       INDICATE CAN HAVE MULTIPLE FILES              
         MVC   UKSYSPRG,=C'SMX'   FILE NAME                                     
         CLI   SYS,C'N'           IF NETWORK REQ                                
         BNE   *+8                                                              
         MVI   UKSYSPRG,C'N'      NAME IS NMX                                   
*                                                                               
         TM    MXOPT,MXMBSOB      SAME NIGHT POSTINGS?                          
         BZ    *+8                                                              
         MVI   UKSYSPRG+2,C'L'    THEN CHANGE SMX/NMX TO SML/NML                
*                                                                               
         CLC   =C'MY',RCPROG      IF REVERSAL                                   
         BNE   *+10                                                             
         MVC   UKSYSPRG+1(2),=C'MY' NAME IS SMY/NMY                             
         L     R1,LOGOC                                                         
         USING LOGOD,R1                                                         
         CLI   LOGOJOB+7,C' '     LAST CHAR OF JOB                              
         BNH   *+10                                                             
         MVC   UKSUBPRG,LOGOJOB+7                                               
         PACK  FULL(2),TODAY+4(3)                                               
         OC    RERUNDT,RERUNDT    SPECIAL RERUN BILLING (QOPT1=D)               
         BZ    *+10                                                             
         PACK  FULL(2),RERUNDT+4(3)                                             
         MVC   UKDAY,FULL         DATE                                          
         MVI   UKCLASS,C'P'       POSTING FILE                                  
*        OI    UKSTAT,X'40'       PUT WORKER FILE ON HOLD FOR NOW               
         GOTO1 WORKER,DMCB,=C'OPEN',WFILE,WID                                   
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'00'                                                            
                                                                                
         L     RE,=V(WKFKEYS)                                                   
         LLC   RF,#WRKFS           # OF WORKER FILES                            
         AHI   RF,1                                                             
         STC   RF,#WRKFS           SAVE # OF FILES SO FAR                       
         CLC   #WRKFS,MAX#WKF      MAX FILES STORED                             
         BNH   *+6                                                              
         DC    H'00'               WE MAXED OUT                                 
                                                                                
         BCTR  RF,0                                                             
         MHI   RF,L'UKINDEX                                                     
         AR    RE,RF                                                            
         MVC   0(L'UKINDEX,RE),WID SAVE OFF INDEX                               
                                                                                
SETIDX   B     EXIT                                                             
         DROP  R2,R1                                                            
         EJECT                                                                  
*------------------------------------------------------------*                  
* ADDWRK - ADD RECORD TO WORKER FILE & UPDATE CURRENT VALUES *                  
*------------------------------------------------------------*                  
*                                                                               
ADDWRK   NTR1                                                                   
         LR    R2,R3                                                            
         LA    R2,SRTKEYLQ(R2)    PT TO WORKER FILE RECORD                      
         BAS   RE,CHKELE          *** TRAP E5 ELE MUST EXIST                    
         BAS   RE,UPDATE          UPDATE WORKER FILE COUNTERS                   
         TM    MXOPT,MXNOPOST     POST =NO OPTION?                              
         BO    ADDWRKX                                                          
         GOTO1 WORKER,DMCB,=C'ADD',WFILE,WID,(R2)                               
         TM    DMCB+8,X'C0'                                                     
         BZ    *+6                                                              
         DC    H'0'                                                             
         USING WKRECD,RE                                                        
         L     RE,WFILE                                                         
         CLI   WKSEQ,MAXCI          MAX CI WE WANT BEFORE ITS TOO LATE          
         BL    *+8                                                              
         MVI   FORCECWK,YES        FORCE CLOSE WHEN NEXT POSSIBLE               
         DROP  RE                                                               
ADDWRKX  B     EXIT                                                             
         EJECT                                                                  
*------------------------------------------------------------*                  
* CHKELE - TRAP TO DIE IF NO E5 ELEMENT IN POSTING RECORDS   *                  
*------------------------------------------------------------*                  
*                                                                               
CHKELE   NTR1                                                                   
         SR    RE,RE                                                            
         LR    R1,R2              PT TO START OF RECORD                         
         LA    R1,4(R1)           PT PAST HEADER                                
         USING TRNELD,R1                                                        
         CLI   0(R1),X'2F'        IF MY RECORD SKIP                             
         BE    EXIT                                                             
CHKELE10 CLI   0(R1),GDAELQ                                                     
         BE    EXIT                                                             
         ICM   RE,1,1(R1)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R1,RE                                                            
         CLI   0(R1),0                                                          
         BNE   CHKELE10                                                         
         DC    H'0'               E5 NOT IN RECORD --- DIE                      
         SPACE                                                                  
*------------------------------------------------------------*                  
* UPDATE - UPDATES RECCNT & TOTCASH FOR CURRENT WORKER FILE  *                  
*------------------------------------------------------------*                  
*                                                                               
UPDATE   NTR1                                                                   
         SR    RE,RE                                                            
         LR    R1,R2              PT TO START OF RECORD                         
         LA    R1,4(R1)           PT PAST HEADER                                
         USING TRNELD,R1                                                        
         CLI   0(R1),X'2F'        IF MY RECORD SKIP                             
         BE    UPDATEX                                                          
UPDATE5  CLI   0(R1),TRNELQ       X'44' ELEMENT                                 
         BE    UPDATE10                                                         
         ICM   RE,1,1(R1)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R1,RE                                                            
         B     UPDATE5                                                          
*                                                                               
UPDATE10 LA    RE,CREDS                                                         
         TM    TRNSTAT,TRNSDR     DEBIT?                                        
         BZ    UPDATE15                                                         
         LA    RE,DEBS                                                          
         AP    TOTCASH,TRNAMNT                                                  
*                                                                               
UPDATE15 AP    0(L'DEBS,RE),TRNAMNT                                             
         AP    RECCNT,=P'1'                                                     
*                                                                               
UPDATEX  B     EXIT                                                             
         EJECT                                                                  
*-------------------------------------------------------------*                 
* CLSWRK - ADD TRAILER RECORD & CLOSE CURRENT WORKER FILE     *                 
*-------------------------------------------------------------*                 
*                                                                               
CLSWRK   NTR1                                                                   
         GOTO1 CLRAREA,DMCB,V(REC),V(RECX)                                      
         L     R2,=V(REC)                                                       
         MVC   2(2,R2),=H'2'                                                    
         XC    WORK,WORK                                                        
         LA    R1,WORK            BUILD TRAILER RECORD                          
         USING PSSUBFD,R1                                                       
         MVI   PSSBEL,PSSBELQ     X'52'                                         
         MVI   PSSBLEN,PSSUBFL    LENGTH                                        
         MVC   PSSBDESC,=CL15'PRINT BILLS'                                      
         ZAP   PSSBRECS,RECCNT    NUMBER OF RECORDS                             
         ZAP   PSSBCASH,TOTCASH   TOTAL CASH                                    
         DROP  R1                                                               
         GOTO1 RECUP,DMCB,(X'FF',2(R2)),WORK,4(R2)                              
         ICM   RE,3,2(R2)           MOVE LENGTH                                 
         LA    RE,2(RE)                                                         
         STCM  RE,3,0(R2)                                                       
         XC    2(2,R2),2(R2)                                                    
         TM    MXOPT,MXNOPOST     POST =NO OPTION?                              
         BO    CLSWRK10                                                         
         XC    DMCB(24),DMCB                                                    
         GOTO1 WORKER,DMCB,=C'ADD',WFILE,WID,V(REC)                             
         GOTO1 (RF),(R1),=C'CLOSE'                                              
CLSWRK10 BAS   RE,SETAGY          SET AGENCY DATA FOR INTERNAL CONTROL          
         ZAP   RECCNT,=P'0'       RESET COUNTERS                                
         ZAP   TOTCASH,=P'0'                                                    
         ZAP   DEBS,=P'0'                                                       
         ZAP   CREDS,=P'0'                                                      
CLSWRKX  B     EXIT                                                             
         EJECT                                                                  
*---------------------------------------*                                       
* SETAGY  - SET AGENCY TABLE            *                                       
*---------------------------------------*                                       
*                                                                               
SETAGY   NTR1                                                                   
         XC    AKEY,AKEY                                                        
         LA    RE,AKEY                                                          
         USING CTIREC,RE                                                        
         MVI   CTIKTYP,CTIKTYPQ   C'I'                                          
         MVC   CTIKID+8(2),ACCOID                                               
         DROP  RE                                                               
         L     R2,ADBUY                                                         
         USING CTIREC,R2                                                        
         GOTO1 DATAMGR,DMCB,=C'DMREAD  ',=C'CTFILE ',AKEY,(R2),0                
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'               WHAT SHOULD I DO???                           
         SR    R0,R0                                                            
         LA    R1,CTIDATA                                                       
         USING CTDSCD,R1                                                        
SETAGY5  CLI   CTDSCEL,0                                                        
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   CTDSCEL,CTDSCELQ   X'02'                                         
         BE    SETAGY10                                                         
         ICM   R0,1,CTDSCLEN                                                    
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R1,R0                                                            
         B     SETAGY5                                                          
*                                                                               
         USING AGYTBLD,RE                                                       
SETAGY10 LA    RE,AGYTBL           A(AGENCY TABLE TOTALS)                       
         SR    RF,RF                                                            
         ICM   RF,1,#AGY           # OF AGENCIES IN TABLE                       
         BZ    SETAGY30            ADD NEW ENTRY                                
SETAGY20 CLC   AGYOID,ACCOID                                                    
         BE    SETAGY25                                                         
         LA    RE,AGYTBLL(RE)                                                   
         BRCT  RF,SETAGY20         NEXT ENTRY                                   
         B     SETAGY30            ADD NEW ENTRY                                
                                                                                
SETAGY25 AP    AGYTAMT,TOTCASH     ADD IF SPLIT THE WORKER FILE                 
         B     SETAGYX                                                          
                                                                                
SETAGY30 LLC   RF,#AGY             ADD NEW ENTRY                                
         AHI   RF,1                                                             
         STC   RF,#AGY             SAVE NEW #                                   
         CHI   RF,MAXAGY                                                        
         BNH   *+6                                                              
         DC    H'00'               NEED TO INCREASE TABLE SIZE                  
                                                                                
         MVC   AGYOID,ACCOID                                                    
         MVC   AGYTNM,CTDSC                                                     
         ZAP   AGYTAMT,TOTCASH                                                  
*                                                                               
SETAGYX  B     EXIT                                                             
         DROP  R2,R1,RE                                                         
         EJECT                                                                  
*----------------------------------------------*                                
* CONTROL - PRINTS PAGE WITH INTERNAL CONTROLS *                                
*----------------------------------------------*                                
CONTROL  NTR1                                                                   
         L     R2,LOGOC                                                         
         USING LOGOD,R2                                                         
         MVI   LOGOTYPE,C'E'      PRINT END REPORT LOGOS                        
         GOTO1 LOGO,DMCB,(R2)                                                   
         DROP  R2                                                               
         L     R5,VMASTC                                                        
         USING MASTD,R5                                                         
         CLI   MCTSTRUN,X'FF'     IF RUN=TEST                                   
         BE    CONTROLX           DON'T BOTHER WITH CONTROL PAGES               
         L     R2,MCVREMOT                                                      
         USING REMOTED,R2                                                       
         XC    MCARC,MCARC                                                      
         GOTO1 MCVPRINT,DMCB,=C'CLOSE'                                          
*                                                                               
         USING AGYTBLD,R3                                                       
         LA    R3,AGYTBL          PT TO TABLE OF WORKER FILES                   
         XC    REMOTKEY,REMOTKEY                                                
         MVI   REMOTJID,C'S'      PRINT CONTROL PAGES ON QUEUE                  
         CLI   SYS,C'N'           IF NETWORK REQ                                
         BNE   *+8                                                              
         MVI   REMOTJID,C'N'                                                    
         MVC   REMOTJID+1(2),RCPROG                                             
         MVI   REMOTCLS,C'T'                                                    
         MVI   REMOTCPY,X'01'                                                   
         MVC   REMOTDST,=X'0452'  FOR 1106, ID=ZIP                              
         MVC   REMOTKEY(8),MCJOB                                                
         MVC   REMOTFNO,REMOTKEY                                                
         MVI   REMOTPRT,0                                                       
         XC    REMOTRTY,REMOTRTY                                                
         XC    REMOTARC,REMOTARC                                                
         NI    REMOTTYP,X'FF'-X'60'                                             
         NI    REMOTTY1,X'FF'-(REMOTAEQ+REMOTARQ+REMOTADQ)                      
         SR    R4,R4                                                            
         ICM   R4,1,#AGY                                                        
         BZ    CONTROLX                                                         
         DROP  R2                                                               
*                                                                               
CONTROL5 BRAS  RE,ACTIVATE                                                      
         XC    HEADHOOK,HEADHOOK  PRINT TOTAL LINE                              
         MVI   FORCEHED,YES                                                     
         MVC   PAGE,=H'1'                                                       
         MVI   RCSUBPRG,5                                                       
         MVC   P+1(15),=C'TOTAL POSTING ='                                      
         EDIT  (P6,AGYTAMT),(13,P+18),2,MINUS=YES,ALIGN=LEFT                    
         LR    R1,R0                                                            
         LA    R1,P+18(R1)                                                      
         MVC   0(3,R1),=C'FOR'                                                  
         MVC   4(6,R1),AGYTNM                                                   
         GOTO1 REPORT                                                           
         LA    R3,AGYTBLL(R3)                                                   
         BCT   R4,CONTROL5                                                      
*                                                                               
CONTROLX L     R1,LOGO            NOP SO SPONSOR DOESN'T END TOO                
         MVC   0(2,R1),=X'07FE'                                                 
         B     EXIT                                                             
         DROP  R3,R5                                                            
         EJECT                                                                  
*                                                                               
         USING AGYTBLD,R3                                                       
         USING UKRECD,R4                                                        
ACTIVATE NTR1                                                                   
         L     R4,=V(WKFKEYS)                                                   
         LLC   R5,#WRKFS           # OF WORKER FILES                            
ACTV020  CLC   AGYOID,UKUSRID                                                   
         BNE   ACTV100                                                          
         MVC   WID,0(R4)           SET KEY                                      
         GOTOR WORKER,DMCB,=C'RESTORE',WFILE,WID                                
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'00'               COULD NOT ACTIVATE. MANUALLY DO?             
                                                                                
ACTV100  AHI   R4,L'UKINDEX                                                     
         BCT   R5,ACTV020                                                       
         J     EXIT                                                             
         DROP  R3                                                               
         EJECT                                                                  
*=============*                                                                 
* LITERAL POOL*                                                                 
*=============*                                                                 
*                                                                               
         LTORG                                                                  
*                                                                               
SORTCARD DC    CL80'SORT FIELDS=(000,000,A),FORMAT=BI,WORK=1'                   
RECCARD  DC    CL80'RECORD TYPE=V,LENGTH=(2024,,,600,600)'                      
*                                 PROVINCE TABLE                                
       ++INCLUDE ACTRAPRV                                                       
*                                                                               
         DS    0D                                                               
         DC    C'**WBUF**'                                                      
WRKRBUFF DS    4096X              WORKER FILE BUFFER                            
*                                                                               
         DS    0D                                                               
         DC    C'**ACCA**'                                                      
ACCAREA  DS    6000X              FOR ACPOSTER                                  
ACCAREAX EQU   *                                                                
         DROP  RB,R9,R8           DROP PREVIOUS BASE REGS                       
         EJECT                                                                  
*-------------------------------------*                                         
* RDCLT - READS ACCOUNT CLIENT HEADER *                                         
*         SETS RECORD VALUES          *                                         
*-------------------------------------*                                         
*                                                                               
         DS    0D                                                               
RDCLT    NMOD1 0,**RDCLT*                                                       
         L     RC,0(R1)                                                         
         LA    R6,4095(RC)                                                      
         LA    R6,1(R6)                                                         
         USING SPMXWRKD,RC,R6     LOCAL WORKING STORAGE                         
         LA    R7,4095(RA)                                                      
         LA    R7,1(R7)                                                         
         USING SPWORKD,RA,R7      GLOBAL WORKING STORAGE                        
*                                                                               
         MVI   SVCURR,0           INIT CURRENCY                                 
         XC    ACLIOFF,ACLIOFF                                                  
         MVC   SJCLTNM,SPACES                                                   
         LA    R2,AKEY            READ CLIENT RECORD                            
         USING CPYRECD,R2                                                       
         MVC   AKEY,SPACES                                                      
         MVC   AKEY(1),ACCTCD2    OHTER COMPANY CODE (IF APPL)                  
         MVC   AKEY+1(2),SVPROD                                                 
         MVC   AKEY+3(3),CLT                                                    
         L     R2,AMYAREA                                                       
         ST    R2,AREC                                                          
         MVI   RDSE,2             READ FROM OTHER ACC FILE(IF APPL)             
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMREAD'                               
         BNE   RDCLTX                                                           
*                                                                               
         GOTO1 GTNAME,DMCB,AREC,SJCLTNM  GET ACCOUNT NAME                       
*                                                                               
         LA    R1,ACCORFST                                                      
         AR    R2,R1                                                            
         USING PPRELD,R2                                                        
         SR    R0,R0                                                            
RDCLT50  CLI   0(R2),0            END OF RECORD                                 
         BE    RDCLTX                                                           
         CLI   0(R2),PPRELQ       X'24'                                         
         BE    RDCLT60                                                          
         CLI   0(R2),RSTELQ       X'30'                                         
         BE    RDCLT70                                                          
RDCLT55  ICM   R0,1,1(R2)         LENGTH OF ELEMENT                             
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R2,R0              BUMP TO NEXT ELEMENT                          
         B     RDCLT50                                                          
*                                                                               
RDCLT60  CLC   PPRGAOFF,SPACES     ANY OVERRIDE ACCOUNTING OFF                  
         BNH   RDCLT55                                                          
         MVC   ACLIOFF,PPRGAOFF                                                 
         B     RDCLT55                                                          
         DROP  R2                                                               
*                                                                               
         USING RSTELD,R2                                                        
RDCLT70  CLI   RSTFILT2,X'40'      ANY ACCOUNT FILTER 2                         
         BNH   RDCLT55                                                          
         MVC   SVCURR,RSTFILT2     (U)SD/(E)UR/(G)BP/(Y)EN/MXN(P)               
         B     RDCLT55                                                          
*                                                                               
RDCLTX   XIT1                                                                   
         DROP  R2                                                               
         EJECT                                                                  
*-------------------*                                                           
* LITERAL POOL      *                                                           
*-------------------*                                                           
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*------------------------------------------------------------------*            
* CHKPROF - READS PROFILES TO SEE IF DDS/OR AGY MAKE POSTINGS =NO  *            
*    XIT  - MAKEPOST SET                                           *            
*------------------------------------------------------------------*            
*                                                                               
         DS    0D                                                               
CHKPROF  NMOD1 0,**CPRF**                                                       
         L     RC,0(R1)                                                         
         LA    R6,4095(RC)                                                      
         LA    R6,1(R6)                                                         
         USING SPMXWRKD,RC,R6     LOCAL WORKING STORAGE                         
         LA    R7,4095(RA)                                                      
         LA    R7,1(R7)                                                         
         USING SPWORKD,RA,R7      GLOBAL WORKING STORAGE                        
*                                                                               
         XC    MSGTYPE,MSGTYPE    MEDIA PROFILE LEVEL                           
         MVI   MSGTYPE,C'P'       PRODUCT PROFILE LEVEL                         
         MVC   MSGTYPE+1(L'PRD),PRD                                             
*                                                                               
         MVI   MAKEPOST,C'N'      DON'T POST                                    
         GOTO1 MXPROF,DMCB,MTPFDPST,PROFWORK                                    
         OC    PROFWORK,PROFWORK  DDS ALLOWS THEM TO POST?                      
         BZ    CHKPROF8           NO PROFILE SET UP = N                         
         CLI   PROFWORK,C'Y'                                                    
         BNE   CHKPROF8           DDS=N = NO POSTINGS                           
         MVI   MAKEPOST,C'Y'      YES DDS ALLOWS POSTING                        
         GOTO1 MXPROF,DMCB,MTPFPST,PROFWORK                                     
         OC    PROFWORK,PROFWORK  THEY THEMSELVES ALLOW POSTING?                
         BNZ   CHKPROF5                                                         
         MVI   MAKEPOST,C'N'      NO DDS=Y & AGY=N --> N                        
         B     CHKPROF8                                                         
*                                                                               
CHKPROF5 CLI   PROFWORK,C'Y'                                                    
         BE    CHKPROFX           DDS=Y & AGY=Y                                 
         MVI   MAKEPOST,C'N'      DDS=Y & AGY=N                                 
*                                                                               
CHKPROF8 CLI   QOPT4,C'M'         IF MARK OR UNPOST VERSION                     
         BE    CHKPROF9                                                         
         CLI   QOPT4,C'U'                                                       
         BNE   *+12                                                             
CHKPROF9 MVI   MAKEPOST,C'Y'      ALWAYS ALLOWED                                
         B     CHKPROFX                                                         
*                                                                               
         LA    R2,PRTBLOCK                                                      
         USING MXPRNTD,R2                                                       
         GOTO1 =A(SETPRT),DMCB,(RC)                                             
         MVI   PMODE,PMMSG        PRINT MSG                                     
         OI    PMSG,PMNOPOST      POST=NO                                       
         MVC   PMSGTYPE,MSGTYPE   LEVEL WHICH PROF/MAINT APPLIES                
         GOTO1 =V(MXPRNT),DMCB,(R2)                                             
CHKPROFX XIT1                                                                   
         DROP  R2                                                               
         EJECT                                                                  
*-------------------*                                                           
* LITERAL POOL      *                                                           
*-------------------*                                                           
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*-------------------------------------------------------------*                 
* RDPRD - READS ACCOUNT PRODUCT HEADER TO SET OVERRIDE VALUES *                 
*-------------------------------------------------------------*                 
         DS    0D                                                               
RDPRD    NMOD1 0,**RDPRD*                                                       
         L     RC,0(R1)                                                         
         LA    R6,4095(RC)                                                      
         LA    R6,1(R6)                                                         
         USING SPMXWRKD,RC,R6     LOCAL WORKING STORAGE                         
         LA    R7,4095(RA)                                                      
         LA    R7,1(R7)                                                         
         USING SPWORKD,RA,R7      GLOBAL WORKING STORAGE                        
*                                                                               
         NI    BIT,X'FF'-PRDOFFC                                                
         XC    INCRIDE,INCRIDE    INCOME OVERRIDE CONTRA ACCOUNT                
         MVC   SVACOFF,ACLIOFF    DEFAULT ACCOUNTING OFFICE TO CLIENT           
         MVC   SJPRDNM,SPACES                                                   
         LA    R2,AKEY            READ PRODUCT RECORD                           
         USING CPYRECD,R2                                                       
         MVC   AKEY,SPACES                                                      
         MVC   AKEY(1),ACCTCD2    OTHER COMPANY CODE (IF APPL)                  
         MVC   AKEY+1(2),SVPROD                                                 
         MVC   AKEY+3(3),CLT                                                    
         MVC   AKEY+6(3),PRD                                                    
         L     R2,AMYAREA                                                       
         ST    R2,AREC                                                          
         MVI   RDSE,2             READ FROM OTHER ACC FILE(IF APPL)             
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMREAD'                               
         BNE   RDPRD70                                                          
         GOTO1 GTNAME,DMCB,AREC,SJPRDNM GET ACCOUNT NAME                        
*                                                                               
         LR    RE,R2                                                            
         LA    R1,ACCORFST                                                      
         AR    RE,R1                                                            
         USING SANELD,RE                                                        
         SR    R0,R0                                                            
RDPRD20  CLI   0(RE),0            END OF RECORD                                 
         BE    RDPRD40                                                          
         CLI   0(RE),SANELQ       X'3D'- SALES ANALSYS ELEMENT                  
         BE    RDPRD30                                                          
         ICM   R0,1,1(RE)         LENGTH OF ELEMENT                             
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    RE,R0              BUMP TO NEXT ELEMENT                          
         B     RDPRD20                                                          
RDPRD30  MVC   INCRIDE,SANCODE    INCOME CONTRA OVERRIDE ACC & NAME             
RDPRD40  LR    RE,R2                                                            
         LA    R1,ACCORFST                                                      
         AR    RE,R1                                                            
         USING PPRELD,RE                                                        
         SR    R0,R0                                                            
RDPRD50  CLI   0(RE),0            END OF RECORD                                 
         BE    RDPRD70                                                          
         CLI   0(RE),PPRELQ       X'24'                                         
         BE    RDPRD60                                                          
         ICM   R0,1,1(RE)         LENGTH OF ELEMENT                             
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    RE,R0              BUMP TO NEXT ELEMENT                          
         B     RDPRD50                                                          
RDPRD60  DS    0H                                                               
         CLC   PPRGAOFF,SPACES    IF PRODUCT ACCOUNTING OFFICE OVERRIDE         
         BNH   RDPRD70                                                          
         OI    BIT,PRDOFFC        SET BIT THAT PROD OFF OVERR FOUND             
         MVC   SVACOFF,PPRGAOFF   USE IT                                        
RDPRD70  DS    0H                                                               
         TM    BIT,PRDOFFC         PRODUCT OFF OVERRIDE FOUND?                  
         BO    RDPRDX              YES SO USE IT                                
*                                  ELSE CHK FOR CLIENT REC                      
         DROP  RE                                                               
         LA    R2,AKEY            READ CLIENT RECORD                            
         USING CPYRECD,R2                                                       
         MVC   AKEY,SPACES                                                      
         MVC   AKEY(1),ACCTCD2    OHTER COMPANY CODE (IF APPL)                  
         MVC   AKEY+1(2),SVPROD                                                 
         MVC   AKEY+3(3),CLT                                                    
         L     R2,AMYAREA                                                       
         ST    R2,AREC                                                          
         MVI   RDSE,2             READ FROM OTHER ACC FILE(IF APPL)             
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMREAD'                               
         BNE   RDPRDX                                                           
*                                                                               
         LA    R1,ACCORFST                                                      
         AR    R2,R1                                                            
         USING PPRELD,R2                                                        
         SR    R0,R0                                                            
RDPRD80  CLI   0(R2),0            END OF RECORD                                 
         BE    RDPRDX                                                           
         CLI   0(R2),PPRELQ       X'24'                                         
         BE    RDPRD90                                                          
         ICM   R0,1,1(R2)         LENGTH OF ELEMENT                             
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R2,R0              BUMP TO NEXT ELEMENT                          
         B     RDPRD80                                                          
*                                                                               
RDPRD90  DS    0H                                                               
         CLC   PPRGAOFF,SPACES     ANY OVERRIDE ACCOUNTING OFF                  
         BNH   RDPRDX                                                           
         MVC   SVACOFF,PPRGAOFF   USE IT                                        
RDPRDX   XIT1                                                                   
         DROP  R2                                                               
         SPACE                                                                  
*-------------------*                                                           
* LITERAL POOL      *                                                           
*-------------------*                                                           
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*------------------------------------------*                                    
* CHKPGR - SET PRODUCT GROUPS IF NECESSARY *                                    
*------------------------------------------*                                    
*                                                                               
         DS    0D                                                               
CHKPGR   NMOD1 0,**CPGR**                                                       
         L     RC,0(R1)                                                         
         LA    R6,4095(RC)                                                      
         LA    R6,1(R6)                                                         
         USING SPMXWRKD,RC,R6     LOCAL WORKING STORAGE                         
         LA    R7,4095(RA)                                                      
         LA    R7,1(R7)                                                         
         USING SPWORKD,RA,R7      GLOBAL WORKING STORAGE                        
*                                                                               
         MVC   PGRNAME,SPACES     SAVE NAME                                     
         GOTO1 MXPROF,DMCB,MTPFPGR,PROFWORK                                     
         OC    PROFWORK,PROFWORK                                                
         BZ    CHKPGRX            NO PRODUCT GROUPS                             
         L     RF,ADPRD                                                         
         LA    R1,PGRP1-PRDHDR(RF)                                              
         CLC   0(1,R1),PROFWORK   TRY TO MATCH ON THE SCHEME                    
         BE    CHKPGR10                                                         
         LA    R1,PGRP2-PRDHDR(RF)                                              
         CLC   0(1,R1),PROFWORK   TRY TO MATCH ON THE SCHEME                    
         BE    CHKPGR10                                                         
         LA    R1,PGRP3-PRDHDR(RF)                                              
         CLC   0(1,R1),PROFWORK   TRY TO MATCH ON THE SCHEME                    
         BE    CHKPGR10                                                         
         LA    R1,PGRP3-PRDHDR(RF)                                              
         CLC   0(1,R1),PROFWORK   TRY TO MATCH ON THE SCHEME                    
         BE    CHKPGR10                                                         
         LA    R1,PGRP3-PRDHDR(RF)                                              
         CLC   0(1,R1),PROFWORK   TRY TO MATCH ON THE SCHEME                    
         BNE   CHKPGRX                                                          
*                                                                               
CHKPGR10 L     RF,UTL             MATCHED ON SCHEME                             
         MVC   4(1,RF),SAVSYS                                                   
         XC    KEY,KEY                                                          
         LA    R2,KEY             READ HIGH LEVEL PRODUCT GROUP RECORD          
         USING PRGKEY,R2          (PGRDEF) TO GET LENGHT OF PROD GRP            
         MVC   PRGKTYP,=X'0D01'                                                 
         MVC   PRGKAGMD,BAGYMD                                                  
         MVC   PRGKCLT,BCLT                                                     
         MVC   PRGKID,0(R1)       MOVE IN SCHEME FROM RECORD                    
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         L     R2,ADPRDGRP                                                      
         ST    R2,AREC                                                          
         GOTO1 GET                                                              
*                                                                               
         LA    R2,PRGEL                                                         
         USING PRGEL01,R2                                                       
CHKPGR15 CLI   0(R2),0            END OF RECORD                                 
         BE    CHKPGRX                                                          
         CLI   0(R2),X'01'                                                      
         BE    CHKPGR20                                                         
         SR    R0,R0                                                            
         ICM   R0,1,1(R2)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         B     CHKPGR15                                                         
CHKPGR20 ZIC   RF,PRGBK1LN                                                      
         ZIC   RE,PRGBK2LN                                                      
         AR    RF,RE               GET LENGTH OF ID                             
         STC   RF,BYTE2            SO WE CAN STORE ID CORRECTLY                 
***                                                                             
         XC    KEY,KEY                                                          
         LA    R2,KEY             READ PRODUCT GROUP RECORD                     
         USING PRGKEY,R2                                                        
         MVC   PRGKTYP,=X'0D01'                                                 
         MVC   PRGKAGMD,BAGYMD                                                  
         MVC   PRGKCLT,BCLT                                                     
         MVC   PRGKID,0(R1)       MOVE IN SCHEME FROM RECORD                    
         MVC   PRGKGRP,1(R1)      MOVE IN PRG CODE FROM RECORD                  
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         L     R2,ADPRDGRP                                                      
         ST    R2,AREC                                                          
         GOTO1 GET                                                              
*                                                                               
         UNPK  DUB(5),PRGKGRP(3)                                                
*        MVC   PGRCD,DUB          SAVE PRODUCT CODE                             
         MVC   PGRCD,PRGKID                                                     
         ZIC   RE,BYTE2                                                         
         BCTR  RE,0                                                             
         EXMVC RE,PGRCD+1,DUB                                                   
         LA    R2,PRGEL                                                         
         USING PRGEL10,R2                                                       
CHKPGR25 CLI   0(R2),0            END OF RECORD                                 
         BE    CHKPGRX                                                          
         CLI   0(R2),X'10'                                                      
         BE    CHKPGR30                                                         
         SR    R0,R0                                                            
         ICM   R0,1,1(R2)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R2,R0                                                            
         B     CHKPGR25                                                         
CHKPGR30 LA    RF,PRGNAM3         USE LOWEST NAME FOUND                         
         CLI   0(RF),C' '                                                       
         BH    CHKPGR40                                                         
         LA    RF,PRGNAM2                                                       
         CLI   0(RF),C' '                                                       
         BH    CHKPGR40                                                         
         LA    RF,PRGNAM1                                                       
CHKPGR40 MVC   PGRNAME,0(RF)      SAVE NAME                                     
CHKPGRX  XIT1                                                                   
         DROP  R2                                                               
         SPACE 2                                                                
*-------------------*                                                           
* LITERAL POOL      *                                                           
*-------------------*                                                           
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*-----------------------------------------------*                               
* SETBLK - SETS UP CALL TO ACPOSTER WITH BASICS *                               
*-----------------------------------------------*                               
         DS    0D                                                               
SETBLK   NMOD1 0,**SBLK**                                                       
         L     RC,0(R1)                                                         
         LA    R6,4095(RC)                                                      
         LA    R6,1(R6)                                                         
         USING SPMXWRKD,RC,R6     LOCAL WORKING STORAGE                         
         LA    R7,4095(RA)                                                      
         LA    R7,1(R7)                                                         
         USING SPWORKD,RA,R7      GLOBAL WORKING STORAGE                        
*                                                                               
         LA    RE,ACPBLK          CLEAR CONTROL BLOCK                           
         LA    RF,L'ACPBLK                                                      
         XCEFL                                                                  
         LA    R2,ACPBLK                                                        
         USING ACPOSTD,R2                                                       
         MVC   ACPACOM,ACOMFACS   A(COMFACS)                                    
         MVC   ACPUTL,UTL         A(UTL)                                        
         MVC   ACPCMPC,ACCTCD     NATIVE COMPANY CODE                           
         MVC   ACPCMPC2,ACCTCD2   OTHER COMPANY CODE                            
         MVC   ACPSE1,ACCTSE      NATIVE FILE SE #                              
         MVC   ACPSE2,ACCTSE2     OTHER SE #                                    
         MVC   ACPSPROD,SVPROD    UNIT,LEDGER                                   
         MVC   ACPMI,SVMI         Y=USE MI RECORDS                              
         MVC   ACPCOST,SVCOST     Y=USE COSTING,BILLING,REVENUE                 
         MVC   ACPALPH,SALPH      AGY ALPHA FOR MEDIA SPLIT FILES               
         MVC   ACPSYS,SYS         SYSTEM                                        
         MVC   ACPMED,MYMED       MEDIA CODE                                    
         MVC   ACPOFC,SVCOFF      CLIENT OFFICE CODE                            
         MVC   ACPOFG,SVOFFG      OFFICE GROUP                                  
         MVC   ACPCLT,CLT         CLIENT CODE                                   
         MVC   ACPPRD,PRD         PRODUCT CODE                                  
* APCTACT IS NOW A 6 BYTE FIELD TO ACCOMODATE PRINT BILLING                     
         MVC   ACPTACT,TRUEAMT    IF AOR BILL- SET SOME TRUE AMTS               
         MVC   ACPTGST,TRUEGST                                                  
         MVC   ACPTGSTI,TRUEGSTI                                                
         MVC   ACPPSTI(ACPMCODE*L'ACPPSTI),PSTIVALS                             
         MVC   ACPSYSBF,=V(SYSBUFF)                                             
         MVC   ACPMEDBF,=V(MEDBUFF)                                             
         MVC   ACPOFGBF,=V(OFGBUFF)                                             
         MVC   ACPOFCBF,=V(OFCBUFF)                                             
         MVC   ACPCLTBF,=V(CLTBUFF)                                             
         MVC   ACPPRDBF,=V(PRDBUFF)                                             
         XIT1                                                                   
         DROP  R2                                                               
         SPACE                                                                  
*-------------------*                                                           
* LITERAL POOL      *                                                           
*-------------------*                                                           
         LTORG                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*----------------------------------*                                            
* SETBLK - SETS UP CALL TO MXPOST  *                                            
*----------------------------------*                                            
         DS    0D                                                               
SETMXBLK NMOD1 0,**SMBLK**                                                      
         L     RC,0(R1)                                                         
         LA    R6,4095(RC)                                                      
         LA    R6,1(R6)                                                         
         USING SPMXWRKD,RC,R6     LOCAL WORKING STORAGE                         
         LA    R7,4095(RA)                                                      
         LA    R7,1(R7)                                                         
         USING SPWORKD,RA,R7      GLOBAL WORKING STORAGE                        
*                                                                               
         L     RE,AMXBLK           CLEAR BLOCK                                  
         LH    RF,=Y(MXPLEN)                                                    
         XCEFL                                                                  
         L     R2,AMXBLK           R2=A(MXPOST BLOCK)                           
         USING MXPOSTD,R2                                                       
         MVC   MXBILL,ADBILL       A(BILL RECORD)                               
         MVC   MXRECUP,RECUP       A(RECUP)                                     
         MVC   MXDTCN,DATCON       A(DATCON)                                    
         MVC   MXMXPROF,MXPROF     V(MXPROF)                                    
*                                                                               
         MVC   MXQOPT1,QOPT1       REQUEST OPTION 1                             
         MVC   MXCNTRY,CNTRY       COUNTRY CODE                                 
         MVC   MXEMU,EMULATE       ACCOUNTING FILE EMULATION                    
         MVC   MXCMP,ACCTCD2       COMP CODE WHERE POSTNGS WILL BE MADE         
         MVC   MXCMP2,ACCTCD       COMPANY CODE OF ORIGINATING FILE             
         MVC   MXNEWOFF,SVNEWOF    MEDIA OFFICE                                 
         MVC   MXSYS,SYS           SYSTEM                                       
         MVC   MXMED,MYMED         MEDIA                                        
         MVC   MXALPHA,QAGY        MEDIA AGENCY                                 
         MVC   MXCLT,CLT           CLIENT                                       
         MVC   MXOCLT,OVRCLT       OVERRIDE CLIENT                              
         MVC   MXPRD,PRD           PRODUCT                                      
         MVC   MXOPRD,OVRPRD       OVERRIDE PRODUCT                             
*                                                                               
         MVC   MXBTYPE,TYPEBIL     BILL TYPE                                    
         MVC   MXDBTYPE,MTYPEBIL   BILL TYPE FOR FFTEL                          
         MVC   MXBTNUM,SVBTNUM     B4, B5, B6, ETC.                             
         MVC   MXFLAG2,SVFLAG      COST2, ETC.                                  
         MVC   MXINV,BINVOICE      BILL INVOICE NUMBER                          
         MVC   MXTINV,TRUEINV      BILL TRUE AOR INVOICE NUMBER                 
         MVC   MXLINV,BINVL        BILL LONG INVOICE NUMBER                     
         MVC   MXTLINV,TRUEINVL    BILL TRUE AOR LONG INVOICE NUMBER            
         MVC   MXPSTN2,PSTNUM2     N'ENTRIES IN MXPSTG                          
         MVC   MXPSTNA,PSTNUMA     N'ENTRIES IN MXPSG2                          
         MVC   MXPREVI,PREVINC     PREVIOUS INCOME FLAG                         
         MVC   MXPREVAI,PREVAINC   PREVIOUS AOR INCOME FLAG                     
         MVC   MXGSTO,GSTO                                                      
         MVC   MXGSTI,TRUEGSTI                                                  
         MVC   MXPSTO(ACPMCODE*L'MXPSTO),PSTOVALS                               
         MVC   MXPSTI(ACPMCODE*L'MXPSTI),PSTIVALS                               
         MVC   MXTODAY,TODAYP      TODAY'S DATE                                 
         MVC   MXREQTDT,REQTDT     TRANSFER REQUEST DATE                        
         MVC   MXRERNDT,RERUNDT    RERUN DATE                                   
         MVC   MXTRANDT,TRANSDT    ADJUSTMENT DATE FROM BILL                    
* MXTAMT IS NOW A 6 BYTE FIELD TO ACCOMODATE PRINT BILLING                      
         MVC   MXTAMT,TRUEAMT      TRUE AOR AMOUNT                              
         MVC   MXSVDESC,SVEDESC    ESTIMATE DESCRIPTION                         
         MVC   MXSVUSR,SVUSR       USER FLAG                                    
         MVC   MXUDESC,SVPDESC1    USER DESCRIPTIONS (4)                        
         MVC   MXUUSER,SVPUSER1    USER DATA (4)                                
         MVC   MXEPONUM,SVEPONUM   ESTIMATE PO NUMBER                           
*                                                                               
         MVC   MXUCMDP,SVUCMDP     PRODUCT UCOM DESCRIPTIONS (4)                
         MVC   MXUCMDE,SVUCMDE     ESTIMATE UCOM DESCRIPTIONS (4)               
         MVC   MXUCMDM,SVUCMDM     MARKET UCOM DESCRIPTIONS (4)                 
*                                                                               
         MVC   MXUCMP,SVUCMP       PRODUCT UCOM FIELDS (4)                      
         MVC   MXUCME,SVUCME       ESTIMATE UCOM FIELDS (4)                     
         MVC   MXUCMM,SVUCMM       MARKET UCOM FIELDS (4)                       
*                                                                               
         MVC   MXUCMPLN,SVUCMPLN   LENGTH OF FOUR PRODUCT UCOM FIELDS           
         MVC   MXUCMELN,SVUCMELN   LENGTH OF FOUR ESTIMATE UCOM FIELDS          
         MVC   MXUCMMLN,SVUCMMLN   LENGTH OF FOUR MARKET UCOM FIELDS            
*                                                                               
         MVC   MXPGRCD(L'PGRCD),PGRCD PRODUCT CODE                              
         MVC   MXPGRNM,PGRNAME     PRODUCT NAME                                 
         MVC   MXSJCLTN,SJCLTNM    SJ CLIENT NAME                               
         MVC   MXSJPRDN,SJPRDNM    SJ PRODUCT NAME                              
         MVC   MXRSYS,SVRSYS       REDEFINED SYSTEM CODE                        
         MVC   MXRMED,SVRMED       REDEFINED MEDIA CODE                         
         MVC   MXFLPRF,SVPROF+6    WB FLIGHT PROFILE SETTING                    
         MVC   MXFLDET,SVBLFLT     WB FLIGHT CODE                               
         MVC   MXCNET,SVCNET       CALCULATED NET                               
         MVC   MXCURR,SVCURR       CURRENCY U/E/G/Y/P                           
         ZAP   MXBNET,SVBNET       MIDAS EXCHANGE FOR NB MEMO                   
         XIT1                                                                   
         SPACE                                                                  
*-------------------*                                                           
* LITERAL POOL      *                                                           
*-------------------*                                                           
         LTORG                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*------------------------------------------------------------*                  
* RDPOST - SETS UP CALL TO ACPOSTER TO READ PM RECS FOR BILL *                  
*------------------------------------------------------------*                  
         DS    0D                                                               
RDPOST   NMOD1 0,**RPST**                                                       
         L     RC,0(R1)                                                         
         LA    R6,4095(RC)                                                      
         LA    R6,1(R6)                                                         
         USING SPMXWRKD,RC,R6     LOCAL WORKING STORAGE                         
         LA    R7,4095(RA)                                                      
         LA    R7,1(R7)                                                         
         USING SPWORKD,RA,R7      GLOBAL WORKING STORAGE                        
*                                                                               
         GOTO1 CLRAREA,DMCB,V(POSTINGS),V(POSTNGSX)                             
         GOTO1 =A(SETBLK),DMCB,(RC) SET UP BASIC ACPOSTER CALL                  
         LA    R2,ACPBLK                                                        
         USING ACPOSTD,R2                                                       
         MVI   ACPTYPE,0             POST MAINT RECORD                          
         MVC   ACPBILL,ADBILL        A(BILL RECORD)                             
         MVC   ACPPOST,=V(IOAREA)    A(POSTINGS ACCS TO RETURN)                 
         MVC   ACPPOST2,=V(POSTINGS) A(POSTINGS ACCS TO RETURN)                 
         MVC   ACPXSORT,XSORT        SET ADDRESS OF XSORT                       
         L     R1,=A(ACCAREA)                                                   
         STCM  R1,15,ACPSVACC     A(SAVED AREA FOR ACPOSTER)                    
         ZIC   RE,PSTNUMA         MAX NUMBER OF POSTING ACCOUNTS                
         STC   RE,ACPNUMA                                                       
         ZIC   RE,PSTNUM2         MAX NUMBER OF POSTINGS                        
         STC   RE,ACPNUM2                                                       
         ICM   RE,15,=AL4(ACCAREAX-ACCAREA)                                     
         STCM  RE,15,ACPNUM3      LENGTH OF SAVED AREA                          
         MVC   ACPGSTO,GSTO       OUTPUT GST                                    
         MVC   ACPPSTO(ACPMCODE*L'ACPPSTO),PSTOVALS                             
         GOTO1 MXPROF,DMCB,MTPFCC,PROFWORK                                      
         MVC   ACPCC,PROFWORK     CC OVERRIDE                                   
         CLI   ACPCC,0                                                          
         BNE   *+8                                                              
         MVI   ACPCC,C'N'                                                       
         GOTO1 MXPROF,DMCB,MTPFSJ,PROFWORK                                      
         MVC   ACPSJPRD,PROFWORK     CHECK FOR SJ PRODUCT RECORD                
         GOTO1 MXPROF,DMCB,MTPFSJ1C,PROFWORK                                    
         MVC   ACP1CPRD,PROFWORK   CHECK FOR PRD LVL 1C                         
         GOTO1 MXPROF,DMCB,MTPFIPCT,PROFWORK                                    
         MVC   ACPIOR,PROFWORK     IOR PERCENTAGE OF GROSS REG BILL             
         GOTO1 MXPROF,DMCB,MTPFPCT,PROFWORK      TRADE PERCENTAGE               
         MVC   ACPTPCT,PROFWORK                                                 
         GOTO1 MXPROF,DMCB,MTPFBAS,PROFWORK      TRADE BASIS                    
         MVC   ACPTBAS,PROFWORK                                                 
         GOTO1 MXPROF,DMCB,MTPFOFF,PROFWORK      TRADE OFFICE                   
         MVC   ACPTOFF,PROFWORK                                                 
*        GOTO1 MXPROF,DMCB,MTPFMOFF,PROFWORK     GROUPM MIDAS TRADE OFF         
*        MVC   ACPTMOFF,PROFWORK                                                
         GOTO1 MXPROF,DMCB,MTPFSPCT,PROFWORK     GROUPM MIDAS SPLIT %           
         MVC   ACPTMPCT,PROFWORK                                                
         CLC   =C'AOR',TYPEBIL     IF AOR BILL                                  
         BNE   RDPOST5                                                          
         GOTO1 MXPROF,DMCB,MTPFIPT2,PROFWORK                                    
         MVC   ACPIOR,PROFWORK     IOR PERCENTAGE OF GROSS AOR BILL             
*                                                                               
RDPOST5  CLC   =C'RET',TYPEBIL    IF RETAIL BILL                                
         BNE   RDPOST10                                                         
         GOTO1 MXPROF,DMCB,MTPF3LDG,PROFWORK                                    
         MVC   ACPLDG,PROFWORK    SET UNIT 3 LEDGER                             
         GOTO1 MXPROF,DMCB,MTPFRCV,PROFWORK                                     
         MVC   ACPRCV,PROFWORK    SET LOOKING FOR RECV = OVERRIDE               
         GOTO1 MXPROF,DMCB,MTPFCST,PROFWORK                                     
         MVC   ACPCST,PROFWORK    SET LOOKING FOR COST = OVERRIDE               
         GOTO1 MXPROF,DMCB,MTPFMGOV,PROFWORK                                    
         MVC   ACPMGROV,PROFWORK    LOOK FOR MARKET GROUP OVERRIDE              
RDPOST10 DS    0H                                                               
         XIT1                                                                   
         DROP  R2                                                               
         EJECT                                                                  
*-------------------*                                                           
* LITERAL POOL      *                                                           
*-------------------*                                                           
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*-------------------------------------*                                         
* POSTCALL - ACTUAL CALL TO ACPOSTER  *                                         
*-------------------------------------*                                         
*                                                                               
         DS    0D                                                               
POSTCALL NMOD1 0,**PCAL**                                                       
         L     RC,0(R1)                                                         
         LA    R6,4095(RC)                                                      
         LA    R6,1(R6)                                                         
         USING SPMXWRKD,RC,R6     LOCAL WORKING STORAGE                         
         LA    R7,4095(RA)                                                      
         LA    R7,1(R7)                                                         
         USING SPWORKD,RA,R7      GLOBAL WORKING STORAGE                        
*                                                                               
         LA    R2,ACPBLK                                                        
         GOTO1 ACPOSTER,DMCB,(R2)                                               
         L     RF,UTL                                                           
         MVC   4(1,RF),SAVSYS     RE-SET TO SPOT SYSTEM NUMBER                  
         XIT1                                                                   
         SPACE                                                                  
*-------------------*                                                           
* LITERAL POOL      *                                                           
*-------------------*                                                           
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*-------------------------------------*                                         
* SETPRT - SETS UP CALL TO MXPRNT     *                                         
*-------------------------------------*                                         
*                                                                               
         DS    0D                                                               
SETPRT   NMOD1 0,**SPRT**                                                       
         L     RC,0(R1)                                                         
         LA    R6,4095(RC)                                                      
         LA    R6,1(R6)                                                         
         USING SPMXWRKD,RC,R6     LOCAL WORKING STORAGE                         
         LA    R7,4095(RA)                                                      
         LA    R7,1(R7)                                                         
         USING SPWORKD,RA,R7      GLOBAL WORKING STORAGE                        
         L     R4,ADBILL                                                        
         USING BILLREC,R4                                                       
*                                                                               
         LA    R2,PRTBLOCK                                                      
         LR    RE,R2                                                            
         LA    RF,PMXSAVE-PBILL   CLEAR OUT REQUEST DETAILS                     
         XCEF                                                                   
*                                                                               
         LA    R2,PRTBLOCK                                                      
         USING MXPRNTD,R2                                                       
         LA    RE,PMXSAVE-PBILL   CLEAR OUT REQUEST DETAILS                     
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         XC    0(0,R2),0(R2)                                                    
         LA    R1,ACPBLK                                                        
         USING ACPOSTD,R1                                                       
         MVC   PBILL,ADBILL       A(BILL)                                       
         MVC   PPOST,=V(POSTINGS) A(POSTINGS)                                   
         MVC   PDTCN,DATCON       A(DATCON)                                     
         MVC   PDMGR,DATAMGR      A(DATAMGR)                                    
         MVC   PCED,CUREDIT       A(CUREDIT)                                    
         MVC   PCLPK,CLPACK       A(CLPACK)                                     
         MVC   PREPORT,REPORT     REPORT                                        
         MVC   PHEXOUT,HEXOUT     A(HEXOUT)                                     
         MVC   PBUFFALO,BUFFALO   A(BUFFALO)                                    
         MVC   POFFICER,AOFFICER  A(OFFICER)                                    
         MVC   PCOMFACS,ACOMFACS  A(COMFACS)                                    
         LA    RE,HEADHOOK                                                      
         ST    RE,PHEAD           A(HEADHOOK)                                   
         LA    RE,P               A(PRINT LINES)                                
         ST    RE,PP1                                                           
         LA    RE,P2                                                            
         ST    RE,PP2                                                           
         LA    RE,P3                                                            
         ST    RE,PP3                                                           
         LA    RE,H1                                                            
         ST    RE,PH1             A(HEADLINES)                                  
         LA    RE,H2                                                            
         ST    RE,PH2                                                           
         LA    RE,H3                                                            
         ST    RE,PH3                                                           
         LA    RE,RCSUBPRG        SAVE A(RCSUBPRG)                              
         ST    RE,PRCSUBP                                                       
         LA    RE,FORCEHED                                                      
         ST    RE,PFORCEH         A(FORCEHED)                                   
         LA    RE,PAGE                                                          
         ST    RE,PPAGE           A(PAGE NUMBER)                                
         LA    RE,SVPROF2                                                       
         ST    RE,PSUMPROF        A(MEDIA MX PROFILE)                           
         MVC   PALPHA,QAGY         AGENCY ALPHA                                 
         MVC   PAGYMD,BAGYMD      BINARY AGENCY/MEDIA                           
         MVC   PSYS,SYS           SYSTEM                                        
         MVC   PMED,MYMED         MEDIA                                         
         MVC   PMEDOFF,SVCOFF     MEDIA OFFICE                                  
         MVC   PCLT,CLT           CLIENT                                        
         MVC   PPRD,PRD           PRODUCT                                       
         MVC   PCOMPANY,ACCTCD2   OTHER COMPANY CODE (IF APPL)                  
         MVC   PCOUNTRY,CNTRY     COUNTRY CODE                                  
         MVC   PSVPROD,SVPROD     UNIT/LEDGER                                   
         MVC   PACCOFF,SVACOFF    ACC OFFICE                                    
         MVC   PACCOF2,SVACOF2    ACC OFFICE OVERRIDE FOR INTERNAL              
         MVC   PMOA,TRANSDT       MOA DATE(CALC FROM BQDATE)                    
         MVC   PINVOICE(L'BINVOICE),BINVOICE                                    
         CLC   BINVL,SPACES                                                     
         BNH   *+10                                                             
         MVC   PINVOICE,BINVL     USER LONGER INV # IF THERE                    
         OC    PINVOICE,SPACES                                                  
         MVC   PSVMPRD,SVMPRD      MASTER PRODUCT CODE                          
         MVC   PAOR,=C'NNNN'      NOT AOR MAIN BILL                             
         MVI   PAORMSG,C'Y'                                                     
         CLC   =C'AOR',TYPEBIL                                                  
         BNE   SETPRT30                                                         
         MVC   PAORMSG,PSTAOR                                                   
         TM    BILSTAT,BSTCAORQ   MUST BE MAIN BILL                             
         BNO   SETPRT30                                                         
         TM    BILSTAT,BSTSADJQ                                                 
         BO    SETPRT30                                                         
         TM    BILSTAT,BSTSNETQ                                                 
         BO    SETPRT30                                                         
         MVC   PAOR,TRUEAMT       TRUE AOR AMOUNT                               
         MVC   PTINV(L'TRUEINV),TRUEINV  TRUE AOR INVOICE NUMBER                
         CLC   TRUEINVL,SPACES                                                  
         BNH   *+10                                                             
         MVC   PTINV,TRUEINVL    TRUE AOR LONGER INVOICE NUMBER                 
         OC    PTINV,SPACES                                                     
SETPRT30 MVC   PPOSTNUM,PSTNUM2   # OF POSTINGS                                 
         MVC   PPOSTERR,ACPERR    ERRORS FOUND WITH POSTINGS                    
         MVC   PPOSTERC,ACPERRC   ERRORS FOUND W/POSTINGS CONTINUED             
         MVC   PPOSTER2,CONERR    ERRORS FOUND WITH CONTRA ACCOUNTS             
         MVC   PPOSTER3,MXERR      ERRORS FOUND FROM MX PROGRAM                 
         XIT1                                                                   
         DROP  R1,R2,R4                                                         
         SPACE                                                                  
*-------------------*                                                           
* LITERAL POOL      *                                                           
*-------------------*                                                           
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                                                               
                                                                                
*----------------------------------------------------------------------         
* SMFOX - OUPUT SMF RECORD FOR CROSSFILE ACTIVITY                               
*----------------------------------------------------------------------         
SMFOX    NTR1  BASE=*,LABEL=*                                                   
         USING CROSD,SMFREC                                                     
*                                                                               
         L     R1,VMASTC                                                        
         USING MASTD,R1                                                         
         XC    SMFREC,SMFREC                                                    
         MVC   CROSLN,=AL2(CROSLNQ)  RECORD LENGTH                              
         MVI   CROSTYP,CROSTYPQ      RECORD TYPE           X                    
         MVI   CROSSUB,CROSSUB1      SUB RECORD TYPE       1                    
         L     R2,MCSSB-MCBLOCK(R1)                                             
         MVC   CROSDSP,SSODSPAC-SSOOFF(R2)                                      
         MVI   CROSYSN,X'02'         SYSTEM                X                    
         CLI   SYS,C'S'                                                         
         BE    *+8                                                              
         MVI   CROSYSN,X'03'         SYSTEM                X                    
         MVC   CROSYSA,SYS           SYSTEM                C                    
         MVI   CROSPGM+0,C'M'        PROGRAM               M                    
         MVI   CROSPGM+1,C'X'        PROGRAM               X                    
         MVC   CROSUID,MCORIGID      COMPANY ID            OO                   
         MVC   CROSPID,MCRHPSWD      PERSON ID             PP                   
         MVC   CROSSAGY,MCRHSAGP     SECURITY AGENCY                            
         MVC   CROSAAGY,ACCALPH      ACCOUNTING AGENCY     AA                   
         MVC   CROSMAGY,RCAGENCY     MEDIA AGENCY          AA                   
         MVC   CROSASE,ACCTSE2       ACCOUNTING SE NUMBER  N                    
         MVC   CROSMSE,SAVSYS        MEDIA SE NUMBER       N                    
         MVC   CROSOFF,SVCOFF        OFFICE                OO                   
         MVC   CROSMED,MED           MEDIA CODE            M                    
         MVC   CROSCLI,CLT           CLIENT CODE           CCC                  
         MVC   CROSPRD,PRD           PRODUCT CODE          PPP                  
         DROP  R1                                                               
*                                                                               
         L     R2,ACOMFACS                                                      
         ICM   R2,15,CSMFOUT-COMFACSD(R2)                                       
         BZ    SMFOXX                                                           
         GOTO1 (R2),DMCB,13,SMFREC                                              
SMFOXX   J     XIT                                                              
SMFREC   DS    XL(CROSLNQ)                                                      
         LTORG                                                                  
                                                                                
*--------------------------------------------*                                  
* SETTYPE - SETS BILL TYPE - ORDER IMPORTANT *                                  
*--------------------------------------------*                                  
*                                                                               
         USING BILLRECD,R5                                                      
SETTYPE  NTR1  BASE=*,LABEL=*                                                   
         MVC   STYPEBIL,TYPEBIL   SAVE PREVIOUS TYPEBIL                         
         MVC   SMTYPBIL,MTYPEBIL                                                
         MVC   TYPEBIL,=C'RET'    RETAIL BILL                                   
         CLI   BRETAIL,0                                                        
         BNE   SETMTYPE                                                         
         MVC   TYPEBIL,=C'BPC'    REGIONAL/LOCAL BILLING                        
         TM    BILSTAT3,BSTREGQ+BSTLMGQ   LMG/REGNL BILLING?                    
         BNZ   SETMTYPE                                                         
         MVC   TYPEBIL,=C'AOR'    AOR BILL                                      
         TM    BILSTAT,X'10'      CLIENT                                        
         BO    SETMTYPE                                                         
         TM    BILSTAT,X'20'      TRUE                                          
         BO    SETMTYPE                                                         
         MVC   TYPEBIL,=C'REG'    REGULAR BILL FOR NOW                          
                                                                                
SETMTYPE TM    BILSTAT3,BSTREGQ    REGIONAL BILLING?                            
         BZ    *+14                                                             
         MVC   MTYPEBIL,=CL5'BPCR'                                              
         B     SETMTYX                                                          
         TM    BILSTAT3,BSTLMGQ    LMG BILLING?                                 
         BZ    *+14                                                             
         MVC   MTYPEBIL,=CL5'BPCL'                                              
         B     SETMTYX                                                          
*                                                                               
         CLI   BRETAIL,0           SET TYPE FOR MXPOST                          
         BE    SETMTY02                                                         
         MVC   MTYPEBIL,=CL5'RET' RETAIL                                        
         CLI   BRETAIL,X'41'                                                    
         BNE   *+12                                                             
         MVI   MTYPEBIL+3,C'S'    CORP SUMMARY                                  
         B     SETMTYX                                                          
                                                                                
         TM    BRETAIL,X'01'                                                    
         BNO   *+12                                                             
         MVI   MTYPEBIL+3,C'C'    CORP RETAIL(CORP OUTLET,CORP CONTRL)          
         B     SETMTYX                                                          
                                                                                
         TM    BRETAIL,X'02'                                                    
         BNO   SETMTYX                                                          
         MVI   MTYPEBIL+3,C'P'    PARTICIPANT RETAIL                            
         B     SETMTYX                                                          
*                                                                               
SETMTY02 TM    BILSTAT,X'20'                                                    
         BNO   SETMT02A                                                         
         MVC   MTYPEBIL,=CL5'AORA' TRUE AOR                                     
         TM    BILSTAT3,BSTTRCNQ                                                
         BNO   *+10                                                             
         MVC   MTYPEBIL,=CL5'AORXA' TRUE AOR WITH TRADE                         
         B     SETMTYX                                                          
                                                                                
SETMT02A TM    BILSTAT,X'10'                                                    
         BNO   SETMTY04                                                         
         MVC   MTYPEBIL,=CL5'AORM' CLIENT AOR MAIN ONE                          
         TM    BILSTAT3,BSTTRCNQ                                                
         BNO   *+10                                                             
         MVC   MTYPEBIL,=CL5'AORXM' CLIENT AOR MAIN ONE WITH TRADE              
         TM    BILSTAT,X'80'                                                    
         BNO   SETMT02B                                                         
         MVC   MTYPEBIL,=CL5'AORJ' CLIENT COMMISSION ADJUSTMENT                 
         TM    BILSTAT3,BSTTRCNQ                                                
         BNO   *+10                                                             
         MVC   MTYPEBIL,=CL5'AORXJ' CLIENT COMMISSION ADJ WITH TRADE            
         B     SETMTYX                                                          
                                                                                
SETMT02B TM    BILSTAT,BSTSCOMQ                                                 
         BNO   *+14                                                             
         MVC   MTYPEBIL,=CL5'AORU' CLIENT UPFRONT COMM                          
         B     SETMTYX                                                          
         TM    BILSTAT,BSTSNETQ                                                 
         BNO   SETMTYX                                                          
         MVC   MTYPEBIL,=CL5'AORN' CLIENT UPFRONT NET                           
         B     SETMTYX                                                          
*                                                                               
SETMTY04 TM    BILSTAT,X'02'                                                    
         BNO   *+14                                                             
         MVC   MTYPEBIL,=CL5'COM' COMMISSION ONLY BILL                          
         B     SETMTYX                                                          
*                                                                               
         TM    BILSTAT,X'80'                                                    
         BNO   SETMTY06                                                         
         MVC   MTYPEBIL,=CL5'ADJ' COMMISSION ADJUSTMENT                         
         TM    BILSTAT3,BSTTRCNQ                                                
         BNO   *+10                                                             
         MVC   MTYPEBIL,=CL5'ADJX' COMMISSION ADJUSTMENT WITH TRADE             
         B     SETMTYX                                                          
*                                                                               
SETMTY06 TM    BILSTAT2,BSTAMAQ                                                 
         BNO   *+14                                                             
         MVC   MTYPEBIL,=CL5'DIFF' ACTUAL-MINUS-ASSIGNED                        
         B     SETMTYX                                                          
*                                                                               
         TM    BILSTAT,BSTSCOMQ                                                 
         BNO   *+14                                                             
         MVC   MTYPEBIL,=CL5'UFC' REGULAR UPFRONT COMM                          
         B     SETMTYX                                                          
*                                                                               
         TM    BILSTAT,BSTSNETQ                                                 
         BNO   *+14                                                             
         MVC   MTYPEBIL,=CL5'UFN' REG UPFRONT NET                               
         B     SETMTYX                                                          
*                                                                               
         TM    BILSTAT,BSTMANQ                                                  
         BNO   *+14                                                             
         MVC   MTYPEBIL,=CL5'MAN' MANUAL BILL                                   
         B     SETMTYX                                                          
*                                                                               
         MVC   MTYPEBIL,=CL5'REG' REGULAR                                       
         TM    BILSTAT3,BSTTRCNQ                                                
         BNO   *+10                                                             
         MVC   MTYPEBIL,=CL5'REGX' REGULAR WITH TRADE                           
         TM    BILSTAT3,BSTMBARQ                                                
         BNO   *+10                                                             
         MVC   MTYPEBIL,=CL5'REGM' GROUP M TRADE MIDAS                          
                                                                                
SETMTYX  XIT1                                                                   
         DROP  R5                                                               
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*--------------------------------------------------*                            
* GETRUE-  FIND TRUE AOR BILL & SAVE INFO          *                            
*--------------------------------------------------*                            
         USING SPMXWRKD,RC,R6     LOCAL WORKING STORAGE                         
         USING SPWORKD,RA,R7      GLOBAL WORKING STORAGE                        
         USING BILLRECD,R2                                                      
GETRUE   NTR1  BASE=*,LABEL=*                                                   
         L     R2,0(,R1)                                                        
         TM    BILSTAT,BSTCAORQ   MUST BE GIVEN MAIN CLIENT BILL                
         BO    *+6                                                              
         DC    H'0'                                                             
                                                                                
         TM    BILSTAT,BSTSNETQ                                                 
         BO    GETRUEX                                                          
         LHI   R3,0                                                             
         MVI   FNDTRUE,C'N'                                                     
         XC    KEY,KEY            RE-ESTABLISH READ SEQUENCE                    
         MVC   KEY(L'BKEY),0(R2)                                                
         LA    RE,DMREAD                                                        
GETRUE02 ST    RE,DMCB                                                          
         CLI   QOPT4,C'U'        IF REVERSAL TO UNPOST                          
         BNE   *+8                                                              
         MVI   DMCB,X'08'        PASS BACK DELETED RECORDS                      
         GOTO1 DATAMGR,DMCB,,SPTDIR,KEY,KEY                                     
         CLC   KEY(BKEYINV-BKEY),0(R2)                                          
         BNE   GETRUE30                                                         
                                                                                
         L     R2,AMYAREA                                                       
         GOTO1 DATAMGR,DMCB,GETREC,SPTFILE,KEY+14,(R2),DMWORK                   
         CLC   RUNDT,BDATE         NO - LOOKING FOR TRUE AOR BILL               
         BNE   GETRUE30                                                         
         TM    BILSTAT,BSTCAORQ    FIND AORM                                    
         BZ    GETRUE05            NOT THIS ONE                                 
         TM    BILSTAT,BSTSADJQ+BSTSCOMQ+BSTSNETQ                               
         BNZ   GETRUE05            NOT AORM THEN                                
         CLI   FNDTRUE,C'Y'        PROCESSED ONE SET ALREADY ?                  
         BE    GETRUE32            YES SO STOP HERE FOR NOW                     
                                                                                
GETRUE05 TM    BILSTAT,BSTTAORQ    BILL MUST BE TRUE AOR                        
         BZ    GETRUE10                                                         
         GOTOR SVTRUE,DMCB,AMYAREA                                              
         MVI   FNDTRUE,C'Y'                                                     
         AHI   R3,1                COUNT # OF BILLS                             
         LA    RE,DMRSEQ           READ NEXT                                    
         B     GETRUE02                                                         
*                                                                               
GETRUE10 TM    BILSTAT,BSTCAORQ    AOR BILL, AT LEAST?                          
         BO    GETRUE12            NO -END OF AOR SET                           
         TM    BILSTAT,BSTSADJQ                                                 
         BZ    GETRUE30                                                         
GETRUE12 AHI   R3,1                                                             
         LA    RE,DMRSEQ           READ NEXT                                    
         B     GETRUE02                                                         
*                                                                               
GETRUE30 CLI   FNDTRUE,C'Y'        IF END OF SET THEN MUST OF FOUND             
         BE    *+6                                                              
         DC    H'00'                                                            
                                                                                
GETRUE32 CHI   R3,4                NO MORE THAN 4                               
         BNH   *+6                                                              
         DC    H'00'                                                            
         STC   R3,NUMABIL                                                       
                                                                                
GETRUEX  XIT1                                                                   
         SPACE                                                                  
*-------------------*                                                           
* LITERAL POOL      *                                                           
*-------------------*                                                           
         LTORG                                                                  
         SPACE                                                                  
         DROP  R2,R6,R7,RA,RB,RC                                                
         EJECT                                                                  
*--------------------------------------------------*                            
* SVTRUE - SAVE SOME INFO FROM TRUE AOR BILL       *                            
*--------------------------------------------------*                            
*                                                                               
         DS    0D                                                               
         USING SPMXWRKD,RC,R6     LOCAL  WORKING STORAGE                        
         USING SPWORKD,RA,R7      GLOBAL WORKING STORAGE                        
         USING BILLRECD,R2                                                      
SVTRUE   NTR1  BASE=*,LABEL=*                                                   
         L     R2,0(,R1)                                                        
         TM    BILSTAT,X'20'      TRUE AOR BILL                                 
         BO    *+6                                                              
         DC    H'0'                                                             
         ZAP   TRUEAMT,BACTP      SAVE APPROPRIATE VALUES                       
         MVC   TEMPDATE,BDATE     GET 'REAL' INVOICE NUMBER                     
         MVC   TEMPINV,BINVNO                                                   
         XC    TRUEINVL,TRUEINVL                                                
         ST    R2,SVADDR2                                                       
         SR    RF,RF                                                            
         CLC   BINVMED,SPACES     ONLY STORE LONG INVOICE IF THE RULES          
         BNH   *+8                ARE IN THE BILL HEADER                        
         LA    RF,TRUEINVL                                                      
         GOTO1 =A(SETINV),DMCB,(RC),TRUEINV,(RF)                                
         XC    TRUEGST,TRUEGST                                                  
         MVI   TRUEGSTI,C' '                                                    
         CLC   BLEN,=H'90'                                                      
         BE    SETRUE5                                                          
         CLI   BVATCOD,0                                                        
         BE    SETRUE5                                                          
         MVC   TRUEGST,BVATAMT                                                  
         MVC   TRUEGSTI,BVATCOD                                                 
*                                                                               
SETRUE5  LA    RE,L'PSTIVALS      CLEAR PST INPUT INFORMATION                   
         MH    RE,=Y(ACPMCODE)                                                  
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         XC    PSTIVALS(0),PSTIVALS                                             
*                                                                               
         LA    RF,PSTIVALS         RF=A(INPUT PST TABLE)                        
         CLC   BLEN,=H'130'        IF NEW BILL LENGTH                           
         BNH   SVTRUEX                                                          
         SR    R0,R0               IF ANY PROVINCIAL VAT ELEMENTS               
         ICM   R0,1,BILNPVTS                                                    
         BZ    SVTRUEX                                                          
         LA    R1,BILPVELD         R1=A(FIRST ELEMENT)                          
         USING BILPVELD,R1                                                      
*                                                                               
SVTRUE8  ZIC   RE,BILPVPRV         PROVINCE NUMBER                              
         BCTR  RE,0                                                             
         MH    RE,=AL2(PRVTABLN)                                                
         L     R2,APRVTAB                                                       
         AR    RE,R2                                                            
         USING PRVTABD,RE                                                       
         MVC   0(2,RF),PRVTCODE    SAVE PROVINCE CODE                           
         MVC   2(1,RF),PRVTIN      SAVE ELEMENT EQUATED ACCOUNT                 
         MVC   3(1,RF),BILPVCOD    SAVE TAX TYPE                                
         MVC   4(4,RF),BILPVBAS    SAVE BASIS AMOUNT                            
         MVC   8(4,RF),BILPVAMT    SAVE TAX AMOUNT                              
         LA    RF,L'PSTIVALS(RF)   BUMP TO NEXT TABLE ENTRY                     
         LA    R1,BILPVLEN(R1)     BUMP TO NEXT ELEMENT IN RECORD               
         BCT   R0,SVTRUE8                                                       
*                                                                               
SVTRUEX  XIT1                                                                   
         DROP  R1,R2                                                            
         SPACE                                                                  
*-------------------*                                                           
* LITERAL POOL      *                                                           
*-------------------*                                                           
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*------------------------------------------------------------*                  
* SETINV - CALLS SPFMTINO TO SAVE 6 BYTE INVOICE NUMBER AND  *                  
*          LONGER INVOICE NUMBER                             *                  
*     XIT- SETS 6 CHAR INVOICE NUMBER IN BINVOICE OR TRUEINV *                  
*          SETS LONGER INVOICE NUMBER IN BINVL OR TRUEINVL   *                  
*          IF THE BILL HEADER IS STORING THE RULES           *                  
*------------------------------------------------------------*                  
*                                                                               
         DS    0D                                                               
SETINV   NMOD1 0,**SINV**                                                       
         L     RC,0(R1)                                                         
         LA    R6,4095(RC)                                                      
         LA    R6,1(R6)                                                         
         USING SPMXWRKD,RC,R6     LOCAL WORKING STORAGE                         
         LA    R7,4095(RA)                                                      
         LA    R7,1(R7)                                                         
         USING SPWORKD,RA,R7      GLOBAL WORKING STORAGE                        
*                                                                               
         LM    R2,R3,4(R1)                                                      
         GOTO1 =V(SPFMTINO),DMCB,(C'B',0),(2,TEMPINV),(MYMED,B1PROF),  +        
               B1XPROF,SVADDR2                                                  
         L     RF,DMCB+8                                                        
         MVC   0(2,R2),0(RF)                                                    
         L     RF,DMCB+4                                                        
         MVC   2(4,R2),3(RF)                                                    
         LTR   R3,R3                 WAN'T LONGER INVOICE NUMBER?               
         BZ    SETINVX                                                          
         L     RF,DMCB                                                          
         MVC   0(L'BINVL,R3),0(RF)   SAVE LONG INVOICE NUMBER                   
*                                                                               
* THIS IS OLD CODE FOR REVERSALS FOR BILLS EARLIER THAN 8/1/95                  
*&&DO                                                                           
*                                                                               
         CLI   REVSTAT,C'Y'        IF REVERSE IN PROGRESS (MY/UNPOST)           
         BNE   SETINV8                                                          
         CLC   REVDATE,=X'BF01'    AND ORIG. TRANS DT LESS THAN 8/1/95          
         BNL   SETINV8                                                          
         CLI   B1XPROF+4,0         THEN KEEP LOOKING AT PROFILE                 
         BE    SETINV8                                                          
         GOTO1 DATCON,DMCB,(0,TEMPDATE),(1,DUB)                                 
         ZIC   R0,DUB              YEAR OF BILL                                 
         ZIC   RF,B1XPROF+4                                                     
         SR    R0,RF                                                            
         BNP   SETINV8                                                          
         MHI   R0,12                                                            
         ZIC   RF,DUB+1            MONTH OF BILL                                
         AR    R0,RF                                                            
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK(2),DUB        SET NEW INVOICE MONTH                         
*                                                                               
SETINV8  MVC   0(L'TEMPINV,R2),WORK                                             
*&&                                                                             
SETINVX  XIT1                                                                   
         SPACE                                                                  
*-------------------*                                                           
* LITERAL POOL      *                                                           
*-------------------*                                                           
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*-------------------------------------------*                                   
* SETVATO- SET OUTPUT GST AND PST CODES     *                                   
*-------------------------------------------*                                   
*                                                                               
         DS    0D                                                               
SETVATO  NMOD1 0,**VATO**                                                       
         L     RC,0(R1)                                                         
         LA    R6,4095(RC)                                                      
         LA    R6,1(R6)                                                         
         USING SPMXWRKD,RC,R6     LOCAL WORKING STORAGE                         
         LA    R7,4095(RA)                                                      
         LA    R7,1(R7)                                                         
         USING SPWORKD,RA,R7      GLOBAL WORKING STORAGE                        
*                                                                               
         L     R5,4(R1)           A(BILL RECORD)                                
         USING BILLRECD,R5                                                      
*                                                                               
         MVI   GSTO,C' '          SET OUTPUT GST CODE                           
         CLC   BLEN,=H'90'                                                      
         BNH   SETVO15                                                          
         CLI   BVATCOD,0                                                        
         BE    SETVO15                                                          
         MVC   GSTO,BVATCOD                                                     
*                                                                               
SETVO15  LA    RE,L'PSTOVALS      CLEAR PST INFO                                
         MH    RE,=Y(ACPMCODE)                                                  
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         XC    PSTOVALS(0),PSTOVALS                                             
*                                                                               
         LA    RF,PSTOVALS         RF=A(OUPUT PST TABLE)                        
         CLC   BLEN,=H'130'        IF NEW BILL LENGTH                           
         BNH   SETVOX                                                           
         SR    R0,R0               IF ANY PROVINCIAL VAT ELEMENTS               
         ICM   R0,1,BILNPVTS                                                    
         BZ    SETVOX                                                           
         LA    R1,BILPVELD         R1=A(FIRST ELEMENT)                          
         USING BILPVELD,R1                                                      
*                                                                               
SETVO20  ZIC   RE,BILPVPRV         PROVINCE NUMBER                              
         BCTR  RE,0                                                             
         MH    RE,=AL2(PRVTABLN)                                                
         L     R2,APRVTAB                                                       
         AR    RE,R2                                                            
         USING PRVTABD,RE                                                       
         MVC   0(2,RF),PRVTCODE    SAVE PROVINCE CODE                           
         MVC   2(1,RF),PRVTOUT     SAVE ELEMENT EQUATED ACCOUNT                 
         MVC   3(1,RF),BILPVCOD    SAVE TAX TYPE                                
         MVC   4(4,RF),BILPVBAS    SAVE BASIS AMOUNT                            
         MVC   8(4,RF),BILPVAMT    SAVE TAX AMOUNT                              
         LA    RF,L'PSTOVALS(RF)   BUMP TO NEXT TABLE ENTRY                     
         LA    R1,BILPVLEN(R1)     BUMP TO NEXT ELEMENT IN RECORD               
         BCT   R0,SETVO20                                                       
*                                                                               
SETVOX   XIT1                                                                   
         DROP  R1,RE,R5                                                         
         SPACE                                                                  
*-------------------*                                                           
* LITERAL POOL      *                                                           
*-------------------*                                                           
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*------------------------------------------*                                    
* GETACC -  GIVEN ROW NUMBER WILL PLUCK    *                                    
*           ACPACC FROM THAT ROW AND SET   *                                    
*           IT INTO CONTRA                 *                                    
*  NTRY - ROW NUMBER - & APOST MUST BE SET *                                    
*  XIT  - SET CONTRA,CONLVL & CC CODE      *                                    
*------------------------------------------*                                    
*                                                                               
         DS    0D                                                               
GETACC   NMOD1 0,**GACC**                                                       
         L     RC,0(R1)                                                         
         LA    R6,4095(RC)                                                      
         LA    R6,1(R6)                                                         
         USING SPMXWRKD,RC,R6     LOCAL WORKING STORAGE                         
         LA    R7,4095(RA)                                                      
         LA    R7,1(R7)                                                         
         USING SPWORKD,RA,R7      GLOBAL WORKING STORAGE                        
*                                                                               
         MVC   BYTE,7(R1)                                                       
         ZIC   RE,PSTNUM2         NUMBER OF POSTING                             
         L     R1,APOST           POSTINGS RETURNED FROM ACPOSTER               
         USING ACPRTND,R1                                                       
GETACC5  CLC   ACPBTYP,BYTE       FIND CORRECT ROW                              
         BE    GETACC10                                                         
         LA    R1,ACPRTNL2(R1)                                                  
         BCT   RE,GETACC5                                                       
         LTR   RB,RB                                                            
         B     GETACCX            ROW NOT FOUND                                 
*                                                                               
GETACC10 MVC   CONLVL(L'ACPLVL),ACPLVL     ACCOUNT LEVEL                        
         MVC   CONTRA(1),ACCTCD2           OTHER COMPANY CODE(IF APPL)          
         MVC   CONTRA+1(14),ACPACC         CONTRA ACCOUNT                       
         OC    CONTRA+1(14),SPACES                                              
         MVC   CONTRAN,ACPACCN             SET CONTRA ACCOUNT NAME              
         CR    RB,RB                                                            
GETACCX  XIT1                                                                   
         DROP  R1                                                               
         SPACE                                                                  
*-------------------*                                                           
* LITERAL POOL      *                                                           
*-------------------*                                                           
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*------------------------------------------*                                    
* PUTSORT - PUT RECORDS TO SORTER          *                                    
*------------------------------------------*                                    
*                                                                               
         DS    0D                                                               
PUTSORT  NMOD1 0,**PSRT**                                                       
         L     RC,0(R1)                                                         
         LA    R6,4095(RC)                                                      
         LA    R6,1(R6)                                                         
         USING SPMXWRKD,RC,R6     LOCAL WORKING STORAGE                         
         LA    R7,4095(RA)                                                      
         LA    R7,1(R7)                                                         
         USING SPWORKD,RA,R7      GLOBAL WORKING STORAGE                        
*                                                                               
         L     R3,4(R1)           WORKER FILE RECORD                            
         LH    R1,0(R3)           LENGTH OF RECORD                              
         SHI   R3,SRTKEYLQ        POINT TO SORTER KEY                           
         USING SRTKEYD,R3                                                       
         AHI   R1,SRTKEYLQ        DATA LENGTH + SORTER KEY LENGTH               
         STH   R1,SRTLEN          SORTER RECORD LENGTH                          
         MVC   SRTID,ACCALPH      ALPHA ID FOR WORKER FILE                      
         MVC   SRTOID,ACCTOID2    NUMERIC ID FROM OTHER WORKER FILE             
         CLI   DETL,YES           DETAIL RECORD?                                
         BNE   PUTSORT4                                                         
         MVC   SRTID,RCAGENCY                                                   
         MVC   SRTOID,ACCTOID1     NUMERIC ID FROM NATIVE WORKER FILE           
         OC    SVPROF2(2),SVPROF2                                               
         BZ    *+10                                                             
         MVC   SRTID,SVPROF2                                                    
PUTSORT4 MVC   SRTINV,BINVOICE    BILL INVOICE NUMBER                           
         CLI   SEQONE,YES         REVERSAL RECORD?                              
         BE    *+8                                                              
         MVI   SRTSEQ,SRTSBOT     FORCE REGULAR POSTINGS RECS FIRST             
         TM    UPSI,UPSIPUTS                                                    
         BZ    PUTSORT5                                                         
         LA    R0,SRTKEYLQ+L'ACTKEY                                             
         GOTOR =V(PRNTBL),DMCB,=C'PUT',(R3),C'DUMP',(R0),=C'1D'                 
PUTSORT5 GOTO1 =V(SORTER),DMCB,=C'PUT',(R3)                                     
         XIT1                                                                   
         DROP  R3                                                               
         SPACE                                                                  
*=============*                                                                 
* LITERAL POOL*                                                                 
*=============*                                                                 
*                                                                               
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                 DROP PREVIOUS BASE REGS                       
         SPACE                                                                  
         EJECT                                                                  
*------------------------------------------*                                    
* READACC - READS A RECORD ON THE ACCOUNT  *                                    
*           FILE AND SETS CONDITION CODE   *                                    
*------------------------------------------*                                    
*                                                                               
         DS    0D                                                               
READACC  NMOD1 0,**RDACC*                                                       
         L     RC,0(R1)                                                         
         LA    R6,4095(RC)                                                      
         LA    R6,1(R6)                                                         
         USING SPMXWRKD,RC,R6     LOCAL WORKING STORAGE                         
         LA    R7,4095(RA)                                                      
         LA    R7,1(R7)                                                         
         USING SPWORKD,RA,R7      GLOBAL WORKING STORAGE                        
*                                                                               
         L     RF,4(R1)                                                         
         MVC   COMMAND,0(RF)                                                    
*                                                                               
         XC    DMCB(24),DMCB                                                    
         L     RF,UTL             SET ACCOUNTING SE NUMBER                      
         MVC   4(1,RF),ACCTSE                                                   
         CLI   RDSE,2             READ FROM OTHER ACC FILE(IF APPL)             
         BNE   *+10                                                             
         MVC   4(1,RF),ACCTSE2                                                  
         GOTO1 DATAMGR,DMCB,COMMAND,=C'ACCOUNT',AKEY,AREC                       
         L     RF,UTL             MAKE SURE ACCOUNTING NUMBER                   
         MVC   4(1,RF),SAVSYS     RE-SET TO SPOT SYSTEM NUMBER                  
         L     RE,AREC                                                          
         USING ACCRECD,RE                                                       
         SR    R1,R1                                                            
         ICM   R1,3,ACCRLEN                                                     
         AR    RE,R1                                                            
         MVI   0(RE),0            PHYSICALLY MARK END OF RECORD                 
*                                                                               
         CLI   DMCB+8,0           SET CC CODE                                   
READAX   XIT1                                                                   
         DROP  RE                                                               
         SPACE                                                                  
*=============*                                                                 
* LITERAL POOL*                                                                 
*=============*                                                                 
*                                                                               
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                 DROP PREVIOUS BASE REGS                       
         SPACE                                                                  
         EJECT                                                                  
*              ROUTINE TO PROCESS AOR BILLS                                     
*                                                                               
         DS    0D                                                               
DOAOR    NMOD1 0,**DAOR**                                                       
         L     RC,0(R1)                                                         
         LA    R6,4095(RC)                                                      
         LA    R6,1(R6)                                                         
         USING SPMXWRKD,RC,R6     LOCAL WORKING STORAGE                         
         LA    R7,4095(RA)                                                      
         LA    R7,1(R7)                                                         
         USING SPWORKD,RA,R7      GLOBAL WORKING STORAGE                        
*                                                                               
         L     RE,ADBILL                                                        
         USING BILLREC,RE                                                       
         TM    BILSTAT,BSTSNETQ                                                 
         BNO   DOAOR10                                                          
         MVI   NUMABIL,1                                                        
         DROP  RE                                                               
         GOTO1 CLRAREA,DMCB,V(MYAREA3),V(MYAREA3X)                              
         B     DOAOR20                                                          
*                                                                               
DOAOR10  MVC   FULL,ADBILL                                                      
         BAS   RE,GETAOR          SET UP DEFAULT ACCOUNTS                       
         BAS   RE,GETST           GET TRUE AOR POSTINGS                         
*                                 RESET FOR MAIN BILL POSTINGS                  
DOAOR20  GOTO1 CLRAREA,DMCB,V(IOAREA),V(IOAREAX)                                
         MVC   FULL,ADBILL                                                      
         BAS   RE,GETAOR                                                        
         LA    R2,ACPBLK                                                        
         USING ACPOSTD,R2                                                       
         GOTO1 =A(RDPOST),DMCB,(RC)                                             
         MVI   ACPAFLG,C'M'       WANT MAIN POSTINGS                            
         GOTO1 =A(POSTCALL),DMCB,(RC)                                           
         LA    R1,AORERRS                                                       
         USING AORERD,R1                                                        
         MVC   AORERR,ACPERR                                                    
         MVC   AORERRC,ACPERRC                                                  
         DROP  R2,R1                                                            
         L     R3,=V(POSTINGS)    LOOK AT POSTINGS RETURNED                     
         USING ACPRTND,R3                                                       
         GOTO1 CLRAREA,DMCB,V(AORBIL1),V(AORBIL1X)                              
         L     R2,AAORBIL1                                                      
         ZIC   R0,PSTNUM2                                                       
DOAOR25  OC    0(ACPRTNL2,R3),0(R3)                                             
         BZ    DOAOR30                                                          
         MVC   0(ACPRTNL2,R2),0(R3)                                             
         LA    R2,ACPRTNL2(R2)                                                  
DOAOR30  LA    R3,ACPRTNL2(R3)                                                  
         BCT   R0,DOAOR25                                                       
*                                                                               
         L     R3,AMYAREA3        MOVE IN TRUE POSTINGS AFTER MAIN              
DOAOR35  OC    0(ACPRTNL2,R3),0(R3)                                             
         BZ    DOAOR40                                                          
         MVC   0(ACPRTNL2,R2),0(R3)                                             
         LA    R2,ACPRTNL2(R2)                                                  
         LA    R3,ACPRTNL2(R3)                                                  
         B     DOAOR35            NOW AORBIL1 HAS BILL1 + AOR BILL PSTS         
         DROP  R3                                                               
*                                                                               
DOAOR40  LA    R3,AORERRS                                                       
         USING AORERD,R3                                                        
         GOTO1 =A(CONTRAS),DMCB,(RC),AAORBIL1                                   
         MVC   AORCERR,CONERR                                                   
         DROP  R3                                                               
         ZIC   R0,NUMABIL                                                       
         CH    R0,=H'2'                                                         
         BNH   DOAOR50            ONLY MAIN & AOR BILL                          
         BAS   RE,DOOTH           DO THE OTHER AOR BILLS IN THE SET             
*                                                                               
DOAOR50  XC    KEY,KEY            RE-ESTABLISH READ SEQUENCE                    
         MVC   KEY(L'BKEY),SKEY                                                 
         GOTO1 DATAMGR,DMCB,DMRDHI,SPTDIR,KEY,KEY                               
         CLC   KEY(L'BKEY),SKEY                                                 
         BE    DOAORX                                                           
         DC    H'0'                                                             
         SPACE                                                                  
DOAORX   XIT1                                                                   
         EJECT                                                                  
* CREATE A FUDGED BILL OF ALL CLIENT X'10' BILLS                                
* SO AS TO GET CORRECT TRUE AOR POSTINGS                                        
GETST    NTR1                                                                   
         L     R2,AMYAREA2                                                      
         USING BILLREC,R2                                                       
         LA    R3,1               COUNTER FOR NUMBER OF BILLS                   
         MVI   FNDTRUE,C'N'                                                     
         XC    KEY,KEY                                                          
         MVC   KEY(L'BKEY),SKEY                                                 
         GOTO1 DATAMGR,DMCB,DMREAD,SPTDIR,KEY,KEY                               
         CLC   KEY(L'BKEY),SKEY                                                 
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 DATAMGR,DMCB,GETREC,SPTFILE,KEY+14,(R2),DMWORK                   
         BAS   RE,CPYBILL         COPY BILL FOR FUDGING                         
*                                                                               
GETST05  GOTO1 DATAMGR,DMCB,DMRSEQ,SPTDIR,KEY,KEY                               
         CLC   KEY(BKEYINV-BKEY),SKEY                                           
         BNE   GETST15            END OF SET - CHECK HAVE EVERYTHING            
GETST08  GOTO1 DATAMGR,DMCB,GETREC,SPTFILE,KEY+14,(R2),DMWORK                   
         CLC   BDATE,RUNDT     IF SET - RUN DATES MUST MATCH                    
         BNE   GETST15                                                          
         TM    BILSTAT,BSTTAORQ  TRUE AOR BILL?                                 
         BNO   GETST10                                                          
         GOTOR SVTRUE,DMCB,AMYAREA2                                             
         MVI   FNDTRUE,C'Y'                                                     
         LA    R3,1(R3)                                                         
         B     GETST05                                                          
*                                                                               
GETST10  TM    BILSTAT,BSTCAORQ   AOR BILL, AT LEAST?                           
         BNO   GETST15            NO -END OF AOR SET                            
         TM    BILSTAT,BSTSADJQ                                                 
         BNO   GETST15                                                          
         BAS   RE,ADSBIL          ADD BILL TO CREATE FUDGED BILL                
         LA    R3,1(R3)                                                         
         B     GETST05                                                          
*                                                                               
GETST15  CLI   FNDTRUE,C'Y'       IF END OF SET - MUST HAVE FOUND               
         BE    *+6                TRUE AOR BILL                                 
         DC    H'0'                                                             
         CHI   R3,4                                                             
         BNH   *+6                                                              
         DC    H'0'               SHOULDN'T BE MORE THAN 4 IN A SET             
         STC   R3,NUMABIL         NUMBER OF AOR BILLS                           
         BAS   RE,DOTRUE          GET TRUE AOR POSTINGS                         
GETSTX   B     DOAORX                                                           
         DROP  R2                                                               
         EJECT                                                                  
* ADSBIL  - ADDS BILL AMOUNTS TO FUDGED BILL IN XTRAREA                         
*                                                                               
ADSBIL   NTR1                                                                   
         L     R2,AMYAREA2                                                      
         USING BILLREC,R2                                                       
         ZAP   SBILNET,BNETP       SAVE NET AMOUNT                              
         ZAP   SBILACT,BACTP       SAVE ACTUAL AMOUNT                           
         ZAP   SBILAMT,BGRSP       SAVE BILL AMOUNT                             
                                                                                
         XC    SBILGST,SBILGST                                                  
         XC    SBILPST(10*L'SBILGST),SBILGST                                    
         CLC   BLEN,=H'90'                                                      
         BNH   ADSBIL10                                                         
         CLI   BVATCOD,0                                                        
         BE    ADSBIL10                                                         
         MVC   SBILGST,BVATAMT     SAVE GST AMOUNT                              
*                                                                               
ADSBIL10 CLC   BLEN,=H'130'        IF NEW BILL LENGTH                           
         BNH   ADSBIL15                                                         
         SR    R0,R0                                                            
         ICM   R0,1,BILNPVTS                                                    
         BZ    ADSBIL15                                                         
         LA    R1,BILPVELD         R1=A(FIRST ELEMENT)                          
         USING BILPVELD,R1                                                      
         LA    RF,SBILPST          RF=A(PST INFO)                               
ADSBIL12 MVC   0(1,RF),BILPVPRV    SAVE PST INFO                                
         MVC   1(4,RF),BILPVBAS                                                 
         MVC   5(4,RF),BILPVAMT                                                 
         LA    RF,L'SBILPST(RF)                                                 
         LA    R1,BILPVLEN(R1)                                                  
         BCT   R0,ADSBIL12                                                      
         DROP  R2,R1                                                            
*                                                                               
ADSBIL15 LA    R2,XTRAREA          ADD SAVED AMTS TO FUDGED BILL REC            
         USING BILLREC,R2                                                       
         ZAP   DUB,BNETP           ADD NET AMOUNT                               
         AP    DUB,SBILNET                                                      
         ZAP   BNETP,DUB                                                        
                                                                                
         ZAP   DUB,BACTP           ADD ACTUAL AMOUNT                            
         AP    DUB,SBILACT                                                      
         ZAP   BACTP,DUB                                                        
                                                                                
         ZAP   DUB,BGRSP           ADD BILL AMOUNT                              
         AP    DUB,SBILAMT                                                      
         ZAP   BGRSP,DUB                                                        
                                                                                
         ICM   R1,15,BVATAMT       ADD GST AMOUNT                               
         A     R1,SBILGST                                                       
         STCM  R1,15,BVATAMT                                                    
         CLC   BLEN,=H'130'        IF NEW BILL LENGTH                           
         BNH   ADSBILX                                                          
         SR    R0,R0                                                            
         ICM   R0,1,BILNPVTS                                                    
         BZ    ADSBILX                                                          
         LA    R1,BILPVELD                                                      
         USING BILPVELD,R1                                                      
*                                                                               
ADSBIL30 LA    RF,SBILPST                                                       
         LA    RE,ACPMCODE                                                      
ADSBIL40 CLC   0(1,RF),BILPVPRV    MATCH ON PROVINCE NUMBER                     
         BE    *+14                                                             
         LA    RF,L'SBILPST(RF)                                                 
         BCT   RE,ADSBIL40                                                      
         DC    H'0'                                                             
*                                                                               
         ICM   RE,15,BILPVBAS      ADD BASIS AMOUNT                             
         ICM   R3,15,1(RF)                                                      
         AR    RE,R3                                                            
         STCM  RE,15,BILPVBAS                                                   
         ICM   RE,15,BILPVAMT      ADD PST AMOUNT                               
         AR    RE,R3                                                            
         STCM  RE,15,BILPVAMT                                                   
         LA    R1,BILPVLEN(R1)     BUMP TO NEXT ELEMENT IN RECORD               
         BCT   R0,ADSBIL30                                                      
*                                                                               
ADSBILX  B     DOAORX                                                           
         DROP  R2,R1                                                            
         EJECT                                                                  
* COPIES MAIN BILL TO XTRAREA                                                   
*                                                                               
CPYBILL  NTR1                                                                   
         LA    R0,XTRAREA                                                       
         L     RE,AMYAREA2                                                      
         LA    R1,1000            MAX BILL RECORD LENGTH                        
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         B     DOAORX                                                           
         SPACE                                                                  
* CALLS ACPOSTER FOR TRUE AOR POSTINGS, THEN RESETS ACCOUNTS,IOAREAS            
* FOR MAIN BILL CALL TO ACPOSTER                                                
*                                                                               
DOTRUE   NTR1                                                                   
         LA    R4,AORERRS                                                       
         USING AORERD,R4                                                        
         LA    R4,AORERRL(R4)     SAVE TRUE AOR ERRORS                          
         GOTO1 =A(RDPOST),DMCB,(RC)                                             
         LA    R3,ACPBLK                                                        
         USING ACPOSTD,R3                                                       
         LA    RE,XTRAREA         RESET ADDRESS OF BILL                         
         ST    RE,ACPBILL                                                       
         MVC   ACPNUM2,PSTNUM2    MAX NUMBER OF POSTINGS                        
         MVI   ACPAFLG,C'T'       WANT TRUE POSTINGS ONLY                       
         GOTO1 =A(POSTCALL),DMCB,(RC)                                           
         MVC   AORERR,ACPERR                                                    
         MVC   AORERRC,ACPERRC                                                  
         DROP  R3                                                               
         L     R3,=V(POSTINGS)    LOOK AT POSTINGS RETURNED                     
         USING ACPRTND,R3                                                       
         GOTO1 CLRAREA,DMCB,V(MYAREA3),V(MYAREA3X)                              
         L     R2,AMYAREA3                                                      
         ZIC   R0,PSTNUM2                                                       
DOTRU25  OC    0(ACPRTNL2,R3),0(R3)                                             
         BZ    DOTRU30                                                          
         MVC   0(ACPRTNL2,R2),0(R3)                                             
         LA    R2,ACPRTNL2(R2)                                                  
DOTRU30  LA    R3,ACPRTNL2(R3)                                                  
         BCT   R0,DOTRU25                                                       
         B     DOAORX                                                           
         DROP  R3                                                               
         EJECT                                                                  
* DOOTH - MORE THAN TWO AOR BILLS IN SET - SAVE THERE POSTINGS                  
*         IN AAORBIL2/AAORBIL3                                                  
DOOTH    NTR1                                                                   
         GOTO1 CLRAREA,DMCB,V(AORBIL2),V(AORBIL2X)                              
         GOTO1 CLRAREA,DMCB,V(AORBIL3),V(AORBIL3X)                              
         XC    KEY,KEY            START FROM BEGINNING OF SET                   
         MVC   KEY(L'BKEY),SKEY                                                 
         L     R2,AAORBIL2        A(NEXT IOAREA TO SAVE POSTINGS)               
         MVC   FULL2,AAORBIL2                                                   
         LA    R4,AORERRS                                                       
         LA    R4,AORERRL(R4)                                                   
         LA    R4,AORERRL(R4)                                                   
DOOTH03  GOTO1 DATAMGR,DMCB,DMRDHI,SPTDIR,KEY,KEY                               
         CLC   KEY(L'BKEY),SKEY                                                 
         BE    *+6                                                              
         DC    H'0'                                                             
DOOTH05  GOTO1 DATAMGR,DMCB,DMRSEQ,SPTDIR,KEY,KEY                               
         CLC   KEY(BKEYINV-BKEY),SKEY                                           
         BNE   DOOTH40            END OF SET - CHECK HAVE EVERYTHING            
         GOTO1 DATAMGR,DMCB,GETREC,SPTFILE,KEY+14,AMYAREA2,DMWORK               
         L     R1,AMYAREA2                                                      
         USING BILLREC,R1                                                       
         CLC   BDATE,RUNDT     IF SET - RUN DATES MUST MATCH                    
         BNE   DOOTH40                                                          
         TM    BILSTAT,BSTTAORQ                                                 
         BO    DOOTH05            SKIP TRUE AOR BILL                            
         TM    BILSTAT,BSTCAORQ   AOR BILL, AT LEAST?                           
         BNO   DOOTH40            END OF AOR SET                                
         TM    BILSTAT,BSTSADJQ                                                 
         BNO   DOOTH40                                                          
         DROP  R1                                                               
DOOTH10  GOTO1 CLRAREA,DMCB,V(IOAREA),V(IOAREAX)                                
         MVC   FULL,AMYAREA2                                                    
         BAS   RE,GETAOR                                                        
         LA    R3,ACPBLK                                                        
         USING ACPOSTD,R3                                                       
         GOTO1 =A(RDPOST),DMCB,(RC)                                             
         GOTO1 =A(SETVATO),DMCB,(RC),AMYAREA2                                   
         MVC   ACPPSTO(ACPMCODE*L'ACPPSTO),PSTOVALS                             
         MVC   ACPBILL,AMYAREA2                                                 
         MVI   ACPAFLG,C'M'       WANT MAIN BILL POSTINGS                       
         GOTO1 =A(POSTCALL),DMCB,(RC)                                           
         MVC   AORERR,ACPERR                                                    
         MVC   AORERRC,ACPERRC                                                  
         DROP  R3                                                               
         L     R3,=V(POSTINGS)    LOOK AT POSTINGS RETURNED                     
         USING ACPRTND,R3                                                       
         ZIC   R0,PSTNUM2                                                       
DOOTH25  OC    0(ACPRTNL2,R3),0(R3)                                             
         BZ    DOOTH30                                                          
         MVC   0(ACPRTNL2,R2),0(R3)                                             
         LA    R2,ACPRTNL2(R2)                                                  
DOOTH30  LA    R3,ACPRTNL2(R3)                                                  
         BCT   R0,DOOTH25                                                       
         GOTO1 =A(CONTRAS),DMCB,(RC),FULL2                                      
         MVC   AORCERR,CONERR                                                   
         ZIC   R0,NUMABIL                                                       
         CHI   R0,3                                                             
         BE    DOOTHX                                                           
         L     R2,AAORBIL3        PT TO NEXT AREA FOR POSTINGS                  
         MVC   FULL2,AAORBIL3                                                   
         LA    R4,AORERRL(R4)                                                   
         L     RE,AMYAREA2                                                      
         XC    KEY,KEY                                                          
         MVC   KEY(L'BKEY),0(RE)                                                
         B     DOOTH03                                                          
*                                                                               
DOOTH40  DS    0H                                                               
DOOTHX   B     DOAORX                                                           
         DROP  R3                                                               
         EJECT                                                                  
*=========================================*                                     
* GETAOR - READS AOR SFM RECORD FOR       *                                     
*          DEFAULT ACCOUNT NAMES          *                                     
*=========================================*                                     
*                                                                               
GETAOR   NTR1                                                                   
         L     R5,FULL                                                          
         USING BILLRECD,R5                                                      
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING AORREC,R4                                                        
         MVC   AORKEY(2),=X'0D45'                                               
         MVC   AORKAGMD,BAGYMD                                                  
         MVC   AORKCLT,BCLT                                                     
         MVC   AORKPRD,PRD                                                      
         MVC   AORKEST+1(1),BEST                                                
*                                                                               
         MVI   AORKDPT,X'FF'       DEFAULT DAYPART                              
         CLI   BLDPT,C' '          TEST BILL FOR ONE DAYPART                    
         BNH   *+10                                                             
         MVC   AORKDPT(1),BLDPT                                                 
*                                                                               
         MVI   AORKSTYP,X'FF'      DEFAULT STATION TYPE                         
         CLI   BLMED,C' '          TEST BILL FOR ONE STATION TYPE               
         BNH   *+10                                                             
         MVC   AORKSTYP(1),BLMED                                                
         MVI   AORKEXT+2,X'FF'     3RD POSITION NOT USED                        
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,DMRDHI,SPTDIR,KEY,KEY                               
         CLC   KEY(8),KEYSAVE      TEST THRU PRD                                
         BNE   GETAORX             NOTHING FOR PRODUCT                          
         CLC   KEY(10),KEYSAVE     TEST THRU EST                                
         BE    GETAOR8                                                          
         CLC   KEY+8(2),=X'FFFF'   DID WE FIND DEFAULT                          
         BE    GETAOR8             YES, USE IT                                  
*                                                                               
GETAOR7  MVC   KEYSAVE+8(2),=X'FFFF'  TRY DEFAULT EST                           
         MVC   KEY,KEYSAVE                                                      
         GOTO1 DATAMGR,DMCB,DMRDHI,SPTDIR,KEY,KEY                               
         CLC   KEY(10),KEYSAVE                                                  
         BNE   GETAORX             NOTHING THERE EITHER                         
*                                                                               
GETAOR8  CLC   KEY+10(1),KEYSAVE+10   DAYPART                                   
         BE    GETAOR10                                                         
         CLI   KEY+10,X'FF'           DID WE FIND DEFAULT                       
         BE    GETAOR10                                                         
         CLI   KEYSAVE+10,X'FF'       DID WE TRY FOR IT                         
         BE    GETAOR19                                                         
*                                                                               
GETAOR9  MVI   KEYSAVE+10,X'FF'       TRY DEFAULT                               
         MVC   KEY,KEYSAVE                                                      
         GOTO1 DATAMGR,DMCB,DMRDHI,SPTDIR,KEY,KEY                               
         CLC   KEY(11),KEYSAVE                                                  
         BNE   GETAOR19               NOTHING                                   
*                                                                               
GETAOR10 CLC   KEY+11(1),KEYSAVE+11   STATION TYPE                              
         BE    GETAOR20                                                         
         CLI   KEY+11,X'FF'           DID WE FIND DEFAULLT                      
         BE    GETAOR20                                                         
         CLI   KEYSAVE+11,X'FF'       DID WE TRY FOR IT                         
         BE    GETAOR18                                                         
         MVI   KEYSAVE+11,X'FF'       TRY DEFAULT                               
         MVC   KEY,KEYSAVE                                                      
         GOTO1 DATAMGR,DMCB,DMRDHI,SPTDIR,KEY,KEY                               
         CLC   KEY(12),KEYSAVE                                                  
         BE    GETAOR20                                                         
*                                  NOTHING FOUND                                
GETAOR18 CLI   KEYSAVE+10,X'FF'    WERE WE LOOKING UNDER ALL DPTS               
         BE    GETAOR19            YES                                          
*                                  NO, DO IT NOW                                
         MVI   KEYSAVE+11,X'FF'    RESET STATION TYPE                           
         CLI   BLMED,C' '                                                       
         BNH   *+10                                                             
         MVC   KEYSAVE+11(1),BLMED                                              
         B     GETAOR9                                                          
*                                                                               
GETAOR19 CLC   KEYSAVE+8(2),=X'FFFF'  WERE WE LOOKING UNDER ALL ESTS            
         BE    GETAORX                YES, NOTHING MORE TO DO                   
*                                     NO, DO IT NOW                             
         MVI   KEYSAVE+10,X'FF'       RESET DAYPART                             
         CLI   BLDPT,C' '                                                       
         BNH   *+10                                                             
         MVC   KEYSAVE+10(1),BLDPT                                              
         MVI   KEYSAVE+11,X'FF'       RESET STATION TYPE                        
         CLI   BLMED,C' '                                                       
         BNH   *+10                                                             
         MVC   KEYSAVE+11(1),BLMED                                              
         B     GETAOR7                                                          
*                                                                               
GETAOR20 GOTO1 DATAMGR,DMCB,GETREC,SPTFILE,KEY+14,ADBUY,DMWORK                  
         BE    *+6                                                              
         DC    H'0'                                                             
         L     R4,ADBUY                                                         
         LA    R1,AORELS                                                        
GETAOR30 CLI   0(R1),0                                                          
         BE    GETAORX                                                          
         CLI   0(R1),X'03'        AOR INFO ELEM                                 
         BE    GETAOR40                                                         
         SR    R0,R0                                                            
         ICM   R0,1,1(R1)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R1,R0                                                            
         B     GETAOR30                                                         
*                                                                               
         USING ACPRTND,R2                                                       
GETAOR40 CLC   AORRCVBL,SPACES    ACCT MUST BE THERE                            
         BNH   GETAORX                                                          
         MVI   BYTE,MBTTARI       AOR INCOME ROW                                
         BAS   RE,GETROW          PTS R2 TO CORRECT ROW                         
         MVC   ACPBTYP,BYTE       SET TYPE                                      
         MVC   ACPLVL,=C'AOR'     LEVEL                                         
         OI    ACPSTAT,ACPCR      CREDIT                                        
         MVC   ACPACC,AORCOMM     MOVE IN SFM INCOME ACCOUNT                    
         XC    WORK,WORK                                                        
         MVC   WORK(L'AORCOMM),AORCOMM                                          
*                                                                               
         MVI   BYTE,MBTTAPR       AOR PAY/RECEIVABLE ROW                        
         BAS   RE,GETROW          PTS R3 TO CORRECT ROW                         
         MVC   ACPBTYP,BYTE       SET TYPE                                      
         MVC   ACPLVL,=C'AOR'     LEVEL                                         
         MVC   ACPACC,AORRCVBL    MOVE IN SFM RECEIVABLE ROW                    
         CLC   ACPACC(2),=C'SR'                                                 
         BNE   GETAOR42                                                         
         OI    ACPSTAT,ACPDEB     RCVBL IS DEBIT                                
         B     GETAOR45                                                         
GETAOR42 OI    ACPSTAT,ACPCR      CREDIT                                        
         DROP  R4                                                               
GETAOR45 OC    WORK,WORK                                                        
         BZ    GETAORX                                                          
         LA    R4,AKEY                                                          
         USING ACTRECD,R4                                                       
         MVC   ACTKEY,SPACES                                                    
         MVC   ACTKCPY,ACCTCD2    OTHER COMPANY CODE (IF APPL)                  
         MVC   ACTKUNT(14),WORK                                                 
         DROP  R4                                                               
         L     R4,ADBUY                                                         
         ST    R4,AREC                                                          
         MVI   RDSE,2             READ FROM OTHER ACC FILE(IF APPL)             
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMREAD'                               
         BNE   GETAORX                                                          
         LA    R1,ACCORFST                                                      
         AR    R4,R1                                                            
         USING ABLELD,R4                                                        
GETAOR50 CLI   ABLEL,0                                                          
         BE    GETAORX            ERROR IN AOR REV ACCOUNT                      
         CLI   ABLEL,ABLELQ       BALANCE ELEMENT                               
         BE    GETAOR70                                                         
         SR    R0,R0                                                            
         ICM   R0,1,ABLLN                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R4,R0                                                            
         B     GETAOR50                                                         
*                                                                               
GETAOR70 BAS   RE,GETFILT         GET ANALYSIS OR UNIT FILTER                   
*                                                                               
GETAORX  B     DOAORX                                                           
         DROP  R4,R5                                                            
         EJECT                                                                  
*==================================================================*            
* GETFILT  - LOOKS UP 12 CHAR FROM UNIT1= FROM SPECIAL POSTING ELE *            
*            IF NOT FOUND - USES ANALYSIS = AS DEFAULT             *            
*==================================================================*            
*                                                                               
GETFILT  NTR1                                                                   
         L     R4,ADBUY                                                         
         LA    R1,ACCORFST                                                      
         AR    R4,R1                                                            
         USING SPAELD,R4                                                        
GETUF3   CLI   0(R4),0            END OF RECORD                                 
         BE    GETAF0                                                           
         CLI   0(R4),SPAELQ       SPECIAL POSTING A/C ELEMENT                   
         BE    GETUF5                                                           
         SR    R0,R0                                                            
         ICM   R0,1,1(R4)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R4,R0                                                            
         B     GETUF3                                                           
*                                                                               
GETUF5   CLI   SPATYPE,SPATANAL   ANALYSIS ACCOUNT?                             
         BNE   GETAF0                                                           
*                                                                               
         MVI   BYTE,MBTTARR         AOR REVENUE ROW                             
         BAS   RE,GETROW            PT R2 TO CORRECT ROW                        
         MVC   ACPBTYP,BYTE                                                     
         MVC   ACPACC,SPACES                                                    
         MVC   ACPACC(2),=C'12'                                                 
         MVC   ACPACC+2(L'SPAAANAL),SPAAANAL                                    
         MVC   ACPLVL,=C'AOR'     LEVEL                                         
         OI    ACPSTAT,ACPCR      CREDIT                                        
*                                                                               
         MVI   BYTE,MBTTABL         AOR BILLING ROW                             
         BAS   RE,GETROW            PT R2 TO CORRECT ROW                        
         MVC   ACPBTYP,BYTE                                                     
         MVC   ACPACC,SPACES                                                    
         MVC   ACPACC(2),=C'11'                                                 
         MVC   ACPACC+2(L'SPAAANAL),SPAAANAL                                    
         MVC   ACPLVL,=C'AOR'     LEVEL                                         
         OI    ACPSTAT,ACPCR      CREDIT                                        
         B     GETFX                                                            
         DROP  R4                                                               
*                                                                               
GETAF0   L     R4,ADBUY                                                         
         LA    R1,ACCORFST                                                      
         AR    R4,R1                                                            
         USING RSTELD,R4                                                        
GETAF3   CLI   0(R4),0            END OF RECORD                                 
         BE    GETFX                                                            
*                                                                               
GETAF4   CLI   0(R4),RSTELQ       RECORD STATUS ELE?                            
         BE    GETAF5                                                           
         SR    R0,R0                                                            
         ICM   R0,1,1(R4)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R4,R0                                                            
         B     GETAF3                                                           
*                                                                               
GETAF5   CLI   RSTCOSTG,0                                                       
         BE    GETFX                                                            
*                                                                               
         MVI   BYTE,MBTTARR       AOR REVENUE ROW                               
         BAS   RE,GETROW          PT R2 TO CORRECT ROW                          
         MVC   ACPBTYP,BYTE                                                     
         MVC   ACPACC,SPACES                                                    
         MVC   ACPACC(2),=C'12'                                                 
         MVC   ACPACC+2(1),RSTCOSTG                                             
         MVC   ACPLVL,=C'AOR'     LEVEL                                         
         OI    ACPSTAT,ACPCR      CREDIT                                        
*                                                                               
         MVI   BYTE,MBTTABL         AOR BILLING ROW                             
         BAS   RE,GETROW            PT R2 TO CORRECT ROW                        
         MVC   ACPBTYP,BYTE                                                     
         MVC   ACPACC,SPACES                                                    
         MVC   ACPACC(2),=C'11'                                                 
         MVC   ACPACC+2(1),RSTCOSTG                                             
         MVC   ACPLVL,=C'AOR'     LEVEL                                         
         OI    ACPSTAT,ACPCR      CREDIT                                        
GETFX    B     DOAORX                                                           
         DROP  R4,R2                                                            
         SPACE 2                                                                
*------------------------------------------*                                    
* GETROW -  GIVEN ROW NUMBER WILL SET R2   *                                    
*           TO POINT TO CORRESPONDING ROW  *                                    
*      NTRY - ROW NUMBER IN BYTE           *                                    
*      XIT  - SETS R2                      *                                    
*------------------------------------------*                                    
*                                                                               
GETROW   NTR1                                                                   
         ZIC   R1,BYTE                                                          
         BCTR  R1,0                                                             
         SR    RE,RE                                                            
         LA    RF,ACPRTNL         LENGTH OF ONE ROW                             
         MR    RE,R1              NUMBER OF ROWS                                
         L     R2,=V(IOAREA)      AREA CONTAINING ALL ROWS                      
         AR    R2,RF                                                            
         XIT1  REGS=(R2)          R2 => ROW REQUESTED                           
         EJECT                                                                  
*-------------------*                                                           
* LITERAL POOL      *                                                           
*-------------------*                                                           
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*-------------------------------------------------------------------*           
* CONTRAS - FINDS CONTRA-ACC & ACC NAME FOR EACH POSTING IN ACPPOST2*           
*  XIT - CREATES YET ANOTHER TBL -CONTBL (ELE TYPE,CONTRA/CONTRA NM *           
*-------------------------------------------------------------------*           
*                                                                               
         DS    0D                                                               
CONTRAS  NMOD1 0,*CONTRA*                                                       
         L     RC,0(R1)                                                         
         LA    R6,4095(RC)                                                      
         LA    R6,1(R6)                                                         
         USING SPMXWRKD,RC,R6     LOCAL WORKING STORAGE                         
         LA    R7,4095(RA)                                                      
         LA    R7,1(R7)                                                         
         USING SPWORKD,RA,R7      GLOBAL WORKING STORAGE                        
*                                                                               
         L     R4,4(R1)                                                         
         MVC   APOST,4(R1)        SAVE ADDRESS OF POSTINGS                      
         USING ACPRTND,R4         LOOK AT POSTINGS RETURNED                     
         MVI   CONERR,0                                                         
         GOTO1 CLRAREA,DMCB,V(CONTBL),V(CONTBLX)                                
         ZIC   R2,PSTNUM2         MAX # OF ROWS RETURNED                        
CONTRA5  OC    0(ACPRTNL2,R4),0(R4)                                             
         BZ    CONTRA99                                                         
         MVC   CONTRA,SPACES                                                    
         MVC   CONTRAN,SPACES                                                   
         MVI   CHKCON,C'N'                                                      
*                                                                               
         CLI   ACPERR2,0          IF ACCOUNT ERROR ALREADY                      
         BNE   CONTRA99           NO POINT IN LOOKING FOR CONTRA ACC            
*                                                                               
         CLI   0(R4),MBTTRCV      RECEIVABLE ACCOUNT?                           
         BE    *+12                                                             
         CLI   0(R4),MBTTIRCV     RECEIVABLE ACCOUNT(MIDAS)?                    
         BNE   CONTRA10                                                         
*                                                                               
         BAS   RE,RCVCONTR        GET CONTRA TO RECEIVABLES ACCOUNT             
         B     CONTRA95                                                         
*                                                                               
CONTRA10 CLI   0(R4),MBTTINC      INCOME ACCOUNT                                
         BE    CONTRA12                                                         
         CLI   0(R4),MBTTARI      AOR INCOME ACCOUNT                            
         BE    CONTRA12                                                         
         CLI   0(R4),MBTTSEL      AOR SELLOFF INCOME ACCOUNT                    
         BE    CONTRA12                                                         
         CLI   0(R4),MBTTINTI     INTERNAL INCOME ACCOUNT                       
         BE    CONTRA12                                                         
         CLI   0(R4),MBTTRINC     TRADE INCOME                                  
         BE    CONTRA12                                                         
         CLI   0(R4),MBTTOPIN     OPCO INCOME                                   
         BE    CONTRA12                                                         
         CLI   0(R4),MBTTOINC     OTHER INCOME (GROUPM MIDAS TRADE)             
         BE    CONTRA12                                                         
         CLI   0(R4),MBTTCD       CASH DISC ACCOUNT?                            
         BNE   CONTRA20                                                         
CONTRA12 BAS   RE,INCCONTR        GET CONTRA TO INCOME ACCOUNTS                 
         B     CONTRA95                                                         
*                                                                               
CONTRA20 CLI   0(R4),MBTTNET      NET ACCOUNT?                                  
         BNE   CONTRA30                                                         
         BAS   RE,NETCONTR        GET CONTRA TO NET ACCOUNT                     
         B     CONTRA95                                                         
*                                                                               
CONTRA30 CLI   0(R4),MBTTCOS      COSTING ACCOUNT?                              
         BE    *+12                                                             
         CLI   0(R4),MBTTINTC                                                   
         BNE   CONTRA50                                                         
         BAS   RE,COSCONTR        GET CONTRA TO COSTING ACCOUNT                 
         B     CONTRA95                                                         
*                                                                               
CONTRA50 CLI   0(R4),MBTTBIL      BILLING ACCOUNT                               
         BE    CONTRA55                                                         
         CLI   0(R4),MBTTREV      REVENUE ACCOUNT                               
         BE    CONTRA55                                                         
         CLI   0(R4),MBTTABL      AOR BILLING                                   
         BE    CONTRA55                                                         
         CLI   0(R4),MBTTARR      AOR REVENUE                                   
         BE    CONTRA55                                                         
         CLI   0(R4),MBTTRBI      TRADE BILLING                                 
         BE    CONTRA55                                                         
         CLI   0(R4),MBTTRRV      TRADE REVENUE                                 
         BE    CONTRA55                                                         
         CLI   0(R4),MBTTOBIL     OTHER BILLING (GROUPM MIDAS TRADE)            
         BE    CONTRA55                                                         
         CLI   0(R4),MBTTOREV     OTHER REVENUE (GROUPM MIDAS TRADE)            
         BE    CONTRA55                                                         
         CLI   0(R4),MBTTOPBL     OPCO BILLING (GROUPM MIDAS TRADE)             
         BE    CONTRA55                                                         
         CLI   0(R4),MBTTOPRV     OPCO REVENUE (GROUPM MIDAS TRADE)             
         BE    CONTRA55                                                         
         CLI   0(R4),MBTTSBL      SELLOFF BILLING                               
         BE    CONTRA55                                                         
         CLI   0(R4),MBTTSRV      SELLOFF REVENUE                               
         BNE   CONTRA60                                                         
*                                                                               
CONTRA55 BAS   RE,ROBCONTR        GET CONTRA FOR ACCOUNT                        
         B     CONTRA95                                                         
*                                                                               
CONTRA60 CLI   0(R4),MBTTGST     GST OUTPUT POSTING ACCOUNT?                    
         BE    CONTRA65                                                         
         CLI   0(R4),MBTTPQO     OR PST OUTPUT POSTING FOR PQ                   
         BE    CONTRA65                                                         
         CLI   0(R4),MBTTNSO     OR PST OUTPUT POSTING FOR NS                   
         BE    CONTRA65                                                         
         CLI   0(R4),MBTTNBO     OR PST OUTPUT POSTING FOR NB                   
         BE    CONTRA65                                                         
         CLI   0(R4),MBTTBCO     OR PST OUTPUT POSTING FOR BC                   
         BE    CONTRA65                                                         
         CLI   0(R4),MBTTONO     OR PST OUTPUT POSTING FOR ON                   
         BE    CONTRA65                                                         
         CLI   0(R4),MBTTPEO     OR PST OUTPUT POSTING FOR PE                   
         BE    CONTRA65                                                         
         CLI   0(R4),MBTTNFO     OR PST OUTPUT POSTING FOR NF                   
         BNE   CONTRA68                                                         
CONTRA65 BAS   RE,VATCONTR       GET CONTRA FOR OUTPUT GST/PST ACCOUNT          
         B     CONTRA95                                                         
*                                                                               
CONTRA68 CLI   0(R4),MBTTGSTI    GST INPUT POSTING ACCOUNT?                     
         BE    CONTRA69                                                         
         CLI   0(R4),MBTTPQI     OR PST INPUT POSTING FOR PQ                    
         BE    CONTRA69                                                         
         CLI   0(R4),MBTTNSI     OR PST INPUT POSTING FOR NS                    
         BE    CONTRA69                                                         
         CLI   0(R4),MBTTNBI     OR PST INPUT POSTING FOR NB                    
         BE    CONTRA69                                                         
         CLI   0(R4),MBTTBCI     OR PST INPUT POSTING FOR BC                    
         BE    CONTRA69                                                         
         CLI   0(R4),MBTTONI     OR PST INPUT POSTING FOR ON                    
         BE    CONTRA69                                                         
         CLI   0(R4),MBTTPEI     OR PST INPUT POSTING FOR PE                    
         BE    CONTRA69                                                         
         CLI   0(R4),MBTTNFI     OR PST INPUT POSTING FOR NF                    
         BNE   CONTRA70                                                         
CONTRA69 BAS   RE,VATICNTR       GET CONTRA FOR INPUT GST/PST ACCOUNT           
         B     CONTRA95                                                         
*                                                                               
CONTRA70 CLI   0(R4),MBTTAPR     AOR PAY/RCVBL ACCOUNT?                         
         BE    CONTRA73                                                         
         CLI   0(R4),MBTTRPT     TRADE PARTNER                                  
         BE    CONTRA73                                                         
         CLI   0(R4),MBTTIPBL    INTERCOMPANY PAYABLE (MIDAS)                   
         BNE   CONTRA75                                                         
CONTRA73 BAS   RE,APYCONTR       GET CONTRA FOR AOR PAY/RCV ACCOUNT             
         B     CONTRA95                                                         
*                                                                               
CONTRA75 CLI   0(R4),MBTTINTB     INTERNAL BILLING                              
         BE    CONTRA80                                                         
         CLI   0(R4),MBTTINTR     INTERNAL REVENUE                              
         BE    *+6                                                              
         DC    H'0'                BAD TYPE                                     
CONTRA80 BAS   RE,INTCONTR         GET INTERNAL COSTING ACCOUT                  
         B     CONTRA95                                                         
*                                                                               
CONTRA95 CLC   CONTRA,SPACES                                                    
         BNH   CONTRA97                                                         
         CLI   CHKCON,C'Y'                                                      
         BNE   *+12                                                             
         BAS   RE,CKCONACC        CHECK CONTRA ACCOUNT                          
         BNE   CONTRA97           RETAIL OVERRIDE ACCOUNT ERROR                 
*                                                                               
         ZIC   RE,PSTNUM2                                                       
         L     R1,=V(CONTBL)                                                    
         USING CONTBLD,R1                                                       
CONTRA96 OC    0(CONTBLL,R1),0(R1)                                              
         BZ    *+14                                                             
         LA    R1,CONTBLL(R1)                                                   
         BCT   RE,CONTRA96                                                      
         DC    H'0'               NO MORE ROOM IN CONTBL                        
         MVC   CONTYPE,0(R4)                                                    
         MVC   CONTYPE2,ACPWHC                                                  
         MVC   CONACC,CONTRA                                                    
         MVC   CONNAME,CONTRAN                                                  
         B     CONTRA99                                                         
*                                                                               
CONTRA97 MVI   ACPBCHG,C'E'       INDICATE ERROR WITH CONTRA ACCOUNT            
         MVI   CONERR,X'80'                                                     
CONTRA99 LA    R4,ACPRTNL2(R4)                                                  
         BCT   R2,CONTRA5                                                       
CONTRAX  XIT1                                                                   
         EJECT                                                                  
*-----------------------------------------------------------*                   
* RCVCONTR - SETS THE CONTRA ACCOUNT & THE CONTRA ACC NAME  *                   
*            FOR THE RECEIVABLES POSTING                    *                   
*-----------------------------------------------------------*                   
*                                                                               
RCVCONTR NTR1                                                                   
         LA    R2,ACPBLK          REQUEST BLOCK                                 
         USING ACPOSTD,R2                                                       
         GOTO1 MXPROF,DMCB,MTPFBILS,PROFWORK                                    
         OC    PROFWORK,PROFWORK   ANY BILL SOURCE/MEDIA NAME ?                 
         BZ    RCVCON2             NO OVERRIDE                                  
         MVC   CONTRA,SPACES                                                    
         MVC   CONTRA+3(12),PROFWORK                                            
         OC    CONTRA+1(14),SPACES                                              
         B     RCVCONX                                                          
*                                                                               
RCVCON2  CLI   ACPMI,C'Y'         ON MI RECORDS?                                
         BNE   RCVCON3                                                          
*                                 YES                                           
         GOTO1 =A(GETACC),DMCB,(RC),MBTTINC  SET CONTRA = INC ACC &             
         BNE   RCVCON2B                                                         
         CLC   CONLVL(2),=C'MI'   ONLY IF INCOME OVERRIDE                       
         BE    RCVCON5                                                          
         B     RCVCON4                                                          
RCVCON2B GOTO1 =A(GETACC),DMCB,(RC),MBTTARI  SET CONTRA = AOR INC ACC           
         BNE   RCVCON5                                                          
         CLC   CONLVL(2),=C'MI'   ONLY IF AOR INCOME OVERRIDE                   
         BE    RCVCON5                                                          
         B     RCVCON4                                                          
*                                 NOT ON MI RECORDS                             
RCVCON3  GOTO1 =A(GETACC),DMCB,(RC),MBTTINC  SET CONTRA = INC ACC &             
         BE    RCVCON4              FOUND MEDIA INCOME POSTINGS                 
         GOTO1 =A(GETACC),DMCB,(RC),MBTTARI  SET CONTRA = AOR INC ACC           
*                                   CONTRAN = CONTRA ACC NAME                   
RCVCON4  MVC   CONTRA,SPACES                                                    
         MVC   CONTRA+3(12),CONTRAN   CMP CODE,UNIT,LEDGER STAY BLANK           
         MVC   CONTRAN,SPACES        CONTRA ACC NAME = SPACES                   
         B     RCVCONX                                                          
*                                                                               
RCVCON5  BAS   RE,RDMI             YES - SET CONTRA = MI DESCRIPTION            
*                                                                               
RCVCONX  XIT1                                                                   
         DROP  R2                                                               
         EJECT                                                                  
*-----------------------------------------------------------*                   
* INCCONTR - SETS THE CONTRA ACCOUNT & THE CONTRA ACC NAME  *                   
*            FOR THE INCOME POSTINGS                        *                   
*-----------------------------------------------------------*                   
*                                                                               
INCCONTR NTR1                                                                   
         LA    R2,ACPBLK          REQUEST BLOCK                                 
         USING ACPOSTD,R2                                                       
         OC    INCRIDE,INCRIDE    OVERRIDE INCOME ACCOUNT & NAME                
         BZ    INCCON20                                                         
         CLC   OVRCLT(6),SPACES   IF OVERRIDE CLI/OR PRD                        
         BNE   INCCON20           THAT TAKES PRECEDENCE                         
         MVC   CONTRA,INCRIDE                                                   
         MVC   CONTRAN,INCRIDE+15                                               
         B     INCCONX                                                          
*                                                                               
INCCON20 MVC   CONTRA(1),ACCTCD2  OTHER COMPANY CODE IF APPL                    
         MVC   CONTRA+1(2),SVPROD CONTRA ACC IS UNIT/LEDGER                     
         MVC   CONTRA+3(3),ACPCLT CLIENT                                        
         CLC   OVRCLT,SPACES                                                    
         BE    *+10                                                             
         MVC   CONTRA+3(3),OVRCLT SET OVERRIDE CLIENT                           
         MVC   CONTRA+6(3),ACPPRD PRODUCT                                       
         CLC   OVRPRD,SPACES                                                    
         BE    *+10                                                             
         MVC   CONTRA+6(3),OVRPRD SET OVERRIDE PRODUCT                          
*                                                                               
         CLC   OVRCLT(6),SPACES   IF OVERRIDE CLI/OR PRD                        
         BE    *+8                                                              
         MVI   CHKCON,C'Y'        MUST CHECK CONTRA ACCOUNT                     
         MVC   CONTRAN,SJPRDNM    SET CONTRAN                                   
*                                                                               
INCCONX  XIT1                                                                   
         DROP  R2                                                               
         EJECT                                                                  
*-----------------------------------------------------------*                   
* NETCONTR - SETS THE CONTRA ACCOUNT & THE CONTRA ACC NAME  *                   
*            FOR THE NET POSTING                            *                   
*-----------------------------------------------------------*                   
*                                                                               
NETCONTR NTR1                                                                   
         LA    R2,ACPBLK          REQUEST BLOCK                                 
         USING ACPOSTD,R2                                                       
         MVC   CONTRA(1),ACCTCD2  OTHER COMPANY CODE IF APPL                    
         MVC   CONTRA+1(2),SVPROD CONTRA ACC IS UNIT/LEDGER                     
         MVC   CONTRA+3(3),ACPCLT CLIENT                                        
         CLC   OVRCLT,SPACES                                                    
         BE    *+14                                                             
         MVC   CONTRA+3(3),OVRCLT SET OVERRIDE CLIENT                           
         MVI   CHKCON,C'Y'                                                      
*                                                                               
         MVC   CONTRAN,SJCLTNM    SET CONTRAN                                   
NETCONX  XIT1                                                                   
         DROP  R2                                                               
         SPACE 2                                                                
*-----------------------------------------------------------*                   
* COSCONTR - SETS THE CONTRA ACCOUNT & THE CONTRA ACC NAME  *                   
*            FOR THE COSTING POSTINGS                       *                   
*-----------------------------------------------------------*                   
*                                                                               
COSCONTR NTR1                                                                   
         LA    R2,ACPBLK          REQUEST BLOCK                                 
         USING ACPOSTD,R2                                                       
         LA    R1,DMCB                                                          
         XC    0(24,R1),0(R1)                                                   
         MVC   DMCB+7(1),ACPWHC                                                 
         GOTO1 =A(GETACC),DMCB,(RC)      SETS CONTRA & CONTRAN                  
COSCONX  XIT1                                                                   
         DROP  R2                                                               
         EJECT                                                                  
*-----------------------------------------------------------*                   
* ROBCONTR - SETS THE CONTRA ACCOUNT & THE CONTRA ACC NAME  *                   
*            FOR THE REVENUES OR BILLINGS POSTING           *                   
*-----------------------------------------------------------*                   
*                                                                               
ROBCONTR NTR1                                                                   
         GOTO1 =A(GETACC),DMCB,(RC),MBTTCOS COSTING ROW NUMBER                  
         XIT1                                                                   
         SPACE                                                                  
*-----------------------------------------------------------*                   
* VATCONTR - SETS THE CONTRA ACCOUNT & THE CONTRA ACC NAME  *                   
*            FOR THE GST/PST POSTING                        *                   
*-----------------------------------------------------------*                   
*                                                                               
VATCONTR NTR1                                                                   
         LA    R2,ACPBLK          REQUEST BLOCK                                 
         USING ACPOSTD,R2                                                       
         MVC   CONTRA(1),ACCTCD2  OTHER COMPANY CODE IF APPL                    
         MVC   CONTRA+1(2),SVPROD CONTRA ACC IS UNIT/LEDGER                     
         MVC   CONTRA+3(3),ACPCLT CLIENT                                        
         CLC   OVRCLT,SPACES                                                    
         BE    *+10                                                             
         MVC   CONTRA+3(3),OVRCLT SET OVERRIDE CLIENT                           
*                                                                               
         MVC   CONTRA+6(3),ACPPRD PRODUCT                                       
         CLC   OVRPRD,SPACES                                                    
         BE    *+10                                                             
         MVC   CONTRA+6(3),OVRPRD SET OVERRIDE PRODUCT                          
*                                                                               
         CLC   OVRCLT(6),SPACES   IF OVERRIDE CLI/PRD                           
         BE    *+8                                                              
         MVI   CHKCON,C'Y'        MUST RE-READ CONTRA ACCOUNT                   
*                                                                               
         MVC   CONTRAN,SJPRDNM    SET CONTRAN                                   
         XIT1                                                                   
         DROP  R2                                                               
         SPACE                                                                  
*-----------------------------------------------------------*                   
* VATICNTR - SETS THE CONTRA ACCOUNT & THE CONTRA ACC NAME  *                   
*            FOR THE INPUT GST/PST POSTING                  *                   
*-----------------------------------------------------------*                   
*                                                                               
VATICNTR NTR1                                                                   
         GOTO1 =A(GETACC),DMCB,(RC),MBTTAPR AOR PAY/RCV ROW                     
         XIT1                                                                   
         EJECT                                                                  
*-----------------------------------------------------------*                   
* APYCONTR - SETS THE CONTRA ACCOUNT & THE CONTRA ACC NAME  *                   
*            FOR THE AOR PAYABLE/RECEIVABLE POSTING         *                   
*-----------------------------------------------------------*                   
*                                                                               
APYCONTR NTR1                                                                   
         LA    R2,ACPBLK          REQUEST BLOCK                                 
         USING ACPOSTD,R2                                                       
         MVC   CONTRA,SPACES                                                    
         LA    R1,DMCB                                                          
         XC    0(24,R1),0(R1)                                                   
         MVC   DMCB+7(1),0(R4)                                                  
         GOTO1 =A(GETACC),DMCB,(RC)                                             
         BNE   APYCONX                                                          
         CLC   CONTRA+1(2),=C'SR' FOR SR ONLY                                   
         BNE   APYCON10                                                         
         GOTO1 =A(GETACC),DMCB,(RC),MBTTARI                                     
         MVC   CONTRA,SPACES                                                    
         MVC   CONTRA+3(12),CONTRAN   CONTRA =AOR INCOME ACC NAME               
         MVC   CONTRAN,SPACES         NO CONTRA ACCOUNT NAME                    
         B     APYCONX                                                          
*                                                                               
APYCON10 CLC   CONTRA+1(2),=C'SS'                                               
         BE    APYCON20                                                         
         CLC   CONTRA+1(2),=C'SP'                                               
         BE    APYCON20                                                         
         CLC   CONTRA+1(2),=C'SQ'                                               
         BE    APYCON20                                                         
         CLC   CONTRA+1(2),=C'ST'                                               
         BE    APYCON20                                                         
         CLC   CONTRA+1(2),=C'SU'                                               
         BE    APYCON20                                                         
*                                 OTHER PAYABLE TYPES                           
         MVC   CONTRA,SPACES                                                    
         MVC   CONTRA(1),ACCTCD2  OHTER COMPANY CODE (IF APPL)                  
         MVC   CONTRA+1(2),=C'SJ' UNIT/LEDGER                                   
         MVC   CONTRA+3(3),CLT    CLIENT                                        
         MVC   CONTRAN,SJCLTNM                                                  
         B     APYCONX                                                          
*                                 SS,SP,SQ,ST,SU CLEARANCE TYPES                
APYCON20 MVC   CONTRA,SPACES                                                    
         MVC   CONTRA+12(3),CLT   CLIENT                                        
*                                                                               
APYCONX  XIT1                                                                   
         DROP  R2                                                               
         DROP  R4                                                               
         EJECT                                                                  
*-----------------------------------------------------------*                   
* INTCONTR - SETS THE CONTRA ACCOUNT & THE CONTRA ACC NAME  *                   
*            FOR THE INTERNAL BILLING/RECEIVING POSTINGS    *                   
*-----------------------------------------------------------*                   
*                                                                               
INTCONTR NTR1                                                                   
         GOTO1 =A(GETACC),DMCB,(RC),MBTTINTC    INTERNAL COSTING ROW #          
         XIT1                                                                   
         SPACE                                                                  
*-------------------------------------*                                         
* RDMI - READ MI RECORD               *                                         
*        XIT - SET CONTRA             *                                         
*-------------------------------------*                                         
*                                                                               
RDMI     NTR1                                                                   
         LA    R2,AKEY                                                          
         USING MINRECD,R2                                                       
         MVC   MINKEY,SPACES                                                    
         MVI   MINKTYP,MINKTYPQ    X'08'                                        
         MVC   MINKCPY,ACCTCD2     OTHER COMPANY CODE (IF APPL)                 
         MVC   MINKMED(1),SYS      SYSTEM                                       
         MVC   MINKMED+1(1),MYMED  MEDIA                                        
         DROP  R2                                                               
         L     R2,AMYAREA                                                       
         ST    R2,AREC                                                          
         MVI   RDSE,2             READ FROM OTHER ACC FILE(IF APPL)             
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMREAD'                               
         BNE   GETMIX                                                           
         LA    R1,ACCORFST                                                      
         AR    R2,R1                                                            
         USING MDIELD,R2                                                        
         SR    R0,R0                                                            
GETMI5   CLI   0(R2),0             END OF RECORD?                               
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   0(R2),MDIELQ       X'19'                                         
         BE    GETMI10                                                          
         ICM   R0,1,1(R2)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R2,R0                                                            
         B     GETMI5                                                           
*                                                                               
GETMI10  MVC   CONTRAN,SPACES                                                   
         MVC   CONTRA,SPACES      LEAVE COMPANY CODE,UNIT/LEDGER BLANK          
         MVC   CONTRA+3(L'MDIDESC),MDIDESC                                      
GETMIX   XIT1                                                                   
         DROP  R2                                                               
         EJECT                                                                  
*--------------------------------------------*                                  
* CKCONACC - MAKE SURE CONTRA ACCOUNT EXISTS *                                  
*            AND SET CONTRAN                 *                                  
*--------------------------------------------*                                  
*                                                                               
CKCONACC NTR1                                                                   
         MVC   CONTRAN,SPACES     MUST RESET CONTRAN                            
         MVC   AKEY,SPACES                                                      
         MVC   AKEY(L'CONTRA),CONTRA                                            
         L     R2,AMYAREA                                                       
         ST    R2,AREC                                                          
         MVI   RDSE,2             READ FROM OTHER ACC FILE(IF APPL)             
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMREAD'                               
         BNE   CKCONACX                                                         
         GOTO1 GTNAME,DMCB,AREC,CONTRAN  GET OVERRIDE CONTRA NAME               
         CR    RB,RB                                                            
CKCONACX XIT1                     CC CODE SET                                   
         EJECT                                                                  
*-------------------*                                                           
* LITERAL POOL      *                                                           
*-------------------*                                                           
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*---------------------------------------------------------*                     
* UNDO   -  UN-TRANSFER A BILL - BY READING POST DETAIL   *                     
*           RECORD(S) FOR BILL AND FOR EACH BILL          *                     
*           MAKES TWO PASSES                              *                     
*           1 -CHECKS ALL ACCOUNTS OKAY FOR RE-POSTINGS   *                     
*              & PRINTS OUT THE RESULTS                   *                     
*           2 -IF RESULTS OKAY , RE-POSTS ORIGINAL SET    *                     
*              WITH -1 , WRITES OUT BOGUS POST DETAIL RECS*                     
*              AND UNMARKS MEDIA BILL RECORD              *                     
*---------------------------------------------------------*                     
*                                                                               
         DS    0D                                                               
UNDO     NMOD1 0,**UNDO**,R9                                                    
         L     RC,0(R1)                                                         
         LA    R6,4095(RC)                                                      
         LA    R6,1(R6)                                                         
         USING SPMXWRKD,RC,R6     LOCAL WORKING STORAGE                         
         LA    R7,4095(RA)                                                      
         LA    R7,1(R7)                                                         
         USING SPWORKD,RA,R7      GLOBAL WORKING STORAGE                        
         L     R5,ADBILL                                                        
         USING BILLRECD,R5                                                      
*                                                                               
         BAS   RE,CLRFLDS         CLEAR/SET INDICATORS                          
*                                                                               
         BAS   RE,RDDTL           READ POST DETAIL REC FOR CURRENT BILL         
         CLI   UNERR,0                                                          
         BNE   UNDOX              NO RECORDS FOUND                              
*                                                                               
UNDO10   MVI   BYTE,C'N'          NO ACCOUNT ELEMENTS FOUND                     
         MVI   THREE,1            FIRST PASS                                    
UNDO12   L     R4,ADBUY           PT TO POST DETAIL RECORD                      
         LA    R1,ACCORFST                                                      
         AR    R4,R1                                                            
         USING MBTELD,R4                                                        
*                                                                               
UNDO15   CLI   0(R4),0            END OF RECORD?                                
         BE    UNDO40                                                           
         CLI   0(R4),MBTELQ       X'2E'- ACCOUNT ELEMENT FOUND?                 
         BNE   UNDO30                                                           
         MVI   BYTE,C'Y'          INDICATE AN ACCOUNT ELEMENT FOUND             
         MVI   TRAREC,C'N'        NO TRANSACTION RECORD FOUND FOR ACC           
         BAS   RE,RDTRA           READ TRAN REC FOR EACH ACC                    
         CLI   TRAREC,C'Y'                                                      
         BNE   UNDOX                                                            
         CLI   THREE,1            FIRST PASS?                                   
         BE    UNDO30             YES                                           
         BAS   RE,UNPOST          2ND PASS- WRITE RECS TO WORKER FILE           
UNDO30   BAS   RE,BUMPR4          BUMP R4                                       
         B     UNDO15                                                           
*                                                                               
UNDO40   CLI   BYTE,C'Y'          ANY ACCOUNTS TO UNDO                          
         BE    *+12                                                             
         OI    UNERR,UNENODTL     NO                                            
         B     UNDOX                                                            
         CLI   THREE,1            FIRST PASS?                                   
         BNE   UNDO50             NO                                            
         CLI   TEST,C'N'          IF TEST - EXIT AFTER 1ST PASS                 
         BNE   UNDOX                                                            
         MVI   THREE,2            INDICATE SECOND PASS                          
         B     UNDO12             NOW - WRITE TO WORKER FILE                    
*                                                                               
UNDO50   BAS   RE,REVAMTS         SET REVERSAL AMOUNTS FOR PRINTING             
         BAS   RE,UNDTL           ADD NEW POST DETAIL RECORD                    
         CLI   DTLREC,C'Y'        THERE IS A 2ND RECORD                         
         BNE   UNDO55                                                           
         MVI   DTLREC,C'N'                                                      
         MVI   UNERR,0            CLEAR ERROR FLAG FOR UNDOING BILL             
         L     RE,ADBUY                                                         
         ST    RE,AREC                                                          
         MVC   AKEY,0(RE)         MOVE IN KEY OF CURRENT RECORD                 
         MVI   RDSE,1             READ FROM NATIVE FILE                         
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMREAD'                               
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMRSEQ'                               
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,SVMBTEL         SAVE 2E ELEMENTS FROM RECORD                  
         B     UNDO10                                                           
*                                                                               
UNDO55   CLI   UNERR,0            ANY PROBLEMS WITH UNDOING POSTINGS?           
         BNE   UNDOX              YES - DON'T UNMARK BILL RECORD                
         BAS   RE,UNMARK          OTHERWISE UN-MARK TRANSFERRED BILL            
         B     UNDOX                                                            
*                                                                               
UNDONO   LTR   RB,RB               SET CC NEQ                                   
         B     UNDOX                                                            
*                                                                               
UNDOYES  CR    RB,RB               SET CC EQ                                    
*                                                                               
UNDOX    XIT1                                                                   
         EJECT                                                                  
*--------------------------------------------*                                  
* CLRFLDS  - CLEAR AND SET SOME INDICATORS   *                                  
*--------------------------------------------*                                  
*                                                                               
CLRFLDS  NTR1                                                                   
         ZAP   SVRCV,=P'0'                                                      
         ZAP   SVGRS,=P'0'                                                      
         ZAP   SVNET,=P'0'                                                      
         ZAP   SVAGYCOM,=P'0'                                                   
         ZAP   SVAAGYCM,=P'0'                                                   
         XC    SVGST,SVGST                                                      
         XC    SVGSTI,SVGSTI                                                    
         XC    SVPST,SVPST                                                      
         XC    SVPSTI,SVPSTI                                                    
         XC    SVAOR,SVAOR                                                      
         XC    SVIOR,SVIOR                                                      
         MVC   SVACOF2,SPACES                                                   
         L     R3,=V(MBTELES)     CLEAR START OF TABLE                          
         XC    0(MBTPDLNQ,R3),0(R3)                                             
         XC    TRANSDT,TRANSDT    CLEAR OUT INV DT  ADJ BY MOA PROF             
         MVI   UNERR,0            CLEAR ERROR FLAG FOR UNDOING BILL             
         MVI   DTLREC,C'N'        NO SECOND DETAIL RECORD                       
         B     UNDOX                                                            
         SPACE 1                                                                
*--------------------------------------------*                                  
* BUMPR4   - BUMP R4 TO NEXT ELEMENT IN REC  *                                  
*--------------------------------------------*                                  
*                                                                               
BUMPR4   SR    R0,R0                                                            
         ICM   R0,1,1(R4)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R4,R0                                                            
         BR    RE                                                               
         SPACE 1                                                                
*-------------------------------------------*                                   
* ADDDISP  - PT R2 TO FIRST ELEMENT IN REC  *                                   
*-------------------------------------------*                                   
*                                                                               
ADDDISP  LA    R1,ACCORFST                                                      
         AR    R2,R1                                                            
         BR    RE                                                               
         SPACE                                                                  
*--------------------------------------------*                                  
* BUMPR2   - BUMP R2 TO NEXT ELEMENT IN REC  *                                  
*--------------------------------------------*                                  
*                                                                               
BUMPR2   SR    R0,R0                                                            
         ICM   R0,1,1(R2)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R2,R0                                                            
         BR    RE                                                               
         EJECT                                                                  
*-----------------------------------------------------------------*             
* RDDTL    - FOR A GIVEN BILL - READS DETAIL RECORD(S)            *             
*  XIT       UNERR --SET IF NO RECORDS FOUND                      *             
*            DTLREC--SET TO C'Y' IF 2 DETAIL RECORDS EXIST        *             
*-----------------------------------------------------------------*             
*                                                                               
RDDTL    NTR1                                                                   
         XC    AKEY,AKEY          READ POST DETAIL(S) RECORD                    
         LA    R2,AKEY            FOR CURRENT BILL                              
         USING MPDRECD,R2                                                       
         MVI   MPDKTYP,MPDKTYPQ   X'2F'                                         
         MVI   MPDKSUB,MPDKSUBQ   X'00'                                         
         MVC   MPDKCPY,ACCTCD     NATIVE COMPANY CODE                           
         MVC   MPDKALPH,SALPH     AGY ALPHA FOR MEDIA SPLIT FILES               
         MVC   MPDKSYS,SYS        SYSTEM                                        
         MVC   MPDKMED,MYMED      MEDIA                                         
         MVC   MPDKCLI,CLT        CLIENT                                        
         MVC   MPDKPRD,PRD        PRODUCT                                       
         ZIC   RE,BKEYEST                                                       
         CVD   RE,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  MPDKEST,DUB        ESTIMATE                                      
*                                                                               
         MVC   MPDKPER,BDATE      BILL MONTH(YYMM)-WAS USING BKEYMBIL           
*                                                                               
         XC    WORK,WORK          GET SERVICE MONTH                             
         MVC   WORK(2),BKEYYSRV                                                 
         MVI   WORK+2,X'01'       FUDGE DAY                                     
         GOTO1 DATCON,DMCB,(3,WORK),(0,WORK+3)                                  
         MVC   MPDKMOS,WORK+3     MONTH OF SERVICE (YYMM)                       
*                                                                               
         MVI   MPDKINV,C'0'        INVOICE NUMBER                               
         MVC   MPDKINV+1(4),BINVNO+2                                            
*                                                                               
         MVI   RDSE,1             READ FROM NATIVE FILE                         
         L     R2,ADBUY                                                         
         ST    R2,AREC                                                          
         MVC   AREC,ADBUY         INTO BUY AREA                                 
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMREAD'                               
         BE    *+12                                                             
         OI    UNERR,UNENODTL     NO DETAIL RECORD FOUND                        
         B     RDDTLX                                                           
*                                                                               
         BAS   RE,SVMBTEL         SAVE 2E ELEMENTS FROM RECORD                  
         TM    MPDRSTA,X'01'      IS THERE A SECOND RECORD?                     
         BNO   RDDTLX                                                           
         MVI   DTLREC,C'Y'        THERE IS A 2ND RECORD                         
RDDTLX   B     UNDOX                                                            
         DROP  R2                                                               
         EJECT                                                                  
*-----------------------------------------------------------------*             
* SVMBTEL  - SAVES X'2E' ELEMENTS IN MBTELES FOR LATER CHECKING   *             
*            IF RETRANSFERING (HANDLE SEQUENCE NUMBERS)           *             
*-----------------------------------------------------------------*             
*                                                                               
SVMBTEL  NTR1                                                                   
         L     R2,AREC             R2=A(POST DETAIL RECORD)                     
         BAS   RE,ADDDISP          R2=A(FIRST ELEMENT)                          
*                                                                               
         L     R3,=V(MBTELES)      R3=A(TABLE OF MBTELD ELEMENTS)               
SVMBTEL2 OC    0(MBTPDLNQ,R3),0(R3) FIND EMPTY SPOT IN TABLE                    
         BZ    *+12                                                             
         LA    R3,MBTPDLNQ(R3)                                                  
         B     SVMBTEL2                                                         
*                                                                               
SVMBTEL3 CLI   0(R2),0            END OF RECORD?                                
         BE    SVMBTELX                                                         
         CLI   0(R2),MBTELQ       X'2E'- ACCOUNT ELEMENT FOUND?                 
         BNE   SVMBTEL5                                                         
         MVC   0(MBTPDLNQ,R3),0(R2) SAVE ELEMENT                                
         LA    R3,MBTPDLNQ(R3)                                                  
SVMBTEL5 BAS   RE,BUMPR2         GET NEXT ELEMENT IN RECORD                     
         B     SVMBTEL3                                                         
*                                                                               
SVMBTELX XC    0(MBTPDLNQ,R3),0(R3) CLEAR LAST ENTRY                            
         B     UNDOX                                                            
         EJECT                                                                  
*--------------------------------------------------------------------*          
* RDTRA - GIVEN A X'2E' ELE (1 ACCOUNT) -READS FOR A TRANSACTION REC *          
*         IF NOT FOUND SETS UNERR & TRAREC                           *          
*--------------------------------------------------------------------*          
*                                                                               
RDTRA    NTR1                                                                   
         LA    R2,AKEY                                                          
         USING TRNRECD,R2                                                       
         MVC   TRNKEY,SPACES                                                    
         MVC   TRNKCPY,ACCTCD2       OTHER COMPANY CODE (IF APPL)               
         MVC   TRNKUNT(14),MBTULA    UNIT/LEDGER/ACCOUNT                        
         CLI   SVNEWOF,C'Y'          IF NEWOFF                                  
         BNE   RDTRA2                                                           
         CLI   EMULATE,C'Y'          AND EMULATING                              
         BNE   RDTRA2                                                           
         MVC   TRNKOFF,MBTOFFC       USE ACC OFF                                
RDTRA2   CLC   =C'SR',MBTULA         IF RECEIVABLES - SKIP COMP CODE            
         BE    RDTRA5                                                           
         CLC   =C'SS',MBTULA         OR CERTAIN PAYABLE ACCOUNTS                
         BE    RDTRA5                                                           
         CLC   =C'SP',MBTULA                                                    
         BE    RDTRA5                                                           
         CLC   =C'SQ',MBTULA                                                    
         BE    RDTRA5                                                           
         CLC   =C'ST',MBTULA                                                    
         BE    RDTRA5                                                           
         CLC   =C'SU',MBTULA                                                    
         BE    RDTRA5                                                           
         CLI   MBTTYP,MBTTRCV      OR IF RECEIVABLES AND 'SB'                   
         BNE   *+14                                                             
         CLC   =C'SB',MBTULA                                                    
         BE    RDTRA5                                                           
         MVC   TRNKCCPY,ACCTCD2      OTHER COMPANY CODE(IF APPL)                
RDTRA5   MVC   TRNKCUNT(14),MBTCNTRA UNIT/LEDGER/CONTRA ACCOUNT                 
         GOTO1 DATCON,DMCB,(0,BQDATE),(1,TRNKDATE)                              
         MVC   TRNKREF,BINVOICE      BILL REF #                                 
         TM    MBTSTAT,MBTSAOR       IF ACCOUNT IS FOR TRUE AOR BILL            
         BNO   *+10                                                             
         MVC   TRNKREF,TRUEINV       USE ITS BILL INVOICE NUMBER                
         MVI   TRNKSBR,0             SUB ZERO                                   
         MVC   SVAKEY,AKEY           SAVE TRANSACTION KEY BUILT                 
         L     R2,AMYAREA                                                       
         ST    R2,AREC                                                          
         MVI   RDSE,2                READ FROM OTHER ACC FILE(IF APPL)          
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMREAD'                               
         BNE   RDTRA30                                                          
*                                                                               
RDTRA10  BAS   RE,CHK3ELE         CHECK F3 OR 23 ELEMENT FOR MATCH              
         BNE   RDTRA20            NOT CORRECT TRANSACTION RECORD                
         BAS   RE,CHKE5ELE        CHECK MATCHES IN E5 ELEMENT                   
         BNE   RDTRA20            NOT CORRECT TRANSACTION RECORD                
         BAS   RE,CHK44ELE        CHECK MATCHES IN 44 ELEMENT                   
         BNE   RDTRA20            NOT CORRECT TRANSACTION RECORD                
         BAS   RE,CHKDBELE        CHECK MATCHES IN DB ELEMENT                   
         BNE   RDTRA20            NOT CORRECT TRANSACTION RECORD                
         MVI   TRAREC,C'Y'        TRANSACTION REC FOUND FOR ACCOUNT             
         B     RDTRAX                                                           
*                                                                               
RDTRA20  DS    0H                 READ FOR NEXT TRANSACTION RECORD              
         MVC   AKEY,0(R2)         MOVE IN KEY OF CURRENT RECORD                 
         MVI   RDSE,2                READ FROM OTHER ACC FILE(IF APPL)          
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMREAD'                               
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMRSEQ'                               
         BNE   RDTRA30                                                          
         CLC   SVAKEY(TRNKSBR-TRNKEY),AKEY                                      
         BE    RDTRA10            CHECK THIS RECORD                             
RDTRA30  OI    UNERR,UNENOTRA     PROBLEM - NO TRANSACTION REC                  
*                                                                               
RDTRAX   B     UNDOX                                                            
         DROP  R2                                                               
         EJECT                                                                  
*-----------------------------------------------------------------*             
* CHK3ELE - CHECKS FOR F3 ELE FIRST OTHERWISE CHECKS 23 ELE MATCHES             
*-----------------------------------------------------------------*             
CHK3ELE  NTR1                                                                   
         L     R2,AMYAREA         R2=A(TRANSACTION RECORD)                      
         BAS   RE,ADDDISP         R2=A(FIRST ELEMENT IN RECORD)                 
         USING MBIELD,R2                                                        
CHK3E5   CLI   0(R2),0            END OF RECORD?                                
         BE    CHK3E20                                                          
         CLI   0(R2),MBIELQ       LOOK FOR F3 ELEMENT                           
         BE    *+12                                                             
         BAS   RE,BUMPR2          BUMP TO NEXT ELEMENT IN RECORD                
         B     CHK3E5                                                           
*                                                                               
         CLI   MBISYS,C'S'        MAKE SURE CORRECT SYSTEM                      
         BNE   UNDONO                                                           
         OC    MBIRDAT,MBIRDAT                                                  
         BNZ   UNDONO             DON'T REVERSE A REVERSAL                      
         CLC   MBIMED,MYMED       MAKE SURE CORRECT MEDIA                       
         BNE   UNDONO                                                           
         CLC   MBICLI,CLT         MAKE SURE CORRECT MEDIA CLIENT                
         BNE   UNDONO                                                           
         CLC   MBIPRD,PRD         MAKE SURE CORRECT MEDIA PRODUCT               
         BNE   UNDONO                                                           
         CLC   MBIEST,EST         MAKE SURE CORRECT MEDIA ESTIMATE              
         BNE   UNDONO                                                           
         MVC   WORK(4),BMONSERV                                                 
         MVC   WORK+4(2),=C'01'                                                 
         GOTO1 DATCON,DMCB,(0,WORK),(1,WORK+6)                                  
         CLC   MBIMOS,WORK+6      MAKE SURE CORRECT YM OF SERVICE               
         BNE   UNDONO                                                           
         CLC   MBITDAT,BILPOST    MAKE SURE CORRECT TRANS DATE                  
         BNE   UNDONO                                                           
         B     UNDOYES            FOUND A MATCH                                 
*                                 NO F3 ELE(OLD POSTING) - CHK 23 ELE           
CHK3E20  L     R2,AMYAREA         R2=A(TRANSACTION RECORD)                      
         BAS   RE,ADDDISP         R2=A(FIRST ELEMENT IN RECORD)                 
         USING OTHELD,R2                                                        
CHK3E25  CLI   0(R2),0            END OF RECORD?                                
         BE    UNDONO                                                           
         CLI   0(R2),OTHELQ       LOOK FOR 23 ELEMENT                           
         BE    *+12                                                             
         BAS   RE,BUMPR2          BUMP TO NEXT ELEMENT IN RECORD                
         B     CHK3E25                                                          
*                                                                               
         CLC   OTHNUM(3),PRD      PRODUCT MUST MATCH                            
         BNE   UNDONO                                                           
         CLC   OTHNUM+3(3),EST    ESTIMATE MUST MATCH                           
         BNE   UNDONO                                                           
         CLC   OTHPROF(2),=C'S '  SYSTEM MUST MATCH                             
         BNE   UNDONO                                                           
         MVC   WORK(4),BMONSERV                                                 
         MVC   WORK+4(2),=C'01'                                                 
         GOTO1 DATCON,DMCB,(0,WORK),(1,WORK+6)                                  
         CLC   OTHPROF+2(2),WORK+6  MONTH OF SERVICE MUST MATCH                 
         BNE   UNDONO                                                           
         B     UNDOYES                                                          
         DROP  R2                                                               
         EJECT                                                                  
*-----------------------------------------------------------------*             
* CHK44ELE - CHECKS IF 44 ELEMENT EXISTS & HAS CORRECT VALUES     *             
*-----------------------------------------------------------------*             
*                                                                               
CHK44ELE NTR1                                                                   
         L     R2,AMYAREA         R2=A(TRANSACTION RECORD)                      
         BAS   RE,ADDDISP         R2=A(FIRST ELEMENT IN RECORD)                 
         USING TRNELD,R2                                                        
*                                                                               
CHK44E5  CLI   0(R2),0            END OF RECORD?                                
         BE    UNDONO                                                           
         CLI   0(R2),TRNELQ       LOOK FOR 44 ELEMENT                           
         BE    *+12                                                             
         BAS   RE,BUMPR2          BUMP TO NEXT ELEMENT IN RECORD                
         B     CHK44E5                                                          
*                                 GET MOA FROM TRANSACTION                      
         GOTO1 =V(CONVMOS),DMCB,TRNELD,WORK                                     
         MVI   WORK+2,1           FUDGE DAY                                     
         GOTO1 DATCON,DMCB,(1,WORK),(0,TRANSDT)                                 
**                                                                              
         CLI   SVPROF+7,C'Y'         HONOR MOA LOCK RULES?                      
         BNE   CHK44E50                                                         
         XC    WORK,WORK                                                        
         GOTO1 DATCON,DMCB,(0,TRANSDT),(6,WORK)                                 
         L     RF,UTL                SET ACCOUNTING SE NUMBER                   
         MVC   4(1,RF),ACCTSE2                                                  
         USING COMFACSD,R1                                                      
         L     R1,ACOMFACS         R1=A(COMFACS)                                
         OC    CPERVAL,CPERVAL     SPONSER DOESN'T FILL IN PERVAL               
         BNZ   *+10                SO NEED TO DO IT                             
         MVC   CPERVAL,=V(PERVAL)                                               
CHK44E10 GOTO1 =V(BMONVAL),DMCB,(6,WORK),(9,ACOMFACS),(0,WORK+20),     +        
               (ACCTCD2,0)                                                      
         LA    R1,WORK+20                                                       
         USING BMONVALD,R1                                                      
         CLI   BMOERR,BMOEOKQ      EVERYTHING OK?                               
         BE    CHK44E50                                                         
         TM    BMOERR,BMOELOKQ     MOA LOCKED?                                  
         BZ    CHK44E20                                                         
         OC    BMOLCKP,BMOLCKP     LAST LOCKED MOA                              
         BZ    CHK44E20                                                         
         XC    WORK(10),WORK                                                    
         MVC   WORK(L'BMOLCKP),BMOLCKP                                          
         MVI   WORK+2,X'01'                                                     
         GOTO1 DATCON,DMCB,(1,WORK),(0,WORK+3)                                  
         GOTO1 ADDAY,DMCB,WORK+3,WORK+3,31                                      
         MVC   TRANSDT,WORK+3                                                   
         B     CHK44E50                                                         
*                                                                               
CHK44E20 TM    BMOERR,BMOERNGQ     DATE OF OUF RANGE?                           
         BZ    CHK44E50                                                         
         OC    BMOMOSP,BMOMOSP                                                  
         BZ    CHK44E50                                                         
         XC    WORK(10),WORK                                                    
         MVC   WORK(L'BMOMOSP),BMOMOSP                                          
         MVI   WORK+2,X'01'                                                     
         GOTO1 DATCON,DMCB,(1,WORK),(0,WORK+3)                                  
         GOTO1 ADDAY,DMCB,WORK+3,WORK+3,31                                      
         GOTO1 DATCON,DMCB,(0,WORK+3),(6,WORK)                                  
         B     CHK44E10           VALIDATE AGAIN USING NEW MONTH                
*                                                                               
CHK44E50 L     RF,UTL                                                           
         MVC   4(1,RF),SAVSYS     RE-SET TO SPOT SYSTEM NUMBER                  
         CP    TRNAMNT,MBTPOST     MATCH ON AMOUNT                              
         BNE   UNDONO                                                           
         CLI   TRNTYPE,9          IF NOT MINE -- SKIP IT                        
         BNE   UNDONO                                                           
         TM    MBTSTAT,MBTSDR     IF DEBIT                                      
         BNO   CHK44E60                                                         
         TM    TRNSTAT,TRNSDR     CHECK TRANSACTION ALSO DEBIT                  
         BNO   UNDONO                                                           
         B     UNDOYES                                                          
*                                                                               
CHK44E60 TM    TRNSTAT,TRNSDR      NOT DEBIT - SO MUST BE CREDIT                
         BO    UNDONO                                                           
         B     UNDOYES                                                          
         DROP  R2                                                               
         EJECT                                                                  
*-----------------------------------------------------------------*             
* CHKE5ELE - CHECKS IF E5 ELEM EXISTS AND HAS CORRECT DATE        *             
*-----------------------------------------------------------------*             
*                                                                               
CHKE5ELE NTR1                                                                   
         L     R2,AMYAREA         R2=A(TRANSACTION RECORD)                      
         BAS   RE,ADDDISP         R2=A(FIRST ELEMENT IN RECORD)                 
         USING GDAELD,R2                                                        
*                                                                               
CHKE5E5  CLI   0(R2),0            END OF RECORD?                                
         BE    UNDONO                                                           
         CLI   0(R2),GDAELQ       LOOK FOR E5 MEDIA BILLING DATE ELE            
         BNE   *+12                                                             
         CLI   GDATYPE,GDATMBD                                                  
         BE    *+12                                                             
         BAS   RE,BUMPR2          BUMP TO NEXT ELEMENT IN RECORD                
         B     CHKE5E5                                                          
*                                                                               
         GOTO1 DATCON,DMCB,(0,BDATE),(1,WORK)                                   
         CLC   GDADATE,WORK       DATES MUST MATCH                              
         BNE   UNDONO                                                           
         B     UNDOYES            MATCH                                         
         DROP  R2                                                               
         EJECT                                                                  
*-----------------------------------------------------------------*             
* CHKDBELE - CHECKS IF DB ELEM EXISTS AND HAS CORRECT ELEMENT TYPE*             
*-----------------------------------------------------------------*             
*                                                                               
CHKDBELE NTR1                                                                   
         L     R2,AMYAREA         R2=A(TRANSACTION RECORD)                      
         BAS   RE,ADDDISP         R2=A(FIRST ELEMENT IN RECORD)                 
         USING FFTELD,R2                                                        
*                                                                               
CHKDBE5  CLI   0(R2),0            END OF RECORD?                                
         BE    UNDOYES            OK FOR NOW WITHOUT 1A                         
         CLI   FFTEL,FFTELQ       LOOK FOR DB TEXT ELEMENT                      
         BNE   *+12                                                             
         CLI   FFTTYPE,FFTTMXTY   LOOK FOR CORRECT TYPE                         
         BE    *+12               NOW CHECK IF ELEMENT TYPE MATCHES             
         BAS   RE,BUMPR2          BUMP TO NEXT ELEMENT IN RECORD                
         B     CHKDBE5                                                          
*                                                                               
         CLC   FFTMXTE(L'MBTTYP),MBTTYP                                         
         BNE   UNDONO                                                           
         B     UNDOYES            MATCH                                         
         DROP  R2                                                               
         EJECT                                                                  
*--------------------------------------------------------*                      
* UNPOST - TAKES ELEMENTS NEEDED FROM TRANSACTION RECORD *                      
*          AND REPOSTS THEM AFTER MULT AMOUNTS BY -1     *                      
*--------------------------------------------------------*                      
*                                                                               
UNPOST   NTR1                                                                   
         GOTO1 CLRAREA,DMCB,V(REC),V(RECX)                                      
         L     R3,=V(REC)                                                       
         MVC   2(2,R3),=H'2'                                                    
         LA    R3,4(R3)                                                         
*                                                                               
         BAS   RE,SETHDR          SET 50 HEADER ELEMENT                         
         LA    R1,PSHEADL         LENGTH OF ELEM                                
         MVC   0(PSHEADL,R3),ELEM MOVE 50 HEADER ELEMENT IN                     
         AHI   R3,PSHEADL         PT R3 TO NEXT POSITION TO ADD ELEMENT         
         BAS   RE,ADDLEN          ADD TO REC LENGTH                             
*                                                                               
         L     R2,AMYAREA         R2=A(TRANSACTION RECORD)                      
         BAS   RE,ADDDISP         R2=A(FIRST ELEMENT IN RECORD)                 
         USING TRNELD,R2                                                        
*                                                                               
UNPST3   CLI   0(R2),0            END OF RECORD?                                
         BE    UNPST50            YES - GO ADD WORKER FILE RECORD               
         CLI   0(R2),DUEELQ       X'61' DUE DATE ELEMENT                        
         BE    UNPST5                                                           
         CLI   0(R2),UFSELQ       X'A2' USER FIELD SELECT ELE                   
         BE    UNPST5                                                           
         CLI   0(R2),OTHELQ       X'23' OTHERS ELEMENT                          
         BE    UNPST5                                                           
         CLI   0(R2),MPGELQ       X'D7' PRD GROUP ELEMENT                       
         BE    UNPST5                                                           
         CLI   0(R2),GDAELQ       X'E5' BILL DATE ELEMENT                       
         BE    UNPST5                                                           
         CLI   0(R2),APEELQ       X'C0' 1C ATTRIBUTE ELEMENT                    
         BE    UNPST5                                                           
         CLI   0(R2),FFTELQ       X'DB' FREE FORM ELEMENT                       
         BNE   *+16                                                             
         CLI   2(R2),FFTTAOR      'DB' AOR INFO TYPE?                           
         BNE   UNPST5             NO JUST COPY THE ELEMENT                      
         B     UNPST10            YES NEED TO MINUS THE AMOUNT                  
         CLI   0(R2),XPYELQ       X'46' EXTRA AOR PAY ELEMENT                   
         BNE   UNPST10                                                          
*                                                                               
UNPST5   ZIC   R1,1(R2)           GET LENGTH OF ELEMENT                         
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),0(R2)      MOVE INTO RECORD W/O CHANGES                  
         LA    R1,1(R1)                                                         
         AR    R3,R1              PT R3 TO NEXT AVAIL POSITION                  
         BAS   RE,ADDLEN                                                        
         B     UNPST30                                                          
*                                                                               
UNPST10  CLI   0(R2),MDTELQ                                                     
         BNE   UNPST15                                                          
         BAS   RE,SETMT           SET 1A MEDIA TRANSFER ELEMENT                 
         B     UNPST25            GO ADD IT                                     
*                                                                               
UNPST15  CLI   0(R2),TRNELQ                                                     
         BNE   UNPST20                                                          
         BAS   RE,SETRAN          SET 44 TRANSACTION ELEMENT                    
         B     UNPST25            GO ADD IT                                     
*                                                                               
UNPST20  CLI   0(R2),SCIELQ                                                     
         BNE   UNPST22                                                          
         BAS   RE,SETSUB          SET 50 SUBSIDARY CASH ELEMENTS                
         B     UNPST25            GO ADD IT                                     
*                                                                               
UNPST22  CLI   0(R2),MBIELQ                                                     
         BNE   UNPST22A                                                         
         BAS   RE,SETMBI          SET F3 ELEMENT                                
         B     UNPST25            AND ADD IT                                    
*                                                                               
UNPST22A CLI   0(R2),MDPELQ                                                     
         BNE   UNPST22B                                                         
         BAS   RE,SET6A           SET 6A ELEMENT                                
         B     UNPST25            AND ADD IT                                    
*                                                                               
UNPST22B CLI   0(R2),FFTELQ                                                     
         BNE   UNPST30                                                          
         BAS   RE,SETDB           SET DB ELEMENT                                
         B     UNPST25            AND ADD IT                                    
*                                                                               
UNPST25  ZIC   R1,ELEM+1          GET ELEMENT LENGTH                            
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),ELEM       MOVE IT IN                                    
         LA    R1,1(R1)                                                         
         AR    R3,R1              MOVE IT INTO ELEMENT                          
         BAS   RE,ADDLEN          UPDATE RECORD LENGTH                          
*                                                                               
UNPST30  BAS   RE,BUMPR2          BUMP TO NEXT ELEMENT IN RECORD                
         B     UNPST3                                                           
*                                                                               
UNPST50  L     R1,=V(REC)         MOVE & ADJUST LENGTH                          
         ICM   R2,3,2(R1)                                                       
         XC    0(4,R1),0(R1)                                                    
         LA    R2,4(R2)                                                         
         STCM  R2,3,0(R1)                                                       
         MVI   SEQONE,C'Y'        REVERSAL RECORD                               
         MVI   DETL,C'N'          NOT A DETAIL RECORD                           
         GOTO1 =A(PUTSORT),DMCB,(RC),V(REC)                                     
UNPSTX   B     UNDOX                                                            
         DROP  R2                                                               
         SPACE 2                                                                
ADDLEN   NTR1                                                                   
         L     R2,=V(REC)                                                       
         SR    RE,RE                                                            
         ICM   RE,3,2(R2)                                                       
         AR    RE,R1                                                            
         STCM  RE,3,2(R2)                                                       
         B     UNDOX                                                            
         EJECT                                                                  
*-------------------------------------------------------------*                 
* SETHDR - CREATES 50 HEADER ELEMENT FOR POSTING RECORD       *                 
*          WHICH REVERSES PREVIOUS POSTINGS                   *                 
*-------------------------------------------------------------*                 
*                                                                               
SETHDR   NTR1                                                                   
         XC    ELEM,ELEM                                                        
         LA    R3,ELEM                                                          
         USING PSHEADD,R3                                                       
         L     R2,AMYAREA         TRANSACTION RECORD                            
         USING TRNRECD,R2                                                       
         MVI   PSHDEL,PSHDELQ     X'50'                                         
         MVI   PSHDLEN,PSHEADL                                                  
         MVC   PSHDANAL,SPACES                                                  
         MVC   PSHDACC,TRNKCULA   ACCOUNT                                       
         MVC   PSHDSBAC,TRNKCULC  CONTRA ACCOUNT                                
         MVC   PSHDSBNM,SPACES                                                  
         MVC   AKEY,SPACES                                                      
         MVC   AKEY(L'TRNKCULC),TRNKCULC                                        
         MVC   AREC,AMYAREA3                                                    
         MVI   RDSE,2                READ FROM OTHER ACC FILE(IF APPL)          
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMREAD'                               
         BNE   SETHDRX                                                          
         GOTO1 GTNAME,DMCB,AREC,PSHDSBNM                                        
*                                                                               
SETHDRX  B     UNDOX                                                            
         DROP  R3,R2                                                            
         EJECT                                                                  
*-------------------------------------------------------------*                 
* SETMT - MOVE 1A ELEMENT INTO ELEM AND COMPLEMENT ALL AMOUNTS*                 
*-------------------------------------------------------------*                 
*                                                                               
SETMT    NTR1                                                                   
         XC    ELEM,ELEM                                                        
         ZIC   R1,1(R2)           ELEMENT LENTH                                 
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   ELEM(0),0(R2)                                                    
         LA    R1,ELEM                                                          
         USING MDTELD,R1                                                        
         ICM   RE,15,MDTNET                                                     
         LCR   RF,RE                                                            
         STCM  RF,15,MDTNET                                                     
         ICM   RE,15,MDTCD                                                      
         LCR   RF,RE                                                            
         STCM  RF,15,MDTCD                                                      
         ICM   RE,15,MDTCOM                                                     
         LCR   RF,RE                                                            
         STCM  RF,15,MDTCOM                                                     
         ICM   RE,15,MDTGRS                                                     
         LCR   RF,RE                                                            
         STCM  RF,15,MDTGRS                                                     
         ICM   RE,15,MDTRECV                                                    
         LCR   RF,RE                                                            
         STCM  RF,15,MDTRECV                                                    
         ICM   RE,15,MDTVAT                                                     
         LCR   RF,RE                                                            
         STCM  RF,15,MDTVAT                                                     
SETMTX   B     UNDOX                                                            
         DROP  R1                                                               
         EJECT                                                                  
*-------------------------------------------------------------*                 
* SET6A - MOVE 6A ELEMENT INTO ELEM AND COMPLEMENT ALL AMOUNTS*                 
*-------------------------------------------------------------*                 
*                                                                               
SET6A    NTR1                                                                   
         XC    ELEM,ELEM                                                        
         ZIC   R1,1(R2)           ELEMENT LENTH                                 
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   ELEM(0),0(R2)                                                    
         LA    R1,ELEM                                                          
         USING MDPELD,R1                                                        
         ZAP   DUB,MDPGRS                                                       
         MP    DUB,=P'-1'                                                       
         ZAP   MDPGRS,DUB                                                       
         ZAP   DUB,MDPNET                                                       
         MP    DUB,=P'-1'                                                       
         ZAP   MDPNET,DUB                                                       
         ZAP   DUB,MDPCOM                                                       
         MP    DUB,=P'-1'                                                       
         ZAP   MDPCOM,DUB                                                       
         ZAP   DUB,MDPCD                                                        
         MP    DUB,=P'-1'                                                       
         ZAP   MDPCD,DUB                                                        
         ZAP   DUB,MDPINTL                                                      
         MP    DUB,=P'-1'                                                       
         ZAP   MDPINTL,DUB                                                      
         ZAP   DUB,MDPRECV                                                      
         MP    DUB,=P'-1'                                                       
         ZAP   MDPRECV,DUB                                                      
         ZAP   DUB,MDPVAT                                                       
         MP    DUB,=P'-1'                                                       
         ZAP   MDPVAT,DUB                                                       
SET6AX   B     UNDOX                                                            
         DROP  R1                                                               
         EJECT                                                                  
*-------------------------------------------------------------*                 
* SETDB - MOVE DB ELEMENT INTO ELEM AND COMPLEMENT ALL AMOUNTS*                 
* NOTE - THIS IS FOR THE DB ELEMENT WITH TYPE 96 ONLY         *                 
*-------------------------------------------------------------*                 
*                                                                               
SETDB    NTR1                                                                   
         XC    ELEM,ELEM                                                        
         ZIC   R1,1(R2)           ELEMENT LENTH                                 
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   ELEM(0),0(R2)                                                    
         LA    R1,ELEM                                                          
         USING FFTELD,R1                                                        
         ZAP   DUB,FFTAMAOR                                                     
         MP    DUB,=P'-1'                                                       
         ZAP   FFTAMAOR,DUB                                                     
SETDBX   B     UNDOX                                                            
         DROP  R1                                                               
         EJECT                                                                  
*-------------------------------------------------------------*                 
* SETRAN- MOVE 44 ELEMENT INTO ELEM AND COMPLEMENT ALL AMOUNTS*                 
*-------------------------------------------------------------*                 
*                                                                               
SETRAN   NTR1                                                                   
         XC    ELEM,ELEM                                                        
         ZIC   R1,1(R2)           ELEMENT LENGTH                                
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   ELEM(0),0(R2)                                                    
         LA    R1,ELEM                                                          
         USING TRNELD,R1                                                        
         ZAP   DUB,TRNAMNT        NECESSARY FOR LARGE NUMBERS                   
         MP    DUB,=P'-1'                                                       
         ZAP   TRNAMNT,DUB                                                      
         MVC   TRNMOS(1),TRANSDT+1  2ND CHARACTER OF YEAR                       
         ZIC   RF,TRANSDT+3                                                     
         CLI   TRANSDT+2,C'1'      IF MONTH IS ONE DIGIT (0-9)                  
         BNE   *+8                 YES - THEN MOVE IT                           
         SH    RF,=H'47'           NO- MAKE 10=C1,11=C2,12=C3,ETC..             
         STC   RF,TRNMOS+1                                                      
         MVI   TRNSUB,0           FORCE SO TRANS APPEAR IN ORDER                
SETRANX  B     UNDOX                                                            
         DROP  R1                                                               
         SPACE 2                                                                
*-------------------------------------------------------------*                 
* SETSUB- MOVE 50 ELEMENT INTO ELEM AND COMPLEMENT ALL AMOUNTS*                 
*-------------------------------------------------------------*                 
*                                                                               
SETSUB   NTR1                                                                   
         XC    ELEM,ELEM                                                        
         ZIC   R1,1(R2)           ELEMENT LENTH                                 
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   ELEM(0),0(R2)                                                    
         LA    R1,ELEM                                                          
         USING SCIELD,R1                                                        
         ZAP   DUB,SCIAMNT        NECESSARY FOR LARGE NUMBERS                   
         MP    DUB,=P'-1'                                                       
         ZAP   SCIAMNT,DUB                                                      
         CLI   SCILN,SCILN3Q                                                    
         BNE   SETSUBX                                                          
         ZAP   DUB,SCIBASE        NECESSARY FOR LARGE NUMBERS                   
         MP    DUB,=P'-1'                                                       
         ZAP   SCIBASE,DUB                                                      
SETSUBX  B     UNDOX                                                            
         DROP  R1                                                               
         SPACE                                                                  
*-------------------------------------------------------------*                 
* SETMBI- MOVE F3 ELEMENT INTO ELEM AND SET REVERSAL DATE     *                 
*-------------------------------------------------------------*                 
*                                                                               
SETMBI   NTR1                                                                   
         XC    ELEM,ELEM                                                        
         ZIC   R1,1(R2)           ELEMENT LENTH                                 
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   ELEM(0),0(R2)                                                    
         LA    R1,ELEM                                                          
         USING MBIELD,R1                                                        
         MVC   MBIRDAT,TODAYP     SET DATE OF THIS REVERSAL                     
         B     UNDOX                                                            
         DROP  R1,R4                                                            
         EJECT                                                                  
*-------------------------------------------------------------*                 
* REVAMTS - SET REVERSAL AMOUNTS PRINTING                     *                 
*-------------------------------------------------------------*                 
*                                                                               
REVAMTS  NTR1                                                                   
         L     R2,ADBUY            R2=A(POST DETAIL RECORD)                     
         BAS   RE,ADDDISP          R2=A(FIRST ELEMENT IN RECORD)                
         USING MBTELD,R2                                                        
*                                                                               
REVAMT5  CLI   0(R2),0                                                          
         BE    REVAMT80                                                         
         CLI   0(R2),MBTELQ                                                     
         BNE   REVAMT70                                                         
*                                                                               
         CLI   MBTTYP,MBTTRCV      RECEIVABLE ROW                               
         BNE   REVAMT10                                                         
         OC    MBTPOST,MBTPOST                                                  
         BZ    *+10                                                             
         ZAP   SVRCV,MBTPOST       SET RECEIVABLE AMOUNT                        
         B     REVAMT70                                                         
*                                                                               
REVAMT10 CLI   MBTTYP,MBTTNET    NET ROW                                        
         BNE   REVAMT20                                                         
         OC    MBTPOST,MBTPOST                                                  
         BZ    *+10                                                             
         ZAP   SVNET,MBTPOST     SET NET AMOUNT                                 
         B     REVAMT70                                                         
*                                                                               
REVAMT20 CLI   MBTTYP,MBTTAPR    AOR PAY/RCVBL ROW                              
         BNE   REVAMT30                                                         
         MVI   SFLIPAOR,C'N'                                                    
         MVI   SMULTCM,C'N'                                                     
         CLC   =C'SR',MBTUNIT                                                   
         BNE   *+8                                                              
         MVI   SFLIPAOR,C'Y'                                                    
         OC    MBTPOST,MBTPOST                                                  
         BZ    REVAMT70                                                         
         ZAP   SVAOR,MBTPOST     SET AOR PAY/RCVBL AMT                          
         CLI   CNTRY,C'C'                                                       
         BE    REVAMT70                                                         
         ZAP   SVAAGYCM,MBTPOST  SET AGY COMM FOR US ONLY                       
         CLC   =C'SR',MBTUNIT                                                   
         BNE   *+8                                                              
         MVI   SMULTCM,C'Y'                                                     
         B     REVAMT70                                                         
*                                                                               
REVAMT30 CLI   MBTTYP,MBTTGST    OUTPUT GST                                     
         BNE   REVAMT40                                                         
         OC    MBTPOST,MBTPOST                                                  
         BZ    *+10                                                             
         ZAP   SVGST,MBTPOST     OUTPUT GST AMOUNT                              
         B     REVAMT70                                                         
*                                                                               
REVAMT40 CLI   MBTTYP,MBTTGSTI   INPUT GST ACCOUNT                              
         BNE   REVAMT50                                                         
         OC    MBTPOST,MBTPOST                                                  
         BZ    *+10                                                             
         ZAP   SVGSTI,MBTPOST                                                   
         OC    MBTMEMO,MBTMEMO                                                  
         BZ    *+10                                                             
         ZAP   SVAAGYCM,MBTMEMO                                                 
         B     REVAMT70                                                         
*                                                                               
REVAMT50 CLI   MBTTYP,MBTTPQO    OUTPUT PST FOR PQ                              
         BE    REVAMT52                                                         
         CLI   MBTTYP,MBTTNBO    OUTPUT PST FOR NB                              
         BE    REVAMT52                                                         
         CLI   MBTTYP,MBTTNSO    OUTPUT PST FOR NS                              
         BE    REVAMT52                                                         
         CLI   MBTTYP,MBTTBCO    OUTPUT PST FOR BC                              
         BE    REVAMT52                                                         
         CLI   MBTTYP,MBTTONO    OUTPUT PST FOR ON                              
         BE    REVAMT52                                                         
         CLI   MBTTYP,MBTTPEO    OUTPUT PST FOR PE                              
         BE    REVAMT52                                                         
         CLI   MBTTYP,MBTTNFO    OUTPUT PST FOR NF                              
         BNE   REVAMT55                                                         
REVAMT52 OC    MBTPOST,MBTPOST   IF OUTPUT AMOUNT                               
         BZ    REVAMT70                                                         
         OC    SVPST,SVPST       SET OR ADD TO PST OUTPUT AMOUNT                
         BNZ   *+14                                                             
         ZAP   SVPST,MBTPOST                                                    
         B     *+10                                                             
         AP    SVPST,MBTPOST                                                    
         B     REVAMT70                                                         
*                                                                               
REVAMT55 CLI   MBTTYP,MBTTPQI    INPUT PST ACCOUNT FOR PQ                       
         BE    REVAMT58                                                         
         CLI   MBTTYP,MBTTNBI    INPUT PST ACCOUNT FOR NB                       
         BE    REVAMT58                                                         
         CLI   MBTTYP,MBTTNSI    INPUT PST ACCOUNT FOR NS                       
         BE    REVAMT58                                                         
         CLI   MBTTYP,MBTTBCI    INPUT PST ACCOUNT FOR BC                       
         BE    REVAMT58                                                         
         CLI   MBTTYP,MBTTONI    INPUT PST ACCOUNT FOR ON                       
         BE    REVAMT58                                                         
         CLI   MBTTYP,MBTTPEI    INPUT PST ACCOUNT FOR PE                       
         BE    REVAMT58                                                         
         CLI   MBTTYP,MBTTNFI    INPUT PST ACCOUNT FOR NF                       
         BNE   REVAMT60                                                         
REVAMT58 OC    MBTPOST,MBTPOST                                                  
         BZ    REVAMT70                                                         
         OC    SVPSTI,SVPSTI     SET OR ADD TO PST INPUT AMOUNT                 
         BNZ   *+14                                                             
         ZAP   SVPSTI,MBTPOST                                                   
         B     *+10                                                             
         AP    SVPSTI,MBTPOST                                                   
         B     REVAMT70                                                         
*                                                                               
REVAMT60 CLI   MBTTYP,MBTTINTI    INTERNAL INCOME POSTING                       
         BNE   REVAMT70                                                         
         OC    MBTPOST,MBTPOST                                                  
         BZ    *+10                                                             
         ZAP   SVIOR,MBTPOST                                                    
         MVC   SVACOF2,MBTOFFC    SAVE 2ND ACCOUNTING OFFICE                    
*                                                                               
REVAMT70 BAS   RE,BUMPR2          BUMP TO NEXT ELEMENT IN RECORD                
         B     REVAMT5                                                          
*                                                                               
REVAMT80 BAS   RE,SETSI           READ MEDIA INC OR AOR INC                     
         OC    MEMO,MEMO                                                        
         BZ    *+10                                                             
         ZAP   SVGRS,MEMO         SET GROSS AMOUNT                              
         OC    AMOUNT,AMOUNT                                                    
         BZ    *+10                                                             
         ZAP   SVAGYCOM,AMOUNT    SET AGENCY COMMISSION AMOUNT                  
REVAMTX  B     UNDOX                                                            
         EJECT                                                                  
*----------------------------------------------------*                          
* SETSI   - GETS MEMO FROM INCOME OR MEDIA INCOME    *                          
*----------------------------------------------------*                          
*                                                                               
SETSI    NTR1                                                                   
         XC    AMOUNT,AMOUNT                                                    
         XC    MEMO,MEMO                                                        
         L     R2,ADBUY           R2=A(POST DETAIL RECORD)                      
         BAS   RE,ADDDISP         R2=A(FIRST ELEMENT IN RECORD)                 
         USING MBTELD,R2                                                        
*                                                                               
SETSI5   CLI   0(R2),0                                                          
         BE    SETSI18                                                          
         CLI   0(R2),MBTELQ                                                     
         BNE   SETSI15                                                          
         CLI   MBTTYP,MBTTINC     INCOME ROW                                    
         BNE   SETSI15                                                          
         OC    MBTPOST,MBTPOST                                                  
         BZ    SETSI10                                                          
         ZAP   AMOUNT,MBTPOST     SAVE AMOUNT                                   
SETSI10  OC    MBTMEMO,MBTMEMO                                                  
         BZ    SETSIX                                                           
         ZAP   MEMO,MBTMEMO       SAVE MEMO                                     
         B     SETSIX                                                           
*                                                                               
SETSI15  BAS   RE,BUMPR2          BUMP TO NEXT ELEMENT IN RECORD                
         B     SETSI5                                                           
         DROP  R2                                                               
*                                 INCOME ACCOUNT NOT FOUND                      
SETSI18  L     R2,ADBUY           R2=A(POST DETAIL RECORD)                      
         BAS   RE,ADDDISP         R2=A(FIRST ELEMENT IN RECORD)                 
         USING MBTELD,R2                                                        
*                                                                               
SETSI20  CLI   0(R2),0                                                          
         BE    SETSIX                                                           
         CLI   0(R2),MBTELQ                                                     
         BNE   SETSI30                                                          
         CLI   MBTTYP,MBTTARI     AOR INCOME ROW                                
         BNE   SETSI30                                                          
         OC    MBTPOST,MBTPOST                                                  
         BZ    SETSI25                                                          
         ZAP   AMOUNT,MBTPOST     SAVE AMOUNT                                   
SETSI25  OC    MBTMEMO,MBTMEMO                                                  
         BZ    SETSIX                                                           
         ZAP   MEMO,MBTMEMO       SAVE MEMO                                     
         B     SETSIX                                                           
*                                                                               
SETSI30  BAS   RE,BUMPR2          BUMP TO FIRST ELEMENT IN RECORD               
         B     SETSI20                                                          
*                                                                               
SETSIX   B     UNDOX                                                            
         DROP  R2                                                               
         EJECT                                                                  
*-------------------------------------------------------------*                 
* UNDTL - ADDS BOGUS POST DETAIL RECORD TO INDICATE POSTINGS  *                 
*         WERE REVERSED                                       *                 
*-------------------------------------------------------------*                 
*                                                                               
UNDTL    NTR1                ADD FAKE DTL RECORD                                
         GOTO1 CLRAREA,DMCB,V(PSTREC),V(PSTRECX)                                
         L     R2,ADBUY           PT TO POST DETAIL RECORD                      
         LA    R3,ACCORFST        KEY LENGTH OF RECORD                          
         L     R4,=V(PSTREC)                                                    
         AHI   R4,4               PT TO RECORD                                  
         MVC   0(ACCORFST,R4),0(R2)      MOVE KEY OF RECORD                     
         AHI   R4,ACCORFST        R4=A(ELEMENT POSTION IN RECORD)               
*                                                                               
         USING GDAELD,R4                                                        
         MVI   GDAEL,GDAELQ                                                     
         MVI   GDALN,GDALNQ                                                     
         MVI   GDATYPE,GDATMBD                                                  
         GOTO1 DATCON,DMCB,(0,BDATE),(1,GDADATE)                                
         AHI   R3,GDALNQ          TOTAL LENGTH OF RECORD                        
*                                                                               
         L     R4,=V(PSTREC)                                                    
         LR    R1,R4                                                            
         LA    R4,4(R4)                                                         
         USING MPDRECD,R4                                                       
         STCM  R3,3,MPDRLEN       TOTAL LENGTH OF RECORD                        
         LA    R3,4(R3)           RECORD + HEADER                               
         STCM  R3,3,0(R1)                                                       
*                                                                               
         MVI   SEQONE,C'Y'        REVERSAL RECORD                               
         MVI   DETL,C'Y'          DETAIL RECORD                                 
         GOTO1 =A(PUTSORT),DMCB,(RC),V(PSTREC)                                  
UNDTLX   B     UNDOX                                                            
         DROP  R4                                                               
         DROP  R5                                                               
         EJECT                                                                  
*-------------------------------------------------------------*                 
* UNMARK -MARKS BILL RECORD UN-TRANSFERED                     *                 
*-------------------------------------------------------------*                 
*                                                                               
UNMARK   NTR1                                                                   
         MVI   TRUEAOR,C'N'                                                     
         L     RF,UTL                                                           
         MVC   4(1,RF),SAVSYS                                                   
         LA    R1,DMCB                                                          
         LA    RE,DMRDHI                                                        
         ST    RE,0(R1)                                                         
         CLI   QOPT4,C'U'         IF REVERSAL TO UNPOST                         
         BNE   *+8                                                              
         MVI   0(R1),X'08'        PASS BACK DELETED RECORDS                     
         L     RE,ADBILL          RE-READ BILL REC JUST UNTRANSFERRED           
         XC    KEY,KEY                                                          
         MVC   KEY(L'BKEY),0(RE)                                                
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,,SPTDIR,KEY,KEY                                     
         CLC   KEY(L'BKEY),KEYSAVE                                              
         BE    *+6                                                              
         DC    H'0'                                                             
         TM    MXOPT,MXNOWRIT     IF WRITE=NO -                                 
         BO    UNMARK8            DON'T UNMARK BILL RECORDS                     
         NI    KEY+13,X'BE'       TURN OFF TRANS '01' & CLOSEOUT X'40'          
         GOTO1 DATAMGR,DMCB,DMWRT,SPTDIR,KEY,KEY                                
         CLI   DM3,0                                                            
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
UNMARK8  MVC   AREC,AMYAREA                                                     
         L     R5,AMYAREA                                                       
         USING BILLRECD,R5                                                      
*                                                                               
UNMARK10 GOTO1 DATAMGR,DMCB,GETREC,SPTFILE,KEY+14,AREC,DMWORK                   
         CLI   TRUEAOR,C'Y'       1ST TIME PASS?                                
         BNE   UNMARK12                                                         
         CLC   RUNDT,BDATE        NO - LOOKING FOR TRUE AOR BILL                
         BE    *+6                                                              
         DC    H'0'                                                             
         TM    BILSTAT,BSTTAORQ   BILL MUST BE TRUE AOR                         
         BNO   UNMARK20                                                         
         MVI   TRUEAOR,C'M'       FOUND IT - UNMARK REC & DIRECTORY             
         B     UNMARK15                                                         
*                                                                               
UNMARK12 TM    BILSTAT,BSTCAORQ   1ST TIME THROUGH - SET FLG IF NECESS          
         BNO   UNMARK15                                                         
         TM    BILSTAT,BSTSADJQ                                                 
         BO    UNMARK15                                                         
         TM    BILSTAT,BSTSNETQ                                                 
         BO    UNMARK15                                                         
         MVI   TRUEAOR,C'Y'                                                     
*                                                                               
UNMARK15 TM    MXOPT,MXNOWRIT     IF WRITE=NO -                                 
         BO    UNMARK18           DON'T UNMARK BILL RECORDS                     
         NI    BCNTRL,X'BE'       TURN OF TRANS & CLOSEOUT '41'                 
         XC    BILPOST,BILPOST    AND CLEAR BILL DATE                           
         GOTO1 DATAMGR,DMCB,PUTREC,SPTFILE,KEY+14,AREC,DMWORK                   
*                                                                               
UNMARK18 CLI   TRUEAOR,C'Y'       IF CLIENT AOR -ALSO MARK TRUE                 
         BNE   UNMARK30                                                         
UNMARK20 MVC   KEYSAVE,KEY                                                      
         LA    R1,DMCB                                                          
         LA    RE,DMRSEQ                                                        
         ST    RE,0(R1)                                                         
         CLI   QOPT4,C'U'         IF REVERSAL TO UNPOST                         
         BNE   *+8                                                              
         MVI   0(R1),X'08'        PASS BACK DELETED RECORDS                     
         GOTO1 DATAMGR,DMCB,,SPTDIR,KEY,KEY                                     
         B     UNMARK10                                                         
*                                                                               
UNMARK30 CLI   TRUEAOR,C'M'                                                     
         BNE   UNMARKX                                                          
         TM    MXOPT,MXNOWRIT     IF WRITE=NO -                                 
         BO    UNMARKX            DON'T UNMARK BILL RECORDS                     
         NI    KEY+13,X'BE'       TURN OF TRANS AND CLOSEOUT X'41'              
         GOTO1 DATAMGR,DMCB,DMWRT,SPTDIR,KEY,KEY                                
         CLI   DM3,0                                                            
         BE    *+6                                                              
         DC    H'0'                                                             
UNMARKX  B     UNDOX                                                            
         DROP  R5                                                               
         EJECT                                                                  
*=============*                                                                 
* LITERAL POOL*                                                                 
*=============*                                                                 
*                                                                               
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB,R9              DROP PREVIOUS BASE REGS                       
         SPACE                                                                  
         EJECT                                                                  
         DS    0D                                                               
PRTREV   NMOD1 0,**PRTRV*                                                       
         L     RC,0(R1)                                                         
         LA    R6,4095(RC)                                                      
         LA    R6,1(R6)                                                         
         USING SPMXWRKD,RC,R6     LOCAL WORKING STORAGE                         
         LA    R7,4095(RA)                                                      
         LA    R7,1(R7)                                                         
         USING SPWORKD,RA,R7      GLOBAL WORKING STORAGE                        
*                                                                               
         LA    R2,PRTBLOCK                                                      
         USING MXPRNTD,R2                                                       
         GOTO1 =A(SETPRT),DMCB,(RC)                                             
         MVI   PMODE,PMBILL       PRINT BILL INFO                               
         MVI   PREV,C'Y'          INDICATE REVERSAL                             
         OC    AORERRS,AORERRS                                                  
         BZ    *+8                                                              
         OI    UNERR,UNEAOR       ERROR WITH AOR SET                            
         MVC   PREVERR,UNERR      REVERSAL ERRORS                               
         MVC   PRRCV,SVRCV        REVERSAL RECEIVABLE AMOUNT                    
         MVC   PRGRS,SVGRS        REVERSAL GROSS AMOUNT                         
         MVC   PRNET,SVNET        REVERSAL NET AMOUNT                           
         MVC   PRAGYCOM,SVAGYCOM  REVERSAL AGENCY COMISSION AMOUNT              
         MVC   PRAOR,SVAOR        REVERRSAL AOR PAY/RCVBL AMOUNT                
         XC    PRCD,PRCD          REVERSAL CD AMOUNT                            
         MVC   PRGST,SVGST        REVERSAL GST AMOUNT                           
         MVC   PRGSTI,SVGSTI      REVERSAL AOR GST INPUT AMOUNT                 
         MVC   PRPSTO,SVPST       REVERSAL PST AMOUNT                           
         MVC   PRPSTI,SVPSTI      REVERSAL AOR PST INPUT AMOUNT                 
         MVC   PRIOR,SVIOR        REVERSAL IOR AMOUNT                           
         MVC   PRAAGYCM,SVAAGYCM  REVERSAL AOR AGY COM AMOUNT                   
         MVC   PRFLPAOR,SFLIPAOR  REVERSAL FLIP AOR AMOUNT                      
         MVC   PRMULTCM,SMULTCM   REVERSAL FLIP(MULT) AGY COMM AMOUNT           
         MVI   PPOSTERR,0         CLEAR OUT POSTING ERRORS                      
         MVI   PPOSTER2,0                                                       
         MVI   PPOSTERC,0                                                       
         GOTO1 =V(MXPRNT),DMCB,(R2)                                             
         XIT1                                                                   
         DROP  R2                                                               
         SPACE 2                                                                
*-------------------------------------------------*                             
* IF THERE IS A MASTER PRODUCT CODE ON THE BILL   *                             
* REC OVERWRITE THE USER FIELDS WITH THE USER     *                             
* FIELDS FROM THAT MASTER PRODUCT.                *                             
*-------------------------------------------------*                             
*                                                                               
         USING BILLRECD,R5                                                      
GETUSER  NTR1  BASE=*,LABEL=*                                                   
         NI    MXERR,X'FF'-(MXNPCLT+MXNMPRD+MXNMPRDE)                           
         CLI   BMASPRD,0           ONE BYTE MASTER PRODUCT?                     
         BNE   GETU05              YES                                          
         CLC   BMASPRDC,SPACES     THREE BYTE MASTER PRODUCT (FOR NET)?         
         BNH   GETUX               NO MASTER PRODUCT CODE-DONE                  
*                                                                               
GETU05   MVC   SVPUSER1,SPACES       CLEAR THE USER FIELDS                      
         MVC   SVPUSER2,SPACES                                                  
         MVC   SVEUSER1,SPACES                                                  
         MVC   SVEUSER2,SPACES                                                  
         MVC   SVUCMP,SPACES         AND UCOMMS TOO                             
         MVC   SVUCME,SPACES                                                    
         MVC   SVUCMM,SPACES                                                    
         MVC   SVEPONUM,SPACES     PO NUMBER                                    
*                                                                               
         CLC   BMASPRDC,SPACES     THREE BYTE MASTER PRODUCT (FOR NET)?         
         BNH   *+14                                                             
         MVC   SVMPRD,BMASPRDC     SAVE MASTER PRODUCT                          
         B     GETU17                                                           
*                                                                               
         USING CLTHDRD,RE                                                       
         L     RE,ADCLT            A(CLIENT REC)                                
         LA    RE,CLIST            PRODUCT CODE LIST                            
         LHI   RF,220              220 MAX PRODUCTS IN CLIST                    
GETU10   CLC   3(1,RE),BMASPRD     COMPARE ON BINARY PRODUCT #                  
         BE    GETU15                                                           
         LA    RE,4(RE)                                                         
         BCT   RF,GETU10                                                        
*                                                                               
         CLI   SYS,C'N'            NOT FOUND IN CLIST? NOW CHECK IN             
         BNE   GETU14              CLIST2 (NEW EXTENSTION) IF NET               
         L     RE,ADCLT            SYSTEM ONLY.                                 
         LA    RE,CLIST2                                                        
         LHI   RF,32               32 MAX PRODUCTS IN CLIST2                    
         OC    0(3,RE),0(RE)       ANYTHING IN THIS NEW EXTENSION?              
         BZ    GETU14              NO-ERROR                                     
GETU12   CLC   3(1,RE),BMASPRD     COMPARE ON BINARY PRODUCT #                  
         BE    GETU15                                                           
         LA    RE,4(RE)                                                         
         BCT   RF,GETU12                                                        
GETU14   OI    MXERR,MXNPCLT       DID NOT FIND PROD FOR THIS CLIENT            
         B     *+10                                                             
*                                                                               
GETU15   MVC   SVMPRD,0(RE)       SAVE MASTER PRODUCT                           
         DROP  RE                                                               
*                                                                               
GETU17   L     RF,UTL                                                           
         MVC   4(1,RF),SAVSYS                                                   
         XC    KEY,KEY                                                          
         LA    R2,KEY             READ PRODUCT RECORD W/ MASTER PROD            
         USING PKEY,R2                                                          
         MVC   PKEYTYPE,=X'00'                                                  
         MVC   PKEYAM,BAGYMD                                                    
         MVC   PKEYCLT,BCLT                                                     
         MVC   PKEYPRD,SVMPRD      MOVE IN MASTER PRODUCT                       
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE                                                  
         BE    *+12                                                             
         OI    MXERR,MXNMPRD       NO MASTER PRODUCT REC FOR THIS PROD          
         B     GETU20                                                           
*                                                                               
         L     R2,ADBUY            USING ADBUY TO READ PROD REC                 
         ST    R2,AREC             BECAUSE I'M NOT READING BUYS                 
         GOTO1 GET                                                              
         MVC   SVPUSER1,PUSER1     SAVE PRODUCT USER FIELD1                     
         OC    SVPUSER1,SPACES                                                  
         MVC   SVPUSER2,PUSER2     SAVE PRODUCT USER FIELD2                     
         OC    SVPUSER2,SPACES                                                  
         DROP  R2                                                               
*                                                                               
GETU20   L     RF,UTL                                                           
         MVC   4(1,RF),SAVSYS                                                   
         XC    KEY,KEY                                                          
         LA    R2,KEY             READ ESTIMATE RECORD W/ MASTER PROD           
         USING EKEY,R2                                                          
         MVC   EKEYTYPE,=X'00'                                                  
         MVC   EKEYAM,BAGYMD                                                    
         MVC   EKEYCLT,BCLT                                                     
         MVC   EKEYPRD,SVMPRD      MOVE IN MASTER PRODUCT                       
         MVC   EKEYEST,BKEYEST       MOVE IN ESTIMATE                           
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE                                                  
         BE    *+12                                                             
         OI    MXERR,MXNMPRDE      NO MASTER PROD REC FOR THIS EST              
         B     GETU30                                                           
*                                                                               
         L     R2,ADBUY            USING ADBUY TO READ ESTIMATE REC             
         ST    R2,AREC             BECAUSE I'M NOT READING BUYS                 
         GOTO1 GET                                                              
         MVC   SVEUSER1,EUSER1     SAVE ESTIMATE USER FIELD1                    
         OC    SVEUSER1,SPACES                                                  
         MVC   SVEUSER2,EUSER2     SAVE ESTIMATE USER FIELD2                    
         OC    SVEUSER2,SPACES                                                  
*                                                                               
GETU30   LA    RE,UCOMBLK         NOW READ NEW UCOM PRODUCT USER                
         LA    RF,L'UCOMBLK       DEFINITION FIELDS WITH MASTER PROD            
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         LA    R3,UCOMBLK                                                       
         USING DDUCOMD,R3                                                       
         MVC   UCACOMF,ACOMFACS    A(COMFACS)                                   
         MVC   UCSYS,SYS           SYSTEM                                       
         MVC   UCPROG,=C'MX'       CALLING PGM(THIS MEANS CHK MBI BIT)          
         MVC   UCSAM,BAGYMD        HEX AGENCY/MEDIA                             
         MVC   UCSCLT,BCLT         CLIENT                                       
         MVC   UCPRD,SVMPRD        MASTER PRODUCT                               
         OI    UCOPT,UCOPRD        RETURN PRODUCT UCOMS                         
         GOTO1 =V(DDUCOM),UCOMBLK                                               
         CLI   UCERROR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         TM    UCDATA,UCDNOCLT+UCDNOPRD     NO CLIENT/PRODUCT FOUND?            
         BNZ   GETU40                                                           
         MVC   SVUCMPLN,UCPLENS    LENGTHS OF 4 PRODUCT UCOM FIELDS             
         L     RF,UCPTTLS                                                       
         MVC   SVUCMP1D(L'SVUCMDP),0(RF) PRODUCT UCOMS DESCRIPTIONS             
         L     RF,UCPDATA                                                       
         MVC   SVUCMP1(L'SVUCMP),0(RF) PRODUCT UCOMS                            
         OC    SVUCMP1D(L'SVUCMDP),SPACES                                       
         OC    SVUCMP1(L'SVUCMP),SPACES                                         
         DROP  R3                                                               
*                                                                               
GETU40   LA    RE,UCOMBLK         NOW READ NEW UCOM ESTIMATE USER               
         LA    RF,L'UCOMBLK       DEFINITION FIELDS WITH MASTER PRODUCT         
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         LA    R3,UCOMBLK                                                       
         USING DDUCOMD,R3                                                       
         MVC   UCACOMF,ACOMFACS    A(COMFACS)                                   
         MVC   UCSYS,SYS           SYSTEM                                       
         MVC   UCPROG,=C'MX'       CALLING PGM(THIS MEANS CHK MBI BIT)          
         MVC   UCSAM,BAGYMD        HEX AGENCY/MEDIA                             
         MVC   UCSCLT,BCLT         CLIENT                                       
         MVC   UCPRD,SVMPRD        MASTER PRODUCT                               
         MVC   UCSEST,BKEYEST      ESTIMATE                                     
         MVC   UCMOS,BKEYYSRV      MOS                                          
         OI    UCOPT,UCOEST+UCOEFF RETURN ESTIMATE UCOMS W/I EFF RANGE          
         GOTO1 =V(DDUCOM),UCOMBLK                                               
         CLI   UCERROR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         TM    UCDATA,UCDNOCLT+UCDNOEST     NO CLIENT/ESTIMAT FOUND?            
         BNZ   GETUX                                                            
         MVC   SVUCMELN,UCELENS    LENGTHS OF 4 ESTIMATE UCOM FIELDS            
         L     RF,UCETTLS                                                       
         MVC   SVUCME1D(L'SVUCMDE),0(RF) ESTIMATE UCOMS DESCRIPTIONS            
         L     RF,UCEDATA                                                       
         MVC   SVUCME1(L'SVUCME),0(RF) ESTIMATE UCOMS                           
         OC    SVUCME1D(L'SVUCMDE),SPACES                                       
         OC    SVUCME1(L'SVUCME),SPACES                                         
*                                                                               
GETUX    XIT1                                                                   
         DROP  R2,R5                                                            
         EJECT                                                                  
                                                                                
*======================================================================         
* LITERAL POOL                                                                  
*======================================================================         
         LTORG                                                                  
         DROP  RB                 DROP PREVIOUS BASE REGS                       
         EJECT                                                                  
                                                                                
*======================================================================         
* OTHER VARIOUS DSECTS                                                          
*======================================================================         
*  ACGENFILE                                                                    
*  ACGENPOST                                                                    
*  ACPOSTD                                                                      
*  DMWRKRK                                                                      
*  DDCROSSD                                                                     
*  DDLOGOD                                                                      
*  DDMASTD                                                                      
*  DDOFFICED                                                                    
*  FASSBOFF                                                                     
*  SPGENBILL                                                                    
*  SPGENCLT                                                                     
*  SPGENPRD                                                                     
*  SPGENPRG                                                                     
*  SPGENEST                                                                     
*  SPREPMODES                                                                   
*  SPGENAOR                                                                     
*  CTGENFILE                                                                    
*  SPREPWORKD                                                                   
*  DDUCOMD                                                                      
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
       ++INCLUDE ACGENPOST                                                      
       ++INCLUDE ACPOSTD                                                        
       ++INCLUDE ACTRAPRVD                                                      
       ++INCLUDE MXPOSTD                                                        
       ++INCLUDE MXPRNTD                                                        
       ++INCLUDE MXTBDSECT                                                      
       ++INCLUDE DMWRKRK                                                        
       ++INCLUDE DDLOGOD                                                        
       ++INCLUDE DDMASTD                                                        
       ++INCLUDE DDREMOTED                                                      
       ++INCLUDE DMDTFIS                                                        
       ++INCLUDE DDOFFICED                                                      
       ++INCLUDE SPREPMODES                                                     
       ++INCLUDE SPGENAOR                                                       
       ++INCLUDE CTGENFILE                                                      
       ++INCLUDE DDUCOMD                                                        
       ++INCLUDE DMWRKRD                                                        
       ++INCLUDE DDCROSSD                                                       
       ++INCLUDE FASSBOFF                                                       
       ++INCLUDE ACBMONVALD                                                     
       ++INCLUDE DDCOMFACS                                                      
BILLRECD DSECT                                                                  
       ++INCLUDE SPGENBILL                                                      
AGYHDRD  DSECT                                                                  
       ++INCLUDE SPGENAGY                                                       
CLTHDRD  DSECT                                                                  
       ++INCLUDE SPGENCLT                                                       
PRDHDRD  DSECT                                                                  
       ++INCLUDE SPGENPRD                                                       
*                                                                               
       ++INCLUDE SPGENPRG                                                       
*                                                                               
ESTHDRD  DSECT                                                                  
       ++INCLUDE SPGENEST                                                       
MKTRECD  DSECT                                                                  
       ++INCLUDE SPGENMKT                                                       
         EJECT                                                                  
*                                                                               
         PRINT ON                                                               
       ++INCLUDE SPREPWORKD                                                     
*                                                                               
         ORG   QSTAUTO                                                          
QBTYPE   DS    CL3                BILL TYPE                                     
         ORG                                                                    
         ORG   QBOOK1                                                           
QTRADT   DS    CL6                TRANSFER DATE                                 
         ORG                                                                    
         ORG   QRERATE                                                          
QIORDT   DS    CL6                INVOICE OR RUN DATE                           
         ORG                                                                    
*                                                                               
         ORG   Q2USER             SECOND REQUEST CARD                           
QBILL1   DS    CL6                START BILL NUMBER                             
QBILL2   DS    CL6                END BILL NUMBER(OR SPACES)                    
         ORG                                                                    
*                                                                               
SPMXWRKD DSECT                                                                  
MAXCI    EQU   220                                                              
EOR      EQU   0                   END OF RECORD                                
YES      EQU   C'Y'                                                             
NO       EQU   C'N'                                                             
         DS    0F                                                               
WFILE    DS    A                  WORKER FILE                                   
NXTPSTE  DS    F                  A(NXT POS- NEW ELEM IN PST DTL REC)           
*                                                                               
AAORBIL1 DS    A                                                                
AAORBIL2 DS    A                                                                
AAORBIL3 DS    A                                                                
FULL2    DS    A                                                                
AMYAREA  DS    A                                                                
AMYAREA2 DS    A                                                                
AMYAREA3 DS    A                                                                
APOST    DS    A                                                                
ACPOSTER DS    A                  A(ACPOSTER)                                   
APRVTAB  DS    A                  A(PRVTAB)                                     
AMXBLK   DS    A                  A(MXBLK)                                      
AOFFICER DS    A                  A(OFFICER)                                    
SVADDR   DS    A                                                                
SVADDR2  DS    A                                                                
*                                                                               
SAVSYS   DS    XL1                SAVED SE NUMBER                               
SALPH    DS    CL2                AGENCY ALPHA FOR MEDIA SPLIT FILES            
ACCALPH  DS    CL2                AGENCY ALPHA FOR ACC CONNECTION               
ACCOID   DS    XL2                NUMERIC ID FOR ACC CONNECTION                 
ACCTSE2  DS    XL1                OTHER ACC FILE SE #                           
ACCTCD2  DS    XL1                OTHER ACC FILE COMPANY CODE                   
ACCTOID1 DS    XL2                NUMERIC ALPHA ID                              
ACCTOID2 DS    XL2                OTHER ACC FILE NUMERIC ALPHA ID               
RDSE     DS    XL1                SE NUMBER OF FILE READING                     
PERR     DS    XL1                INTERNAL ERRORS                               
PNOACC   EQU   X'40'              NO ACCOUNT NAME                               
PNOPOST  EQU   X'80'              POSTING NOT SUCESSFUL                         
POST     DS    CL1                Y=POST SUCCESSFUL                             
MXOPT    DS    XL1                INTERNAL CONTROL VALUES                       
MXNOPOST EQU   X'80'              DO NOT POST TO WORKER FILE                    
MXNOWRIT EQU   X'40'              DO NOT WRITE ANY RECORDS                      
MXMBSOB  EQU   X'20'              SAME NIGHT POSTING                            
TRAREC   DS    CL1                Y= TRANSACTION RECORD FOUND FOR ACC           
CONERR   DS    CL1                ERROR WITH CONTRA ACCOUNT                     
CNTRY    DS    CL1                C'C'=CANADIAN AGENCY                          
ANYBILL  DS    CL1                Y= BILL FOR THIS CLIENT                       
TEST     DS    CL1                                                              
PSTAOR   DS    CL1                                                              
ABILCNT  DS    XL1                                                              
BYTE2    DS    XL1                                                              
EMULATE  DS    CL1                Y=NEW ACC FILE EMULATED                       
*                                                                               
NUMABIL  DS    XL1                NUMBER OF AOR BILLS IN SET                    
PSTNUMA  DS    XL1                MAX NUMBER OF ENTRIES FOR IOAREA              
PSTNUM2  DS    XL1                MAX NUMBER OF ENTRIES FOR POSTINGS            
SYS      DS    CL1                S=SPOT,N=NET                                  
MYMED    DS    CL1                                                              
MAKEPOST DS    CL1                DEFAULT IS NOT TO MAKE ANY POSTINGS           
DETL     DS    CL1                C'Y'- DTL REC -ADD TO SIGN ON ACC             
UNERR    DS    CL1                UNDO ERROR                                    
UNENODTL EQU   X'80'              NO DETAIL RECORDS                             
UNENOTRA EQU   X'40'              NO TRANSACTION RECORD FOR AN ACC              
UNEAOR   EQU   X'20'              ERROR WITH AOR SET                            
*                                                                               
* ANY CHANGES MADE TO MXERR MUST ALSO BE MADE TO PPOSTER3 IN MXPRNTD *          
*                                                                               
MXERR    DS    XL1                ERRORS FROM THE MX PROGRAM                    
MXNPCLT  EQU   X'80'              NO PRODUCT FOUND FOR THIS CLIENT              
MXNMPRD  EQU   X'40'              NO PROD REC FOR THIS MASTER PRODUCT           
MXNMPRDE EQU   X'20'              NO EST REC FOR THIS MASTER PRODUCT            
*                                                                               
BINVOICE DS    CL6                BILL INVOICE NUMBER                           
SVMI     DS    CL1                C'Y'= ON MI RECORDS                           
SVNEWOF  DS    CL1                C'Y'= USES NEW OFFICE CODES                   
SVCOST   DS    CL1                C'Y'= USE COSTING,BILLING,REV                 
SVPROD   DS    CL2                UNIT/LEDGER                                   
SVRSYS   DS    CL1                REDEFINED SYSTEM CODE FROM B1 PROFILE         
SVRMED   DS    CL1                 REDEFINED MEDIA CODE FORM B1 PROFILE         
GSTO     DS    CL1                OUTPUT GST CODE FROM BILL                     
PSTOVALS DS    14CL12             OUTPUT PST CODE FROM BILL                     
SVCOFF   DS    CL1                SAVED CLIENT MEDIA OFFICE CODE                
ACLIOFF  DS    CL2                SAVED CLIENT ACC OFFICE CODE                  
SVACOFF  DS    CL2                SAVED CLIENT/PRODUCT ACC OFFICE CODE          
SVACOF2  DS    CL2                ACC OFFICE CODE OVERRIDE FOR INT              
SVOFFG   DS    CL2                SAVED OFFICE GROUP                            
MYCLTB   DS    XL2                                                              
MYPRD    DS    CL3                                                              
DTLREC   DS    XL1                X'01'= TWO POST DETAIL RECORDS                
SEQONE   DS    XL1                C'Y' = REVERSAL RECORD POSTING                
TRUEAOR  DS    XL1                C'Y'- MUST BE TRUE AOR BILL                   
FNDTRUE  DS    XL1                                                              
PREVINC  DS    CL1                Y= ALREADY POSTED TO MEDIA INC ACC            
PREVAINC DS    CL1                Y= ALREADY POSTED TO AOR INC ACC              
TEMPSE   DS    XL1                                                              
TEMPCD   DS    XL1                                                              
TEMPOID  DS    XL2                                                              
CHKCON   DS    XL1                MAKE SURE CONTRA ACCOUNT EXISTS               
OVRCLT   DS    XL3                OVERRIDE CLIENT & PRODUCT -- DO NOT           
OVRPRD   DS    XL3                SEPERATE THE TWO ****                         
MSGTYPE  DS    CL4                                                              
SVPROF   DS    XL16               SAVED MX (ACC) PROFILE                        
SVPROF2  DS    XL16               SAVED MX (MEDIA) PROFILE                      
B1XPROF  DS    XL16               SAVED MEDIA B1X PROFILE (MEDIA LEVEL)         
B1PROF   DS    XL16               SAVED MEDIA B1 PROF (AT LOWEST LEVEL)         
REVSTAT  DS    XL1                Y=REVERSING  - NEEDED FOR B1X PROF            
REVDATE  DS    XL2                TRANSFER DATE- NEEDED FOR B1X PROF            
*                                                                               
TOTCASH  DS    PL6                TOTAL CASH OF POSTINGS                        
RECCNT   DS    PL6                # OF RECORDS                                  
MEMO     DS    PL6                MEMO AMOUNT                                   
AMOUNT   DS    PL6                POSTING AMOUNT                                
DEBS     DS    PL6                                                              
CREDS    DS    PL6                                                              
         DS    0D                                                               
DUB2     DS    PL8                                                              
*                                                                               
SFLIPAOR DS    XL1                                                              
SMULTCM  DS    XL1                                                              
SVRCV    DS    PL6                                                              
SVGRS    DS    PL6                                                              
SVNET    DS    PL6                                                              
SVAGYCOM DS    PL6                                                              
SVGST    DS    PL6                                                              
SVPST    DS    PL6                                                              
SVAOR    DS    PL6                                                              
SVGSTI   DS    PL6                                                              
SVPSTI   DS    PL6                                                              
SVAAGYCM DS    PL6                                                              
SVIOR    DS    PL6                                                              
*                                                                               
         DS    0D                                                               
SBILGST  DS    XL4                                                              
SBILPST  DS    10CL9                                                            
SBILAMT  DS    PL6                                                              
SBILNET  DS    PL6                                                              
SBILACT  DS    PL6                                                              
*                                                                               
TEMPINV  DS    CL6                                                              
TEMPDATE DS    CL6                                                              
TRANSDT  DS    CL6                ADJUSTMENT DATE FOR BILL                      
RERUNDT  DS    CL6                DATE OF RERUN (QOPT1=D)                       
RUNDT    DS    CL6                RUNDT FOR AOR BILL                            
REQTDT   DS    XL2                REQUESTED RE-TRANSFER DATE                    
WID      DS    CL16               ID FOR WORKER FILE                            
SKEY     DS    CL33               CURRENT BILL KEY                              
SESTKEY  DS    CL(L'BKEY)         CURRENT ESTIMATE KEY                          
NEXTKEY  DS    CL(L'BKEY)         NEXT BILL RECORD TO PROCESS                   
SVEDESC  DS    CL20               ESTIMATE DESCRIPTION                          
SVUSR    DS    XL1                 NEW USER DEFINATION FIELDS                   
SVPU1    EQU   X'80'               TRANSFER PRODUCT USER FIELD 1                
SVPU2    EQU   X'40'               TRANSFER PRODUCT USER FIELD 2                
SVEU1    EQU   X'20'               TRANSFER ESTIMATE USER FIELD 1               
SVEU2    EQU   X'10'               TRANSFER ESTIMATE USER FIELD 2               
SVPUSER1 DS    CL(L'PUSER1)        PRODUCT USER FIELD 1 DATA                    
SVPUSER2 DS    CL(L'PUSER2)        PRODUCT USER FIELD 2 DATA                    
SVEUSER1 DS    CL(L'EUSER1)        ESTIMATE USER FIELD 1 DATA                   
SVEUSER2 DS    CL(L'EUSER2)        ESTIMATE USER FIELD 2 DATA                   
SVPDESC1 DS    CL(L'CPU1)          PRODUCT USER FIELD 1 DATA                    
SVPDESC2 DS    CL(L'CPU2)          PRODUCT USER FIELD 2 DATA                    
SVEDESC1 DS    CL(L'CEU1)          ESTIMATE USER FIELD 1 DATA                   
SVEDESC2 DS    CL(L'CEU2)          ESTIMATE USER FIELD 2 DATA                   
SVEPONUM DS    CL(L'EPONUM)        PO NUMBER                                    
*                                                                               
SVUCMPLN DS    0XL4                                                             
SVUP1LN  DS    XL1                 LENGTH OF 1ST PRODUCT UCOM FIELD             
SVUP2LN  DS    XL1                 LENGTH OF 2ND PRODUCT UCOM FIELD             
SVUP3LN  DS    XL1                 LENGTH OF 3RD PRODUCT UCOM FIELD             
SVUP4LN  DS    XL1                 LENGTH OF 4TH PRODUCT UCOM FIELD             
*                                                                               
SVUCMELN DS    0XL4                                                             
SVUE1LN  DS    XL1                 LENGTH OF 1ST ESTIMATE UCOM FIELD            
SVUE2LN  DS    XL1                 LENGTH OF 2ND ESTIMATE UCOM FIELD            
SVUE3LN  DS    XL1                 LENGTH OF 3RD ESTIMATE UCOM FIELD            
SVUE4LN  DS    XL1                 LENGTH OF 4TH ESTIMATE UCOM FIELD            
*                                                                               
SVUCMMLN DS    0XL4                                                             
SVUM1LN  DS    XL1                 LENGTH OF 1ST MARKET UCOM FIELD              
SVUM2LN  DS    XL1                 LENGTH OF 2ND MARKET UCOM FIELD              
SVUM3LN  DS    XL1                 LENGTH OF 3RD MARKET UCOM FIELD              
SVUM4LN  DS    XL1                 LENGTH OF 4TH MARKET UCOM FIELD              
*                                                                               
SVUCMDP  DS    0CL80                                                            
SVUCMP1D DS    CL20                UCOM PRODUCT USER FIELD DESC 1               
SVUCMP2D DS    CL20                UCOM PRODUCT USER FIELD DESC 2               
SVUCMP3D DS    CL20                UCOM PRODUCT USER FIELD DESC 3               
SVUCMP4D DS    CL20                UCOM PRODUCT USER FIELD DESC 4               
*                                                                               
SVUCMDE  DS    0CL80                                                            
SVUCME1D DS    CL20                UCOM ESTIMATE USER FIELD DESC 1              
SVUCME2D DS    CL20                UCOM ESTIMATE USER FIELD DESC 2              
SVUCME3D DS    CL20                UCOM ESTIMATE USER FIELD DESC 3              
SVUCME4D DS    CL20                UCOM ESTIMATE USER FIELD DESC 4              
*                                                                               
SVUCMDM  DS    0CL80                                                            
SVUCMM1D DS    CL20                UCOM MARKET USER FIELD DESC 1                
SVUCMM2D DS    CL20                UCOM MARKET USER FIELD DESC 2                
SVUCMM3D DS    CL20                UCOM MARKET USER FIELD DESC 3                
SVUCMM4D DS    CL20                UCOM MARKET USER FIELD DESC 4                
*                                                                               
SVUCMP   DS    0CL128                                                           
SVUCMP1  DS    CL32                UCOM PRODUCT USER FIELD 1                    
SVUCMP2  DS    CL32                UCOM PRODUCT USER FIELD 2                    
SVUCMP3  DS    CL32                UCOM PRODUCT USER FIELD 3                    
SVUCMP4  DS    CL32                UCOM PRODUCT USER FIELD 4                    
*                                                                               
SVUCME   DS    0CL128                                                           
SVUCME1  DS    CL32                UCOM ESTIMATE USER FIELD 1                   
SVUCME2  DS    CL32                UCOM ESTIMATE USER FIELD 2                   
SVUCME3  DS    CL32                UCOM ESTIMATE USER FIELD 3                   
SVUCME4  DS    CL32                UCOM ESTIMATE USER FIELD 4                   
*                                                                               
SVUCMM   DS    0CL128                                                           
SVUCMM1  DS    CL32                UCOM MARKET USER FIELD 1                     
SVUCMM2  DS    CL32                UCOM MARKET USER FIELD 2                     
SVUCMM3  DS    CL32                UCOM MARKET USER FIELD 3                     
SVUCMM4  DS    CL32                UCOM MARKET USER FIELD 4                     
*                                                                               
UPSI     DS    XL1                                                              
UPSISOUT EQU   X'80'              .  SORTER OUT                                 
UPSIPUTS EQU   X'40'              .  SORTER IN                                  
*                                                                               
FORCECWK DS    CL1                 YES/NO, FORCE WORKER FILE TO CLOSE           
#WRKFS   DS    XL1                 # OF WORKER FILES CREATED                    
MAX#WKF  DS    XL1                 MAX NUMBER OF WORKER FILES                   
#AGY     DS    XL1                 # OF AGENCIES WORKER FILE TOTALS             
MAXAGY   EQU   10                                                               
PRVINV   DS    CL(L'SRTINV)                                                     
SVBLFLT  DS    CL(L'BLFLT)                                                      
SVCNET   DS    XL4                 CALCULATED NET                               
SVBNET   DS    PL6                 MIDAS EXCHANGE (FOR NB MEMO)                 
SVINVL   DS    CL10                LONGER INVOICE NUMBER                        
*                                                                               
BIT      DS    XL1                                                              
PRDOFFC  EQU   X'80'              PRODUCT OFF OVERRIDE FOUND                    
SVFLAG   DS    XL1                FLAG FOR MX                                   
COS2FLG  EQU   X'01'              COS2 BILL (BSTC2Q)                            
SVMPRD   DS    CL3                SAVED MASTER PRODUCT CODE                     
SVBTNUM  DS    CL2                BTYPE NUMBER B4,B5,B6,B7                      
TYPEBIL  DS    CL3                TYPE OF BILL (AOR,REG,ETC...)                 
STYPEBIL DS    CL3                SAVED TYPEBIL                                 
MTYPEBIL DS    CL5                TYPEBIL FOR MXPOST                            
SMTYPBIL DS    CL5                SAVED TYPEBIL FOR MXPOST                      
TRUEAMT  DS    PL6                BACTP OF BILL # 2                             
TRUEGST  DS    XL4                BVATAMT OF BILL # 2                           
TRUEGSTI DS    CL1                TRUE INPUT GST CODE                           
TRUEINV  DS    CL6                INVOICE # OF BILL2 (TRUE AOR BILL)            
BINVL    DS    CL10               LONGER INVOICE NUMBER                         
TRUEINVL DS    CL10                AOR TRUE LONGER INVOICE NUMBER               
PSTIVALS DS    14CL12             TRUE INPUT PST INFORMATION                    
         DS    0CL51              DON'T SEPERATE THE TWO                        
CONTRA   DS    CL15               COMPANY CODE & CONTRA ACCOUNT NUMBER          
CONTRAN  DS    CL36               CONTRA ACCOUNT NAME                           
CONLVL   DS    CL(L'ACPLVL)       LEVEL OF ACCOUNT                              
*                                                                               
PGRCD    DS    CL4                PRODUCT CODE                                  
PGRNAME  DS    CL24               PRODUCT NAME                                  
*                                                                               
SJCLTNM  DS    CL36               SJ CLIENT RECORD ACC NAME                     
SJPRDNM  DS    CL36               SJ PRODUCT RECORD ACC NAME                    
*                                                                               
SVCURR   DS    CL1                SAVE CURRENCY                                 
*                                                                               
INCRIDE  DS    CL51               INCOME CONTRA OVERRIDE NAME                   
AKEY     DS    XL42               ACCOUNTING KEYS MUST BE 42                    
SVAKEY   DS    XL42                                                             
ELEM     DS    CL256              ELEMENT                                       
UCOMBLK  DS    CL(UCOMDLNQ)                                                     
XTRAREA  DS    CL1000                                                           
*                                                                               
*              TABLE OF MXCOMMON ROUTINES                                       
*                                                                               
COMTAB   DS    0A                                                               
MXPROF   DS    V                                                                
SETMOA   DS    V                                                                
CLRAREA  DS    V                                                                
GTNAME   DS    V                                                                
         DS    4V                  SPARE                                        
NCOMTAB  EQU   (*-COMTAB)/L'COMTAB                                              
*                                                                               
         DS    0F                                                               
AGYTBL   DS    CL(MAXAGY*AGYTBLL)   ALPHA AGENCY NAME                           
AORERRS  DS    CL(MAXAOR*AORERRL)   ERROR TABLE FOR A SET OF AOR BILLS          
MAXAOR   EQU   4                                                                
*                                                                               
         DS    D                                                                
PRTBLOCK DS    CL(PLENGTH)        REQUEST CONTROL BLOCK FOR MXPRNT              
ACPBLK   DS    CL(ACPOSTL)        REQUEST CONTROL BLOCK FOR ACPOSTER            
TEMPROW  DS    CL(ACPRTNL2)       LENGTH OF ONE ROW RETURNED                    
PROFWORK DS    CL20               VALUE FROM PROFILE REQUESTED                  
         EJECT                                                                  
* - AGYTBLD-TABLE FULL AGENCY NAME FOLLOWED BY TOTAL AMOUNT ADDED               
*           TO WORKER FILE ( ONE ENTRY PER WORKER FILE CREATED                  
AGYTBLD  DSECT                                                                  
AGYOID   DS    XL2                AGENCY OID                                    
AGYTNM   DS    CL6                NAME                                          
AGYTAMT  DS    PL6                TOTAL FOR WORKER FILE(S)                      
AGYTBLL  EQU   (*-AGYTBLD)                                                      
         SPACE                                                                  
* - CONTBLD-TABLE POSTING NUMBER WITH ITS CONTRA/CONTRA NAME                    
CONTBLD  DSECT                                                                  
CONTYPE  DS    XL1                ACCOUNT TYPE                                  
CONTYPE2 DS    XL1                IF CONTYPE=05(COSTING) TYPE2=06,07            
CONACC   DS    CL15               CONTRA ACCOUNT                                
CONNAME  DS    CL36               CONTRA ACCOUNT NAME                           
CONTBLL  EQU   (*-CONTYPE)                                                      
         SPACE 2                                                                
* - AORERD - TABLE OF ERRORS FOR AN AOR SET                                     
AORERD   DSECT                                                                  
AORERR   DS    XL1                POSTING ERRORS                                
AORERRC  DS    XL1                POSTING ERRORS CONTINUED                      
AORCERR  DS    XL1                CONTRA ACCOUNT ERROR                          
         DS    XL26               SPARE                                         
AORERRL  EQU   (*-AORERR)                                                       
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'139SPREPMX02 06/26/19'                                      
         END                                                                    
