*          DATA SET SPREPDG02  AT LEVEL 003 AS OF 10/12/04                      
*PHASE SPDG02A                                                                  
                                                                                
*=====================================================================          
* THIS PROGRAM READS THE SEQUENTIAL DATASET GENERATED BY SPREPDQ02              
* AND GENERATES DATASETS FOR TRANSMISSION TO IQ.                                
*                                                                               
* FIRST CHARACTER (RECORD TYPE) IS STRIPPED BEFORE OUTPUT                       
* C'H' AND C'A' RECORDS GO TO AVAIL FILE                                        
* C'S' RECORDS GO TO DETAIL FILE                                                
*                                                                               
*   CALL DYNALLOC TO OPEN OUTPUT DCB'S                                          
*   WHEN IT GETS TO EOF, CLOSE AND FREE THE DCB                                 
*   CALL DYNALLOC AGAIN WITH NEW FILENAME ...                                   
*=====================================================================          
                                                                                
SPDG02   TITLE 'GENERATE DATASETS FOR TRANSMISSION TO IQ'                       
         PRINT NOGEN                                                            
SPDG02   CSECT                                                                  
*                                                                               
         DS    4096C                                                            
         ORG   *-4096                                                           
*                                                                               
         NMOD1 0,SPDG02                                                         
*                                                                               
         L     RA,0(R1)                                                         
         LR    R9,RA                                                            
         AHI   R9,4096                                                          
         USING SPWORKD,RA,R9                                                    
*                                                                               
         L     RC,=A(SPDG02WK)                                                  
         USING SPDG02WK,RC                                                      
*                                                                               
         CLI   MODE,REQFRST                                                     
         BE    REQF                                                             
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
*                                                                               
REQF     OPEN  (IQFILE,(INPUT))                                                 
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
REQ10    L     R0,ADBUY                                                         
         AHI   R0,-4                                                            
         GET   IQFILE,(0)                                                       
*                                                                               
         L     R8,ADBUY                                                         
         USING IQREC,R8                                                         
*                                                                               
         CLI   IQTYPE,C'H'                                                      
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
*==========================================================                     
* BUILD FILENAME                                                                
* D.<DATE>.M<MONSVC>.<MEDCLT>.<MSTREST>.<A/S>.OUT                               
*==========================================================                     
                                                                                
         MVC   AVLNAME,SPACES                                                   
*                                                                               
         LA    R4,AVLNAME                                                       
         MVI   0(R4),C'D'                                                       
         AHI   R4,1                                                             
         MVI   0(R4),C'.'                                                       
         AHI   R4,1                                                             
*                                                                               
         GOTO1 DATCON,DMCB,(5,0),(X'20',(R4))  GET NUMERIC YYMMDD               
         AHI   R4,6                                                             
         MVI   0(R4),C'.'                                                       
         AHI   R4,1                                                             
*                                                                               
         MVI   0(R4),C'M'          MONTH INDICATOR                              
         MVC   1(2,R4),IQMONTH                                                  
         MVC   3(2,R4),IQYEAR+2                                                 
         AHI   R4,5                                                             
         MVI   0(R4),C'.'                                                       
         AHI   R4,1                                                             
*                                                                               
         MVC   0(1,R4),IQMED                                                    
         MVC   1(3,R4),IQCLT                                                    
         AHI   R4,4                                                             
         MVI   0(R4),C'.'                                                       
         AHI   R4,1                                                             
*                                                                               
         PACK  DUB,IQEST                                                        
         CVB   R0,DUB                                                           
         EDIT  (R0),(3,(R4)),ALIGN=LEFT                                         
         AR    R4,R0                                                            
         MVI   0(R4),C'.'                                                       
         AHI   R4,1                                                             
*                                                                               
         MVC   0(5,R4),=C'S.OUT'   SET FOR DETAIL (SPOTS) FILE                  
         MVC   DTLNAME,AVLNAME     COPY OUTPUT TO DTL DSN                       
*                                                                               
         MVC   0(5,R4),=C'A.OUT'   THEN OVERWRITE                               
         AHI   R4,5                                                             
* FIRST TRY IS TO OPEN EXISTING FILE                                            
         LA    R0,AVLHLQ           START OF DSN                                 
         SR    R4,R0                                                            
         STH   R4,SAVELEN          SAVE LENGTH FOR NEXT OPEN                    
         GOTO1 DYNALLOC,DMCB,(C'H',=C'IQAVLOUT'),((R4),AVLHLQ),        X        
               (X'40',0)                                                        
         TM    8(R1),X'20'         TEST OPEN FAILED                             
         BZ    REQ12                                                            
*                                                                               
         GOTO1 (RF),(R1)           THEN TRY AGAIN                               
*                                                                               
REQ12    OPEN  (IQAVLOUT,(OUTPUT))                                              
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVI   OPENFLAG,C'Y'       SET OUTPUT FILE OPEN                         
*&&DO                                                                           
         LA    R4,IQAVLHDR                                                      
         MVC   0(1,R4),IQMED                                                    
         AHI   R4,1                                                             
         MVI   0(R4),C'|'                                                       
         AHI   R4,1                                                             
*                                                                               
         MVC   0(3,R4),IQCLT                                                    
         AHI   R4,2                POINT TO LAST CHAR                           
         CLI   0(R4),C' '                                                       
         BNH   *+8                                                              
         AHI   R4,1                                                             
         MVI   0(R4),C'|'                                                       
         AHI   R4,1                                                             
*                                                                               
         PACK  DUB,IQEST                                                        
         CVB   R0,DUB                                                           
         EDIT  (R0),(3,(R4)),ALIGN=LEFT                                         
         AR    R4,R0                                                            
         MVI   0(R4),C'|'                                                       
         AHI   R4,1                                                             
*                                                                               
         MVC   0(2,R4),IQMONTH                                                  
         AHI   R4,2                                                             
         MVI   0(R4),C'|'                                                       
         AHI   R4,1                                                             
*                                                                               
         MVC   0(4,R4),IQYEAR                                                   
         AHI   R4,4                                                             
         MVI   0(R4),C'|'                                                       
         AHI   R4,1                                                             
*                                                                               
         LA    R0,IQAVLHLN                                                      
         SR    R4,R0                                                            
         STH   R4,IQAVLHLN         SET HEADER LEN                               
         PUT   IQAVLOUT,(0)                                                     
*&&                                                                             
         L     R0,ADBUY            PUT HEADER AS READ WITH DEMOS                
         AHI   R0,-4                                                            
         PUT   IQAVLOUT,(0)                                                     
                                                                                
*=============================================================                  
* NOW ALLOCATE AND OPEN DTLOUT FILE                                             
*=============================================================                  
                                                                                
         LH    R4,SAVELEN          GET LENGTH OF FILENAME FOR YI                
         GOTO1 DYNALLOC,DMCB,(C'H',=C'IQDTLOUT'),((R4),DTLHLQ),        X        
               (X'40',0)                                                        
         TM    8(R1),X'20'         TEST OPEN FAILED                             
         BZ    REQ14               IT WORKED                                    
*                                                                               
         GOTO1 (RF),(R1)                                                        
*                                                                               
REQ14    OPEN  (IQDTLOUT,(OUTPUT))                                              
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
         EJECT                                                                  
REQ20    L     R0,ADBUY                                                         
         AHI   R0,-4                                                            
         GET   IQFILE,(0)                                                       
*                                                                               
         L     R8,ADBUY                                                         
         CLC   0(4,R8),=C'*EOF'                                                 
         BE    REQ30                                                            
*                                                                               
         MVC   RECTYPE,0(R8)                                                    
*                                                                               
         AHI   R8,-4               BACK UP TO RECLEN                            
         SR    R0,R0                                                            
         ICM   R0,3,0(R8)                                                       
         AHI   R0,-2               SUBTRACT 1 FOR ID + 1 FOR SEP                
         SLL   R0,16               LEFT ALIGN                                   
         STCM  R0,15,2(R8)         SET NEW LEN AND OVERWRITE REC ID             
         AHI   R8,2                                                             
*                                                                               
         L     R1,=A(IQAVLOUT)                                                  
         CLI   RECTYPE,C'A'        TEST AVAIL                                   
         BE    REQ25                                                            
         L     R1,=A(IQDTLOUT)                                                  
         CLI   RECTYPE,C'D'        TEST DETAIL                                  
         BE    REQ25                                                            
         DC    H'0'                                                             
*                                                                               
REQ25    LR    R0,R8                                                            
         PUT   (1),(0)                                                          
         B     REQ20                                                            
*                                                                               
REQ30    CLI   OPENFLAG,C'Y'       TEST OUTPUT FILE OPEN                        
         BNE   REQ35                                                            
*                                                                               
         CLOSE IQAVLOUT                                                         
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLOSE IQDTLOUT                                                         
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
                                                                                
*===========================================================                    
* DO UNALLOCATE TO FREE DCB FOR NEXT DATASET                                    
*===========================================================                    
                                                                                
         GOTO1 DYNALLOC,DMCB,(C'U',=CL8'IQAVLOUT')                              
         GOTO1 DYNALLOC,DMCB,(C'U',=CL8'IQDTLOUT')                              
*                                                                               
REQ35    MVI   OPENFLAG,C'N'       SET OUTPUT FILE CLOSED                       
*                                                                               
         CLI   EOFFLAG,C'Y'        DID WE GET TO EOF ON INPUT                   
         BNE   REQ10                                                            
         GOTO1 AENDREQ             THEN IT'S EOJ !                              
*                                                                               
INPUTEOF MVI   EOFFLAG,C'Y'                                                     
         B     REQ30                                                            
         DS    0D                                                               
         DC    CL8'SPDG02WK'                                                    
SPDG02WK DS    0D                                                               
*                                                                               
RECTYPE  DC    C' '                                                             
EOFFLAG  DC    C'N'                                                             
OPENFLAG DC    C'N'                                                             
SAVELEN  DS    H                                                                
         DS    0D                                                               
COUNTS   DS    0XL24                                                            
AVLIN    DC    PL4'0',CL20'AVAILS IN'                                           
DTLIN    DC    PL4'0',CL20'DETAILS IN'                                          
COUNTX   EQU   *                                                                
*                                                                               
         DS    0D                                                               
IQAVLHLN DS    H                                                                
         DS    H                                                                
IQAVLHDR DS    CL200                                                            
*                                                                               
* INCLUDE LOWERCASE DATA                                                        
       ++INCLUDE SPREPDGLC                                                      
*                                                                               
IQFILE   DCB   DDNAME=IQFILE,DSORG=PS,RECFM=VB,LRECL=1024,             X        
               MACRF=GM,EODAD=INPUTEOF                                          
*                                                                               
IQAVLOUT DCB   DDNAME=IQAVLOUT,DSORG=PS,RECFM=VB,LRECL=1024,MACRF=PM            
*                                                                               
IQDTLOUT DCB   DDNAME=IQDTLOUT,DSORG=PS,RECFM=VB,LRECL=1024,MACRF=PM            
*                                                                               
         DS    0D                                                               
EOFLN    DC    AL2(EOFLNX-EOFLN)   FILE SEPARATOR RECORD                        
         DC    H'0'                                                             
         DC    CL16'*EOF*EOF*EOF*EOF'                                           
EOFLNX   EQU   *                                                                
         EJECT                                                                  
IQREC    DSECT                                                                  
*                                                                               
IQTYPE   DS    CL1                 C'H'/C'A'/C'S'                               
         DS    CL1                 DELIM                                        
*                                                                               
IQMED    DS    CL1                                                              
         DS    CL1                 DELIM                                        
IQCLT    DS    CL3                                                              
         DS    CL1                 DELIM                                        
IQEST    DS    CL3                                                              
         DS    CL1                 DELIM                                        
IQMONTH  DS    CL2                                                              
         DS    CL1                 DELIM                                        
IQYEAR   DS    CL4                                                              
         DS    CL1                 DELIM                                        
         ORG                                                                    
         PRINT OFF                                                              
       ++INCLUDE SPREPWORKD                                                     
       ++INCLUDE SPREPMODES                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003SPREPDG02 10/12/04'                                      
         END                                                                    
