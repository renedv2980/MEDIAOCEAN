*          DATA SET PPREPMX02  AT LEVEL 108 AS OF 06/26/19                      
*PHASE PPMX02B                                                                  
*INCLUDE MXPRNT                                                                 
*INCLUDE MXPOST                                                                 
*INCLUDE MXCOMMON                                                               
*INCLUDE MXTABLE                                                                
*INCLUDE CONVMOS                                                                
*INCLUDE SORTER                                                                 
*INCLUDE DDUCOM                                                                 
*INCLUDE PRINT                                                                  
*INCLUDE PRNTBL                                                                 
*INCLUDE PPFMTINO                                                               
*INCLUDE BMONVAL                                                                
*INCLUDE PERVAL                                                                 
*INCLUDE PPGETPO#                                                               
         SPACE                                                                  
*-------------------------------------------------------------*                 
* NOTE ABOUT ASSEMBLY ERROR -                                 *                 
*  PPRCOST IS DEFINED IN BOTH PPNEWFILE & ACGENFILE, SORRY    *                 
*                                                             *                 
* NOTE ABOUT REVERSAL OPTIONS--                               *                 
*                                                             *                 
* QOPT1 = R    AUTOMATIC GENERATED BY PPREQ00                 *                 
* QOPT3 = R,I  FORCED - WITH DATE IN QIORDT                   *                 
* QOPT4 = M    DDS ONLY (MARK) OPTION                         *                 
*              MARKS BILLS TRANSFERED & SETS TRANSFER DATE =  *                 
*              TODAY (QOPT3 AND QIORDT MUST BE SET)           *                 
* QOPT4 = U    DDS ONLY (UNPOST) OPTION                       *                 
*              READS DELETED BILL RECORDS AND UNPOSTS         *                 
*                                                             *                 
* NOTE ABOUT SPECIAL BILLING RERUN --                         *                 
* NEW WAY:                                                    *                 
* USE CONTROL CARDS: RERUN=YES, DATE=MM/DD/YY, WRITE=NO,      *                 
* POSTING=YES  (PGM WILL SET REQUEST CARD OLD WAY)            *                 
* OLD WAY:                                                    *                 
* QOPT1 = D    IF BILLING DIES AT NIGHT - NEXT MORNING        *                 
* &QTRADT SET  RUN WITH THIS OPTION - READ TRANSFERRED BILLS  *                 
* &POSTING=YES W/ DATE GIVEN AND REGENERATE POSTINGS - AFTER  *                 
* & WRITE=NO   IT RUNS CALL SHIN SHIN TO RUN '99' DUMMY ACC   *                 
*              UPDATE TO MAKE SURE EVERYTHING O.K.            *                 
*---------------------------------------------------------------------*         
* USER    JIRA       DATE                  CHANGE LOG                 *         
* ---- ----------  -------- ------------------------------------------*         
* SMUR  SPEC-33293 03/05/19 PO NUMBER FOR MX  (19.2)                  *         
* SMUR  SPEC-9487  03/29/17 CLIENT FILTER FOR CURRENCY                *         
*---------------------------------------------------------------------*         
         TITLE 'BILLING TRANSFER - ADDS POSTINGS TO WRKFIL'                     
PPMX02   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,PPMX02,R9,R8,R7                                                
*                                                                               
         L     RA,0(R1)                                                         
         USING PPWORKD,RA         GLOBAL WORKING STORAGE                        
         L     R6,PPFILEC                                                       
         USING PPFILED,R6                                                       
         LA    RC,SPACEND                                                       
         USING PPMXWRKD,RC        LOCAL WORKING STORAGE                         
         MVI   RC2DSECT,C'Y'      USE SECOND DSECT                              
         L     R1,PPWORK2C        2ND PPG WORKD                                 
         USING PPWORK2D,R1                                                      
         LA    RE,P1              SAVE PRINT LINE                               
         STCM  RE,15,L1                                                         
         LA    RE,P2              SAVE PRINT LINE 2                             
         STCM  RE,15,L2                                                         
         LA    RE,P3              SAVE PRINT LINE 2                             
         STCM  RE,15,L3                                                         
         DROP  R1                                                               
         L     RF,UTL                                                           
         MVC   SAVSYS,4(RF)       SAVE SYSTEM NUMBER                            
*   FIRST'S                                                                     
*                                                                               
         CLI   MODE,RUNFRST       RUN FIRST                                     
         BE    RUNF                                                             
         CLI   MODE,FESTREQ       REQUEST FIRST                                 
         BE    REQF                                                             
         CLI   MODE,OFCFRST       OFFICE FIRST                                  
         BE    OFCF                                                             
         CLI   MODE,FESTCLI       CLIENT FIRST                                  
         BE    CLTF                                                             
         CLI   MODE,FESTPRO       PRODUCT FIRST                                 
         BE    PRDF                                                             
         CLI   MODE,PROCEST       PROCESS ESTIMATES                             
         BE    ESTF                                                             
*   LAST'S                                                                      
*                                                                               
         CLI   MODE,LESTPRO       PRODUCT LAST                                  
         BE    PRDL                                                             
         CLI   MODE,LESTCLI       CLIENT LAST                                   
         BE    CLTL                                                             
         CLI   MODE,OFCLAST       OFFICE LAST                                   
         BE    OFCL                                                             
         CLI   MODE,LESTREQ       REQUEST LAST                                  
         BE    REQL                                                             
         CLI   MODE,RUNLAST       RUN LAST                                      
         BE    RUNL                                                             
*                                                                               
EXIT     L     RF,UTL                                                           
         MVC   4(1,RF),SAVSYS     ALWAYS RESET SYSTEM NUMBER                    
         XIT1                                                                   
*                                                                               
XITNO    LTR   RB,RB               GENERAL XIT - W/CC CODE NOT EQUAL            
         B     XIT                                                              
XITYES   CR    RB,RB               GENERAL XIT W/CC CODE EQUAL                  
         B     XIT                                                              
*                                                                               
XIT      XIT1                                                                   
         EJECT                                                                  
*-----------------------------------------------------------------*             
* RUN FIRST - READ COMPANY RECORD AND SET SVPROD,SVMI AND SVCOST  *             
*             INITIALIZE COUNTERS & SET UP ID FOR WORKER FILE     *             
*-----------------------------------------------------------------*             
*                                                                               
RUNF     SR    R3,R3               SET UP ENTRIES TO MXCOMMON ROUTINES          
         LA    R4,COMTAB                                                        
         LA    R5,NCOMTAB                                                       
RUNF10   MVC   0(L'COMTAB,R4),=V(MXCOMMON)                                      
         STC   R3,0(R4)                                                         
         AHI   R3,1                                                             
         AHI   R4,L'COMTAB                                                      
         BCT   R5,RUNF10                                                        
*                                                                               
         L     RE,=V(WKFKEYS)                                                   
         SHI   RE,1                                                             
         MVC   MAX#WKF,0(RE)                                                    
         L     R1,PPWORK2C        2ND PPG WORKD                                 
         L     R1,VMASTC-PPWORK2D(R1)                                           
         USING MASTD,R1                                                         
         CLI   MCPOSTNG,NO        IF POSTING=NO IN MASTER                       
         BNE   *+8                                                              
         OI    MXOPT,MXNOPOST     THEN SET POST=NO INTERNALLY                   
         CLI   MCWRITE,NO         IF WRITE=NO IN MASTER                         
         BNE   *+8                                                              
         OI    MXOPT,MXNOWRIT     THEN SET WRITE=NO INTERNALLY                  
         MVC   UPSI,MCUPSI                                                      
         DROP  R1                                                               
*                                 SET IOAREA ADDRESSES                          
         MVC   AMYAREA,=V(MYAREA)                                               
         MVC   AMYAREA2,=V(MYAREA2)                                             
         MVC   AMYAREA3,=V(MYAREA3)                                             
         MVC   AAORBIL1,=V(AORBIL1) AOR BILLS ONLY                              
         MVC   AAORBIL2,=V(AORBIL2)                                             
         MVC   AAORBIL3,=V(AORBIL3)                                             
         MVC   AMXBLK,=V(MXBLK)                                                 
*                                                                               
         LA    RE,PRVTAB                                                        
         ST    RE,APRVTAB         RE=A(PROVINCE TABLE)                          
*                                 SET IOAREA ADDRESSES                          
         L     RF,=V(IOAREAX)                                                   
         L     RE,=V(IOAREA)                                                    
         SR    RF,RE              LENGTH OF IOAREA                              
         SR    RE,RE                                                            
         LA    R1,ACPRTNL         LENGTH OF EACH ENTRY                          
         DR    RE,R1                                                            
         STC   RF,PSTNUMA         NUMBER OF ENTRIES                             
         L     RF,=V(POSTNGSX)                                                  
         L     RE,=V(POSTINGS)                                                  
         SR    RF,RE              LENGTH OF POSTINGS AREA                       
         SR    RE,RE                                                            
         LA    R1,ACPRTNL2        LENGTH OF EACH ENTRY                          
         DR    RE,R1                                                            
         STC   RF,PSTNUM2         NUMBER OF ENTRIES                             
*                                                                               
*                                                                               
         L     R3,PPWORK2C        2ND PPG WORKD                                 
         L     R3,VMASTC-PPWORK2D(R3)                                           
         USING MASTD,R3                                                         
         OC    ACPOSTER,ACPOSTER                                                
         BNZ   RUNF20                                                           
*                                                                               
         MVC   MCDUB,=CL8'T62210'                                               
         GOTO1 MCVLOADM,DMCB,0                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   ACPOSTER,DMCB+4                                                  
*                                                                               
RUNF20   XC    DMCB(4),DMCB        NEED ADDRESS OF OFFICER                      
         MVC   DMCB+4(4),=X'D9000A38'                                           
         L     RF,VCOMFACS                                                      
         L     RF,(CCALLOV-COMFACSD)(RF)                                        
         GOTO1 (RF),DMCB                                                        
         MVC   AOFFICER,DMCB                                                    
         DROP  R3                                                               
*                                                                               
         BAS   RE,CHKMEDIA        READ FOR MEDIA MX CONTROL PROFILE             
         MVC   ACCTCD2,ACCTCD     SET EQUAL IF NO SPILT FILES                   
         MVC   ACCTSE2,ACCTSE                                                   
         MVC   TEMPCD,ACCTCD      SET TEMP VALUES FOR SETFILE                   
         MVC   TEMPSE,ACCTSE                                                    
         BAS   RE,SETFILE         SET ACCOUNTING FILE                           
         MVC   ACCTOID1,TEMPOID   SAVE ORIGIN ID                                
         MVC   ACCTOID2,TEMPOID                                                 
*                                                                               
         XC    SVPROF,SVPROF      READ ACCOUNT MX CONTROL PROFILE               
         XC    WORK,WORK                                                        
         MVI   WORK,C'A'-X'40'    ACCOUNTING SYSTEM                             
         MVC   WORK+2(2),=C'MX'   PROFILE NAME                                  
         MVC   WORK+4(1),ACCTCD   NATIVE COMPANY CODE                           
         MVC   WORK+12(2),RCAGYFLT                                              
         GOTO1 GETPROF,DMCB,WORK,SVPROF,DATAMGR                                 
         GOTO1 DATCON,DMCB,(4,RCDATE),(0,TODAYD)                                
         GOTO1 DATCON,DMCB,(0,TODAYD),(2,BTODAYP)                               
         BAS   RE,CHKSPLT          RESOLVE VALUE FOR MPRKALPH                   
         LA    R1,SRTKEY-SRTKEYD+1                                              
         CVD   R1,DUB                                                           
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  SORTCARD+13(3),DUB                                               
         LA    R1,SRTKEYLQ-(SRTKEY-SRTKEYD)                                     
         CVD   R1,DUB                                                           
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  SORTCARD+17(3),DUB                                               
         GOTO1 =V(SORTER),DMCB,SORTCARD,RECCARD                                 
*                                                                               
RUNFX    B     EXIT                                                             
         EJECT                                                                  
*---------------------------------------------------*                           
* CHKMEDIA - READ FOR SPECIAL MEDIA CONTROL PROFILE *                           
*   XIT - ACCOUNTING CONNECTIONS FUDGED TO READ FROM*                           
*         WHATEVER FILE SPECIFICED IN PROFILE       *                           
*---------------------------------------------------*                           
*                                                                               
CHKMEDIA NTR1                     CHECKS IF 3 MEDIA FILE -> 1 ACC FILE          
         XC    SVPROF2,SVPROF2                                                  
         XC    WORK,WORK                                                        
         MVC   WORK(4),=C'P0MX'   PRINT MX CONTROL PROFILE                      
         MVC   WORK+4(2),RCAGYFLT AGENCY ID                                     
         GOTO1 GETPROF,DMCB,WORK,SVPROF2,DATAMGR                                
         OC    SVPROF2(2),SVPROF2                                               
         BZ    CHKMEDX                                                          
         CLC   SVPROF2(2),=C'00'                                                
         BNE   *+14                                                             
         XC    SVPROF2(2),SVPROF2                                               
         B     CHKMEDX                                                          
*                                                                               
         XC    AKEY,AKEY                                                        
         LA    RE,AKEY                                                          
         USING CT5REC,RE                                                        
         MVI   CT5KTYP,CT5KTYPQ   C'5'                                          
         MVC   CT5KALPH,SVPROF2   ALPHA USER ID                                 
         DROP  RE                                                               
         L     R2,AMYAREA                                                       
         USING CT5REC,R2                                                        
         GOTO1 DATAMGR,DMCB,=C'DMREAD  ',=C'CTFILE ',AKEY,(R2),0                
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'               WHAT SHOULD I DO???                           
         SR    R0,R0                                                            
         LA    R1,CT5DATA                                                       
         USING CTSYSD,R1                                                        
CHKMED5  CLI   0(R1),0                                                          
         BNE   *+6                                                              
         DC    H'0'               WHAT SHOULD I DO????                          
         CLI   0(R1),CTSYSELQ     X'21' - SYSTEM ELEMENT                        
         BNE   CHKMED8                                                          
         CLI   CTSYSNUM,6         ACC SYSTEM                                    
         BE    CHKMED10                                                         
CHKMED8  ICM   R0,1,1(R1)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R1,R0                                                            
         B     CHKMED5                                                          
*                                                                               
CHKMED10 MVC   ACCTSE,CTSYSSE     SE NUM                                        
         MVC   ACCTCD,CTSYSAGB    COMPANY CODE                                  
CHKMEDX  B     EXIT                                                             
         DROP  R2,R1                                                            
         EJECT                                                                  
*-------------------------------------------------------*                       
* SETFILE - OPEN ACC FILES & SET COMPANY RECORD VALUES  *                       
*-------------------------------------------------------*                       
*                                                                               
SETFILE  NTR1                                                                   
         L     RF,UTL                                                           
         MVC   4(1,RF),TEMPSE     SET SE NUMBER OF FILE                         
         L     R2,AMYAREA         USE AS WORK AREA                              
         LA    R3,FILELIST                                                      
         MVI   EMULATE,C'N'                                                     
         GOTO1 DATAMGR,DMCB,=C'DTFADD',=C'ACCOUNT'                              
         L     RE,12(R1)          GET A(ACCOUNT DCB)                            
         TM    ISFTYPE-ISDTF(RE),ISFTEMU                                        
         BNO   SETFIL10                                                         
         LA    R3,EMULIST                                                       
         MVI   EMULATE,C'Y'       IF IS EMULATED                                
SETFIL10 GOTO1 DATAMGR,DMCB,=C'OPEN',=C'ACCFIL ',(R3),(R2)                      
*                                                                               
         XC    AKEY,AKEY          READ COMPANY RECORD                           
         LA    R2,AKEY                                                          
         USING CPYRECD,R2                                                       
         MVC   CPYKEY,SPACES                                                    
         MVC   CPYKCPY,TEMPCD     COMPANY CODE                                  
         L     R2,AMYAREA                                                       
         ST    R2,AREC                                                          
         MVI   RDSE,2             READ FROM OTHER ACC FILE                      
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMREAD'                               
         BE    *+6                                                              
         DC    H'0'               *** NEEDS TO BE CHANGED                       
         LA    R1,ACCORFST                                                      
         AR    R2,R1                                                            
         USING CPYELD,R2                                                        
         SR    R0,R0                                                            
*                                                                               
SETFIL20 CLI   0(R2),0            END OF RECORD                                 
         BNE   *+6                                                              
         DC    H'0'               *** MUST BE                                   
         CLI   0(R2),CPYELQ       MUST HAVE X'10' ELEMENT                       
         BE    SETFIL30                                                         
         ICM   R0,1,1(R2)         LENGTH OF ELEMENT                             
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R2,R0              BUMP TO NEXT ELEMENT                          
         B     SETFIL20                                                         
*                                                                               
SETFIL30 MVC   SVPROD,CPYPROD     SAVE UNIT/LEDGER FOR CLT/PRODUCT              
         MVI   SVMI,C'Y'          MI RECORDS USED                               
         TM    CPYSTAT4,CPYSMINT                                                
         BO    *+8                                                              
         MVI   SVMI,C'N'                                                        
         MVI   SVNEWOF,C'Y'       NEW OFFICE CODES USED                         
         TM    CPYSTAT4,CPYSOFF2                                                
         BO    *+8                                                              
         MVI   SVNEWOF,C'N'                                                     
         MVI   SVCOST,C'Y'        USE COSTING,BILL,REV                          
         TM    CPYSTAT1,CPYSCOST                                                
         BO    *+8                                                              
         MVI   SVCOST,C'N'                                                      
         MVC   TEMPOID,CPYUID     ORIGIN ID                                     
*                                                                               
         CLI   CPYLN,CPYLN3Q      MUST BE AT LEAST THIS BIG                     
         BL    SETFILX                                                          
         TM    CPYSTAT9,CPYMBSOD  SAME NIGHT POSTINGS?                          
         BZ    SETFILX                                                          
         OI    MXOPT,MXMBSOB                                                    
*                                                                               
SETFILX  B     XIT                                                              
         DROP  R2                                                               
         SPACE 1                                                                
FILELIST DC    CL8'NACCFIL '                                                    
         DC    CL8'NCTFILE '                                                    
         DC    CL10'X'                                                          
         SPACE 1                                                                
EMULIST  DC    CL8'NACCDIR '                                                    
         DC    CL8'NACCMST '                                                    
         DC    CL8'NACCARC '                                                    
         DC    CL8'NCTFILE '                                                    
         DC    CL10'X'                                                          
         EJECT                                                                  
*-------------------------------------------*                                   
* REQUEST FIRST - SET INITIAL VALUES        *                                   
*-------------------------------------------*                                   
*                                                                               
REQF     L     R3,PPWORK2C        2ND PPG WORKD                                 
         USING PPWORK2D,R3                                                      
         L     R2,VMASTC-PPWORK2D(R3)                                           
         USING MASTD,R2                                                         
         CLI   MCRERUN,YES         IF RERUN = Y                                 
         BNE   REQF2                                                            
         MVI   QOPT1,C'D'          SET DIED OPTION AND TRANSFER DATE            
         MVI   QCONTREQ,C'*'       SET CONTINUATION                             
         GOTO1 DATCON,DMCB,(4,MCDATE),(0,QTRADT)                                
         DROP  R2,R3                                                            
*                                                                               
REQF2    BAS   RE,CHKSPLT         CHECK IF MEDIA SPLIT FILES                    
         CLI   QOPT1,C' '         IF REGULAR BILLING RUN                        
         BNE   *+12                                                             
         BAS   RE,ANYREC          CHECK ANY POSTING RECORDS?                    
         BNE   REQFEXIT           SKIP TO NEXT REQUEST                          
*                                                                               
         MVC   MYMED,QMEDIA       SAVE CURRENT MEDIA                            
*****    LA    RF,PAGYREC         SET COUNTRY                                   
*****    USING PAGYREC,RF         ***THIS IS CAUSING WARNINGS**PRESTON          
         MVC   CNTRY,PAGYNAT                                                    
*****    DROP  RF                                                               
*                                                                               
         XC    B1XPROF,B1XPROF    READ B1X PROF                                 
         XC    WORK,WORK                                                        
         MVC   WORK(4),=C'PB1X'                                                 
         NI    WORK,X'BF'                                                       
         MVC   WORK+4(2),QAGENCY                                                
         MVC   WORK+6(1),QMEDIA                                                 
         GOTO1 GETPROF,DMCB,WORK,B1XPROF,DATAMGR                                
*                                                                               
         CLC   QEST,SPACES        IF NO ESTIMATE NUMBER                         
         BNE   REQF5                                                            
         MVC   QEST,=C'ALL'       ALL ESTIMATES                                 
         MVC   QSTART,=X'F8F0F0F1F0F1'  ALL DATES                               
         MVC   QEND,=X'F2F5F1F2F3F1'                                            
*                                                                               
REQF5    L     R2,PPWORK2C        2ND PPG WORKD                                 
         USING PPWORK2D,R2                                                      
         XC    RERUNDT,RERUNDT                                                  
         CLI   QOPT1,C'R'         IF REVERSAL                                   
         BE    REQF8                                                            
         CLI   QOPT1,C'D'         IF QOPT1=D                                    
         BNE   REQF10                                                           
         MVC   RERUNDT,QTRADT     SAVE DATE FOR WORKER FILE ID                  
REQF8    XC    REQTDT,REQTDT                                                    
         CLC   QTRADT,SPACES      SET TRANSFER DATE - IF GIVEN                  
         BNH   REQF10                                                           
         GOTO1 DATCON,DMCB,(0,QTRADT),(2,REQTDT)                                
         DROP  R2                                                               
REQF10   L     RE,=A(ACCAREA)     SAVED AREA FOR POSTER                         
         ICM   RF,15,=AL4(ACCAREAX-ACCAREA)                                     
         XCEF                                                                   
         MVI   RCSUBPRG,10                                                      
         LA    R2,PRTBLOCK                                                      
         USING MXPRNTD,R2                                                       
         LA    RE,PNUMTOTS                                                      
         LA    R1,PMXSAVE                                                       
REQF30   ZAP   0(L'PMXSAVE,R1),=P'0'                                            
         AHI   R1,L'PMXSAVE                                                     
         BCT   RE,REQF30                                                        
*                                 SET MXPRNT UP FOR INITIALIZATION              
         GOTO1 =A(SETPRT),DMCB,(RC)                                             
         MVI   PMODE,PMINIT                                                     
         GOTO1 =V(MXPRNT),DMCB,(R2)                                             
REQFX    B     EXIT                                                             
         DROP  R2                                                               
REQFEXIT L     RF,UTL             SKIP TO NEXT REQUEST                          
         MVC   4(1,RF),SAVSYS                                                   
         L     R2,PPWORK2C        2ND PPG WORKD                                 
         USING PPWORK2D,R2                                                      
         GOTO1 AENDREQ                                                          
         DROP  R2                                                               
         EJECT                                                                  
*---------------------------------------------------*                           
* CHKSPLT - CHECK IF AGENCY IS ON SPLIT MEDIA FILES *                           
*---------------------------------------------------*                           
*                                                                               
CHKSPLT  NTR1                                                                   
         XC    SALPH,SALPH                                                      
         OC    SVPROF2(2),SVPROF2                                               
         BZ    CHKSPX                                                           
         XC    AKEY,AKEY          READ POST RULE RECORD                         
         LA    R2,AKEY                                                          
         USING MPRRECD,R2                                                       
         MVI   MPRKTYP,MPRKTYPQ   X'2F'                                         
         MVI   MPRKSUB,MPRKSUBQ   X'01'                                         
         MVC   MPRKCPY,ACCTCD     NATIVE COMPANY CODE                           
         MVC   MPRKALPH,QAGENCY   ORIGINAL AGENCY ALPHA                         
         MVI   MPRKSYS,C'P'       SYSTEM                                        
         MVI   MPRKPRO,MPRKREG    REGULAR                                       
         L     R2,AMYAREA                                                       
         ST    R2,AREC                                                          
         USING MPRRECD,R2                                                       
         MVI   RDSE,1             READ FROM NATIVE ACC FILE                     
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMREAD'                               
         CLC   0(MPRKMED-MPRKTYP,R2),AKEY                                       
         BNE   CHKSPX                                                           
         SR    R0,R0                                                            
         LA    R1,ACCORFST                                                      
         AR    R2,R1                                                            
         USING MTPELD,R2                                                        
CHKSP10  CLI   0(R2),0                                                          
         BE    CHKSPX                                                           
         CLI   0(R2),MTPELQ                                                     
         BNE   CHKSP15                                                          
         CLI   MTPFNUM,MTPFDMED     DDS MEDIA FILE?                             
         BE    CHKSP20                                                          
CHKSP15  ICM   R0,1,1(R2)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R2,R0                                                            
         B     CHKSP10                                                          
*                                                                               
CHKSP20  MVC   SALPH,QAGENCY      SPLIT MEDIA FILE AGY ID                       
CHKSPX   B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*---------------------------------------------------*                           
* ANYREC - SEE IF AT LEAST SYSTEM LVL RECORD EXISTS *                           
*---------------------------------------------------*                           
*                                                                               
ANYREC   NTR1                                                                   
         CLI   QOPT1,C'R'         IF MARKING VERSION - DON'T CHECK              
         BNE   ANYREC5                                                          
         CLI   QOPT4,C'M'                                                       
         BE    ANYRECX                                                          
*                                                                               
ANYREC5  XC    AKEY,AKEY          READ POST DETAIL(S) RECORD                    
         LA    R2,AKEY            FOR CURRENT BILL                              
         USING MPRRECD,R2                                                       
         MVI   MPRKTYP,MPRKTYPQ   X'2F'                                         
         MVI   MPRKSUB,MPRKSUBQ   X'00'                                         
         MVC   MPRKCPY,ACCTCD     NATIVE COMPANY CODE                           
         MVC   MPRKALPH,SALPH     AGY ALPHA FOR MEDIA SPLIT FILES               
         MVI   MPRKSYS,C'P'       SYSTEM                                        
         L     R2,AMYAREA                                                       
         ST    R2,AREC                                                          
         MVI   RDSE,1             READ FROM NATIVE ACC FILE                     
         USING MPRRECD,R2                                                       
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMREAD'                               
         CLC   0(MPRKMED-MPRKTYP,R2),AKEY                                       
         BE    ANYRECX                                                          
         DROP  R2                                                               
*                                                                               
         LA    R2,PRTBLOCK                                                      
         USING MXPRNTD,R2                                                       
         GOTO1 =A(SETPRT),DMCB,(RC)                                             
         MVI   PMODE,PMMSG        PRINT MSG                                     
         OI    PMSG,PMNOREC       NO POSTING RECORDS                            
         MVC   PMSGTYPE,MSGTYPE   LEVEL WHICH PROF/MAINT APPLIES                
         GOTO1 =V(MXPRNT),DMCB,(R2)                                             
         LTR   RB,RB                                                            
ANYRECX  XIT1                     CC CODE SET                                   
         DROP  R2                                                               
         EJECT                                                                  
*-------------------------------------------*                                   
* OFFICE FIRST - EJECT PAGE                 *                                   
*-------------------------------------------*                                   
*                                                                               
OFCF     MVI   FORCEHED,C'Y'      FORCE TO A NEW PAGE                           
         B     EXIT                                                             
         SPACE                                                                  
*-------------------------------------------*                                   
* CLIENT FIRST - EXTRACTS MEDIA OFFICE CODE *                                   
*      AND THEN GETS OFFICE GROUP           *                                   
*-------------------------------------------*                                   
*                                                                               
CLTF     MVI   FORCEHED,C'Y'      FORCE TO NEW PAGE                             
         MVI   ANYBILL,C'N'       NO BILLS PROCESSED FOR THIS CLIENT            
         XC    MYCLT,MYCLT                                                      
         B     EXIT                                                             
         SPACE                                                                  
*-------------------------------------------*                                   
* PRODUCT FIRST - READ ACC PRODUCT RECORD   *                                   
*-------------------------------------------*                                   
*                                                                               
PRDF     XC    MYPRD,MYPRD                                                      
         B     EXIT                                                             
         EJECT                                                                  
*---------------------------------------------------------------*               
* ESTIMATE FIRST - FOR EACH ESTIMATE RECORD -READS THROUGH BILLS*               
*---------------------------------------------------------------*               
*                                                                               
ESTF     MVC   PKEY,KEY           SAVE OFF THE KEYS                             
         MVC   PKEYSAVE,KEYSAVE                                                 
         LA    R2,PESTREC                                                       
         USING PESTREC,R2                                                       
         CLC   =C'ALL',QPRODUCT                                                 
         BE    ESTF2                                                            
         CLC   QPRODUCT,SPACES                                                  
         BE    ESTF2                                                            
         CLC   PESTKPRD,QPRODUCT  MUST CHECK IF SINGLE PRODUCT REQUEST          
         BNE   ESTFX              BECAUSE PPFILCON DOESN'T                      
ESTF2    MVC   SVEDESC,PESTNAME   SAVE ESTIMATE DESCRIPTION                     
         OC    SVEDESC,SPACES                                                   
         MVC   SVEUSER1,SPACES                                                  
         MVC   SVEUSER2,SPACES                                                  
         MVC   SVEPONUM,SPACES     PO NUMBER                                    
*                                                                               
*        XC    SVUCMPLN,SVUCMPLN                                                
         XC    SVUCMELN,SVUCMELN                                                
         XC    SVUCMRLN,SVUCMRLN                                                
         XC    SVUCMDLN,SVUCMDLN                                                
*                                                                               
*        MVC   SVUCMDP,SPACES                                                   
         MVC   SVUCMDE,SPACES                                                   
         MVC   SVUCMDR,SPACES                                                   
         MVC   SVUCMDD,SPACES                                                   
*                                                                               
*        MVC   SVUCMP,SPACES                                                    
         MVC   SVUCME,SPACES                                                    
         MVC   SVUCMR,SPACES                                                    
         MVC   SVUCMD,SPACES                                                    
*                                                                               
         LA    R1,PESTELEM        FIRST EL                                      
ESTF2A   CLI   0(R1),0                                                          
         BE    ESTF2H                                                           
         CLI   0(R1),X'08'        PESTUDEF MAKE SURE YOU HAVE USER DATA         
         BE    ESTF2D                                                           
         CLI   0(R1),X'65'        PO NUMBER ELEMENT                             
         BE    ESTF2F                                                           
ESTF2B   SR    R0,R0                                                            
         IC    R0,1(R1)                                                         
         AR    R1,R0                                                            
         B     ESTF2A                                                           
*                                                                               
ESTF2D   MVC   SVEUSER1,PEUSER1-PESTUDEF(R1)   USER FIELDS                      
         MVC   SVEUSER2,PEUSER2-PESTUDEF(R1)                                    
         B     ESTF2B              LOOK FOR PO ELEMENT                          
*                                                                               
ESTF2F   MVC   SVEPONUM,PEPONPON-PESTPOND(R1)  PO NUMBER                        
*                                                                               
ESTF2H   SR    RE,RE                                                            
         ICM   RE,3,PESTKEST      SAVE CHAR ESTIMATE NUMBER                     
         STCM  RE,3,MYBEST                                                      
         CVD   RE,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  ESTNUM,DUB+6(2)    ESTIMATE NUMBER                               
         MVC   SESTKEY,0(R2)      SAVE ESTIMATE KEY                             
         MVI   SESTKEY+3,X'08'    FUDGE IT FOR COMPARE                          
*                                                                               
         LA    RE,UCOMBLK         NOW READ NEW UCOM USER DEF FIELDS             
         LA    RF,L'UCOMBLK                                                     
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         LA    R3,UCOMBLK                                                       
         USING DDUCOMD,R3                                                       
         MVC   UCACOMF,VCOMFACS    A(COMFACS)                                   
         MVI   UCSYS,C'P'          PRINT SYSTEM                                 
         MVC   UCPROG,=C'MX'       CALLING PGM(THIS MEANS CHK MBI BIT)          
         MVC   UCAGY,PESTKAGY      ALPHA CODE                                   
         MVC   UCMED,PESTKMED      MEDIA CODE                                   
         MVC   UCCLT,PESTKCLT      CLIENT                                       
         MVC   UCPRD,PESTKPRD      PRODUCT                                      
         MVC   UCEST,MYBEST        ESTIMATE                                     
         OI    UCOPT,UCOEST        RETURN ESTIMATE UCOMS                        
         GOTO1 =V(DDUCOM),UCOMBLK                                               
         CLI   UCERROR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         TM    UCDATA,UCDNOCLT+UCDNOEST     NO CLIENT/ESTIMAT FOUND?            
         BNZ   ESTF2K                                                           
         MVC   SVUCMELN,UCELENS    LENGTHS OF 4 ESTIMATE UCOM FIELDS            
         L     RF,UCETTLS                                                       
         MVC   SVUCME1D(L'SVUCMDE),0(RF) ESTIMATE UCOMS DESCRIPTIONS            
         L     RF,UCEDATA                                                       
         MVC   SVUCME1(L'SVUCME),0(RF) ESTIMATE UCOMS                           
         OC    SVUCME1D(L'SVUCMDE),SPACES                                       
         OC    SVUCME1(L'SVUCME),SPACES                                         
*                                                                               
ESTF2K   XC    KEY,KEY            --- GIVEN ONE EST - LOOK FOR BIL RECS         
         LA    R4,KEY                                                           
         USING PBILLREC,R4                                                      
         MVC   PBILKAGY,PESTKAGY  AGENCY CODE                                   
         MVC   PBILKMED,PESTKMED  MEDIA  CODE                                   
         MVI   PBILKRCD,X'08'     RECORD ID                                     
         MVC   PBILKCLT,PESTKCLT  CLIENT                                        
         MVC   PBILKPRD,PESTKPRD  CURRENT PRODUCT                               
         MVC   PBILKEST,PESTKEST  ESTIMATE NUMBER                               
         MVI   PBILKBNO+1,X'01'                                                 
         MVC   KEYSAVE,KEY                                                      
         LA    R1,DMCB                                                          
         LA    RE,DMRDHI                                                        
         ST    RE,0(R1)                                                         
         CLI   QOPT4,C'U'         IF REVERSAL TO UNPOST                         
         BNE   *+8                                                              
         MVI   0(R1),X'08'        PASS BACK DELETED RECORDS                     
         GOTO1 DATAMGR,DMCB,,PRTDIR,KEY,KEY                                     
         B     ESTF5                                                            
         DROP  R2                                                               
*                                                                               
ESTF3    BAS   RE,GETSME          RE-ESTABLISH READ SEQUENCE                    
ESTF4    BAS   RE,GETNXT          GET NEXT BILL RECORD                          
ESTF5    CLC   KEY(PBILKMOS-PBILLKEY),SESTKEY                                   
         BNE   ESTFX              MUST MATCH ON AGY/REC/CLT/PRD/EST             
*                                                                               
ESTF5A   CLI   QOPT1,C'D'                                                       
         BE    ESTF6              TAKE UNTRANSFERRED BILLS ONLY                 
         CLI   QOPT1,C'R'         REVERSE OPTION?                               
         BNE   ESTF7                                                            
         CLI   QOPT4,C'M'                                                       
         BE    ESTF7                                                            
ESTF6    TM    KEY+26,X'01'       WANT TRANSFERRED BILLS ONLY                   
         BNO   ESTF4                                                            
         CLI   QOPT4,C'U'         AND UNPOST OPTION?                            
         BNE   ESTF10                                                           
         TM    KEY+25,X'80'       WANT DELETED BILLS ONLY                       
         BNO   ESTF4                                                            
         B     ESTF10                                                           
*                                                                               
ESTF7    TM    KEY+26,X'01'                                                     
         BO    ESTF4              REGULAR REQUEST - WANT UNTRANS ONLY           
         DROP  R4                                                               
         SPACE                                                                  
ESTF10   LA    R4,PBILLREC        --- FILTER BILL RECORDS                       
         USING PBILLREC,R4                                                      
         ST    R4,AREC                                                          
         ST    R4,ABILL           A(BILL RECORD)                                
****************************************************************                
*   THE FOLLOWING INSTRUCTIONS WERE MOVED AFTER THE DATAMGER   *                
*   CALL.  THE PROBLEM IS YOU HAVEN'T YET READ THE RECORD.     *                
*   SO, PBILPOST IS ZERO'S, HENCE THE REVDATE IS INCORRECT.    *                
*          JDIL 07/30/97                                       *                
*********                                               ********                
*        MVI   REVSTAT,C'N'       SET NOT REVERSING A BILL     *                
*        CLI   QOPT1,C'R'         REVERSE OPTION?              *                
*        BNE   ESTF11                                          *                
*        MVI   REVSTAT,C'Y'       SET REVERSING A BILL         *                
*        MVC   REVDATE,PBILPOST   SET ORIGINAL TRANSFER DATE   *                
****************************************************************                
ESTF11   XC    SKEY,SKEY                                                        
         MVC   SKEY(L'PBILLKEY),KEY                                             
         GOTO1 DATAMGR,DMCB,GETREC,PRTFILE,KEY+27,AREC,DMWORK                   
*                                                                               
         XC    REVDATE,REVDATE                                                  
         MVI   REVSTAT,C'N'       SET NOT REVERSING A BILL                      
         CLI   QOPT1,C'R'         REVERSE OPTION?                               
         BNE   *+14                                                             
         MVI   REVSTAT,C'Y'       SET REVERSING A BILL                          
         MVC   REVDATE,PBILPOST   SET ORIGINAL TRANSFER DATE                    
*                                                                               
         BAS   RE,FILTBIL         SETS BILL TYPE & CHECKS SOME REQU.            
         BNE   ESTF4              DON'T USE THIS BILL                           
         CLI   PBILADID,C'.'      . IN FIRST BYTE MEANS WB FLIGHT #             
         BE    *+14               INSTEAD OF AD-ID                              
         MVC   SVADID,PBILADID     AD-ID                                        
         B     *+10                                                             
         MVC   SVBLFLT,PBILWBF                                                  
*                                                                               
         CLC   PBILLJOB(3),=X'FFD7D6'        SEE IF A PO#                       
         BNE   ESTF11P                                                          
*                                                                               
*                                                                               
         LA    RE,PCLTELEM         LOOKING FOR PO# ELEM                         
         SR    R0,R0                                                            
ESTF11D  CLI   0(RE),0                                                          
         BE    ESTF11M                                                          
         CLI   0(RE),X'52'                                                      
         BNE   ESTF11G                                                          
         USING PCLTPOEL,RE                                                      
         MVC   SVPOTYP,PCLTPOLV     SAVE TYPE (LEVEL)                           
         TM    PCLTPOF1,P_POLOVQ    SEE IF OVERRIDING PO# LABLE                 
         BZ    ESTF11M                                                          
         MVC   SVPOLBL,PCLTPONM                                                 
         OC    SVPOLBL,SVPOLBL                                                  
         B     ESTF11M                                                          
ESTF11G  ICM   R0,1,1(RE)                                                       
         AR    RE,R0                                                            
         B     ESTF11D                                                          
*                                                                               
ESTF11M  XC    WORK,WORK                                                        
         GOTO1 =V(PPGETPO#),DMCB,(C'L',ABILL),(SVPOTYP,WORK),          +        
               VCOMFACS                                                         
         MVC   SVBLPO,WORK                                                      
*                                                                               
ESTF11P  CLC   MYCLT,PBILKCLT     NEW CLIENT IN BILL RECORD?                    
         BE    ESTF11X                                                          
         MVC   MYCLT,PBILKCLT     YES - SET NEW CLT VALUES                      
         XC    MYPRD,MYPRD                                                      
         BAS   RE,NEWCLT                                                        
         BAS   RE,RDB1PROF         READ B1 PROFILE                              
ESTF11X  CLC   MYPRD,PBILKPRD     NEW PRODUCT IN BILL RECORD?                   
         BE    ESTF13                                                           
         MVC   MYPRD,PBILKPRD     YES - SET NEW PRD VALUES                      
         BAS   RE,NEWPRD                                                        
         BAS   RE,RDPROF          CALL ACPOSTER - TO GET PROFILES               
         BAS   RE,CHKPROF         CHECK OKAY TO POST                            
         CLI   MAKEPOST,C'N'                                                    
         BNE   ESTF12                                                           
         MVI   MODE,LESTPRO       POSTING=NO FOR PRD - SKIP TO NEXT             
         LTR   RB,RB                                                            
         B     EXIT                                                             
ESTF12   BAS   RE,CHKDIV          CHECK PRODUCT DIVISIONS                       
         B     ESTF14                                                           
*                                                                               
ESTF13   CLC   STYPEBIL,TYPEBIL    IF BILL TYPE CHANGED                         
         BE    *+8                                                              
         BAS   RE,RDPROF           RE-READ POSTING PROFILES                     
         EJECT                                                                  
ESTF14   DS    0H                                                               
         MVC   SVCNET,PBILTRCD    SAVE CALCULATE NET FOR RCV AND INC            
         ZAP   DUB,=P'0'                                                        
         LA    RE,PBILLEL                                                       
         USING PBILOTH,RE                                                       
         SR    R0,R0                                                            
ESTF14A  CLI   0(RE),0                                                          
         BE    ESTF14C                                                          
         CLI   0(RE),X'09'                                                      
         BE    ESTF14B                                                          
         ZIC   R1,1(RE)                                                         
         AR    RE,R1                                                            
         B     ESTF14A                                                          
ESTF14B  ZAP   DUB,PBILLOPN       NET FOR COS2 BILLS                            
ESTF14C  SR    RF,RF                                                            
         ICM   RF,15,PBILTRCD     TRADE CREDIT (ON REGM BILLS)                  
         CVD   RF,DUB2                                                          
         SP    DUB,DUB2           ADD TO GET MIDAS EXCHANGE AMT                 
         ZAP   SVBNET,DUB         AND STORE FOR MXPOST                          
         CLI   QOPT4,C'M'         DDS ONLY OPTION -JUST MARK ALL BILLS          
         BNE   ESTF15                                                           
         BAS   RE,MARK            CHECK BILL MATCHES MARK REQUIREMENTS          
         BNE   ESTF3              NO                                            
         BAS   RE,WRITBIL                                                       
         B     ESTF3                                                            
*                                                                               
ESTF15   CLI   QOPT1,C'R'         REVERSE OPTION?                               
         BNE   ESTF18                                                           
         BAS   RE,REVERSE                                                       
         BNE   ESTF5                                                            
*                                                                               
ESTF18   MVI   ANYBILL,C'Y'       BILL PROCESSED FOR THIS CLIENT                
         MVI   PREVINC,C'N'       NO MEDIA INC POSTING                          
         MVI   PREVAINC,C'N'      NO AOR INC POSTING                            
         MVI   PSTAOR,C'Y'        NO PROB WITH POSTINGS                         
         GOTO1 DATCON,DMCB,(3,PBILINVD),(0,TRANSDT)                             
         GOTO1 MXPROF,DMCB,MTPFIOFC,PROFWORK                                    
         MVC   SVACOF2,PROFWORK                                                 
         OC    SVACOF2,SPACES                                                   
         CLC   =C'REGM',MTYPEBIL                                                
         BNE   ESTF19                                                           
         GOTO1 MXPROF,DMCB,MTPFMOFF,PROFWORK     GROUPM MIDAS TRADE OFF         
         MVC   SVACOF2,PROFWORK                                                 
         OC    SVACOF2,SPACES                                                   
***      GOTO1 MXPROF,DMCB,MTPFSPCT,PROFWORK     GROUPM MIDAS SPLIT %           
***      MVC   ACPTMPCT,PROFWORK                                                
ESTF19   GOTO1 SETMOA,DMCB,TRANSDT,ADDAY                                        
*                                                                               
         CLI   SVPROF+7,C'Y'         HONOR MOA LOCK RULES?                      
         BNE   ESTF30                                                           
         XC    WORK,WORK                                                        
         GOTO1 DATCON,DMCB,(0,TRANSDT),(6,WORK)                                 
         L     RF,UTL                SET ACCOUNTING SE NUMBER                   
         MVC   4(1,RF),ACCTSE2                                                  
         USING COMFACSD,R1                                                      
         L     R1,VCOMFACS         R1=A(COMFACS)                                
         OC    CPERVAL,CPERVAL     SPONSER DOESN'T FILL IN PERVAL               
         BNZ   *+10                SO NEED TO DO IT                             
         MVC   CPERVAL,=V(PERVAL)                                               
ESTF20   GOTO1 =V(BMONVAL),DMCB,(6,WORK),(9,VCOMFACS),(0,WORK+20),     +        
               (ACCTCD2,0)                                                      
         LA    R1,WORK+20                                                       
         USING BMONVALD,R1                                                      
         CLI   BMOERR,BMOEOKQ      EVERYTHING OK?                               
         BE    ESTF30                                                           
         TM    BMOERR,BMOELOKQ     MOA LOCKED?                                  
         BZ    ESTF25                                                           
         OC    BMOLCKP,BMOLCKP     LAST LOCKED MOA                              
         BZ    ESTF25                                                           
         XC    WORK(10),WORK                                                    
         MVC   WORK(L'BMOLCKP),BMOLCKP                                          
         MVI   WORK+2,X'01'                                                     
         GOTO1 DATCON,DMCB,(1,WORK),(0,WORK+3)                                  
         GOTO1 ADDAY,DMCB,WORK+3,WORK+3,31                                      
         MVC   TRANSDT,WORK+3                                                   
         B     ESTF30                                                           
*                                                                               
ESTF25   TM    BMOERR,BMOERNGQ     DATE OF OUF RANGE?                           
         BZ    ESTF30                                                           
         OC    BMOMOSP,BMOMOSP                                                  
         BZ    ESTF30                                                           
         XC    WORK(10),WORK                                                    
         MVC   WORK(L'BMOMOSP),BMOMOSP                                          
         MVI   WORK+2,X'01'                                                     
         GOTO1 DATCON,DMCB,(1,WORK),(0,WORK+3)                                  
         GOTO1 ADDAY,DMCB,WORK+3,WORK+3,31                                      
         GOTO1 DATCON,DMCB,(0,WORK+3),(6,WORK)                                  
         B     ESTF20             VALIDATE AGAIN USING NEW MONTH                
*                                                                               
ESTF30   L     RF,UTL                                                           
         MVC   4(1,RF),SAVSYS     RE-SET TO SPOT SYSTEM NUMBER                  
         BAS   RE,SETRET          SET RETAIL CLI/PRD OVERRIDES                  
         BAS   RE,CHKREG          CHECK FOR REGION AND DISTRICT                 
*                                                                               
         OC    PGRCD,PGRCD         ANY DIVISION                                 
         BZ    ESTF40              NO, CAN'T DO REGIONS OR DISTRICT             
*                                                                               
         OC    REGCODE,REGCODE     ANY REGION?                                  
         BZ    ESTF40              NO, CAN'T DO DISTRICT EITHER                 
*                                                                               
         LA    RE,UCOMBLK         NOW READ NEW UCOM USER DEF FIELDS             
         LA    RF,L'UCOMBLK                                                     
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         LA    R3,UCOMBLK                                                       
         USING DDUCOMD,R3                                                       
         MVC   UCACOMF,VCOMFACS    A(COMFACS)                                   
         MVI   UCSYS,C'P'          PRINT SYSTEM                                 
         MVC   UCPROG,=C'MX'       CALLING PGM(THIS MEANS CHK MBI BIT)          
         MVC   UCAGY,PESTKAGY      ALPHA CODE                                   
         MVC   UCMED,PESTKMED      MEDIA CODE                                   
         MVC   UCCLT,PESTKCLT      CLIENT                                       
         MVC   UCPRD,PESTKPRD      PRODUCT                                      
         MVC   UCEST,MYBEST        ESTIMATE                                     
         MVC   UCDIV,PGRCD         DIVISON                                      
         MVC   UCREG,REGCODE       REGION                                       
         OI    UCOPT,UCOREG        RETURN REGION UCOMS                          
         GOTO1 =V(DDUCOM),UCOMBLK                                               
         CLI   UCERROR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         TM    UCDATA,UCDNOCLT+UCDNOEST     NO CLIENT/ESTIMATE FOUND?           
         BNZ   ESTF40                                                           
         MVC   SVUCMRLN,UCRLENS    LENGTHS OF 4 REGION UCOM FIELDS              
         L     RF,UCRTTLS                                                       
         MVC   SVUCMR1D(L'SVUCMDR),0(RF) REGION UCOMS DESCRIPTIONS              
         L     RF,UCRDATA                                                       
         MVC   SVUCMR1(L'SVUCMR),0(RF) REGION UCOMS                             
         OC    SVUCMR1D(L'SVUCMDR),SPACES                                       
         OC    SVUCMR1(L'SVUCMR),SPACES                                         
*                                                                               
         OC    DISTCODE,DISTCODE   ANY DISTRICT?                                
         BZ    ESTF40              NO, DONW                                     
*                                                                               
         LA    RE,UCOMBLK         NOW READ NEW UCOM USER DEF FIELDS             
         LA    RF,L'UCOMBLK                                                     
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         LA    R3,UCOMBLK                                                       
         USING DDUCOMD,R3                                                       
         MVC   UCACOMF,VCOMFACS    A(COMFACS)                                   
         MVI   UCSYS,C'P'          PRINT SYSTEM                                 
         MVC   UCPROG,=C'MX'       CALLING PGM(THIS MEANS CHK MBI BIT)          
         MVC   UCAGY,PESTKAGY      ALPHA CODE                                   
         MVC   UCMED,PESTKMED      MEDIA CODE                                   
         MVC   UCCLT,PESTKCLT      CLIENT                                       
         MVC   UCPRD,PESTKPRD      PRODUCT                                      
         MVC   UCEST,MYBEST        ESTIMATE                                     
         MVC   UCDIV,PGRCD         DIVISON                                      
         MVC   UCREG,REGCODE       REGION                                       
         MVC   UCDST,DISTCODE      DISTRICT                                     
         OI    UCOPT,UCODST        RETURN DISTRICT UCOMS                        
         GOTO1 =V(DDUCOM),UCOMBLK                                               
         CLI   UCERROR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         TM    UCDATA,UCDNOCLT+UCDNOEST     NO CLIENT/ESTIMATE FOUND?           
         BNZ   ESTF40                                                           
         MVC   SVUCMDLN,UCDLENS    LENGTHS OF 4 DISTRCIT UCOM FIELDS            
         L     RF,UCDTTLS                                                       
         MVC   SVUCMD1D(L'SVUCMDD),0(RF) DISTRICT UCOMS DESCRIPTIONS            
         L     RF,UCDDATA                                                       
         MVC   SVUCMD1(L'SVUCMD),0(RF) DISTRICT UCOMS                           
         OC    SVUCMD1D(L'SVUCMDD),SPACES                                       
         OC    SVUCMD1(L'SVUCMD),SPACES                                         
*                                                                               
ESTF40   XC    AORERRS,AORERRS                                                  
         CLC   =C'AOR',TYPEBIL                                                  
         BE    ESTF50                                                           
         GOTO1 =A(RDPOST),DMCB,(RC)                                             
         GOTO1 =A(POSTCALL),DMCB,(RC)                                           
         BAS   RE,BILLPOST        PROCESS THE BILL                              
         B     ESTF3                                                            
*                                                                               
ESTF50   MVC   TEMPINV,PBILKBNO   RESET INVOICE NUMBER                          
                                                                                
         SR    RF,RF                                                            
         XC    BINVL,BINVL                                                      
         CLC   PBILIMED,SPACES    ONLY STORE LONG INVOICE IF THE RULES          
         BNH   *+8                ARE IN THE BILL HEADER                        
         LA    RF,BINVL                                                         
         GOTO1 =A(SETINV),DMCB,(RC),BINVOICE,(RF)                               
         GOTO1 =A(DOAOR),DMCB,(RC)                                              
         OC    AORERRS,AORERRS                                                  
         BZ    ESTF60                                                           
         TM    PBILCMSW,X'08'     AOR UPFRONT NET IS NOT IN A SET               
         BO    ESTF60                                                           
         MVI   PSTAOR,C'N'        ERROR WITH POSTINGS IN AOR SET                
ESTF60   MVI   ABILCNT,1          NUMBER OF AOR BILLS IN SET                    
         LA    R2,AAORBIL1        FIRST A(OF POSTINGS)                          
         LA    R3,AORERRS                                                       
         USING AORERD,R3                                                        
ESTF70   BAS   RE,SETERR          SET CURRENT ERROR MSGS                        
         BAS   RE,SETPST          SETS V(POSTINGS) AND RESET PST INFO           
         BAS   RE,BILLPOST                                                      
         BAS   RE,GETSME          RE-ESTABLISH READ SEQUENCE                    
         LA    R2,4(R2)           PT TO NEXT ADDRESS OF POSTINGS                
ESTF80   LA    R3,AORERRL(R3)                                                   
         SR    R1,R1                                                            
         IC    R1,ABILCNT                                                       
         CLM   R1,1,NUMABIL                                                     
         BE    ESTF4                                                            
         LA    R1,1(R1)                                                         
         STC   R1,ABILCNT                                                       
         MVI   PREVINC,C'N'       RESET THESE POSTING FLAGS                     
         MVI   PREVAINC,C'N'                                                    
         BAS   RE,GETNXT          GET NEXT BILL IN AOR SET                      
         XC    SKEY,SKEY                                                        
         MVC   SKEY(L'PBILLKEY),KEY                                             
         LA    R4,PBILLREC                                                      
         BAS   RE,GETRC           GET THE RECORD                                
         TM    PBILCMSW,X'20'                                                   
         BO    ESTF80                                                           
         MVC   TEMPDATE,PBILLDAT  SET 'REAL' INVOICE NUMBER                     
         MVC   TEMPINV,PBILKBNO                                                 
         XC    BINVL,BINVL                                                      
         SR    RF,RF                                                            
         CLC   PBILIMED,SPACES    ONLY STORE LONG INVOICE IF THE RULES          
         BNH   *+8                ARE IN THE BILL HEADER                        
         LA    RF,BINVL                                                         
         GOTO1 =A(SETINV),DMCB,(RC),BINVOICE,(RF)                               
         B     ESTF70                                                           
*                                                                               
ESTFX    MVC   KEY,PKEY           RESTORE PPG'S KEY                             
         GOTO1 HIGH                                                             
         B     EXIT                                                             
         EJECT                                                                  
GETNXT   NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         LA    R1,DMCB                                                          
         LA    RE,DMRSEQ                                                        
         ST    RE,0(R1)                                                         
         CLI   QOPT4,C'U'         IF REVERSAL TO UNPOST                         
         BNE   *+8                                                              
         MVI   0(R1),X'08'        PASS BACK DELETED RECORDS                     
         GOTO1 DATAMGR,DMCB,,PRTDIR,KEY,KEY                                     
         B     EXIT                                                             
         SPACE                                                                  
* GETSME  - REREAD SAVED KEY                                                    
*                                                                               
GETSME   NTR1                                                                   
         L     RF,UTL                                                           
         MVC   4(1,RF),SAVSYS      MEDIA SYSTEM NUMBER                          
         XC    KEY,KEY                                                          
         MVC   KEY(L'PBILLKEY),SKEY                                             
         LA    R1,DMCB                                                          
         LA    RE,DMREAD                                                        
         ST    RE,0(R1)                                                         
         CLI   QOPT4,C'U'         IF REVERSAL TO UNPOST                         
         BNE   *+8                                                              
         MVI   0(R1),X'08'        PASS BACK DELETED RECORDS                     
         GOTO1 DATAMGR,DMCB,,PRTDIR,KEY,KEY                                     
         CLC   KEY(L'PBILLKEY),SKEY                                             
         BE    *+6                                                              
         DC    H'0'                                                             
         B     EXIT                                                             
* GETSME2 - REREAD SAME KEY                                                     
*                                                                               
GETSME2  NTR1                                                                   
         L     RF,UTL                                                           
         MVC   4(1,RF),SAVSYS      MEDIA SYSTEM NUMBER                          
         MVC   KEYSAVE,KEY                                                      
         LA    R1,DMCB                                                          
         LA    RE,DMREAD                                                        
         ST    RE,0(R1)                                                         
         CLI   QOPT4,C'U'         IF REVERSAL TO UNPOST                         
         BNE   *+8                                                              
         MVI   0(R1),X'08'        PASS BACK DELETED RECORDS                     
         GOTO1 DATAMGR,DMCB,,PRTDIR,KEY,KEY                                     
         CLC   KEY(L'PBILLKEY),KEYSAVE                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         B     EXIT                                                             
         SPACE                                                                  
GETRC    NTR1                                                                   
         ST    R4,AREC                                                          
         GOTO1 DATAMGR,DMCB,GETREC,PRTFILE,KEY+27,AREC,DMWORK                   
         B     EXIT                                                             
         EJECT                                                                  
*-------------------------------------------------------------*                 
* FILTBIL - SETS BIL TYPE & FILTERS BILL DEPENDING ON REQUEST *                 
*-------------------------------------------------------------*                 
*                                                                               
FILTBIL  NTR1                                                                   
         CLI   QOPT1,C' '         REGULAR REQUEST                               
         BNE   FILTB10                                                          
         OC    PBILPOST,PBILPOST                                                
         BNZ   XITNO              WANT UNTRANSFERRED ONLY                       
         B     FILTB20                                                          
*                                                                               
FILTB10  CLI   QOPT1,C'D'                                                       
         BNE   FILTB20                                                          
         CLC   PBILPOST,REQTDT    MATCH ON DATE                                 
         BNE   XITNO                                                            
FILTB20  GOTO1 CLRAREA,DMCB,V(IOAREA),V(IOAREAX)                                
         BAS   RE,SETTYPE         SET BILL TYPE & JOB CODE                      
         MVC   SVBTNUM(2),PBILLTYP   B4, B5, ETC. (MOVING 2 BYTES)              
         XC    SVFLAG,SVFLAG                                                    
         TM    PBILLIND,X'82'     IF COST2 EITHER TYPE ($ OR FACTOR)            
         BZ    *+14                                                             
         MVC   SVFLAG,PBILLIND    SET COST2 FLAG                                
         NI    SVFLAG,X'FF'-X'01' TURN OFF FINANCIAL BILL FLAG IF ANY           
*                                                                               
         MVC   TEMPDATE,PBILLDAT  SET 'REAL' INVOICE NUMBER                     
         MVC   TEMPINV,PBILKBNO                                                 
         XC    BINVL,BINVL                                                      
         SR    RF,RF                                                            
         CLC   PBILIMED,SPACES    ONLY STORE LONG INVOICE IF THE RULES          
         BNH   *+8                ARE IN THE BILL HEADER                        
         LA    RF,BINVL                                                         
         GOTO1 =A(SETINV),DMCB,(RC),BINVOICE,(RF)                               
         MVC   RUNDT,PBILLDAT             SET RUN DATE OF BILL                  
         GOTO1 =A(SETVATO),DMCB,(RC),AREC SET OUTPUT GST/PST CODES              
         CLC   =C'RET',TYPEBIL    FOR RETAIL BILLS ONLY                         
         BNE   *+12                                                             
         BAS   RE,CHKBZERO        BILL BILL AMOUNTS ZERO                        
         BE    XITNO              YES ALL ZERO                                  
*                                                                               
         CLI   QOPT1,C'R'         REVERSE OPTION?                               
         BNE   FILTB30                                                          
         CLI   QOPT4,C'M'                                                       
         BE    FILTB30                                                          
         BAS   RE,CHKREV          CHECK REVERSAL REQUIREMENTS                   
         BNE   XITNO                                                            
*                                                                               
FILTB30  TM    PBILCMSW,X'20'                                                   
         BO    XITNO              TRUE AOR BILL ALREADY TAKEN CARE OF           
         B     XITYES                                                           
         EJECT                                                                  
                                                                                
*----------------------------------------------------------------------         
* SMFOX - OUPUT SMF RECORD FOR CROSSFILE ACTIVITY                               
*----------------------------------------------------------------------         
SMFOX    NTR1                                                                   
         USING CROSD,SMFREC                                                     
         L     R1,PPWORK2C        2ND PPG WORKD                                 
         L     R1,VMASTC-PPWORK2D(R1)                                           
         USING MASTD,R1                                                         
         XC    SMFREC,SMFREC                                                    
         MVC   CROSLN,=AL2(CROSLNQ)  RECORD LENGTH                              
         MVI   CROSTYP,CROSTYPQ      RECORD TYPE           X                    
         MVI   CROSSUB,CROSSUB1      SUB RECORD TYPE       1                    
         L     R2,MCSSB-MCBLOCK(R1)                                             
         MVC   CROSDSP,SSODSPAC-SSOOFF(R2)                                      
         MVI   CROSYSN,X'04'         SYSTEM                X'04'                
         MVC   CROSYSA,SVRSYS        SYSTEM                P                    
         MVI   CROSPGM+0,C'M'        PROGRAM               M                    
         MVI   CROSPGM+1,C'X'        PROGRAM               X                    
         MVC   CROSUID,MCORIGID      COMPANY ID            OO                   
         MVC   CROSSAGY,MCRHSAGP     SECURITY AGENCY                            
         MVC   CROSPID,MCRHPSWD      PERSON ID             PP                   
         MVC   CROSAAGY,ACCALPH      ACCOUNTING AGENCY     AA                   
         MVC   CROSMAGY,QAGENCY      MEDIA AGENCY          AA                   
         MVC   CROSASE,ACCTSE2       ACCOUNTING SE NUMBER  N                    
         MVC   CROSMSE,SAVSYS        MEDIA SE NUMBER       N                    
         MVC   CROSOFF,SVCOFF        OFFICE                OO                   
         MVC   CROSMED,MYMED         MEDIA CODE            M                    
         MVC   CROSCLI,MYCLT         CLIENT CODE           CCC                  
         MVC   CROSPRD,MYPRD         PRODUCT CODE          PPP                  
         DROP  R1                                                               
*                                                                               
         L     RF,VCOMFACS                                                      
         ICM   R2,15,CSMFOUT-COMFACSD(R2)                                       
         BZ    SMFOXX                                                           
         GOTO1 (R2),DMCB,13,SMFREC                                              
SMFOXX   J     XIT                                                              
SMFREC   DS    XL(CROSLNQ)                                                      
                                                                                
*----------------------------------------------------------------------         
* SETTYPE - SETS THE BILL TYPE                                                  
*----------------------------------------------------------------------         
SETTYPE  NTR1                                                                   
         MVC   STYPEBIL,TYPEBIL    SAVE PREVIOUS BILL TYPE                      
         MVC   JOBCD,SPACES                                                     
         MVC   TYPEBIL,=C'FIN'                                                  
         LA    RE,PBILLEL                                                       
         USING PBILOTH,RE                                                       
         SR    R0,R0                                                            
SETTYP5  CLI   0(RE),0                                                          
         BE    SETTYP10                                                         
         CLI   0(RE),X'09'                                                      
         BE    SETTYP8                                                          
         ICM   R0,1,1(RE)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    RE,R0                                                            
         B     SETTYP5                                                          
SETTYP8  CLC   PBILLJOB,SPACES    FINANCIAL?                                    
         BNH   *+10                                                             
         MVC   JOBCD,PBILLJOB                                                   
         TM    PBILLIND,X'01'     FINANCIAL?                                    
         BO    SETMTYPE           YES -                                         
*                                                                               
SETTYP10 MVC   TYPEBIL,=C'RET'    RETAIL                                        
         CLI   PBRETAIL,0                                                       
         BNE   SETMTYPE                                                         
         MVC   TYPEBIL,=C'AOR'    AOR                                           
         TM    PBILCMSW,X'10'                                                   
         BO    SETMTYPE                                                         
         TM    PBILCMSW,X'20'                                                   
         BO    SETMTYPE                                                         
         MVC   TYPEBIL,=C'REG'                                                  
*                                                                               
SETMTYPE LA    RE,PBILLEL                                                       
         USING PBILOTH,RE                                                       
         SR    R0,R0                                                            
                                                                                
SETMTY02 CLI   0(RE),0                                                          
         BE    SETMTY06                                                         
         CLI   0(RE),X'09'                                                      
         BNE   SETMTY04                                                         
         CLC   PBILLJOB,SPACES    FINANCIAL?                                    
         BNH   *+10                                                             
         MVC   JOBCD,PBILLJOB                                                   
         TM    PBILLIND,X'01'                                                   
         BNO   SETMTY06                                                         
         MVC   MTYPEBIL,=CL5'FINM' FINANCIAL MAIN ONE                           
         CLI   PBILLTYP,C'R'                                                    
         BNE   SETMTYX                                                          
         MVI   MTYPEBIL+3,C'R'     FINANCIAL REBATE                             
         B     SETMTYX                                                          
*                                                                               
SETMTY04 ICM    R0,1,1(RE)                                                      
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    RE,R0                                                            
         B     SETMTY02                                                         
*                                                                               
SETMTY06 CLI   PBRETAIL,0                                                       
         BE    SETMTY08                                                         
         MVC   MTYPEBIL,=CL5'RET' RETAIL                                        
         CLI   PBRETAIL,X'41'                                                   
         BNE   *+12                                                             
         MVI   MTYPEBIL+3,C'S'    CORP SUMMARY                                  
         B     SETMTYX                                                          
         TM    PBRETAIL,X'01'                                                   
         BNO   *+12                                                             
         MVI   MTYPEBIL+3,C'C'    CORP RETAIL(CORP OUTLET,CORP CONTRL)          
         B     SETMTYX                                                          
         TM    PBRETAIL,X'02'                                                   
         BNO   SETMTYX                                                          
         MVI   MTYPEBIL+3,C'P'    PARTICIPANT RETAIL                            
         B     SETMTYX                                                          
                                                                                
SETMTY08 TM    PBILCMSW,X'20'                                                   
         BNO   *+14                                                             
         MVC   MTYPEBIL,=CL5'AORA' TRUE AOR                                     
         B     SETMTYX                                                          
                                                                                
         TM    PBILCMSW,X'10'                                                   
         BNO   SETMTY14                                                         
         MVC   MTYPEBIL,=CL5'AORM' CLIENT AOR MAIN ONE                          
                                                                                
         TM    PBILCMSW,X'01'                                                   
         BNO   SETMTY10                                                         
         MVI   MTYPEBIL+3,C'U'   CLIENT AOR UPFRONT COMMISSION                  
         CLI   PBILSEP,C'C'                                                     
         BNE   *+10                                                             
         MVC   MTYPEBIL+3(2),=C'CU' CLIENT AOR CD W/ UPFRONT COMMISSION         
         B     SETMTYX                                                          
                                                                                
SETMTY10 TM    PBILCMSW,X'08'                                                   
         BNO   SETMTY12                                                         
         MVI   MTYPEBIL+3,C'N'   CLIENT AOR UPFRONT NET                         
         CLI   PBILSEP,C'C'                                                     
         BNE   *+10                                                             
         MVC   MTYPEBIL+3(2),=C'CN' CLIENT AOR CD W/ UPFRONT NET                
         B     SETMTYX                                                          
                                                                                
SETMTY12 CLI   PBILSEP,C'A'                                                     
         BNE   *+12                                                             
         MVI   MTYPEBIL+3,C'J'   CLIENT ADJUSTMENT AOR                          
         B     SETMTYX                                                          
         CLI   PBILSEP,C'C'                                                     
         BNE   SETMTYX                                                          
         MVI   MTYPEBIL+3,C'C'   CLIENT CASH DISCOUNT AOR                       
         B     SETMTYX                                                          
                                                                                
SETMTY14 TM    PBILCMSW,X'02'                                                   
         BNO   *+14                                                             
         MVC   MTYPEBIL,=CL5'COM' COMMISSION ONLY                               
         B     SETMTYX                                                          
                                                                                
         CLI   PBILCDSW,C'S'                                                    
         BNE   SETMTY16                                                         
         MVC   MTYPEBIL,=CL5'CDM' MAIN BILL OF A CD PAIR                        
         CLI   PBILSEP,C'C'                                                     
         BNE   *+10                                                             
         MVC   MTYPEBIL,=CL5'CDC' CD BILL OF PAIR                               
         TM    PBILCMSW,X'01'    CHECK UPFRONT WITH THESE TYPES TOO             
         BNO   *+8                                                              
         MVI   MTYPEBIL+3,C'U'                                                  
         TM    PBILCMSW,X'08'                                                   
         BNO   *+8                                                              
         MVI   MTYPEBIL+3,C'N'                                                  
         B     SETMTYX                                                          
                                                                                
SETMTY16 CLI   PBILSEP,C'A'                                                     
         BNE   *+14                                                             
         MVC   MTYPEBIL,=CL5'ADJ' COMMISSION ADJUSTMENT                         
         B     SETMTYX                                                          
*                                                                               
         TM    PBILCMSW,X'01'                                                   
         BNO   *+14                                                             
         MVC   MTYPEBIL,=CL5'UFC' CLIENT AOR UPFRONT COMMISSION                 
         B     SETMTYX                                                          
         TM    PBILCMSW,X'08'                                                   
         BNO   *+14                                                             
         MVC   MTYPEBIL,=CL5'UFN' CLIENT AOR UPFRONT NET                        
         B     SETMTYX                                                          
*                                                                               
         CLI   PBILLTYP,C'M'      MANUAL BILL?                                  
         BNE   *+14                                                             
         MVC   MTYPEBIL,=CL5'MAN'                                               
         B     SETMTYX                                                          
*                                                                               
         MVC   MTYPEBIL,=CL5'REG' REGULAR                                       
         TM    PBILSTAT,X'20'                                                   
         BNO   *+10                                                             
         MVC   MTYPEBIL,=CL5'REGM' GROUP M TRADE MIDAS                          
                                                                                
SETMTYX  B     EXIT                                                             
         DROP  RE                                                               
         EJECT                                                                  
*---------------------------------------------------------------*               
* SETRET - SET OVERRIDE CLI/PRD BASED ON RPROF                  *               
*---------------------------------------------------------------*               
*                                                                               
SETRET   NTR1                                                                   
         MVC   OVRCLT,SPACES                                                    
         MVC   OVRPRD,SPACES                                                    
         CLC   =C'RET',TYPEBIL    ONLY FOR CORP RETAIL                          
         BNE   SETRETX                                                          
         CLI   PBRETAIL,X'01'                                                   
         BE    SETRET10                                                         
         CLI   PBRETAIL,X'41'                                                   
         BNE   SETRETX                                                          
SETRET10 GOTO1 MXPROF,DMCB,MTPFCOV,PROFWORK                                     
         CLC   PROFWORK,SPACES                                                  
         BE    SETRET15                                                         
         MVC   OVRCLT,PROFWORK                                                  
         OC    OVRCLT,SPACES                                                    
SETRET15 GOTO1 MXPROF,DMCB,MTPFPOV,PROFWORK                                     
         CLC   PROFWORK,SPACES                                                  
         BE    SETRETX                                                          
         MVC   OVRPRD,PROFWORK                                                  
         OC    OVRPRD,SPACES                                                    
SETRETX  B     XIT                                                              
         EJECT                                                                  
*----------------------------------------------------------*                    
* CHKBZERO - CHECKS IF ALL AMOUNTS IN RETAIL BILL ARE ZERO *                    
*    SETS CC  =0 SKIP BILL, NOT EQU ZERO - USE BILL        *                    
*----------------------------------------------------------*                    
*                                                                               
CHKBZERO NTR1                                                                   
         CLI   PBRETAIL,X'81'     SKIP ALL CORP CONTROL                         
         BE    XITYES             IF ALL ZERO AMOUNTS                           
         CP    PBILLGRS,=P'0'     SKIP ZERO BILLS                               
         BNE   XITNO                                                            
         CP    PBILLNET,=P'0'                                                   
         BNE   XITNO                                                            
         CP    PBILLBIL,=P'0'                                                   
         BNE   XITNO                                                            
         CP    PBILLRCV,=P'0'                                                   
         BNE   XITNO                                                            
         LA    RE,PBILLEL                                                       
         SR    R0,R0                                                            
CHKBZ10  CLI   0(RE),0                                                          
         BE    XITYES             NO AMOUNTS                                    
         CLI   0(RE),X'0A'                                                      
         BE    CHKBZ20                                                          
         CLI   0(RE),X'84'                                                      
         BE    CHKBZ30                                                          
CHKBZ12  ICM   R0,1,1(RE)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    RE,R0                                                            
         B     CHKBZ10                                                          
*                                                                               
         USING PBILVEL,RE                                                       
CHKBZ20  OC    PBILLVAT,PBILLVAT                                                
         BNZ   XITNO              NOT ALL AMOUNTS ZERO                          
         B     CHKBZ12             KEEP ON LOOKING                              
         DROP  RE                                                               
*                                                                               
         USING PBLPSTEL,RE                                                      
CHKBZ30  OC    PBLPVAMT,PBLPVAMT                                                
         BNZ   XITNO               VAT AMOUNT NOT ZERO                          
         OC    PBLPVBAS,PBLPVBAS   VAT BASIS AMOUNT NOT ZERO                    
         BNZ   XITNO                                                            
         B     CHKBZ12                                                          
         DROP  RE                                                               
         EJECT                                                                  
*-------------------------------------------------------------*                 
* SETERR - SETS CURRENT ERROR MSG FOR BILL IN AOR SET         *                 
*-------------------------------------------------------------*                 
*                                                                               
SETERR   NTR1                                                                   
         LA    R1,ACPBLK                                                        
         USING ACPOSTD,R1                                                       
         MVC   ACPERR,AORERR                                                    
         MVC   ACPERRC,AORERRC                                                  
         MVC   CONERR,AORCERR                                                   
         ZIC   RE,ABILCNT         IF MAIN AOR ERRORS                            
         CH    RE,=H'1'                                                         
         BNE   SETERRX                                                          
         LA    R3,AORERRL(R3)                                                   
         OC    ACPERR,AORERR      COMBINE ERROR W/TRUE AOR BILL ERRS            
         OC    ACPERRC,AORERRC                                                  
         CLI   AORCERR,0                                                        
         BNE   SETERRX                                                          
         MVI   CONERR,1                                                         
SETERRX  B     EXIT                                                             
         DROP  R1,R3                                                            
         SPACE                                                                  
*----------------------------------------------------------*                    
* SETPST - FOR AOR BILLS MOVE SAVED POSTINGS TO V(POSTINGS)*                    
*          AND RESET PST VALUES                            *                    
*----------------------------------------------------------*                    
*                                                                               
SETPST   NTR1                                                                   
         GOTO1 CLRAREA,DMCB,V(POSTINGS),V(POSTNGSX)                             
         ICM   RE,15,0(R2)           CURRENT POSTINGS                           
         L     RF,=V(POSTINGS)                                                  
         ZIC   R1,PSTNUM2                                                       
*                                                                               
SETPST5  MVC   0(ACPRTNL2,RF),0(RE)                                             
         LA    RE,ACPRTNL2(RE)                                                  
         LA    RF,ACPRTNL2(RF)                                                  
         BCT   R1,SETPST5                                                       
*                                                                               
         GOTO1 =A(SETVATO),DMCB,(RC),ABILL                                      
         B     EXIT                                                             
         EJECT                                                                  
                                                                                
***********************************************************************         
* NEWCLT - NEW CLIENT - READ PROF MAINT RECORDS                                 
***********************************************************************         
NEWCLT   NTR1                                                                   
*                                                                               
         XC    OVERCLT,OVERCLT    CLEAR CLIENT OVERRIDE                         
         LA    R2,PCLTREC                                                       
         USING PCLTREC,R2                                                       
         MVC   SVCOFF,PCLTOFF     MEDIA OFFICE                                  
         MVI   SVUSR,0                                                          
*                                                                               
         LA    RE,PCLTELEM                                                      
         SR    R0,R0                                                            
CLT5     CLI   0(RE),0                                                          
         BE    CLT20                                                            
         CLI   0(RE),X'20'                                                      
         BE    CLT10                                                            
         CLI   0(RE),X'30'         CLIENT CODE ELEMENT                          
         BE    CLT14                                                            
CLT5A    ICM   R0,1,1(RE)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    RE,R0                                                            
         B     CLT5                                                             
*                                                                               
         USING PCLTUDEF,RE                                                      
CLT10    TM    PCLTP1F1,X'08'      TEST TRANSFER P1                             
         BZ    *+8                                                              
         OI    SVUSR,SVPU1                                                      
         TM    PCLTP2F1,X'08'      TEST TRANSFER P2                             
         BZ    *+8                                                              
         OI    SVUSR,SVPU2                                                      
         TM    PCLTE1F1,X'08'      TEST TRANSFER E1                             
         BZ    *+8                                                              
         OI    SVUSR,SVEU1                                                      
         TM    PCLTE2F1,X'08'      TEST TRANSFER E2                             
         BZ    *+8                                                              
         OI    SVUSR,SVEU2                                                      
         MVC   SVPDESC1,PCLTPU1    USER FIELD DESCRIPTIONS                      
         MVC   SVPDESC2,PCLTPU2                                                 
         MVC   SVEDESC1,PCLTEU1                                                 
         MVC   SVEDESC2,PCLTEU2                                                 
         B     CLT5A               NEXT ELEMENT                                 
*                                                                               
         USING PCLTDRD,RE                                                       
CLT14    MVC   OVERCLT,PCLTDRDC    SAVE CLIENT OVERRIDE                         
         B     CLT5A               NEXT ELEMENT                                 
         DROP  RE                                                               
*                                                                               
CLT20    CLI   SVPROF+1,YES       USE OFFICE GROUPS?                            
         BNE   CLTF50                                                           
         XC    SVOFFG,SVOFFG      CLEAR OLD OFFICE GROUP                        
*                                                                               
         LA    R3,GRPTBL                                                        
CLTF30   CLI   0(R3),X'FF'        END OF OFFICE GROUP (LIST) TABLE?             
         BE    CLTF50                                                           
*                                                                               
         XC    DUB,DUB                                                          
         LA    R1,DUB                                                           
         USING OFFICED,R1                                                       
         MVI   OFCSYS,C'P'        PRINT SYSTEM                                  
         MVI   OFCAUTH,C'$'       OFFICE GROUP IDENTIFIER                       
         MVC   OFCAUTH+1(1),0(R3) OFFICE GROUP CODE                             
         MVC   HALF,OFCAUTH                                                     
         MVC   OFCOFC,SVCOFF      MEDIA OFFICE CODE                             
         MVC   OFCAGY,QAGENCY                                                   
         DROP  R1                                                               
                                                                                
         GOTO1 AOFFICER,DMCB,DUB,VCOMFACS                                       
         CLI   0(R1),0                                                          
         BE    CLTF40                                                           
         LA    R3,1(,R3)          TRY NEXT OFFICE LIST                          
         B     CLTF30                                                           
*                                                                               
CLTF40   MVC   SVOFFG,HALF        SAVE NEW OFFICE LIST                          
*                                                                               
CLTF50   BAS   RE,RDPROF                                                        
         GOTO1 MXPROF,DMCB,MTPFDACC,PROFWORK                                    
         BAS   RE,SETACC                                                        
         OC    PROFWORK,PROFWORK  SPECIAL ACCFILE TO READ FROM?                 
         BNZ   CLTF60             YES                                           
         CLC   ACCTCD2,ACCTCD                                                   
         BE    CLTF70                                                           
         MVC   PROFWORK(2),ACCALPH                                              
CLTF60   BAS   RE,CHGFILE          CHANGE TO NEW ACCFILE                        
CLTF70   CLC   ACCALPH,QAGENCY     SAME AGENCY?                                 
         BE    *+8                 YES: CONTINUE                                
         BRAS  RE,SMFOX            NO:  KEEP A RECORD OF THAT                   
         GOTO1 =A(RDCLT),DMCB,(RC) READ ACC CLIENT RECORD & SET VALUES          
CLTFX    B     EXIT                                                             
         DROP  R2                                                               
*                                                                               
GRPTBL   DC    C'ABCDEFGHIJKLMNOPQRSTUVWXYZ'  OFFICE GROUPS (LISTS)             
         DC    C'123456789'                   ..                                
         DC    C'<>#@'                        SPECIAL CHARACTERS                
         DC    X'41424344464748494C'          NEW 2 CHAR LIST'S 1 BYTE          
         DC    X'51525354565758595B5D5E5F'    HEX VALUE                         
         DC    X'61626364666768696B6C6D6F'    ..                                
         DC    X'71727374767778797B7C'        ..                                
         DC    X'81828384868788898B8C8D8E'    ..                                
         DC    X'91929394969798999B9C9D9E'    ..                                
         DC    X'A1A2A3A4A6A7A8A9ABACADAE'    ..                                
         DC    X'B1B2B3B4B6B7B8B9BBBCBDBE'    ..                                
         DC    X'CBCCCDCE'                    ..                                
         DC    X'DBDCDDDE'                    ..                                
         DC    X'EBECEDEE'                    ..                                
         DC    X'FBFCFDFE'                    ..                                
         DC    X'FF'                          END OF TABLE                      
         EJECT                                                                  
                                                                                
***********************************************************************         
* READ B1 PROFILE FOR LOWEST LEVEL TO SEE IF                                    
* SYSTEM/MEDIA CODE HAS BEEN REDEFINED                                          
* HIERARCHY FROM HIGHEST TO LOWEST:                                             
* 1. AGENCY LEVEL                                                               
* 2. MEDIA LEVEL                                                                
* 3. MEDIA "ALL" OFFICE LEVEL                                                   
* 4. OFFICE LEVEL                                                               
* RULES:  A.  *,* = NULL,DEFAULT MEDIA                                          
*         B.  *,M = DEF SYS ("P"),REDEF MEDIA                                   
*         C.  S,* = REDEF SYS,NULL                                              
*         D.  S,M = REDEF SYS,REDEF MEDIA                                       
***********************************************************************         
RDB1PROF NTR1                                                                   
         XC    SVRSYS,SVRSYS                                                    
         XC    SVRMED,SVRMED                                                    
         XC    B1PROF,B1PROF                                                    
         XC    WORK,WORK                                                        
*                                                                               
         MVC   WORK(4),=C'P0B1'                                                 
         MVC   WORK+4(2),RCAGYFLT  FILL IN AGENCY                               
         MVC   WORK+6(1),MYMED     FILL IN MEDIA                                
         MVC   WORK+7(3),MYCLT     FILL IN CLIENT                               
         MVI   WORK+10,C'*'                                                     
         MVC   WORK+11(1),SVCOFF   FILL IN OFFICE                               
         GOTO1 GETPROF,DMCB,WORK,B1PROF,DATAMGR                                 
         OC    B1PROF,B1PROF                                                    
         BZ    RDB1X               NO B1 PROFILES SET UP FOR THIS AGY           
         CLC   B1PROF+2(2),=C'**'  BOTH DEFINED AS "**" THEN...                 
         BNE   *+14                                                             
         MVC   SVRMED,MYMED        ...JUST STORE THE MEDIA                      
         B     RDB1X                                                            
         MVI   SVRSYS,C'P'         MOVE IN DEFAULT SYSTEM                       
         CLI   B1PROF+2,C'*'       "*" = DON'T STORE                            
         BE    *+10                                                             
         MVC   SVRSYS,B1PROF+2     MOVE IN REDEFIEND SYSTEM                     
*        MVC   SVRMED,MYMED        MOVE IN DEFAULT MEDIA                        
         CLI   B1PROF+3,C'*'                                                    
         BE    RDB1X                                                            
         MVC   SVRMED,B1PROF+3     MOVE IN REDEFINED MEDIA                      
RDB1X    B     XIT                                                              
*                                                                               
*-------------------------------------------*                                   
* CHGFILE - SETS VALUES FOR NEW ACCFILE     *                                   
*-------------------------------------------*                                   
*                                                                               
CHGFILE  NTR1                                                                   
         XC    AKEY,AKEY                                                        
         LA    RE,AKEY                                                          
         USING CT5REC,RE                                                        
         MVI   CT5KTYP,CT5KTYPQ   C'5'                                          
         MVC   CT5KALPH,PROFWORK  ALPHA USER ID                                 
         DROP  RE                                                               
         L     R2,AMYAREA                                                       
         USING CT5REC,R2                                                        
         GOTO1 DATAMGR,DMCB,=C'DMREAD  ',=C'CTFILE ',AKEY,(R2),0                
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'               WHAT SHOULD I DO???                           
         SR    R0,R0                                                            
         LA    R1,CT5DATA                                                       
         USING CTSYSD,R1                                                        
CHGFIL5  CLI   0(R1),0                                                          
         BNE   *+6                                                              
         DC    H'0'               WHAT SHOULD I DO????                          
         CLI   0(R1),CTSYSELQ     X'21' - SYSTEM ELEMENT                        
         BNE   CHGFIL8                                                          
         CLI   CTSYSNUM,6         ACC SYSTEM                                    
         BE    CHGFIL10                                                         
CHGFIL8  ICM   R0,1,1(R1)                                                       
         AR    R1,R0                                                            
         B     CHGFIL5                                                          
*                                                                               
CHGFIL10 MVC   ACCTSE2,CTSYSSE    SE NUM                                        
         MVC   ACCTCD2,CTSYSAGB   COMPANY CODE                                  
         MVC   TEMPSE,ACCTSE2                                                   
         MVC   TEMPCD,ACCTCD2                                                   
         BAS   RE,SETFILE         OPEN UP FILES & SET VALLUES                   
         MVC   ACCTOID2,TEMPOID                                                 
         B     XIT                                                              
         DROP  R1,R2                                                            
         EJECT                                                                  
*---------------------------------------------*                                 
* SETACC - SETS CURRENT ALPHA ID              *                                 
*---------------------------------------------*                                 
*                                                                               
SETACC   NTR1                                                                   
         XC    ACCALPH,ACCALPH                                                  
         OC    SVPROF2(2),SVPROF2 IF GIVEN OVERRIDE ACC CONNECTION              
         BZ    SETACC5                                                          
         MVC   ACCALPH,SVPROF2    IN CONTROL PROFILE - USE IT                   
         B     SETACC10                                                         
*                                                                               
SETACC5  OC    PROFWORK,PROFWORK  IF GIVEN OVERRIDE ACC FILE IN                 
         BZ    SETACC8                                                          
         MVC   ACCALPH,PROFWORK   MY PROF MAINT RECORDS - USE THAT              
         B     SETACC10                                                         
*                                                                               
SETACC8  MVC   ACCALPH,RCAGYFLT    NONE OF ABOVE - USE CUR ACC CONNECT          
SETACC10 OC    ACCALPH,ACCALPH                                                  
         BNZ   *+6                                                              
         DC    H'0'                                                             
SETACCX  B     EXIT                                                             
         SPACE 2                                                                
*----------------------------------------------------------*                    
* NEWPRD - NEW PRODUCT- READ PROF MAINT RECORDS            *                    
*----------------------------------------------------------*                    
*                                                                               
NEWPRD   NTR1                                                                   
         LA    R2,PPRDREC                                                       
         USING PPRDREC,R2                                                       
*                                                                               
         MVC   SVPUSER1,SPACES                                                  
         MVC   SVPUSER2,SPACES                                                  
         XC    SVUCMPLN,SVUCMPLN                                                
         MVC   SVUCMDP,SPACES                                                   
         MVC   SVUCMP,SPACES                                                    
         LA    R1,PPRDELEM        FIRST EL                                      
NEWPR02  CLI   0(R1),0                                                          
         BE    NEWPR06                                                          
         CLI   0(R1),X'08'        PPRDUDEF MAKE SURE YOU HAVE USER DATA         
         BE    NEWPR04                                                          
         SR    R0,R0                                                            
         IC    R0,1(R1)                                                         
         AR    R1,R0                                                            
         B     NEWPR02                                                          
NEWPR04  MVC   SVPUSER1,PUSER1-PPRDUDEF(R1)   USER FIELDS                       
         MVC   SVPUSER2,PUSER2-PPRDUDEF(R1)                                     
         OC    SVPUSER1,SPACES                                                  
         OC    SVPUSER2,SPACES                                                  
*                                                                               
NEWPR06  LA    RE,UCOMBLK                                                       
         LA    RF,L'UCOMBLK                                                     
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         LA    R3,UCOMBLK         NOW GET NEW UCOM USER DEF FIELDS              
         USING DDUCOMD,R3                                                       
         MVC   UCACOMF,VCOMFACS    A(COMFACS)                                   
         MVI   UCSYS,C'P'          PRINT SYSTEM                                 
         MVC   UCPROG,=C'MX'       CALLING PGM(THIS MEANS CHK MBI BIT)          
         MVC   UCAGY,QAGENCY       ALPHA CODE                                   
         MVC   UCMED,MYMED         MEDIA CODE                                   
         MVC   UCCLT,MYCLT         CLIENT                                       
         MVC   UCPRD,MYPRD         PRODUCT                                      
         OI    UCOPT,UCOPRD        RETURN PRODUCT UCOMS                         
         GOTO1 =V(DDUCOM),UCOMBLK                                               
         CLI   UCERROR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         TM    UCDATA,UCDNOCLT+UCDNOPRD     NO CLIENT/PRODUCT FOUND?            
         BNZ   NEWPR10                                                          
         MVC   SVUCMPLN,UCPLENS    LENGTHS OF 4 PRODUCT UCOM FIELDS             
         L     RF,UCPTTLS                                                       
         MVC   SVUCMP1D(L'SVUCMDP),0(RF) PRODUCT UCOMS DESCRIPTIONS             
         L     RF,UCPDATA                                                       
         MVC   SVUCMP1(L'SVUCMP),0(RF) PRODUCT UCOMS                            
         OC    SVUCMP1D(L'SVUCMDP),SPACES                                       
         OC    SVUCMP1(L'SVUCMP),SPACES                                         
*                                                                               
NEWPR10  BAS   RE,RDPROF                                                        
         GOTO1 MXPROF,DMCB,MTPFDACC,PROFWORK                                    
         BAS   RE,SETACC          SET CURRENT ACC POWER CODE                    
         OC    PROFWORK,PROFWORK  SPECIAL ACCFILE TO READ FROM?                 
         BNZ   NEWPR20            YES                                           
         CLC   ACCTCD2,ACCTCD                                                   
         BE    NEWPR30                                                          
         MVC   PROFWORK(2),ACCALPH                                              
NEWPR20  BAS   RE,CHGFILE          CHANGE TO NEW ACCFILE                        
NEWPR30  CLC   ACCALPH,QAGENCY     SAME AGENCY?                                 
         BE    *+8                 YES: CONTINUE                                
         BRAS  RE,SMFOX            NO:  KEEP A RECORD OF THAT                   
         GOTO1 =A(RDPRD),DMCB,(RC) READ ACC PRD RECORDS & SET VALUES            
PRDFX    B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
*------------------------------------------------------------------*            
* CHKPROF - READS PROFILES TO SEE IF DDS/OR AGY MAKE POSTINGS =NO  *            
*    XIT  - MAKEPOST SET                                           *            
*------------------------------------------------------------------*            
*                                                                               
CHKPROF  NTR1                                                                   
         XC    MSGTYPE,MSGTYPE    MEDIA PROFILE LEVEL                           
         MVI   MSGTYPE,C'P'       PRODUCT PROFILE LEVEL                         
         MVC   MSGTYPE+1(L'MYPRD),MYPRD                                         
*                                                                               
         MVI   MAKEPOST,C'N'      DON'T POST                                    
         GOTO1 MXPROF,DMCB,MTPFDPST,PROFWORK                                    
         OC    PROFWORK,PROFWORK  DDS ALLOWS THEM TO POST?                      
         BZ    CHKPROF8           NO PROFILE SET UP = N                         
         CLI   PROFWORK,YES                                                     
         BNE   CHKPROF8           DDS=N = NO POSTINGS                           
         MVI   MAKEPOST,C'Y'      YES DDS ALLOWS POSTING                        
         GOTO1 MXPROF,DMCB,MTPFPST,PROFWORK                                     
         OC    PROFWORK,PROFWORK  THEY THEMSELVES ALLOW POSTING?                
         BNZ   CHKPROF5                                                         
         MVI   MAKEPOST,C'N'      NO DDS=Y & AGY=N --> N                        
         B     CHKPROF8                                                         
*                                                                               
CHKPROF5 CLI   PROFWORK,YES                                                     
         BE    CHKPROFX           DDS=Y & AGY=Y                                 
         MVI   MAKEPOST,C'N'      DDS=Y & AGY=N                                 
*                                                                               
CHKPROF8 CLI   QOPT4,C'M'         IF MARK OR UNPOST VERSION                     
         BE    CHKPROF9                                                         
         CLI   QOPT4,C'U'                                                       
         BNE   *+12                                                             
CHKPROF9 MVI   MAKEPOST,C'Y'      ALWAYS ALLOWED                                
         B     CHKPROFX                                                         
*                                                                               
         LA    R2,PRTBLOCK                                                      
         USING MXPRNTD,R2                                                       
         GOTO1 =A(SETPRT),DMCB,(RC)                                             
         MVI   PMODE,PMMSG        PRINT MSG                                     
         OI    PMSG,PMNOPOST      POST=NO                                       
         MVC   PMSGTYPE,MSGTYPE   LEVEL WHICH PROF/MAINT APPLIES                
         GOTO1 =V(MXPRNT),DMCB,(R2)                                             
CHKPROFX B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
*----------------------------------------------------------*                    
* CHKDIV - GETS DIVISIONS IF PROFILE SET                   *                    
*----------------------------------------------------------*                    
*                                                                               
CHKDIV   NTR1                                                                   
         LA    R2,PPRDREC                                                       
         USING PPRDREC,R2                                                       
         XC    PGRCD,PGRCD        CLEAR DIVISION/PRODUCT GROUP CODE             
         XC    PGRNAME,PGRNAME    & NAME                                        
         GOTO1 MXPROF,DMCB,MTPFPGR,PROFWORK                                     
         OC    PROFWORK,PROFWORK                                                
         BZ    CHKDIVX            NO PRODUCT GROUPS                             
         CLI   PROFWORK,NO                                                      
         BE    CHKDIVX            NO PRODUCT GROUPS                             
         CLC   PPRDDIV,SPACES     ANY PRODUCT GROUPS FOR THIS PRODUCT?          
         BNH   CHKDIVX                                                          
         L     RF,UTL                                                           
         MVC   4(1,RF),SAVSYS                                                   
         XC    KEY,KEY                                                          
         LA    R1,KEY                                                           
         USING PDIVREC,R1                                                       
         MVC   PDIVKAGY,PPRDKAGY  AGENCY CODE                                   
         MVC   PDIVKMED,PPRDKMED  MEDIA CODE                                    
         MVI   PDIVKRCD,X'03'     RECORD TYPE                                   
         MVC   PDIVKCLT,PPRDKCLT  CLIENT CODE                                   
         MVC   PDIVKDIV,PPRDDIV   DIVISION FROM PRODUCT RECORD                  
         DROP  R1                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(25),KEYSAVE                                                  
         BE    CHKDIV20                                                         
***                                                                             
         MVC   WORK,SPACES                                                      
         MVC   WORK(32),=CL32'AUTONOTE*YKVA,BPLA:*MISSING DIV '                 
*        GOTO1 HEXOUT,DMCB,KEYSAVE,WORK+32,10                                   
         MVC   WORK+32(10),KEYSAVE                                              
         OC    WORK,SPACES                                                      
         MVI   BYTE,L'WORK                                                      
         GOTO1 DATAMGR,DMCB,=C'OPMSG',(BYTE,WORK)                               
***                                                                             
CHKDIV20 LA    R3,PDIVREC                                                       
         ST    R3,AREC                                                          
         GOTO1 GETPRT                                                           
         USING PDIVREC,R3                                                       
         MVC   PGRCD,PPRDDIV      SAVE DIVISION                                 
         MVC   PGRNAME,PDIVNAME   SAVE DIVISION NAME                            
CHKDIVX  B     EXIT                                                             
         DROP  R2,R3                                                            
         EJECT                                                                  
*----------------------------------------------------------*                    
* CHKREG - GETS REGION AND DISTRICT CODE AND NAME          *                    
*----------------------------------------------------------*                    
*                                                                               
CHKREG   NTR1                                                                   
         LA    R2,PPRDREC                                                       
         USING PPRDREC,R2                                                       
         LA    R4,PBILLREC                                                      
         XC    REGCODE,REGCODE    CLEAR SAVE AREAS                              
         XC    REGNAME,REGNAME                                                  
         XC    DISTCODE,DISTCODE                                                
         XC    DISTNAME,DISTNAME                                                
         CLI   PBILLTYP,C'M'      SKIP IF MANUAL OR RETAIL BILL                 
         BE    CHKREGX                                                          
         CLI   PBRETAIL,0                                                       
         BNE   CHKREGX                                                          
         TM    PBILCMSW,X'10'+X'20' SKIP AOR TOO                                
         BNZ   CHKREGX                                                          
         CLC   PNBMGR1,SPACES     IF NO REGION FORGET IT                        
         BNH   CHKREGX                                                          
         CLC   PNBMGR2,SPACES     IF NO DISTRICT FORGET IT                      
         BNH   CHKREGX                                                          
         OC    PNBPGR1,PNBPGR1    IS THERE A DIVISION ON BILL?                  
         BNZ   CHKREG04           YES - CONTINUE                                
         CLC   PPRDDIV,SPACES     IS THERE A DIVISION ON THE PRODUCT?           
         BNH   CHKREGX                                                          
*                                                                               
CHKREG04 CLC   =C'999',PNBMGR1    DEFAULT REGION 999?                           
         BNE   CHKREG06                                                         
         MVC   REGCODE,=C'999'    SAVE DEFAULT REGION/DISTRICT                  
         MVC   REGNAME,=CL20'MASTER REGION'                                     
         MVC   DISTCODE,=C'999'                                                 
         MVC   DISTNAME,=CL20'MASTER DISTRICT'                                  
         B     CHKREGX                                                          
*                                                                               
CHKREG06 XC    KEY,KEY                                                          
         LA    R1,KEY                                                           
         USING PREGREC,R1                                                       
         MVC   PREGKAGY,QAGENCY   AGENCY CODE                                   
         MVC   PREGKMED,MYMED     MEDIA CODE                                    
         MVI   PREGKRCD,X'04'     RECORD TYPE FOR REGION                        
         LA    RE,MYCLT                                                         
         OC    OVERCLT,OVERCLT    CLT OVERRIDE FOR REG/DIS LOOKUP?              
         BZ    *+8                                                              
         LA    RE,OVERCLT                                                       
         MVC   PREGKCLT,0(RE)     CLIENT CODE                                   
         LA    RE,PNBPGR1         DIVISION FROM BILL RECORD                     
         OC    PNBPGR1,PNBPGR1                                                  
         BNZ   *+8                                                              
         LA    RE,PPRDDIV         DIVISION CODE FROM PRODUCT                    
         MVC   PREGKDIV,0(RE)     DIVISION                                      
         MVC   PREGKREG,PNBMGR1   REGION FROM BILL RECORD                       
         DROP  R1                                                               
         GOTO1 HIGH               HIGH ON DIRECTORY                             
         CLC   KEY(25),KEYSAVE                                                  
         BNE   CHKREGX                                                          
         LA    R3,PREGREC                                                       
         ST    R3,AREC                                                          
         GOTO1 GETREG             GET THE REGION RECORD                         
         USING PREGREC,R3                                                       
         MVC   REGCODE,PREGKREG   SAVE REGION CODE                              
         MVC   REGNAME,PREGNAME   AND NAME                                      
*                                                                               
         XC    KEY,KEY                                                          
         LA    R1,KEY                                                           
         USING PDSTREC,R1                                                       
         MVC   PDSTKAGY,QAGENCY   AGENCY CODE                                   
         MVC   PDSTKMED,MYMED     MEDIA CODE                                    
         MVI   PDSTKRCD,X'05'     RECORD TYPE FOR DISTRICT                      
         LA    RE,MYCLT                                                         
         OC    OVERCLT,OVERCLT    CLT OVERRIDE FOR REG/DIS LOOKUP?              
         BZ    *+8                                                              
         LA    RE,OVERCLT                                                       
         MVC   PDSTKCLT,0(RE)     CLIENT CODE                                   
         LA    RE,PNBPGR1         DIVISION FROM BILL RECORD                     
         OC    PNBPGR1,PNBPGR1                                                  
         BNZ   *+8                                                              
         LA    RE,PPRDDIV         DIVISION CODE FROM PRODUCT                    
         MVC   PDSTKDIV,0(RE)     DIVISION                                      
         MVC   PDSTKREG,PNBMGR1   REGION FROM BILL RECORD                       
         MVC   PDSTKDST,PNBMGR2   DISTRICT FROM BILL RECORD                     
         DROP  R1                                                               
         GOTO1 HIGH               HIGH ON DIRECTORY                             
         CLC   KEY(25),KEYSAVE                                                  
         BNE   CHKREGX                                                          
         LA    R3,PDSTREC                                                       
         ST    R3,AREC                                                          
         GOTO1 GETDIST            GET THE DISTRICT RECORD                       
         USING PDSTREC,R3                                                       
         MVC   DISTCODE,PDSTKDST  SAVE DISTRICT CODE                            
         MVC   DISTNAME,PDSTNAME  AND NAME                                      
CHKREGX  B     EXIT                                                             
         DROP  R2,R3                                                            
         EJECT                                                                  
*----------------------------------------------------------*                    
* MARK - CHECKS BILL BY BILL TYPE AND (R,I) DATE TO SEE    *                    
*        IF BILL SHOULD BE MARKED TRANSFERRED              *                    
*   XIT  CC NOT EQUAL IF BILL DOESN'T QUALIFY              *                    
*----------------------------------------------------------*                    
*                                                                               
MARK     NTR1                                                                   
         MVI   PSTAOR,C'Y'        NO AOR PROBLEMS                               
         CLC   =C'ALL',QBTYPE     PARTICULAR BILL TYPE REQUESTED?               
         BE    *+14                                                             
         CLC   QBTYPE,TYPEBIL     BILL TYPES MUST MATCH                         
         BNE   XITNO              NO - SKIP TO NEXT BILL                        
*                                                                               
         MVC   WORK(6),PBILLDAT                                                 
         CLI   QOPT3,C'R'                                                       
         BE    MARK5                                                            
         GOTO1 DATCON,DMCB,(3,PBILINVD),(0,WORK)                                
MARK5    CLC   WORK(6),QIORDT     UPTO AND INCLUDING DATE                       
         BH    XITNO              REQUESTED                                     
*                                                                               
         CLC   =C'AOR',TYPEBIL    GET TRUE INVOICE NUMBER                       
         BNE   MARK10                                                           
         GOTOR GETRUE,DMCB,ABILL                                                
*                                                                               
MARK10   BAS   RE,PRTBIL          PRINT BILL TO BE MARKED                       
         B     XITYES                                                           
         EJECT                                                                  
*----------------------------------------------------------*                    
* CHKREV  - CHECKS BILL BY BILL TYPE, (R,I) DATE, TRANSFER *                    
*           DATE OR BILL NUMS TO SEE IF BILL SHOULD BE REV *                    
*   XIT -   IF BILL DOESN'T MEET REQUIREMENTS              *                    
*----------------------------------------------------------*                    
*                                                                               
CHKREV   NTR1                                                                   
         CLC   =C'ALL',QBTYPE     PARTICULAR BILL TYPE REQUESTED?               
         BE    *+14                                                             
         CLC   QBTYPE,TYPEBIL     BILL TYPES MUST MATCH                         
         BNE   XITNO                                                            
*                                                                               
         OC    REQTDT,REQTDT      PARTICULAR TRANSFER DATE REQUESTED?           
         BZ    *+14                                                             
         CLC   PBILPOST,REQTDT    TRANSFER DATES MUST MATCH                     
         BNE   XITNO                                                            
*                                                                               
         CLI   QOPT3,C' '         MATCH ON DATE REQUESTED?                      
         BE    CHKREV20                                                         
         MVC   WORK(6),PBILLDAT                                                 
         CLI   QOPT3,C'R'                                                       
         BE    CHKREV10                                                         
         GOTO1 DATCON,DMCB,(3,PBILINVD),(0,WORK)                                
CHKREV10 CLC   QIORDT,WORK                                                      
         BNE   XITNO                                                            
*                                                                               
CHKREV20 L     R1,PPWORK2C        2ND PPG WORKD                                 
         USING PPWORK2D,R1                                                      
         CLC   QBILL1,SPACES                                                    
         BE    CHKREV40                                                         
         CLC   QBILL2,SPACES                                                    
         BNE   CHKREV30                                                         
         CLC   QBILL1,BINVOICE    ONE BILL NUMBER                               
         BNE   XITNO                                                            
         B     CHKREV35                                                         
CHKREV30 CLC   BINVOICE,QBILL1   CHECK AGAINST RANGE OF BILLS                   
         BL    XITNO                                                            
         BE    CHKREV35                                                         
         CLC   BINVOICE,QBILL2                                                  
         BH    XITNO                                                            
         B     CHKREV40                                                         
         DROP  R1                                                               
CHKREV35 CLC   =C'AOR',TYPEBIL    FIRST BILL NUMBER                             
         BNE   XITYES                                                           
         TM    PBILCMSW,X'10'     IF AOR - MUST BE FIRST IN SET                 
         BNO   XITNO                                                            
         CLI   PBILSEP,C'R'                                                     
         BNE   XITNO                                                            
         B     XITYES                                                           
*                                                                               
CHKREV40 TM    PBILCMSW,X'20'     IF TRUE AOR BILL                              
         BO    XITNO              SKIP IT- ALREADY PROCESSED                    
         B     XITYES                                                           
         EJECT                                                                  
*----------------------------------------------------------*                    
* REVERSE - REVERSES A BILL, IF AOR REVERSE A SET          *                    
*   XIT -   IF BILL DIDN'T REVERSE CORRECTLY OR UNPOST OPT *                    
*           CC SET NOT EQUAL                               *                    
*----------------------------------------------------------*                    
*                                                                               
REVERSE  NTR1                                                                   
         CLC   =C'AOR',TYPEBIL                                                  
         BE    REV50                                                            
         XC    AORERRS,AORERRS                                                  
         MVI   TEST,C'N'                                                        
         GOTO1 =A(UNDO),DMCB,(RC)                                               
         BAS   RE,PRTREV          PRINT REVERSAL                                
         BAS   RE,GETSME          RE-ESTABLISH SEQUENCE                         
         MVC   AREC,ABILL                                                       
         GOTO1 DATAMGR,DMCB,GETREC,PRTFILE,KEY+27,AREC,DMWORK                   
         CLI   QOPT4,C'U'         IF REVERSAL TO UNPOST - SKIP RETRANS          
         BE    REV20                                                            
         CLI   UNERR,0                                                          
         BE    XIT                NO PROBLEM UNDOING -TRANSFER BILL             
REV20    BAS   RE,GETNXT          PROBLEM - SKIP TRANSFER OF THIS BILL          
         LTR   RB,RB                                                            
         B     XIT                                                              
*                                                                               
REV50    MVI   TEST,C'Y'                                                        
         LA    R3,AORERRS                                                       
         XC    AORERRS,AORERRS                                                  
         GOTOR GETRUE,DMCB,ABILL                                                
         BAS   RE,DOREV           FIRST TIME PASS - TEST                        
*                                                                               
         MVI   TEST,C'N'          TRANSFER SECOND TIME AROUND                   
         OC    AORERRS,AORERRS                                                  
         BZ    *+8                                                              
         MVI   TEST,C'B'          ERRORS FOUND -DON'T TRANS 2ND TIME            
*                                                                               
         BAS   RE,GETSME          GET FIRST BILL IN AOR SET AGAIN               
         GOTO1 DATAMGR,DMCB,GETREC,PRTFILE,KEY+27,AREC,DMWORK                   
         BAS   RE,DOREV           SECOND PASS -TRANSFER/PRINT                   
*                                                                               
         CLI   TEST,C'B'          CAN'T RETRANS OR UNPOST(WHICH DOESN'T         
         BE    REV70              RETRANS)                                      
         CLI   QOPT4,C'U'                                                       
         BNE   REV80                                                            
REV70    MVC   KEY,NEXTKEY        - SKIP TO NEXT BILL RECORD TO PROCESS         
         BAS   RE,GETSME2                                                       
         LTR   RB,RB                                                            
         B     XIT                                                              
*                                 IF RETRANS - THEN START AT                    
REV80    BAS   RE,GETSME          FIRST BILL IN AOR SET                         
         GOTO1 DATAMGR,DMCB,GETREC,PRTFILE,KEY+27,AREC,DMWORK                   
         CR    RB,RB                                                            
         B     XIT                                                              
         EJECT                                                                  
DOREV    NTR1                                                                   
         USING AORERD,R3                                                        
*                                                                               
DOREV10  MVC   TEMPDATE,PBILLDAT  SET 'REAL' INVOICE NUMBER                     
         MVC   TEMPINV,PBILKBNO                                                 
         XC    BINVL,BINVL                                                      
         SR    RF,RF                                                            
         CLC   PBILIMED,SPACES    ONLY STORE LONG INVOICE IF THE RULES          
         BNH   *+8                ARE IN THE BILL HEADER                        
         LA    RF,BINVL                                                         
         GOTO1 =A(SETINV),DMCB,(RC),BINVOICE,(RF)                               
         GOTO1 =A(UNDO),DMCB,(RC)                                               
         MVC   AREC,ABILL                                                       
         CLI   TEST,C'Y'                                                        
         BE    DOREV20                                                          
         BAS   RE,PRTREV          PRINT REVERSAL FOR TEST=N & B                 
         B     DOREV30                                                          
*                                                                               
DOREV20  MVC   AORERR,UNERR                                                     
         LA    R3,AORERRL(R3)                                                   
*                                                                               
DOREV30  L     RE,ABILL                                                         
         XC    KEY,KEY                                                          
         MVC   KEY(L'PBILLKEY),0(RE)                                            
         BAS   RE,GETSME2         RE-ESTABLISH KEY                              
DOREV40  BAS   RE,GETNXT                                                        
         CLC   KEY(PBILKBNO-PBILLKEY),SKEY                                      
         BNE   DOREV60            END OF SET                                    
         GOTO1 DATAMGR,DMCB,GETREC,PRTFILE,KEY+27,AREC,DMWORK                   
         CLC   PBILLDAT,RUNDT      IF DIFFERENT RUN DATE                        
         BNE   DOREV60             END OF SET                                   
         TM    PBILCMSW,X'20'                                                   
         BO    DOREV40                                                          
         TM    PBILCMSW,X'10'      IF NOT CLIENT BILL                           
         BNO   DOREV60             END OF SET                                   
         TM    PBILCMSW,X'08'      IF NET BILL                                  
         BO    DOREV60             END OF SET                                   
         CLI   PBILSEP,C'R'        IF REGULAR                                   
         BE    DOREV60             END OF SET                                   
         B     DOREV10                                                          
*                                                                               
DOREV60  MVC   NEXTKEY,KEY        SAVE NEXT NEW KEY FOR PROCESSING              
DOREVX   B     XIT                                                              
         EJECT                                                                  
*---------------------------------------------*                                 
* PRTREV - CALL MXPRNT TO PRINT REVERSAL LINE *                                 
*---------------------------------------------*                                 
*                                                                               
PRTREV   NTR1                                                                   
         LA    R2,PRTBLOCK                                                      
         USING MXPRNTD,R2                                                       
         GOTO1 =A(SETPRT),DMCB,(RC)                                             
         MVI   PMODE,PMBILL       PRINT BILL INFO                               
         MVI   PREV,C'Y'          INDICATE REVERSAL                             
         OC    AORERRS,AORERRS                                                  
         BZ    *+8                                                              
         OI    UNERR,UNEAOR                                                     
         MVC   PREVERR,UNERR      REVERSAL ERRORS                               
         MVC   PRRCV,SVRCV        REVERSAL RECEIVABLE AMOUNT                    
         MVC   PRGRS,SVGRS        REVERSAL GROSS AMOUNT                         
         MVC   PRNET,SVNET        REVERSAL NET AMOUNT                           
         MVC   PRAGYCOM,SVAGYCOM  REVERSAL AGENCY COMMISSION AMOUNT             
         MVC   PRAOR,SVAOR        REVERSAL AOR PAY/RCVBL AMOUNT                 
         MVC   PRCD,SVCD          REVERSAL CD AMOUNT                            
         MVC   PRGST,SVGST        REVERSAL OUTPUT GST AMOUNT                    
         MVC   PRGSTI,SVGSTI      REVERSAL AOR INPUT GST AMOUNT                 
         MVC   PRPSTO,SVPST       REVERSAL OUTPUT PST AMOUNT                    
         MVC   PRPSTI,SVPSTI      REVERSAL AOR INPUT PST AMOUNT                 
         MVC   PRIOR,SVIOR        REVERSAL IOR AMOUNT                           
         MVC   PRAAGYCM,SVAAGYCM  REVERSAL AOR AGY COM AMOUNT                   
         MVC   PRMULTCM,SMULTCM   REVERSAL FLIP (MULT) AGY COMM                 
         MVC   PJBCD,SVJBCD       ORIGINAL JOB CODE                             
         MVC   PRFLPAOR,SFLIPAOR  REVERSAL FLIP AOR AMOUNT FLAG                 
         MVI   PPOSTERR,0         CLEAR OUT POSTING ERRORS                      
         MVI   PPOSTER2,0                                                       
         MVI   PPOSTERC,0                                                       
         GOTO1 =V(MXPRNT),DMCB,(R2)                                             
         B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
*------------------------------------------------------------*                  
* BILLPOST - MAKE POSTINGS/ADDS POST DETAIL/MARKS BILL RECORD*                  
*------------------------------------------------------------*                  
*                                                                               
BILLPOST NTR1                                                                   
         GOTO1 =A(CONTRAS),DMCB,(RC),V(POSTINGS)                                
         BAS   RE,PRTBIL          PRINT BILL DETAILS                            
         BAS   RE,CHKERRS         IF NO ERRORS                                  
         BNE   BILLPX             (ERRORS - DON'T POST BILL)                    
*                                                                               
         BAS   RE,PSTHDR           INITIALIZE POST DETAIL RECORD                
         GOTO1 =A(SETMXBLK),DMCB,(RC) SET BLOCK FOR MXPOST                      
         L     R3,AMXBLK           R3=A(PARAMETER BLOCK FOR MXPOST)             
         USING MXPOSTD,R3                                                       
         ZIC   R2,PSTNUM2          R2=(MAX NUM OF POSTING ROWS)                 
         L     R4,=V(POSTINGS)     R4=A(POSTING RETURNED FROM ACPOSTER)         
         USING ACPRTND,R4                                                       
*                                                                               
BILLP30  OC    0(ACPRTNL2,R4),0(R4) TEST END OF POSTINGS                        
         BZ    BILLP70                                                          
         GOTO1 CLRAREA,DMCB,V(REC),V(RECX)                                      
         MVC   MXACCOFF,SVACOFF    SET SJ CLI/PRD ACC OFFICE                    
         CLI   ACPBTYP,MBTTINTI    IF INTERNAL POSTING                          
         BL    BILLP50                                                          
         CLI   ACPBTYP,MBTTINTR                                                 
         BH    BILLP50                                                          
         CLC   SVACOF2,SPACES      AND DIFFERENT ACC OFF SPECIFIED              
         BNH   BILLP50                                                          
         MVC   MXACCOFF,SVACOF2    USE IT                                       
*                                                                               
BILLP50  CLI   ACPBTYP,MBTTOINC    IF INTERNAL POSTING                          
         BL    BILLP60                                                          
****     CLI   ACPBTYP,MBTTOREV                                                 
         CLI   ACPBTYP,MBTTIRCV                                                 
         BH    BILLP60                                                          
         CLC   SVACOF2,SPACES      AND DIFFERENT ACC OFFICE SPECIFIED           
         BNH   BILLP60                                                          
         MVC   MXACCOFF,SVACOF2    USE IT                                       
*                                                                               
BILLP60  MVC   TEMPROW,0(R4)       SET CURRENT POSTING ROW                      
         LA    RE,TEMPROW                                                       
         ST    RE,MXROW            PASS ADDRESS TO MXPOST                       
         BAS   RE,STCONTRA         SET CONTRA AND CONTRAN                       
         MVC   MXCONTRA,CONTRA     PASS CONTRA ACOCUNT                          
         MVC   MXCONTRN,CONTRAN    AND CONTRA ACCOUNT NAME                      
         BAS   RE,SETWKCD          SET WORK CODE IF ANY                         
         MVC   MXWKCD,WKCD         PASS WORK CODE                               
         MVC   MXJOBCD,SPACES      PRE CLEAR JOB CODE                           
         BAS   RE,SETJBCD          PASS JOBCODE AND DESCRIPTION                 
         GOTO1 =V(MXPOST),DMCB,(R3) CALL MXPOST FOR POSTING FOR ACC             
         MVI   SEQONE,NO           NOT A REVERSAL RECORD                        
         MVI   DETL,NO             NOT A DETAIL RECORD                          
         GOTO1 =A(PUTSORT),DMCB,(RC),V(REC)                                     
         BAS   RE,PSTELE           ADD ELEM TO POSTING DETAIL REC               
*                                                                               
BILLP70  LA    R4,ACPRTNL2(R4)     ADD ELEM TO POSTING DETAIL RECORD            
         BCT   R2,BILLP30          DECREMENT NUMBER OF POSTINGS LEFT            
*                                                                               
         BAS   RE,ADDTL            ADD POSTING DETAL(S) CREATED                 
         BAS   RE,WRITBIL          MARK BILL RECORD                             
BILLPX   B     XIT                                                              
         DROP  R3,R4                                                            
         EJECT                                                                  
*              PRTBIL - ROUTINE PRINTS ONE BILL                                 
*                                                                               
PRTBIL   NTR1                                                                   
         LA    R2,PRTBLOCK                                                      
         USING MXPRNTD,R2                                                       
         GOTO1 =A(SETPRT),DMCB,(RC)                                             
         MVI   PMODE,PMBILL       PRINT BILL INFO                               
         GOTO1 =V(MXPRNT),DMCB,(R2)                                             
         B     XIT                                                              
         DROP  R2                                                               
         SPACE 2                                                                
*              CHKERRS - CHECK FLAGS SET DURING POSTING                         
*                                                                               
CHKERRS  NTR1                                                                   
         LA    R1,ACPBLK                                                        
         USING ACPOSTD,R1                                                       
         CLI   ACPERR,0           ANY ERROR WITH POSTINGS?                      
         BNE   XITNO              YES - GO TO NEXT BILL                         
         CLI   ACPERRC,0          ANY OTHER ERRORS WITH POSTINGS                
         BNE   XITNO                                                            
         DROP  R1                                                               
         CLI   CONERR,0           ANY ERRORS WITH CONTRA ACCOUNTS?              
         BNE   XITNO              YES - GO TO NEXT BILL                         
         CLI   PSTAOR,C'Y'                                                      
         BNE   XITNO                                                            
         B     XITYES                                                           
         EJECT                                                                  
*              STCONTRA - ROUTINE TO SET CONTRA AND CONTRAN                     
*                                                                               
STCONTRA NTR1                                                                   
         LA    R3,TEMPROW          R3=A(CURRENT POSTING ROW)                    
         USING ACPRTND,R3                                                       
         ZIC   RE,PSTNUM2          RE=(MAX NUM OF ENTRIES IN TABLE)             
         L     R1,=V(CONTBL)       R1=A(CONTRA ACCOUNT TABLE)                   
         USING CONTBLD,R1                                                       
*                                                                               
STCNTR2  CLC   CONTYPE,ACPBTYP    MATCH ON CURRENT ROW                          
         BNE   *+14                                                             
         CLC   CONTYPE2,ACPWHC                                                  
         BE    STCNTR5                                                          
         LA    R1,CONTBLL(R1)      BUMP TO NEXT ENTRY IN TABLE                  
         BCT   RE,STCNTR2          LOOP                                         
         DC    H'0'                MUST BE IN TABLE                             
*                                                                               
STCNTR5  MVC   CONTRA,CONACC       SET CONTRA ACCOUNT                           
         OC    CONTRA+1(14),SPACES                                              
         MVC   CONTRAN,CONNAME     AND CONTRA ACCOUNT NAME                      
         OC    CONTRAN,SPACES                                                   
         B     XIT                                                              
         DROP  R1,R3                                                            
         SPACE 2                                                                
*---------------------------------------------*                                 
* SETWKCD-  SETS WKCD TO WORK CODE FOR SJ ACC *                                 
*---------------------------------------------*                                 
*                                                                               
SETWKCD  NTR1                                                                   
         MVC   WKCD,SPACES                                                      
         LA    RE,TEMPROW         CURRENT ACCOUNT INFO                          
         USING ACPRTND,RE                                                       
         CLC   ACPACC(2),=C'SJ'                                                 
         BNE   SETWKCDX                                                         
         GOTO1 MXPROF,DMCB,MTPFWKSJ,PROFWORK                                    
         OC    PROFWORK,PROFWORK                                                
         BZ    *+10               NO WORK CODE                                  
         MVC   WKCD,PROFWORK                                                    
SETWKCDX B     EXIT                                                             
         DROP  RE                                                               
         EJECT                                                                  
* SETJBCD- SETS JOBCODE AND DESCRIPTION IN MXBLK FOR MXPOST                     
*                                                                               
SETJBCD  NTR1                                                                   
         CLC   JOBCD,SPACES                                                     
         BE    SETJBCDX                                                         
         MVC   WORK,SPACES                                                      
         MVC   WORK(6),JOBCD                                                    
         XC    KEY,KEY                                                          
         LA    R3,KEY                                                           
         USING PJOBREC,R3                                                       
         MVC   PJOBKAGY,PBILKAGY   AGENCY CODE                                  
         MVC   PJOBKMED,PBILKMED   MEDIA CODE                                   
         MVI   PJOBKRCD,X'15'      RECORD TYPE                                  
         MVC   PJOBKCLT,PBILKCLT   CLIENT CODE                                  
         MVC   PJOBKPRD,PBILKPRD   PRODUCT CODE                                 
         MVC   PJOBKJOB,JOBCD      JOB CODE                                     
         DROP  R3                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(25),KEYSAVE                                                  
         BNE   GETJBCD5                                                         
         LA    R1,4095(R6)        GET ACCESS TO PJOBREC                         
         LA    R1,1(R1)                                                         
         USING PPFILED+4096,R1                                                  
         LA    R3,PJOBREC                                                       
         ST    R3,AREC                                                          
         DROP  R1                                                               
         GOTO1 GETJOB                                                           
         USING PJOBREC,R3                                                       
         CLI   PJOBCAP1,C' '                                                    
         BNH   GETJBCD5                                                         
         MVC   WORK+7(25),PJOBCAP1                                              
         LA    RF,WORK+32                                                       
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-8                                                           
         MVC   2(25,RF),PJOBCAP2  FLOAT 2ND LINE                                
GETJBCD5 OC    WORK,SPACES                                                      
*                                                                               
         L     R3,AMXBLK                                                        
         USING MXPOSTD,R3                                                       
         MVC   MXJOBCD,JOBCD                                                    
         MVC   MXJBNARR,SPACES                                                  
         LA    RF,WORK+L'WORK-1                                                 
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-8                                                           
         LA    R0,WORK                                                          
         SR    RF,R0                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   MXJBNARR(0),WORK                                                 
         LA    RF,1(RF)                                                         
         STC   RF,MXJBLEN                                                       
SETJBCDX B     XIT                                                              
         DROP  R3                                                               
         EJECT                                                                  
*-------------------------------------------*                                   
* ADDTL -ADD POST DETAIL RECORD(S) CREATED  *                                   
*-------------------------------------------*                                   
*                                                                               
ADDTL    NTR1                                                                   
         L     R1,=V(PSTREC)      PT R1 TO HEADER                               
         LR    R2,R1                                                            
         LA    R2,4(R2)           PT R2 TO RECORD                               
         USING MPDRECD,R2                                                       
         SR    RE,RE                                                            
         ICM   RE,3,0(R1)         LENGTH OF WORKER RECORD                       
         SH    RE,=H'4'           WORKER RECORD HEADER                          
         STCM  RE,3,MPDRLEN       SET RECORD LENGTH                             
         MVI   SEQONE,NO          NOT A REVERSAL RECORD                         
         MVI   DETL,YES           DETAIL RECORD                                 
         GOTO1 =A(PUTSORT),DMCB,(RC),V(PSTREC)                                  
*                                                                               
         TM    MPDRSTA,X'01'      SECOND POST DETAIL RECORD?                    
         BNO   ADDTLX             NO                                            
         L     R1,=V(PSTREC2)     PT R1 TO HEADER                               
         LR    R2,R1                                                            
         LA    R2,4(R2)           PT R2 TO RECORD                               
         SR    RE,RE                                                            
         ICM   RE,3,0(R1)                                                       
         SH    RE,=H'4'                                                         
         STCM  RE,3,MPDRLEN                                                     
         MVI   SEQONE,NO          NOT A REVERSAL RECORD                         
         MVI   DETL,YES           DETAIL RECORD                                 
         GOTO1 =A(PUTSORT),DMCB,(RC),V(PSTREC2)                                 
ADDTLX   B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*-------------------------------------------------------------*                 
* WRITBIL - MARKS BILL RECORD TRANSFERRED & SETS DATE IN BILL *                 
*           IF MAIN AOR BILL-READS AHEAD & MARKS TRUE AOR ALSO*                 
*-------------------------------------------------------------*                 
WRITBIL  NTR1                                                                   
         MVI   TRUEAOR,NO                                                       
         BAS   RE,GETSME          RE-ESTABLISH READ SEQUENCE                    
         SR    R3,R3                                                            
         IC    R3,NUMABIL          # OF AOR BILLS                               
*                                                                               
WRITB2   TM    MXOPT,MXNOWRIT     IF WRITE=NO -                                 
         BO    WRITB10            DON'T MARK BILL RECORDS                       
         OI    KEY+26,X'01'       MARK DIRECTORY KEY TRANS                      
         GOTO1 DATAMGR,DMCB,DMWRT,PRTDIR,KEY,KEY                                
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
WRITB10  LA    R4,PBILLREC                                                      
         ST    R4,AREC                                                          
WRITB12  GOTO1 DATAMGR,DMCB,GETREC,PRTFILE,KEY+27,AREC,DMWORK                   
         CLI   TRUEAOR,C'Y'       1ST TIME PASS ?                               
         BNE   WRITB20                                                          
         CLC   RUNDT,PBILLDAT     NO-LOOKING FOR TRUE AOR BILL                  
         BE    *+6                                                              
         DC    H'0'               MUST FIND IT                                  
         TM    PBILCMSW,X'20'                                                   
         BNO   WRITB40                                                          
         MVI   TRUEAOR,C'M'       FOUND IT - MARK RECORD & DIRECTORY            
         B     WRITB30                                                          
*                                                                               
WRITB20  TM    PBILCMSW,X'10'     1ST TIME THROUGH - SET FLG IF NECES           
         BNO   WRITB30                                                          
         TM    PBILCMSW,X'08'     AORN - HAS NO TRUE ATTACHED                   
         BO    WRITB30                                                          
         CLI   PBILSEP,C'R'                                                     
         BNE   WRITB30                                                          
         MVI   TRUEAOR,C'Y'       MUST FIND AND MARK TRUE AOR BILL              
WRITB30  TM    MXOPT,MXNOWRIT     IF WRITE=NO -                                 
         BO    WRITB40            DON'T MARK BILL RECORDS                       
         OI    PBILLCTL+1,X'01'   MARK RECORD TRANSFERRED                       
         MVC   PBILPOST,BTODAYP   AND SET TODAYS DATE                           
         GOTO1 DATAMGR,DMCB,PUTREC,PRTFILE,KEY+27,AREC,DMWORK                   
*                                                                               
WRITB40  CLI   TRUEAOR,C'Y'       IF CLIENT AOR BILL - ALSO MARK TRUE           
         BNE   WRITB50                                                          
         CLI   QOPT4,C'M'          DDS ONLY, JUST MARK BILLS                    
         BE    WRITB60                                                          
         BAS   RE,GETNXT          GET NEXT BILL                                 
         B     WRITB70                                                          
WRITB50  CLI   TRUEAOR,C'M'                                                     
         BNE   WRITBX                                                           
         TM    MXOPT,MXNOWRIT                                                   
         BO    WRITBX                                                           
         OI    KEY+26,X'01'       MARK DIRECTORY KEY TRANS                      
         GOTO1 DATAMGR,DMCB,DMWRT,PRTDIR,KEY,KEY                                
         CLI   DMCB+8,0                                                         
         BE    WRITBX                                                           
         DC    H'0'                                                             
WRITB60  BAS   RE,GETNXT                                                        
WRITB70  BRCT  R3,WRITB10                                                       
WRITBX   B     XIT                                                              
         EJECT                                                                  
*---------------------------------------------------*                           
* RDPROF - CALLS ACPOSTER TO READ PROF MAINT RECORDS*                           
*---------------------------------------------------*                           
*                                                                               
RDPROF   NTR1                                                                   
         GOTO1 CLRAREA,DMCB,V(PROFREC),V(PROFRECX)                              
         GOTO1 =A(SETBLK),DMCB,(RC) SET UP BASIC ACPOSTER CALL                  
         LA    R2,ACPBLK          FOR CALL                                      
         USING ACPOSTD,R2                                                       
         MVI   ACPTYPE,1          PROF MAINT RECORD                             
         MVC   ACPPOST,=V(PROFREC) A(PROFILES RETURNED)                         
*                                                                               
         MVI   ACPTYPE2,MPRKPPB   REG/FIN/PROD                                  
         CLC   =C'RET',TYPEBIL    IF RETAIL BILL                                
         BNE   *+8                                                              
         MVI   ACPTYPE2,MPRKRTL   READ FOR REG + RET BILLING                    
         CLC   =C'REGM',MTYPEBIL                                                
         BNE   *+8                                                              
         MVI   ACPTYPE2,MPRKMTRA  READ FOR REG + MPOST BILLING                  
         GOTO1 =A(POSTCALL),DMCB,(RC)                                           
*                                                                               
         MVC   SPJBCD,SPACES                                                    
         GOTO1 MXPROF,DMCB,MTPFJBCD,PROFWORK                                    
         OC    PROFWORK,PROFWORK  ANY JOB CODE OVERRIDE FOR SEP CD?             
         BZ    RDPROFX                                                          
         MVC   SPJBCD,PROFWORK                                                  
RDPROFX  B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*----------------------------------------------------------------*              
* PSTHDR  - SETS UP START OF POST DETAIL REC WHICH WILL BE ADDED *              
*           IF BILL RECORD IS TRANSFERRED                        *              
*----------------------------------------------------------------*              
*                                                                               
PSTHDR   NTR1                                                                   
         GOTO1 CLRAREA,DMCB,V(PSTREC),V(PSTRECX)                                
         L     R1,=V(PSTREC)      PT R1 TO HEADER                               
         LR    R2,R1                                                            
         LA    R2,4(R2)           PT R2 TO RECORD                               
         USING MPDRECD,R2                                                       
         LA    RE,ACCORFST                                                      
         LA    RE,4(RE)           INCLUDE WORKER FILE RECORD HEADER             
         LA    RE,1(RE)           + 1 X'00' -MARK END OF RECORD                 
         STCM  RE,3,0(R1)         STARTING LENGTH OF WORKER RECORD              
         MVI   MPDKTYP,MPDKTYPQ   X'2F'                                         
         MVI   MPDKSUB,MPDKSUBQ   X'00'                                         
         MVC   MPDKCPY,ACCTCD     NATIVE COMPANY CODE                           
         MVC   MPDKALPH,SALPH     AGY ALPH FOR MEDIA SPLIT FILES                
         MVI   MPDKSYS,C'P'       SYSTEM                                        
         MVC   MPDKMED,PBILKMED   MEDIA                                         
         MVC   MPDKCLI,PBILKCLT   CLIENT                                        
         MVC   MPDKPRD,PBILKPRD   PRODUCT                                       
         SR    RE,RE                                                            
         ICM   RE,3,PBILKEST                                                    
         CVD   RE,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  MPDKEST,DUB        ESTIMATE #                                    
         MVC   FULL(2),PBILKBMN                                                 
         MVI   FULL+2,1           FUDGE DAY                                     
         GOTO1 DATCON,DMCB,(3,FULL),(0,WORK)                                    
         MVC   MPDKPER,WORK       BILL MONTH (YYMM)                             
         MVC   FULL(2),PBILKMOS                                                 
         MVI   FULL+2,X'01'       FUDGE DAY                                     
         GOTO1 DATCON,DMCB,(3,FULL),(0,WORK)                                    
         MVC   MPDKMOS,WORK       MONTH OF SERVICE (YYMM)                       
         EDIT  PBILKBNO,(5,MPDKINV),FILL=0                                      
         LA    R1,ACCORFST        DISP TO FIRST ELEM                            
         AR    R2,R1                                                            
         STCM  R2,15,NXTPSTE      A(ADD NEXT ELEMENT)                           
         MVI   DTLREC,0                                                         
         B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*------------------------------------------------*                              
* PSTELE  - FOR EACH ACCOUNT RETURNED FROM       *                              
*           ACPOSTER (& RECORD ADDED TO WRKFILE) *                              
*           --- AN ELEMENT NEEDS TO ADDED TO     *                              
*           THE POST DETAIL RECORD               *                              
* NOTE : IF POST DETAIL RECORD BECOMES TOO LARGE *                              
*        ROUTINE WILL SPLIT TO A NEW DETAIL REC  *                              
* CAUTION : MAX OF TWO DTL RECORDS ALLOWED       *                              
*------------------------------------------------*                              
*                                                                               
PSTELE   NTR1                                                                   
         LA    RE,MBTPDLNQ        LENGTH OF ELEMENT TO ADD                      
         SR    R1,R1                                                            
         CLI   DTLREC,0           ON 1ST RECORD?                                
         BE    PSTELE5            YES- GO CHECK LENGTH                          
         L     R2,=V(PSTREC2)                                                   
         ICM   R1,3,0(R2)         GET LENGTH OF RECORD #2                       
         AR    R1,RE              ADD NEW ELE TO LENGTH                         
         STCM  R1,3,0(R2)         AND GO ADD IT TO RECORD                       
         B     PSTELE10                                                         
*                                                                               
PSTELE5  L     R2,=V(PSTREC)      YES                                           
         ICM   R1,3,0(R2)         YES                                           
         AR    R1,RE              ADD ELEMENT TO BE ADDED                       
         C     R1,=F'1000'        RECORD OVER 1000 BYTES?                       
         BH    PSTELE8            YES - CREATE 2ND RECORD                       
         STCM  R1,3,0(R2)         NO - UPDATE LENGTH AND GO ADD IT              
         B     PSTELE10                                                         
*                                                                               
PSTELE8  BAS   RE,DOPDTL2         NEED TO CREATE A 2ND POST DTL REC             
*                                                                               
PSTELE10 LA    R1,TEMPROW         CURRENT ACCOUNT INFO                          
         USING ACPRTND,R1                                                       
         XC    ELEM,ELEM                                                        
         LA    R2,ELEM                                                          
         USING MBTELD,R2                                                        
         MVI   MBTEL,MBTELQ       X'2E'                                         
         MVI   MBTLLN,MBTPDLNQ    LENGTH                                        
         MVC   MBTTYP,ACPBTYP     POSTING TYPE                                  
         MVC   MBTCHNG,ACPCHDT    LAST CHANGE DATE                              
         MVC   MBTULA(14),ACPACC  UNIT/LEDGER/ACCOUNT                           
         OC    MBTULA(14),SPACES                                                
         MVC   MBTOFFC,SVACOFF    ACCOUNTING OFFICE                             
         CLI   ACPBTYP,MBTTINTI    IF INTERNAL POSTING                          
         BL    PSTELE14                                                         
         CLI   ACPBTYP,MBTTINTR                                                 
         BH    PSTELE14                                                         
         B     PSTELE15                                                         
*                                                                               
PSTELE14 CLI   ACPBTYP,MBTTOINC   IF OTHER INCOME POSTING                       
         BE    PSTELE15                                                         
         CLI   ACPBTYP,MBTTOBIL   IF OTHER BILLING POSTING                      
         BE    PSTELE15                                                         
         CLI   ACPBTYP,MBTTOREV   IF OTHER REVENUE POSTING                      
         BE    PSTELE15                                                         
         CLI   ACPBTYP,MBTTIRCV   INTERCOMPANY RECEIVABLE                       
         BNE   PSTELE20                                                         
PSTELE15 CLC   SVACOF2,SPACES      AND DIFFERENT ACC OFF SPECIFIED              
         BNH   PSTELE20                                                         
         MVC   MBTOFFC,SVACOF2     USE IT                                       
PSTELE20 CLC   WKCD,SPACES                                                      
         BE    *+10                                                             
         MVC   MBTOFFC,WKCD       OR WORK CODE                                  
         TM    ACPSTAT,ACPDEB     DEBIT?                                        
         BNO   *+8                                                              
         OI    MBTSTAT,MBTSDR     STATUS BYTE (DEBIT OR CREDIT)                 
         CLI   ACPBIL2,2                                                        
         BNE   *+8                                                              
         OI    MBTSTAT,MBTSAOR    INDICATE TRUE AOR BILL                        
         MVC   MBTCNTRA,CONTRA+1  CONTRA ACCOUNT                                
         MVC   MBTPOST,ACPAMT2    POSTING AMOUNT                                
         MVC   MBTMEMO,ACPMEMO2   MEMO AMOUNT                                   
         DROP  R2,R1                                                            
*                                                                               
         ICM   R1,15,NXTPSTE      POSITION TO INSERT NEXT ELEMENT               
         LA    RE,MBTPDLNQ        ELEMENT LENGTH                                
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R1),ELEM       MOVE ELEMENT INTO RECORD AREA                 
         LA    RE,1(RE)           RESTORE ELEMENT LENGTH                        
         AR    R1,RE              PT TO NXT AVAILABLE SPOT IN                   
         STCM  R1,15,NXTPSTE      RECORD TO ADD NEW ELEMENT                     
         MVI   0(R1),0                                                          
         B     XIT                                                              
         EJECT                                                                  
*-------------------------------------------*                                   
* DOPDTL2-  CREATES 2ND DETAIL RECORD       *                                   
*-------------------------------------------*                                   
*                                                                               
DOPDTL2  NTR1                                                                   
         L     R3,=V(PSTREC2)     SECOND POST DETAIL RECORD                     
         L     R2,=V(PSTREC)      FIRST POST DETAIL RECORD                      
         LA    R1,ACCORFST        KEY PART OF RECORD                            
         LA    R1,4(R1)            + HEADER                                     
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),0(R2)                                                    
         LA    R1,1(R1)           RESTORE LENGTH MOVED                          
         AR    R3,R1                                                            
         STCM  R3,15,NXTPSTE      SET POSITION TO ADD 1ST ELEMENT               
*                                                                               
         L     R3,=V(PSTREC2)     SECOND POST DETAIL RECORD                     
         LA    R1,ACCORFST        KEY PART OF RECORD                            
         LA    R1,4(R1)           ADD 4 FOR WORKER FILE HEADER                  
         LA    R1,1(R1)           + 1 X'00' TO MARK END OF RECORD               
         LA    RE,MBTPDLNQ        + FIRST ELEMENT                               
         AR    R1,RE                                                            
         STCM  R1,3,0(R3)         = LENGTH OF WORKER REC SO FAR                 
*                                                                               
         MVI   DTLREC,X'01'       INDICATE A 2ND DTL RECORD                     
         L     R3,=V(PSTREC2)     SECOND POST DETAIL RECORD                     
         LA    R3,4(R3)                                                         
         USING MPDRECD,R3                                                       
         MVI   MPDKSEQ,X'01'      GIVE 2ND REC A SUB LINE                       
         DROP  R3                                                               
*                                                                               
         L     R3,=V(PSTREC)                                                    
         LA    R3,4(R3)                                                         
         USING MPDRECD,R3                                                       
         OI    MPDRSTA,X'01'      INDICATE SECOND RECORD TO FOLLOW              
         B     XIT                                                              
         DROP  R3                                                               
         EJECT                                                                  
*-------------------------------------------------*                             
* OFFICE LAST  - CALL MXPRNT TO GET MED OFF TOTS  *                             
*-------------------------------------------------*                             
*                                                                               
OFCL     CLI   ANYBILL,C'Y'       BILL PROCESSED FOR THIS OFFICE?               
         BNE   OFCLX                                                            
         LA    R2,PRTBLOCK                                                      
         USING MXPRNTD,R2                                                       
         GOTO1 =A(SETPRT),DMCB,(RC)                                             
         MVI   PMODE,PMTOTAL      PRINT OUT TOTAL LINE                          
         MVI   PTOTTYPE,PTOTMOF   MEDIA OFFICE TOTAL                            
         GOTO1 =V(MXPRNT),DMCB,(R2)                                             
OFCLX    B     EXIT                                                             
         DROP  R2                                                               
         SPACE                                                                  
*-------------------------------------------------*                             
* CLIENT LAST  - CALL MXPRNT TO GET CLIENT TOTALS *                             
*-------------------------------------------------*                             
*                                                                               
CLTL     CLI   ANYBILL,C'Y'       BILL PROCESSED FOR THIS CLIENT?               
         BNE   CLTLX                                                            
         LA    R2,PRTBLOCK                                                      
         USING MXPRNTD,R2                                                       
         GOTO1 =A(SETPRT),DMCB,(RC)                                             
         MVI   PMODE,PMTOTAL      PRINT OUT TOTAL LINE                          
         MVI   PTOTTYPE,PTOTCLT   CLIENT TOTAL                                  
         GOTO1 =V(MXPRNT),DMCB,(R2)                                             
CLTLX    B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
*----------------------------------------------*                                
* PRODUCT LAST - CALL MXPRNT FOR PRODUCT TOTAL *                                
*----------------------------------------------*                                
*                                                                               
PRDL     CLI   ANYBILL,C'Y'       BILL PROCESSED FOR THIS CLIENT?               
         BNE   PRDLX                                                            
         LA    R2,PRTBLOCK                                                      
         USING MXPRNTD,R2                                                       
         GOTO1 =A(SETPRT),DMCB,(RC)                                             
         MVI   PMODE,PMTOTAL      PRINT OUT TOTAL LINE                          
         MVI   PTOTTYPE,PTOTPRD   PRODUCT TOTAL                                 
         GOTO1 =V(MXPRNT),DMCB,(R2)                                             
PRDLX    B     EXIT                                                             
         DROP  R2                                                               
         SPACE                                                                  
*----------------------------------------------------------------*              
* REQ LAST -  PRINT MEDIA TOTALS,BILLS IN ERROR AND THE SUMMARIES*              
*----------------------------------------------------------------*              
*                                                                               
REQL     LA    R2,PRTBLOCK                                                      
         USING MXPRNTD,R2                                                       
         GOTO1 =A(SETPRT),DMCB,(RC)                                             
         MVI   PMODE,PMTOTAL                                                    
         MVI   PTOTTYPE,PTOTMED                                                 
         GOTO1 =V(MXPRNT),DMCB,(R2)                                             
*                                 PRINT OUT SUMMARIES                           
         GOTO1 =A(SETPRT),DMCB,(RC)                                             
         MVI   PMODE,PMRECAP                                                    
         GOTO1 =V(MXPRNT),DMCB,(R2)                                             
*                                 PRINT OUT BILLS IN ERROR                      
         GOTO1 =A(SETPRT),DMCB,(RC)                                             
         MVI   PMODE,PMBILERR                                                   
         GOTO1 =V(MXPRNT),DMCB,(R2)                                             
         B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
*-------------------------------------------*                                   
* RUN LAST -  WRITE TRAILER RECORD OUT AND  *                                   
*             CLOSE UP WORKER FILE & REPORT *                                   
*-------------------------------------------*                                   
*                                                                               
RUNL     ZAP   RECCNT,=P'0'       RESET COUNTERS                                
         ZAP   TOTCASH,=P'0'                                                    
         ZAP   DEBS,=P'0'                                                       
         ZAP   CREDS,=P'0'                                                      
         XC    ACCOID,ACCOID                                                    
         XC    PRVINV,PRVINV                                                    
         L     RE,=A(WRKRBUFF)                                                  
         ST    RE,WFILE                                                         
         GOTO1 =V(SORTER),DMCB,=C'GET'                                          
         ICM   R3,15,4(R1)                                                      
         BZ    RUNL60             NO RECORDS IN SORT                            
*                                                                               
         USING SRTKEYD,R3                                                       
RUNL10   TM    UPSI,UPSISOUT                                                    
         BZ    RUNL12                                                           
         LA    R0,SRTKEYLQ+L'ACTKEY                                             
         GOTOR =V(PRNTBL),DMCB,=C'GET',(R3),C'DUMP',(R0),=C'1D'                 
*                                                                               
RUNL12   CLI   FORCECWK,YES        CLOSE WORKER FILE                            
         BNE   RUNL26              NO, THINGS AS USUAL                          
         CLC   ACCOID,SRTOID      MATCH ON ORIGINAL ID                          
         BNE   RUNL28              THIS WAS GOING TO CLOSE FILE ANYWAY          
         CLC   DEBS,CREDS          DEBITS EQUAL CREDITS ?                       
         BNE   RUNL26              CAN'T BREAK                                  
         CLC   SRTINV,PRVINV                                                    
         BE    RUNL26              PUT LIKE INVOICES IN SAME WF                 
         B     RUNL28              OKAY TO CLOSE NOW                            
*                                                                               
RUNL26   CLC   ACCOID,SRTOID      MATCH ON ORIGIN ID, WORKER FILE ID            
         BE    RUNL30                                                           
                                                                                
RUNL28   OC    ACCOID,ACCOID      FIRST TIME?                                   
         BZ    *+8                YES                                           
         BAS   RE,CLSWRK          NO -CLOSE PREVIOUS WORKER FILE                
         MVC   ACCOID,SRTOID      SET NUMERIC ORIGIN ID                         
         BAS   RE,SETID           SET WORKER FILE ID                            
                                                                                
RUNL30   BAS   RE,ADDWRK          ADD WORKER FILE RECORD                        
         MVC   PRVINV,SRTINV                                                    
         GOTO1 =V(SORTER),DMCB,=C'GET'                                          
         ICM   R3,15,4(R1)                                                      
         BNZ   RUNL10                                                           
         BAS   RE,CLSWRK          CLOSE WORKER FILE                             
*                                                                               
RUNL60   BAS   RE,CONTROL         INTERAL CONTROLS                              
         GOTO1 =V(SORTER),DMCB,=C'END'                                          
RUNLX    B     EXIT                                                             
         EJECT                                                                  
*---------------------------------------*                                       
* SETID - SETS ID FOR NEW WORKER FILE   *                                       
*---------------------------------------*                                       
*                                                                               
SETID    NTR1                                                                   
         MVI   FORCECWK,NO         DON'T FORCE CLOSE OF WORKER FILE             
         XC    WID,WID                                                          
         LA    R2,WID                                                           
         USING UKRECD,R2          SET ID(INDEX) OF WORKER FILE                  
         MVC   UKUSRID,ACCOID                                                   
         MVI   UKFLAG,X'01'       INDICATE CAN HAVE MULTIPLE FILES              
         MVC   UKSYSPRG,=C'PMX'   FILE NAME                                     
         TM    MXOPT,MXMBSOB      SAME NIGHT POSTINGS?                          
         BZ    *+8                                                              
         MVI   UKSYSPRG+2,C'L'    THEN CHANGE SMX/NMX TO SML/NML                
         CLC   =C'MY',RCPROG      IF REVERSAL -- NAME IS PMY                    
         BNE   *+10                                                             
         MVC   UKSYSPRG+1(2),=C'MY'                                             
         L     R1,LOGOC                                                         
         USING LOGOD,R1                                                         
         CLI   LOGOJOB+7,C' '     LAST CHAR OF JOB                              
         BNH   *+10                                                             
         MVC   UKSUBPRG,LOGOJOB+7                                               
         PACK  FULL(2),TODAYD+4(3)                                              
         OC    RERUNDT,RERUNDT                                                  
         BZ    *+10                                                             
         PACK  FULL(2),RERUNDT+4(3)    SPECIAL BILLLING RE-RUN                  
         MVC   UKDAY,FULL                                                       
         MVI   UKCLASS,C'P'                                                     
*        OI    UKSTAT,X'40'       PUT WORKER FILE ON HOLD FOR NOW               
         GOTO1 WORKER,DMCB,=C'OPEN',WFILE,WID                                   
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'00'                                                            
                                                                                
         L     RE,=V(WKFKEYS)                                                   
         LLC   RF,#WRKFS           # OF WORKER FILES                            
         AHI   RF,1                                                             
         STC   RF,#WRKFS           SAVE # OF FILES SO FAR                       
         CLC   #WRKFS,MAX#WKF      MAX FILES STORED                             
         BNH   *+6                                                              
         DC    H'00'               WE MAXED OUT                                 
                                                                                
         BCTR  RF,0                                                             
         MHI   RF,L'UKINDEX                                                     
         AR    RE,RF                                                            
         MVC   0(L'UKINDEX,RE),WID SAVE OFF INDEX                               
                                                                                
SETIDX   B     EXIT                                                             
         DROP  R2,R1                                                            
         EJECT                                                                  
*------------------------------------------------------------*                  
* ADDWRK - ADD RECORD TO WORKER FILE & UPDATE CURRENT VALUES *                  
*------------------------------------------------------------*                  
*                                                                               
ADDWRK   NTR1                                                                   
         LR    R2,R3              PT TO SORTER RECORD                           
         LA    R2,SRTKEYLQ(R2)    PT TO START OF WORKER FILE RECORD             
         BAS   RE,CHKELE          *** TRAP E5 ELEMENT MUST EXIST                
         BAS   RE,UPDATE          UPDATE WORKER FILE COUNTERS                   
         TM    MXOPT,MXNOPOST     POST =NO OPTION?                              
         BO    ADDWRKX                                                          
         GOTO1 WORKER,DMCB,=C'ADD',WFILE,WID,(R2)                               
         TM    DMCB+8,X'C0'                                                     
         BZ    *+6                                                              
         DC    H'0'                                                             
         USING WKRECD,RE                                                        
         L     RE,WFILE                                                         
         CLI   WKSEQ,MAXCI          MAX CI WE WANT BEFORE ITS TOO LATE          
         BL    *+8                                                              
         MVI   FORCECWK,YES        FORCE CLOSE WHEN NEXT POSSIBLE               
         DROP  RE                                                               
ADDWRKX  B     EXIT                                                             
         EJECT                                                                  
*------------------------------------------------------------*                  
* CHKELE - TRAP TO DIE IF NO E5 ELEMENT IN POSTING RECORDS   *                  
*------------------------------------------------------------*                  
*                                                                               
CHKELE   NTR1                                                                   
         SR    RE,RE                                                            
         LR    R1,R2              PT TO START OF RECORD                         
         LA    R1,4(R1)           PT PAST HEADER                                
         USING TRNELD,R1                                                        
         CLI   0(R1),X'2F'        IF MY RECORD SKIP                             
         BE    EXIT                                                             
CHKELE10 CLI   0(R1),GDAELQ                                                     
         BE    EXIT                                                             
         ICM   RE,1,1(R1)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R1,RE                                                            
         CLI   0(R1),0                                                          
         BNE   CHKELE10                                                         
         DC    H'0'               E5 NOT IN RECORD --- DIE                      
         SPACE                                                                  
*------------------------------------------------------------*                  
* UPDATE - UPDATES RECCNT & TOTCASH FOR CURRENT WORKER FILE  *                  
*------------------------------------------------------------*                  
*                                                                               
UPDATE   NTR1                                                                   
         SR    RE,RE                                                            
         LR    R1,R2              PT TO START OF RECORD                         
         LA    R1,4(R1)           PT PAST HEADER                                
         USING TRNELD,R1                                                        
         CLI   0(R1),X'2F'        IF MY RECORD SKIP                             
         BE    UPDATEX                                                          
UPDATE5  CLI   0(R1),TRNELQ       X'44' ELEMENT                                 
         BE    UPDATE10                                                         
         ICM   RE,1,1(R1)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R1,RE                                                            
         B     UPDATE5                                                          
*                                                                               
UPDATE10 LA    RE,CREDS                                                         
         TM    TRNSTAT,TRNSDR     DEBIT?                                        
         BZ    UPDATE15                                                         
         LA    RE,DEBS                                                          
         AP    TOTCASH,TRNAMNT                                                  
UPDATE15 AP    0(L'DEBS,RE),TRNAMNT                                             
         AP    RECCNT,=P'1'                                                     
*                                                                               
UPDATEX  B     EXIT                                                             
         EJECT                                                                  
*-------------------------------------------------------------*                 
* CLSWRK - ADD TRAILER RECORD & CLOSE CURRENT WORKER FILE     *                 
*-------------------------------------------------------------*                 
*                                                                               
CLSWRK   NTR1                                                                   
         GOTO1 CLRAREA,DMCB,V(REC),V(RECX)                                      
         L     R2,=V(REC)                                                       
         MVC   2(2,R2),=H'2'                                                    
         XC    WORK,WORK                                                        
         LA    R1,WORK            BUILD TRAILER RECORD                          
         USING PSSUBFD,R1                                                       
         MVI   PSSBEL,PSSBELQ     X'52'                                         
         MVI   PSSBLEN,PSSUBFL    LENGTH                                        
         MVC   PSSBDESC,=CL15'PRINT BILLS'                                      
         ZAP   PSSBRECS,RECCNT    NUMBER OF RECORDS                             
         ZAP   PSSBCASH,TOTCASH   TOTAL CASH                                    
         DROP  R1                                                               
         GOTO1 RECUP,DMCB,(X'FF',2(R2)),WORK,4(R2)                              
         ICM   RE,3,2(R2)           MOVE LENGTH                                 
         LA    RE,2(RE)                                                         
         STCM  RE,3,0(R2)                                                       
         XC    2(2,R2),2(R2)                                                    
         TM    MXOPT,MXNOPOST     POST =NO OPTION?                              
         BO    CLSWRK10                                                         
         XC    DMCB(24),DMCB                                                    
         GOTO1 WORKER,DMCB,=C'ADD',WFILE,WID,V(REC)                             
         GOTO1 (RF),(R1),=C'CLOSE'                                              
*                                                                               
CLSWRK10 BAS   RE,SETAGY          SET AGENCY DATA FOR INTERNAL CONTROL          
         ZAP   RECCNT,=P'0'       RESET COUNTERS                                
         ZAP   TOTCASH,=P'0'                                                    
         ZAP   DEBS,=P'0'                                                       
         ZAP   CREDS,=P'0'                                                      
CLSWRKX  B     EXIT                                                             
         EJECT                                                                  
*---------------------------------------*                                       
* SETAGY  - SET AGENCY TABLE            *                                       
*---------------------------------------*                                       
*                                                                               
SETAGY   NTR1                                                                   
         XC    AKEY,AKEY                                                        
         LA    RE,AKEY                                                          
         USING CTIREC,RE                                                        
         MVI   CTIKTYP,CTIKTYPQ   C'I'                                          
         MVC   CTIKID+8(2),ACCOID                                               
         DROP  RE                                                               
         L     R2,AMYAREA                                                       
         USING CTIREC,R2                                                        
         GOTO1 DATAMGR,DMCB,=C'DMREAD  ',=C'CTFILE ',AKEY,(R2),0                
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'               WHAT SHOULD I DO???                           
         SR    R0,R0                                                            
         LA    R1,CTIDATA                                                       
         USING CTDSCD,R1                                                        
SETAGY5  CLI   CTDSCEL,0                                                        
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   CTDSCEL,CTDSCELQ   X'02'                                         
         BE    SETAGY10                                                         
         ICM   R0,1,CTDSCLEN                                                    
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R1,R0                                                            
         B     SETAGY5                                                          
*                                                                               
         USING AGYTBLD,RE                                                       
SETAGY10 LA    RE,AGYTBL           A(AGENCY TABLE TOTALS)                       
         SR    RF,RF                                                            
         ICM   RF,1,#AGY           # OF AGENCIES IN TABLE                       
         BZ    SETAGY30            ADD NEW ENTRY                                
SETAGY20 CLC   AGYOID,ACCOID                                                    
         BE    SETAGY25                                                         
         LA    RE,AGYTBLL(RE)                                                   
         BRCT  RF,SETAGY20         NEXT ENTRY                                   
         B     SETAGY30            ADD NEW ENTRY                                
                                                                                
SETAGY25 AP    AGYTAMT,TOTCASH     ADD IF SPLIT THE WORKER FILE                 
         B     SETAGYX                                                          
                                                                                
SETAGY30 LLC   RF,#AGY             ADD NEW ENTRY                                
         AHI   RF,1                                                             
         STC   RF,#AGY             SAVE NEW #                                   
         CHI   RF,MAXAGY                                                        
         BNH   *+6                                                              
         DC    H'00'               NEED TO INCREASE TABLE SIZE                  
                                                                                
         MVC   AGYOID,ACCOID                                                    
         MVC   AGYTNM,CTDSC                                                     
         ZAP   AGYTAMT,TOTCASH                                                  
SETAGYX  B     EXIT                                                             
         DROP  R2,R1,RE                                                         
         EJECT                                                                  
*----------------------------------------------*                                
* CONTROL - PRINTS PAGE WITH INTERNAL CONTROLS *                                
*----------------------------------------------*                                
*                                                                               
CONTROL  NTR1                                                                   
         L     R2,LOGOC                                                         
         USING LOGOD,R2                                                         
         MVI   LOGOTYPE,C'E'      PRINT END REPORT LOGOS                        
         GOTO1 LOGO,DMCB,(R2)                                                   
         DROP  R2                                                               
         L     R5,PPWORK2C                                                      
         L     R5,VMASTC-PPWORK2D(R5)                                           
         USING MASTD,R5                                                         
         CLI   MCTSTRUN,X'FF'     IF RUN=TEST                                   
         BE    CONTROLX           DON'T BOTHER WITH CONTROL PAGES               
         L     R2,MCVREMOT                                                      
         USING REMOTED,R2                                                       
         XC    MCARC,MCARC                                                      
         GOTO1 PRINT,DMCB,=C'CLOSE'                                             
*                                                                               
         USING AGYTBLD,R3                                                       
         LA    R3,AGYTBL          PT TO TABLE OF WORKER FILES                   
         XC    REMOTKEY,REMOTKEY                                                
         MVI   REMOTJID,C'P'      PRINT CONTROL PAGES ON QUEUE                  
         MVC   REMOTJID+1(2),RCPROG                                             
         MVI   REMOTCLS,C'T'                                                    
         MVI   REMOTCPY,X'01'                                                   
         MVC   REMOTDST,=X'0452'  FOR 1106, ID=ZIP                              
         MVC   REMOTKEY(8),MCJOB                                                
         MVC   REMOTFNO,REMOTKEY                                                
         MVI   REMOTPRT,0                                                       
         XC    REMOTRTY,REMOTRTY                                                
         XC    REMOTARC,REMOTARC                                                
         NI    REMOTTYP,X'FF'-X'60'                                             
         NI    REMOTTY1,X'FF'-(REMOTAEQ+REMOTARQ+REMOTADQ)                      
         SR    R4,R4                                                            
         ICM   R4,1,#AGY                                                        
         BZ    CONTROLX                                                         
         DROP  R2                                                               
*                                                                               
CONTROL5 BRAS  RE,ACTIVATE                                                      
         L     R2,L1              PRINT TOTAL DOLLAR FIGURE                     
         XC    HEADHOOK,HEADHOOK                                                
         MVI   FORCEHED,C'Y'                                                    
         MVC   PAGE,=H'1'                                                       
         MVI   RCSUBPRG,5                                                       
         MVC   1(15,R2),=C'TOTAL POSTING ='                                     
         LA    R2,18(R2)                                                        
         EDIT  (P6,AGYTAMT),(13,0(R2)),2,MINUS=YES,ALIGN=LEFT                   
         AR    R2,R0                                                            
         MVC   0(3,R2),=C'FOR'                                                  
         MVC   4(6,R2),AGYTNM                                                   
         GOTO1 REPORT                                                           
         LA    R3,AGYTBLL(R3)                                                   
         BCT   R4,CONTROL5                                                      
*                                                                               
CONTROLX L     R1,LOGO            NOP LOGO SO PPG WON'T PRINT END TOO           
         MVC   0(2,R1),=X'07FE'                                                 
         B     EXIT                                                             
         DROP  R3,R5                                                            
         EJECT                                                                  
*                                                                               
         USING AGYTBLD,R3                                                       
         USING UKRECD,R4                                                        
ACTIVATE NTR1                                                                   
         L     R4,=V(WKFKEYS)                                                   
         LLC   R5,#WRKFS           # OF WORKER FILES                            
ACTV020  CLC   AGYOID,UKUSRID                                                   
         BNE   ACTV100                                                          
         MVC   WID,0(R4)           SET KEY                                      
         GOTOR WORKER,DMCB,=C'RESTORE',WFILE,WID                                
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'00'               COULD NOT ACTIVATE. MANUALLY DO?             
                                                                                
ACTV100  AHI   R4,L'UKINDEX                                                     
         BCT   R5,ACTV020                                                       
         J     EXIT                                                             
         DROP  R3                                                               
         EJECT                                                                  
*=============*                                                                 
* LITERAL POOL*                                                                 
*=============*                                                                 
*                                                                               
         LTORG                                                                  
         SPACE                                                                  
SORTCARD DC    CL80'SORT FIELDS=(000,000,A),FORMAT=BI,WORK=1'                   
RECCARD  DC    CL80'RECORD TYPE=V,LENGTH=(2024,,,600,600)'                      
         SPACE                                                                  
*                                  PROVINCE TABLE                               
       ++INCLUDE ACTRAPRV                                                       
*                                                                               
         DS    0D                                                               
         DC    C'**WBUF**'                                                      
WRKRBUFF DS    4096X              FOR WORKER                                    
         SPACE                                                                  
         DS    0D                                                               
         DC    C'**ACCA**'                                                      
ACCAREA  DS    6000X              FOR ACPOSTER                                  
ACCAREAX EQU   *                                                                
         SPACE                                                                  
         DROP  RB,R9,R8           DROP PREVIOUS BASE REGS                       
         SPACE                                                                  
         EJECT                                                                  
*---------------------------------------------------------*                     
* RDCLT - READS ACCOUNT CLIENT HEADER - SET RECORD VALUES *                     
*---------------------------------------------------------*                     
*                                                                               
         DS    0D                                                               
RDCLT    NMOD1 0,**RCLT**                                                       
         L     RC,0(R1)                                                         
         USING PPMXWRKD,RC        LOCAL WORKING STORAGE                         
*                                                                               
         MVI   SVCURR,0           INIT CURRENCY                                 
         XC    ACLIOFF,ACLIOFF                                                  
         MVC   SJCLTNM,SPACES                                                   
         LA    R2,AKEY                                                          
         USING CPYRECD,R2                                                       
         MVC   AKEY,SPACES                                                      
         MVC   AKEY(1),ACCTCD2    OTHER COMPANY CODE (IF APPL)                  
         MVC   AKEY+1(2),SVPROD                                                 
         LA    RF,PCLTREC                                                       
         MVC   AKEY+3(3),PCLTKCLT-PCLTREC(RF)                                   
         L     R2,AMYAREA                                                       
         ST    R2,AREC                                                          
         MVI   RDSE,2             READ FROM OTHER ACCFILE (IF APPL)             
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMREAD'                               
         BNE   RDCLTX                                                           
*                                                                               
         GOTO1 GTNAME,DMCB,AREC,SJCLTNM  GET ACCOUNT NAME                       
*                                                                               
         LA    R1,ACCORFST                                                      
         AR    R2,R1                                                            
         USING PPRELD,R2                                                        
         SR    R0,R0                                                            
RDCLT50  CLI   0(R2),0            END OF RECORD                                 
         BE    RDCLTX                                                           
         CLI   0(R2),PPRELQ       X'24'                                         
         BE    RDCLT60                                                          
         CLI   0(R2),RSTELQ       X'30'                                         
         BE    RDCLT70                                                          
RDCLT55  ICM   R0,1,1(R2)         LENGTH OF ELEMENT                             
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R2,R0              BUMP TO NEXT ELEMENT                          
         B     RDCLT50                                                          
*                                                                               
RDCLT60  CLC   PPRGAOFF,SPACES     ANY OVERRIDE ACCOUNTING OFF                  
         BNH   RDCLT55                                                          
         MVC   ACLIOFF,PPRGAOFF                                                 
         B     RDCLT55                                                          
         DROP  R2                                                               
*                                                                               
         USING RSTELD,R2                                                        
RDCLT70  CLI   RSTFILT2,X'40'      ANY ACCOUNT FILTER 2                         
         BNH   RDCLT55                                                          
         MVC   SVCURR,RSTFILT2     U/E/G/Y/P                                    
         B     RDCLT55                                                          
*                                                                               
RDCLTX   XIT1                                                                   
         DROP  R2                                                               
         EJECT                                                                  
*=============*                                                                 
* LITERAL POOL*                                                                 
*=============*                                                                 
*                                                                               
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*----------------------------------------------------------*                    
* RDPRD - READS ACCOUNT PRODUCT HEADER - SET RECORD VALUES *                    
*----------------------------------------------------------*                    
*                                                                               
         DS    0D                                                               
RDPRD    NMOD1 0,**RPRD**                                                       
         L     RC,0(R1)                                                         
         USING PPMXWRKD,RC        LOCAL WORKING STORAGE                         
*                                                                               
         NI    BIT,X'FF'-PRDOFFC                                                
         MVC   SJPRDNM,SPACES                                                   
         XC    INCRIDE,INCRIDE    INCOME OVERRIDE CONTRA ACCOUNT                
         MVC   SVACOFF,ACLIOFF    DEFAULT ACCOUNTING OFFICE TO CLIENT           
         LA    R2,AKEY                                                          
         USING CPYRECD,R2                                                       
         MVC   AKEY,SPACES                                                      
         MVC   AKEY(1),ACCTCD2    OHTER COMPANY CODE (IF APPL)                  
         MVC   AKEY+1(2),SVPROD                                                 
         LA    RF,PCLTREC                                                       
         MVC   AKEY+3(3),PCLTKCLT-PCLTREC(RF)                                   
         LA    RF,PPRDREC                                                       
         MVC   AKEY+6(3),PPRDKPRD-PPRDREC(RF)                                   
         L     R2,AMYAREA                                                       
         ST    R2,AREC                                                          
         MVI   RDSE,2             READ FROM OTHER ACC FILE                      
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMREAD'                               
         BNE   RDPRD70                                                          
         GOTO1 GTNAME,DMCB,AREC,SJPRDNM  GET ACCOUNT NAME                       
*                                                                               
         LR    RE,R2                                                            
         LA    R1,ACCORFST                                                      
         AR    RE,R1                                                            
         USING SANELD,RE                                                        
         SR    R0,R0                                                            
RDPRD20  CLI   0(RE),0            END OF RECORD                                 
         BE    RDPRD40                                                          
         CLI   0(RE),SANELQ       X'3D'- SALES ANALSYS ELEMENT                  
         BE    RDPRD30                                                          
         ICM   R0,1,1(RE)         LENGTH OF ELEMENT                             
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    RE,R0              BUMP TO NEXT ELEMENT                          
         B     RDPRD20                                                          
*                                                                               
RDPRD30  MVC   INCRIDE,SANCODE    INCOME CONTRA OVERRIDE ACC & NAME             
*                                                                               
RDPRD40  LR    RE,R2                                                            
         LA    R1,ACCORFST                                                      
         AR    RE,R1                                                            
         USING PPRELD,RE                                                        
         SR    R0,R0                                                            
RDPRD50  CLI   0(RE),0            END OF RECORD                                 
         BE    RDPRD70                                                          
         CLI   0(RE),PPRELQ       X'24'                                         
         BE    RDPRD60                                                          
         ICM   R0,1,1(RE)         LENGTH OF ELEMENT                             
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    RE,R0              BUMP TO NEXT ELEMENT                          
         B     RDPRD50                                                          
*                                                                               
RDPRD60  CLC   PPRGAOFF,SPACES    IF PRODUCT ACC OFFICE OVERRIDE                
         BNH   RDPRD70                                                          
         OI    BIT,PRDOFFC                                                      
         MVC   SVACOFF,PPRGAOFF    USE IT                                       
RDPRD70  DS    0H                                                               
         TM    BIT,PRDOFFC         PRODUCT OFF OVERRIDE FOUND?                  
         BO    RDPRDX              YES SO USE IT                                
*                                  ELSE CHK FOR CLIENT REC                      
         DROP  RE                                                               
         LA    R2,AKEY            READ CLIENT RECORD                            
         USING CPYRECD,R2                                                       
         MVC   AKEY,SPACES                                                      
         MVC   AKEY(1),ACCTCD2    OHTER COMPANY CODE (IF APPL)                  
         MVC   AKEY+1(2),SVPROD                                                 
         LA    RF,PCLTREC                                                       
         MVC   AKEY+3(3),PCLTKCLT-PCLTREC(RF)                                   
         L     R2,AMYAREA                                                       
         ST    R2,AREC                                                          
         MVI   RDSE,2             READ FROM OTHER ACC FILE(IF APPL)             
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMREAD'                               
         BNE   RDPRDX                                                           
*                                                                               
         LA    R1,ACCORFST                                                      
         AR    R2,R1                                                            
         USING PPRELD,R2                                                        
         SR    R0,R0                                                            
RDPRD80  CLI   0(R2),0            END OF RECORD                                 
         BE    RDPRDX                                                           
         CLI   0(R2),PPRELQ       X'24'                                         
         BE    RDPRD90                                                          
         ICM   R0,1,1(R2)         LENGTH OF ELEMENT                             
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R2,R0              BUMP TO NEXT ELEMENT                          
         B     RDPRD80                                                          
*                                                                               
RDPRD90  DS    0H                                                               
         CLC   PPRGAOFF,SPACES     ANY OVERRIDE ACCOUNTING OFF                  
         BNH   RDPRDX                                                           
         MVC   SVACOFF,PPRGAOFF   USE IT                                        
RDPRDX   XIT1                                                                   
         DROP  R2                                                               
         SPACE                                                                  
*=============*                                                                 
* LITERAL POOL*                                                                 
*=============*                                                                 
*                                                                               
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*-----------------------------------------------*                               
* SETBLK - SETS UP CALL TO ACPOSTER WITH BASICS *                               
*-----------------------------------------------*                               
*                                                                               
         DS    0D                                                               
SETBLK   NMOD1 0,**SBLK**                                                       
         L     RC,0(R1)                                                         
         USING PPMXWRKD,RC        LOCAL WORKING STORAGE                         
*                                                                               
         LA    RE,ACPBLK           CLEAR CONTROL BLOCK                          
         LA    RF,L'ACPBLK                                                      
         XCEFL                                                                  
*                                                                               
         LA    R2,ACPBLK                                                        
         USING ACPOSTD,R2                                                       
         MVC   ACPACOM,VCOMFACS   A(COMFACS)                                    
         MVC   ACPUTL,UTL         A(UTL)                                        
         MVC   ACPDTCN,DATCON     A(DATCON)                                     
         MVC   ACPCMPC,ACCTCD     NATIVE COMPANY CODE                           
         MVC   ACPCMPC2,ACCTCD2   OTHER COMPANY CODE                            
         MVC   ACPSE1,ACCTSE      NATIVE SE NUMBER                              
         MVC   ACPSE2,ACCTSE2     OTHER SE NUMBER                               
         MVC   ACPSPROD,SVPROD    UNIT,LEDGER                                   
         MVC   ACPMI,SVMI         Y=USE MI RECORDS                              
         MVC   ACPCOST,SVCOST     Y=USE COSTING,BILLING,REVENUE                 
         MVC   ACPALPH,SALPH      AGENCY ALPHA FOR MEDIA SPLIT FILES            
         MVI   ACPSYS,C'P'        SYSTEM                                        
         MVC   ACPMED,MYMED       MEDIA CODE                                    
         MVC   ACPOFC,SVCOFF      CLIENT OFFICE CODE                            
         MVC   ACPOFG,SVOFFG      OFFICE GROUP                                  
         MVC   ACPCLT,MYCLT       CLIENT CODE                                   
         MVC   ACPPRD,MYPRD       PRODUCT CODE                                  
         MVC   ACPTACT,TRUEAMT    IF AOR BILL - SET TRUE BILL AMTS              
         MVC   ACPTGST,TRUEGST                                                  
         MVC   ACPTGSTI,TRUEGSTI                                                
         MVC   ACPPSTI(ACPMCODE*L'ACPPSTI),PSTIVALS                             
         MVC   ACPSYSBF,=V(SYSBUFF)                                             
         MVC   ACPMEDBF,=V(MEDBUFF)                                             
         MVC   ACPOFGBF,=V(OFGBUFF)                                             
         MVC   ACPOFCBF,=V(OFCBUFF)                                             
         MVC   ACPCLTBF,=V(CLTBUFF)                                             
         MVC   ACPPRDBF,=V(PRDBUFF)                                             
         XIT1                                                                   
         DROP  R2                                                               
         EJECT                                                                  
*=============*                                                                 
* LITERAL POOL*                                                                 
*=============*                                                                 
*                                                                               
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*----------------------------------*                                            
* SETMBLK - SETS UP CALL TO MXPOST *                                            
*----------------------------------*                                            
*                                                                               
         DS    0D                                                               
SETMXBLK NMOD1 0,**SMBLK**                                                      
         L     RC,0(R1)                                                         
         USING PPMXWRKD,RC        LOCAL WORKING STORAGE                         
*                                                                               
         L     RE,AMXBLK            CLEAR BLOCK                                 
         LH    RF,=Y(MXPLEN)                                                    
         XCEFL                                                                  
         L     R2,AMXBLK           R2=A(MXPOST BLOCK)                           
         USING MXPOSTD,R2                                                       
         MVC   MXBILL,ABILL        A(BILL RECORD)                               
         MVC   MXRECUP,RECUP       A(RECUP)                                     
         MVC   MXDTCN,DATCON       A(DATCON)                                    
         MVC   MXMXPROF,MXPROF     V(MXPROF)                                    
*                                                                               
         MVC   MXQOPT1,QOPT1       REQUEST OPTION 1                             
         MVC   MXCNTRY,CNTRY       COUNTRY CODE                                 
         MVC   MXEMU,EMULATE       ACCOUNTING FILE EMULATION                    
         MVC   MXCMP,ACCTCD2       COMP CODE WHERE POSTNGS WILL BE MADE         
         MVC   MXCMP2,ACCTCD       COMPANY CODE OF ORIGINATING FILE             
         MVC   MXNEWOFF,SVNEWOF    MEDIA OFFICE                                 
         MVI   MXSYS,C'P'          SYSTEM PRINT                                 
         MVC   MXMED,MYMED         MEDIA                                        
         MVC   MXALPHA,QAGENCY     MEDIA AGENCY                                 
         MVC   MXCLT,MYCLT         CLIENT                                       
         MVC   MXOCLT,OVRCLT       OVERRIDE CLIENT                              
         MVC   MXPRD,MYPRD         PRODUCT                                      
         MVC   MXOPRD,OVRPRD       OVERRIDE PRODUCT                             
*                                                                               
         MVC   MXBTYPE,TYPEBIL     BILL TYPE                                    
         MVC   MXDBTYPE,MTYPEBIL   BILL TYPE FOR FFTEL                          
         MVC   MXBTNUM,SVBTNUM     B4, B5, B6, ETC.                             
         MVC   MXFLAG2,SVFLAG      COST2, ETC.                                  
         MVC   MXINV,BINVOICE      BILL INVOICE NUMBER                          
         MVC   MXTINV,TRUEINV      BILL TRUE AOR INVOICE NUMBER                 
         MVC   MXLINV,BINVL        BILL LONG INVOICE NUMBER                     
         MVC   MXTLINV,TRUEINVL    BILL TRUE AOR LONG INVOICE NUMBER            
         MVC   MXPSTN2,PSTNUM2     N'ENTRIES IN MXPSTG                          
         MVC   MXPSTNA,PSTNUMA     N'ENTRIES IN MXPSG2                          
         MVC   MXPREVI,PREVINC     PREVIOUS INCOME FLAG                         
         MVC   MXPREVAI,PREVAINC   PREVIOUS AOR INCOME FLAG                     
         MVC   MXGSTO,GSTO                                                      
         MVC   MXGSTI,TRUEGSTI                                                  
         MVC   MXPSTO(ACPMCODE*L'MXPSTO),PSTOVALS                               
         MVC   MXPSTI(ACPMCODE*L'MXPSTI),PSTIVALS                               
         MVC   MXTODAY,BTODAYP     TODAYS DATE                                  
         MVC   MXREQTDT,REQTDT     TRANSFER REQUEST DATE                        
         MVC   MXRERNDT,RERUNDT    RERUN DATE                                   
         MVC   MXTRANDT,TRANSDT    ADJUSTMENT DATE FROM BILL                    
         MVC   MXTAMT,TRUEAMT      TRUE AOR AMOUNT                              
         MVC   MXWKCD,WKCD         WORK CODE                                    
         MVC   MXSPJBCD,SPJBCD     SPECIAL JOB CODE                             
         MVC   MXSVDESC,SVEDESC    ESTIMATE DESCRIPTION                         
         MVC   MXSVUSR,SVUSR       USER FLAG                                    
         MVC   MXUDESC,SVPDESC1    USER DESCRIPTIONS (4)                        
         MVC   MXUUSER,SVPUSER1    USER DATA (4)                                
         MVC   MXEPONUM,SVEPONUM   ESTIMATE PO NUMBER                           
*                                                                               
         MVC   MXUCMDP,SVUCMDP     PRODUCT UCOM DESCRIPTIONS (4)                
         MVC   MXUCMDE,SVUCMDE     ESTIMATE UCOM DESCRIPTIONS (4)               
         MVC   MXUCMDR,SVUCMDR     REGION UCOM DESCRIPTIONS (4)                 
         MVC   MXUCMDD,SVUCMDD     DISTRICT UCOM DESCRIPTIONS (4)               
*                                                                               
         MVC   MXUCMP,SVUCMP       PRODUCT UCOM FIELDS (4)                      
         MVC   MXUCME,SVUCME       ESTIMATE UCOM FIELDS (4)                     
         MVC   MXUCMR,SVUCMR       REGION UCOM FIELDS (4)                       
         MVC   MXUCMD,SVUCMD       DISTRICT UCOM FILEDS (4)                     
*                                                                               
         MVC   MXUCMPLN,SVUCMPLN   LENGTH OF FOUR PRODUCT UCOM FIELDS           
         MVC   MXUCMELN,SVUCMELN   LENGTH OF FOUR ESTIMATE UCOM FIELDS          
         MVC   MXUCMRLN,SVUCMRLN   LENGTH OF FOUR REGION UCOM FIELDS            
         MVC   MXUCMDLN,SVUCMDLN   LENGTH OF FOUR DISTRICT UCOM FIELDS          
*                                                                               
         MVC   MXPGRCD(L'PGRCD),PGRCD                                           
         MVC   MXPGRNM(L'PGRNAME),PGRNAME                                       
         MVC   MXSJCLTN,SJCLTNM    SJ CLIENT NAME                               
         MVC   MXSJPRDN,SJPRDNM    SJ PRODUCT NAME                              
         MVC   MXREGCD,REGCODE     PRINT REGION CODE                            
         MVC   MXREGNM,REGNAME     PRINT REGION NAME                            
         MVC   MXDISTCD,DISTCODE   PRINT DISTICT CODE                           
         MVC   MXDISTNM,DISTNAME   PRINT DISTRICT NAME                          
         MVC   MXRSYS,SVRSYS       REDEFINED SYSTEM CODE                        
         MVC   MXRMED,SVRMED       REDEFINED MEDIA CODE                         
         MVC   MXADID,SVADID       AD-ID CODE                                   
         MVC   MXFLPRF,SVPROF+6    WB FLIGHT PROFILE SETTING                    
         MVC   MXFLDET,SVBLFLT     WB FLIGHT CODE                               
         MVC   MXPONUM,SVBLPO      PO#                                          
         MVC   MXPOLBL,SVPOLBL     PO# LABLE OVERRIDE                           
         MVC   MXCNET,SVCNET       CALCULATED NET                               
         MVC   MXCURR,SVCURR       CURRENCY U/E/G/Y/P                           
         ZAP   MXBNET,SVBNET       MIDAS EXCHANGE FOR NB MEMO                   
         XIT1                                                                   
         SPACE                                                                  
*=============*                                                                 
* LITERAL POOL*                                                                 
*=============*                                                                 
*                                                                               
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*---------------------------------------------------*                           
* RDPOST - CALLS ACPOSTER TO READ POST MAINT RECORDS*                           
*          FOR A BILL                               *                           
*---------------------------------------------------*                           
*                                                                               
         DS    0D                                                               
RDPOST   NMOD1 0,**RPST**                                                       
         L     RC,0(R1)                                                         
         USING PPMXWRKD,RC        LOCAL WORKING STORAGE                         
*                                                                               
         GOTO1 CLRAREA,DMCB,V(POSTINGS),V(POSTNGSX)                             
         GOTO1 =A(SETBLK),DMCB,(RC) SET UP BASIC ACPOSTER CALL                  
         LA    R2,ACPBLK                                                        
         USING ACPOSTD,R2                                                       
         MVI   ACPTYPE,0          POST MAINT RECORD                             
         MVC   ACPBILL,ABILL                                                    
         MVC   ACPPOST,=V(IOAREA)    A(POSTING ACCS TO RETURN)                  
         MVC   ACPPOST2,=V(POSTINGS) A(POSTINGS RETURNED)                       
         MVC   ACPXSORT,XSORT     PASS XSORT                                    
         L     R1,=A(ACCAREA)                                                   
         STCM  R1,15,ACPSVACC     A(SAVED AREA FOR ACPOSTER)                    
         MVC   ACPNUMA,PSTNUMA    MAX NUMBER OF POSTING ACCOUNTS                
         MVC   ACPNUM2,PSTNUM2    MAX NUMBER OF POSTINGS                        
         ICM   RE,15,=AL4(ACCAREAX-ACCAREA)                                     
         STCM  RE,15,ACPNUM3      LENGTH OF SAVED AREA                          
         MVC   ACPGSTO,GSTO       OUTPUT GST FROM BILL                          
         MVC   ACPPSTO(10*L'ACPPSTO),PSTOVALS                                   
         GOTO1 MXPROF,DMCB,MTPFCC,PROFWORK                                      
         MVC   ACPCC,PROFWORK     CC OVERRIDE                                   
         CLI   ACPCC,0                                                          
         BNE   *+8                                                              
         MVI   ACPCC,C'N'                                                       
         GOTO1 MXPROF,DMCB,MTPFSJ,PROFWORK                                      
         MVC   ACPSJPRD,PROFWORK  CHECK FOR SJ PRODUCT RECORD                   
         GOTO1 MXPROF,DMCB,MTPFSJ1C,PROFWORK                                    
         MVC   ACP1CPRD,PROFWORK  CHECK FOR PRD LVL 1C                          
         GOTO1 MXPROF,DMCB,MTPFIPCT,PROFWORK                                    
         MVC   ACPIOR,PROFWORK     IOR PERCENTAGE OF GROSS REG BILL             
         GOTO1 MXPROF,DMCB,MTPFSPCT,PROFWORK     GROUPM MIDAS SPLIT %           
         MVC   ACPTMPCT,PROFWORK                                                
         CLC   =C'AOR',TYPEBIL     IF AOR BILL                                  
         BNE   RDPOST5                                                          
         GOTO1 MXPROF,DMCB,MTPFIPT2,PROFWORK                                    
         MVC   ACPIOR,PROFWORK     IOR PERCENTAGE OF GROSS AOR BILL             
*                                                                               
RDPOST5  CLC   =C'RET',TYPEBIL    IF RETAIL BILL                                
         BNE   RDPOST10                                                         
         GOTO1 MXPROF,DMCB,MTPF3LDG,PROFWORK                                    
         MVC   ACPLDG,PROFWORK    SET UNIT 3 LEDGER                             
         GOTO1 MXPROF,DMCB,MTPFRCV,PROFWORK                                     
         MVC   ACPRCV,PROFWORK    SET LOOKING FOR RECV = OVERRIDE               
         GOTO1 MXPROF,DMCB,MTPFCST,PROFWORK                                     
         MVC   ACPCST,PROFWORK    SET LOOKING FOR COST = OVERRIDE               
         GOTO1 MXPROF,DMCB,MTPFMGOV,PROFWORK                                    
         MVC   ACPMGROV,PROFWORK  LOOKING MARKET GROUP OVERRIDE                 
*                                                                               
RDPOST10 GOTO1 MXPROF,DMCB,MTPFRCSJ,PROFWORK                                    
         MVC   ACPRVSJ,PROFWORK    RCVBL SJ                                     
         GOTO1 MXPROF,DMCB,MTPFJBCD,PROFWORK                                    
         MVC   ACPJBCD,PROFWORK    JOB CODE FOR SEP CD                          
         GOTO1 MXPROF,DMCB,MTPFSUFX,PROFWORK                                    
         MVC   ACPSUFX,PROFWORK    SUSPENSE SUFFIX                              
         GOTO1 MXPROF,DMCB,MTPFRBFX,PROFWORK                                    
         MVC   ACPRBFX,PROFWORK    REBATE SUFFIX                                
         GOTO1 MXPROF,DMCB,MTPFCLRC,PROFWORK                                    
         MVC   ACPRCLT,PROFWORK    POST TO CLIENT LVL RCVBL                     
         GOTO1 MXPROF,DMCB,MTPFWKSJ,PROFWORK                                    
         MVC   ACPWKSJ,PROFWORK    WORK CODE FOR SJ                             
RDPOST20 DS    0H                                                               
         XIT1                                                                   
         DROP  R2                                                               
         SPACE                                                                  
*=============*                                                                 
* LITERAL POOL*                                                                 
*=============*                                                                 
*                                                                               
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*-----------------------------------------------*                               
* POSTCALL - ACTUAL CALL TO ACPOSTER            *                               
*-----------------------------------------------*                               
*                                                                               
         DS    0D                                                               
POSTCALL NMOD1 0,**PCAL**                                                       
         L     RC,0(R1)                                                         
         USING PPMXWRKD,RC        LOCAL WORKING STORAGE                         
*                                                                               
         LA    R2,ACPBLK                                                        
         GOTO1 ACPOSTER,DMCB,(R2)                                               
         L     RF,UTL             MAKE SURE ACCOUNTING NUMBER                   
         MVC   4(1,RF),SAVSYS     RE-SET TO MEDIA SYSTEM NUMBER                 
         XIT1                                                                   
         SPACE                                                                  
*=============*                                                                 
* LITERAL POOL*                                                                 
*=============*                                                                 
*                                                                               
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*--------------------------------*                                              
* SETPRT - SETS UP CALL TO MXPRNT*                                              
*--------------------------------*                                              
*                                                                               
         DS    0D                                                               
SETPRT   NMOD1 0,**SPRT**                                                       
         L     RC,0(R1)                                                         
         USING PPMXWRKD,RC        LOCAL WORKING STORAGE                         
         L     R5,ABILL                                                         
         USING PBILLREC,R5                                                      
*                                                                               
         USING MXPRNTD,R2                                                       
         LA    R2,PRTBLOCK                                                      
         LR    RE,R2                                                            
         LA    RF,PMXSAVE-PBILL   CLEAR OUT REQUEST DETAILS                     
         XCEF                                                                   
*                                                                               
         LA    R1,ACPBLK                                                        
         USING ACPOSTD,R1                                                       
         MVC   PBILL,ABILL        A(BILL RECORD)                                
         MVC   PPOST,=V(POSTINGS) A(POSTINGS)                                   
         MVC   PDTCN,DATCON       A(DATCON)                                     
         MVC   PDMGR,DATAMGR      A(DATAMGR)                                    
         MVC   PREPORT,REPORT     REPORT                                        
         MVC   PHEXOUT,HEXOUT     A(HEXOUT)                                     
         MVC   PBUFFALO,BUFFALO   A(BUFFALO)                                    
         MVC   POFFICER,AOFFICER  A(OFFICER)                                    
         MVC   PCOMFACS,VCOMFACS  A(COMFACS)                                    
         LA    RE,HEADHOOK                                                      
         ST    RE,PHEAD           A(HEADHOOK)                                   
         MVC   PP1,L1             A(PRINT LINES)                                
         MVC   PP2,L2                                                           
         MVC   PP3,L3                                                           
         LA    RE,H2                                                            
         ST    RE,PH2             A(HEADLINES)                                  
         LA    RE,H3                                                            
         ST    RE,PH3                                                           
         LA    RE,RCSUBPRG        SAVE A(RCSUBPRG)                              
         ST    RE,PRCSUBP                                                       
         LA    RE,FORCEHED                                                      
         ST    RE,PFORCEH         A(FORCEHED)                                   
         LA    RE,PAGE            A(PAGE NUMBER)                                
         ST    RE,PPAGE                                                         
         LA    RE,SVPROF2         A(MX PROFILE)                                 
         ST    RE,PSUMPROF                                                      
         MVC   PALPHA,QAGENCY     AGENCY ALPHA                                  
         MVC   PAGY,ACCALPH       AGENCY                                        
         MVI   PSYS,C'P'          SYSTEM                                        
         MVC   PMED,MYMED         MEDIA                                         
         MVC   PMEDOFF,SVCOFF     MEDIA OFFICE                                  
         MVC   PCLT,MYCLT         CLIENT                                        
         MVC   PPRD,MYPRD         PRODUCT                                       
         MVC   PCOMPANY,ACCTCD2   OTHER COMPANY CODE (IF APPL)                  
         MVC   PCOUNTRY,CNTRY     COUNTRY CODE                                  
         MVC   PSVPROD,SVPROD     UNIT/LEDGER                                   
         MVC   PACCOFF,SVACOFF    ACC OFFICE                                    
         MVC   PACCOF2,SVACOF2    ACC OFFICE OVERRIDE FOR INTERNAL              
         MVC   PMOA,TRANSDT       MOA DATE(CALC FROM BQDATE)                    
         MVC   PAOR,=C'NNNN'      NOT MAIN AOR BILL                             
         MVI   PAORMSG,C'Y'                                                     
         MVC   PINVOICE(L'BINVOICE),BINVOICE                                    
         CLC   BINVL,SPACES                                                     
         BNH   *+10                                                             
         MVC   PINVOICE,BINVL     USER LONGER INV # IF THERE                    
         OC    PINVOICE,SPACES                                                  
         CLC   =C'AOR',TYPEBIL                                                  
         BNE   SETPRT30                                                         
         MVC   PAORMSG,PSTAOR     Y = POSTING AOR BILL SET                      
         TM    PBILCMSW,X'08'     SET TRUE INFO ONLY W/ MAIN CLT BILL           
         BO    SETPRT30                                                         
         CLI   PBILSEP,C'R'                                                     
         BNE   SETPRT30                                                         
         MVC   PAOR,TRUEAMT       TRUE AOR AMOUNT                               
         MVC   PTINV(L'TRUEINV),TRUEINV  TRUE AOR INVOICE NUMBER                
         CLC   TRUEINVL,SPACES                                                  
         BNH   *+10                                                             
         MVC   PTINV,TRUEINVL    TRUE AOR LONGER INVOICE NUMBER                 
         OC    PTINV,SPACES                                                     
SETPRT30 MVC   PPOSTNUM,PSTNUM2   # OF POSTINGS                                 
         MVC   PPOSTERR,ACPERR    ERRORS FOUND WITH POSTINGS                    
         MVC   PPOSTER2,CONERR    ERRORS FOUND WITH CONTRA ACCOUNTS             
         MVC   PPOSTERC,ACPERRC   ERRORS W/POSTINGS CONTINUED                   
         GOTO1 MXPROF,DMCB,MTPFRCSJ,PROFWORK                                    
         CLI   PROFWORK,C'Y'      IF POSTING TO SJ & NOT REBATE BILILNG         
         BNE   SETPRT35                                                         
         CLI   PBILLTYP,C'R'                                                    
         BE    *+10                                                             
         MVC   PJBCD,JOBCD        PRINT JOB CODE FROM BILL RECORD               
SETPRT35 CLI   PBILCDSW,C'S'                                                    
         BNE   SETPRT40                                                         
         CLI   PBILSEP,C'C'       IF CD BILL & IF SPROF MAINT OVERRIDE          
         BNE   SETPRT40                                                         
         CLC   SPJBCD,SPACES                                                    
         BNH   SETPRT40                                                         
         MVC   PJBCD,SPJBCD       USE IT                                        
SETPRT40 DS    0H                                                               
         XIT1                                                                   
         DROP  R1,R2,R5                                                         
         SPACE 2                                                                
*=============*                                                                 
* LITERAL POOL*                                                                 
*=============*                                                                 
*                                                                               
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*---------------------------------------------*                                 
* GETRUE - FIND TRUE AOR BILL & SAVE INFO     *                                 
*---------------------------------------------*                                 
*                                                                               
         USING PPMXWRKD,RC        LOCAL WORKING STORAGE                         
         USING PPWORKD,RA,R7      GLOBAL WORKING STORAGE                        
         USING PBILLREC,R2                                                      
GETRUE   NTR1  BASE=*,LABEL=*                                                   
         L     R2,0(,R1)                                                        
         TM    PBILCMSW,X'10'      MUST BE GIVEN MAIN CLIENT BILL               
         BO    *+6                                                              
         DC    H'0'                                                             
         TM    PBILCMSW,X'08'      NET AS NO TRUE AOR                           
         BO    GETRUEX                                                          
*                                                                               
         LHI   R3,0                                                             
         MVI   FNDTRUE,C'N'                                                     
         XC    KEY,KEY                                                          
         MVC   KEY(L'PBILLKEY),0(R2)                                            
         LA    RE,DMREAD                                                        
GETRUE02 ST    RE,DMCB                                                          
         CLI   QOPT4,C'U'         IF REVERSAL TO UNPOST                         
         BNE   *+8                                                              
         MVI   DMCB,X'08'        PASS BACK DELETED RECORDS                      
         GOTO1 DATAMGR,DMCB,,PRTDIR,KEY,KEY                                     
*        CLC   KEY(L'PBILLKEY),0(R2)                                            
*        BE    *+6                                                              
*        DC    H'0'                                                             
         CLC   KEY(PBILKBNO-PBILLKEY),0(R2)                                     
         BNE   GETRUE30                                                         
         L     R2,AMYAREA                                                       
         GOTO1 DATAMGR,DMCB,GETREC,PRTFILE,KEY+27,(R2),DMWORK                   
         CLC   RUNDT,PBILLDAT                                                   
         BNE   GETRUE30                                                         
         TM    PBILCMSW,X'10'      FIND AORM                                    
         BZ    GETRUE05            NOT THIS ONE                                 
         TM    PBILCMSW,X'89'      X'80'+X'08'+X'01'                            
         BNZ   GETRUE05            NOT AORM THEN                                
         CLI   FNDTRUE,C'Y'        PROCESSED ONE SET ALREADY ?                  
         BE    GETRUE32            YES SO STOP HERE FOR NOW                     
*                                                                               
GETRUE05 TM    PBILCMSW,X'20'      BILL MUST BE TRUE AOR                        
         BZ    GETRUE10                                                         
         GOTOR SVPTRUE,DMCB,AMYAREA                                             
         MVI   FNDTRUE,C'Y'                                                     
         AHI   R3,1                COUNT # OF BILLS                             
         LA    RE,DMRSEQ           READ NEXT                                    
         B     GETRUE02                                                         
*                                                                               
GETRUE10 TM    PBILCMSW,X'10'      AOR BILL, AT LEAST?                          
         BO    GETRUE12            NO -END OF AOR SET                           
         TM    PBILCMSW,X'80'                                                   
         BZ    GETRUE30                                                         
GETRUE12 AHI   R3,1                                                             
         LA    RE,DMRSEQ           READ NEXT                                    
         B     GETRUE02                                                         
*                                                                               
GETRUE30 CLI   FNDTRUE,C'Y'        IF END OF SET THEN MUST OF FOUND             
         BE    *+6                                                              
         DC    H'00'                                                            
                                                                                
GETRUE32 CHI   R3,4                NO MORE THAN 4                               
         BNH   *+6                                                              
         DC    H'00'                                                            
         STC   R3,NUMABIL                                                       
                                                                                
GETRUEX  XIT1                                                                   
         SPACE                                                                  
*-------------------*                                                           
* LITERAL POOL      *                                                           
*-------------------*                                                           
         LTORG                                                                  
         SPACE                                                                  
         DROP  R2,R7,RA,RB,RC                                                   
         EJECT                                                                  
*---------------------------------------------*                                 
* SVPTRUE - SAVE SOME INFO FROM TRUE AOR BILL *                                 
*---------------------------------------------*                                 
*                                                                               
         USING PPMXWRKD,RC        LOCAL  WORKING STORAGE                        
         USING PPWORKD,RA,R7      GLOBAL WORKING STORAGE                        
         USING PBILLREC,R2                                                      
SVPTRUE  NTR1  BASE=*,LABEL=*                                                   
         L     R2,0(,R1)                                                        
*                                                                               
         ZAP   TRUEAMT,PBILLRCV   TRUE AOR AMOUNT                               
         MVC   TEMPINV,PBILKBNO                                                 
         MVC   TEMPDATE,PBILLDAT                                                
         XC    TRUEINVL,TRUEINVL                                                
         SR    RF,RF                                                            
         CLC   PBILIMED,SPACES    ONLY STORE LONG INVOICE IF THE RULES          
         BNH   *+8                ARE IN THE BILL HEADER                        
         LA    RF,TRUEINVL                                                      
         GOTO1 =A(SETINV),DMCB,(RC),TRUEINV,(RF)                                
         XC    TRUEGST,TRUEGST     CLEAR TRUE GST AMOUNT                        
         MVI   TRUEGSTI,C' '       CLEAR TRUE GST CODE                          
         LA    RE,L'PSTIVALS       CLEAR PST INPUT INFORMATION                  
         MH    RE,=Y(ACPMCODE)                                                  
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         XC    PSTIVALS(0),PSTIVALS                                             
         LA    RF,PSTIVALS         RF=A(INPUT PST TABLE)                        
*                                                                               
         SR    R0,R0                                                            
         LA    R1,PBILLEL          R1=A(FIRST ELEMENT)                          
SVPTRU20 CLI   0(R1),0             TEST END OF RECORD                           
         BE    SVPTRUX                                                          
         CLI   0(R1),X'0A'         TEST GST ELEMENT                             
         BNE   SVPTRU25                                                         
         USING PBILVEL,R1                                                       
         MVC   TRUEGST,PBILLVAT    SAVE TRUE AOR GST AMOUNT                     
         MVC   TRUEGSTI,PBILLVCD   SAVE TRUE AOR GST CODE                       
         B     SVPTRU30                                                         
*                                                                               
SVPTRU25 CLI   0(R1),X'84'         TEST PST ELEMENT                             
         BNE   SVPTRU30                                                         
         USING PBLPSTEL,R1                                                      
         ZIC   RE,PBLPVPRV                                                      
         BCTR  RE,0                                                             
         MH    RE,=AL2(PRVTABLN)                                                
         L     R2,APRVTAB                                                       
         AR    RE,R2                                                            
         USING PRVTABD,RE                                                       
         MVC   0(2,RF),PRVTCODE    SAVE PROVINCE CODE                           
         MVC   2(1,RF),PRVTIN      SAVE ELEMENT EQUATED ACCOUNT                 
         MVC   3(1,RF),PBLPVCOD    SAVE TAX TYPE                                
         MVC   4(4,RF),PBLPVBAS    SAVE BASIS AMOUNT                            
         MVC   8(4,RF),PBLPVAMT    SAVE AMOUNT                                  
         LA    RF,L'PSTIVALS(RF)                                                
*                                                                               
SVPTRU30 ICM   R0,1,1(R1)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R1,R0                                                            
         B     SVPTRU20                                                         
*                                                                               
SVPTRUX  XIT1                                                                   
         DROP  R1,R2,RE                                                         
         EJECT                                                                  
*-------------------*                                                           
* LITERAL POOL      *                                                           
*-------------------*                                                           
         LTORG                                                                  
         SPACE                                                                  
         EJECT                                                                  
*------------------------------------------------------------*                  
* SETINV - CALLS PPFMTINO TO SAVE 6 BYTE INVOICE NUMBER AND  *                  
*          LONGER INVOICE NUMBER                             *                  
*     XIT- SETS 6 CHAR INVOICE NUMBER IN BINVOICE OR TRUEINV *                  
*          SETS LONGER INVOICE NUMBER IN BINVL OR TRUEINVL   *                  
*          IF THE BILL HEADER IS STORING THE RULES           *                  
*------------------------------------------------------------*                  
         DS    0D                                                               
SETINV   NMOD1 0,**SINV**                                                       
         L     RC,0(R1)                                                         
         USING PPMXWRKD,RC        LOCAL WORKING STORAGE                         
         LM    R2,R3,4(R1)                                                      
*                                                                               
         GOTO1 =V(PPFMTINO),DMCB,(C'B',PBILLDAT),(2,TEMPINV),B1PROF,   +        
               B1XPROF,PBILLREC                                                 
         L     RF,DMCB+8                                                        
         MVC   0(2,R2),0(RF)                                                    
         L     RF,DMCB+4                                                        
         MVC   2(4,R2),3(RF)                                                    
         LTR   R3,R3                 WAN'T LONGER INVOICE NUMBER?               
         BZ    SETINVX                                                          
         L     RF,DMCB                                                          
         MVC   0(L'BINVL,R3),0(RF)   SAVE LONG INVOICE NUMBER                   
*                                                                               
* THIS IS OLD CODE FOR REVERSALS FOR BILLS EARLIER THAN 8/1/95                  
*&&DO                                                                           
         MVC   WORK(2),TEMPDATE+2                                               
         CLI   REVSTAT,C'Y'        IF REVERSE IN PROGRESS (MY/UNPOST)           
         BNE   SETINV25                                                         
         CLC   REVDATE,=X'BF01'    AND ORIG. TRANS DT LESS THAN 8/1/95          
         BNL   SETINV25                                                         
         GOTO1 DATCON,DMCB,(0,TEMPDATE),(1,DUB)                                 
*                                                                               
         CLI   B1XPROF+4,0         THEN KEEPING LOOKING AT B1X PROFILE          
         BE    SETINV20                                                         
         ZIC   R0,DUB             YEAR OF BILL                                  
         ZIC   RF,B1XPROF+4       INVOICE BASE YEAR                             
         SR    R0,RF              DIFFERENCE                                    
         BNP   SETINV20                                                         
         MHI   R0,12                                                            
         ZIC   RF,DUB+1            MONTH OF BILL                                
         AR    R0,RF                                                            
*                                                                               
SETINV10 CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK(2),DUB        SET NEW INVOICE MONTH                         
         B     SETINV30                                                         
*                                                                               
SETINV20 CLI   B1XPROF+5,0        SEE IF BUMPING INV MONTH                      
         BE    SETINV30                                                         
         ZIC   RF,DUB+1           MONTH OF BILL                                 
         ZIC   R0,B1XPROF+5                                                     
         AR    R0,RF                                                            
         CH    R0,=H'12'                                                        
         BNH   SETINV10                                                         
         SH    R0,=H'12'                                                        
         B     SETINV10                                                         
*                                                                               
SETINV25 OC    PBILIMO,PBILIMO     CHECK NEW FIELD                              
         BZ    *+10                                                             
         MVC   WORK(2),PBILIMO                                                  
*                                                                               
SETINV30 MVC   HALF,TEMPINV                                                     
         LH    R0,HALF                                                          
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK+2(4),DUB                                                    
*&&                                                                             
*                                                                               
SETINVX  XIT1                                                                   
*=============*                                                                 
* LITERAL POOL*                                                                 
*=============*                                                                 
*                                                                               
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*-----------------------------------------------*                               
* SETVATO - SET OUTPUT GST AND PST INFORMATION  *                               
*-----------------------------------------------*                               
*                                                                               
         DS    0D                                                               
SETVATO  NMOD1 0,**VATO**                                                       
         L     RC,0(R1)                                                         
         USING PPMXWRKD,RC        LOCAL WORKING STORAGE                         
         L     R5,4(R1)                                                         
         USING PBILLREC,R5                                                      
*                                                                               
         MVI   GSTO,C' '          CLEAR GST CODE                                
         LA    RE,L'PSTOVALS      CLEAR PST INFO                                
         MH    RE,=Y(ACPMCODE)                                                  
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         XC    PSTOVALS(0),PSTOVALS                                             
         LA    RF,PSTOVALS        RF=A(OUTPUT PST TABLE)                        
*                                                                               
         SR    R0,R0                                                            
         LA    RE,PBILLEL         RE=A(FIRST ELEMENT)                           
SETVATO5 CLI   0(RE),0            TEST END OF RECORD                            
         BE    SETVATOX                                                         
         CLI   0(RE),X'0A'        TEST GST ELEMENT                              
         BNE   *+14                                                             
         USING PBILVEL,RE                                                       
         MVC   GSTO,PBILLVCD      SET OUTPUT GST CODE                           
         B     SETVATO8                                                         
*                                                                               
         CLI   0(RE),X'84'        TEST PST ELEMENT                              
         BNE   SETVATO8                                                         
         USING PBLPSTEL,RE                                                      
         ZIC   R1,PBLPVPRV                                                      
         BCTR  R1,0                                                             
         MH    R1,=AL2(PRVTABLN)                                                
         L     R2,APRVTAB                                                       
         AR    R1,R2                                                            
         USING PRVTABD,R1                                                       
         MVC   0(2,RF),PRVTCODE    SAVE PROVINCE CODE                           
         MVC   2(1,RF),PRVTOUT     SAVE ELEMENT EQUATED ACCOUNT                 
         MVC   3(1,RF),PBLPVCOD    SAVE TAX TYPE                                
         MVC   4(4,RF),PBLPVBAS    SAVE BASIS AMOUNT                            
         MVC   8(4,RF),PBLPVAMT    SAVE AMOUNT                                  
         LA    RF,L'PSTOVALS(RF)                                                
*                                                                               
SETVATO8 ICM   R0,1,1(RE)          BUMP TO NEXT ELEMENT IN RECORD               
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    RE,R0                                                            
         B     SETVATO5                                                         
*                                                                               
SETVATOX XIT1                                                                   
         DROP  RE,R1,R5                                                         
         SPACE                                                                  
*=============*                                                                 
* LITERAL POOL*                                                                 
*=============*                                                                 
*                                                                               
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*------------------------------------------*                                    
* GETACC -  GIVEN ROW NUMBER WILL PLUCK    *                                    
*           ACPACC FROM THAT ROW AND SET   *                                    
*           IT INTO CONTRA                 *                                    
*      NTRY - ROW NUMBER                   *                                    
*      XIT - SET CONTRA & CC CODE          *                                    
*------------------------------------------*                                    
*                                                                               
         DS    0D                                                               
GETACC   NMOD1 0,**GACC**                                                       
         L     RC,0(R1)                                                         
         USING PPMXWRKD,RC        LOCAL WORKING STORAGE                         
*                                                                               
         MVC   BYTE,7(R1)                                                       
         ZIC   R3,PSTNUM2         NUMBER OF POSTING                             
         L     R1,APOST           POSTINGS RETURNED FROM ACPOSTER               
         USING ACPRTND,R1                                                       
GETACC5  CLC   ACPBTYP,BYTE       FIND CORRECT ROW                              
         BE    GETACC10                                                         
         LA    R1,ACPRTNL2(R1)                                                  
         BCT   R3,GETACC5                                                       
         LTR   RB,RB                                                            
         B     GETACCX            ROW NOT FOUND                                 
*                                                                               
GETACC10 LA    RE,ACPBLK                                                        
         USING ACPOSTD,RE                                                       
         MVC   CONLVL(L'ACPLVL),ACPLVL     ACCOUNT LEVEL                        
         MVC   CONTRA(1),ACCTCD2           OTHER COMPANY CODE                   
         MVC   CONTRA+1(14),ACPACC         CONTRA ACCOUNT                       
         OC    CONTRA+1(14),SPACES                                              
         MVC   CONTRAN,ACPACCN    SET CONTRA ACCOUNT NAME                       
         CR    RB,RB                                                            
GETACCX  XIT1                                                                   
         DROP  R1,RE                                                            
         SPACE                                                                  
*=============*                                                                 
* LITERAL POOL*                                                                 
*=============*                                                                 
*                                                                               
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*------------------------------------------*                                    
* PUTSORT - PUTS RECORD TO SORTER          *                                    
*------------------------------------------*                                    
*                                                                               
         DS    0D                                                               
PUTSORT  NMOD1 0,**PSRT**                                                       
         L     RC,0(R1)                                                         
         USING PPMXWRKD,RC        LOCAL WORKING STORAGE                         
*                                                                               
         L     R3,4(R1)           WORKER FILE RECORD                            
         LH    R1,0(R3)           LENGTH OF RECORD                              
         SHI   R3,SRTKEYLQ        PT TO SORTER KEY                              
         USING SRTKEYD,R3                                                       
         AHI   R1,SRTKEYLQ        DATA LENGTH + SORTER KEY LENGTH               
         STH   R1,SRTLEN          SORTER RECORD LENGTH                          
         MVC   SRTID,ACCALPH      ALPHA ID FOR WORKER FILE                      
         MVC   SRTOID,ACCTOID2    NUMERIC ID FROM OTHER WORKER FILE             
         CLI   DETL,C'Y'          DETAIL RECORD?                                
         BNE   PUTSORT4                                                         
         MVC   SRTID,RCAGYFLT     ADD TO ACC SIGN ON ID                         
         MVC   SRTOID,ACCTOID1    NUMERIC ID FROM NATIVE WORKER FILE            
         OC    SVPROF2(2),SVPROF2                                               
         BZ    PUTSORT4                                                         
         MVC   SRTID,SVPROF2                                                    
*                                                                               
PUTSORT4 MVC   SRTINV,BINVOICE    BILL INVOICE NUMBER                           
         CLI   SEQONE,C'Y'        REVERSAL RECORD?                              
         BE    *+8                                                              
         MVI   SRTSEQ,SRTSBOT     FORCE REGULAR POSTINGS RECS FIRST             
         TM    UPSI,UPSIPUTS                                                    
         BZ    PUTSORT5                                                         
         LA    R0,SRTKEYLQ+L'ACTKEY                                             
         GOTOR =V(PRNTBL),DMCB,=C'PUT',(R3),C'DUMP',(R0),=C'1D'                 
PUTSORT5 GOTO1 =V(SORTER),DMCB,=C'PUT',(R3)                                     
         XIT1                                                                   
         DROP  R3                                                               
         SPACE                                                                  
*=============*                                                                 
* LITERAL POOL*                                                                 
*=============*                                                                 
*                                                                               
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                 DROP PREVIOUS BASE REGS                       
         SPACE                                                                  
         EJECT                                                                  
*------------------------------------------*                                    
* READACC - READS A RECORD ON THE ACCOUNT  *                                    
*           FILE AND SETS CONDITION CODE   *                                    
*------------------------------------------*                                    
*                                                                               
         DS    0D                                                               
READACC  NMOD1 0,**RDACC*                                                       
         L     RC,0(R1)                                                         
         USING PPMXWRKD,RC        LOCAL WORKING STORAGE                         
*                                                                               
         L     RF,4(R1)                                                         
         MVC   COMMAND,0(RF)                                                    
*                                                                               
         XC    DMCB(24),DMCB                                                    
         L     RF,UTL             SET SE NUMBER                                 
         MVC   4(1,RF),ACCTSE     NATIVE ACC FILE                               
         CLI   RDSE,2                                                           
         BNE   *+10                                                             
         MVC   4(1,RF),ACCTSE2    OTHER ACC FILE                                
         GOTO1 DATAMGR,DMCB,COMMAND,=C'ACCOUNT',AKEY,AREC                       
         L     RF,UTL             MAKE SURE ACCOUNTING NUMBER                   
         MVC   4(1,RF),SAVSYS     RE-SET TO SPOT SYSTEM NUMBER                  
         L     RE,AREC                                                          
         USING ACCRECD,RE                                                       
         SR    R1,R1                                                            
         ICM   R1,3,ACCRLEN                                                     
         AR    RE,R1                                                            
         MVI   0(RE),0            PHYSICALLY MARK END OF RECORD                 
*                                                                               
         CLI   DMCB+8,0           SET CC CODE                                   
READAX   XIT1                                                                   
         SPACE 2                                                                
*=============*                                                                 
* LITERAL POOL*                                                                 
*=============*                                                                 
*                                                                               
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                 DROP PREVIOUS BASE REGS                       
         EJECT                                                                  
         DS    0D                                                               
DOAOR    NMOD1 0,**DAOR**                                                       
         L     RC,0(R1)                                                         
         USING PPMXWRKD,RC        LOCAL WORKING STORAGE                         
*                                                                               
         L     RE,ABILL                                                         
         USING PBILLREC,RE                                                      
         TM    PBILCMSW,X'08'                                                   
         BNO   DOAOR10                                                          
         MVI   NUMABIL,1                                                        
         DROP  RE                                                               
         GOTO1 CLRAREA,DMCB,V(MYAREA3),V(MYAREA3X)                              
         B     DOAOR20                                                          
*                                                                               
DOAOR10  MVC   FULL,ABILL                                                       
         BAS   RE,GETAOR          SET UP DEFAULT ACCOUNTS                       
         BAS   RE,GETPT           GET TRUE AOR POSTINGS                         
*                                 RESET FOR MAIN BILL POSTINGS                  
DOAOR20  GOTO1 CLRAREA,DMCB,V(POSTINGS),V(POSTNGSX)                             
         GOTO1 CLRAREA,DMCB,V(IOAREA),V(IOAREAX)                                
         MVC   FULL,ABILL                                                       
         BAS   RE,GETAOR                                                        
         LA    R2,ACPBLK                                                        
         USING ACPOSTD,R2                                                       
         GOTO1 =A(RDPOST),DMCB,(RC)                                             
         MVI   ACPAFLG,C'M'       GET MAIN BILL POSTINGS ONLY                   
         GOTO1 =A(POSTCALL),DMCB,(RC)                                           
         LA    R1,AORERRS                                                       
         USING AORERD,R1                                                        
         MVC   AORERR,ACPERR                                                    
         MVC   AORERRC,ACPERRC                                                  
         DROP  R2,R1                                                            
         L     R3,=V(POSTINGS)    LOOK AT POSTINGS RETURNED                     
         USING ACPRTND,R3                                                       
         GOTO1 CLRAREA,DMCB,V(AORBIL1),V(AORBIL1X)                              
         L     R2,AAORBIL1                                                      
         ZIC   R0,PSTNUM2                                                       
DOAOR25  OC    0(ACPRTNL2,R3),0(R3)                                             
         BZ    DOAOR30                                                          
         MVC   0(ACPRTNL2,R2),0(R3)                                             
         LA    R2,ACPRTNL2(R2)                                                  
DOAOR30  LA    R3,ACPRTNL2(R3)                                                  
         BCT   R0,DOAOR25                                                       
*                                                                               
         L     R3,AMYAREA3        MOVE IN TRUE POSTINGS AFTER MAIN              
DOAOR35  OC    0(ACPRTNL2,R3),0(R3)                                             
         BZ    DOAOR40                                                          
         MVC   0(ACPRTNL2,R2),0(R3)                                             
         LA    R2,ACPRTNL2(R2)                                                  
         LA    R3,ACPRTNL2(R3)                                                  
         B     DOAOR35            NOW AORBIL1 HAS BILL1 + AOR BILL PSTS         
         DROP  R3                                                               
*                                                                               
DOAOR40  LA    R3,AORERRS                                                       
         USING AORERD,R3                                                        
         GOTO1 =A(CONTRAS),DMCB,(RC),AAORBIL1                                   
         MVC   AORCERR,CONERR                                                   
         DROP  R3                                                               
         ZIC   R0,NUMABIL                                                       
         CH    R0,=H'2'                                                         
         BNH   DOAOR50            ONLY MAIN & AOR BILL                          
         BAS   RE,DOOTH           DO THE OTHER AOR BILLS IN THE SET             
*                                                                               
DOAOR50  XC    KEY,KEY            RE-ESTABLISH READ SEQUENCE                    
         MVC   KEY(L'PBILLKEY),SKEY                                             
         GOTO1 DATAMGR,DMCB,DMRDHI,PRTDIR,KEY,KEY                               
         CLC   KEY(L'PBILLKEY),SKEY                                             
         BE    DOAORX                                                           
         DC    H'0'                                                             
         SPACE                                                                  
DOAORX   XIT1                                                                   
         EJECT                                                                  
* CREATE A FUDGED BILL OF ALL CLIENT X'10' BILLS                                
* SO AS TO GET CORRECT TRUE AOR POSTINGS                                        
GETPT    NTR1                                                                   
         L     R2,AMYAREA2                                                      
         USING PBILLREC,R2                                                      
         LA    R3,1               COUNTER FOR NUMBER OF BILLS                   
         MVI   FNDTRUE,C'N'                                                     
         XC    KEY,KEY                                                          
         MVC   KEY(L'PBILLKEY),SKEY                                             
         GOTO1 DATAMGR,DMCB,DMREAD,PRTDIR,KEY,KEY                               
         CLC   KEY(L'PBILLKEY),SKEY                                             
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 DATAMGR,DMCB,GETREC,PRTFILE,KEY+27,(R2),DMWORK                   
         BAS   RE,CPYBILL         COPY BILL FOR FUDGING                         
*                                                                               
GETPT05  GOTO1 DATAMGR,DMCB,DMRSEQ,PRTDIR,KEY,KEY                               
         CLC   KEY(PBILKBNO-PBILLKEY),SKEY                                      
         BNE   GETPT15            END OF SET - CHECK HAVE EVERYTHING            
GETPT08  GOTO1 DATAMGR,DMCB,GETREC,PRTFILE,KEY+27,(R2),DMWORK                   
         CLC   PBILLDAT,RUNDT     IF SET - RUN DATES MUST MATCH                 
         BNE   GETPT15                                                          
         TM    PBILCMSW,X'20'     TRUE AOR BILL?                                
         BNO   GETPT10                                                          
         GOTOR SVPTRUE,DMCB,AMYAREA2                                            
         MVI   FNDTRUE,C'Y'                                                     
         LA    R3,1(R3)                                                         
         B     GETPT05                                                          
*                                                                               
GETPT10  TM    PBILCMSW,X'10'     AOR BILL, AT LEAST?                           
         BNO   GETPT15            END OF AOR SET                                
         TM    PBILCMSW,X'08'                                                   
         BO    GETPT15                                                          
         CLI   PBILSEP,C'R'                                                     
         BE    GETPT15            BEGINNING OF NEW AOR SET                      
         BAS   RE,ADPBIL          ADD BILL TO CREATE FUDGED BILL                
         LA    R3,1(R3)                                                         
         B     GETPT05                                                          
*                                                                               
GETPT15  CLI   FNDTRUE,C'Y'       IF END OF SET - MUST HAVE FOUND               
         BE    *+6                TRUE AOR BILL                                 
         DC    H'0'                                                             
         CHI   R3,4                                                             
         BNH   *+6                                                              
         DC    H'0'               SHOULDN'T BE MORE THAN 4 IN A SET             
         STC   R3,NUMABIL         NUMBER OF AOR BILLS                           
         BAS   RE,DOTRUE          GET TRUE AOR POSTINGS                         
GETPTX   B     DOAORX                                                           
         DROP  R2                                                               
         EJECT                                                                  
* ADPBIL  - ADDS BILL AMOUNTS TO FUDGED BILL IN XTRAREA                         
*                                                                               
ADPBIL   NTR1                                                                   
         L     R2,AMYAREA2                                                      
         USING PBILLREC,R2                                                      
         ZAP   BILGRS,PBILLGRS                                                  
         ZAP   BILBIL,PBILLBIL                                                  
         ZAP   BILNET,PBILLNET                                                  
         ZAP   BILRCV,PBILLRCV                                                  
         XC    BILGST,BILGST                                                    
         XC    BILPST(10*L'BILPST),BILPST                                       
         LA    RF,BILPST                                                        
*                                                                               
         SR    R0,R0                                                            
         LA    R1,PBILLEL         R1=A(FIRST ELEMENT)                           
ADPBIL5  CLI   0(R1),0            TEST END OF RECORD                            
         BE    ADPBIL15                                                         
         CLI   0(R1),X'0A'        IF GST ELEMENT                                
         BNE   *+14                                                             
         USING PBILVEL,R1                                                       
         MVC   BILGST,PBILLVAT    SET GST AMOUNT                                
         B     ADPBIL8                                                          
         CLI   0(R1),X'84'        IF PST ELEMENT                                
         BNE   ADPBIL8                                                          
         USING PBLPSTEL,R1                                                      
         MVC   0(1,RF),PBLPVPRV   SET PST INFORMATION                           
         MVC   1(4,RF),PBLPVBAS                                                 
         MVC   5(4,RF),PBLPVAMT                                                 
         LA    RF,L'BILPST(RF)                                                  
*                                                                               
ADPBIL8  ICM   R0,1,1(R1)         BUMP TO NEXT ELEMENT IN RECORD                
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R1,R0                                                            
         B     ADPBIL5                                                          
         DROP  R1,R2                                                            
*                                                                               
ADPBIL15 LAY   R2,XTRAREA       R2=A(FUDGED BILL RECORD)                        
         USING PBILLREC,R2                                                      
         AP    PBILLGRS,BILGRS                                                  
         AP    PBILLBIL,BILBIL                                                  
         AP    PBILLNET,BILNET                                                  
         AP    PBILLRCV,BILRCV                                                  
*                                                                               
         LA    R1,PBILLEL        R1=A(FIRST ELEMENT)                            
ADPBIL20 CLI   0(R1),0           TEST END OF RECORD                             
         BE    ADPBILX                                                          
*                                                                               
         CLI   0(R1),X'0A'       IF GST ELEMENT                                 
         BNE   ADPBIL25                                                         
         USING PBILVEL,R1                                                       
         ICM   RF,15,PBILLVAT    GST AMOUNT                                     
         ICM   RE,15,BILGST                                                     
         AR    RE,RF                                                            
         STCM  RE,15,PBILLVAT                                                   
         B     ADPBIL30                                                         
*                                                                               
ADPBIL25 CLI   0(R1),X'84'        IF PST ELEMENT                                
         BNE   ADPBIL30                                                         
         USING PBLPSTEL,R1                                                      
         LA    RF,BILPST                                                        
         LA    R0,ACPMCODE                                                      
ADPBIL27 CLC   0(1,RF),PBLPVPRV   SET PST INFORMATION                           
         BE    ADPBIL28                                                         
         LA    RF,L'BILPST(RF)                                                  
         BCT   R0,ADPBIL27                                                      
         DC    H'0'                                                             
ADPBIL28 ICM   RE,15,PBLPVBAS     ADD TO BASIS                                  
         ICM   R3,15,1(RF)                                                      
         AR    RE,R3                                                            
         STCM  RE,15,PBLPVBAS                                                   
         ICM   RE,15,PBLPVAMT     ADD TO AMOUNT                                 
         ICM   R3,15,5(RF)                                                      
         AR    RE,R3                                                            
         STCM  RE,15,PBLPVAMT                                                   
*                                                                               
ADPBIL30 SR    R0,R0                                                            
         ICM   R0,1,1(R1)         BUMP TO NEXT ELEMENT IN RECORD                
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R1,R0                                                            
         B     ADPBIL20                                                         
*                                                                               
ADPBILX  B     DOAORX                                                           
         DROP  R2,R1                                                            
         EJECT                                                                  
* COPIES MAIN BILL TO XTRAREA                                                   
*                                                                               
CPYBILL  NTR1                                                                   
         LAY   R0,XTRAREA                                                       
         L     RE,AMYAREA2                                                      
         LA    R1,1000            MAX BILL RECORD LENGTH                        
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         B     DOAORX                                                           
         SPACE                                                                  
* CALLS ACPOSTER FOR TRUE AOR POSTINGS, THEN RESETS ACCOUNTS,IOAREAS            
* FOR MAIN BILL CALL TO ACPOSTER                                                
*                                                                               
DOTRUE   NTR1                                                                   
         LA    R4,AORERRS                                                       
         USING AORERD,R4                                                        
         LA    R4,AORERRL(R4)     SAVE TRUE AOR ERRORS                          
         GOTO1 =A(RDPOST),DMCB,(RC)                                             
         LA    R3,ACPBLK                                                        
         USING ACPOSTD,R3                                                       
         LAY   RE,XTRAREA         RESET ADDRESS OF BILL                         
         ST    RE,ACPBILL                                                       
         MVC   ACPNUM2,PSTNUM2    MAX NUMBER OF POSTINGS                        
         MVI   ACPAFLG,C'T'       WANT TRUE AOR POSTINGS                        
         GOTO1 =A(POSTCALL),DMCB,(RC)                                           
         MVC   AORERR,ACPERR                                                    
         MVC   AORERRC,ACPERRC                                                  
         DROP  R3                                                               
         L     R3,=V(POSTINGS)    LOOK AT POSTINGS RETURNED                     
         USING ACPRTND,R3                                                       
         GOTO1 CLRAREA,DMCB,V(MYAREA3),V(MYAREA3X)                              
         L     R2,AMYAREA3                                                      
         ZIC   R0,PSTNUM2                                                       
DOTRU25  OC    0(ACPRTNL2,R3),0(R3)                                             
         BZ    DOTRU30                                                          
         MVC   0(ACPRTNL2,R2),0(R3)                                             
         LA    R2,ACPRTNL2(R2)                                                  
DOTRU30  LA    R3,ACPRTNL2(R3)                                                  
         BCT   R0,DOTRU25                                                       
         B     DOAORX                                                           
         DROP  R3                                                               
         EJECT                                                                  
* DOOTH - MORE THAN TWO AOR BILLS IN SET - SAVE THERE POSTINGS                  
*         IN AAORBIL2/AAORBIL3                                                  
DOOTH    NTR1                                                                   
         GOTO1 CLRAREA,DMCB,V(AORBIL2),V(AORBIL2X)                              
         GOTO1 CLRAREA,DMCB,V(AORBIL3),V(AORBIL3X)                              
         XC    KEY,KEY            START FROM BEGINNING OF SET                   
         MVC   KEY(L'PBILLKEY),SKEY                                             
         L     R2,AAORBIL2        A(NEXT IOAREA TO SAVE POSTINGS)               
         MVC   FULL2,AAORBIL2                                                   
         LA    R4,AORERRS                                                       
         LA    R4,AORERRL(R4)                                                   
         LA    R4,AORERRL(R4)                                                   
         GOTO1 DATAMGR,DMCB,DMRDHI,PRTDIR,KEY,KEY                               
         CLC   KEY(L'PBILLKEY),SKEY                                             
         BE    *+6                                                              
         DC    H'0'                                                             
DOOTH05  GOTO1 DATAMGR,DMCB,DMRSEQ,PRTDIR,KEY,KEY                               
         CLC   KEY(PBILKBNO-PBILLKEY),SKEY                                      
         BNE   DOOTH40            END OF SET - CHECK HAVE EVERYTHING            
         GOTO1 DATAMGR,DMCB,GETREC,PRTFILE,KEY+27,AMYAREA2,DMWORK               
         L     R1,AMYAREA2                                                      
         USING PBILLREC,R1                                                      
         CLC   PBILLDAT,RUNDT     IF SET - RUN DATES MUST MATCH                 
         BNE   DOOTH40                                                          
         TM    PBILCMSW,X'20'                                                   
         BO    DOOTH05            SKIP TRUE AOR BILL                            
         TM    PBILCMSW,X'10'     AOR BILL, AT LEAST?                           
         BNO   DOOTH40            END OF AOR SET                                
         TM    PBILCMSW,X'08'                                                   
         BO    DOOTH40                                                          
         CLI   PBILSEP,C'R'                                                     
         BE    DOOTH40            BEGINNING OF NEW AOR SET                      
         DROP  R1                                                               
         GOTO1 CLRAREA,DMCB,V(IOAREA),V(IOAREAX)                                
         MVC   FULL,AMYAREA2                                                    
         BAS   RE,GETAOR                                                        
         LA    R3,ACPBLK                                                        
         USING ACPOSTD,R3                                                       
         GOTO1 =A(RDPOST),DMCB,(RC)                                             
         GOTO1 =A(SETVATO),DMCB,(RC),AMYAREA2                                   
         MVC   ACPPSTO(10*L'ACPPSTO),PSTOVALS                                   
         MVC   ACPBILL,AMYAREA2                                                 
         MVI   ACPAFLG,C'M'       WANT MAIN BILL POSTINGS                       
         GOTO1 =A(POSTCALL),DMCB,(RC)                                           
         MVC   AORERR,ACPERR                                                    
         MVC   AORERRC,ACPERRC                                                  
         DROP  R3                                                               
         L     R3,=V(POSTINGS)    LOOK AT POSTINGS RETURNED                     
         USING ACPRTND,R3                                                       
         ZIC   R0,PSTNUM2                                                       
DOOTH25  OC    0(ACPRTNL2,R3),0(R3)                                             
         BZ    DOOTH30                                                          
         MVC   0(ACPRTNL2,R2),0(R3)                                             
         LA    R2,ACPRTNL2(R2)                                                  
DOOTH30  LA    R3,ACPRTNL2(R3)                                                  
         BCT   R0,DOOTH25                                                       
         GOTO1 =A(CONTRAS),DMCB,(RC),FULL2                                      
         MVC   AORCERR,CONERR                                                   
         ZIC   R0,NUMABIL                                                       
         CHI   R0,3                                                             
         BE    DOOTHX                                                           
         L     R2,AAORBIL3        PT TO NEXT AREA FOR POSTINGS                  
         MVC   FULL2,AAORBIL3                                                   
         LA    R4,AORERRL(R4)                                                   
         L     RE,AMYAREA2                                                      
         XC    KEY,KEY                                                          
         MVC   KEY(L'PBILLKEY),0(RE)                                            
         GOTO1 DATAMGR,DMCB,DMRDHI,PRTDIR,KEY,KEY                               
         B     DOOTH05                                                          
*                                                                               
DOOTH40  DS    0H                                                               
DOOTHX   B     DOAORX                                                           
         EJECT                                                                  
*=========================================*                                     
* GETAOR - READS AOR SFM RECORD FOR       *                                     
*          DEFAULT ACCOUNT NAMES          *                                     
*=========================================*                                     
*                                                                               
GETAOR   NTR1                                                                   
         XC    KEY,KEY                                                          
         LA    R2,KEY                                                           
         USING AORREC,R2                                                        
         L     R4,FULL                                                          
         USING PBILLREC,R4                                                      
         MVC   AORKAGY,PBILKAGY   AGENCY                                        
         MVC   AORKMED,PBILKMED   MEDIA                                         
         MVI   AORKRCD,X'14'      RECORD ID                                     
         MVC   AORKCLT,PBILKCLT   CLIENT                                        
         MVC   AORKPRD,PBILKPRD   PRODUCT                                       
         MVC   AORKEST,PBILKEST   ESTIMATE NUMBER                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,DMRDHI,PRTDIR,KEY,KEY                               
         CLC   KEY(L'AORKEY),KEYSAVE                                            
         BE    GETAOR10                                                         
*                                                                               
         XC    KEY,KEY                                                          
         MVC   AORKAGY,PBILKAGY   AGENCY                                        
         MVC   AORKMED,PBILKMED   MEDIA                                         
         MVI   AORKRCD,X'14'      RECORD ID                                     
         MVC   AORKCLT,PBILKCLT   CLIENT                                        
         MVC   AORKPRD,PBILKPRD   PRODUCT                                       
         MVC   AORKEST,=X'FFFF'   READ DEFAULT RECORD                           
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,DMRDHI,PRTDIR,KEY,KEY                               
         CLC   KEY(L'AORKEY),KEYSAVE                                            
         BNE   GETAORX                                                          
         DROP  R2                                                               
GETAOR10 GOTO1 DATAMGR,DMCB,GETREC,PRTFILE,KEY+27,AMYAREA,DMWORK                
         L     R2,AMYAREA                                                       
         LA    R2,33(R2)                                                        
GETAOR30 CLI   0(R2),0                                                          
         BE    GETAORX                                                          
         CLI   0(R2),X'03'        AOR INFO ELEM                                 
         BE    GETAOR40                                                         
         SR    R0,R0                                                            
         ICM   R0,1,1(R2)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R2,R0                                                            
         B     GETAOR30                                                         
*                                                                               
         USING AORELEM,R2                                                       
         USING ACPRTND,R3                                                       
GETAOR40 CLC   AORRCVBL,SPACES    ACCT MUST BE THERE                            
         BNH   GETAORX                                                          
         MVI   BYTE,MBTTARI       AOR INCOME ROW                                
         BAS   RE,GETROW          PTS R3 TO CORRECT ROW                         
         MVC   ACPBTYP,BYTE       SET TYPE                                      
         MVC   ACPLVL,=C'AOR'     LEVEL                                         
         OI    ACPSTAT,ACPCR      CREDIT                                        
         MVC   ACPACC,AORCOMM     MOVE IN SFM INCOME ACCOUNT                    
         XC    WORK,WORK                                                        
         MVC   WORK(L'AORCOMM),AORCOMM                                          
*                                                                               
         MVI   BYTE,MBTTAPR       AOR PAY/RECEIVABLE ROW                        
         BAS   RE,GETROW          PTS R3 TO CORRECT ROW                         
         MVC   ACPBTYP,BYTE       SET TYPE                                      
         MVC   ACPLVL,=C'AOR'     LEVEL                                         
         MVC   ACPACC,AORRCVBL    MOVE IN SFM RECEIVABLE ROW                    
         CLC   ACPACC(2),=C'SR'                                                 
         BNE   GETAOR42                                                         
         OI    ACPSTAT,ACPDEB     RCVBL IS DEBIT                                
         B     GETAOR45                                                         
GETAOR42 OI    ACPSTAT,ACPCR      CREDIT                                        
         DROP  R2                                                               
GETAOR45 OC    WORK,WORK                                                        
         BZ    GETAORX                                                          
         LA    R2,AKEY                                                          
         USING ACTRECD,R2                                                       
         MVC   ACTKEY,SPACES                                                    
         MVC   ACTKCPY,ACCTCD2    OTHER COMPANY CODE (IF APPL)                  
         MVC   ACTKUNT(14),WORK                                                 
         DROP  R2                                                               
         L     R2,AMYAREA                                                       
         ST    R2,AREC                                                          
         MVI   RDSE,2             READ FROM OTHER ACC FILE                      
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMREAD'                               
         BNE   GETAORX                                                          
         LA    R1,ACCORFST                                                      
         AR    R2,R1                                                            
         USING ABLELD,R2                                                        
GETAOR50 CLI   ABLEL,0                                                          
         BE    GETAORX            ERROR IN AOR REV ACCOUNT                      
         CLI   ABLEL,ABLELQ       BALANCE ELEMENT                               
         BE    GETAOR70                                                         
         SR    R0,R0                                                            
         ICM   R0,1,ABLLN                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R2,R0                                                            
         B     GETAOR50                                                         
*                                                                               
GETAOR70 BAS   RE,GETFILT         GET ANALYSIS OR UNIT FILTER                   
GETAORX  B     DOAORX                                                           
         DROP  R2,R4                                                            
         EJECT                                                                  
*==================================================================*            
* GETFILT  - LOOKS UP 12 CHAR FROM UNIT1= FROM SPECIAL POSTING ELE *            
*            IF NOT FOUND - USES ANALYSIS = AS DEFAULT             *            
*==================================================================*            
*                                                                               
GETFILT  NTR1                                                                   
         L     R2,AMYAREA                                                       
         LA    R1,ACCORFST                                                      
         AR    R2,R1                                                            
         USING SPAELD,R2                                                        
GETUF3   CLI   0(R2),0            END OF RECORD                                 
         BE    GETAF0                                                           
         CLI   0(R2),SPAELQ       SPECIAL POSTING A/C ELEMENT                   
         BE    GETUF5                                                           
         SR    R0,R0                                                            
         ICM   R0,1,1(R2)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R2,R0                                                            
         B     GETUF3                                                           
*                                                                               
GETUF5   CLI   SPATYPE,SPATANAL   ANALYSIS ACCOUNT?                             
         BNE   GETAF0                                                           
*                                                                               
         MVI   BYTE,MBTTARR         AOR REVENUE ROW                             
         BAS   RE,GETROW            PT R3 TO CORRECT ROW                        
         MVC   ACPBTYP,BYTE                                                     
         MVC   ACPACC,SPACES                                                    
         MVC   ACPACC(2),=C'12'                                                 
         MVC   ACPACC+2(L'SPAAANAL),SPAAANAL                                    
         MVC   ACPLVL,=C'AOR'     LEVEL                                         
         OI    ACPSTAT,ACPCR      CREDIT                                        
*                                                                               
         MVI   BYTE,MBTTABL         AOR BILLING ROW                             
         BAS   RE,GETROW            PT R3 TO CORRECT ROW                        
         MVC   ACPBTYP,BYTE                                                     
         MVC   ACPACC,SPACES                                                    
         MVC   ACPACC(2),=C'11'                                                 
         MVC   ACPACC+2(L'SPAAANAL),SPAAANAL                                    
         MVC   ACPLVL,=C'AOR'     LEVEL                                         
         OI    ACPSTAT,ACPCR      CREDIT                                        
         B     GETFX                                                            
         DROP  R2                                                               
*                                                                               
GETAF0   L     R2,AMYAREA                                                       
         LA    R1,ACCORFST                                                      
         AR    R2,R1                                                            
         USING RSTELD,R2                                                        
GETAF3   CLI   0(R2),0            END OF RECORD                                 
         BE    GETFX                                                            
*                                                                               
GETAF4   CLI   0(R2),RSTELQ       RECORD STATUS ELE?                            
         BE    GETAF5                                                           
         SR    R0,R0                                                            
         ICM   R0,1,1(R2)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R2,R0                                                            
         B     GETAF3                                                           
*                                                                               
GETAF5   CLI   RSTCOSTG,0                                                       
         BE    GETFX                                                            
*                                                                               
         MVI   BYTE,MBTTARR       AOR REVENUE ROW                               
         BAS   RE,GETROW          PT R2 TO CORRECT ROW                          
         MVC   ACPBTYP,BYTE                                                     
         MVC   ACPACC,SPACES                                                    
         MVC   ACPACC(2),=C'12'                                                 
         MVC   ACPACC+2(1),RSTCOSTG                                             
         MVC   ACPLVL,=C'AOR'     LEVEL                                         
         OI    ACPSTAT,ACPCR      CREDIT                                        
*                                                                               
         MVI   BYTE,MBTTABL         AOR BILLING ROW                             
         BAS   RE,GETROW            PT R2 TO CORRECT ROW                        
         MVC   ACPBTYP,BYTE                                                     
         MVC   ACPACC,SPACES                                                    
         MVC   ACPACC(2),=C'11'                                                 
         MVC   ACPACC+2(1),RSTCOSTG                                             
         MVC   ACPLVL,=C'AOR'     LEVEL                                         
         OI    ACPSTAT,ACPCR      CREDIT                                        
GETFX    B     DOAORX                                                           
         DROP  R3,R2                                                            
         SPACE 2                                                                
*------------------------------------------*                                    
* GETROW -  GIVEN ROW NUMBER WILL SET R2   *                                    
*           TO POINT TO CORRESPONDING ROW  *                                    
*      NTRY - ROW NUMBER IN BYTE           *                                    
*      XIT  - SETS R3                      *                                    
*------------------------------------------*                                    
*                                                                               
GETROW   NTR1                                                                   
         ZIC   R1,BYTE                                                          
         BCTR  R1,0                                                             
         SR    RE,RE                                                            
         LA    RF,ACPRTNL         LENGTH OF ONE ROW                             
         MR    RE,R1              NUMBER OF ROWS                                
         L     R3,=V(IOAREA)      AREA CONTAINING ALL ROWS                      
         AR    R3,RF                                                            
         XIT1  REGS=(R3)          R3 => ROW REQUESTED                           
         EJECT                                                                  
*=============*                                                                 
* LITERAL POOL*                                                                 
*=============*                                                                 
*                                                                               
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*---------------------------------------------*                                 
* CONTRAS - TRYS TO FIND CONTRA-ACCOUNT & ACC *                                 
*           NAME FOR EACH ACCOUNT IN ACPPOST2 *                                 
*  XIT     - CREATES YET ANOTHER TABLE - CONTBL                                 
*            WHICH HAS THE ELE TYPE FOLLOWED  *                                 
*            BY CONTRA/CONTRA NAME            *                                 
*---------------------------------------------*                                 
*                                                                               
         DS    0D                                                               
CONTRAS  NMOD1 0,**CNTR**                                                       
         L     RC,0(R1)                                                         
         USING PPMXWRKD,RC        LOCAL WORKING STORAGE                         
         L     R3,4(R1)                                                         
         MVC   APOST,4(R1)        SAVE FOR GETACC                               
         USING ACPRTND,R3                                                       
*                                                                               
         MVI   CONERR,0                                                         
         GOTO1 CLRAREA,DMCB,V(CONTBL),V(CONTBLX)                                
         ZIC   R2,PSTNUM2                                                       
CONTRA5  OC    0(ACPRTNL2,R3),0(R3)                                             
         BZ    CONTRA99                                                         
         MVI   CHKCON,C'N'                                                      
         MVC   CONTRA,SPACES      CLEAR IT OUT                                  
         MVC   CONTRAN,SPACES     CLEAR IT OUT                                  
         SPACE 1                                                                
         CLI   ACPERR2,0          IF ACCOUNT ERROR ALREADY                      
         BNE   CONTRA99           NO POINT IN LOOKING FOR CONTRA ACC            
*                                                                               
         CLI   0(R3),MBTTRCV      RECEIVABLE ACCOUNT?                           
         BE    *+12                                                             
         CLI   0(R3),MBTTIRCV     RECEIVABLE ACCOUNT(MIDAS)?                    
         BNE   CONTRA8                                                          
*                                                                               
         BAS   RE,RCVCONTR        GET CONTRA TO RECEIVABLES ACCOUNT             
         B     CONTRA95                                                         
*                                                                               
CONTRA8  CLI   0(R3),MBTTINC      INCOME ACCOUNT?                               
         BE    CONTRA9                                                          
         CLI   0(R3),MBTTARI      AOR INCOME ACCOUNT                            
         BE    CONTRA9                                                          
         CLI   0(R3),MBTTSEL      SELLOFF INCOME ACCOUNT                        
         BE    CONTRA9                                                          
         CLI   0(R3),MBTTINTI     INTERNAL INCOME ACCOUNT                       
         BE    CONTRA9                                                          
         CLI   0(R3),MBTTOINC     OTHER INCOME (GROUPM MIDAS TRADE)             
         BE    CONTRA9                                                          
         CLI   0(R3),MBTTOPIN     OPCO INCOME                                   
         BE    CONTRA9                                                          
         CLI   0(R3),MBTTCD       CASH DISC ACCOUNT?                            
         BNE   CONTRA15                                                         
CONTRA9  BAS   RE,INCCONTR        GET CONTRA TO ALL SI ACCOUNTS                 
         B     CONTRA95                                                         
*                                                                               
CONTRA15 CLI   0(R3),MBTTNET      NET ACCOUNT?                                  
         BNE   CONTRA20                                                         
         BAS   RE,NETCONTR        GET CONTRA TO NET ACCOUNT                     
         B     CONTRA95                                                         
*                                                                               
CONTRA20 CLI   0(R3),MBTTCOS      COSTING ACCOUNT?                              
         BE    *+12                                                             
         CLI   0(R3),MBTTINTC                                                   
         BNE   CONTRA30                                                         
         BAS   RE,COSCONTR        GET CONTRA TO COSTING ACCOUNT                 
         B     CONTRA95                                                         
*                                                                               
CONTRA30 CLI   0(R3),MBTTBIL      BILLING ACCOUNT                               
         BE    CONTRA40                                                         
         CLI   0(R3),MBTTREV      REVENUE ACCOUNT                               
         BE    CONTRA40                                                         
         CLI   0(R3),MBTTABL      AOR BILLING                                   
         BE    CONTRA40                                                         
         CLI   0(R3),MBTTARR      AOR REVENUE                                   
         BE    CONTRA40                                                         
         CLI   0(R3),MBTTSBL      SELLOFF BILLING                               
         BE    CONTRA40                                                         
         CLI   0(R3),MBTTOBIL     OTHER BILLING (GROUPM MIDAS TRADE)            
         BE    CONTRA40                                                         
         CLI   0(R3),MBTTOREV     OTHER REVENUE (GROUPM MIDAS TRADE)            
         BE    CONTRA40                                                         
         CLI   0(R3),MBTTOPBL     OPCO BILLING (GROUPM MIDAS TRADE)             
         BE    CONTRA40                                                         
         CLI   0(R3),MBTTOPRV     OPCO REVENUE (GROUPM MIDAS TRADE)             
         BE    CONTRA40                                                         
         CLI   0(R3),MBTTSRV      SELLOFF REVENUE                               
         BNE   CONTRA50                                                         
CONTRA40 BAS   RE,ROBCONTR        GET CONTRA FOR ACCOUNT                        
         B     CONTRA95                                                         
*                                                                               
CONTRA50 CLI   0(R3),MBTTGST     IF GST OUTPUT POSTING ACCOUNT                  
         BE    CONTRA55                                                         
         CLI   0(R3),MBTTPQO     OR IF PST PQ OUTPUT ACCOUNT                    
         BE    CONTRA55                                                         
         CLI   0(R3),MBTTNBO     OR IF PST NB OUTPUT ACCOUNT                    
         BE    CONTRA55                                                         
         CLI   0(R3),MBTTNSO     OR IF PST NS OUTPUT ACCOUNT                    
         BE    CONTRA55                                                         
         CLI   0(R3),MBTTBCO     OR IF PST BC OUTPUT ACCOUNT                    
         BE    CONTRA55                                                         
         CLI   0(R3),MBTTONO     OR IF PST ON OUTPUT ACCOUNT                    
         BE    CONTRA55                                                         
         CLI   0(R3),MBTTPEO     OR IF PST PE OUTPUT ACCOUNT                    
         BE    CONTRA55                                                         
         CLI   0(R3),MBTTNFO     OR IF PST NF OUTPUT ACCOUNT                    
         BNE   CONTRA65                                                         
CONTRA55 BAS   RE,VATCONTR       GET CONTRA FOR OUTPUT GST/PST ACCOUNT          
         B     CONTRA95                                                         
*                                                                               
CONTRA65 CLI   0(R3),MBTTGSTI    GST INPUT POSTING ACCOUNT                      
         BE    CONTRA68                                                         
         CLI   0(R3),MBTTPQI     OR IF PST PQ INPUT ACCOUNT                     
         BE    CONTRA68                                                         
         CLI   0(R3),MBTTNBI     OR IF PST NB INPUT ACCOUNT                     
         BE    CONTRA68                                                         
         CLI   0(R3),MBTTNSI     OR IF PST NS INPUT ACCOUNT                     
         BE    CONTRA68                                                         
         CLI   0(R3),MBTTBCI     OR IF PST BC INPUT ACCOUNT                     
         BE    CONTRA68                                                         
         CLI   0(R3),MBTTONI     OR IF PST ON INPUT ACCOUNT                     
         BE    CONTRA68                                                         
         CLI   0(R3),MBTTPEI     OR IF PST PE INPUT ACCOUNT                     
         BE    CONTRA68                                                         
         CLI   0(R3),MBTTNFI     OR IF PST NF INPUT ACCOUNT                     
         BNE   CONTRA70                                                         
CONTRA68 BAS   RE,VATICNTR       GET CONTRA FOR INPUT GST/PST ACCOUNT           
         B     CONTRA95                                                         
*                                                                               
CONTRA70 CLI   0(R3),MBTTAPR     AOR PAY/RCVBL ACCOUNT?                         
         BE    *+12                                                             
         CLI   0(R3),MBTTIPBL    INTERCOMPANY PAYABLE (MIDAS)                   
         BNE   CONTRA80                                                         
         BAS   RE,APYCONTR       GET CONTRA FOR AOR PAY/RCV ACCOUNT             
         B     CONTRA95                                                         
*                                                                               
CONTRA80 CLI   0(R3),MBTTRCS      SUSPENSE ACCOUNT                              
         BE    CONTRA82                                                         
         CLI   0(R3),MBTTRCR      REBATE ACCOUNT                                
         BNE   CONTRA85                                                         
CONTRA82 BAS   RE,SORCONTR        GET SUSPENSE OR REBATE CONTRA                 
         B     CONTRA95                                                         
*                                                                               
CONTRA85 CLI   0(R3),MBTTINTB     INTERNAL BILLING                              
         BE    *+12                                                             
         CLI   0(R3),MBTTINTR     INTERNAL REVENUE                              
         BNE   CONTRA95                                                         
         BAS   RE,INTCONTR         GET INTERNAL COSTING ACCOUNT                 
         B     CONTRA95                                                         
*                                                                               
CONTRA95 CLC   CONTRA,SPACES                                                    
         BNH   CONTRA97                                                         
         CLI   CHKCON,C'Y'        RE-READ CONTRA ACCOUNT?                       
         BNE   *+12                                                             
         BAS   RE,CKCONACC                                                      
         BNE   CONTRA97           CONTRA ACCOUNT ERROR                          
*                                                                               
         ZIC   RE,PSTNUM2                                                       
         L     R1,=V(CONTBL)                                                    
         USING CONTBLD,R1                                                       
CONTRA96 OC    0(CONTBLL,R1),0(R1)                                              
         BZ    *+14                                                             
         LA    R1,CONTBLL(R1)                                                   
         BCT   RE,CONTRA96                                                      
         DC    H'0'                                                             
         MVC   CONTYPE,0(R3)                                                    
         MVC   CONTYPE2,ACPWHC                                                  
         MVC   CONACC,CONTRA                                                    
         MVC   CONNAME,CONTRAN                                                  
         B     CONTRA99                                                         
         DROP  R1                                                               
CONTRA97 MVI   ACPBCHG,C'E'       INDICATE ERROR WITH CONTRA ACCOUNT            
         MVI   CONERR,1                                                         
CONTRA99 LA    R3,ACPRTNL2(R3)                                                  
         BCT   R2,CONTRA5                                                       
*                                                                               
CONTRAX  XIT1                                                                   
         EJECT                                                                  
*-----------------------------------------------------------*                   
* RCVCONTR - SETS THE CONTRA ACCOUNT & THE CONTRA ACC NAME  *                   
*            FOR THE RECEIVABLES POSTING                    *                   
*-----------------------------------------------------------*                   
*                                                                               
RCVCONTR NTR1                                                                   
         LA    R1,DMCB                                                          
         XC    0(24,R1),0(R1)                                                   
         MVC   DMCB+7(1),0(R3)                                                  
         GOTO1 =A(GETACC),DMCB,(RC)                                             
         CLC   CONTRA+1(2),=C'SJ' IF ACCOUNT IS SJ (NOT SR)                     
         BNE   *+12                                                             
         BAS   RE,SJCONTRA        SET CONTRA & CONTRAN FOR SJ                   
         B     RCVCONX                                                          
*                                                                               
         BAS   RE,GETRCV          SET CONTRA, CONTRAN & SVCNTRA                 
RCVCONX  XIT1                                                                   
         SPACE                                                                  
* GETRCV - GET RECEIVABLE CONTRA AND SAVE IT                                    
*                                                                               
GETRCV   NTR1                                                                   
         LA    R2,ACPBLK                                                        
         USING ACPOSTD,R2                                                       
         GOTO1 MXPROF,DMCB,MTPFBILS,PROFWORK                                    
         OC    PROFWORK,PROFWORK   ANY BILL SOURCE/MEDIA NAME ?                 
         BZ    GETRCV2             NO OVERRIDE                                  
         MVC   CONTRA,SPACES                                                    
         MVC   CONTRA+3(12),PROFWORK                                            
         OC    CONTRA+1(14),SPACES                                              
         MVC   CONTRAN,SPACES                                                   
         B     GETRCVX                                                          
*                                                                               
GETRCV2  CLI   ACPMI,C'Y'           ON MI RECORDS?                              
         BNE   GETRCV3                                                          
         GOTO1 =A(GETACC),DMCB,(RC),MBTTINC  SET CONTRA = INC ACC               
         BNE   GETRCV2B                                                         
         CLC   CONLVL(2),=C'MI'     ONLY IF INCOME OVERRIDE                     
         BE    GETRCV5                                                          
         B     GETRCV4                                                          
GETRCV2B GOTO1 =A(GETACC),DMCB,(RC),MBTTARI  SET CONTRA = AOR INC ACC           
         BNE   GETRCV5                                                          
         CLC   CONLVL(2),=C'MI'     ONLY IF AOR INCOME OVERRIDE                 
         BE    GETRCV5                                                          
         B     GETRCV4                                                          
*                                   NOT ON MI RECORDS                           
GETRCV3  GOTO1 =A(GETACC),DMCB,(RC),MBTTINC SET CONTRA=INC ACC                  
         BE    GETRCV4                                                          
         GOTO1 =A(GETACC),DMCB,(RC),MBTTARI SET CONTRA=AOR INC                  
GETRCV4  MVC   CONTRA,SPACES                                                    
         MVC   CONTRA+3(12),CONTRAN  CMP CODE,UNIT,LEDGER STAY BLANK            
         MVC   CONTRAN,SPACES        CONTRA ACC NAME = SPACES                   
         B     GETRCVX                                                          
*                                                                               
GETRCV5  BAS   RE,RDMI            YES - SET CONTRA = MI DESCRIPTION             
GETRCVX  MVC   SVSRCNTR,CONTRA    SAVE CONTRA & CONTRAN                         
         XIT1                                                                   
         DROP  R2                                                               
         SPACE                                                                  
* SJCONTRA - SETS THE CONTRA ACCOUNT & THE CONTRA ACC NAME  FOR SJ              
*                                                                               
SJCONTRA NTR1                                                                   
         GOTO1 =A(GETACC),DMCB,(RC),MBTTINC SET CONTRA=INC ACC                  
         BE    SJCONTRX                                                         
         GOTO1 =A(GETACC),DMCB,(RC),MBTTARI SET CONTRA=AOR INC                  
SJCONTRX XIT1                                                                   
         EJECT                                                                  
*-----------------------------------------------------------*                   
* INCCONTR - SETS THE CONTRA ACCOUNT & THE CONTRA ACC NAME  *                   
*            FOR THE SI POSTINGS                            *                   
*-----------------------------------------------------------*                   
*                                                                               
INCCONTR NTR1                                                                   
         LA    R2,ACPBLK                                                        
         USING ACPOSTD,R2                                                       
         OC    INCRIDE,INCRIDE    OVERRIDE INCOME ACCOUNT & NAME                
         BZ    INCCON20                                                         
         CLC   OVRCLT(6),SPACES   IF ANY OVERRIDE CLI/PRD                       
         BNE   INCCON20           THAT TAKES PRECEDENCE                         
         MVC   CONTRA,INCRIDE                                                   
         MVC   CONTRAN,INCRIDE+15                                               
         B     INCCONX                                                          
*                                                                               
INCCON20 MVC   CONTRA(1),ACCTCD2  OTHER COMPANY CODE IF APPL                    
         MVC   CONTRA+1(2),SVPROD CONTRA ACC IS UNIT/LEDGER                     
         MVC   CONTRA+3(3),ACPCLT CLIENT                                        
         CLC   OVRCLT,SPACES                                                    
         BE    *+10                                                             
         MVC   CONTRA+3(3),OVRCLT OVERRIDE CLIENT                               
         MVC   CONTRA+6(3),ACPPRD PRODUCT                                       
         CLC   OVRPRD,SPACES                                                    
         BE    *+10                                                             
         MVC   CONTRA+6(3),OVRPRD OVERRIDE PRODUCT                              
*                                                                               
         CLC   OVRCLT(6),SPACES   IF ANY OVERRIDE CLI/PRD                       
         BE    *+8                                                              
         MVI   CHKCON,C'Y'        MUST RE-READ CONTRA RECORD                    
         MVC   CONTRAN,SJPRDNM    SET CONTRAN                                   
INCCONX  B     CONTRAX                                                          
         DROP  R2                                                               
         EJECT                                                                  
*-----------------------------------------------------------*                   
* NETCONTR - SETS THE CONTRA ACCOUNT & THE CONTRA ACC NAME  *                   
*            FOR THE NET POSTING                            *                   
*-----------------------------------------------------------*                   
*                                                                               
NETCONTR NTR1                                                                   
         LA    R2,ACPBLK                                                        
         USING ACPOSTD,R2                                                       
         MVC   CONTRA(1),ACCTCD2  OTHER COMPANY CODE IF APPL                    
         MVC   CONTRA+1(2),SVPROD CONTRA ACC IS UNIT/LEDGER                     
         MVC   CONTRA+3(3),ACPCLT CLIENT                                        
         CLC   OVRCLT,SPACES                                                    
         BE    *+14                                                             
         MVC   CONTRA+3(3),OVRCLT OVERRIDE CLIENT                               
         MVI   CHKCON,C'Y'        MUST RE-READ CONTRA ACCOUNT                   
*                                                                               
         MVC   CONTRAN,SJCLTNM    SET CONTRAN                                   
NETCONX  B     CONTRAX                                                          
         DROP  R2                                                               
         SPACE 2                                                                
*-----------------------------------------------------------*                   
* COSCONTR - SETS THE CONTRA ACCOUNT & THE CONTRA ACC NAME  *                   
*            FOR THE COSTING POSTINGS                       *                   
*-----------------------------------------------------------*                   
*                                                                               
COSCONTR NTR1                                                                   
         LA    R2,ACPBLK                                                        
         USING ACPOSTD,R2                                                       
         LA    R1,DMCB                                                          
         XC    0(24,R1),0(R1)                                                   
         MVC   DMCB+7(1),ACPWHC                                                 
         GOTO1 =A(GETACC),DMCB,(RC)   SETS CONTRA & CONTRAN                     
COSCONX  B     CONTRAX                                                          
         DROP  R2                                                               
         EJECT                                                                  
*-----------------------------------------------------------*                   
* ROBCONTR - SETS THE CONTRA ACCOUNT & THE CONTRA ACC NAME  *                   
*            FOR THE REVENUES OR BILLINGS POSTING           *                   
*-----------------------------------------------------------*                   
*                                                                               
ROBCONTR NTR1                                                                   
         GOTO1 =A(GETACC),DMCB,(RC),MBTTCOS COSTING ROW NUMBER                  
ROBCONX  B     CONTRAX                                                          
         SPACE                                                                  
*-----------------------------------------------------------*                   
* INTCONTR - SETS THE CONTRA ACCOUNT & THE CONTRA ACC NAME  *                   
*            FOR THE INTERNAL REVENUE/BILLING POSTING       *                   
*-----------------------------------------------------------*                   
*                                                                               
INTCONTR NTR1                                                                   
         GOTO1 =A(GETACC),DMCB,(RC),MBTTINTC    INTERNAL COSTING ROW #          
         XIT1                                                                   
         SPACE                                                                  
*-----------------------------------------------------------*                   
* VATCONTR - SETS THE CONTRA ACCOUNT & THE CONTRA ACC NAME  *                   
*            FOR THE GST AND PST POSTINGS                   *                   
*-----------------------------------------------------------*                   
VATCONTR NTR1                                                                   
         LA    R2,ACPBLK                                                        
         USING ACPOSTD,R2                                                       
         MVC   CONTRA(1),ACCTCD2  OTHER COMPANY CODE IF APPL                    
         MVC   CONTRA+1(2),SVPROD CONTRA ACC IS UNIT/LEDGER                     
         MVC   CONTRA+3(3),ACPCLT CLIENT                                        
         CLC   OVRCLT,SPACES                                                    
         BE    *+10                                                             
         MVC   CONTRA+3(3),OVRCLT OVERRIDE CLIENT                               
         MVC   CONTRA+6(3),ACPPRD PRODUCT                                       
         CLC   OVRPRD,SPACES                                                    
         BE    *+10                                                             
         MVC   CONTRA+6(3),OVRPRD OVERRIDE PRODUCT                              
*                                                                               
         CLC   OVRCLT(6),SPACES   IF ANY OVERRIDE CLI/PRD                       
         BE    *+8                                                              
         MVI   CHKCON,C'Y'        MUST RE-READ CONTRA RECORD                    
         MVC   CONTRAN,SJPRDNM    SET CONTRAN                                   
         B     CONTRAX                                                          
         DROP  R2                                                               
         SPACE                                                                  
*-----------------------------------------------------------*                   
* VATICNTR - SETS THE CONTRA ACCOUNT & THE CONTRA ACC NAME  *                   
*            FOR THE INPUT GST AND PST POSTINGS             *                   
*-----------------------------------------------------------*                   
*                                                                               
VATICNTR NTR1                                                                   
         GOTO1 =A(GETACC),DMCB,(RC),MBTTAPR AOR PAY/RCV ROW                     
         B     CONTRAX                                                          
         DROP  R3                                                               
         EJECT                                                                  
*-----------------------------------------------------------*                   
* SORCONTR - SETS THE CONTRA ACCOUNT & THE CONTRA ACC NAME  *                   
*            FOR THE SUSPENSE & REBATE POSTING              *                   
*-----------------------------------------------------------*                   
*                                                                               
SORCONTR NTR1                                                                   
         BAS   RE,GETRCV                                                        
         CLI   SVMI,C'Y'          IF NOT MI AND COULDN'T FIND CONTRA            
         BE    SORCONX                                                          
         CLC   CONTRA,SPACES                                                    
         BNE   SORCONX                                                          
         ZIC   R0,PSTNUMA         TRY FINDING CONTRA FROM MAINT RECS            
         L     RE,=V(IOAREA)                                                    
         USING ACPRTND,RE                                                       
SORCON5  CLI   ACPBTYP,MBTTINC    BY READING MEDIA INC OR AOR INC REC           
         BE    SORCON20                                                         
         LA    RE,ACPRTNL(RE)                                                   
         BCT   R0,SORCON5                                                       
*                                                                               
         ZIC   R0,PSTNUMA                                                       
         L     RE,=V(IOAREA)                                                    
SORCON10 CLI   ACPBTYP,MBTTARI                                                  
         BE    SORCON20                                                         
         LA    RE,ACPRTNL(RE)                                                   
         BCT   R0,SORCON10                                                      
         B     SORCONX            STILL CAN'T FIND CONTRA                       
*                                                                               
SORCON20 CLC   ACPACC,SPACES                                                    
         BNH   SORCONX             STILL CAN'T FIND CONTRA                      
         LA    R2,AKEY                                                          
         USING ACTRECD,R2                                                       
         MVC   ACTKEY,SPACES                                                    
         MVC   ACTKCPY,ACCTCD2                                                  
         MVC   ACTKUNT(14),ACPACC                                               
         MVC   AREC,AMYAREA                                                     
         MVI   RDSE,2                                                           
         GOTO1 =A(READACC),DMCB,(RC),=C'DMREAD'                                 
         BNE   SORCONX                                                          
         GOTO1 GTNAME,DMCB,AREC,ACCNAME                                         
         MVC   CONTRA,SPACES                                                    
         MVC   CONTRA+3(12),ACCNAME                                             
         MVC   CONTRAN,SPACES                                                   
SORCONX  B     CONTRAX                                                          
         DROP  RE,R2                                                            
         EJECT                                                                  
*-----------------------------------------------------------*                   
* APYCONTR - SETS THE CONTRA ACCOUNT & THE CONTRA ACC NAME  *                   
*            FOR THE AOR PAYABLE/RECEIVABLE POSTING         *                   
*-----------------------------------------------------------*                   
*                                                                               
APYCONTR NTR1                                                                   
         LA    R2,ACPBLK                                                        
         USING ACPOSTD,R2                                                       
         MVC   CONTRA,SPACES                                                    
         LA    R1,DMCB                                                          
         XC    0(24,R1),0(R1)                                                   
         MVC   DMCB+7(1),0(R3)                                                  
         GOTO1 =A(GETACC),DMCB,(RC)                                             
         BNE   APYCONX                                                          
         CLC   CONTRA+1(2),=C'SR'                                               
         BNE   APYCON10                                                         
         GOTO1 =A(GETACC),DMCB,(RC),MBTTARI                                     
         MVC   CONTRA,SPACES                                                    
         MVC   CONTRA+3(12),CONTRAN     CONTRA = AOR INCOME ACC NAME            
         MVC   CONTRAN,SPACES            NO CONTRA ACCOUNT NAME                 
         B     APYCONX                                                          
*                                                                               
APYCON10 CLC   CONTRA+1(2),=C'SS'                                               
         BE    APYCON20                                                         
         CLC   CONTRA+1(2),=C'SP'                                               
         BE    APYCON20                                                         
         CLC   CONTRA+1(2),=C'SQ'                                               
         BE    APYCON20                                                         
         CLC   CONTRA+1(2),=C'ST'                                               
         BE    APYCON20                                                         
         CLC   CONTRA+1(2),=C'SU'                                               
         BE    APYCON20                                                         
*                                 OTHER PAYABLE TYPES                           
         MVC   CONTRA,SPACES                                                    
         MVC   CONTRA(1),ACCTCD2  OTHER COMPANY CODE                            
         MVC   CONTRA+1(2),=C'SJ' UNIT/LEDGER                                   
         MVC   CONTRA+3(3),ACPCLT CLIENT                                        
         MVC   CONTRAN,SJCLTNM                                                  
         B     APYCONX                                                          
*                                 SS,SP,SQ,ST,SU CLEARANCE TYPES                
APYCON20 MVC   CONTRA,SPACES                                                    
         MVC   CONTRA+12(3),ACPCLT CLIENT                                       
*                                                                               
APYCONX  B     CONTRAX                                                          
         DROP  R2                                                               
         EJECT                                                                  
*------------------------------------------*                                    
* RDMI - READ MI RECORD ON XIT SETS CONTRA *                                    
*------------------------------------------*                                    
*                                                                               
RDMI     NTR1                                                                   
         LA    R2,AKEY                                                          
         USING MINRECD,R2                                                       
         MVC   MINKEY,SPACES                                                    
         MVI   MINKTYP,MINKTYPQ    X'08'                                        
         MVC   MINKCPY,ACCTCD2     OTHER COMPANY CODE                           
         MVI   MINKMED,C'P'        FIRST BYTE IS SYSTEM                         
         MVC   MINKMED+1(1),MYMED  2ND BYTE IS MEDIA                            
         DROP  R2                                                               
         L     R2,AMYAREA                                                       
         ST    R2,AREC                                                          
         MVI   RDSE,2             READ FROM OTHER ACC FILE                      
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMREAD'                               
         BNE   GETMIX                                                           
         LA    R1,ACCORFST                                                      
         AR    R2,R1                                                            
         USING MDIELD,R2                                                        
         SR    R0,R0                                                            
GETMI5   CLI   0(R2),0             END OF RECORD?                               
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   0(R2),MDIELQ       X'19'                                         
         BE    GETMI10                                                          
         ICM   R0,1,1(R2)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R2,R0                                                            
         B     GETMI5                                                           
*                                                                               
GETMI10  MVC   CONTRAN,SPACES                                                   
         MVC   CONTRA,SPACES      LEAVE COMPANY CODE,UNIT/LEDGER BLANK          
         MVC   CONTRA+3(L'MDIDESC),MDIDESC                                      
GETMIX   XIT1                                                                   
         DROP  R2                                                               
         EJECT                                                                  
*------------------------------------------------*                              
* CKCONACC - READS NEW CONTRA ACC & SETS CONTRAN *                              
*------------------------------------------------*                              
*                                                                               
CKCONACC NTR1                                                                   
         MVC   CONTRAN,SPACES                                                   
         MVC   AKEY,SPACES                                                      
         MVC   AKEY(L'CONTRA),CONTRA                                            
         L     R2,AMYAREA                                                       
         ST    R2,AREC                                                          
         MVI   RDSE,2             READ FROM OTHER ACC FILE                      
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMREAD'                               
         BNE   CKCONACX                                                         
         GOTO1 GTNAME,DMCB,AREC,CONTRAN                                         
         CR    RB,RB                                                            
CKCONACX XIT1                     CC CODE SET                                   
         EJECT                                                                  
*=============*                                                                 
* LITERAL POOL*                                                                 
*=============*                                                                 
*                                                                               
         LTORG                                                                  
         SPACE                                                                  
         DROP  RB                                                               
         EJECT                                                                  
*---------------------------------------------------------*                     
* UNDO   -  UN-TRANSFER A BILL - BY READING POST DETAIL   *                     
*           RECORD(S) FOR BILL AND FOR EACH BILL MAKES TWO*                     
*           PASSES                                        *                     
*           1 -CHECKS ALL ACCOUNTS OKAY FOR RE-POSTINGS   *                     
*              AND PRINTS OUT THE RESULTS                 *                     
*           2 -IF RESULTS OKAY, RE-POSTS ORIGINAL SET     *                     
*              WITH -1, WRITES OUT BOGUS POST DETAIL RECS *                     
*              AND UNMARKS MEDIA BILL RECORD              *                     
*---------------------------------------------------------*                     
*                                                                               
         DS    0D                                                               
UNDO     NMOD1 0,**UNDO**,R9                                                    
         L     RC,0(R1)                                                         
         USING PPMXWRKD,RC        LOCAL WORKING STORAGE                         
         L     R5,ABILL                                                         
         USING PBILLREC,R5                                                      
*                                                                               
         BAS   RE,CLRFLDS          CLEAR/SET INDICATORS                         
*                                                                               
         BAS   RE,RDDTL           READ POST DETAIL REC FOR CURRENT BILL         
         CLI   UNERR,0                                                          
         BNE   UNDOX              NO RECORDS FOUND                              
*                                                                               
UNDO10   MVI   BYTE,C'N'          NO ACCOUNT ELEMENTS FOUND                     
         MVI   THREE,1            FIRST PASS                                    
UNDO12   L     R4,AMYAREA                                                       
         LA    R1,ACCORFST                                                      
         AR    R4,R1                                                            
         USING MBTELD,R4                                                        
UNDO15   CLI   0(R4),0                                                          
         BE    UNDO40                                                           
         CLI   0(R4),MBTELQ       X'2E'                                         
         BNE   UNDO30                                                           
         MVI   BYTE,C'Y'          ACCOUNT ELEMENT FOUND                         
         MVI   TRAREC,C'N'        NO TRANSACTION RECORD FOUND FOR ACC           
         BAS   RE,RDTRA           READ TRAN REC FOR EACH ACC                    
         CLI   TRAREC,C'Y'                                                      
         BNE   UNDOX                                                            
         CLI   THREE,1            FIRST PASS?                                   
         BE    UNDO30                                                           
         BAS   RE,UNPOST          2ND PASS - WRITE RECS TO WORKER FILE          
UNDO30   BAS   RE,BUMPR4          BUMP TO NEXT ELEMENT IN RECORD                
         B     UNDO15                                                           
*                                                                               
UNDO40   CLI   BYTE,C'Y'                                                        
         BE    *+12                                                             
         OI    UNERR,UNENODTL                                                   
         B     UNDOX                                                            
         CLI   THREE,1            FIRST PASS?                                   
         BNE   UNDO45                                                           
         CLI   TEST,C'N'          IF TEST - EXIT AFTER 1ST PASS                 
         BNE   UNDOX                                                            
         MVI   THREE,2            SECOND PASS                                   
         B     UNDO12             NOW WRITE TO WORKER FILE                      
*                                                                               
UNDO45   BAS   RE,REVAMTS         SET REVERSAL AMOUNTS FOR PRINT                
         BAS   RE,UNDTL           ADD NEW POST DETAIL RECORD                    
         CLI   DTLREC,C'Y'        THERE IS A 2ND RECORD                         
         BNE   UNDO50                                                           
         MVI   DTLREC,C'N'                                                      
         L     RE,AMYAREA                                                       
         ST    RE,AREC                                                          
         MVC   AKEY,0(RE)         MOVE IN KEY OF CURRENT RECORD                 
         MVI   RDSE,1             READ FROM NATIVE ACC FILE                     
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMREAD'                               
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMRSEQ'                               
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,SVMBTEL         SAVE 2E ELEMENTS FROM RECORD                  
         B     UNDO10                                                           
*                                                                               
UNDO50   CLI   UNERR,0            ANY PROBLEMS WITH UNDOING POSTINGS            
         BNE   UNDOX              YES - DON'T UNMARK BILL RECORD                
         BAS   RE,UNMARK          UNMARK BILL RECORD                            
         B     UNDOX                                                            
*                                                                               
UNDONO   LTR   RB,RB               SET CC NEQ                                   
         B     UNDOX                                                            
*                                                                               
UNDOYES  CR    RB,RB               SET CC EQ                                    
UNDOX    XIT1                                                                   
         EJECT                                                                  
*------------------------------------------*                                    
* CLRFLDS - CLEAR AND SET SOME INDCIATORS  *                                    
*------------------------------------------*                                    
         SPACE                                                                  
CLRFLDS  NTR1                                                                   
         ZAP   SVRCV,=P'0'                                                      
         ZAP   SVGRS,=P'0'                                                      
         ZAP   SVNET,=P'0'                                                      
         ZAP   SVAGYCOM,=P'0'                                                   
         ZAP   SVAAGYCM,=P'0'                                                   
         XC    SVGST,SVGST                                                      
         XC    SVGSTI,SVGSTI                                                    
         XC    SVPST,SVPST                                                      
         XC    SVPSTI,SVPSTI                                                    
         XC    SVCD,SVCD                                                        
         XC    SVIOR,SVIOR                                                      
         XC    SVAOR,SVAOR                                                      
         MVC   SVACOF2,SPACES                                                   
         XC    TRANSDT,TRANSDT    INV DATE ADJUSTED BY PROFILES                 
         L     R3,=V(MBTELES)     CLEAR START OF TABLE                          
         XC    0(MBTPDLNQ,R3),0(R3)                                             
         MVC   SVJBCD,SPACES                                                    
         MVI   UNERR,0            CLEAR ERROR FLAG FOR UNDOING BILL             
         MVI   DTLREC,C'N'        NO SECOND RECORD                              
         B     UNDOX                                                            
         SPACE                                                                  
*--------------------------------------------*                                  
* BUMPR4 - BUMP R4 TO NEXT ELEMENT IN RECORD *                                  
*--------------------------------------------*                                  
*                                                                               
BUMPR4   SR    R0,R0                                                            
         ICM   R0,1,1(R4)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R4,R0                                                            
         BR    RE                                                               
         SPACE                                                                  
*----------------------------------------------*                                
* ADDDISP- POINT R2 TO FIRST ELEMENT IN RECORD *                                
*----------------------------------------------*                                
*                                                                               
ADDDISP  LA    R1,ACCORFST                                                      
         AR    R2,R1                                                            
         BR    RE                                                               
         SPACE                                                                  
*--------------------------------------------*                                  
* BUMPR2 - BUMP R2 TO NEXT ELEMENT IN RECORD *                                  
*--------------------------------------------*                                  
*                                                                               
BUMPR2   SR    R0,R0                                                            
         ICM   R0,1,1(R2)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R2,R0                                                            
         BR    RE                                                               
         EJECT                                                                  
*-------------------------------------------------------*                       
* RDDTL - FOR A GIVEN BILL - READS DETAIL RECORD(S) FOR *                       
*         ACCOUNTS THAT WERE PREVIOUSLY POSTED TO       *                       
*  XIT    UNERR - SET IF NO RECORDS FOUND               *                       
*         DTLREC - SET TO Y IF 2 DETAIL RECS EXIST      *                       
*-------------------------------------------------------*                       
*                                                                               
RDDTL    NTR1                                                                   
         XC    AKEY,AKEY          READ POST DETAIL(S) RECORD                    
         LA    R2,AKEY            FOR CURRENT BILL                              
         USING MPDRECD,R2                                                       
         MVI   MPDKTYP,MPDKTYPQ   X'2F'                                         
         MVI   MPDKSUB,MPDKSUBQ   X'00'                                         
         MVC   MPDKCPY,ACCTCD     NATIVE COMPANY CODE                           
         MVC   MPDKALPH,SALPH     AGY ALPHA FOR SPLIT MEDIA FILES               
         MVI   MPDKSYS,C'P'       SYSTEM                                        
         MVC   MPDKMED,PBILKMED   MEDIA                                         
         MVC   MPDKCLI,PBILKCLT   CLIENT                                        
         MVC   MPDKPRD,PBILKPRD   PRODUCT                                       
         SR    RE,RE                                                            
         ICM   RE,3,PBILKEST                                                    
         CVD   RE,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  MPDKEST,DUB        ESTIMATE #                                    
*                                                                               
         MVC   FULL(2),PBILKBMN   (YM) BILLING MONTH                            
         MVI   FULL+3,1                                                         
         GOTO1 DATCON,DMCB,(3,FULL),(0,WORK)                                    
         MVC   MPDKPER,WORK       SET BILLING MONTH (YYMM)                      
         MVC   FULL,PBILKMOS      YM OF SERVICE                                 
         MVI   FULL+3,1                                                         
         GOTO1 DATCON,DMCB,(3,FULL),(0,WORK)                                    
         MVC   MPDKMOS,WORK       MONTH OF SERVICE (YYMM)                       
         EDIT  PBILKBNO,(5,MPDKINV),FILL=0                                      
*                                                                               
         L     R2,AMYAREA                                                       
         ST    R2,AREC                                                          
         MVI   RDSE,1             READ FROM NATIVE ACC FILE                     
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMREAD'                               
         BE    *+12                                                             
         OI    UNERR,UNENODTL     NO DETAIL RECORD FOUND                        
         B     RDDTLX                                                           
*                                                                               
         BAS   RE,SVMBTEL         SAVE 2E ELEMENTS FROM RECORD                  
         TM    MPDRSTA,X'01'      IS THERE A SECOND RECORD?                     
         BNO   RDDTLX                                                           
         MVI   DTLREC,C'Y'        THERE IS A 2ND RECORD                         
RDDTLX   B     UNDOX                                                            
         DROP  R2                                                               
         EJECT                                                                  
*-----------------------------------------------------------------*             
* SVMBTEL - SAVES X'2E' ELEMENTS IN MBTELES FOR LATER CHECKING    *             
*           IF RETRANSFERING (HANDLE SEQUENCE NUMBERS)            *             
*-----------------------------------------------------------------*             
*                                                                               
SVMBTEL  NTR1                                                                   
         L     R2,AREC             R2=A(POST DETAIL RECORD)                     
         BAS   RE,ADDDISP          R2=A(FIRST ELEMENT IN RECORD)                
*                                                                               
         L     R3,=V(MBTELES)      R3=A(TABLE OF MBTELD ELEMENTS)               
SVMBTEL2 OC    0(MBTPDLNQ,R3),0(R3) FIND EMPTY SPOT IN TABLE                    
         BZ    *+12                                                             
         LA    R3,MBTPDLNQ(R3)                                                  
         B     SVMBTEL2                                                         
*                                                                               
SVMBTEL3 CLI   0(R2),0             TEST END OF RECORD                           
         BE    SVMBTELX                                                         
         CLI   0(R2),MBTELQ        TEST X'2E' ELEMENT                           
         BNE   SVMBTEL5                                                         
         MVC   0(MBTPDLNQ,R3),0(R2) SAVE ELEMENT                                
         LA    R3,MBTPDLNQ(R3)                                                  
*                                                                               
SVMBTEL5 BAS   RE,BUMPR2           BUMP TO NEXT ELEMENT IN RECORD               
         B     SVMBTEL3                                                         
*                                                                               
SVMBTELX XC    0(MBTPDLNQ,R3),0(R3) CLEAR LAST ENTRY                            
         B     UNDOX                                                            
         EJECT                                                                  
*-----------------------------------------------------------------*             
* RDTRA -FOR GIVEN X'2E' ELEMENT (ONE ACC) READS FOR A TRANS REC  *             
*            IF NOT FOUND SETS UNERR / IF FOUND SETS TRAREC       *             
*-----------------------------------------------------------------*             
RDTRA    NTR1                                                                   
         XC    AKEY,AKEY                                                        
         LA    R3,AKEY                                                          
         USING TRNRECD,R3                                                       
         MVC   TRNKEY,SPACES                                                    
         MVC   TRNKCPY,ACCTCD2       OTHER COMPANY CODE                         
         MVC   TRNKUNT(14),MBTULA    ACCOUNT                                    
         CLC   MBTULA(2),=C'SJ'      IF SJ ACCOUNT                              
         BE    RDTRA2                                                           
         CLI   SVNEWOF,C'Y'          OR IF NEW ACC OFFICE                       
         BNE   RDTRA5                                                           
         CLI   EMULATE,C'Y'          AND IF NEW FILE                            
         BNE   RDTRA5                                                           
RDTRA2   MVC   TRNKOFF,MBTOFFC       SET WORK CODE/ACCOUNTING OFFICE            
RDTRA5   CLC   MBTULA(2),=C'SR'      IF ACCOUNT IS RECEIVABLE                   
         BE    RDTRA8                SKIP THE COMPANY CODE                      
         CLC   MBTULA(2),=C'SS'      OR IF CERTAIN PAYABLE ACCOUNTS             
         BE    RDTRA8                SKIP THE COMPANY CODE                      
         CLC   MBTULA(2),=C'SP'                                                 
         BE    RDTRA8                                                           
         CLC   MBTULA(2),=C'SQ'                                                 
         BE    RDTRA8                                                           
         CLC   MBTULA(2),=C'ST'                                                 
         BE    RDTRA8                                                           
         CLC   MBTULA(2),=C'SU'                                                 
         BE    RDTRA8                                                           
         CLI   MBTTYP,MBTTRCV      OR IF RECEIVABLES AND 'SB'                   
         BNE   *+14                                                             
         CLC   MBTULA(2),=C'SB'                                                 
         BE    RDTRA8                                                           
         MVC   TRNKCCPY,ACCTCD2                                                 
RDTRA8   MVC   TRNKCUNT(14),MBTCNTRA CONTRA ACCOUNT                             
         GOTO1 DATCON,DMCB,(3,PBILINVD),(1,TRNKDATE)                            
         MVC   TRNKREF,BINVOICE      BILL REF #                                 
         TM    MBTSTAT,MBTSAOR       IF ACCOUNT IS FOR TRUE AOR BILL            
         BNO   *+10                                                             
         MVC   TRNKREF,TRUEINV       USE ITS BILL INVOICE NUMBER                
         MVI   TRNKSBR,0             SUB ZERO                                   
         MVC   SVAKEY,AKEY           SAVE TRANSACTIION KEY BUILD                
         L     R3,AMYAREA2                                                      
         ST    R3,AREC                                                          
         MVI   RDSE,2             READ FROM OTHER ACC FILE                      
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMREAD'                               
         BNE   RDTRA30                                                          
*                                                                               
RDTRA10  BAS   RE,CHK3ELE         CHECK F3/23 ELEMENT                           
         BNE   RDTRA20            NOT CORRECT TRANSACTION RECORD                
         BAS   RE,CHKE5ELE        CHECK MATCHES IN E5 ELEMENT                   
         BNE   RDTRA20            NOT CORRECT TRANSACTION RECORD                
         BAS   RE,CHK44ELE        CHECK MATCHES IN 44 ELEMENT                   
         BNE   RDTRA20            NOT CORRECT TRANSACTION RECORD                
         BAS   RE,CHKDBELE        CHECK MATCHES IN DB ELEMENT                   
         BNE   RDTRA20            NOT CORRECT TRANSACTION RECORD                
         MVI   TRAREC,C'Y'        TRANSACTION RECORD FOUND FOR ACC              
         B     RDTRAX                                                           
*                                                                               
RDTRA20  L     RE,AREC            READ FOR NEXT TRANSACTION RECORD              
         MVC   AKEY,0(RE)         MOVE IN KEY OF CURRENT RECORD                 
         MVI   RDSE,2             READ FROM OTHER ACC FILE                      
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMREAD'                               
         BE    *+6                                                              
         DC    H'0'                                                             
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMRSEQ'                               
         BNE   RDTRA30                                                          
         CLC   SVAKEY(TRNKSBR-TRNKEY),AKEY                                      
         BE    RDTRA10            CHECK THIS RECORD                             
RDTRA30  OI    UNERR,UNENOTRA     PROBLEM - NO TRANSACTION REC                  
RDTRAX   B     UNDOX                                                            
         DROP  R3                                                               
         EJECT                                                                  
*-----------------------------------------------------------------*             
* CHK3ELE - CHECKS FOR F3 ELE FIRST OTHERWISE CHECKS 23 ELE MATCHES             
*-----------------------------------------------------------------*             
*                                                                               
CHK3ELE  NTR1                                                                   
         L     R2,AMYAREA2         R2=A(TRANSACTION RECORD)                     
         BAS   RE,ADDDISP          R2=A(FIRST ELEMENT IN RECORD)                
         USING MBIELD,R2                                                        
*                                                                               
CHK3E5   CLI   0(R2),0            END OF RECORD?                                
         BE    CHK3E20                                                          
         CLI   0(R2),MBIELQ       LOOK FOR F3 ELEMENT                           
         BE    *+12                                                             
         BAS   RE,BUMPR2          BUMP TO NEXT ELEMENT IN RECORD                
         B     CHK3E5                                                           
*                                                                               
         CLI   MBISYS,C'P'        MAKE SURE CORRECT SYSTEM                      
         BNE   UNDONO                                                           
         OC    MBIRDAT,MBIRDAT                                                  
         BNZ   UNDONO             DON'T REVERSE A REVERSAL                      
         CLC   MBIMED,PBILKMED    MAKE SURE CORRECT MEDIA                       
         BNE   UNDONO                                                           
         CLC   MBICLI,PBILKCLT    MAKE SURE CORRECT MEDIA CLIENT                
         BNE   UNDONO                                                           
         CLC   MBIPRD,PBILKPRD    MAKE SURE CORRECT MEDIA PRODUCT               
         BNE   UNDONO                                                           
         CLC   MBIEST,ESTNUM      MAKE SURE CORRECT MEDIA ESTIMATE              
         BNE   UNDONO                                                           
         MVC   FULL(2),PBILKMOS                                                 
         MVI   FULL+2,1                                                         
         GOTO1 DATCON,DMCB,(3,FULL),(1,WORK)                                    
         CLC   MBIMOS,WORK        MAKE SURE CORRECT YM OF SERVICE               
         BNE   UNDONO                                                           
         CLC   MBITDAT,PBILPOST   MAKE SURE CORRECT TRANS DATE                  
         BNE   UNDONO                                                           
         B     UNDOYES            FOUND A MATCH                                 
*                                 NO F3 ELE(OLD POSTING)- CHECK 23 ELE          
CHK3E20  L     R2,AMYAREA2        R2=A(TRANSACTION RECORD)                      
         BAS   RE,ADDDISP         R2=A(FIRST ELEMENT IN RECORD)                 
         USING OTHELD,R2                                                        
*                                                                               
CHK3E25  CLI   0(R2),0            END OF RECORD?                                
         BE    UNDONO                                                           
         CLI   0(R2),OTHELQ       LOOK FOR 23 ELEMENT                           
         BE    *+12                                                             
         BAS   RE,BUMPR2          BUMP TO NEXT ELEMENT IN RECORD                
         B     CHK3E25                                                          
*                                                                               
         CLC   OTHNUM(3),PBILKPRD PRODUCT MUST MATCH                            
         BNE   UNDONO                                                           
         CLC   OTHNUM+3(3),ESTNUM ESTIMATE MUST MATCH                           
         BNE   UNDONO                                                           
         CLC   OTHPROF(2),=C'P '  SYSTEM MUST MATCH                             
         BNE   UNDONO                                                           
         MVC   FULL(2),PBILKMOS                                                 
         MVI   FULL+2,1                                                         
         GOTO1 DATCON,DMCB,(3,FULL),(1,WORK)                                    
         CLC   OTHPROF+2(2),WORK  MONTH OF SERVICE MUST MATCH                   
         BNE   UNDONO                                                           
         B     UNDOYES            MATCH                                         
         DROP  R2                                                               
         EJECT                                                                  
*-----------------------------------------------------------------*             
* CHK44ELE - CHECKS IF 44 ELEMENT EXISTS & HAS CORRECT VALUES     *             
*-----------------------------------------------------------------*             
*                                                                               
CHK44ELE NTR1                                                                   
         L     R2,AMYAREA2         R2=A(TRANSACTION RECORD)                     
         BAS   RE,ADDDISP          R2=A(FIRST ELEMENT OF RECORD)                
         USING TRNELD,R2                                                        
*                                                                               
CHK44E5  CLI   0(R2),0            END OF RECORD?                                
         BE    UNDONO                                                           
         CLI   0(R2),TRNELQ       LOOK FOR 44 ELEMENT                           
         BE    *+12                                                             
         BAS   RE,BUMPR2          BUMP TO NEXT ELEMENT IN RECORD                
         B     CHK44E5                                                          
*                                 GET MOA FROM TRANSACTION RECORD               
         GOTO1 =V(CONVMOS),DMCB,TRNELD,WORK                                     
         MVI   WORK+2,1           FUDGE DAY                                     
         GOTO1 DATCON,DMCB,(1,WORK),(0,TRANSDT)                                 
*                                                                               
         CLI   SVPROF+7,C'Y'         HONOR MOA LOCK RULES?                      
         BNE   CHK44E50                                                         
         XC    WORK,WORK                                                        
         GOTO1 DATCON,DMCB,(0,TRANSDT),(6,WORK)                                 
         L     RF,UTL                SET ACCOUNTING SE NUMBER                   
         MVC   4(1,RF),ACCTSE2                                                  
         USING COMFACSD,R1                                                      
         L     R1,VCOMFACS         R1=A(COMFACS)                                
         OC    CPERVAL,CPERVAL     SPONSER DOESN'T FILL IN PERVAL               
         BNZ   *+10                SO NEED TO DO IT                             
         MVC   CPERVAL,=V(PERVAL)                                               
CHK44E10 GOTO1 =V(BMONVAL),DMCB,(6,WORK),(9,VCOMFACS),(0,WORK+20),     +        
               (ACCTCD2,0)                                                      
         LA    R1,WORK+20                                                       
         USING BMONVALD,R1                                                      
         CLI   BMOERR,BMOEOKQ      EVERYTHING OK?                               
         BE    CHK44E50                                                         
         TM    BMOERR,BMOELOKQ     MOA LOCKED?                                  
         BZ    CHK44E20                                                         
         OC    BMOLCKP,BMOLCKP     LAST LOCKED MOA                              
         BZ    CHK44E20                                                         
         XC    WORK(10),WORK                                                    
         MVC   WORK(L'BMOLCKP),BMOLCKP                                          
         MVI   WORK+2,X'01'                                                     
         GOTO1 DATCON,DMCB,(1,WORK),(0,WORK+3)                                  
         GOTO1 ADDAY,DMCB,WORK+3,WORK+3,31                                      
         MVC   TRANSDT,WORK+3                                                   
         B     CHK44E50                                                         
*                                                                               
CHK44E20 TM    BMOERR,BMOERNGQ     DATE OF OUF RANGE?                           
         BZ    CHK44E50                                                         
         OC    BMOMOSP,BMOMOSP                                                  
         BZ    CHK44E50                                                         
         XC    WORK(10),WORK                                                    
         MVC   WORK(L'BMOMOSP),BMOMOSP                                          
         MVI   WORK+2,X'01'                                                     
         GOTO1 DATCON,DMCB,(1,WORK),(0,WORK+3)                                  
         GOTO1 ADDAY,DMCB,WORK+3,WORK+3,31                                      
         GOTO1 DATCON,DMCB,(0,WORK+3),(6,WORK)                                  
         B     CHK44E10           VALIDATE AGAIN USING NEW MONTH                
*                                                                               
CHK44E50 L     RF,UTL                                                           
         MVC   4(1,RF),SAVSYS     RE-SET TO SPOT SYSTEM NUMBER                  
         CLI   TRNTYPE,9          IF NOT MY TYPE - SKIP IT                      
         BNE   UNDONO                                                           
         CP    TRNAMNT,MBTPOST     POSTING AMOUNT MUST MATCH                    
         BNE   UNDONO                                                           
         TM    MBTSTAT,MBTSDR     IF DEBIT                                      
         BNO   CHK44E60                                                         
         TM    TRNSTAT,TRNSDR     CHECK TRANSACTION ALSO DEBIT                  
         BNO   UNDONO                                                           
         CLC   =C'SJ',MBTULA      IF SJ - MUST BE NONCOMMISSIONABLE             
         BNE   UNDOYES                                                          
         TM    TRNSTAT,TRNSNOCM                                                 
         BNO   UNDONO                                                           
         B     UNDOYES                                                          
*                                                                               
CHK44E60 TM    TRNSTAT,TRNSDR     NOT DEBIT - SO MUST BE CREDIT                 
         BO    UNDONO                                                           
         B     UNDOYES            MATCH                                         
         DROP  R2                                                               
         EJECT                                                                  
*-----------------------------------------------------------------*             
* CHKE5ELE - CHECKS IF E5 ELEM EXISTS AND HAS CORRECT DATE        *             
*-----------------------------------------------------------------*             
*                                                                               
CHKE5ELE NTR1                                                                   
         L     R2,AMYAREA2         R2=A(TRANSACTION RECORD)                     
         BAS   RE,ADDDISP          R2=A(FIRST ELEMENT IN RECORD)                
         USING GDAELD,R2                                                        
*                                                                               
CHKE5E5  CLI   0(R2),0            END OF RECORD?                                
         BE    UNDONO                                                           
         CLI   0(R2),GDAELQ       LOOK FOR E5 MEDIA BILLING DATE ELE            
         BNE   *+12                                                             
         CLI   GDATYPE,GDATMBD                                                  
         BE    *+12                                                             
CHKE5E8  BAS   RE,BUMPR2           BUMP TO NEXT ELEMENT IN RECORD               
         B     CHKE5E5                                                          
*                                                                               
         GOTO1 DATCON,DMCB,(0,PBILLDAT),(1,WORK)                                
         CLC   GDADATE,WORK       DATES MUST MATCH                              
         BNE   UNDONO                                                           
         B     UNDOYES                                                          
         DROP  R2                                                               
         EJECT                                                                  
*-----------------------------------------------------------------*             
* CHKDBELE - CHECKS IF DB ELEM EXISTS AND HAS CORRECT ELEMENT TYPE*             
*-----------------------------------------------------------------*             
*                                                                               
CHKDBELE NTR1                                                                   
         L     R2,AMYAREA         R2=A(TRANSACTION RECORD)                      
         BAS   RE,ADDDISP         R2=A(FIRST ELEMENT IN RECORD)                 
         USING FFTELD,R2                                                        
*                                                                               
CHKDBE5  CLI   0(R2),0            END OF RECORD?                                
         BE    UNDOYES            YES, OK IF NOT THERE                          
         CLI   FFTEL,FFTELQ       LOOK FOR DB TEXT ELEMENT                      
         BNE   *+12                                                             
         CLI   FFTTYPE,FFTTMXTY   LOOK FOR CORRECT TYPE                         
         BE    *+12               NOW CHECK IF ELEMENT TYPE MATCHES             
         BAS   RE,BUMPR2          BUMP TO NEXT ELEMENT IN RECORD                
         B     CHKDBE5                                                          
*                                                                               
         CLC   FFTMXTE(L'MBTTYP),MBTTYP                                         
         BNE   UNDONO                                                           
         B     UNDOYES            MATCH                                         
         DROP  R2                                                               
         EJECT                                                                  
*--------------------------------------------------------*                      
* UNPOST - TAKES ELEMENTS NEEDED FROM TRANSACTION RECORD *                      
*          AND REPOSTS THEM AFTER MULT AMOUNTS BY -1     *                      
*--------------------------------------------------------*                      
*                                                                               
UNPOST   NTR1                                                                   
         GOTO1 CLRAREA,DMCB,V(REC),V(RECX)                                      
         L     R3,=V(REC)                                                       
         MVC   2(2,R3),=H'2'                                                    
         LA    R3,4(R3)           PT TO RECORD                                  
*                                                                               
         BAS   RE,SETHDR          SET 50 HEADER ELEMENT                         
         LA    R1,PSHEADL                                                       
         MVC   0(PSHEADL,R3),ELEM MOVE 50 HEADER ELEMENT IN                     
         AHI   R3,PSHEADL         PT R3 TO NEXT POSITION TO ADD ELEMENT         
         BAS   RE,ADDLEN          ADD TO REC LENGTH                             
*                                                                               
         L     R2,AMYAREA2         R2=A(TRANSACTION RECORD)                     
         BAS   RE,ADDDISP          R2=A(FIRST ELEMENT IN RECORD)                
         USING TRNELD,R2                                                        
*                                                                               
UNPST3   CLI   0(R2),0            END OF RECORD?                                
         BE    UNPST50            YES - GO ADD WORKER FILE RECORD               
         CLI   0(R2),DUEELQ       X'61' DUE DATE ELEMENT                        
         BE    UNPST5                                                           
         CLI   0(R2),UFSELQ       X'A2' USER FIELD SELECT ELE                   
         BE    UNPST5                                                           
         CLI   0(R2),MPGELQ       X'D7' PGROUP ELEMENT                          
         BE    UNPST5                                                           
         CLI   0(R2),GDAELQ       X'E5' BILL DATE ELEMENT                       
         BE    UNPST5                                                           
         CLI   0(R2),APEELQ       X'C0' 1C ATTRIBUTE ELEMENT                    
         BE    UNPST5                                                           
         CLI   0(R2),OTHELQ       X'23' OTHERS ELEMENT                          
         BNE   *+12                                                             
         BAS   RE,CHK23J          IF 23 'J' SAVE JOBCODE                        
         B     UNPST5                                                           
         CLI   0(R2),FFTELQ       X'DB' ELEMENT                                 
         BNE   *+16                                                             
         CLI   2(R2),FFTTAOR      'DB' AOR INFO TYPE?                           
         BNE   UNPST5             NO JUST COPY THE ELEMENT                      
         B     UNPST10            YES NEED TO MINUS THE AMOUNT                  
         CLI   0(R2),XPYELQ       X'46' EXTRA AOR PAY ELEMENT                   
         BNE   UNPST10                                                          
*                                                                               
UNPST5   ZIC   R1,1(R2)           GET LENGTH OF ELEMENT                         
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),0(R2)      MOVE INTO RECORD W/O CHANGES                  
         LA    R1,1(R1)                                                         
         AR    R3,R1              PT R3 TO NEXT AVAIL POSITION                  
         BAS   RE,ADDLEN                                                        
         B     UNPST30                                                          
*                                                                               
UNPST10  CLI   0(R2),MDTELQ                                                     
         BNE   UNPST15                                                          
         BAS   RE,SETMT           SET 1A MEDIA TRANSFER ELEMENT                 
         B     UNPST25            GO ADD IT                                     
*                                                                               
UNPST15  CLI   0(R2),TRNELQ                                                     
         BNE   UNPST20                                                          
         BAS   RE,SETRAN          SET 44 TRANSACTION ELEMENT                    
         B     UNPST25            GO ADD IT                                     
*                                                                               
UNPST20  CLI   0(R2),SCIELQ                                                     
         BNE   UNPST22                                                          
         BAS   RE,SETSUB          SET 50 SUBSIDARY CASH ELEMENTS                
         B     UNPST25            GO ADD IT                                     
*                                                                               
UNPST22  CLI   0(R2),MBIELQ                                                     
         BNE   UNPST22A                                                         
         BAS   RE,SETMBI          SET F3 ELEMENT                                
         B     UNPST25            GO ADD IT                                     
*                                                                               
UNPST22A CLI   0(R2),MDPELQ                                                     
         BNE   UNPST22B                                                         
         BAS   RE,SET6A           SET 6A ELEMENT                                
         B     UNPST25            AND ADD IT                                    
*                                                                               
UNPST22B CLI   0(R2),FFTELQ                                                     
         BNE   UNPST30                                                          
         BAS   RE,SETDB           SET DB ELEMENT                                
         B     UNPST25            AND ADD IT                                    
*                                                                               
UNPST25  ZIC   R1,ELEM+1          GET ELEMENT LENGTH                            
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),ELEM       MOVE IT IN                                    
         LA    R1,1(R1)                                                         
         AR    R3,R1              MOVE IT INTO ELEMENT                          
         BAS   RE,ADDLEN          UPDATE RECORD LENGTH                          
*                                                                               
UNPST30  BAS   RE,BUMPR2          BUMP TO NEXT ELEMENT IN RECORD                
         B     UNPST3                                                           
*                                                                               
UNPST50  L     RE,=V(REC)         MOVE & ADJUST LENGTH                          
         ICM   R1,3,2(RE)                                                       
         XC    0(4,RE),0(RE)                                                    
         LA    R1,4(R1)                                                         
         STCM  R1,3,0(RE)                                                       
         MVI   SEQONE,YES         REVERSAL RECORD                               
         MVI   DETL,NO            NOT A DETAIL RECORD                           
         GOTO1 =A(PUTSORT),DMCB,(RC),V(REC)                                     
UNPSTX   B     UNDOX                                                            
         DROP  R2                                                               
         SPACE 2                                                                
ADDLEN   NTR1                                                                   
         L     RF,=V(REC)                                                       
         SR    RE,RE                                                            
         ICM   RE,3,2(RF)                                                       
         AR    RE,R1                                                            
         STCM  RE,3,2(RF)                                                       
         B     UNDOX                                                            
         EJECT                                                                  
*-------------------------------------------------------------*                 
* SETHDR - CREATES 50 HEADER ELEMENT FOR POSTING RECORD       *                 
*          WHICH REVERSES PREVIOUS POSTINGS                   *                 
*-------------------------------------------------------------*                 
*                                                                               
SETHDR   NTR1                                                                   
         XC    ELEM,ELEM                                                        
         LA    R3,ELEM                                                          
         USING PSHEADD,R3                                                       
         L     R2,AMYAREA2        TRANSACTION RECORD                            
         USING TRNRECD,R2                                                       
         MVI   PSHDEL,PSHDELQ     X'50'                                         
         MVI   PSHDLEN,PSHEADL                                                  
         MVC   PSHDANAL,SPACES    ACC OFFICE CODE NOT USED YET                  
         CLC   TRNKCULA+1(2),=C'SJ'                                             
         BNE   *+10                                                             
         MVC   PSHDANAL,TRNKOFF   WORK CODE FOR SJ ONLY                         
         MVC   PSHDACC,TRNKCULA   ACCOUNT                                       
         MVC   PSHDSBAC,TRNKCULC  CONTRA ACCOUNT                                
         MVC   PSHDSBNM,SPACES    CONTRA ACCOUNT NAME                           
         MVC   AKEY,SPACES                                                      
         MVC   AKEY(L'TRNKCULC),TRNKCULC                                        
         MVC   AREC,AMYAREA3                                                    
         MVI   RDSE,2             READ FROM OTHER ACC FILE                      
         GOTO1 =A(READACC),DMCB,(RC),=CL8'DMREAD'                               
         BNE   SETHDRX                                                          
         GOTO1 GTNAME,DMCB,AREC,PSHDSBNM                                        
*                                                                               
SETHDRX  B     UNDOX                                                            
         DROP  R3,R2                                                            
         EJECT                                                                  
*-------------------------------------------------------------*                 
* SETMT - MOVE 1A ELEMENT INTO ELEM AND COMPLEMENT ALL AMOUNTS*                 
*-------------------------------------------------------------*                 
*                                                                               
SETMT    NTR1                                                                   
         XC    ELEM,ELEM                                                        
         ZIC   R1,1(R2)           ELEMENT LENTH                                 
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   ELEM(0),0(R2)                                                    
         LA    R1,ELEM                                                          
         USING MDTELD,R1                                                        
         ICM   RE,15,MDTNET                                                     
         LCR   RF,RE                                                            
         STCM  RF,15,MDTNET                                                     
         ICM   RE,15,MDTCD                                                      
         LCR   RF,RE                                                            
         STCM  RF,15,MDTCD                                                      
         ICM   RE,15,MDTCOM                                                     
         LCR   RF,RE                                                            
         STCM  RF,15,MDTCOM                                                     
         ICM   RE,15,MDTGRS                                                     
         LCR   RF,RE                                                            
         STCM  RF,15,MDTGRS                                                     
         ICM   RE,15,MDTRECV                                                    
         LCR   RF,RE                                                            
         STCM  RF,15,MDTRECV                                                    
         ICM   RE,15,MDTVAT                                                     
         LCR   RF,RE                                                            
         STCM  RF,15,MDTVAT                                                     
SETMTX   B     UNDOX                                                            
         DROP  R1                                                               
         EJECT                                                                  
*-------------------------------------------------------------*                 
* SET6A - MOVE 6A ELEMENT INTO ELEM AND COMPLEMENT ALL AMOUNTS*                 
*-------------------------------------------------------------*                 
*                                                                               
SET6A    NTR1                                                                   
         XC    ELEM,ELEM                                                        
         ZIC   R1,1(R2)           ELEMENT LENTH                                 
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   ELEM(0),0(R2)                                                    
         LA    R1,ELEM                                                          
         USING MDPELD,R1                                                        
         ZAP   DUB,MDPGRS                                                       
         MP    DUB,=P'-1'                                                       
         ZAP   MDPGRS,DUB                                                       
         ZAP   DUB,MDPNET                                                       
         MP    DUB,=P'-1'                                                       
         ZAP   MDPNET,DUB                                                       
         ZAP   DUB,MDPCOM                                                       
         MP    DUB,=P'-1'                                                       
         ZAP   MDPCOM,DUB                                                       
         ZAP   DUB,MDPCD                                                        
         MP    DUB,=P'-1'                                                       
         ZAP   MDPCD,DUB                                                        
         ZAP   DUB,MDPINTL                                                      
         MP    DUB,=P'-1'                                                       
         ZAP   MDPINTL,DUB                                                      
         ZAP   DUB,MDPRECV                                                      
         MP    DUB,=P'-1'                                                       
         ZAP   MDPRECV,DUB                                                      
         ZAP   DUB,MDPVAT                                                       
         MP    DUB,=P'-1'                                                       
         ZAP   MDPVAT,DUB                                                       
SET6AX   B     UNDOX                                                            
         DROP  R1                                                               
         EJECT                                                                  
*-------------------------------------------------------------*                 
* SETDB - MOVE DB ELEMENT INTO ELEM AND COMPLEMENT ALL AMOUNTS*                 
* NOTE - THIS IS FOR THE DB ELEMENT WITH TYPE 96 ONLY         *                 
*-------------------------------------------------------------*                 
*                                                                               
SETDB    NTR1                                                                   
         XC    ELEM,ELEM                                                        
         ZIC   R1,1(R2)           ELEMENT LENTH                                 
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   ELEM(0),0(R2)                                                    
         LA    R1,ELEM                                                          
         USING FFTELD,R1                                                        
         ZAP   DUB,FFTAMAOR                                                     
         MP    DUB,=P'-1'                                                       
         ZAP   FFTAMAOR,DUB                                                     
SETDBX   B     UNDOX                                                            
         DROP  R1                                                               
         EJECT                                                                  
*-------------------------------------------------------------*                 
* CHK23J - IF 23 'J' SAVE JOBCODE FOR PRINTING REVERSAL LINE  *                 
*-------------------------------------------------------------*                 
*                                                                               
CHK23J   NTR1                                                                   
         XC    ELEM,ELEM                                                        
         ZIC   R1,1(R2)           ELEMENT LENTH                                 
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   ELEM(0),0(R2)                                                    
         LA    R1,ELEM                                                          
         USING OTHELD,R1                                                        
         CLI   OTHPROF,C'J'       PRODUCTION                                    
         BNE   *+10                                                             
         MVC   SVJBCD,OTHNUM      SAVE IT                                       
         B     UNDOX                                                            
         DROP  R1                                                               
         EJECT                                                                  
*-------------------------------------------------------------*                 
* SETRAN- MOVE 44 ELEMENT INTO ELEM AND COMPLEMENT ALL AMOUNTS*                 
*-------------------------------------------------------------*                 
*                                                                               
SETRAN   NTR1                                                                   
         XC    ELEM,ELEM                                                        
         ZIC   R1,1(R2)           ELEMENT LENTH                                 
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   ELEM(0),0(R2)                                                    
         LA    R1,ELEM                                                          
         USING TRNELD,R1                                                        
         ZAP   DUB,TRNAMNT        NECESSARY FOR LARGE NUMBERS                   
         MP    DUB,=P'-1'                                                       
         ZAP   TRNAMNT,DUB                                                      
         MVC   TRNMOS(1),TRANSDT+1  2ND CHARACTER OF YEAR                       
         ZIC   RF,TRANSDT+3                                                     
         CLI   TRANSDT+2,C'1'      IF MONTH IS ONE DIGIT (0-9)                  
         BNE   *+8                 YES - THEN MOVE IT                           
         SH    RF,=H'47'           NO- MAKE 10=C1,11=C2,12=C3,ETC..             
         STC   RF,TRNMOS+1                                                      
         MVI   TRNSUB,0           SO TRANSACTION RECS ADDED IN ORDER            
SETRANX  B     UNDOX                                                            
         DROP  R1                                                               
         SPACE 1                                                                
*-------------------------------------------------------------*                 
* SETSUB- MOVE 50 ELEMENT INTO ELEM AND COMPLEMENT ALL AMOUNTS*                 
*-------------------------------------------------------------*                 
*                                                                               
SETSUB   NTR1                                                                   
         XC    ELEM,ELEM                                                        
         ZIC   R1,1(R2)           ELEMENT LENTH                                 
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   ELEM(0),0(R2)                                                    
         LA    R1,ELEM                                                          
         USING SCIELD,R1                                                        
         ZAP   DUB,SCIAMNT        NECESSARY FOR LARGE NUMBERS                   
         MP    DUB,=P'-1'                                                       
         ZAP   SCIAMNT,DUB                                                      
         CLI   SCILN,SCILN3Q                                                    
         BNE   SETSUBX                                                          
         ZAP   DUB,SCIBASE        NECESSARY FOR LARGE NUMBERS                   
         MP    DUB,=P'-1'                                                       
         ZAP   SCIBASE,DUB                                                      
SETSUBX  B     UNDOX                                                            
         DROP  R1                                                               
         SPACE                                                                  
*-------------------------------------------------------------*                 
* SETMBI- MOVE F3 ELEMENT INTO ELEM AND SET REVERSAL DATE     *                 
*-------------------------------------------------------------*                 
*                                                                               
SETMBI   NTR1                                                                   
         XC    ELEM,ELEM                                                        
         ZIC   R1,1(R2)           ELEMENT LENTH                                 
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   ELEM(0),0(R2)                                                    
         LA    R1,ELEM                                                          
         USING MBIELD,R1                                                        
         MVC   MBIRDAT,BTODAYP    SET DATE OF THIS REVERSAL                     
         B     UNDOX                                                            
         DROP  R1,R4                                                            
         EJECT                                                                  
*---------------------------------------------*                                 
* REVAMTS - SET REVERSAL AMOUNTS FO  PRINTING *                                 
*---------------------------------------------*                                 
*                                                                               
REVAMTS  NTR1                                                                   
         L     R2,AMYAREA          R2=A(POST DETAIL RECORD)                     
         BAS   RE,ADDDISP          R2=A(FIRST ELEMENT IN RECORD)                
         USING MBTELD,R2                                                        
*                                                                               
REVAMT5  CLI   0(R2),0                                                          
         BE    REVAMT80                                                         
         CLI   0(R2),MBTELQ                                                     
         BNE   REVAMT70                                                         
*                                                                               
         CLI   MBTTYP,MBTTRCV      RECEIVABLE ROW                               
         BNE   REVAMT10                                                         
         OC    MBTPOST,MBTPOST                                                  
         BZ    REVAMT8                                                          
         ZAP   SVRCV,MBTPOST     SET RECEIVABLE AMOUNT                          
REVAMT8  OC    MBTMEMO,MBTMEMO                                                  
         BZ    *+10                                                             
         ZAP   SVCD,MBTMEMO       SET CD AMOUNT                                 
         B     REVAMT70                                                         
*                                                                               
REVAMT10 CLI   MBTTYP,MBTTNET    NET ROW                                        
         BNE   REVAMT20                                                         
         OC    MBTPOST,MBTPOST                                                  
         BZ    *+10                                                             
         ZAP   SVNET,MBTPOST     SET NET AMOUNT                                 
         B     REVAMT70                                                         
*                                                                               
REVAMT20 CLI   MBTTYP,MBTTAPR    AOR PAY/RCVBL ROW                              
         BNE   REVAMT30                                                         
         MVI   SFLIPAOR,C'N'                                                    
         MVI   SMULTCM,C'N'                                                     
         CLC   =C'SR',MBTUNIT                                                   
         BNE   *+8                                                              
         MVI   SFLIPAOR,C'Y'                                                    
         OC    MBTPOST,MBTPOST                                                  
         BZ    REVAMT70                                                         
         ZAP   SVAOR,MBTPOST     SET AOR PAY/RCVBL AMT                          
         CLI   CNTRY,C'C'                                                       
         BE    REVAMT70                                                         
         ZAP   SVAAGYCM,MBTPOST  SET AGY COMM AMOUNT FOR U.S.                   
         CLC   =C'SR',MBTUNIT                                                   
         BNE   *+8                                                              
         MVI   SMULTCM,C'Y'                                                     
         B     REVAMT70                                                         
*                                                                               
REVAMT30 CLI   MBTTYP,MBTTGST    OUTPUT GST                                     
         BNE   REVAMT40                                                         
         OC    MBTPOST,MBTPOST                                                  
         BZ    *+10                                                             
         ZAP   SVGST,MBTPOST     OUTPUT GST AMOUNT                              
         B     REVAMT70                                                         
*                                                                               
REVAMT40 CLI   MBTTYP,MBTTGSTI    INPUT GST                                     
         BNE   REVAMT55                                                         
         OC    MBTPOST,MBTPOST                                                  
         BZ    *+10                                                             
         ZAP   SVGSTI,MBTPOST                                                   
         OC    MBTMEMO,MBTMEMO                                                  
         BZ    *+10                                                             
         ZAP   SVAAGYCM,MBTMEMO                                                 
         B     REVAMT70                                                         
*                                                                               
REVAMT55 CLI   MBTTYP,MBTTPQO    OUTPUT PST FOR PQ                              
         BE    REVAMT58                                                         
         CLI   MBTTYP,MBTTNBO    OUTPUT PST FOR NB                              
         BE    REVAMT58                                                         
         CLI   MBTTYP,MBTTNSO    OUTPUT PST FOR NS                              
         BE    REVAMT58                                                         
         CLI   MBTTYP,MBTTBCO    OUTPUT PST FOR BC                              
         BE    REVAMT58                                                         
         CLI   MBTTYP,MBTTONO    OUTPUT PST FOR ON                              
         BE    REVAMT58                                                         
         CLI   MBTTYP,MBTTPEO    OUTPUT PST FOR PE                              
         BE    REVAMT58                                                         
         CLI   MBTTYP,MBTTNFO    OUTPUT PST FOR NF                              
         BNE   REVAMT60                                                         
REVAMT58 OC    MBTPOST,MBTPOST   AND OUTPUT PST AMOUNT                          
         BZ    REVAMT70                                                         
         OC    SVPST,SVPST       SET OR ADD TO THE AMOUNT                       
         BNZ   *+14                                                             
         ZAP   SVPST,MBTPOST                                                    
         B     *+10                                                             
         AP    SVPST,MBTPOST                                                    
         B     REVAMT70                                                         
*                                                                               
REVAMT60 CLI   MBTTYP,MBTTPQI    INPUT PST FOR PQ                               
         BE    REVAMT62                                                         
         CLI   MBTTYP,MBTTNBI    INPUT PST FOR NB                               
         BE    REVAMT62                                                         
         CLI   MBTTYP,MBTTNSI    INPUT PST FOR NS                               
         BE    REVAMT62                                                         
         CLI   MBTTYP,MBTTBCI    INPUT PST FOR BC                               
         BE    REVAMT62                                                         
         CLI   MBTTYP,MBTTONI    INPUT PST FOR ON                               
         BE    REVAMT62                                                         
         CLI   MBTTYP,MBTTPEI    INPUT PST FOR PE                               
         BE    REVAMT62                                                         
         CLI   MBTTYP,MBTTNFI    INPUT PST FOR NF                               
         BNE   REVAMT65                                                         
REVAMT62 OC    MBTPOST,MBTPOST   AND INPUT PST AMOUNT                           
         BZ    REVAMT70                                                         
         OC    SVPSTI,SVPSTI     SET OR ADD TO INPUT PST                        
         BNZ   *+14                                                             
         ZAP   SVPSTI,MBTPOST                                                   
         B     *+10                                                             
         AP    SVPSTI,MBTPOST                                                   
         B     REVAMT70                                                         
*                                                                               
REVAMT65 CLI   MBTTYP,MBTTINTI   INTERNAL INCOME POSTING                        
         BNE   REVAMT70                                                         
         OC    MBTPOST,MBTPOST                                                  
         BZ    *+10                                                             
         ZAP   SVIOR,MBTPOST                                                    
         MVC   SVACOF2,MBTOFFC    SAVE 2ND ACCOUNTING OFFICE                    
*                                                                               
REVAMT70 BAS   RE,BUMPR2          BUMP TO NEXT ELEMENT IN RECORD                
         B     REVAMT5                                                          
*                                                                               
REVAMT80 BAS   RE,SETSI           READ MEDIA INC OR AOR INC                     
         OC    MEMO,MEMO                                                        
         BZ    *+10                                                             
         ZAP   SVGRS,MEMO         SET GROSS AMOUNT                              
         OC    AMOUNT,AMOUNT                                                    
         BZ    *+10                                                             
         ZAP   SVAGYCOM,AMOUNT    SET AGENCY COMMISSION AMOUNT                  
REVAMTX  B     UNDOX                                                            
         DROP  R2                                                               
         EJECT                                                                  
*----------------------------------------------------*                          
* SETSI   - GETS MEMO FROM INCOME OR MEDIA INCOME    *                          
*----------------------------------------------------*                          
*                                                                               
SETSI    NTR1                                                                   
         XC    AMOUNT,AMOUNT                                                    
         XC    MEMO,MEMO                                                        
         L     R2,AMYAREA         R2=A(POST DETAIL RECORD)                      
         BAS   RE,ADDDISP         R2=A(FIRST ELEMENT IN RECORD)                 
         USING MBTELD,R2                                                        
*                                                                               
SETSI5   CLI   0(R2),0                                                          
         BE    SETSI18                                                          
         CLI   0(R2),MBTELQ                                                     
         BNE   SETSI15                                                          
         CLI   MBTTYP,MBTTINC     INCOME ROW                                    
         BNE   SETSI15                                                          
         OC    MBTPOST,MBTPOST                                                  
         BZ    SETSI10                                                          
         ZAP   AMOUNT,MBTPOST     SAVE AMOUNT                                   
SETSI10  OC    MBTMEMO,MBTMEMO                                                  
         BZ    SETSIX                                                           
         ZAP   MEMO,MBTMEMO       SAVE MEMO                                     
         B     SETSIX                                                           
SETSI15  BAS   RE,BUMPR2          BUMP TO NEXT ELEMENT IN RECORD                
         B     SETSI5                                                           
         DROP  R2                                                               
*                                 INCOME ACCOUNT NOT FOUND                      
SETSI18  L     R2,AMYAREA         R2=A(POST DETAIL RECORD)                      
         BAS   RE,ADDDISP         R2=A(FIRST ELEMENT IN RECORD)                 
         USING MBTELD,R2                                                        
*                                                                               
SETSI20  CLI   0(R2),0                                                          
         BE    SETSIX                                                           
         CLI   0(R2),MBTELQ                                                     
         BNE   SETSI30                                                          
         CLI   MBTTYP,MBTTARI     AOR INCOME ROW                                
         BNE   SETSI30                                                          
         OC    MBTPOST,MBTPOST                                                  
         BZ    SETSI25                                                          
         ZAP   AMOUNT,MBTPOST     SAVE AMOUNT                                   
SETSI25  OC    MBTMEMO,MBTMEMO                                                  
         BZ    SETSIX                                                           
         ZAP   MEMO,MBTMEMO       SAVE MEMO                                     
         B     SETSIX                                                           
SETSI30  BAS   RE,BUMPR2          BUMP TO NEXT ELEMENT IN RECORD                
         B     SETSI20                                                          
*                                                                               
SETSIX   B     UNDOX                                                            
         DROP  R2                                                               
         EJECT                                                                  
*-------------------------------------------------------------*                 
* UNDTL - ADDS BOGUS POST DETAIL RECORD TO INDICATE POSTINGS  *                 
*         WERE REVERSED                                       *                 
*-------------------------------------------------------------*                 
*                                                                               
UNDTL    NTR1                ADD FAKE DTL RECORD                                
         GOTO1 CLRAREA,DMCB,V(PSTREC),V(PSTRECX)                                
         L     R2,AMYAREA         PT TO POST DETAIL RECORD ON FILE              
         LA    R3,ACCORFST        KEY LENGTH OF RECORD                          
         L     R4,=V(PSTREC)                                                    
         AHI   R4,4               BUMP TO RECORD                                
         MVC   0(ACCORFST,R4),0(R2)      MOVE KEY OF RECORD                     
         AHI   R4,ACCORFST        R4=A(ELEMENT POSTION IN RECORD)               
*                                                                               
         USING GDAELD,R4                                                        
         MVI   GDAEL,GDAELQ                                                     
         MVI   GDALN,GDALNQ                                                     
         MVI   GDATYPE,GDATMBD                                                  
         GOTO1 DATCON,DMCB,(0,PBILLDAT),(1,GDADATE)                             
         AHI   R3,GDALNQ          TOTOAL LENGTH OF RECORD                       
*                                                                               
         L     R4,=V(PSTREC)                                                    
         LR    R1,R4                                                            
         LA    R4,4(R4)                                                         
         USING MPDRECD,R4                                                       
         STCM  R3,3,MPDRLEN       TOTOAL LENGTH OF RECORD                       
         LA    R3,4(R3)           RECORD + HEADER                               
         STCM  R3,3,0(R1)                                                       
*                                                                               
         MVI   SEQONE,YES         REVERSAL RECORD                               
         MVI   DETL,YES           DETAIL RECORD                                 
         GOTO1 =A(PUTSORT),DMCB,(RC),V(PSTREC)                                  
UNDTLX   B     UNDOX                                                            
         DROP  R4                                                               
         DROP  R5                                                               
         EJECT                                                                  
*-------------------------------------------------------------*                 
* UNMARK -UN MARKS TRANSFERRED BILL RECORD                    *                 
*-------------------------------------------------------------*                 
*                                                                               
UNMARK   NTR1                                                                   
         MVI   TRUEAOR,C'N'                                                     
         L     RF,UTL                                                           
         MVC   4(1,RF),SAVSYS                                                   
         LA    R1,DMCB                                                          
         LA    RE,DMRDHI                                                        
         ST    RE,0(R1)                                                         
         CLI   QOPT4,C'U'         TO UNPOST                                     
         BNE   *+8                                                              
         MVI   0(R1),X'08'        PASS BACK DELETED RECORDS                     
         L     RE,ABILL           REREAD KEY JUST UNTRANSFERRED                 
         XC    KEY,KEY                                                          
         MVC   KEY(L'PBILLKEY),0(RE)                                            
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,,PRTDIR,KEY,KEY                                     
         CLC   KEY(L'PBILLKEY),KEYSAVE                                          
         BE    *+6                                                              
         DC    H'0'                                                             
UNMARK2  TM    MXOPT,MXNOWRIT     IF WRITE=NO                                   
         BO    UNMARK8                                                          
         NI    KEY+26,X'BE'       TURN OF TRANS & CLOSEOUT BITS                 
         GOTO1 DATAMGR,DMCB,DMWRT,PRTDIR,KEY,KEY                                
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
UNMARK8  MVC   AREC,AMYAREA                                                     
         L     R5,AREC                                                          
         USING PBILLREC,R5                                                      
*                                                                               
UNMARK10 GOTO1 DATAMGR,DMCB,GETREC,PRTFILE,KEY+27,AREC,DMWORK                   
         CLI   TRUEAOR,C'Y'       1ST TIME PASS?                                
         BNE   UNMARK12                                                         
         CLC   RUNDT,PBILLDAT     NO -LOOKING FOR TRUE AOR BILL                 
         BE    *+6                                                              
         DC    H'0'               MUST FIND IT                                  
         TM    PBILCMSW,X'20'                                                   
         BNO   UNMARK20                                                         
         MVI   TRUEAOR,C'M'       FOUND IT - UNMARK REC & DIRECT                
         B     UNMARK15                                                         
*                                                                               
UNMARK12 TM    PBILCMSW,X'10'     1ST TIME THROUGH - SET FLG IF NECESS          
         BNO   UNMARK15                                                         
         TM    PBILCMSW,X'08'                                                   
         BO    UNMARK15                                                         
         CLI   PBILSEP,C'R'                                                     
         BNE   UNMARK15                                                         
         MVI   TRUEAOR,C'Y'                                                     
*                                                                               
UNMARK15 TM    MXOPT,MXNOWRIT                                                   
         BO    UNMARK18                                                         
         NI    PBILLCTL+1,X'BE'   TURN OFF TRANS & CLOSEOUT BITS                
         XC    PBILPOST,PBILPOST  AND CLEAR BILL DATE                           
         GOTO1 DATAMGR,DMCB,PUTREC,PRTFILE,KEY+27,AREC,DMWORK                   
*                                                                               
UNMARK18 CLI   TRUEAOR,C'Y'       IF CLIENT AOR - ALSO UNMARK TRUE              
         BNE   UNMARK30                                                         
UNMARK20 MVC   KEYSAVE,KEY                                                      
         LA    R1,DMCB                                                          
         LA    RE,DMRSEQ                                                        
         ST    RE,0(R1)                                                         
         CLI   QOPT4,C'U'         TO UNPOST                                     
         BNE   *+8                                                              
         MVI   0(R1),X'08'        PASS BACK DELETED RECORDS                     
         GOTO1 DATAMGR,DMCB,,PRTDIR,KEY,KEY                                     
         B     UNMARK10                                                         
*                                                                               
UNMARK30 CLI   TRUEAOR,C'M'                                                     
         BNE   UNMARKX                                                          
         TM    MXOPT,MXNOWRIT     IF WRITE=NO                                   
         BO    UNMARKX                                                          
         NI    KEY+26,X'BE'       TURN OFF TRANS & CLOSEOUT BITS                
         GOTO1 DATAMGR,DMCB,DMWRT,PRTDIR,KEY,KEY                                
         CLI   DMCB+8,0                                                         
         BE    UNMARKX                                                          
         DC    H'0'                                                             
*                                                                               
UNMARKX  B     UNDOX                                                            
         EJECT                                                                  
                                                                                
*======================================================================         
* LITERAL POOL                                                                  
*======================================================================         
         LTORG                                                                  
         DROP  RB,R9,R5              DROP PREVIOUS BASE REGS                    
                                                                                
*======================================================================         
* OTHER VARIOUS DSECTS                                                          
*======================================================================         
*  ACGENFILE                                                                    
*  ACGENPOST                                                                    
*  ACPOSTD                                                                      
*  MXPRNTD                                                                      
*  MXTBDSECT                                                                    
*  DMWRKRK                                                                      
*  DDLOGOD                                                                      
*  DDMASTD                                                                      
*  DDREMOTED                                                                    
*  DDOFFICED                                                                    
*  FASSBOFF                                                                     
*  PPNEWFILE                                                                    
*  PAORREC                                                                      
*  CTGENFILE                                                                    
*  PPMODEQU                                                                     
*  DDUCOMD                                                                      
*  DMWRKRD                                                                      
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
       ++INCLUDE ACGENPOST                                                      
       ++INCLUDE ACPOSTD                                                        
       ++INCLUDE ACTRAPRVD                                                      
       ++INCLUDE MXPOSTD                                                        
       ++INCLUDE MXPRNTD                                                        
       ++INCLUDE MXTBDSECT                                                      
       ++INCLUDE DMWRKRK                                                        
       ++INCLUDE DDLOGOD                                                        
       ++INCLUDE DMDTFIS                                                        
       ++INCLUDE DDREPMASTD                                                     
       ++INCLUDE DDREMOTED                                                      
       ++INCLUDE DDOFFICED                                                      
       ++INCLUDE FASSBOFF                                                       
       ++INCLUDE PPMODEQU                                                       
       ++INCLUDE PAORREC                                                        
       ++INCLUDE PBLPSTEL                                                       
       ++INCLUDE CTGENFILE                                                      
       ++INCLUDE DDUCOMD                                                        
       ++INCLUDE DDCOMFACSD                                                     
       ++INCLUDE DMWRKRD                                                        
       ++INCLUDE DDCROSSD                                                       
       ++INCLUDE ACBMONVALD                                                     
*PREFIX=P  =PPRELEM                                                             
       ++INCLUDE PPNEWFILE                                                      
*PREFIX=                                                                        
         EJECT                                                                  
*                                                                               
         PRINT ON                                                               
       ++INCLUDE PPREPWORK                                                      
*                                                                               
PPWORKD  DSECT                                                                  
         ORG   QPUBALL                                                          
QBTYPE   DS    CL3                TRANSFER TYPE FOR REVERSAL                    
QIORDT   DS    CL6                INVOICE OR RUN DATE FOR REVERSAL              
         ORG                                                                    
*                                                                               
       ++INCLUDE PPREPWORK2                                                     
*                                                                               
         ORG   Q2USER             SECOND REQUEST CARD                           
QBILL1   DS    CL6                BILL NUMBER 1                                 
QBILL2   DS    CL6                BILL #2 (OR SPACES)                           
QTRADT   DS    CL6                TRANSFER DATE FOR REVERSAL                    
         ORG                                                                    
         EJECT                                                                  
PPMXWRKD DSECT                                                                  
MAXCI    EQU   220                                                              
EOR      EQU   0                   END OF RECORD                                
YES      EQU   C'Y'                                                             
NO       EQU   C'N'                                                             
         DS    0F                                                               
NXTPSTE  DS    F                  A(NXT POS- NEW ELEM IN PST DTL REC)           
WFILE    DS    A                  WORKER FILE                                   
ABILL    DS    A                  ADDRESS OF BILL RECORD                        
AMYAREA  DS    A                  ADDRESSES OF IOAREAS                          
AMYAREA2 DS    A                                                                
AMYAREA3 DS    A                                                                
AAORBIL1 DS    A                  ADDRESSES OF IOAREAS FOR AOR BILLS            
AAORBIL2 DS    A                                                                
AAORBIL3 DS    A                                                                
FULL2    DS    A                                                                
APOST    DS    A                                                                
ACPOSTER DS    A                   A(ACPOSTER)                                  
APRVTAB  DS    A                   A(PRVTAB)                                    
AMXBLK   DS    A                   A(MXBLK)                                     
AOFFICER DS    A                   A(OFFICER)                                   
*                                                                               
L1       DS    A                  SAVED PRINT LINE 1                            
L2       DS    A                  SAVED PRINT LINE 2                            
L3       DS    A                  SAVED PRINT LINE 3                            
*                                                                               
SAVSYS   DS    XL1                SAVED SE NUMBER                               
SALPH    DS    CL2                AGY ALPHA FOR MEDIA SPLIT FILES               
ACCALPH  DS    CL2                ACC POWER CODE                                
ACCOID   DS    XL2                NUMERIC ALPHA AGENCY (ORIGIN ID               
TEMPCD   DS    XL1                                                              
TEMPSE   DS    XL1                                                              
TEMPOID  DS    XL2                                                              
ACCTCD2  DS    XL1                                                              
ACCTSE2  DS    XL1                                                              
ACCTOID1 DS    XL2                                                              
ACCTOID2 DS    XL2                                                              
RDSE     DS    XL1                                                              
PERR     DS    XL1                INTERNAL ERRORS                               
PNOACC   EQU   X'40'              NO ACCOUNT NAME                               
PNOPOST  EQU   X'80'              POSTING NOT SUCESSFUL                         
MXOPT    DS    XL1                INTERNAL CONTROL VALUES                       
MXMBSOB  EQU   X'20'              SAME NIGHT POSTING                            
MXNOPOST EQU   X'80'              DO NOT POST TO WORKER FILE                    
MXNOWRIT EQU   X'40'              DO NOT WRITE ANY RECORDS                      
TRUEAOR  DS    CL1                Y= TRUE AOR BILL NEXT ON FILE                 
PREVINC  DS    CL1                                                              
PREVAINC DS    CL1                                                              
TRAREC   DS    CL1                Y= TRANSACTION REC EXISTS FOR ACC             
ANYBILL  DS    CL1                                                              
SEQONE   DS    CL1                                                              
DETL     DS    CL1                ADDING DETAIL REC -TO SIGN ON ACC             
TEST     DS    CL1                                                              
ABILCNT  DS    CL1                                                              
BYTE2    DS    CL1                                                              
*                                                                               
MSGTYPE  DS    CL4                1(C,M,P) 3 FOR CODE                           
MAKEPOST DS    CL1                DEFAULT IS NOT TO MAKE ANY POSTINGS           
CONERR   DS    CL1                CONTRA ACCOUNT ERROR                          
ACCERR   DS    CL1                C'Y'- ERROR WITH ONE ACCOUNT                  
UNERR    DS    CL1                UNDO ERROR                                    
UNENODTL EQU   X'80'              NO DETAIL RECORDS                             
UNENOTRA EQU   X'40'              NO TRANSACTION RECORD FOR AN ACC              
UNEAOR   EQU   X'20'              ERROR WITH AOR SET                            
*                                                                               
SVMI     DS    CL1                C'Y'= ON MI RECORDS                           
SVNEWOF  DS    CL1                C'Y'= USES NEW OFFICES IN KEY                 
SVCOST   DS    CL1                C'Y'= USE COSTING,BILLING,REV                 
SVPROD   DS    CL2                UNIT/LEDGER                                   
SVRSYS   DS    CL1                REDEFINED SYSTEM CODE FROM B1 PROF            
SVRMED   DS    CL1                 REDEFINED MEDIA CODE FROM B1 PROF            
GSTO     DS    CL1                SAVED GST CODE FROM MAIN BILL                 
PSTOVALS DS    14CL12             SAVED PST CODE FROM MAIN BILL                 
SVCOFF   DS    CL1                SAVED CLIENT MEDIA OFFICE CODE                
ACLIOFF  DS    CL2                SAVED CLIENT ACC OFFICE CODE                  
SVACOFF  DS    CL2                SAVED CLIENT/PRODUCT ACC OFFICE CODE          
SVACOF2  DS    CL2                ACC OFFICE CODE OVERRIDE FOR INT              
SVOFFG   DS    CL2                SAVED OFFICE GROUP                            
DTLREC   DS    XL1                X'01'= TWO POST DETAIL RECORDS                
FNDTRUE  DS    CL1                Y= TRUE AOR RECORD FOUND                      
PSTNUMA  DS    XL1                                                              
PSTNUM2  DS    XL1                                                              
NUMABIL  DS    XL1                                                              
PSTAOR   DS    XL1                Y= NO ERRORS WITH AOR SET                     
CNTRY    DS    CL1                C'C' = CANADIAN                               
WKCD     DS    CL2                WORK CODE FOR SJ                              
EMULATE  DS    CL1                Y= NEW FILE EMULATION                         
RUNDT    DS    CL(L'PBILLDAT)                                                   
CHKCON   DS    CL1                                                              
OVRRIDE  DS    0CL6               OVERRIDE CLIENT AND PRODUCT                   
OVRCLT   DS    CL3                OVERRIDE CLIENT                               
OVRPRD   DS    CL3                OVERRIDE PRODUCT                              
*                                                                               
TOTCASH  DS    PL6                TOTAL CASH OF POSTINGS                        
RECCNT   DS    PL6                # OF RECORDS                                  
MEMO     DS    PL6                MEMO AMOUNT                                   
AMOUNT   DS    PL6                POSTING AMOUNT                                
DEBS     DS    PL6                                                              
CREDS    DS    PL6                                                              
         DS    0D                                                               
DUB2     DS    PL8                                                              
*                                                                               
SFLIPAOR DS    XL1                                                              
SMULTCM  DS    XL1                                                              
SVRCV    DS    PL6                                                              
SVGRS    DS    PL6                                                              
SVNET    DS    PL6                                                              
SVAGYCOM DS    PL6                                                              
SVAOR    DS    PL6                                                              
SVGST    DS    PL6                                                              
SVPST    DS    PL6                                                              
SVCD     DS    PL6                                                              
SVGSTI   DS    PL6                                                              
SVPSTI   DS    PL6                                                              
SVIOR    DS    PL6                                                              
SVAAGYCM DS    PL6                                                              
         DS    2PL6                                                             
*                                                                               
BILGRS   DS    PL6                                                              
BILBIL   DS    PL6                                                              
BILNET   DS    PL6                                                              
BILRCV   DS    PL6                                                              
BILGST   DS    XL4                                                              
BILPST   DS    10XL9                                                            
*                                                                               
JOBCD    DS    CL6                                                              
SVJBCD   DS    CL6                                                              
SPJBCD   DS    CL6                                                              
*                                                                               
MYMED    DS    CL1                                                              
MYCLT    DS    CL3                                                              
MYPRD    DS    CL3                                                              
MYBEST   DS    XL2                                                              
ESTNUM   DS    CL3                                                              
BTODAYP  DS    XL2                PACKED TODAYS DATE                            
TODAYD   DS    CL6                TODAYS DATE EB.                               
TRANSDT  DS    CL6                ADJUSTMENT DATE FOR BILL                      
RERUNDT  DS    CL6                SPECIAL BILLING RE-RUN DT                     
REQTDT   DS    XL2                REQUEST TRANSFER DATE                         
COMMAND  DS    CL8                                                              
BINVOICE DS    CL6                                                              
TEMPINV  DS    XL2                                                              
TEMPDATE DS    CL6                                                              
SVADID   DS    CL12                AD-ID FROM BILL HEADER                       
SVCNET   DS    XL4                CALCULATED NET                                
SVBNET   DS    PL6                 MIDAS EXCHANGE (FOR NB MEMO)                 
*                                                                               
BIT      DS    XL1                                                              
PRDOFFC  EQU   X'80'                                                            
SVFLAG   DS    XL1                FLAG FOR MX                                   
COS2FAC  EQU   X'80'              COS2 BILL FACTOR                              
COS2DOL  EQU   X'02'              COS2 BILL $ TYPE                              
WID      DS    CL16               ID FOR WORKER FILE                            
SESTKEY  DS    CL(L'PBILLKEY)     CURRENT ESTIMATE KEY                          
NEXTKEY  DS    CL(L'PBILLKEY)     NEXT KEY TO PROCESS                           
SKEY     DS    CL(L'KEY)          BILL SAVE KEY                                 
PKEY     DS    CL(L'KEY)          ESTIMATE SAVE KEY(PPGS KEY)                   
PKEYSAVE DS    CL(L'KEYSAVE)                                                    
SVPROF   DS    CL16               SAVED ACC MX PROFILE                          
SVPROF2  DS    CL16               SAVED MEDIA MX PROFILE                        
B1XPROF  DS    CL16               SAVED MEDIA B1X PROFILE                       
B1PROF   DS    CL16               SAVED MEDIA B1 PROF(AT LOWEST LEVEL)          
REVSTAT  DS    CL1                Y=REVERSING BILL-NEEDED FOR B1X PROF          
REVDATE  DS    XL2                DATE OF ORIGINAL TRANSFER                     
SVEDESC  DS    CL20               ESTIMATE DESCRIPTION                          
SVUSR    DS    XL1                 NEW USER DEFINITION FIELDS                   
SVPU1    EQU   X'80'               TRANSFER PRODUCT USER FIELD 1                
SVPU2    EQU   X'40'               TRANSFER PRODUCT USER FIELD 2                
SVEU1    EQU   X'20'               TRANSFER ESTIMATE USER FIELD 1               
SVEU2    EQU   X'10'               TRANSFER ESTIMATE USER FIELD 2               
SVPUSER1 DS    CL(L'PUSER1)        PRODUCT USER FIELD 1 DATA                    
SVPUSER2 DS    CL(L'PUSER2)        PRODUCT USER FIELD 2 DATA                    
SVEUSER1 DS    CL(L'PEUSER1)       ESTIMATE USER FIELD 1 DATA                   
SVEUSER2 DS    CL(L'PEUSER2)       ESTIMATE USER FIELD 2 DATA                   
SVPDESC1 DS    CL(L'PCLTPU1)       PRODUCT USER FIELD 1 DATA                    
SVPDESC2 DS    CL(L'PCLTPU2)       PRODUCT USER FIELD 2 DATA                    
SVEDESC1 DS    CL(L'PCLTEU1)       ESTIMATE USER FIELD 1 DATA                   
SVEDESC2 DS    CL(L'PCLTEU2)       ESTIMATE USER FIELD 2 DATA                   
SVEPONUM DS    CL(L'PEPONPON)      ESTIMATE PO NUMBER                           
*                                                                               
SVUCMPLN DS    0XL4                                                             
SVUP1LN  DS    XL1                 LENGTH OF 1ST PRODUCT UCOM FIELD             
SVUP2LN  DS    XL1                 LENGTH OF 2ND PRODUCT UCOM FIELD             
SVUP3LN  DS    XL1                 LENGTH OF 3RD PRODUCT UCOM FIELD             
SVUP4LN  DS    XL1                 LENGTH OF 4TH PRODUCT UCOM FIELD             
*                                                                               
SVUCMELN DS    0XL4                                                             
SVUE1LN  DS    XL1                 LENGTH OF 1ST ESTIMATE UCOM FIELD            
SVUE2LN  DS    XL1                 LENGTH OF 2ND ESTIMATE UCOM FIELD            
SVUE3LN  DS    XL1                 LENGTH OF 3RD ESTIMATE UCOM FIELD            
SVUE4LN  DS    XL1                 LENGTH OF 4TH ESTIMATE UCOM FIELD            
*                                                                               
SVUCMRLN DS    0XL4                                                             
SVUR1LN  DS    XL1                 LENGTH OF 1ST REGION UCOM FIELD              
SVUR2LN  DS    XL1                 LENGTH OF 2ND REGION UCOM FIELD              
SVUR3LN  DS    XL1                 LENGTH OF 3RD REGION UCOM FIELD              
SVUR4LN  DS    XL1                 LENGTH OF 4TH REGION UCOM FIELD              
*                                                                               
SVUCMDLN DS    0XL4                                                             
SVUD1LN  DS    XL1                 LENGTH OF 1ST DISTRICT UCOM FIELD            
SVUD2LN  DS    XL1                 LENGTH OF 2ND DISTRICT UCOM FIELD            
SVUD3LN  DS    XL1                 LENGTH OF 3RD DISTRICT UCOM FIELD            
SVUD4LN  DS    XL1                 LENGTH OF 4TH DISTRICT UCOM FIELD            
*                                                                               
SVUCMDP  DS    0CL80                                                            
SVUCMP1D DS    CL20                UCOM PRODUCT USER FIELD DESC 1               
SVUCMP2D DS    CL20                UCOM PRODUCT USER FIELD DESC 2               
SVUCMP3D DS    CL20                UCOM PRODUCT USER FIELD DESC 3               
SVUCMP4D DS    CL20                UCOM PRODUCT USER FIELD DESC 4               
*                                                                               
SVUCMDE  DS    0CL80                                                            
SVUCME1D DS    CL20                UCOM ESTIMATE USER FIELD DESC 1              
SVUCME2D DS    CL20                UCOM ESTIMATE USER FIELD DESC 2              
SVUCME3D DS    CL20                UCOM ESTIMATE USER FIELD DESC 3              
SVUCME4D DS    CL20                UCOM ESTIMATE USER FIELD DESC 4              
*                                                                               
SVUCMDR  DS    0CL80                                                            
SVUCMR1D DS    CL20                UCOM REGION USER FIELD DESC 1                
SVUCMR2D DS    CL20                UCOM REGION USER FIELD DESC 2                
SVUCMR3D DS    CL20                UCOM REGION USER FIELD DESC 3                
SVUCMR4D DS    CL20                UCOM REGION USER FIELD DESC 4                
*                                                                               
SVUCMDD  DS    0CL80                                                            
SVUCMD1D DS    CL20                UCOM DISTRICT USER FIELD DESC 1              
SVUCMD2D DS    CL20                UCOM DISTRICT USER FIELD DESC 2              
SVUCMD3D DS    CL20                UCOM DISTRICT USER FIELD DESC 3              
SVUCMD4D DS    CL20                UCOM DISTRICT USER FIELD DESC 4              
*                                                                               
SVUCMP   DS    0CL128                                                           
SVUCMP1  DS    CL32                UCOM PRODUCT USER FIELD 1                    
SVUCMP2  DS    CL32                UCOM PRODUCT USER FIELD 2                    
SVUCMP3  DS    CL32                UCOM PRODUCT USER FIELD 3                    
SVUCMP4  DS    CL32                UCOM PRODUCT USER FIELD 4                    
*                                                                               
SVUCME   DS    0CL128                                                           
SVUCME1  DS    CL32                UCOM ESTIMATE USER FIELD 1                   
SVUCME2  DS    CL32                UCOM ESTIMATE USER FIELD 2                   
SVUCME3  DS    CL32                UCOM ESTIMATE USER FIELD 3                   
SVUCME4  DS    CL32                UCOM ESTIMATE USER FIELD 4                   
*                                                                               
SVUCMR   DS    0CL128                                                           
SVUCMR1  DS    CL32                UCOM REGION USER FIELD 1                     
SVUCMR2  DS    CL32                UCOM REGION USER FIELD 2                     
SVUCMR3  DS    CL32                UCOM REGION USER FIELD 3                     
SVUCMR4  DS    CL32                UCOM REGION USER FIELD 4                     
*                                                                               
SVUCMD   DS    0CL128                                                           
SVUCMD1  DS    CL32                UCOM DISTRICT USER FIELD 1                   
SVUCMD2  DS    CL32                UCOM DISTRICT USER FIELD 2                   
SVUCMD3  DS    CL32                UCOM DISTRICT USER FIELD 3                   
SVUCMD4  DS    CL32                UCOM DISTRICT USER FIELD 4                   
*                                                                               
UPSI     DS    XL1                                                              
UPSISOUT EQU   X'80'              .  SORTER OUT                                 
UPSIPUTS EQU   X'40'              .  SORTER IN                                  
*                                                                               
FORCECWK DS    CL1                 YES/NO, FORCE WORKER FILE TO CLOSE           
#WRKFS   DS    XL1                 # OF WORKER FILES CREATED                    
MAX#WKF  DS    XL1                 MAX NUMBER OF WORKER FILES                   
#AGY     DS    XL1                 # OF AGENCIES WORKER FILE TOTALS             
MAXAGY   EQU   10                                                               
PRVINV   DS    CL(L'SRTINV)                                                     
BINVL    DS    CL10                LONGER INVOICE NUMBER                        
TRUEINVL DS    CL10                AOR TRUE LONGER INVOICE NUMBER               
*                                                                               
SVBLFLT  DS    CL10                                                             
SVBLPO   DS    CL25               PO#                                           
SVPOLBL  DS    CL12               PO LABLE OVERRIDE                             
SVPOTYP  DS    CL1                PO TYPE (LEVEL)                               
PGRCD    DS    CL3                DIVISION/PRODUCT GROUP CODE                   
PGRNAME  DS    CL20                    "  /   "      "   NAME                   
REGCODE  DS    CL3                REGION CODE                                   
REGNAME  DS    CL20               REGION NAME                                   
DISTCODE DS    CL3                DISTRICT CODE                                 
DISTNAME DS    CL20               DISTRICT NAME                                 
OVERCLT  DS    CL3                OVERRIDING CLIENT CODE FOR REG/DIS            
SVBTNUM  DS    CL2                BTYPE NUMBER B4,B5,B6,B7                      
TYPEBIL  DS    CL3                TYPE OF BILL (AOR,REG,ETC...)                 
STYPEBIL DS    CL3                SAVED TYPEBIL                                 
MTYPEBIL DS    CL5                SAVED TYPEBIL FOR MXPOST                      
TRUEAMT  DS    XL6                BACTUAL OF BILL # 2                           
TRUEGST  DS    XL4                BVATAMT OF BILL # 2                           
TRUEGSTI DS    CL1                INPUT GST CODE FROM TRUE AOR BILL             
TRUEINV  DS    CL6                INVOICE # OF BILL2 (TRUE AOR BILL)            
PSTIVALS DS    14CL12             INPUT PST INFORMATION                         
SVSRCNTR DS    CL51                                                             
         DS    0CL51              DON'T SEPERATE THE TWO                        
CONTRA   DS    CL15               COMPANY CODE & CONTRA ACCOUNT NUMBER          
CONTRAN  DS    CL36               CONTRA ACCOUNT NAME                           
CONLVL   DS    CL(L'ACPLVL)       ACCOUNT LEVEL                                 
*                                                                               
ACCNAME  DS    CL36               ACCOUNT NAME                                  
SJCLTNM  DS    CL36               SJ CLIENT RECORD ACC NAME                     
SJPRDNM  DS    CL36               SJ PRODUCT RECORD ACC NAME                    
*                                                                               
SVCURR   DS    CL1                SAVE CURRENCY                                 
*                                                                               
INCRIDE  DS    CL51               INCOME CONTRA OVERRIDE NAME                   
AKEY     DS    XL42               ACCOUNTING KEYS MUST BE 42                    
SVAKEY   DS    XL(L'AKEY)                                                       
*                                                                               
*              TABLE OF MXCOMMON ROUTINES                                       
*                                                                               
COMTAB   DS    0A                                                               
MXPROF   DS    V                                                                
SETMOA   DS    V                                                                
CLRAREA  DS    V                                                                
GTNAME   DS    V                                                                
         DS    4V                  SPARE                                        
NCOMTAB  EQU   (*-COMTAB)/L'COMTAB                                              
*                                                                               
         DS    0F                                                               
AGYTBL   DS    CL(MAXAGY*AGYTBLL) ALPHA AGENCY NAME/DOLLAR AMT                  
AORERRS  DS    CL(MAXAOR*AORERRL) ERROR TABLE FOR A SET OF AOR BILLS            
MAXAOR   EQU   4                                                                
*                                                                               
         DS    D                                                                
PRTBLOCK DS    CL(PLENGTH)        REQUEST CONTROL BLOCK FOR MXPRNT              
ACPBLK   DS    CL(ACPOSTL)        REQUEST CONTROL BLOCK FOR ACPOSTER            
TEMPROW  DS    CL(ACPRTNL2)       LENGTH OF ONE ROW RETURNED                    
PROFWORK DS    CL20               VALUE FROM PROFILE REQUESTED                  
UCOMBLK  DS    CL(UCOMDLNQ)                                                     
ELEM     DS    CL256              ELEMENT                                       
XTRAREA  DS    CL1000                                                           
         EJECT                                                                  
* - AGYTBLD-TABLE FULL AGENCY NAME FOLLOWED BY TOTAL AMOUNT ADDED               
*           TO WORKER FILE ( ONE ENTRY PER WORKER FILE CREATED                  
AGYTBLD  DSECT                                                                  
AGYOID   DS    XL2                AGENCY OID                                    
AGYTNM   DS    CL6                NAME                                          
AGYTAMT  DS    PL6                TOTAL FOR WORKER FILE(S)                      
AGYTBLL  EQU   (*-AGYTBLD)                                                      
*                                                                               
*- CONTBLD - TABLE WITH POSTING NUMBER AND ITS CONTRA/CONTRA NAME               
CONTBLD  DSECT                                                                  
CONTYPE  DS    XL1                ACCOUNT TYPE                                  
CONTYPE2 DS    XL1                IF CONTYPE=05(COSTING) TYPE2=6,7              
CONACC   DS    CL15               CONTRA ACCOUNT                                
CONNAME  DS    CL36               CONTRA ACCOUNT NAME                           
CONTBLL  EQU   (*-CONTYPE)                                                      
*                                                                               
*- AORERSD - POSTING ERRORS FOR ALL AOR BILLS (SHOULD BE 4XL5)                  
*- ORDER MAIN BILL ERRORS, AOR ERRORS, BILL2 , BILL3                            
AORERD   DSECT                                                                  
AORERR   DS    XL1                AOR POSTING ERRORS                            
AORERRC  DS    XL1                AOR POSTING ERRORS CONTINUED                  
AORCERR  DS    XL1                CONTRA ACCOUNT ERRORS                         
         DS    XL2                SPARE                                         
AORERRL  EQU   (*-AORERR)                                                       
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'108PPREPMX02 06/26/19'                                      
         END                                                                    
